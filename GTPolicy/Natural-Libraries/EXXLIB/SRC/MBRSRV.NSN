* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Server switch
************************************************************************
* NAME         : MBRSRV                                                *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : CRM requests server                                   *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20100128      Initial Creation
* NGG    20140321      New service SAS
**********************************************************
*
DEFINE DATA PARAMETER
1 FUNCTION-NAME       (A) Dynamic        /* In
1 INPUT-DATA          (A) Dynamic        /* In
1 RESPONSE-DATA       (A) Dynamic        /* Out
*
LOCAL
1 #MESSAGE            (A) DYNAMIC
1 #MESSAGE-ID         (A) DYNAMIC
1 #ERROR-CODE         (A2)
1 #ERROR-LOG          (I4) CONST <3>
1 #REQUEST-LOG        (I4) CONST <4>
1 #UPDATE-LOG         (I4) CONST <5>
1 #Unsupported        (A2) CONST <'-1'>
1 #Timeout            (A2) CONST <'-2'>
1 #Natural-error      (A2) CONST <'-3'>
*
1 #MD-CHECK         (A24) CONST <'MilDataCheck'>
1 #GOM-REPORT       (A24) CONST <'GOMReport'>
1 #SAS-APPLICATION  (A24) CONST <'SAS Application'>
1 #NEW-APPLICATION  (A24) CONST <'New Application'>
1 #MEMBER-SEARCH    (A24) CONST <'Search Members'>
1 #QUOTE-REQUEST    (A24) CONST <'Quote Request'>
1 #SERVICE-RATES    (A24) CONST <'Service Rates'>
1 #SERVICE-REQUEST  (A24) CONST <'Service Request'>
1 #REQUEST-SERVICE  (A24) CONST <'RequestService'>
1 #SERVICE-ELIGIBLE (A24) CONST <'Service Eligible'>
1 #SERVICE-FPF      (A24) CONST <'Service FPF'>
1 #CAML-QUERY       (A24) CONST <'CAML Query'>
1 #CREATE-CONTACT   (A24) CONST <'CreateContact'>
1 #UPDATE-SERVICE   (A24) CONST <'UpdateService'>
1 #SVC-ELIGIBLE-WITHI (A) DYNAMIC CONST <'ServiceEligibleWithInsurance'>
*
1 #J                (I4)
END-DEFINE
*
ON ERROR
  IF *ERROR-NR = 3009
    RESPONSE-DATA := #Timeout
  ELSE
    RESET #ERROR-CODE #MESSAGE
    CALLNAT 'CRMLOG' #ERROR-CODE #MESSAGE
      #ERROR-LOG #MESSAGE-ID
    RESPONSE-DATA := #Natural-error
    BACKOUT TRANSACTION
  END-IF
  ESCAPE ROUTINE
END-ERROR
*
#MESSAGE := INPUT-DATA
#MESSAGE-ID := 'UNIX'
CALLNAT 'CRMLOG' #ERROR-CODE #MESSAGE
  #REQUEST-LOG #MESSAGE-ID
*
* Parse Process and Serialize CUSTOMER-DATA based on request
*
DECIDE ON FIRST VALUE OF FUNCTION-NAME
  VALUE #GOM-REPORT
    CALLNAT 'GOREPN01' INPUT-DATA RESPONSE-DATA
  VALUE #MD-CHECK
    CALLNAT 'GOVLDMN0' INPUT-DATA RESPONSE-DATA
  VALUE #SAS-APPLICATION
    CALLNAT 'MBRSASA'  INPUT-DATA RESPONSE-DATA
      #MESSAGE-ID
  VALUE #NEW-APPLICATION
    CALLNAT 'MBRNAPPL' INPUT-DATA RESPONSE-DATA
      #MESSAGE-ID
  VALUE #MEMBER-SEARCH
    CALLNAT 'MBRSRCH'  INPUT-DATA RESPONSE-DATA
  VALUE #QUOTE-REQUEST
    CALLNAT 'MBRQTE'   INPUT-DATA RESPONSE-DATA
  VALUE #SERVICE-RATES
    CALLNAT 'MBRATE'  INPUT-DATA RESPONSE-DATA
  VALUE #SERVICE-REQUEST
**    CALLNAT 'MBRSVC'  INPUT-DATA RESPONSE-DATA   /* Temp
    CALLNAT 'CRMNSRP2'  INPUT-DATA RESPONSE-DATA
  VALUE #SERVICE-ELIGIBLE
    CALLNAT 'MBRSVCE'  INPUT-DATA RESPONSE-DATA
  VALUE #SERVICE-FPF
    CALLNAT 'MBRSVCFP'  INPUT-DATA RESPONSE-DATA
  VALUE #SVC-ELIGIBLE-WITHI
    CALLNAT 'MBRSVCEI'  INPUT-DATA RESPONSE-DATA
  VALUE #CREATE-CONTACT
    CALLNAT 'CRMNCOP1'  INPUT-DATA RESPONSE-DATA
  VALUE #REQUEST-SERVICE
    CALLNAT 'CRMNSRP2'  INPUT-DATA RESPONSE-DATA
  VALUE #UPDATE-SERVICE
    CALLNAT 'CRMNSUP2'  INPUT-DATA RESPONSE-DATA
  VALUE #CAML-QUERY
    CALLNAT 'CAMLQRY'  INPUT-DATA RESPONSE-DATA
  NONE VALUE
    RESPONSE-DATA := #Unsupported
END-DECIDE
*
*  Log the result
*
#J := *LENGTH ( RESPONSE-DATA )
IF #J > 0
  IF SUBSTR(RESPONSE-DATA,1,1) = '-'
    EXAMINE RESPONSE-DATA FOR ':' GIVING POSITION #J
    IF #J > 1
      SUBTRACT 1 FROM #J
      MOVE SUBSTR ( RESPONSE-DATA, 1 ,#J) TO RESPONSE-DATA
    END-IF
  END-IF
  IF RESPONSE-DATA = #Natural-error
    CALLNAT 'CRMLOG' #ERROR-CODE #MESSAGE
      #UPDATE-LOG #MESSAGE-ID
    IF #MESSAGE = 'UPDATED'
      END TRANSACTION
    END-IF
  ELSE
    COMPRESS  FUNCTION-NAME RESPONSE-DATA INTO #MESSAGE
    #MESSAGE-ID := 'UNIX'
    CALLNAT 'CRMLOG' #ERROR-CODE #MESSAGE
      #REQUEST-LOG #MESSAGE-ID
  END-IF
END-IF
*
END
