* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************************************
* NAME         : F-PLAN-DESCRIPTION (F-PLAN-D)
* DESCRIPTION  : Get plan description
********************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG   REASON
* YAK    07/27/2008        Program Written
* YAK    09/26/2012  YK1   Use new function to determine if Level Term II Plan
* YAK    04/01/2015  YK2   Add 5th return parm
********************************************************************************
DEFINE FUNCTION F-PLAN-DESCRIPTION RETURNS (A/5) DYNAMIC             /* YK2
  DEFINE DATA
  PARAMETER
  1 #PLAN (A3)
  LOCAL USING OBJLGENT
  LOCAL
  1 #FLEX-PLAN     (A2)
  1 #FLEX-KEY      (A3)
  1 #SEC-KEY       (A7)
  1 #PROGRAM       (A8)
  1 #ERROR-MESSAGE (A70)
  1 #SEV-LEVEL     (A1) INIT <' '>
  1 #PLAN-KEY      (A4) CONST <'PLAN'>
  END-DEFINE
*
DECIDE FOR FIRST CONDITION
  WHEN (SUBSTR(#PLAN,3,1) = 'F' AND #PLAN NE 'SPF')
    #FLEX-PLAN := SUBSTR(#PLAN,1,2)
    EXAMINE #FLEX-PLAN FOR 'P' DELETE
    #FLEX-KEY  := '99F'
* WHEN SUBSTR(#PLAN,1,1) = 'U' OR = 'V' OR = 'W' OR = 'X' OR = 'Y' OR = 'Z' /* YK1
  WHEN F-LT2-TRUE(<#PLAN>)                                                  /* YK1
    #FLEX-PLAN := SUBSTR(#PLAN,2,2)
    COMPRESS SUBSTR(#PLAN,1,1) '99' INTO #FLEX-KEY LEAVING NO
  WHEN NONE
    #FLEX-KEY := #PLAN
END-DECIDE
COMPRESS #PLAN-KEY #FLEX-KEY INTO #SEC-KEY LEAVING NO
FIND(1) GEN-TABLE-V WITH SEC-KEY = #SEC-KEY SORTED BY PRIMARY-KEY DESCENDING
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  F-PLAN-DESCRIPTION(1) := GEN-DESCRIPTION
  F-PLAN-DESCRIPTION(2) := GEN-ALPHA-VALUE
  F-PLAN-DESCRIPTION(3) := SUBSTR(ADDL-INFO,1,75)
  F-PLAN-DESCRIPTION(4) := SUBSTR(ADDL-INFO,76)
  IF #FLEX-PLAN NE ' '
    IF SUBSTR(#FLEX-PLAN,1,1) = '0'
      #FLEX-PLAN := SUBSTR(#FLEX-PLAN,2)
    END-IF
    EXAMINE F-PLAN-DESCRIPTION(1) FOR '99' REPLACE #FLEX-PLAN
    EXAMINE F-PLAN-DESCRIPTION(2) FOR '99' REPLACE #FLEX-PLAN
    F-PLAN-DESCRIPTION(5) := #FLEX-PLAN                              /* YK2
  END-IF
  ESCAPE ROUTINE
END-FIND
*
COMPRESS 'There is NO description for the requested plan'
  #SEC-KEY INTO #ERROR-MESSAGE
CALLNAT 'OBJNBERR' #PROGRAM #ERROR-MESSAGE #SEV-LEVEL
*
END-FUNCTION
END
