* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1002SAT   ** underwriting test - called by c1000pat **
*     most of this is no longer used
********************************************************************************
*   DATE      USER  TAG         REASON                                         *
*  20040327   PAM   PM1         Use parameter data area                        *
* 09/30/2008  DRW   DW2   No Underwriting if GEN PLUS
* 10/30/2012  VXT   VT1   Add logic to read the gen table  /ticket 1185/       *
* 20130108    NGG   NG1   EZ Term mask of underwriting message
* 11/13/2013  VXT   VT2   Add new message for Uderwriting  /Ticket 1830/       *
* 05/01/2017  vxt   vt3   Group Term Plan                                      *
********************************************************************************
DEFINE DATA
parameter USING C1002LAT                                          /* PM1

*
LOCAL
1 #FAT-M-TBL    /* male
  2 #FAT-M-60 (A20) INIT <'100-139-141-143-146-'>
  2 #FAT-M-61 (A20) INIT <'102-144-146-148-151-'>
  2 #FAT-M-62 (A20) INIT <'103-148-150-153-156-'>
  2 #FAT-M-63 (A20) INIT <'104-153-155-158-161-'>
  2 #FAT-M-64 (A20) INIT <'105-158-160-163-166-'>
  2 #FAT-M-65 (A20) INIT <'106-163-165-168-171-'>
  2 #FAT-M-66 (A20) INIT <'107-168-170-173-177-'>
  2 #FAT-M-67 (A20) INIT <'111-174-176-179-182-'>
  2 #FAT-M-68 (A20) INIT <'115-179-181-184-187-'>
  2 #FAT-M-69 (A20) INIT <'119-184-186-189-193-'>
  2 #FAT-M-70 (A20) INIT <'123-189-192-195-199-'>
  2 #FAT-M-71 (A20) INIT <'127-194-197-201-204-'>
  2 #FAT-M-72 (A20) INIT <'131-200-203-206-210-'>
  2 #FAT-M-73 (A20) INIT <'135-205-208-212-216-'>
  2 #FAT-M-74 (A20) INIT <'139-211-214-218-222-'>
  2 #FAT-M-75 (A20) INIT <'143-217-220-224-228-'>
  2 #FAT-M-76 (A20) INIT <'147-223-226-230-234-'>
  2 #FAT-M-77 (A20) INIT <'151-229-232-236-240-'>
  2 #FAT-M-78 (A20) INIT <'153-235-238-242-247-'>
  2 #FAT-M-79 (A20) INIT <'159-241-244-248-253-'>
  2 #FAT-M-80 (A20) INIT <'166-247-250-255-259-'>
1 REDEFINE #FAT-M-TBL
  2 #FAT-M-HEIGHT  (21) /* 60 inches thru 80 inches
    3 #FAT-M-AGE    (5) /* min/17-20/21-27/28-39/40 & over
      4 #FAT-M   (N3)
      4 #FAT-M-X (A1)
*
1 #FAT-F-TBL    /* female
  2 #FAT-F-58 (A20) INIT <'090-112-115-119-122-'>
  2 #FAT-F-59 (A20) INIT <'092-116-119-123-126-'>
  2 #FAT-F-60 (A20) INIT <'094-120-123-127-130-'>
  2 #FAT-F-61 (A20) INIT <'096-124-127-131-135-'>
  2 #FAT-F-62 (A20) INIT <'098-129-132-137-139-'>
  2 #FAT-F-63 (A20) INIT <'100-133-137-141-144-'>
  2 #FAT-F-64 (A20) INIT <'102-137-141-143-148-'>
  2 #FAT-F-65 (A20) INIT <'104-141-145-149-153-'>
  2 #FAT-F-66 (A20) INIT <'106-146-150-154-158-'>
  2 #FAT-F-67 (A20) INIT <'109-149-154-159-162-'>
  2 #FAT-F-68 (A20) INIT <'112-154-159-164-167-'>
  2 #FAT-F-69 (A20) INIT <'115-158-163-168-172-'>
  2 #FAT-F-70 (A20) INIT <'118-163-168-173-177-'>
  2 #FAT-F-71 (A20) INIT <'122-167-172-177-182-'>
  2 #FAT-F-72 (A20) INIT <'125-172-177-183-188-'>
  2 #FAT-F-73 (A20) INIT <'128-177-182-188-193-'>
  2 #FAT-F-74 (A20) INIT <'130-183-189-194-198-'>
  2 #FAT-F-75 (A20) INIT <'133-188-194-200-204-'>
  2 #FAT-F-76 (A20) INIT <'136-194-200-206-209-'>
  2 #FAT-F-77 (A20) INIT <'139-199-205-211-215-'>
  2 #FAT-F-78 (A20) INIT <'141-204-210-216-220-'>
  2 #FAT-F-79 (A20) INIT <'144-209-215-222-226-'>
  2 #FAT-F-80 (A20) INIT <'147-214-220-227-232-'>
1 REDEFINE #FAT-F-TBL
  2 #FAT-F-HEIGHT  (23) /* 58 inches thru 80 inches
    3 #FAT-F-AGE    (5) /* min/17-20/21-27/28-39/40 & over
      4 #FAT-F   (N3)
      4 #FAT-F-X (A1)
*
1 #TODAY     (N8)
1 #HEIGHT    (N3)
1 #HEIGHT-X  (I2)
1 #AGE-X     (I2)
1 #FAT        (P3)
1 #ADJ-FAT    (P3)
1 #SKINNY     (P3)
1 #ADJ-SKINNY (P3)
1 #AGE    (N4)
*  1 #YES-ON-MED-l  (L)                                                    /* vt2
*
1 #I1  (I2)
1 #I2  (I2)
*
1 #WYYYYMMDD-N (N8)
1 REDEFINE #WYYYYMMDD-N
  2 #WYYYY-N (N4)
  2 #WMMDD-N (N4)
*
1 CODES view of A-GEN-TABLE                                                    /* vt1 \/
  2 GEN-KEY                           (A4) /*    D
  2 GEN-SUB-KEY                       (A3)
  2 GEN-ALPHA-VALUE                   (A50)
  2 GEN-NUMBER-VALUE
  2 GEN-DESCRIPTION                   (A75)
  2 ADDL-INFO
*
1 st view of A-status
  2 id-number
  2 suspend-code
*
1 #cn        (a6)
1 redefine #cn
  2 #member-cn  (n6)
*
1 #TABLE-KEY  (A7)
1 REDEFINE #TABLE-KEY
  2 #GEN-KEY         (A4)
  2 #SUB-KEY         (A3)                                                       /* vt1 /\

END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

* FOR #I1 1 TO 17                                          /* vt2 \/
*   IF #MED-INFO (#I1) = 'Y'
*     MOVE TRUE TO #YES-ON-MED
*     ESCAPE BOTTOM
*   END-IF
* END-FOR                                                   /* vt2 /\

*
MOVE *DATN TO #TODAY
*
if #plan-1002 = 'GPL'  or = 'GP2'      /*TT1            /* DW2
  reset #underwriting
  escape routine
end-if
*
IF (#PLAN-1002  = 'SO' OR = 'FS') AND (NOT #YES-ON-MED-l)                   /* vt2 was #yes-on-med
  MOVE 'Serv-only Approved' TO #UNDERWRITING
  ESCAPE ROUTINE
END-IF
*
if  #PLAN-1002 eq 'GT1'                                        /* vt3 05/01/2017  \/
  move 'Group Term Approved' to #underwriting
  escape routine
end-if                                                         /* vt3 05/01/2017  /\
*
COMPUTE #WYYYYMMDD-N = #DATE-OF-BIRTH - #TODAY
MOVE #WYYYY-N TO #AGE
IF (#MIL-STATUS = 'A') and (#PLAN-1002 ne 'GT1' )             /* vt3 05/01/2017
  IF #AGE LT 40
    IF NOT #YES-ON-MED-l                                                   /* vt2 was #yes-on-med
      MOVE 'Approved' TO #UNDERWRITING
    ELSE
      MOVE 'Underwriting must Approve' TO #UNDERWRITING
    END-IF
  ELSE
    MOVE 'Underwriting must Approve (Over age 39)' TO #UNDERWRITING
  END-IF
*
  IF #YES-ON-MED-l                                                        /* vt2 was #yes-on-med
    COMPRESS #UNDERWRITING '(YES on Med)' INTO #UNDERWRITING
  END-IF
ELSE
  IF #AGE LT 6
    MOVE 'To Underwriting for Well Baby Statement' TO #UNDERWRITING
  ELSE
    IF #AGE LT 13
      MOVE 'To Underwriting - DBS' TO #UNDERWRITING
    ELSE
      IF #AGE LT 25
        MOVE 'To Underwriting - DBS/URINALYSIS/PART PARAMEDICAL'
          TO #UNDERWRITING
      ELSE
        IF (#AGE LT 46) OR ((#AGE LT 56) AND (#SCR-FACE-UNITS LT 100))
          MOVE 'To Underwriting -Exam' TO #UNDERWRITING
        ELSE
          MOVE 'To Underwriting - PHYSICIAN/LAB & EKG' TO #UNDERWRITING
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
COMPUTE #HEIGHT = (#HEIGHT-FT * 12) + #HEIGHT-IN
IF #AGE LT 17 RESET #AGE-X     ELSE
  IF #AGE LT 21 MOVE 2 TO #AGE-X ELSE
    IF #AGE LT 28 MOVE 3 TO #AGE-X ELSE
      IF #AGE LT 40 MOVE 4 TO #AGE-X ELSE
        MOVE 5 TO #AGE-X END-IF END-IF END-IF END-IF
*
IF #AGE-X = 0
  ESCAPE ROUTINE
END-IF
*
RESET #FAT #SKINNY #ADJ-FAT #ADJ-SKINNY
IF #SEX-1002 = 'M'
  COMPUTE #HEIGHT-X = #HEIGHT - 59   /* 60 is first height
  IF (#HEIGHT-X GE 1) AND (#HEIGHT-X LE 21)
    COMPUTE ROUNDED #ADJ-SKINNY = #FAT-M (#HEIGHT-X, 1)      * 1.25
    COMPUTE ROUNDED #ADJ-FAT    = #FAT-M (#HEIGHT-X, #AGE-X) * 1.25
    COMPUTE #SKINNY = #ADJ-SKINNY - #WEIGHT
    IF #SKINNY LE 0
      IF #AGE-X NE 0
        COMPUTE #FAT = #WEIGHT - #ADJ-FAT
      END-IF
    END-IF
  END-IF
ELSE     /* female
  COMPUTE #HEIGHT-X = #HEIGHT - 57   /* 58 is first height
  IF (#HEIGHT-X GE 1) AND (#HEIGHT-X LE 23)
    COMPUTE ROUNDED #ADJ-SKINNY = #FAT-F (#HEIGHT-X, 1)      * 1.25
    COMPUTE ROUNDED #ADJ-FAT    = #FAT-F (#HEIGHT-X, #AGE-X) * 1.25
    COMPUTE #SKINNY = #ADJ-SKINNY - #WEIGHT
    IF #SKINNY LE 0
      IF #AGE-X NE 0
        COMPUTE #FAT = #WEIGHT - #ADJ-FAT
      END-IF
    END-IF
  END-IF
END-IF
*
* RESET #UNDERWRITING   /* vt 01292014
*
* write '=' #intro-flag-1002 '=' #face-amount '=' #whole-life
* if f-lt2-true(<#PLAN-1002>)  or #plan-1002 = 'LT'                                                                            /* vt2 \/
if   #plan-1002 = 'LT'     /* vt 07022013
  if  #face-amount gt 400001.00
    MOVE 'Underwriting - over $400,000 ' TO #UNDERWRITING
  else
    reset #underwriting
  end-if
end-if
*
if   #whole-life
  decide on first value of #intro-flag-1002
    Value  'S'
      if #mil-status-member = 'A'
        if (#age lt 40)
          if  NOT #YES-ON-MED-l
            if (#face-amount gt 300001.00)
              MOVE 'Underwriting - over $300,000 ' TO #UNDERWRITING
            else
              reset #underwriting
            end-if
          else
            move 'Send to underwriting' to #underwriting
          end-if
        else
          move 'Send to underwriting' to #underwriting
        end-if
      else
        move 'Send to underwriting' to #underwriting
      end-if
    Value 'K' , 'G'
      if #FACE-AMOUNT GT 100001.00
        MOVE 'Underwriting - over $100,000 ' TO #UNDERWRITING
      else   reset #underwriting
      END-IF
    Value 'M'
      if #mil-status = 'A'
        if (#age lt 40)
          if  NOT #YES-ON-MED-l
            if #FACE-AMOUNT GT 400001.00
              MOVE 'Underwriting - over $400,000 ' TO #UNDERWRITING
            else
              reset #underwriting
            END-IF
          else
            move 'Send to underwriting' to #underwriting
          end-if
        else
          move 'Send to underwriting' to #underwriting
        end-if
      else
        move 'Send to underwriting' to #underwriting
      end-if
    None
      ignore
  end-decide
end-if
*
if f-lt2-true(<#plan-1002>)
  MOVE 'Send to underwriting' TO #UNDERWRITING
end-if                                                     /* vt2 /\
*
if #PLAN-1002 NE 'GT1'                                     /* vt3 05/01/2017
  IF (#FAT LE 0) AND (#SKINNY LE 0)
    COMPRESS #UNDERWRITING '* Weight Acceptable'
      INTO #UNDERWRITING LEAVING NO
  ELSE
    IF #PLAN-1002 NE 'EZ'                /* NG1
      IF #FAT GT 0
        COMPRESS #UNDERWRITING '* Send to Underwriting'
          INTO #UNDERWRITING LEAVING NO
      END-IF
*
      IF #SKINNY GT 0
        COMPRESS #UNDERWRITING '* Send to Underwriting'
          INTO #UNDERWRITING LEAVING NO
      END-IF
    END-IF
  END-IF
end-if                                                   /* vt3 05/01/2017
*
examine #member-CN-aa for 'P' replace ' '                                                         /* vt1 \/
move #member-cn-aa to #cn
move right #cn to #cn
* write '=' #cn
find  st with st.id-number = #member-cn
  if no
    escape bottom
  end-norec
*  write '=' st.suspend-code
  if st.suspend-code ne ' '
    MOVE '044' TO #SUB-KEY
    MOVE 'CODE' TO #GEN-KEY
    find  CODES WITH SEC-KEY = #TABLE-KEY
*    write st.suspend-code
      if st.suspend-code = codes.gen-alpha-value
*      write codes.gen-description
        compress   #underwriting ';'  CODES.addl-info into #underwriting leaving no
        escape bottom
      end-if
    end-find
  end-if
end-find                                                                                        /* vt1 /\

END
