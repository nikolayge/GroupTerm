* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************************************
* NAME         : F-LOAN-MODE (F-LOAN-M)
* DESCRIPTION  : Get Loan Payment Mode
********************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG   REASON
* YAK    11/21/2016        Created for EB&P II
* YAK    08/15/2017  YK1   Correct logic for CAP
* YAK    09/01/2017  YK2   Add Bill Cycle
********************************************************************************
DEFINE FUNCTION F-LOAN-MODE RETURNS (A1)
  DEFINE DATA
  PARAMETER
  1 #CN        (N6)
  1 #POLICY-ID (A) DYNAMIC
  1 #LoanType  (A) DYNAMIC OPTIONAL
  1 #MonthlyAmount (N7.2) OPTIONAL
  1 #BillCycle (A1) OPTIONAL
  LOCAL
  1 ST-V VIEW OF A-STATUS
    2 C*ACTIVE-SEGMENTS (N2)
    2 POLICY-ID (9)
    2 STATUS    (9)
    2 MODE      (9)
    2 BILL-CYCLE (9)
    2 X-LOAN-AMOUNT (9)
    2 X-LOAN-AMOUNT-2 (9)
    2 X-CAP-AMT (9)
    2 X-CAP-AMT-2 (9)
    2 X-ALT-LP-AMT (9)
    2 X-ALT-LP-AMT-2 (9)
    2 X-ALT-LP-IND (9)
    2 X-ALT-LP-IND-2 (9)
    2 CONV-DURATION (9)
  1 #I   (I2)
  1 #LOAN-MODE-WITH-BILL-CYCLE (A1) CONST <'8'>             /* YK2
  END-DEFINE
*
IF #LoanType SPECIFIED
  RESET F-LOAN-MODE
  FIND ST-V WITH ID-NUMBER = #CN
    FOR #I = 1 TO C*ACTIVE-SEGMENTS
      IF STATUS(#I) = 'D'
        DECIDE FOR FIRST CONDITION
          WHEN #LoanType = 'C' AND (CONV-DURATION(#I) = 999 AND X-LOAN-AMOUNT(#I) <> 0) OR
              (X-LOAN-AMOUNT(#I) = 0 AND X-ALT-LP-AMT(#I) <> 0)           /* YK1
            F-LOAN-MODE := X-ALT-LP-IND(#I)
            #MonthlyAmount := X-ALT-LP-AMT(#I)
          WHEN #LoanType = 'F' AND POLICY-ID(#I) = #POLICY-ID
            F-LOAN-MODE := X-ALT-LP-IND(#I)
            #MonthlyAmount := X-ALT-LP-AMT(#I)
          WHEN #LoanType = 'V' AND POLICY-ID(#I) = #POLICY-ID
            F-LOAN-MODE := X-ALT-LP-IND-2(#I)
            #MonthlyAmount := X-ALT-LP-AMT-2(#I)
          WHEN ANY
            IF F-LOAN-MODE = ' '
              F-LOAN-MODE := '0'
            END-IF
* YK2 >
            IF F-LOAN-MODE = #LOAN-MODE-WITH-BILL-CYCLE
              #BillCycle := ST-V.BILL-CYCLE(#I)
            ELSE
              #BillCycle := '3'
            END-IF
* < YK2
            ESCAPE BOTTOM
          WHEN NONE
            IGNORE
        END-DECIDE
      END-IF
    END-FOR
  END-FIND
END-IF
END-FUNCTION
END
