* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************
* SUBPROGRAM-ID: GPRN006F - Find all the policies for this Customer
* (CN) as insured plus family members policies
***************************************************************
*        MODIFICATION LOG
***************************************************************
* USER   DATE      TAG  REASON
* YAK    12152015       Initial Creation (MC_FIR)
* bz-9+  12/01/2017     Add 9+ Policies  (HD-3671)
***************************************************************
*
DEFINE DATA
PARAMETER USING GPRA006N
*
LOCAL
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 INTRO-FLAG
  2 C*LINKED-CN
  2 LINKED-CN (20)
  2 C*ACTIVE-SEGMENTS
  2 POLICY-ID (9)
  2 NUM-MBRSHIPS (9)
  2 STATUS (9)
*
1 #LINKED-CN     (N6/20)
1 I  (I2)
1 II (I2)
1 K  (I2)
1 C@LINKED-CN (I2)
1 #ACTIVE     (A1) INIT <'D'>
* -----------------------------------------
1 v_CNs            (N6/1:11)                      /* bz-9+
1 #9               (N2)                           /* bz-9+
1 #i               (N2)                           /* bz-9+
1 O9-V VIEW OF OVER-9-LINKS                       /* bz-9+ 
  2 PRIME-CN         (N6) (EM=ZZZZZZ)             /* bz-9+
  2 C*SECONDARY-CN   (N2)                         /* bz-9+
  2 SECONDARY-CN     (N6/10)(EM=ZZZZZZ)           /* bz-9+ 
* -----------------------------------------  
END-DEFINE
*
* DEFINE WORK FILE 9 '/usr/SAG/TEMP/db-227-MC.txt' ATTRIBUTES 'APPEND'  /* --db

  v_CNs(1) := PARM-CN                              /* bz-9+  >
  #9       := 1                                    
  FIND(1) O9-V WITH PRIME-CN = v_CNs(1) 
    v_CNs(2:11) := SECONDARY-CN(1:10)
    #9          := #9 + C*SECONDARY-CN  
  END-FIND                                         /* bz-9+ <
  FOR #i = 1 TO #9
*     WRITE WORK 9 VARIABLE #i ' of ' #9 ' : ' v_CNs(#i)          /* --db
     FIND ST-V WITH ID-NUMBER = v_CNs(#i)          /* bz-9+ <        
        FOR I = 1 TO C*ACTIVE-SEGMENTS
          IF ST-V.STATUS(I) = 'D'
            II := II + 1
            RESIZE ARRAY PARM-POLICIES TO (*:II)
            PARM-POLICIES.ID-NUMBER(II)    := ST-V.ID-NUMBER
            PARM-POLICIES.POLICY-INDEX(II) := I
            PARM-POLICIES.POLICY-NUMBER(II):= ST-V.POLICY-ID(I)
          END-IF
        END-FOR
     END-FIND
  END-FOR
*
  PARM-MEMBER-CN := PARM-CN
  FIND(1) ST-V WITH ID-NUMBER = PARM-MEMBER-CN
    #LINKED-CN(*) := LINKED-CN(*)
    C@LINKED-CN   := C*LINKED-CN
  END-FIND

FOR K = 1 TO C@LINKED-CN
  FIND(1) ST-V WITH ID-NUMBER = #LINKED-CN(K)
    IF INTRO-FLAG = 'F' OR (NOT (INTRO-FLAG = 'S' OR = 'K' OR = 'G'))
      ESCAPE BOTTOM
    END-IF
    FOR I = 1 TO C*ACTIVE-SEGMENTS
      IF ST-V.STATUS(I) = 'D'
        II := II + 1
        RESIZE ARRAY PARM-POLICIES TO (*:II)
        PARM-POLICIES.ID-NUMBER(II)    := ST-V.ID-NUMBER
        PARM-POLICIES.POLICY-INDEX(II) := I
        PARM-POLICIES.POLICY-NUMBER(II):= ST-V.POLICY-ID(I)
      END-IF
    END-FOR
  END-FIND
END-FOR
*
* CLOSE WORK FILE 9                /* --db
END
