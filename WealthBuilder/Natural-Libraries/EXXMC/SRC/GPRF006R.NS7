* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************
* SUBPROGRAM-ID: GPRF006R - Find all the policies this Customer
***************************************************************
*        MODIFICATION LOG
***************************************************************
* USER   DATE      TAG  REASON
* YAK    10012016       Initial Creation (Member Center for life)
*                       EB&P Phase III
***************************************************************
*
DEFINE FUNCTION F-PS-MODIFIABLE RETURNS (L/2)
  DEFINE DATA
  PARAMETER
  1 #PARM-CN (N6)
*
  LOCAL
  1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 C*ACTIVE-SEGMENTS
    2 POLICY-ID   (9)
    2 STATUS      (9)
    2 MODE        (9)
*
  1 POS-V VIEW OF A-POLICY-CONTACTS
    2 POLICY-NUMBER
    2 RELATION
    2 REFERENCE-NUMBER
*
  1 #PRIMARY-OWNER       (N2) CONSTANT    <2>
  1 #ACTIVE              (A2) CONSTANT    <'D'>
  1 #CC-MODE             (A1/4) CONSTANT  <'1','2','4','0'>
  1 #EZ-Pay              (A1) CONSTANT    <'E'>
*
  1 #I    (I2)
  1 #CC-OWNER (L)
  1 #EZ-OWNER (L)
  1 #CC-NON-OWNER (L)
  1 #EZ-NON-OWNER (L)
*
  END-DEFINE
*
F-PS-MODIFIABLE(*) := TRUE
FIND(1) ST-V WITH ID-NUMBER = #PARM-CN
  FOR #I = 1 TO C*ACTIVE-SEGMENTS
    IF ST-V.STATUS(#I) = #ACTIVE AND (ST-V.MODE(#I) = #CC-MODE(*) OR  ST-V.MODE(*) = #EZ-Pay)
      FIND POS-V WITH POLICY-NUMBER = ST-V.POLICY-ID(#I)
          WHERE RELATION = #PRIMARY-OWNER
        DECIDE FOR FIRST CONDITION
          WHEN POS-V.REFERENCE-NUMBER = #PARM-CN AND ST-V.MODE(#I) = #CC-MODE(*)
            #CC-OWNER := TRUE
          WHEN POS-V.REFERENCE-NUMBER = #PARM-CN AND ST-V.MODE(#I) = #EZ-Pay
            #EZ-OWNER := TRUE
          WHEN POS-V.REFERENCE-NUMBER <> #PARM-CN AND ST-V.MODE(#I) = #CC-MODE(*)
            #CC-NON-OWNER := TRUE
          WHEN POS-V.REFERENCE-NUMBER <> #PARM-CN AND ST-V.MODE(#I) = #EZ-Pay
            #EZ-NON-OWNER := TRUE
          WHEN NONE
            IGNORE
        END-DECIDE
      END-FIND
    END-IF
  END-FOR
END-FIND
* Check if owns all his policies
IF #CC-OWNER AND #CC-NON-OWNER
  F-PS-MODIFIABLE(1) := FALSE
END-IF
IF #EZ-OWNER AND #EZ-NON-OWNER
  F-PS-MODIFIABLE(2) := FALSE
END-IF
*
END-FUNCTION
END
