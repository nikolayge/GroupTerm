* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GPREMSXX
*    FINDS THE PREMIUM RATE BASED ON PLAN AND AGE AND ISSUE-YYYY
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    12032004  DW1     Add LTII rates eff 1/1/2005                 *
* DRW    02212005  DW2     Add AL (annuity Life) rates                 *
* DRW    05162005  DW3     Add Sex for LTII rates                      *
* DRW  12/19/2007  PM4     Add 2008 LT2 Male NS Super Select rates     *
* DRW  08/1/2008   DW5     New GP plan                                 *
* DRW  09/03/2009  DW6     Call SPRSMS97 for AFSC Rates                *
* RSE  06/22/2010  RE1     Add AER and other widow Plans and 3SR       *
* YAK    09262012  YK1     Use function to identify Level Term II plans*
* YAK    11062012  YK2     Add No Medical Term plan (EZ)               *
* NGG    11082012  NG1     Add LTII rates eff 1/1/2013 and             *
*                          No Medical Term plan (EZ) - PRM suffix only *
* NGG    11082012  NG2     New service SAS                             *
* NGG    20141023  NG3     Multiple annuity life products              *
* NGG    20141023  NG4     Value added whole life                      *
************************************************************************
*
* SMOKER/AFSC/RT/LT/SVC-ONLY RATES USE GPRSMS97
* NON-SMOKER/LTII RATES            USE GPRNSS97
*
** to find rates scan for OL/20-PAY/30-PAY/7-PAY/SNG-PRM
*
DEFINE DATA PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #PLAN-PASS       (A3)
1 REDEFINE #PLAN-PASS
  2 #PLAN-PASS-A1 (A1)
1 #AGE             (N2)
1 #APREM           (N7.2)   /* NG3
1 #ISSUE-DATE      (N8)
1 REDEFINE #ISSUE-DATE
  2 #ISSUE-YYYY (N4)
  2 #ISSUE-MMDD (N4)
1 #SMOK-FLAG       (A1)
1 #FAC-AMT         (N7.2)
1 #SEX             (A1)
1 #SERVICE         (A1) OPTIONAL
*
LOCAL
1 #SCN-TYP-IND-1 (A1/13)   /* used only to display premiums
1 REDEFINE #SCN-TYP-IND-1
  2 #SCN-TYP-IND-2 (A1/13)
1 REDEFINE #SCN-TYP-IND-1
  2 #SCN-TYP-IND-3 (A1/13)
1 REDEFINE #SCN-TYP-IND-1
  2 #SCN-TYP-IND-4 (A1/13)
1 #SCR-GEN-PLUS-RATES    (A1)          /* DW5
1 #SCR-AFSC-RATES (A1)
1 #SCR-LTII-RATES   (A1)
1 #SCR-LTII-RATES-2005  (A1)    /* DW1
1 #SCR-LTII-RATES-2008  (A1)    /* PM4
1 #SCR-LTII-RATES-2013  (A1)    /* NG1
1 #SCR-EZ-RATES         (A1)    /* NG1
1 #PRM-SEL  (I2)
*
1 #I1      (I2)
1 #CALL-GPREMSXX (A8)
1 #WORK-7-7 (N7.7)    /* DW2
1 #SPECIAL-GROUP                                                    /* RE1 Start
  2 #SPECIAL-PLAN-TAB         (A40) INIT<'AERFERNERCERUER'>
  2 REDEFINE #SPECIAL-PLAN-TAB
    3 #SPECIAL-PLAN       (A3/10)
1 #SPC-PLAN         (L)                                             /* RE1 End
1 #RATE-DATE        (N8)   /* NG4
1 #POLICY-NUMBER    (A16)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
* Log the call
CALLNAT 'RTENLOG'
  #CGA-SCR-COMMAND  
  #CGA-SCR-ID
  #CGA-SCR-SUFFIX
  #PLAN-PASS
  #AGE
  #APREM
  #ISSUE-DATE
  #SMOK-FLAG
  #FAC-AMT
  #SEX
IF #CGA-SCR-SUFFIX = 'PRM'
  MOVE '*' TO #CGA-SCR-COMMAND
*
  MOVE 'N' TO #SMOK-FLAG
  INPUT MARK *#SCR-AFSC-RATES USING MAP 'GPREMM01'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  FOR #I1 1 TO 13
    IF #SCN-TYP-IND-1 (#I1) NE ' '
      MOVE #I1 TO #PRM-SEL
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #SCR-GEN-PLUS-RATES NE ' '      /* DW5
    MOVE 18 TO #PRM-SEL
  END-IF
*
  IF #SCR-AFSC-RATES NE ' '
    MOVE 14 TO #PRM-SEL
  END-IF
*
  IF #SCR-LTII-RATES NE ' '
    MOVE 15 TO #PRM-SEL
  END-IF
*
  IF #SCR-LTII-RATES-2005 NE ' '    /* DW1
    MOVE 16 TO #PRM-SEL
  END-IF
*
  IF #SCR-LTII-RATES-2008 NE ' '    /* PM4
    MOVE 17 TO #PRM-SEL             /* PM4
  END-IF                            /* PM4
*
  IF #SCR-LTII-RATES-2013 NE ' '    /* NG1
    MOVE 19 TO #PRM-SEL             /* NG1
  END-IF                            /* NG1
*
  IF #SCR-EZ-RATES NE ' '           /* NG1
    MOVE 20 TO #PRM-SEL             /* NG1
  END-IF                            /* NG1
*
  IF #PRM-SEL = 0
    REINPUT 'Select Premium Type'
      MARK *#SCN-TYP-IND-1 (1)
  END-IF
*
  IF #PRM-SEL = 18      /* Gen Plus                       /* DW5
    MOVE 'GPRSMS97' TO #CALL-GPREMSXX
  ELSE
    IF #PRM-SEL = 14   /* AFSC RATES
      MOVE 'SPRSMS97' TO #CALL-GPREMSXX           /* DW6
    ELSE
      IF #PRM-SEL = 15 OR = 16 /* LTII RATES before & after 1/1/2005 DW1
          OR = 17                /* LT2 2008 rates  PM4
          OR = 19                /* LT2 2013 rates  NG1
          OR = 20                /* No Medical Terms Rates NG1
        MOVE 'GPRNSS97' TO #CALL-GPREMSXX
      ELSE
        IF #PRM-SEL LE 6   /* PREMIUMS STARTED 1997
          IF (#SMOK-FLAG = 'S')
              OR (#PRM-SEL = 6)    /* TERM OR SVC-ONLY RATES
            MOVE 'GPRSMS97' TO #CALL-GPREMSXX
          ELSE
            MOVE 'GPRNSS97' TO #CALL-GPREMSXX
          END-IF
        ELSE               /* PREMIUMS PRIOR TO 1997
          MOVE 'GPREMS96' TO #CALL-GPREMSXX
        END-IF
      END-IF
    END-IF
  END-IF
ELSE
  IF SUBSTR(#PLAN-PASS,1,2) = 'AL'                     /* DW2
*  IF IS-ANNUITY-LIFE (< #PLAN-PASS >)      /* NG3
    CALLNAT 'GPREMALR' #PLAN-PASS #APREM #FAC-AMT
    ESCAPE ROUTINE
  END-IF
  IF #PLAN-PASS NE ' ' AND #PLAN-PASS = #SPECIAL-PLAN(*)      /* RE1
    MOVE TRUE TO #SPC-PLAN                                    /* RE1
  END-IF                                                      /* RE1
  IF #SERVICE SPECIFIED                                       /* NG1
      AND #SERVICE = 'S'                                      /* NG2
    MOVE TRUE TO #SPC-PLAN
  END-IF
* NG1 Start
  DECIDE FOR FIRST CONDITION
    WHEN #PLAN-PASS = 'EZ'                  /* YK2
* EZ does not have smoking/non-smoking; do not add to all plans logic below, process separately
      MOVE 'GPRSMS97' TO #CALL-GPREMSXX     /* YK2
    WHEN #PLAN-PASS = '3S' OR = '3SC' OR = '3SW' OR = '3SR'
        OR #SPC-PLAN
      MOVE 'SPRSMS97' TO #CALL-GPREMSXX
    WHEN NONE
      CALLNAT 'RTEN0002'  /* NG4
        #CGA-SCR-ID
        #PLAN-PASS
        #ISSUE-DATE     /* YYYYMMDD
        #POLICY-NUMBER
        #RATE-DATE
      IF (#RATE-DATE GE 19970101) AND (#CGA-SCR-SUFFIX NE 'OLD')
        IF  ((#SMOK-FLAG = 'S')
            AND (NOT F-LT2-TRUE(<#PLAN-PASS>)))    /* YK1
           OR (#PLAN-PASS = 'RT' OR = 'LT' OR = 'SO' OR = 'S1' OR = 'FS'
            OR = 'GPL' OR = 'GP' OR= 'GP2')  /* DW5
          MOVE 'GPRSMS97' TO #CALL-GPREMSXX
        ELSE
          MOVE 'GPRNSS97' TO #CALL-GPREMSXX  /* WILL DO VAR TERM RATES
        END-IF
      ELSE
        MOVE 'GPREMS96' TO #CALL-GPREMSXX
      END-IF
  END-DECIDE
* NG1 End
END-IF
*
CALLNAT #CALL-GPREMSXX #CGA-SCR-COMMAND #CGA-SCR-ID #CGA-SCR-SUFFIX
  #PLAN-PASS #AGE #APREM
  #RATE-DATE
  #SMOK-FLAG #FAC-AMT #PRM-SEL
  #SEX            /* DW3
*
END
