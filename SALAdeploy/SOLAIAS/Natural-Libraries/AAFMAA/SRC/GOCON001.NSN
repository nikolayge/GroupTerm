* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GOCON001 - The contact update/add routine
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    04/04/2011  None    Program written                           *
* NGG    10/10/2011  NG1     CHECK CONTACT TYPE                        *
* NGG    11/08/2011  NG2     CHECK NAME AND DATE OF BIRTH              *
* NGG    08/07/2017  NG3     Online app changes                        *
* NGG    12/28/2017  NG4     SOLA project                              *
************************************************************************
DEFINE DATA
PARAMETER USING GOCOA001
PARAMETER
1 #CLERK-ID (A3)
1 #MSG (A) DYNAMIC
1 #UPDATED                            (L)
LOCAL
1 CT-V VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 CONTACT-STATUS (A1)
  2 CONTACT-TYPE   (A1)
  2 ID-NUMBER (N6)
  2 RANK-ID (N6)
  2 RANK-PROTECT (A1)
  2 RANK-PROTECT-DATE (N8)
  2 LAST-RANK-DATE-UPD (N8)
  2 LAST-RANK-USER-UPD (A8)
  2 PREV-RANK-ID (N6)
  2 MILITARY-STATUS (A1)
  2 SSN-PROTECT (A1)
  2 SSN-PROTECT-DATE (N8)
  2 PREFIX-TITLE (A25)
  2 FULL-NAME
    3 FIRST-NAME (A25)
    3 MIDDLE-NAME (A25)
    3 LAST-NAME (A25)
    3 SUFFIX (A10)
  2 GENDER-CD (A1)
  2 DATE-OF-BIRTH (N8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8)
  2 BIRTH-CERT-CODE (A1)
  2 SPECIAL-PAY-IND (A1)
  2 DEATH-CERT-CODE (A1)
  2 HOMICIDE-SUICIDE-ACCIDENT-IND (A1)
  2 DELETE-FLAG (A1)
  2 LAST-USER-UPD (A8)
  2 LAST-DATE-UPD (N8)
  2 LAST-TIME-UPD (N7)
  2 FIRM-ORGANIZATION-NAME (A75)
  2 WEBSITE-URL
  2 CRM-CONTACT-GUI
*
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
  2 REDEFINE COMMENTS
    3 #NEW-CONTACT-ID (N8)
*
1 BLANK   (A1) CONST <' '>
END-DEFINE
*
RESET #MSG
  #UPDATED
IF #CONTACT.CONTACT-ID > 0
  PERFORM UPDATE-CONTACT
ELSE
  DECIDE FOR FIRST CONDITION
    WHEN #FORCE-NEW = TRUE
      PERFORM NEW-CONTACT
    WHEN #CONTACT.CRM-CONTACT-GUI NE BLANK  /* NG3 \/
      FIND CT-V WITH CRM-CONTACT-GUI = #CONTACT.CRM-CONTACT-GUI
        IF NO RECORDS FOUND
          PERFORM NEW-CONTACT
          ESCAPE BOTTOM
        END-NOREC
        MOVE BY NAME CT-V TO #CONTACT
      END-FIND                             /* NG3 /\
    WHEN #CONTACT.ID-NUMBER > 0
      FIND CT-V WITH ID-NUMBER = #CONTACT.ID-NUMBER
        IF NO RECORDS FOUND
          PERFORM NEW-CONTACT
          ESCAPE BOTTOM
        END-NOREC
        MOVE BY NAME CT-V TO #CONTACT
      END-FIND
    WHEN #CONTACT.SSN > 0
      FIND CT-V WITH SSN = #CONTACT.SSN
        IF NO RECORDS FOUND
          PERFORM NEW-CONTACT
          ESCAPE BOTTOM
        END-NOREC
        MOVE BY NAME CT-V TO #CONTACT
      END-FIND
    WHEN #CONTACT.CONTACT-ID > 0                       /* NG1
      FIND CT-V WITH CONTACT-ID = #CONTACT.CONTACT-ID
        IF NO RECORDS FOUND
          PERFORM NEW-CONTACT
          ESCAPE BOTTOM
        END-NOREC
        MOVE BY NAME CT-V TO #CONTACT
      END-FIND
    WHEN #CONTACT.DATE-OF-BIRTH > 0                    /* NG2
      FIND CT-V WITH DATE-OF-BIRTH = #CONTACT.DATE-OF-BIRTH
        IF NO RECORDS FOUND
          PERFORM NEW-CONTACT
          ESCAPE BOTTOM
        END-NOREC
        IF CT-V.FIRST-NAME = #CONTACT.FIRST-NAME
            AND CT-V.LAST-NAME = #CONTACT.LAST-NAME
          MOVE BY NAME CT-V TO #CONTACT
          ESCAPE BOTTOM
        END-IF
      END-FIND
      IF #CONTACT.CONTACT-ID = 0
        PERFORM NEW-CONTACT
      END-IF
    WHEN NONE
      PERFORM NEW-CONTACT
*      #MSG := 'Contact add: No SSN and CN'
  END-DECIDE
END-IF
*
DEFINE UPDATE-CONTACT
/*     ==============
FIND CT-V WITH CONTACT-ID = #CONTACT.CONTACT-ID
  MOVE BY NAME #CONTACT TO CT-V
  CT-V.LAST-DATE-UPD  := *DATN
  CT-V.LAST-TIME-UPD  := *TIMN
  IF #CLERK-ID NE BLANK
    CT-V.LAST-USER-UPD := #CLERK-ID
  ELSE
    CT-V.LAST-USER-UPD  := *USER
  END-IF
  UPDATE
END-FIND
END-SUBROUTINE
*
DEFINE NEW-CONTACT
/*     ===========
IF #CONTACT.CONTACT-TYPE = BLANK         /* NG1 start
  #CONTACT.CONTACT-TYPE := 'I'
END-IF
IF #CONTACT.CONTACT-TYPE = 'I'
  IF #CONTACT.FIRST-NAME = BLANK
      AND #CONTACT.LAST-NAME = BLANK
    ESCAPE ROUTINE
  END-IF
ELSE
  IF #CONTACT.FIRM-ORGANIZATION-NAME = BLANK
    ESCAPE ROUTINE
  END-IF
END-IF                                  /* NG1 end
RESET CT-V
MOVE BY NAME #CONTACT TO CT-V

FIND (1) TERM WITH TERMINAL-ID = 'MASTERCO'
  ADD 1 TO #NEW-CONTACT-ID
  UPDATE
END-FIND
CT-V.CONTACT-ID := #NEW-CONTACT-ID

**CT-V.CRM-CONTACT-GUI := #CONTACT.CRM-CONTACT-GUI  /* NG4

CT-V.LAST-DATE-UPD  := *DATN
CT-V.LAST-TIME-UPD  := *TIMN
IF #CLERK-ID NE BLANK
  CT-V.LAST-USER-UPD := #CLERK-ID
ELSE
  CT-V.LAST-USER-UPD  := *USER
END-IF
CT-V.RANK-PROTECT := 'N'
CT-V.SPECIAL-PAY-IND := 'N'
CT-V.SSN-PROTECT := 'N'
IF CT-V.RANK-ID > 0
  CT-V.LAST-RANK-DATE-UPD := *DATN
  CT-V.LAST-RANK-USER-UPD := CT-V.LAST-USER-UPD
END-IF
CT-V.DEATH-CERT-CODE := 'N'
CT-V.HOMICIDE-SUICIDE-ACCIDENT-IND := 'N'
*
STORE CT-V
#CONTACT.CONTACT-ID := #NEW-CONTACT-ID
#UPDATED := TRUE
IF CT-V.PREFIX-TITLE = BLANK
  #CONTACT.PREFIX-TITLE := F-DEFAULT-PREFIX (< #NEW-CONTACT-ID >)
  IF #CONTACT.PREFIX-TITLE NE BLANK
    GET CT-V *ISN (0188)
    CT-V.PREFIX-TITLE := #CONTACT.PREFIX-TITLE
    UPDATE
  END-IF
END-IF
*
END-SUBROUTINE
*
ON ERROR
  COMPRESS 'Contact add:' *ERROR-NR *ERROR-LINE *PROGRAM *DATN *TIME INTO #MSG
    WITH DELIMITER ':'
  ESCAPE ROUTINE
END-ERROR
*
END
