* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNATDA.
/**
/** :author nguentchev
/* 
/* NAME     DATE   TAG   DESCRIPTION
/* ====     ====   ===   ===========
/* NGG   20180208  NG1   SOLA Project
/*
DEFINE DATA
PARAMETER USING LWSAAT
PARAMETER 
1 ErrorMsg   (A) DYNAMIC
LOCAL USING LWSLRQST  /* REQUEST codes
LOCAL USING LWSLITRL
LOCAL
/*  2 premium  (N7.2)
/*  2 term     (N3)
1 BLANK   (A1) CONST <' '>
1 AT-V VIEW OF A-APPL-TRACKING
  2 SSN (N9.0)
  2 NAME (A25)
  2 USER-SOURCE   (A3)
  2 DATE-RECEIVED (N8.0)
  2 ACTION-DATE   (N8)
  2 DATE-OF-BIRTH (N8.0)
  2 ACTION-FLAG   (A1)
  2 POLICY-TYPE   (A1)
  2 INS-ONLY      (A1)
  2 CAP-IND       (A1)
  2 CAP-LOAN-AMT  (N5.2)
  2 MODE             (A1)
  2 FACE-AMT-UNITS   (N4.0)
  2 FACE-AMT-DOLS    (N3.0)
  2 EXCH-1035        (N7.2)
  2 LUMP-SUM         (N7.2)
  2 E-DEP-MAX-AMT    (N7.2)
  2 E-DEPOSIT-AMT    (N7.2)
  2 PLAN             (A3)
  2 REDEFINE PLAN
    3 PLAN-1    (A1)
    3 PLAN-2    (A2)
  2 SEX              (A1)
  2 SMOKER-FLAG      (A1)
  2 APPL-SOURCE      (A7)
  2 MEMBER-CN        (A6)
  2 ID-NUMBER        (N6.0)
  2 HEIGHT-FT        (N2.0)
  2 HEIGHT-IN        (N2.0)
  2 WEIGHT           (N3.0)
  2 AT-STATUS        (A2)
  2 RATING-CODE      (A1)
  2 FIRST-PLACE-FEE  (N7.2)
  2 CONTACT-ID       (N8.0)
  2 CRM-APPL-ID    (A50)  /* Quote ID = Obsolete
  2 AT-GUID        (A50)  /* IASOrder.CrmApplicationID
1 CT-V VIEW OF A-CONTACTS
  2 CRM-CONTACT-GUI
1 #POLICY-ID       (A16)
1 #DESIGNATION-ID  (N8)
END-DEFINE
/* 
RESET ErrorMsg
IF AT-DATA.atnumber <= 0
  ErrorMsg := 'AT is not populated'
  ESCAPE ROUTINE
END-IF

FIND (1) AT-V WITH ID-NUMBER = AT-DATA.atnumber

  AT-DATA.Quote-GUID := AT-V.CRM-APPL-ID
  iascontactid := AT-V.CONTACT-ID
  FIND (1) CT-V WITH CONTACT-ID = AT-V.CONTACT-ID
    AT-DATA.ContactGUID := CT-V.CRM-CONTACT-GUI
  END-FIND

  AT-DATA.applicationtrackingid := AT-V.AT-GUID

  IF AT-DATA.Quote-GUID = BLANK
      and AT-DATA.applicationtrackingid = BLANK
    ErrorMsg := 'Quote GUID and AT-GUID are missing'
    ESCAPE ROUTINE
  END-IF

  /* Create
  IF AT-DATA.applicationtrackingid = BLANK
      OR= #RECORD-NOT-FOUND
    /* Get Contact GUID using Web API
    RESET AT-DATA.ContactGUID
    CALLNAT 'LWSNSRV' CONTACT-GET-REQ
      AT-V.CONTACT-ID
      #POLICY-ID
      AT-V.ID-NUMBER
      #DESIGNATION-ID
      ErrorMsg
    IF SUBSTR(ErrorMsg,1,4) EQ 'GUID' 
        AND *LENGTH(ErrorMsg) GT 36
/* Responce is like this: GUID=2ef77-...30fa
      AT-DATA.ContactGUID := SUBSTR(ErrorMsg,6,36)
      RESET ErrorMsg
    END-IF
    if AT-DATA.ContactGUID = BLANK
      ErrorMsg := 'Contact GUID is missing'
      ESCAPE ROUTINE
    END-IF
  end-if

  faceamount := FACE-AMT-UNITS * 1000 + FACE-AMT-DOLS
  membercn := VAL (MEMBER-CN)
  height := HEIGHT-FT + HEIGHT-IN / 100

  IF CAP-IND = 'Y'
    capindicator := 'true'
  ELSE
    capindicator := 'false'
  END-IF
  caploanamount := AT-V.CAP-LOAN-AMT
  applicationsource := AT-V.APPL-SOURCE

  exchange1035 := AT-V.EXCH-1035
  depositamount := AT-V.E-DEPOSIT-AMT
  depositmax := AT-V.E-DEP-MAX-AMT
  ratingcode := AT-V.RATING-CODE
  premium := AT-V.FIRST-PLACE-FEE

  AT-DATA.lumpsum := AT-V.LUMP-SUM
  AT-DATA.weight := AT-V.weight
  AT-DATA.plan := AT-V.PLAN

  AT-DATA.datereceived := AT-V.DATE-RECEIVED
  AT-DATA.actiondate := AT-V.ACTION-DATE

  IF AT-V.SMOKER-FLAG = 'Y'
    AT-DATA.smoker := 'true'
  ELSE
    AT-DATA.smoker := 'false'
  END-IF

  AT-DATA.atstatus := AT-V.AT-STATUS

  decide for first condition
    when AT-V.PLAN = 'RT'
      AT-DATA.term := 5
    when AT-V.PLAN IS (N3)
      AT-DATA.term := VAL (AT-V.PLAN)
    when AT-V.PLAN-2 IS (N2)
      AT-DATA.term := VAL (AT-V.PLAN-2)
    when none reset AT-DATA.term
  end-decide

END-FIND
END
