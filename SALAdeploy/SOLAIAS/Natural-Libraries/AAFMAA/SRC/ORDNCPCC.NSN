* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram ORDNCPCC.
/**
/** :author nguentchev
/* TODO Enter your code here
define data
parameter
1 #Insured-CN    (N6)
1 #Payer-CN      (N6)
1 #POLICY-NUMBER (A16)
1 CCSageGuid     (A32)
1 #Msg           (A) Dynamic
LOCAL USING OBJACC01
LOCAL USING GPRATR06 
local
1 BLANK            (A1) CONST <' '>
1 #GET-BY-ID       (A) DYNAMIC CONST <'GET-BY-GUID'>
1 #GET-CC          (A) DYNAMIC CONST <'GET-CC'>
1 #ADD             (A) DYNAMIC CONST <'ADD'>
1 #GET-BY-ISN      (A) DYNAMIC CONST <'GET-BY-ISN'>
1 CNT-V VIEW OF A-CONTACTS
  2 CRM-CONTACT-GUI
  2 CONTACT-ID
1 CC-ARRAY         (P9/3)
1 #D  (N8) 1 REDEFINE #D
  2 #YYYY  (A4)
  2 #MM    (A2)
1 J  (I4)
1 FUNCTION_CODE    (A) DYNAMIC INIT <'CreditCardInfo'>
1 #TRAN-CONTACT-ID (N8)
end-define

RESET PARM-CC-DATA
  PARM-CC-INFO
/*   Get Payer CC records
#PARM-ID-NUMBER := #Payer-CN
PARM-CC-DATA.#ACTION := #GET-CC
CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO
  CC-ARRAY (*)

if PARM-CC-DATA.#NUMBER-OF-RECORDS > 0
  #D := *DATN
  IF PARM-CC-INFO.CC-EXPIRATION-YEAR > #YYYY
      OR PARM-CC-INFO.CC-EXPIRATION-YEAR = #YYYY
      AND PARM-CC-INFO.CC-EXPIRATION-MONTH >= #MM
    ESCAPE ROUTINE  /* There is active CC linked to Payer
  END-IF
  PARM-CC-DATA.#ACTION := #GET-BY-ISN
  FOR J 1 TO 3
    IF CC-ARRAY (J) > 0
      PARM-CC-DATA.#ISN := CC-ARRAY (J)
      CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO
      IF PARM-CC-INFO.CC-EXPIRATION-YEAR > #YYYY
          OR PARM-CC-INFO.CC-EXPIRATION-YEAR = #YYYY
          AND PARM-CC-INFO.CC-EXPIRATION-MONTH >= #MM
        ESCAPE ROUTINE /* There is active CC linked to Payer
      END-IF
    END-IF
  END-FOR
end-if

PERFORM POPULATE-BEFORE-IMAGE
/*   Get Insured CC by GUID
#PARM-ID-NUMBER := #Insured-CN
PARM-CC-DATA.#ACTION := #GET-BY-ID
PARM-CC-INFO.CC-GUID := CCSageGuid
CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO

if PARM-CC-DATA.#NUMBER-OF-RECORDS > 0
  RESET PARM-CC-DATA
    LAST-TIME-UPDATE
    LAST-USER-UPDATE
    LAST-DATE-UPDATE
  #PARM-ID-NUMBER := #Payer-CN
  PARM-CC-DATA.#ACTION := #ADD
  PARM-CC-INFO.ID-NUMBER := #Payer-CN
  PARM-CC-INFO.POLICY-NUMBER := #POLICY-NUMBER
  find cnt-v with id-number = #Payer-CN
    CRM-CONTACT-ID := cnt-v.CRM-CONTACT-GUI
    #TRAN-CONTACT-ID := cnt-v.CONTACT-ID
  end-find
  CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO
  if PARM-CC-DATA.#RESULT-CODE NE BLANK
    #Msg := F-ERROR-DESCRIPTION(<#RESULT-CODE>)
  else
    PERFORM POPULATE-AFTER-IMAGE
    TR_ID_NUMBER  := PARM-CC-INFO.ID-NUMBER
    TR_NAME       := FUNCTION-CONTACT-NAME(<#TRAN-CONTACT-ID>)
    CALLNAT 'GPRNTR06' FUNCTION_CODE TRAN_LOG_HEADER
      BEFORE_IMAGE
      AFTER_IMAGE
  end-if
end-if

DEFINE SUBROUTINE POPULATE-BEFORE-IMAGE
/*                --------------------- 
BEFORE_IMAGE.DATE-LAST-UPDATE        := PARM-CC-INFO.LAST-DATE-UPDATE
BEFORE_IMAGE.CC-NUMBER               := PARM-CC-INFO.CC-NUMBER
BEFORE_IMAGE.CC-GUID                 := PARM-CC-INFO.CC-GUID              
COMPRESS PARM-CC-INFO.CC-EXPIRATION-MONTH PARM-CC-INFO.CC-EXPIRATION-YEAR
  INTO BEFORE_IMAGE.CC-EXPIRATION-DATE
BEFORE_IMAGE.CCHOLDER-NAME           := PARM-CC-INFO.CCHOLDER-NAME
BEFORE_IMAGE.CCHOLDER-ADDRESS-1      := PARM-CC-INFO.CCHOLDER-STREET-ADDR
BEFORE_IMAGE.CCHOLDER-ADDRESS-2      := PARM-CC-INFO.CCHOLDER-ADDL-ADDR
IF CC-ADDR-FORMAT-CODE = '1'
  BEFORE_IMAGE.CCHOLDER-ADDRESS-3    := PARM-CC-INFO.CCHOLDER-ZIP-CODE
ELSE
COMPRESS PARM-CC-INFO.CCHOLDER-POSTAL-CODE PARM-CC-INFO.CCHOLDER-COUNTRY
    INTO BEFORE_IMAGE.CCHOLDER-ADDRESS-3
END-IF
COMPRESS PARM-CC-INFO.CCHOLDER-CITY PARM-CC-INFO.CCHOLDER-STATE-CD BEFORE_IMAGE.CCHOLDER-ADDRESS-3
  INTO BEFORE_IMAGE.CCHOLDER-ADDRESS-3
BEFORE_IMAGE.CC-ADDR-VALIDATION-CODE := PARM-CC-INFO.CC-ADDR-VALIDATION-CODE
BEFORE_IMAGE.CC-DEFAULT-IND := PARM-CC-INFO.CC-DEFAULT-IND                 
IF BEFORE_IMAGE.CC-DEFAULT-IND = ' '                                       
  BEFORE_IMAGE.CC-DEFAULT-IND := 'N'                                       
END-IF  
END-SUBROUTINE

DEFINE SUBROUTINE POPULATE-AFTER-IMAGE
/*                --------------------
AFTER_IMAGE.CC-NUMBER               := PARM-CC-INFO.CC-NUMBER
AFTER_IMAGE.CC-GUID                 := PARM-CC-INFO.CC-GUID              
COMPRESS PARM-CC-INFO.CC-EXPIRATION-MONTH PARM-CC-INFO.CC-EXPIRATION-YEAR
  INTO AFTER_IMAGE.CC-EXPIRATION-DATE
AFTER_IMAGE.CCHOLDER-NAME           := PARM-CC-INFO.CCHOLDER-NAME
AFTER_IMAGE.CCHOLDER-ADDRESS-1      := PARM-CC-INFO.CCHOLDER-STREET-ADDR
AFTER_IMAGE.CCHOLDER-ADDRESS-2      := PARM-CC-INFO.CCHOLDER-ADDL-ADDR
IF CC-ADDR-FORMAT-CODE = '1'
  AFTER_IMAGE.CCHOLDER-ADDRESS-3    := PARM-CC-INFO.CCHOLDER-ZIP-CODE
ELSE
COMPRESS PARM-CC-INFO.CCHOLDER-POSTAL-CODE PARM-CC-INFO.CCHOLDER-COUNTRY
    INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
END-IF
COMPRESS PARM-CC-INFO.CCHOLDER-CITY PARM-CC-INFO.CCHOLDER-STATE-CD AFTER_IMAGE.CCHOLDER-ADDRESS-3
  INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
AFTER_IMAGE.CC-ADDR-VALIDATION-CODE := PARM-CC-INFO.CC-ADDR-VALIDATION-CODE
AFTER_IMAGE.CC-DEFAULT-IND := PARM-CC-INFO.CC-DEFAULT-IND                 
IF AFTER_IMAGE.CC-DEFAULT-IND = ' '                                       
  AFTER_IMAGE.CC-DEFAULT-IND := 'N'                                       
END-IF                                                                    
END-SUBROUTINE

END
