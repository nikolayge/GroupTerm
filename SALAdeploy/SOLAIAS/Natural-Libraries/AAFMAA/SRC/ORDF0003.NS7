* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Function ORDF0003.
/**
/** :author nguentchev
DEFINE FUNCTION GetCRMContactGUID RETURNS (A) DYNAMIC
  define data
  parameter
  1 #contact-id (N8) 
  LOCAL USING LWSLRQST  /* REQUEST codes
  local
  1 #NONE                    (A4) CONST <'NONE'>
  1 BLANK                    (A1) CONST <' '>
  1 ErrorMsg   (A) DYNAMIC
  1 CONTACT VIEW OF A-CONTACTS
    2 CRM-CONTACT-GUI
  1 #POLICY-ID  (A16)    /* NOT USED
  1 #AT-NUMBER  (N6)     /* NOT USED
  1 #DESIGNATION-ID (N8) /* NOT USED
*
  end-define
/* 
FIND (1) CONTACT WITH CONTACT-ID = #CONTACT-ID
  IF CONTACT.CRM-CONTACT-GUI EQ BLANK OR= #NONE  
    GetCRMContactGUID := #NONE
    /* Get Contact GUID using Web API
    CALLNAT 'LWSNSRV' CONTACT-GET-REQ
      #CONTACT-ID
      #POLICY-ID
      #AT-NUMBER
      #DESIGNATION-ID
      ErrorMsg
    IF SUBSTR(ErrorMsg,1,4) EQ 'GUID' 
        AND *LENGTH(ErrorMsg) GT 36
/* Responce is like this: GUID=2ef77-...30fa
      CONTACT.CRM-CONTACT-GUI := SUBSTR(ErrorMsg,6,36)
      UPDATE
      GetCRMContactGUID := CONTACT.CRM-CONTACT-GUI
    END-IF
  ELSE
    GetCRMContactGUID := CONTACT.CRM-CONTACT-GUI
  END-IF
END-FIND

END-FUNCTION
END
