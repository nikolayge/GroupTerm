* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNGUID.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 #REQUEST           (I4)
1 #POLICY-NUMBER     (A16)
1 #DESIGNATION-ID    (N8)
1 #POLICY-GUID       (A50)
1 #POLICY-ROLE-GUID  (A50/1:V)
1 #POLICY-ROLE       (N2/1:V)
1 #DESIGNATION-GUID  (A50)
1 #MSG               (A) DYNAMIC
LOCAL USING LWSLUID   /* GIUD operations
LOCAL
1 #RD-SUCCESS    (A7) CONST <'Success'>
1 BLANK          (A1) CONST <' '>
1 QUOTE          (A1) CONST <H'22'>
1 #GET-ALL       (I4) CONST <0>
1 PC-V VIEW OF A-POLICY-CONTACTS
  2 RELATION         (N2)
  2 POLICY-GUID      (A50)
  2 POLICY-ROLE-GUID (A50)
1 DS-V VIEW OF A-DESIGNATION
  2 DESIGNATION-GUID  (A50)
1 #PR-COUNT  (I4)
1 J          (I4)
1 #X         (A99)
1 #A         (A2)
END-DEFINE
/*
#MSG := #RD-SUCCESS

DECIDE ON FIRST VALUE OF #REQUEST
/*            Policy relations  CRM GUIDs
  VALUE SAVE-PR-GUID
    FOR J 1 TO 99
      IF #POLICY-ROLE (J) = 0
        ESCAPE BOTTOM
      END-IF
      MOVE EDITED #POLICY-ROLE (J) (EM=99) TO #A
      COMPRESS #X #A INTO #X
    END-FOR
    FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
      MOVE EDITED RELATION (EM=99) TO #A
      EXAMINE #X FOR #A GIVING POSITION J
      IF J > 0
        #PR-COUNT := (J - 1) / 3 + 1
        PC-V.POLICY-ROLE-GUID := #POLICY-ROLE-GUID (#PR-COUNT)
        UPDATE
      END-IF
    END-FIND
/*            Policy relations  CRM GUID Delete
  VALUE DELETE-PR-GUID
    FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
      RESET PC-V.POLICY-ROLE-GUID
      UPDATE
    END-FIND
 /*            Policy relations  CRM GUID Read
  VALUE GET-PR-GUID
    IF #POLICY-NUMBER NE BLANK
      FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
        IF PC-V.POLICY-GUID GT BLANK
          #POLICY-GUID := PC-V.POLICY-GUID
        END-IF
        IF PC-V.POLICY-ROLE-GUID GT BLANK
          ADD 1 TO #PR-COUNT
          #POLICY-ROLE-GUID (#PR-COUNT) := PC-V.POLICY-ROLE-GUID
          #POLICY-ROLE (#PR-COUNT) := PC-V.RELATION
        END-IF
      END-FIND
    END-IF

  VALUE #GET-ALL
    IF #POLICY-NUMBER NE BLANK
      FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
        IF PC-V.POLICY-GUID GT BLANK
          #POLICY-GUID := PC-V.POLICY-GUID
        END-IF
        IF PC-V.POLICY-ROLE-GUID GT BLANK
          ADD 1 TO #PR-COUNT
          #POLICY-ROLE-GUID (#PR-COUNT) := PC-V.POLICY-ROLE-GUID
          #POLICY-ROLE (#PR-COUNT) := PC-V.RELATION
        END-IF
      END-FIND
    END-IF
    IF #DESIGNATION-ID > 0
      FIND DS-V WITH DESIGNATION-ID = #DESIGNATION-ID
        #DESIGNATION-GUID := DS-V.DESIGNATION-GUID
      END-FIND
    END-IF
/*                      Policy CRM GUID Read
  VALUE GET-PO-GUID
    IF #POLICY-NUMBER NE BLANK
      FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
        IF PC-V.POLICY-GUID NE BLANK
          #POLICY-GUID := PC-V.POLICY-GUID
          ESCAPE BOTTOM
        END-IF
      END-FIND
    END-IF
/*                     Policy CRM GUID Update/Create
  VALUE SAVE-PO-GUID
    IF #POLICY-NUMBER NE BLANK
        AND #POLICY-GUID NE BLANK
      FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
        IF PC-V.RELATION = 1 /* insured
          EXAMINE #POLICY-GUID FOR QUOTE AND DELETE
          PC-V.POLICY-GUID := #POLICY-GUID
          UPDATE
          ESCAPE BOTTOM
        END-IF
      END-FIND
    ELSE
      #MSG := 'Store policy GUID failed'
    END-IF
/*                     Policy CRM GUID Delete
  VALUE DELETE-PO-GUID
    IF #POLICY-NUMBER NE BLANK
      FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
        RESET PC-V.POLICY-GUID
        UPDATE
      END-FIND
    END-IF
/*                     Designation CRM GUID Create/Update
  VALUE SAVE-DS-GUID
    IF #DESIGNATION-ID NE 0
        AND #DESIGNATION-GUID NE BLANK
      FIND DS-V WITH DESIGNATION-ID = #DESIGNATION-ID
        EXAMINE #DESIGNATION-GUID FOR QUOTE AND DELETE
        DS-V.DESIGNATION-GUID := #DESIGNATION-GUID
        UPDATE
      END-FIND
    ELSE
      #MSG := 'Store designation GUID failed'
    END-IF
/*                     Designation CRM GUID Read
  VALUE GET-DS-GUID
    FIND (1) DS-V WITH DESIGNATION-ID = #DESIGNATION-ID
      #DESIGNATION-GUID  := DS-V.DESIGNATION-GUID
    END-FIND
/*                     Designation CRM GUID Delete
  VALUE DELETE-DS-GUID
    FIND (1) DS-V WITH DESIGNATION-ID = #DESIGNATION-ID
      RESET DS-V.DESIGNATION-GUID
      UPDATE
    END-FIND

  NONE VALUE
    #MSG := 'Request is not implemented'
END-DECIDE

ON ERROR
  COMPRESS FULL
    #REQUEST
    #POLICY-NUMBER
    #DESIGNATION-ID
    INTO #MSG WITH DELIMITER ','

  CALLNAT 'LWSNIELG' #MSG
  ESCAPE ROUTINE
END-ERROR
END
