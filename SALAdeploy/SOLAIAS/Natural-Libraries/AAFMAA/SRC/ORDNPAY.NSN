* >Natural Source Header 000000
* :Mode S
* :CP
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram ORDNPAY.
/**
/** :author nguentchev
/*  At this point AT, STATUS and Contacts are created
/*  The Contact ID abd ID-NUMBER are required to create payor information
/*  For all applications there is one insured and one payer
/*
/* If the CC information is input in the Sales App process, the E-Deposit 
/* flag on AT should be populated with C 
/*
DEFINE DATA
PARAMETER
  USING ORDPDA
/* 2 #PARM-ID-NUMBER (N6.0) /* Payer CN
/* 2 CRM-CONTACT-ID (A36)   /* Payer CRM Contact GUID
/* 2 CC-GUID (A32)          /* CC Sage GUID
LOCAL USING OBJACC01
LOCAL USING GPRATR06 
LOCAL
1 CC-INFO-V VIEW OF A-CC-INFO
  2 CC-EXPIRATION-MONTH (A2)
  2 CC-EXPIRATION-YEAR  (A4)
  2 CC-DEFAULT-IND (A1)
1 BLANK            (A1) CONST <' '>
1 #GET-ALL         (A) DYNAMIC CONST <'GET-ALL'>
1 #GET-BY-ID       (A) DYNAMIC CONST <'GET-BY-GUID'>
1 #ADD             (A) DYNAMIC CONST <'ADD'>
1 #UPDATE-ADDRESS  (A) DYNAMIC CONST <'UPDATE-ADDRESS'>
1 j   (i4)
1 #ZipNine   (A9) 1 redefine #ZipNine
  2 #ZipFive   (A5)
  2 #ZipFour   (A4)
1 cnt-v view of a-contacts
  2 contact-id (n8)
  2 id-number  (n6)
1 #msg  (a) Dynamic
1 Payer-Contact-Id  (a36)
1 AT-V VIEW OF A-APPL-TRACKING
  2 E-DEPOSIT-IND (A1)
1 #TRAN-CONTACT-ID (N8)
1 #GEN-KEY   (A4)
1 GEN-TABLE-V VIEW OF A-GEN-TABLE
  2 GEN-KEY (A4)
  2 GEN-SUB-KEY (A3)
  2 GEN-ALPHA-VALUE (A50)
1 FUNCTION_CODE    (A) DYNAMIC INIT <'CreditCardInfo'>
END-DEFINE
/* 
RESET PARM-CC-DATA
  PARM-CC-INFO

if IASOrder.CCNumber = BLANK  /* No credit card info
  escape routine
end-if

IASOrder.UserCode := *TRIM (IASOrder.UserCode)
MOVE *TRANSLATE(IASOrder.UserCode, UPPER) TO IASOrder.UserCode
#GEN-KEY := 'WEBU'
FIND GEN-TABLE-V WITH GEN-KEY = #GEN-KEY
  ACCEPT IF *TRANSLATE(GEN-ALPHA-VALUE,UPPER)= IASOrder.UserCode
  TR_CLERK := GEN-SUB-KEY
  ESCAPE BOTTOM
END-FIND

/* get insured/payor CN and Contact ID
/* Only one application per request ==> Index 1
for j 1 to C@ApplicationRoles (1)
  if IASOrder.Name(1,j) = 'Insured'
    PARM-CC-INFO.CRM-CONTACT-ID := IASOrder.CRMContactID (1,j)  /* Guid
    cnt-v.contact-id := IASOrder.IASContactID(1,j)
    if IASOrder.IASContactID(1,j) > 0
      find (1) cnt-v with contact-id = cnt-v.contact-id
        PARM-CC-DATA.#PARM-ID-NUMBER := cnt-v.id-number
      end-find
    else
      find (1) cnt-v with CRM-CONTACT-GUI = IASOrder.CRMContactID (1,j)
        PARM-CC-DATA.#PARM-ID-NUMBER := cnt-v.id-number
      end-find
    end-if
    #TRAN-CONTACT-ID := cnt-v.contact-id
  end-if
  if IASOrder.Name(1,j) = 'Payer'
    Payer-Contact-Id := IASOrder.CRMContactID (1,j)
  end-if
end-for

if Payer-Contact-Id NE BLANK
    and Payer-Contact-Id NE PARM-CC-INFO.CRM-CONTACT-ID /* Insured
  find (1) cnt-v with CRM-CONTACT-GUI = Payer-Contact-Id
    if cnt-v.id-number = 0
      /* Create payer CN
      CALLNAT 'C2002NNM'
        cnt-v.contact-id
        cnt-v.id-number
        #msg
      update
    end-if
  end-find
end-if

PARM-CC-DATA.#ACTION := #GET-BY-ID
PARM-CC-INFO.CC-GUID := IASOrder.CCSageGuid
CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO

if PARM-CC-DATA.#NUMBER-OF-RECORDS = 0  /* New Credit card

  PARM-CC-DATA.#ACTION := #ADD

/* Populate Payment information
  PARM-CC-INFO.ID-NUMBER := PARM-CC-DATA.#PARM-ID-NUMBER
  PARM-CC-INFO.CC-NUMBER := IASOrder.CCNumber
  IF *LENGTH(IASOrder.CCExpMonth) = 1
  COMPRESS '0' IASOrder.CCExpMonth INTO PARM-CC-INFO.CC-EXPIRATION-MONTH
      LEAVING NO
  ELSE
    PARM-CC-INFO.CC-EXPIRATION-MONTH := IASOrder.CCExpMonth
  END-IF
  PARM-CC-INFO.CC-EXPIRATION-YEAR := IASOrder.CCExpYear
  PARM-CC-INFO.CCHOLDER-NAME := IASOrder.CardHolder

  PERFORM CC-ADDRESS

  CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO
ELSE
  PERFORM POPULATE-BEFORE-IMAGE

  PARM-CC-DATA.#ACTION := #UPDATE-ADDRESS

  PERFORM CC-ADDRESS

  CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO 

  PERFORM POPULATE-AFTER-IMAGE
  TR_ID_NUMBER  := PARM-CC-INFO.ID-NUMBER
  TR_NAME       := FUNCTION-CONTACT-NAME(<#TRAN-CONTACT-ID>)
  CALLNAT 'GPRNTR06' FUNCTION_CODE TRAN_LOG_HEADER
    BEFORE_IMAGE
    AFTER_IMAGE
END-IF

if PARM-CC-DATA.#RESULT-CODE NE BLANK
  IASOrder.ErrorDescription := F-ERROR-DESCRIPTION(<#RESULT-CODE>)
else
  FOR j 1 TO IASOrder.C@Application
    F1.
    FIND (1) AT-V WITH AT-GUID = IASOrder.CrmApplicationID (j)
      IF NO RECORDS FOUND
        ESCAPE BOTTOM
      END-NOREC
      AT-V.E-DEPOSIT-IND := 'C'
      update
    END-FIND
  END-FOR
end-if

/* Check if there are more than 3 CC. Prevent abend in CA CC
FNL.
FIND NUMBER CC-INFO-V WITH ID-NUMBER = PARM-CC-DATA.#PARM-ID-NUMBER
J := *NUMBER (FNL.)
IF J > 3
  PERFORM DELETE-BY-EXPIRATION
END-IF

DEFINE CC-ADDRESS
/*     ----------
/* CC Address
PARM-CC-INFO.CCHOLDER-STREET-ADDR := IASOrder.Street
PARM-CC-INFO.CCHOLDER-ADDL-ADDR := IASOrder.Unit
PARM-CC-INFO.CCHOLDER-CITY     := *TRANSLATE(IASOrder.City,UPPER)
PARM-CC-INFO.CCHOLDER-STATE-CD := *TRANSLATE(IASOrder.State,UPPER)
PARM-CC-INFO.CCHOLDER-COUNTRY  :=
  *TRANSLATE(IASorder.CountryCode,UPPER)
/* Only US address
IF PARM-CC-INFO.CCHOLDER-COUNTRY ='US' OR = 'USA' OR = BLANK
  #ZipNine := IASOrder.Zip
  PARM-CC-INFO.CCHOLDER-ZIP-CODE := val(#ZipFive)
  PARM-CC-INFO.CC-ADDR-FORMAT-CODE := '1'
ELSE
  PARM-CC-INFO.CCHOLDER-POSTAL-CODE := "Postal"
  PARM-CC-INFO.CC-ADDR-FORMAT-CODE := '2'
END-IF

PARM-CC-INFO.CC-DEFAULT-IND := 'Y'
PARM-CC-INFO.LAST-TIME-UPDATE := *TIMN
PARM-CC-INFO.LAST-USER-UPDATE :=
  FUNCTION-WEB-USER(<IASOrder.UserCode>)(2)
PARM-CC-INFO.LAST-DATE-UPDATE := *DATN

END-SUBROUTINE

DEFINE DELETE-BY-EXPIRATION
/*     --------------------
FIND CC-INFO-V WITH ID-NUMBER = PARM-CC-DATA.#PARM-ID-NUMBER
    WHERE CC-INFO-V.CC-DEFAULT-IND NE 'Y'
  PARM-CC-DATA.#ISN := *ISN
END-ALL
  AND
SORT BY 
    CC-INFO-V.CC-EXPIRATION-YEAR
    CC-INFO-V.CC-EXPIRATION-MONTH
    USING PARM-CC-DATA.#ISN

  GET CC-INFO-V #ISN
  DELETE

  ESCAPE BOTTOM

END-SORT
END-SUBROUTINE

DEFINE SUBROUTINE POPULATE-BEFORE-IMAGE
/*                --------------------- 
BEFORE_IMAGE.DATE-LAST-UPDATE        := PARM-CC-INFO.LAST-DATE-UPDATE
BEFORE_IMAGE.CC-NUMBER               := PARM-CC-INFO.CC-NUMBER
BEFORE_IMAGE.CC-GUID                 := PARM-CC-INFO.CC-GUID              
COMPRESS PARM-CC-INFO.CC-EXPIRATION-MONTH PARM-CC-INFO.CC-EXPIRATION-YEAR
  INTO BEFORE_IMAGE.CC-EXPIRATION-DATE
BEFORE_IMAGE.CCHOLDER-NAME           := PARM-CC-INFO.CCHOLDER-NAME
BEFORE_IMAGE.CCHOLDER-ADDRESS-1      := PARM-CC-INFO.CCHOLDER-STREET-ADDR
BEFORE_IMAGE.CCHOLDER-ADDRESS-2      := PARM-CC-INFO.CCHOLDER-ADDL-ADDR
IF CC-ADDR-FORMAT-CODE = '1'
  BEFORE_IMAGE.CCHOLDER-ADDRESS-3    := PARM-CC-INFO.CCHOLDER-ZIP-CODE
ELSE
COMPRESS PARM-CC-INFO.CCHOLDER-POSTAL-CODE PARM-CC-INFO.CCHOLDER-COUNTRY
    INTO BEFORE_IMAGE.CCHOLDER-ADDRESS-3
END-IF
COMPRESS PARM-CC-INFO.CCHOLDER-CITY PARM-CC-INFO.CCHOLDER-STATE-CD BEFORE_IMAGE.CCHOLDER-ADDRESS-3
  INTO BEFORE_IMAGE.CCHOLDER-ADDRESS-3
BEFORE_IMAGE.CC-ADDR-VALIDATION-CODE := PARM-CC-INFO.CC-ADDR-VALIDATION-CODE
BEFORE_IMAGE.CC-DEFAULT-IND := PARM-CC-INFO.CC-DEFAULT-IND                 
IF BEFORE_IMAGE.CC-DEFAULT-IND = ' '                                       
  BEFORE_IMAGE.CC-DEFAULT-IND := 'N'                                       
END-IF  
END-SUBROUTINE

DEFINE SUBROUTINE POPULATE-AFTER-IMAGE
/*                --------------------
AFTER_IMAGE.CC-NUMBER               := PARM-CC-INFO.CC-NUMBER
AFTER_IMAGE.CC-GUID                 := PARM-CC-INFO.CC-GUID              
COMPRESS PARM-CC-INFO.CC-EXPIRATION-MONTH PARM-CC-INFO.CC-EXPIRATION-YEAR
  INTO AFTER_IMAGE.CC-EXPIRATION-DATE
AFTER_IMAGE.CCHOLDER-NAME           := PARM-CC-INFO.CCHOLDER-NAME
AFTER_IMAGE.CCHOLDER-ADDRESS-1      := PARM-CC-INFO.CCHOLDER-STREET-ADDR
AFTER_IMAGE.CCHOLDER-ADDRESS-2      := PARM-CC-INFO.CCHOLDER-ADDL-ADDR
IF CC-ADDR-FORMAT-CODE = '1'
  AFTER_IMAGE.CCHOLDER-ADDRESS-3    := PARM-CC-INFO.CCHOLDER-ZIP-CODE
ELSE
COMPRESS PARM-CC-INFO.CCHOLDER-POSTAL-CODE PARM-CC-INFO.CCHOLDER-COUNTRY
    INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
END-IF
COMPRESS PARM-CC-INFO.CCHOLDER-CITY PARM-CC-INFO.CCHOLDER-STATE-CD AFTER_IMAGE.CCHOLDER-ADDRESS-3
  INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
AFTER_IMAGE.CC-ADDR-VALIDATION-CODE := PARM-CC-INFO.CC-ADDR-VALIDATION-CODE
AFTER_IMAGE.CC-DEFAULT-IND := PARM-CC-INFO.CC-DEFAULT-IND                 
IF AFTER_IMAGE.CC-DEFAULT-IND = ' '                                       
  AFTER_IMAGE.CC-DEFAULT-IND := 'N'                                       
END-IF                                                                    
END-SUBROUTINE
END
