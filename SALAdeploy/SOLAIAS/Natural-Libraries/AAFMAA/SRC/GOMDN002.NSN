* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
**************************************************************************
* SUBPROGRAM-ID: GOMDN002
* DESCRIPTION:   Access rank data
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
*
* PAM   09/10/2011 PM1  Store military service
*
**************************************************************************
*
DEFINE DATA
PARAMETER USING GOMDA001
PARAMETER
1 #MSG       (A) DYNAMIC
*
LOCAL
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID          (N8)
  2 RANK-ID             (N6)
  2 RANK-PROTECT        (A1)
  2 PREV-RANK-ID        (N6)
  2 MILITARY-STATUS     (A1)
  2 RANK-PROTECT-DATE   (N8)
  2 LAST-RANK-DATE-UPD  (N8)
  2 LAST-RANK-USER-UPD  (A8)
*
1 BLANK   (A1) CONST <' '>
1 ST-V VIEW OF A-STATUS             /* PM1
  2 MILITARY-SERVICE                 /* PM1
* 
1 RR-V VIEW OF A-RANK-REFERENCE
  2 RANK-ABBR           (A25)
  2 PAY-GRADE           (A3)
  2 MILITARY-ID         (A3)        /* PM1
  2 RANK-IN-SAS         (A3)
*
1 #RANK-IN-SAS       (A3)
1 #MESSAGE-NBR       (N3)
1 #PAY-IND           (A1)
*
END-DEFINE
*
RESET #MSG
IF #MIL.CONTACT-ID <= 0
  COMPRESS *PROGRAM 'No contact ID' INTO #MSG
ELSE
  FIND (1) CONTACT WITH CONTACT-ID = #MIL.CONTACT-ID
    MOVE BY NAME CONTACT TO #MIL
  END-FIND
*
  IF CONTACT.RANK-ID = 0
    CONTACT.RANK-ID := FUNCTION-RANK-ID
      (<#MIL.MILITARY-ID,
      #MIL.RANK-ABBR,
      #RANK-IN-SAS,
      #PAY-IND,#MESSAGE-NBR>)
  END-IF
*
  FIND (1) RR-V WITH RANK-ID = CONTACT.RANK-ID
    MOVE BY NAME RR-V TO #MIL
  END-FIND
*
  IF #MIL.MILITARY-ID = BLANK
    FIND (1) ST-V WITH ST-V.MEMBER-CONTACT-ID = #MIL.CONTACT-ID   /* PM1
      #MIL.MILITARY-ID := ST-V.MILITARY-SERVICE                   /* PM1
    END-FIND
  END-IF                                                   /* PM1
END-IF
*
END
