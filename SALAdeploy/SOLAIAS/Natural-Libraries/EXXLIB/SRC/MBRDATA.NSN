* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Look for PRINT before deployment
* Look for PRINT before deployment
* Look for PRINT before deployment
*
* CRM Admin interface : Pass Member data to Sales CRM as an XML message
************************************************************************
* NAME         : MBRDATA                                               *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Returns Contact data elements                         *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON                                         *
* NGG    20110508       Initial Creation                               *
* NGG    20110920  NG1  Change phone synch logic - MBRDATA1            *
* NGG    20111104  NG2  Add Process Indicator and Intro flag           *
* NGG    20120606  NG3  Additional filds to synch                      *
* NGG    20130308  NG4  Removes leap year skip
* NGG    08/07/2017 NG5 Online app changes                             *
* NGG    02/02/2018 NG6 SOLA Project                                   *
************************************************************************
* <Customers>
*  <Customer>
*    <ContactID>123456</ContactID>
*    <CN>123456</CN> ...
*
*  </Customer>
* </Customers>
*
DEFINE DATA
PARAMETER
1 #STATUS-ISN         (P8)
1 XML_SERIALZE_OUTPUT (A) DYNAMIC
1 #CSV-LINE           (A) DYNAMIC
1 #CRM-Result         (A) DYNAMIC
1 #CRM-Request        (I4)
1 #CONTACT-ID-IN      (N8) OPTIONAL
1 #CONTACT-ID-CRM     (A)  DYNAMIC OPTIONAL
*
LOCAL USING GOCOA001
LOCAL USING GOPHA001
LOCAL USING GOEMA001
LOCAL USING GOADA001
LOCAL USING GOADA002
LOCAL USING GODIA001
LOCAL USING GOMIA001
LOCAL USING GOPIA001
LOCAL
1 #PHONES     (6)                    /* NG1
  2 PHONE-TYPE-CODE     (A1)
  2 INTL-FLAG           (A1)
  2 PREFERRED-PHONE-IND (A1)
  2 DIAL-NUMBER         (A) DYNAMIC
  2 EXT                 (A6)
  2 REMARKS             (A) DYNAMIC
*
1 #EMAIL1
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
1 #EMAIL2
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
1 #EMAIL3
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
*
1 #PRIMARY
  2 STREET            (A) DYNAMIC
  2 UNIT              (A) DYNAMIC
  2 CITY              (A) DYNAMIC
  2 ZIP-CODE          (A) DYNAMIC
  2 STATE-CODE        (A) DYNAMIC
  2 COUNTRY-CODE      (A2)
  2 RETURNED-MAIL-IND (A1)
  2 ADDR-VALID-IND    (A5) /* TRUE/FALSE
*
1 #SECONDARY
  2 STREET            (A) DYNAMIC
  2 UNIT              (A) DYNAMIC
  2 CITY              (A) DYNAMIC
  2 ZIP-CODE          (A) DYNAMIC
  2 STATE-CODE        (A) DYNAMIC
  2 COUNTRY-CODE      (A2)
  2 RETURNED-MAIL-IND (A1)
  2 ADDR-VALID-IND    (A5) /* TRUE/FALSE
*
1 #CRM-Request-Rel    (I4)
1 #DATE-OF-BIRTH      (N8.0)
1 REDEFINE #DATE-OF-BIRTH
  2 DATE-OF-BIRTH-YYYY(N4)
  2 DATE-OF-BIRTH-MM  (A2)
  2 DATE-OF-BIRTH-DD  (A2)
1 REDEFINE #DATE-OF-BIRTH
  2 DATE-OF-BIRTH-A   (A8)
*
1 #TRUEFALSE    (A5)
1 #ID-NUMBER    (A7)
1 #Owner        (A) DYNAMIC
1 #RANK-ABBR    (A) DYNAMIC
1 #MILITARY-ID  (A) DYNAMIC
1 #RTYPE        (A1)
*
1 GEN-TABLE-V VIEW OF A-GEN-TABLE
  2 GEN-KEY (A4)
  2 GEN-SUB-KEY (A3)
  2 GEN-ALPHA-VALUE (A50)
*

1 MEMBER VIEW OF A-STATUS
  2 ID-NUMBER       (N6.0)
  2 REDEFINE ID-NUMBER
    3 #ID-NUMBER-S  (A6)
  2 MEMBER-FLAG     (A1)
  2 WIDOW-FLAG      (A1)
  2 APPL-SOURCE        (A7)
  2 MEMBER-CONTACT-ID  (N8)
  2 PROCESS-IND        (A1)
  2 INTRO-FLAG         (A1)
*
1 #ZIP-5      (A5)
1 #ZIP-4      (A4)
1 #ISMEMBER   (A1)
1 #GEN-KEY    (A4)
1 #Admin-user (A3)
1 #GENDER     (A6)
1 #SSN        (A11) 1 REDEFINE #SSN
  2 #SSN-N    (A9)
1 REDEFINE #SSN
  2 #SSN-3    (A3)
  2 #SSN-2    (A2)
  2 #SSN-4    (A4)
1 #TELE-NUMBER  (A12) 1 REDEFINE #TELE-NUMBER
  2 #TELE-NUMBER-10 (A10)
1 REDEFINE #TELE-NUMBER
  2 #TELE-NUMBER-3  (A3)
  2 #TELE-NUMBER-3A (A3)
  2 #TELE-NUMBER-4  (A4)
1 #NXT-OF-KIN-TELE-NUMBER  (A12) 1 REDEFINE #NXT-OF-KIN-TELE-NUMBER
  2 #NXT-NUMBER-10 (A10)
1 REDEFINE #NXT-OF-KIN-TELE-NUMBER
  2 #NXT-NUMBER-3  (A3)
  2 #NXT-NUMBER-3A (A3)
  2 #NXT-NUMBER-4  (A4)
1 #ID                      (A)  DYNAMIC
1 DATE-OF-RANK-A           (A8)
1 DATE-OF-DEATH-A          (A8)
1 PRIOR-DATE-OF-RANK-A     (A8)
1 DATE-RETIRED-A           (A8)
1 DATE-OF-MARRIAGE-A       (A8)
1 DATE-INIT-ENTRY-MIL-SVC-A(A8)
1 BASE-PAY-EF-DATE-A       (A8)
1 ACTIVE-DUTY-BASE-DATE-A  (A8)
1 #ERROR-CODE              (A2)
1 #VA-DISABILITY-PCT       (P3)
1 #VA-DISABILITY-PCT-A     (A3)
*
1 #NONE                    (A4) CONST <'NONE'>
1 BLANK                    (A1) CONST <' '>
1 #COMMA                   (A1) CONST <','>
1 #DASH                    (A1) CONST <'-'>
1 #TAB                     (A1) CONST <H'09'>
1 #Timeout                 (A2) CONST <'-2'>
1 #Natural-error           (A2) CONST <'-3'>
1 USA                      (A2) CONST <'US'>
1 PUERTORICO               (A2) CONST <'PR'>
1 #FALSE                   (A5) CONST <'false'>
1 #TRUE                    (A5) CONST <'true'>
1 #MESSAGE-LOG             (I4) CONST <2>
1 #ERROR-LOG               (I4) CONST <3>
1 #REQUEST-LOG             (I4) CONST <4>
1 #UPDATE-LOG              (I4) CONST <5>
1 #PHONE-TAG               (A8/6) CONST <'<Phone1>','<Phone2>','<Phone3>','<Phone4>','<Phone5>','<Phone6>'>
1 #PHONE-TAG-E             (A9/6) CONST <'</Phone1>','</Phone2>','</Phone3>','</Phone4>','</Phone5>','</Phone6>'>
1 #J                       (I4)
1 #N                       (I4)
END-DEFINE
*
IF #STATUS-ISN > 0
  G1.
  GET MEMBER #STATUS-ISN
  IF *ISN (G1.) = 0
COMPRESS 'There is no memeber record with ISN' #STATUS-ISN INTO #CRM-Result
    ESCAPE ROUTINE
  END-IF
  #CONTACT.CONTACT-ID := MEMBER.MEMBER-CONTACT-ID
ELSE
  IF #CONTACT-ID-IN SPECIFIED
    #CONTACT.CONTACT-ID := #CONTACT-ID-IN
  ELSE
    COMPRESS 'There is no CONTACT record with CI' #CONTACT-ID-IN
      INTO #CRM-Result
    ESCAPE ROUTINE
  END-IF
  FIND (1) MEMBER WITH MEMBER-CONTACT-ID = #CONTACT.CONTACT-ID
  END-FIND
END-IF
*
* ASSIGN XML_SERIALZE_OUTPUT = '<?xml version="1.0"?>'
RESET XML_SERIALZE_OUTPUT
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Contacts'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Contact'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
PERFORM COLLECT-DATA
*
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Contact'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Contacts'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
IF #CRM-Request > 0
CALLNAT 'CRMSYNC' XML_SERIALZE_OUTPUT (AD=O) #CRM-Request (AD=O) #CRM-Result (AD=M)
END-IF

DEFINE COLLECT-DATA
/*     ============
PERFORM FORMAT-RULES
PERFORM GENERATE-XML
PERFORM CSV-LINE
END-SUBROUTINE
*
DEFINE PRIMARY-ADDRESS
/*     ===============
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<PrimaryAddress'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #PRIMARY.STREET NE BLANK
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Street>'
    '<![CDATA['
    #PRIMARY.STREET
    ']]>'
    '</Street>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Unit>'
    '<![CDATA['
    #PRIMARY.UNIT
    ']]>'
    '</Unit>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<State>' #PRIMARY.STATE-CODE
    '</State>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<City>' #PRIMARY.CITY
    '</City>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<CountryCode>' #PRIMARY.COUNTRY-CODE
    '</CountryCode>' INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Zip>' #PRIMARY.ZIP-CODE
    '</Zip>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  IF #PRIMARY.ADDR-VALID-IND = 'Y'
    #PRIMARY.ADDR-VALID-IND := #TRUE
  ELSE
    #PRIMARY.ADDR-VALID-IND := #FALSE
  END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Certified>' #PRIMARY.ADDR-VALID-IND
    '</Certified>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Undeliverable>' #PRIMARY.RETURNED-MAIL-IND
    '</Undeliverable>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</PrimaryAddress'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
END-SUBROUTINE
*
DEFINE SECONDARY-ADDRESS
/*     ===============
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<SecondaryAddress'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #SECONDARY.STREET NE BLANK
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Street>'
    '<![CDATA['
    #SECONDARY.STREET
    ']]>'
    '</Street>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Unit>'
    '<![CDATA['
    #SECONDARY.UNIT
    ']]>'
    '</Unit>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<State>' #SECONDARY.STATE-CODE
    '</State>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<City>' #SECONDARY.CITY
    '</City>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<CountryCode>' #SECONDARY.COUNTRY-CODE
    '</CountryCode>' INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Zip>' #SECONDARY.ZIP-CODE
    '</Zip>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  IF #SECONDARY.ADDR-VALID-IND = BLANK OR= 'N'
    #SECONDARY.ADDR-VALID-IND := #FALSE
  ELSE
    #SECONDARY.ADDR-VALID-IND := #TRUE
  END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Certified>' #SECONDARY.ADDR-VALID-IND
    '</Certified>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Undeliverable>' #SECONDARY.RETURNED-MAIL-IND
    '</Undeliverable>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</SecondaryAddress'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
END-SUBROUTINE
*
DEFINE SERIALZE_PHONES               /* NG1 Start
/*     ===============
RESET #N
FOR #J 1 TO 6
  IF #PHONES.DIAL-NUMBER ( #J ) NE BLANK
    ADD 1 TO #N
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT #PHONE-TAG ( #N )
      INTO XML_SERIALZE_OUTPUT LEAVING NO
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<PhoneNumber>'
      #PHONES.DIAL-NUMBER ( #J ) '</PhoneNumber>'
      INTO XML_SERIALZE_OUTPUT LEAVING NO
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Extension>' #PHONES.EXT (#J)
      '</Extension>' INTO XML_SERIALZE_OUTPUT LEAVING NO
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Remarks>'
      #PHONES.REMARKS ( #J ) '</Remarks>'
      INTO XML_SERIALZE_OUTPUT LEAVING NO
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Type>'
      #PHONES.PHONE-TYPE-CODE (#J ) '</Type>'
      INTO XML_SERIALZE_OUTPUT LEAVING NO
    IF #PHONES.PREFERRED-PHONE-IND ( #J ) = 'Y'
      #TRUEFALSE := #TRUE
    ELSE
      #TRUEFALSE := #FALSE
    END-IF
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Preferred>' #TRUEFALSE
      '</Preferred>' INTO XML_SERIALZE_OUTPUT LEAVING NO
    IF #PHONES.INTL-FLAG ( #J ) = 'Y'
      #TRUEFALSE := #TRUE
    ELSE
      #TRUEFALSE := #FALSE
    END-IF
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<International>' #TRUEFALSE
      '</International>' INTO XML_SERIALZE_OUTPUT LEAVING NO
    COMPRESS NUMERIC XML_SERIALZE_OUTPUT #PHONE-TAG-E ( #N )
      INTO XML_SERIALZE_OUTPUT LEAVING NO
  END-IF
END-FOR
END-SUBROUTINE
*                                    /* NG1 End
DEFINE EMAIL1
/*     ======
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Email1'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #EMAIL1.EMAIL-ADDRESS NE BLANK
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<EmailAddress>'
    '<![CDATA['
    #EMAIL1.EMAIL-ADDRESS
    ']]>'
    '</EmailAddress>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Type>' #EMAIL1.EMAIL-TYPE
    '</Type>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Status>' #EMAIL1.STATUS
    '</Status>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Email1'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
END-SUBROUTINE
*
DEFINE EMAIL2
/*     ======
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Email2'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #EMAIL2.EMAIL-ADDRESS NE BLANK
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<EmailAddress>'
    '<![CDATA['
    #EMAIL2.EMAIL-ADDRESS
    ']]>'
    '</EmailAddress>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Type>' #EMAIL2.EMAIL-TYPE
    '</Type>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Status>' #EMAIL2.STATUS
    '</Status>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Email2'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
END-SUBROUTINE
*
DEFINE EMAIL3
/*     ======
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Email3'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #EMAIL3.EMAIL-ADDRESS NE BLANK
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<EmailAddress>'
    '<![CDATA['
    #EMAIL3.EMAIL-ADDRESS
    ']]>'
    '</EmailAddress>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Type>' #EMAIL3.EMAIL-TYPE
    '</Type>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Status>' #EMAIL3.STATUS
    '</Status>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Email3'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
END-SUBROUTINE
*
DEFINE FORMAT-RULES
/*     ============
IF #CSV-LINE = 'Import'
  #Owner := 'AAFMAA\administrator'
ELSE
  RESET #Owner
END-IF
*
IF MEMBER.PROCESS-IND NE 'C'
    AND NOT MEMBER.INTRO-FLAG = 'S' OR= 'K' OR= 'G'
  ASSIGN #ISMEMBER = 'Y'
ELSE
  ASSIGN #ISMEMBER = 'N'
END-IF
*
IF MEMBER.PROCESS-IND = 'C'
    AND MEMBER.INTRO-FLAG = 'A'
/*            Testing Online App   NG5
*  COMPRESS MEMBER.ID-NUMBER 'P' INTO #ID-NUMBER LEAVING NO    /* NG5

  ASSIGN #ID-NUMBER = MEMBER.ID-NUMBER                         /* NG5

ELSE
  IF MEMBER.ID-NUMBER = 0
    RESET #ID-NUMBER
  ELSE
    ASSIGN #ID-NUMBER = MEMBER.ID-NUMBER
  END-IF
END-IF
#ID := #NONE
*   Contact GUID is passed as a parm
IF #CONTACT-ID-CRM SPECIFIED
  IF #CONTACT-ID-CRM NE BLANK
    #ID := #CONTACT-ID-CRM 
  ELSE
    #ID := GetCRMContactGUID (< #CONTACT.CONTACT-ID >)
    #CONTACT-ID-CRM := #ID
  END-IF
ELSE
  #ID := GetCRMContactGUID (< #CONTACT.CONTACT-ID >)
END-IF

CALLNAT 'GOCON002' #CONTACT #MSG
IF #CONTACT.GENDER-CD = 'F'
  MOVE 'Female' TO #GENDER
ELSE
  IF #CONTACT.GENDER-CD = 'M'
    MOVE 'Male' TO #GENDER
  END-IF
END-IF
IF #CONTACT.SSN > 0
  MOVE EDITED #CONTACT.SSN (EM=9(9)) TO #SSN-N
  COMPRESS #SSN-3 #SSN-2 #SSN-4 INTO #SSN WITH DELIMITER #DASH
ELSE
  RESET #SSN
END-IF
#DATE-OF-BIRTH := #CONTACT.DATE-OF-BIRTH
IF #CONTACT.DATE-OF-BIRTH = 0
    OR (DATE-OF-BIRTH > 0 AND DATE-OF-BIRTH NE MASK(18-20YYMMDD))
*    OR (DATE-OF-BIRTH > 0 AND DATE-OF-BIRTH-A EQ MASK(YYYY'0229'))   /* Temporary CRM ????  Removed NG4
*   print 'Rejected Date of Birth' MEMBER.DATE-OF-BIRTH
  RESET DATE-OF-BIRTH-A
END-IF
#RANK-ABBR := F-GET-RANK-ABBR (< #CONTACT.RANK-ID >)
#MILITARY-ID := F-GET-RANK-MILID (< #CONTACT.RANK-ID >)
*                                  Phones    NG1
CALLNAT 'MBRDATA1' #CONTACT.CONTACT-ID #PHONES (*)
*                                  eMail
CALLNAT 'MBRDATA2' #CONTACT.CONTACT-ID #EMAIL1 #EMAIL2 #EMAIL3
*                                   Address
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT.CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'S'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-REL.STATUS NE 'D'
  MOVE BY NAME #ADDR-POOL TO #SECONDARY
  #SECONDARY.RETURNED-MAIL-IND := #ADDR-REL.RETURNED-MAIL-IND
  IF #ADDR-POOL.COUNTRY-CODE = USA OR= PUERTORICO
    IF ZIP-5 > 0 AND ZIP-4 > 0
      MOVE EDITED ZIP-5 (EM=99999) TO #ZIP-5
      MOVE EDITED ZIP-4 (EM=9999)  TO #ZIP-4
    COMPRESS #ZIP-5 #ZIP-4 INTO #SECONDARY.ZIP-CODE WITH DELIMITER #DASH
    ELSE
      MOVE EDITED ZIP-5 (EM=99999) TO #SECONDARY.ZIP-CODE
    END-IF
  ELSE
    #SECONDARY.ZIP-CODE := #ADDR-POOL.POSTAL-CODE
    #SECONDARY.STATE-CODE := #ADDR-POOL.INTL-REGION
  END-IF
END-IF
*                       Primary Address
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT.CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'P'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-REL.STATUS NE 'D'
  MOVE BY NAME #ADDR-POOL TO #PRIMARY
  #PRIMARY.RETURNED-MAIL-IND := #ADDR-REL.RETURNED-MAIL-IND
  IF #ADDR-POOL.COUNTRY-CODE = USA OR= PUERTORICO
    IF ZIP-5 > 0 AND ZIP-4 > 0
      MOVE EDITED ZIP-5 (EM=99999) TO #ZIP-5
      MOVE EDITED ZIP-4 (EM=9999)  TO #ZIP-4
      COMPRESS #ZIP-5 #ZIP-4 INTO #PRIMARY.ZIP-CODE WITH DELIMITER #DASH
    ELSE
      MOVE EDITED ZIP-5 (EM=99999) TO #PRIMARY.ZIP-CODE
    END-IF
  ELSE
    #PRIMARY.ZIP-CODE := #ADDR-POOL.POSTAL-CODE
    #PRIMARY.STATE-CODE := #ADDR-POOL.INTL-REGION
  END-IF
END-IF
*                                  Military data
#DEATH-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GODIN002' #DEATH-INFO
#MILITARY-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GOMIN002' #MILITARY-INFO
IF #MILITARY-INFO.VA-DISABILITY-PCT NE 0
  #VA-DISABILITY-PCT := #MILITARY-INFO.VA-DISABILITY-PCT * 100
  MOVE EDITED #VA-DISABILITY-PCT ( EM=ZZ9 ) TO #VA-DISABILITY-PCT-A
  IF #VA-DISABILITY-PCT-A = '999'
    RESET #VA-DISABILITY-PCT-A
  END-IF
ELSE
  RESET #VA-DISABILITY-PCT-A
END-IF
#PERSONAL-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GOPIN002' #PERSONAL-INFO
*
DECIDE FOR EVERY CONDITION              /* RE1 START
  WHEN #MILITARY-INFO.DATE-OF-RANK > 0
    MOVE #MILITARY-INFO.DATE-OF-RANK TO DATE-OF-RANK-A
  WHEN #DEATH-INFO.DATE-OF-DEATH > 0
    MOVE #DEATH-INFO.DATE-OF-DEATH TO DATE-OF-DEATH-A
  WHEN #MILITARY-INFO.PRIOR-DATE-OF-RANK > 0
    MOVE #MILITARY-INFO.PRIOR-DATE-OF-RANK TO PRIOR-DATE-OF-RANK-A
  WHEN #MILITARY-INFO.DATE-RETIRED > 0
    MOVE #MILITARY-INFO.DATE-RETIRED TO DATE-RETIRED-A
  WHEN #PERSONAL-INFO.DATE-OF-MARRIAGE > 0
    MOVE #PERSONAL-INFO.DATE-OF-MARRIAGE TO DATE-OF-MARRIAGE-A
  WHEN #MILITARY-INFO.DATE-INIT-ENTRY-MIL-SVC > 0
MOVE #MILITARY-INFO.DATE-INIT-ENTRY-MIL-SVC TO DATE-INIT-ENTRY-MIL-SVC-A
  WHEN #MILITARY-INFO.BASE-PAY-EF-DATE > 0
    MOVE #MILITARY-INFO.BASE-PAY-EF-DATE TO BASE-PAY-EF-DATE-A
  WHEN #MILITARY-INFO.ACTIVE-DUTY-BASE-DATE > 0
    MOVE #MILITARY-INFO.ACTIVE-DUTY-BASE-DATE TO ACTIVE-DUTY-BASE-DATE-A
  WHEN NONE
    IGNORE
END-DECIDE                              /* RE1 END
*
IF #CONTACT.CONTACT-TYPE = 'O' AND #CONTACT.LAST-NAME = ' '  /* RE2 START
MOVE #CONTACT.FIRM-ORGANIZATION-NAME TO #CONTACT.LAST-NAME  /* RE2 START
END-IF                                                       /* RE2 START
*
END-SUBROUTINE
/*
DEFINE GENERATE-XML
/*     ============
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<ContactID>' #CONTACT.CONTACT-ID '</ContactID>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
IF #ID NE #NONE
  COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Id>' #ID '</Id>'
    INTO XML_SERIALZE_OUTPUT LEAVING NO
END-IF
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<CN>' #ID-NUMBER '</CN>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<AppSource>' MEMBER.APPL-SOURCE '</AppSource>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Owner>' #Owner '</Owner>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<BranchOfService>' #MILITARY-ID
  '</BranchOfService>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<MilitaryStatus>' #CONTACT.MILITARY-STATUS
  '</MilitaryStatus>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Prefix>'
  '<![CDATA['
  #CONTACT.PREFIX-TITLE
  ']]>'
  '</Prefix>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<FirstName>'
  '<![CDATA['
  #CONTACT.FIRST-NAME
  ']]>'
  '</FirstName>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<MiddleName>'
  '<![CDATA['
  #CONTACT.MIDDLE-NAME
  ']]>'
  '</MiddleName>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<LastName>'
  '<![CDATA['
  #CONTACT.LAST-NAME
  ']]>'
  '</LastName>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Suffix>'
  '<![CDATA['
  #CONTACT.SUFFIX
  ']]>'
  '</Suffix>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<MilitaryRank>' #RANK-ABBR
  '</MilitaryRank>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<MilitaryRankId>' #CONTACT.RANK-ID
  '</MilitaryRankId>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<ContactType>' #CONTACT.CONTACT-TYPE
  '</ContactType>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<OrganizationName>'
  '<![CDATA['
  #CONTACT.FIRM-ORGANIZATION-NAME
  ']]>'
  '</OrganizationName>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<GovtID>' #SSN
  '</GovtID>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateOfBirth>' DATE-OF-BIRTH-A
  '</DateOfBirth>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
*                Male Female
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Gender>' #GENDER
  '</Gender>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
*                Member Flag
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<IsMember>' #ISMEMBER
  '</IsMember>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateOfPromotion>' DATE-OF-RANK-A   /* RE1
  '</DateOfPromotion>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateOfDeath>' DATE-OF-DEATH-A  /* RE1
  '</DateOfDeath>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateOfPreviousPromotion>' PRIOR-DATE-OF-RANK-A /* RE1
  '</DateOfPreviousPromotion>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateRetired>' DATE-RETIRED-A  /* RE1
  '</DateRetired>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<SBPOption>' #MILITARY-INFO.MIL-SBP-OPT
  '</SBPOption>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DateOfMarriage>' DATE-OF-MARRIAGE-A  /* RE1
  '</DateOfMarriage>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<CauseOfDeath>'
  '<![CDATA['
  #DEATH-INFO.CAUSE-OF-DEATH
  ']]>'
  '</CauseOfDeath>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DIEMS>' DATE-INIT-ENTRY-MIL-SVC-A  /* RE1
  '</DIEMS>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<PEBD>' BASE-PAY-EF-DATE-A  /* RE1
  '</PEBD>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<BASD>' ACTIVE-DUTY-BASE-DATE-A  /* RE1
  '</BASD>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<VAClaimNumber>' #MILITARY-INFO.VA-CLAIM-NUMBER /* RE2
  '</VAClaimNumber>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<VADisabilityPercent>' #VA-DISABILITY-PCT-A
  '</VADisabilityPercent>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<VADisabilityDate>' #MILITARY-INFO.VA-DISABLE-DATE  /* RE2
  '</VADisabilityDate>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DeathDocumentValidationCode>'
  #DEATH-INFO.DEATH-DOCUMENT-VALIDATION-CODE
  '</DeathDocumentValidationCode>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<DeathServiceConnected>' #MILITARY-INFO.DEATH-SERVICE-CON
  '</DeathServiceConnected>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<IntroFlag>' MEMBER.INTRO-FLAG  '</IntroFlag>'  /* NG2
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<ProcessIndicator>' MEMBER.PROCESS-IND          /* NG2
  '</ProcessIndicator>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
*                 Additional data elements    NG3
CALLNAT 'MBRDATA3' #CONTACT.CONTACT-ID XML_SERIALZE_OUTPUT
*                 Primary Address
PERFORM PRIMARY-ADDRESS
*                 Secondary Address
PERFORM SECONDARY-ADDRESS
*                 Up to six phones      NG1
PERFORM SERIALZE_PHONES
*                 email ONE
PERFORM EMAIL1
*                 email TWO
PERFORM EMAIL2
*                 email THREE
PERFORM EMAIL3
*
END-SUBROUTINE
/*
DEFINE CSV-LINE
/*     ========
IF #CRM-Request EQ 0
  RESET #CSV-LINE
END-IF
END-SUBROUTINE
*
DEFINE SET-OWNER
/*     =========
#GEN-KEY := 'WEBU'
#Admin-user := 'AAK'
READ GEN-TABLE-V WITH GEN-KEY = #GEN-KEY
  IF GEN-KEY NE #GEN-KEY
    RESET #Owner
    ESCAPE BOTTOM
  END-IF
  ACCEPT IF GEN-SUB-KEY = #Admin-user
  #Owner := GEN-ALPHA-VALUE
  ESCAPE BOTTOM
END-READ
END-SUBROUTINE
*
ON ERROR
/*
  RESET #ERROR-CODE
  CALLNAT 'CRMLOG' #ERROR-CODE #CRM-Result
    #ERROR-LOG
/*
  ESCAPE ROUTINE
END-ERROR

END
