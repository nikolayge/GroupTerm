* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Access A-STATUS
* OBJNMINF - Subprogram to get/update military
* and some other member information from Status file
****************************************************
* MODIFICATION LOG
****************************************************
* USER   DATE      TAG  REASON
* YAK    10152010  YAK  Initial Creation
* YAK    01262016  YK1  Add fields for MC-II FIR
****************************************************
*
DEFINE DATA
PARAMETER USING OBJAMINF
*
LOCAL USING OBJLMINF
LOCAL USING ERROR_L
LOCAL
1 #SO     (A) DYNAMIC CONST <'SO'>
1 #ACTIVE (A) DYNAMIC CONST <'D'>
1 #I      (I2)
END-DEFINE
*
* Main processing
*
DECIDE ON FIRST VALUE #ACTION
  VALUE 'UPDATE', 'UPDATE-MIL-MC'
    IF ISN = 0
      PERFORM UPDATE-RECORD-BY-KEY
    ELSE
      PERFORM UPDATE-RECORD-BY-ISN
    END-IF
  VALUE 'GET'
    PERFORM GET-RECORD
  VALUE 'GET-MBR-ST'
    PERFORM GET-MBR-STATUS
  VALUE 'UPDATE-MIL-AUC'
    PERFORM UPDATE-MIL-AUC
  NONE
    IGNORE
END-DECIDE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-ISN
**************************************
*
GT-U. GET MIL-INFO-V ISN
IF PARM-MEMBER-INFO.RANK <> MIL-INFO-V.RANK
  PARM-MEMBER-INFO.PRIOR-DATE-OF-RANK := MIL-INFO-V.DATE-OF-RANK
END-IF
MOVE BY NAME PARM-MEMBER-INFO.MIL-INFO TO MIL-INFO-V
WRITE WORK FILE 1 VARIABLE 'UPDATE MEMBER-INFO ID = ' MIL-INFO-V.ID-NUMBER
UPDATE (GT-U.)
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-KEY
**************************************
*
FIND-U.
FIND(1) MIL-INFO-V WITH ID-NUMBER = PARM-MEMBER-INFO.ID-NUMBER
  IF PARM-MEMBER-INFO.RANK <> MIL-INFO-V.RANK
    PARM-MEMBER-INFO.PRIOR-DATE-OF-RANK := MIL-INFO-V.DATE-OF-RANK
  END-IF
  MOVE BY NAME PARM-MEMBER-INFO.MIL-INFO TO MIL-INFO-V
  UPDATE (FIND-U.)
  WRITE WORK FILE 1 VARIABLE 'UPDATE MEMBER-INFO ID = ' MIL-INFO-V.ID-NUMBER
END-FIND
*
END-SUBROUTINE
*
********************************
DEFINE SUBROUTINE UPDATE-MIL-AUC
********************************
*
FIND-U1.
FIND(1) MIL-INFO-V WITH ID-NUMBER = PARM-MEMBER-INFO.ID-NUMBER
  IF PARM-MEMBER-INFO.RANK <> MIL-INFO-V.RANK
    PARM-MEMBER-INFO.PRIOR-DATE-OF-RANK := MIL-INFO-V.DATE-OF-RANK
  END-IF
  MIL-INFO-V.RANK               := PARM-MEMBER-INFO.RANK
  MIL-INFO-V.MILITARY-STATUS    := PARM-MEMBER-INFO.MILITARY-STATUS
  MIL-INFO-V.MILITARY-SERVICE   := PARM-MEMBER-INFO.MILITARY-SERVICE
  MIL-INFO-V.DATE-OF-RANK       := PARM-MEMBER-INFO.DATE-OF-RANK
  MIL-INFO-V.PRIOR-DATE-OF-RANK := PARM-MEMBER-INFO.PRIOR-DATE-OF-RANK
  MIL-INFO-V.DATE-RETIRED       := PARM-MEMBER-INFO.DATE-RETIRED
  UPDATE (FIND-U1.)
  WRITE WORK FILE 6 VARIABLE 'UPDATE MEMBER-INFO ID = ' MIL-INFO-V.ID-NUMBER
* Pass all fields back to correctly populate After-Image for transaction log
  MOVE BY NAME MIL-INFO-V TO PARM-MEMBER-INFO.MIL-INFO
END-FIND
*
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE GET-RECORD
****************************
*
FIND(1) MIL-INFO-V WITH ID-NUMBER = PARM-MEMBER-INFO.ID-NUMBER
  IF NO RECORDS FOUND
    PARM-MEMBER-INFO.#RETURN-CODE := #ID-NOT-FOUND
  END-NOREC
  ISN    := *ISN
  MOVE BY NAME MIL-INFO-V TO PARM-MEMBER-INFO
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
********************************
DEFINE SUBROUTINE GET-MBR-STATUS
********************************
*
FIND(1) MIL-INFO-V WITH ID-NUMBER = PARM-MEMBER-INFO.ID-NUMBER
  IF NO RECORDS FOUND
    PARM-MEMBER-INFO.#RETURN-CODE := #ID-NOT-FOUND
  END-NOREC
  PARM-MEMBER-INFO.MEMBER-CONTACT-ID  := MIL-INFO-V.MEMBER-CONTACT-ID
  PARM-MEMBER-INFO.MEMBER-FLAG        := MIL-INFO-V.MEMBER-FLAG
  PARM-MEMBER-INFO.WIDOW-FLAG         := MIL-INFO-V.WIDOW-FLAG
  PARM-MEMBER-INFO.WIDOW-FLAG         := MIL-INFO-V.WIDOW-FLAG
  PARM-MEMBER-INFO.SPOUSE-CONTACT-ID  := MIL-INFO-V.SPOUSE-CONTACT-ID
  PARM-MEMBER-INFO.PROCESS-IND        := MIL-INFO-V.PROCESS-IND
  PARM-MEMBER-INFO.INTRO-FLAG         := MIL-INFO-V.INTRO-FLAG
  PARM-MEMBER-INFO.MILITARY-SERVICE   := MIL-INFO-V.MILITARY-SERVICE
  PARM-MEMBER-INFO.NUMBER-MEMBERSHIPS := MIL-INFO-V.NUMBER-MEMBERSHIPS
  PARM-MEMBER-INFO.DI-BA-MAIL-CD      := MIL-INFO-V.DI-BA-MAIL-CD
  PARM-MEMBER-INFO.ANNUAL-RPT-FLAG    := MIL-INFO-V.ANNUAL-RPT-FLAG
  PARM-MEMBER-INFO.MARRIAGE-CERT-CODE := MIL-INFO-V.MARRIAGE-CERT-CODE   /* YK1
  PARM-MEMBER-INFO.STATUS-CODE-SP     := MIL-INFO-V.STATUS-CODE-SP       /* YK1
  IF PLAN(*) = #SO
    EXAMINE PLAN(*) FOR #SO INDEX #I
    IF #I <> 0 AND STATUS(#I) = #ACTIVE
      PARM-MEMBER-INFO.SERVICE-ONLY := TRUE
    END-IF
  END-IF
END-FIND
END-SUBROUTINE
*
END
