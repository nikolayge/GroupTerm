* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online
* Member Benefits Calculation routine
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    07142011       Initial Creation
* YAK    02042016  YK2  MC-II FIR - add new calculations 
********************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.BENEFIT-ANALYSIS-BLOCK
PARAMETER
1 #BENEFIT-TYPE (A) DYNAMIC
1 #CN  (N6)
1 #BENEFIT-AMOUNT   (P5)
1 #DATE-OF-BIRTH    (N8) OPTIONAL
1 #BENEFIT-DATE     (N6) OPTIONAL
1 #BENEFIT-PCT      (P1.2) OPTIONAL
*
LOCAL USING OBJLMINF
LOCAL
*
1 #BENEFITS-SS            (A) DYNAMIC CONST<'BenefitsSS'>
1 #BENEFITS-VA            (A) DYNAMIC CONST<'BenefitsVA'>
1 #BENEFITS-MILITARY      (A) DYNAMIC CONST<'BenefitsMilitary'>
1 #I                (I2)
1 #II               (I2)
1 #I1               (I2)
* 1 #CGA-SCR-COMMAND
* 1 #CGA-DOB          (N8)
* 1 REDEFINE #CGA-DOB
*   2 #CGA-DOB-YYYY  (N4)
*   2 #CGA-DOB-MM    (N2)
*   2 #CGA-DOB-DD    (N2)
* 1 #CGA-S-S-START-DATE (N6)
1 #MBR-DOB    (N8)
1 REDEFINE #MBR-DOB
  2 #MBR-YYYYMM  (N6)
  2 REDEFINE #MBR-YYYYMM
    3 #MBR-YYYY    (N4)
    3 #MBR-MM      (N2)
  2 #MBR-DD      (N2)
1 #MBR-BENE-DT         (N8)
1 REDEFINE #MBR-BENE-DT
  2 #MBR-BENE-YYYYMM    (N6)
  2 REDEFINE #MBR-BENE-YYYYMM
    3 #MBR-BENE-YYYY     (N4)
    3 #MBR-BENE-MM       (N2)
  2 #MBR-BENE-DD        (N2)
1 #MEMBR-BENE-MNTHLY-INCR (P.7) INIT <.0055555>
*  ABOVE IS (1.00 - 0.800) / 36 MONTHS
1 #MEMBR-BENE-NU-MNTHLY-INCR   (P.7)  INIT <.0041667>
1 #MBR-SS-MONS-PRIOR-TO-65     (P3)
1 #MONTHS-PRIOR-TO-65  (P3)
1 #MBR-SS-RET-PCT              (P1.6)
1 #WYYYYMMDD-LOW
  2 #WYYYY-LOW (N4)
  2 #WMM-LOW   (N2)
  2 #WDD-LOW   (N2)
1 REDEFINE #WYYYYMMDD-LOW
  2 #WYYYYMMDD-LOW-N (N8)
1 #SS-MBR-RET-YEARS  (N3)
1 #SS-MBR-RET-MONS   (N2)
1 #SS-MBR-RET-PCT    (A6)
1 #SS-MBR-RET-AMT    (N5.2)
*
1 #WYYYYMMDD-HIGH
  2 #WYYYY-HIGH  (N4)
  2 #WMM-HIGH    (N2)
  2 #WDD-HIGH    (N2)
1 REDEFINE #WYYYYMMDD-HIGH
  2 #WYYYYMMDD-HIGH-N (N8)
1 REDEFINE #WYYYYMMDD-HIGH
  2 #DATE-DIFF        (N8)
1 REDEFINE #WYYYYMMDD-HIGH
  2 #DATE-DIFF-A      (A8)
1 #YYYYMMDD  (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM      (N2)
  2 #DD-HIGH (N2)
1 #MBR-ADDL-FRA-MONS           (P2) /* FULL RET AGE MONS PAST AGE 65
1 #MBR-FRA-MONS-FROM-BIRTH     (P3) /* FULL RET AGE IN MONTHS
1 #MBR-TAKE-BENE-MONS-FROM-BIRTH (P3) /* TAKE RET DT IN MONTHS
1 #MBR-NUM-MONS-PRIOR-TO-FRA   (P5) /* MONS PRIOR TO FULL RET AGE
1 #ADJ-MONS  (L)
* below is linkage to GVADCSXX   /* DW14
1 #ID-NUMBER             (N6)
1 #VA-DISAB-PCT          (N1.2)
1 #VA-SPC-COMP-CODE      (A5)   /* DW18
1 #VA-DISAB-BASIC-AMT    (N5)
1 #VA-SPC-COMP-AMT       (N5)
1 #VA-ADDL-SPS-AMT       (N5)
1 #VA-1-DEP-UND-18-AMT   (N5)
1 #VA-EA-ADDL-UND-18-AMT (N5)
1 #VA-AGE-18-24-AMT      (N5)
1 #VA-CHILDREN-AMT       (N5)
1 #VA-TOT-DISAB-AMT      (N5)
1 #EFF-DT                (A7)
1 #UNDER-18              (N3)
1 #UNDER-18-ADDL         (N3)
1 #AGE-18-24             (N3)
1 #1ST-CHLD-UND-18       (N5)
1 #ADDL-CHLD-UND-18      (N5)
1 #EACH-SCH-CHILD        (N5)
1 #SPS-Y-N               (A1)
1 #EFF-DATE              (A7)
1 #COMBAT-CALC-IND       (A1)
* above is linkage to GVADCSXX
*
END-DEFINE
*
DECIDE ON FIRST VALUE #BENEFIT-TYPE
  VALUE #BENEFITS-SS
    PERFORM CALCULATE-SS-BENEFITS
  VALUE #BENEFITS-VA
    PERFORM CALCULATE-VA-BENEFITS
  VALUE #BENEFITS-MILITARY
    PERFORM CALCULATE-MILITARY-BENEFITS
  NONE
    IGNORE
END-DECIDE
*
***************************************
DEFINE SUBROUTINE CALCULATE-SS-BENEFITS
***************************************
* Calculate if not entered - copy from E4200PDM
*
FIND(1) MIL-INFO-V WITH ID-NUMBER = #CN
END-FIND
*
IF #BENEFIT-DATE SPECIFIED AND
    #DATE-OF-BIRTH SPECIFIED AND #BENEFIT-DATE NE 0
    AND (#BENEFIT-AMOUNT = 0)
    AND (MIL-INFO-V.SOC-SEC-PIA NE 0)
    AND (#DATE-OF-BIRTH GE 19180101)
  IF MIL-INFO-V.STATUS-CODE = 'D'
    MOVE MIL-INFO-V.SOC-SEC-PIA TO MIL-INFO-V.MBR-SS
  ELSE
* * * * * * * CALCULATE MEMBER EARLY RETIREMENT SOC-SEC PIA * * *
* * * * * * * * THIS SAME LOGIC IS IN D3080PBA AND D3010BR *
    MOVE 00 TO #MONTHS-PRIOR-TO-65
    MOVE 1.00 TO #MBR-SS-RET-PCT
    #CGA-DOB := #DATE-OF-BIRTH
    #CGA-S-S-START-DATE := #BENEFIT-DATE
    PERFORM CALC-MBR-SS-BENE
    COMPUTE ROUNDED MIL-INFO-V.MBR-SS = MIL-INFO-V.SOC-SEC-PIA  * #MBR-SS-RET-PCT
    IF MIL-INFO-V.STATUS-CODE = ' '
      MOVE 'Z' TO MIL-INFO-V.STATUS-CODE                /* #SS STATUS-CODE
    END-IF
  END-IF
END-IF
#BENEFIT-AMOUNT := MIL-INFO-V.MBR-SS
END-SUBROUTINE
*
***************************************
DEFINE SUBROUTINE CALCULATE-VA-BENEFITS
***************************************
*
#VA-DISAB-PCT := #BENEFIT-PCT
CALLNAT 'GVADCSXX'
  #CN
  #VA-DISAB-PCT
  #VA-SPC-COMP-CODE
  #VA-DISAB-BASIC-AMT
  #VA-SPC-COMP-AMT
  #VA-ADDL-SPS-AMT
  #VA-1-DEP-UND-18-AMT
  #VA-EA-ADDL-UND-18-AMT
  #VA-AGE-18-24-AMT
  #VA-CHILDREN-AMT
  #VA-TOT-DISAB-AMT
  #EFF-DATE
  #UNDER-18
  #UNDER-18-ADDL
  #AGE-18-24
  #1ST-CHLD-UND-18
  #ADDL-CHLD-UND-18
  #EACH-SCH-CHILD
  #SPS-Y-N
  #COMBAT-CALC-IND
*
#BENEFIT-AMOUNT := #VA-TOT-DISAB-AMT
END-SUBROUTINE
*
*********************************************
DEFINE SUBROUTINE CALCULATE-MILITARY-BENEFITS
*********************************************
*
FORMAT LS = 90
FIND(1) MIL-INFO-V WITH ID-NUMBER = #CN
  #CGA-ISN := *ISN
END-FIND
*
#CGA-SCR-COMMAND := #CGA-ORIG-COMMAND := 'RP'
#CGA-ID := #CGA-ORIG-ID := #CN
#CGA-SCR-SUFFIX := #CGA-ORIG-SUFFIX := 'DI*'
#CGA-CLERK := 'WEB'
#CGA-SECURITY-CODE := 0

FETCH RETURN 'D3000PXX' /* D3660PRP moves ret pay to #CGA-MF-SCR-PARM-1-N
IF *DATA NE 0
  INPUT #BENEFIT-AMOUNT
  RELEASE STACK
END-IF
*
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE CALCULATE-SS-PIA
**********************************
*
FIND(1) MIL-INFO-V WITH ID-NUMBER = #CN
END-FIND
*
IF (MIL-INFO-V.SOC-SEC-ST-DATE <> 0)
    AND (MIL-INFO-V.MBR-SS <> 0)
    AND (MIL-INFO-V.SOC-SEC-PIA = 0)                /* ONLY CALCULATE IF PIA NOT ENTERED
    AND (#DATE-OF-BIRTH GE 19180101)      /* CANNOT BE REAL OLD
  IF MIL-INFO-V.STATUS-CODE = 'D'
    MOVE MIL-INFO-V.MBR-SS TO MIL-INFO-V.SOC-SEC-PIA
  ELSE
*
* * * * * * * CALCULATE MEMBER EARLY RETIREMENT SOC-SEC PIA * * *
* * * * * * * * THIS SAME LOGIC IS IN D3080PBA AND D3010BR *
*
    MOVE 00 TO #MONTHS-PRIOR-TO-65
    MOVE 1.00 TO #MBR-SS-RET-PCT
    MOVE #DATE-OF-BIRTH TO #CGA-DOB
    #CGA-S-S-START-DATE := #BENEFIT-DATE
    PERFORM CALC-MBR-SS-BENE
*
    COMPUTE ROUNDED MIL-INFO-V.SOC-SEC-PIA = MIL-INFO-V.MBR-SS  / #MBR-SS-RET-PCT
*
    IF MIL-INFO-V.STATUS-CODE = ' '
      MOVE 'Z' TO MIL-INFO-V.STATUS-CODE                /* #SS STATUS-CODE
    END-IF
  END-IF
END-IF
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE CALC-MBR-SS-BENE
**********************************
*
IF #CGA-DOB = 0
  ESCAPE ROUTINE
END-IF
*
MOVE #CGA-DOB TO #WYYYYMMDD-LOW-N
MOVE #CGA-DOB TO #MBR-DOB
*
IF (#CGA-DOB-DD EQ 1)
  SUBTRACT 1 FROM #WMM-LOW
*
  IF #WMM-LOW EQ 0
    MOVE 12 TO #WMM-LOW
    SUBTRACT 1 FROM #WYYYY-LOW
  END-IF
END-IF
*
IF #CGA-S-S-START-DATE = 0
  COMPUTE #MBR-BENE-DT = #WYYYYMMDD-LOW-N + 620000 /* age 62 default
ELSE
  COMPUTE #MBR-BENE-DT = #CGA-S-S-START-DATE * 100
END-IF
*
MOVE #MBR-BENE-DT TO #WYYYYMMDD-HIGH-N
RESET #WDD-LOW                    /* TO MAKE MONTH INCREASE AT
RESET #WDD-HIGH                   /* BEGINNING OF MONTH
*
PERFORM COMPUTE-DATE-DIFFERENCE
IF #CGA-DOB LT 19380102   /* IF NOT USING INCREASED FULL RET AGE
  IF #MBR-BENE-DT EQ #WYYYYMMDD-LOW-N + 650000 /* IF AGE 65
    MOVE 0  TO #MBR-SS-MONS-PRIOR-TO-65
    MOVE 65 TO #SS-MBR-RET-YEARS
    MOVE 00 TO #SS-MBR-RET-MONS
    MOVE 1.000 TO #MBR-SS-RET-PCT
  END-IF
*
  IF #MBR-BENE-DT GT #WYYYYMMDD-LOW-N + 650000  /* IF OVER AGE 65
    MOVE 0  TO #MBR-SS-MONS-PRIOR-TO-65
*
    IF #MBR-BENE-MM < #WMM-LOW
      ADD 12 TO #MBR-BENE-MM
    END-IF
*
    COMPUTE #MBR-MM = (#MBR-BENE-MM - #WMM-LOW)
    COMPUTE #MBR-YYYY = (#MBR-BENE-YYYY - #WYYYY-LOW)
*
    MOVE #MBR-YYYY TO #SS-MBR-RET-YEARS
    MOVE #MBR-MM   TO #SS-MBR-RET-MONS
    MOVE 1.000 TO #MBR-SS-RET-PCT
  END-IF
*
  IF #MBR-BENE-DT LT #WYYYYMMDD-LOW-N + 650000  /* IF UNDER AGE 65
    MOVE #WYYYY-HIGH TO #SS-MBR-RET-YEARS
    MOVE #WMM-HIGH   TO #SS-MBR-RET-MONS
*
    IF #WYYYY-HIGH LT 62
      MOVE 36 TO #MBR-SS-MONS-PRIOR-TO-65
      MOVE .8000 TO #MBR-SS-RET-PCT
    ELSE
      IF #WYYYY-HIGH GT 64
        MOVE 00 TO #MBR-SS-MONS-PRIOR-TO-65
        MOVE 1.00 TO #MBR-SS-RET-PCT
      ELSE
        COMPUTE #MBR-SS-MONS-PRIOR-TO-65 =
          (((65 - #WYYYY-HIGH) - 1) * 12) + (12 - #WMM-HIGH)
        COMPUTE ROUNDED #MBR-SS-RET-PCT = 1.00
          - (#MEMBR-BENE-MNTHLY-INCR * #MBR-SS-MONS-PRIOR-TO-65)
      END-IF
    END-IF
  END-IF
ELSE      /* new as of 5/1993 retirement age calcs
  MOVE #CGA-DOB TO #YYYYMMDD
  IF #YYYYMMDD GE 19600102
    MOVE 24 TO #MBR-ADDL-FRA-MONS
  ELSE
    IF #YYYYMMDD GE 19550102
      IF (#MM = 01) AND (#DD-HIGH = 01) /*born on jan 01
        SUBTRACT 1 FROM #YYYY
      END-IF
      COMPUTE #MBR-ADDL-FRA-MONS = ((#YYYY - 1954) * 2) + 12 /*fixd 5/05
    ELSE
      IF #YYYYMMDD GE 19430102
        MOVE 12 TO #MBR-ADDL-FRA-MONS
      ELSE
        IF (#MM = 01) AND (#DD-HIGH = 01) /*born on jan 01
          SUBTRACT 1 FROM #YYYY
        END-IF
        COMPUTE #MBR-ADDL-FRA-MONS = ((#YYYY - 1937) * 2)
      END-IF
    END-IF
  END-IF
*
  COMPUTE #MBR-FRA-MONS-FROM-BIRTH = (65 * 12) + #MBR-ADDL-FRA-MONS
*   below added  per KRM 3/18/2002
  IF #ADJ-MONS
    SUBTRACT 1 FROM #MBR-FRA-MONS-FROM-BIRTH
  END-IF
*
  COMPUTE #MBR-TAKE-BENE-MONS-FROM-BIRTH =(#WYYYY-HIGH * 12) + #WMM-HIGH
  COMPUTE #MBR-NUM-MONS-PRIOR-TO-FRA =
    #MBR-FRA-MONS-FROM-BIRTH - #MBR-TAKE-BENE-MONS-FROM-BIRTH
  IF #MBR-TAKE-BENE-MONS-FROM-BIRTH GT #MBR-FRA-MONS-FROM-BIRTH
    MOVE 1.00 TO #MBR-SS-RET-PCT
*
    IF #MBR-ADDL-FRA-MONS = 24
      MOVE 67 TO #SS-MBR-RET-YEARS
      MOVE 00 TO #SS-MBR-RET-MONS
    ELSE
      MOVE 65 TO #SS-MBR-RET-YEARS
      MOVE #MBR-ADDL-FRA-MONS TO #SS-MBR-RET-MONS
*
      IF #SS-MBR-RET-MONS GT 12
        SUBTRACT 12 FROM #SS-MBR-RET-MONS
        ADD 1       TO   #SS-MBR-RET-MONS
      END-IF
    END-IF
  ELSE
    MOVE #WYYYY-HIGH TO #SS-MBR-RET-YEARS
    MOVE #WMM-HIGH   TO #SS-MBR-RET-MONS
    IF #MBR-NUM-MONS-PRIOR-TO-FRA LE 36
      COMPUTE ROUNDED #MBR-SS-RET-PCT = 1.00
        - (#MEMBR-BENE-MNTHLY-INCR * #MBR-NUM-MONS-PRIOR-TO-FRA)
    ELSE
      COMPUTE ROUNDED #MBR-SS-RET-PCT = 1.00 - (.20 +
        (#MEMBR-BENE-NU-MNTHLY-INCR * (#MBR-NUM-MONS-PRIOR-TO-FRA - 36)))
    END-IF
  END-IF
END-IF
END-SUBROUTINE
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE COMPUTE-DATE-DIFFERENCE
* * * * * * * * * * * * * * * * * * * * *
*
IF #WDD-LOW GT #WDD-HIGH
  SUBTRACT 1 FROM #WMM-HIGH
  ADD      1 TO   #WDD-HIGH
  IF #WMM-HIGH = 02
    ADD 28 TO #WDD-HIGH
  ELSE
    IF #WMM-HIGH = 04 OR = 06 OR = 09 OR = 11
      ADD 30 TO #WDD-HIGH
    ELSE
      ADD 31 TO #WDD-HIGH
    END-IF
  END-IF
END-IF
*
IF #WMM-LOW GT #WMM-HIGH
  SUBTRACT 1 FROM #WYYYY-HIGH
  ADD 12 TO #WMM-HIGH
END-IF
*
SUBTRACT #WYYYYMMDD-LOW-N FROM #WYYYYMMDD-HIGH-N
*
END-SUBROUTINE
END
