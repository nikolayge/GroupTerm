* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online/AUC
* Contact Information - Syncronize Spouse/Former Spouse fields with Status File
* MBCN0064
*************************************************************************************
*        MODIFICATION LOG
*************************************************************************************
* USER   DATE      TAG  REASON
* YAK    05112012       Initial Creation - Restrucutured (moved from MBCN0061)
*************************************************************************************
*
DEFINE DATA
PARAMETER USING MBCA0064
PARAMETER USING C2230ACB
PARAMETER USING C2231ACB
LOCAL USING OBJLFINF
LOCAL
*
1 #DATN                 (N8)
1 #AUC                  (A) DYNAMIC INIT <'AUC'>
1 #SPOUSE               (A) DYNAMIC CONST <'S'>
1 #FORMER-SPOUSE        (A) DYNAMIC CONST <'X'>
1 #FORMER-SPOUSE-STATUS (A) DYNAMIC CONST <'F'>
1 #NO-CODE              (A) DYNAMIC CONST <'N'>
END-DEFINE
*
ST_U.
FIND(1) ST-V WITH ID-NUMBER = Status_Sync.CN
  #LAST-FM-DATE     := ST-V.LAST-FM-DATE
  ST-V.LAST-FM-DATE := #DATN
  DECIDE FOR FIRST CONDITION
* Adding spouse
    WHEN RelationshipCode = #SPOUSE
        AND ExistingRelationshipCode = ' '
      ST-V.DATE-OF-MARRIAGE   := SpouseDateOfMarriage
      RESET  ST-V.DATE-OF-DIVORCE
      IF SystemSource = #AUC
        ST-V.BIRTH-CERT-CODE-SP := SpouseBirthCertCode
        ST-V.MARRIAGE-CERT-CODE := SpouseMarriageCertCode
        ST-V.STATUS-CODE-SP     := DependentStatusCode
      ELSE
        ST-V.BIRTH-CERT-CODE-SP := #NO-CODE
        ST-V.MARRIAGE-CERT-CODE := #NO-CODE
        ST-V.STATUS-CODE-SP     := #SPOUSE
      END-IF
      PERFORM SYNC-SPOUSE-RECORD
* Adding former spouse
    WHEN RelationshipCode = #FORMER-SPOUSE
        AND ExistingRelationshipCode = ' '
      ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE  := FormerSpouseDateOfDivorce
      IF SystemSource = #AUC
        ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE := FormerSpouseDateOfMarriage
        ST-V.FS-BIRTH-CERT-CD               := FormerSpouseBirthCertCode
        ST-V.FS-MARR-CERT-CD                := FormerSpouseMarriageCertCode
        ST-V.FORMER-SPOUSE-STATUS           := DependentStatusCode
      ELSE
        ST-V.FS-BIRTH-CERT-CD               := #NO-CODE
        ST-V.FS-MARR-CERT-CD                := #NO-CODE
        ST-V.FORMER-SPOUSE-STATUS           := #FORMER-SPOUSE-STATUS
      END-IF
      PERFORM SYNC-FORMER-SPOUSE-RECORD
* Changing relationship from spouse to former spouse
    WHEN RelationshipCode = #FORMER-SPOUSE
        AND ExistingRelationshipCode = #SPOUSE
      #OLD-DOM         := ST-V.DATE-OF-MARRIAGE
      #OLD-DIV         := ST-V.DATE-OF-DIVORCE
      #OLD-MCC         := ST-V.MARRIAGE-CERT-CODE
      #OLD-BCC         := ST-V.BIRTH-CERT-CODE-SP
      #OLD-STATUS-CODE := ST-V.STATUS-CODE-SP
      ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE  := FormerSpouseDateOfDivorce
      IF SystemSource = #AUC
        ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE := FormerSpouseDateOfMarriage
        ST-V.FS-BIRTH-CERT-CD               := FormerSpouseBirthCertCode
        ST-V.FS-MARR-CERT-CD                := FormerSpouseMarriageCertCode
        ST-V.FORMER-SPOUSE-STATUS           := DependentStatusCode
      ELSE
        ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE := ST-V.DATE-OF-MARRIAGE
        ST-V.FS-BIRTH-CERT-CD               := ST-V.BIRTH-CERT-CODE-SP
        ST-V.FS-MARR-CERT-CD                := ST-V.MARRIAGE-CERT-CODE
        ST-V.FORMER-SPOUSE-STATUS           := #FORMER-SPOUSE-STATUS
      END-IF
      PERFORM SYNC-FORMER-SPOUSE-RECORD
      PERFORM RESET-SPOUSE-RECORD
* Changing relationship from former spouse to spouse
    WHEN RelationshipCode = #SPOUSE
        AND ExistingRelationshipCode = #FORMER-SPOUSE
      #OLD-DOM         := ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE
      #OLD-DIV         := ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE
      #OLD-MCC         := ST-V.FS-MARR-CERT-CD
      #OLD-BCC         := ST-V.FS-BIRTH-CERT-CD
      #OLD-STATUS-CODE := ST-V.FORMER-SPOUSE-STATUS
      RESET ST-V.DATE-OF-DIVORCE
      ST-V.DATE-OF-MARRIAGE   := SpouseDateOfMarriage
      IF SystemSource = #AUC
        ST-V.BIRTH-CERT-CODE-SP := SpouseBirthCertCode
        ST-V.MARRIAGE-CERT-CODE := SpouseMarriageCertCode
        ST-V.STATUS-CODE-SP     := DependentStatusCode
      ELSE
        ST-V.BIRTH-CERT-CODE-SP := ST-V.FS-BIRTH-CERT-CD
        ST-V.MARRIAGE-CERT-CODE := ST-V.FS-MARR-CERT-CD
        ST-V.STATUS-CODE-SP     := #SPOUSE
      END-IF
      PERFORM SYNC-SPOUSE-RECORD
      PERFORM RESET-FORMER-SPOUSE-RECORD
* Updating spouse
    WHEN RelationshipCode = #SPOUSE
      #OLD-DOM         := ST-V.DATE-OF-MARRIAGE
      #OLD-MCC         := ST-V.MARRIAGE-CERT-CODE
      #OLD-BCC         := ST-V.BIRTH-CERT-CODE-SP
      #OLD-STATUS-CODE := ST-V.STATUS-CODE-SP
      ST-V.DATE-OF-MARRIAGE   := SpouseDateOfMarriage
      IF SystemSource = #AUC
        ST-V.BIRTH-CERT-CODE-SP := SpouseBirthCertCode
        ST-V.MARRIAGE-CERT-CODE := SpouseMarriageCertCode
        ST-V.STATUS-CODE-SP     := DependentStatusCode
      END-IF
      PERFORM SYNC-SPOUSE-RECORD
* Updating former spouse
    WHEN RelationshipCode = #FORMER-SPOUSE
      #OLD-DOM         := ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE
      #OLD-DIV         := ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE
      #OLD-MCC         := ST-V.FS-MARR-CERT-CD
      #OLD-BCC         := ST-V.FS-BIRTH-CERT-CD
      #OLD-STATUS-CODE := ST-V.FORMER-SPOUSE-STATUS
      ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE  := FormerSpouseDateOfDivorce
      IF SystemSource = #AUC
        ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE := FormerSpouseDateOfMarriage
        ST-V.FS-BIRTH-CERT-CD               := FormerSpouseBirthCertCode
        ST-V.FS-MARR-CERT-CD                := FormerSpouseMarriageCertCode
        ST-V.FORMER-SPOUSE-STATUS           := DependentStatusCode
      END-IF
      PERFORM SYNC-FORMER-SPOUSE-RECORD
* Changing relationship from non-spouse/non-former spouse to spouse
    WHEN RelationshipCode = #SPOUSE
      RESET #OLD-DOM #OLD-DIV #OLD-MCC #OLD-BCC #OLD-STATUS-CODE
      RESET  ST-V.DATE-OF-DIVORCE
      ST-V.DATE-OF-MARRIAGE   := SpouseDateOfMarriage
      IF SystemSource = #AUC
        ST-V.BIRTH-CERT-CODE-SP := SpouseBirthCertCode
        ST-V.MARRIAGE-CERT-CODE := SpouseMarriageCertCode
        ST-V.STATUS-CODE-SP     := DependentStatusCode
      ELSE
        ST-V.BIRTH-CERT-CODE-SP := #NO-CODE
        ST-V.MARRIAGE-CERT-CODE := #NO-CODE
        ST-V.STATUS-CODE-SP     := #SPOUSE
      END-IF
      PERFORM SYNC-SPOUSE-RECORD
* Changing relationship from non-spouse/non former spouse to former spouse
    WHEN RelationshipCode = #FORMER-SPOUSE
      RESET #OLD-DOM #OLD-DIV #OLD-MCC #OLD-BCC #OLD-STATUS-CODE
      ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE    := FormerSpouseDateOfDivorce
      IF SystemSource = #AUC
        ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE := FormerSpouseDateOfMarriage
        ST-V.FS-BIRTH-CERT-CD               := FormerSpouseBirthCertCode
        ST-V.FS-MARR-CERT-CD                := FormerSpouseMarriageCertCode
        ST-V.FORMER-SPOUSE-STATUS           := DependentStatusCode
      ELSE
        ST-V.FS-BIRTH-CERT-CD               := #NO-CODE
        ST-V.FS-MARR-CERT-CD                := #NO-CODE
        ST-V.FORMER-SPOUSE-STATUS           := #FORMER-SPOUSE-STATUS
      END-IF
      PERFORM SYNC-FORMER-SPOUSE-RECORD
* Changing relationship from spouse to non-spouse/non former spouse
    WHEN ExistingRelationshipCode = #SPOUSE AND
        (RelationshipCode <> #SPOUSE AND RelationshipCode <> #FORMER-SPOUSE)
      #OLD-BCC := ST-V.BIRTH-CERT-CODE-SP
      #OLD-DOM := ST-V.DATE-OF-MARRIAGE
      #OLD-DIV := ST-V.DATE-OF-DIVORCE
      #OLD-MCC := ST-V.MARRIAGE-CERT-CODE
      #OLD-STATUS-CODE := ST-V.STATUS-CODE-SP
      PERFORM RESET-SPOUSE-RECORD
* Changing relationship from former spouse to non-spouse/non former spouse
    WHEN ExistingRelationshipCode = #FORMER-SPOUSE AND
        (RelationshipCode <> #SPOUSE AND RelationshipCode <> #FORMER-SPOUSE)
      #OLD-BCC := ST-V.FS-BIRTH-CERT-CD
      #OLD-DOM := ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE
      #OLD-DIV := ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE
      #OLD-MCC := ST-V.FS-MARR-CERT-CD
      #OLD-STATUS-CODE := ST-V.FORMER-SPOUSE-STATUS
      PERFORM RESET-FORMER-SPOUSE-RECORD
    WHEN NONE
      IGNORE
  END-DECIDE
  UPDATE (ST_U.)
END-FIND
*
**==================================
DEFINE SUBROUTINE RESET-SPOUSE-RECORD
**==================================
*
RESET ST-V.SPOUSE-CONTACT-ID ST-V.NAME-SP
  ST-V.SSN-SP ST-V.STATUS-CODE-SP
  ST-V.BIRTH-CERT-CODE-SP ST-V.MARRIAGE-CERT-CODE
RESET ST-V.DATE-OF-MARRIAGE ST-V.DATE-OF-DIVORCE
*
END-SUBROUTINE
**==========================================
DEFINE SUBROUTINE RESET-FORMER-SPOUSE-RECORD
**==========================================
*
RESET ST-V.FORMER-SPOUSE-CONTACT-ID ST-V.FORMER-SPOUSE-NAME
  ST-V.FORMER-SPOUSE-SSN
  ST-V.FORMER-SPOUSE-STATUS ST-V.FS-BIRTH-CERT-CD
  ST-V.FS-MARR-CERT-CD
RESET ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE
*
END-SUBROUTINE
**==================================
DEFINE SUBROUTINE SYNC-SPOUSE-RECORD
**==================================
*
ST-V.SPOUSE-CONTACT-ID     := Status_Sync.ContactID
* ST-V.NAME-SP             := EDIT_NAME_FMLS(<Status_Sync.FullName>)    /* YK1
ST-V.NAME-SP               := EDIT_NAME_LFMS(<Status_Sync.FullName>)    /* YK1
ST-V.SSN-SP                  := Status_Sync.SSN                         /* Spouse SSN
* ST-V.DATE-OF-BIRTH-SP      := Status_Sync.DateOfBirth                 /* Spouse Date of Birth
* ST-V.SEX-SP                := Status_Sync.GenderCode
* After_Image For TR register
#DATE-OF-MARRIAGE          := ST-V.DATE-OF-MARRIAGE
#DATE-OF-DIVORCE           := ST-V.DATE-OF-DIVORCE
#MCC                       := ST-V.MARRIAGE-CERT-CODE
*
END-SUBROUTINE
*
**=========================================
DEFINE SUBROUTINE SYNC-FORMER-SPOUSE-RECORD
**=========================================
*
ST-V.FORMER-SPOUSE-CONTACT-ID  := Status_Sync.ContactID
* ST-V.FORMER-SPOUSE-NAME      := EDIT_NAME_FMLS(<Status_Sync.FullName>)   /* YK1
ST-V.FORMER-SPOUSE-NAME        := EDIT_NAME_LFMS(<Status_Sync.FullName>)   /* YK1
ST-V.FORMER-SPOUSE-SSN         := Status_Sync.SSN                          /* Former Spouse SSN
* ST-V.FORMER-SPOUSE-DT-OF-BIRTH := Status_Sync.DateOfBirth                /* Former Spouse Date of Birth
* After_Image for TR register
#DATE-OF-MARRIAGE              := ST-V.FORMER-SPOUSE-DATE-OF-MARRIAGE
#DATE-OF-DIVORCE               := ST-V.FORMER-SPOUSE-DATE-OF-DIVORCE
#MCC                           := ST-V.FS-MARR-CERT-CD
*
END-SUBROUTINE
*
END
