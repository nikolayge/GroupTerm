* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE FUNCTION F-GET-INV-TYPE RETURNS (A) DYNAMIC 
DEFINE DATA
PARAMETER
1 #INV-CATEGORY (A) DYNAMIC
1 #REQUEST-TYPE (A) DYNAMIC
LOCAL USING OBJLGENT
LOCAL
1 #SEC-KEY       (A7) 
1 REDEFINE #SEC-KEY
  2 #GEN-KEY     (A4) 
  2 #GEN-SUB-KEY (A3)
1 #DEL           (A1) CONST <','>  
1 #CODE          (A2) 
1 #INV-TYPE      (A) DYNAMIC CONST <'INV-TYPE'>
1 #INV-KEY       (A) DYNAMIC CONST <'INV-KEY'>
END-DEFINE
*
#GEN-KEY := 'CODE'
DECIDE ON FIRST VALUE #INV-CATEGORY
  VALUE '1'
    #GEN-SUB-KEY := 'D01' 
  VALUE '2'  
    #GEN-SUB-KEY := 'D02'  
  VALUE '3'  
    #GEN-SUB-KEY := 'D03'       
  VALUE '4'
    #GEN-SUB-KEY := 'D04' 
  VALUE '5'  
    #GEN-SUB-KEY := 'D05'  
  VALUE '6'  
    #GEN-SUB-KEY := 'D06'       
  VALUE '7'
    #GEN-SUB-KEY := 'D07'   
  VALUE '7A'
    #GEN-SUB-KEY := 'DA7'   
  VALUE '8'
    #GEN-SUB-KEY := 'D08'   
  NONE
    IGNORE 
END-DECIDE
* Return if only key required
IF #REQUEST-TYPE = #INV-KEY
  F-GET-INV-TYPE := #GEN-SUB-KEY
  ESCAPE ROUTINE
END-IF
*  
FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY 
  IF NO RECORDS FOUND
     RESET F-GET-INV-TYPE
  END-NOREC
  COMPRESS F-GET-INV-TYPE #DEL GEN-TABLE-V.GEN-ALPHA-VALUE
     INTO F-GET-INV-TYPE LEAVING NO
  IF #GEN-SUB-KEY = 'D07' OR = 'DA7'   
    #CODE := GEN-TABLE-V.GEN-ALPHA-VALUE
    EXAMINE #CODE FOR 'R' REPLACE 'W'    
    COMPRESS F-GET-INV-TYPE #DEL #CODE
     INTO F-GET-INV-TYPE LEAVING NO
  END-IF   
END-FIND 
*  
END-FUNCTION
END
