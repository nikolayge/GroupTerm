* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: T-21A  CN/CI Glance/Misc
* CN Contact links 
*
DEFINE DATA 
PARAMETER 
1 p_T-21  
  2 p_CN (N6) 
  2 p_CI (N8) 
  2 p_Out  (A/*) DYNAMIC 
LOCAL USING ADDA0001 
LOCAL 
* ----------------------------------------------- Contact 
1 CNT-V VIEW OF A-CONTACTS 
  2  CONTACT-ID                        (N8.0)   /*  N D  
  2  CONTACT-STATUS                    (A1)     /*  F   Such as B,D, H,I,J 
  2  CONTACT-TYPE                      (A1)     /*  N    
  2  ID-NUMBER                         (N6.0)   /*  D  
  2  RANK-ID                           (N6.0)   /*  N D  
  2  FULL-NAME                                      
   3  FIRST-NAME                       (A25)    /*  N    
   3  MIDDLE-NAME                      (A25)    /*  N    
   3  LAST-NAME                        (A25)    /*  N    
   3  SUFFIX                           (A10)    /*  N    
  2  GENDER-CD                         (A1)     /*  N    
  2  DATE-OF-BIRTH                     (N8.0)   /*  N D  
  2  SSN                               (N9.0)   /*  N D  
  2  DATE-OF-DEATH                     (N8.0)   /*  N D  
  2  BIRTH-CERT-CODE                   (A1)     /*  N    
  2  DEATH-CERT-CODE                   (A1)     /*  N    
  2  FIRM-ORGANIZATION-NAME            (A75)    /*  N D  
   2 LAST-USER-UPD        
   2 LAST-DATE-UPD    
* 
1 CNT-R VIEW OF A-CONTACTS 
  2  SSN                               (N9.0)   /*  N D  
 
1 ST-V VIEW OF A-STATUS 
    2 ID-NUMBER 
    2 REDEFINE ID-NUMBER
       3 CN-A1           (A1)
       3 CN_A5           (A5)
    2 MEMBER-CONTACT-ID 
    2 SPOUSE-CONTACT-ID 
    2 STATUS-CODE-SP 
    2 FORMER-SPOUSE-CONTACT-ID 
    2 FORMER-HUSBAND-CONTACT-ID 
    2 SSN 
    2 C*LINKED-CN 
    2 LINKED-CN (N6/40) 
    2 PROCESS-IND 
    2 INTRO-FLAG 
    2 MEMBER-FLAG 
    2 WIDOW-FLAG
    2 ANNUAL-RPT-FLAG 
    2 INS-MAIL-CODE 
    2 NEWSLETTER-CODE 
    2 DI-BA-MAIL-CD 
    2 NUMBER-MEMBERSHIPS
    2 SUSPEND-CODE 
    2 C*ACTIVE-SEGMENTS 
    2 ACTIVE-SEGMENTS (9)       
      3  STATUS             (A1) 
      3  PLAN               (A3) 
      3  PAY-STATUS         (A2) 
      3  MODE               (A1) 
      3  ISSUE-DATE         (N8) 
      3  REDEFINE ISSUE-DATE
         4 ISSUE-DATE_A     (A8)
      3  POLICY-ID          (A16)
      3  PAID-UP-DATE       (N8)    
      3  PAID-TO-DATE       (N8) 
      3  FACE-AMOUNT        (P7.2) 
      3  CASH-VALUE         (P7.2) 
      3  DEATH-BENEFIT      (P7.2)
* 
1 STL-V VIEW OF A-STATUS 
    2 ID-NUMBER 
    2 MEMBER-CONTACT-ID 
    2 SPOUSE-CONTACT-ID 
    2 STATUS-CODE-SP 
    2 FORMER-SPOUSE-CONTACT-ID 
    2 FORMER-HUSBAND-CONTACT-ID 
    2 C*LINKED-CN 
    2 LINKED-CN (N6/40) 
    2 PROCESS-IND 
    2 INTRO-FLAG 
    2 MEMBER-FLAG 
    2 WIDOW-FLAG 
*         
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS 
   2 CONTACT-1-ID     
   2 ROLE-1-CD        
   2 CONTACT-2-ID     
   2 ROLE-2-CD 
   2 LAST-USER-UPD        
   2 LAST-DATE-UPD        
* 
 1 PH-V  VIEW OF A-CONTACT-PHONE 
    2 PHONE-ID 
    2 CONTACT-ID 
    2 PHONE-TYPE-CODE        (A1)    /*   Home/Work/Fax/etc 
    2 TYPE                   (A1)    /*   Domestic / International 
    2 INTL-ACCESS-CODE       (N3)    /*   International Access Cd 
    2 INTL-DIAL-NUMBER       (N20)   /*   International Dial Numbe 
    2 AREA-CODE              (N3)    /*   D Area Code 
    2 DIAL-NUMBER            (N7)    /*   Dial Number 
    2 REDEFINE DIAL-NUMBER 
      3 DIAL-NUMBER-3   (A3)  /* bz1  
      3 DIAL-NUMBER-4   (A4)  /* bz1 
    2 EXT                    (N6)    /*   Extension 
    2 PREFERRED-PHONE-IND    (A1)    /*   Preferred Phone
    2 INVALID-IND 
    2 LAST-DATE-UPD                  
    2 LAST-USER-UPD   
* 
  1 EML-V VIEW of A-CONTACT-EMAIL 
    2 EMAIL-ID    
    2 CONTACT-ID  
    2 EMAIL-PREFERRED-IND 
    2 EMAIL-UNDELIV 
    2 EMAIL-ADDRESS    
    2 LAST-DATE-UPD                  
    2 LAST-USER-UPD   
* 
 1 AR-V VIEW OF A-ADDRESS-RELATIONSHIP 
   2 CONTACT-ID        (N8) 
   2 ADDRESS-POOL-ID   (N8) 
* 
 1 POOL-V VIEW OF A-ADDRESS-POOL 
   2 ADDRESS-POOL-ID   (N8) 
* 
 1 FIR-V VIEW OF A-FIRS 
  2 ID-NUMBER
  2 CONTACT-ID-NUMBER
  2 C*DEPENDENT-TABLE  
  2 DEPENDENT-TABLE (30) 
    3 DEPN-CONTACT-ID 
    3 DEPN-NAME      
    3 DEPN-BIRTH-CD  
    3 DEPN-STATUS-CD 
    3 DEPN-REMARKS   
    3 DEPN-DATE-OF-BIRTH  
    3 DEPN-SSN 
  2 C*POLICY-TABLE
  2 C*POL-TABLE-2
  2 C*POL-TABLE-3
  2 C*DOCUMENT             
* 
 1 SMP-V VIEW OF A-SVC-MEMBER 
  2 SVC-ID-NUMBER      (N6) 
  2 SVC-PLAN 
  2 SVC-STATUS 
  2 SVC-PAID-TO-DATE     
  2 SVC-DATE-ENTERED     
  2 SVC-TERMINATION-DATE     
* 
1 AT-V VIEW OF A-APPL-TRACKING 
 2 SSN 
 2 ID-NUMBER 
 2 MEMBER-CN 
 2 CONTACT-ID 
 2 PLAN 
 2 ACTION-FLAG 
 2 INTRO-FLAG 
 2 NAME 
 2 AT-STATUS
 2 ACTION-DATE
 2 AT-GUID
*  
1 POL-V VIEW OF A-POLICY-CONTACTS
   2 POLICY-NUMBER           (A16)  /* D
   2 CONTACT-ID
   2 RELATION                (N2) 
   2 REFERENCE-NUMBER        (N6)

1 v_Bene_Pol (A)   DYNAMIC
1 bene_CI    (N8)
1 rel(A11/11) INIT <'Insured',         /*  1
                    'Owner Prime',     /*  2
                    'Member',          /*  3  
                    'Owner Scs',       /*  4
                    'Bene Prime',      /*  5 
                    'Bene Unf',        /*  6 
                    'Bene Ctg',       /*  7 
                    'Payer',           /*  8
                    'Linked',          /*  9
                    'Owner Co',        /*  10
                    'Bene Ctg U'>      /* 11
                    
*
1 v_POOL-ID  (N8) 
1  dm               (A1)    INIT <H'09'> 
1 #i         (I2) 
1 #j         (I2) 
1 #k         (I2) 
1 #x         (I2) 
1 #indx      (N1)
1 #indx#j     (N1)
1 #num       (N1)
1 v_Pol      (A16) 
1 v_A        (A)  DYNAMIC 
1 v_AA       (A)  DYNAMIC 
1 v_AAA      (A)  DYNAMIC 
1 v_A8_1       (A8) 
1 v_A8_2       (A8) 
1 v_A10        (A10) 
1 v_A12        (A12)
1 A3         (A3)  
END-DEFINE 
* ON ERROR 
*   COMPRESS "CN:" ST-V.ID-NUMBER  "Pgm:" *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR INTO v_A 
*   PERFORM sbr_ADD 
*   ESCAPE ROUTINE 
* END-ERROR 
* 
  v_A := '<pre>'  
  PERFORM sbr_ADD 
  
 IF p_CN <> 0 
    FIND (1) ST-V WITH ID-NUMBER = p_CN 
         IF NO RECORD 
           COMPRESS p_CN 'No Status' INTO v_A 
           PERFORM sbr_ADD 
           ESCAPE BOTTOM 
         END-NOREC 
       COMPRESS  
           '<big><b>'     'STATUS CN :' ST-V.ID-NUMBER  
                'CI:' MEMBER-CONTACT-ID '</big></b>' 
                'Sp CI:  ' SPOUSE-CONTACT-ID  
                'FSp CI: ' FORMER-SPOUSE-CONTACT-ID 
                '<b>Flags:[</b>'   
                         'Mem='        MEMBER-FLAG  
                         ',Wid='       WIDOW-FLAG  
                         ',PrInd='     PROCESS-IND  
                         ',IntrFl='    INTRO-FLAG 
                         ',AnnRep='    ANNUAL-RPT-FLAG
                         ',InsMl='     INS-MAIL-CODE 
                         ',NewsLtr='   NEWSLETTER-CODE 
                         ',DI_BA='     DI-BA-MAIL-CD
                         ',SuspCd='   SUSPEND-CODE 
                '<b>]</b>'          
       INTO v_A 
       PERFORM sbr_ADD 
 
        IF p_CI = 0  
           p_CI   := MEMBER-CONTACT-ID  
           COMPRESS '    ( CI' p_CI 'taken from Status )' INTO v_A 
           PERFORM sbr_ADD 
        END-IF     
        FOR #i =1 TO C*LINKED-CN 
        IF #i = 1  
             MOVE EDITED C*LINKED-CN (EM=Z9) TO v_A 
             COMPRESS FULL '  -- Has' v_A 'Links --' INTO v_A 
             PERFORM sbr_ADD 
        END-IF
            v_A :=' '
            MOVE EDITED #i(EM=Z9')') TO A3
            FIND (1) STL-V WITH ID-NUMBER = ST-V.LINKED-CN(#i) 
               IF NO RECORD FOUND  
                  IF ST-V.LINKED-CN(#i) >= 500000    
                     FIND(1) AT-V WITH ID-NUMBER = ST-V.LINKED-CN(#i)
                          COMPRESS A3 ST-V.LINKED-CN(#i) 'AT' 'st:' AT-STATUS 'cn:' MEMBER-CN 'ci:' AT-V.CONTACT-ID 
                                    'I-fl:' AT-V.INTRO-FLAG 'plan:' AT-V.PLAN 'act. date:' ACTION-DATE    
                          INTO v_A
                     END-FIND
                  ELSE
                     COMPRESS A3 '  Link CN :' ST-V.LINKED-CN(#i) 'Not Found.' INTO v_A 
                  END-IF
                  PERFORM sbr_ADD 
                  ESCAPE BOTTOM 
               END-NOREC               
               COMPRESS A3 '<b>' ST-V.LINKED-CN(#i) '('STL-V.INTRO-FLAG ')' '</b>' INTO v_A LEAVING NO SPACE
              v_AA := ' '
              FOR #j = 1 TO STL-V.C*LINKED-CN 
                IF #j <= 15  
                  IF STL-V.LINKED-CN(#j) <> 0 
                    COMPRESS v_AA LINKED-CN(#j) INTO v_AA  
                  END-IF 
                ELSE
                 COMPRESS v_AA '--' INTO v_AA LEAVING NO SPACE  
                END-IF 
              END-FOR 
              COMPRESS '[' STL-V.C*LINKED-CN ']' INTO v_AAA LEAVING NO SPACE
              COMPRESS v_A v_AAA    
                      ' => {' '<i><font color="blue">' v_AA '</i></font>' '}' 
                      'Mem CI:' STL-V.MEMBER-CONTACT-ID  
                      'Sp CI:' STL-V.SPOUSE-CONTACT-ID 
                      'FSp CI:' STL-V.FORMER-SPOUSE-CONTACT-ID 
                      'Flags(mwpi):'  STL-V.MEMBER-FLAG '-'  
                                STL-V.WIDOW-FLAG  '-' 
                                STL-V.PROCESS-IND '-' 
                                STL-V.INTRO-FLAG  '-' 
              INTO v_A 
              PERFORM sbr_ADD 
            END-FIND  
        END-FOR 
    END-FIND 
 END-IF 
 
  IF p_CI <> 0 
    v_A := " " 
    PERFORM sbr_ADD 
    FIND (1) CNT-V WITH CONTACT-ID = p_CI 
         IF NO RECORD 
            COMPRESS p_CI 'No Contact' INTO v_A 
            PERFORM sbr_ADD 
            ESCAPE BOTTOM 
         END-NOREC
         bene_CI := p_CI 
         PERFORM sbr_IF-BENE 
         IF v_Bene_Pol <> " "  
            COMPRESS  'Beneficiary of' v_Bene_Pol INTO v_Bene_Pol 
         ELSE 
            COMPRESS  'Not a Beneficiary'  INTO v_Bene_Pol 
         END-IF 
         COMPRESS 'Type=' CONTACT-TYPE INTO v_AA LEAVING NO SPACE
         COMPRESS FULL '<big><b>' 'CONTACT CN:' CNT-V.ID-NUMBER 'CI:' CNT-V.CONTACT-ID '</big></b>' 
            v_AA 'SSN:' CNT-V.SSN ':' CNT-V.LAST-USER-UPD ':' CNT-V.LAST-DATE-UPD v_Bene_Pol 
         INTO v_A  
         PERFORM sbr_ADD 
         IF p_CN = 0 p_CN := CNT-V.ID-NUMBER  
            COMPRESS '   ( CN' p_CN 'taken from contact )' INTO v_A 
            PERFORM sbr_ADD 
         END-IF 
    END-FIND 
 
    FIND  CR-V WITH CONTACT-1-ID = p_CI 
         IF NO RECORD 
           COMPRESS p_CI 'No Rel' INTO v_A 
           PERFORM sbr_ADD 
           ESCAPE BOTTOM 
         END-NOREC 
         IF *COUNTER = 1 v_AA := 'Relations ' 
         ELSE v_AA := '          ' END-IF  
         MOVE EDITED CONTACT-1-ID (EM=ZZZZZZZ9) TO v_A8_1 
         MOVE EDITED CONTACT-2-ID (EM=ZZZZZZZ9) TO v_A8_2 
         MOVE EDITED *COUNTER (EM=Z9')')        TO v_A 
         bene_CI := CONTACT-2-ID
         PERFORM sbr_IF-BENE 
         IF v_Bene_Pol <> " "  
            COMPRESS  'Beneficiary of' v_Bene_Pol INTO v_Bene_Pol 
         END-IF 
         FIND CNT-R WITH CONTACT-ID = CONTACT-2-ID END-FIND 
         COMPRESS FULL 
                v_AA v_A V_A8_1 ROLE-1-CD  
                v_A8_2 ROLE-2-CD '            :' CNT-R.SSN ':' 
                CR-V.LAST-USER-UPD ':' CR-V.LAST-DATE-UPD v_Bene_Pol 
         INTO v_A  
         PERFORM sbr_ADD 
    END-FIND 
    FIND  CR-V WITH CONTACT-2-ID = p_CI 
         IF NO RECORD 
            COMPRESS 'No Reverse Relations for' p_CI INTO v_A 
            PERFORM sbr_ADD 
            ESCAPE BOTTOM 
         END-NOREC 
         COMPRESS 
            'Reverse Rel' 
            CONTACT-1-ID 
            ROLE-1-CD 
            CONTACT-2-ID 
            ROLE-2-CD 
            CR-V.LAST-USER-UPD 
            CR-V.LAST-DATE-UPD  
         INTO v_A  
         PERFORM sbr_ADD 
    END-FIND 
 END-IF 
 
IF p_CI <> 0   
   v_A := '<br/> <b>--- Phones  ---</b>' 
   PERFORM sbr_ADD 
 
 FIND PH-V  WITH PH-V.CONTACT-ID = p_CI
   v_AA := ' ' 
   MOVE EDITED *COUNTER (EM=Z9')') TO v_A 
      DECIDE ON FIRST VALUE OF PH-V.TYPE
      VALUE 'D'      /* domestic phones 
      COMPRESS NUMERIC '(' PH-V.AREA-CODE ')' PH-V.DIAL-NUMBER-3 '-' PH-V.DIAL-NUMBER-4 ' ext:' EXT INTO v_AA
        LEAVING NO SPACE 
      IF EXT <> 0  
         COMPRESS v_AA 'Ext' PH-V.EXT INTO  v_AA 
      END-IF 
     VALUE  'I'      /* international
        COMPRESS NUMERIC '(' PH-V.INTL-ACCESS-CODE ')' PH-V.INTL-DIAL-NUMBER ' ext:' EXT INTO v_AA LEAVING NO SPACE 
     NONE 
        COMPRESS ' ! Wrong Type: [' PH-V.TYPE '] !' INTO v_AA 
     END-DECIDE  

   COMPRESS 'Dom/Int:' TYPE ' Type:' PHONE-TYPE-CODE  ' Pref: [' PREFERRED-PHONE-IND ']'
            ' Invalid: [' INVALID-IND ']'  v_AA  INTO v_AA LEAVING NO SPACE 
   MOVE EDITED PH-V.LAST-DATE-UPD(EM=9999-99-99) TO v_A10
   COMPRESS  v_A PH-V.CONTACT-ID v_AA v_A10 PH-V.LAST-USER-UPD INTO v_A 
   PERFORM sbr_ADD 
 END-FIND 
END-IF 
 
IF p_CI <> 0   
   v_A := '<br/> <b>--- Emails  ---</b>' 
   PERFORM sbr_ADD 
 FIND EML-V  WITH EML-V.CONTACT-ID = p_CI 
   MOVE EDITED *COUNTER (EM=Z9')') TO v_A 
   MOVE EDITED EML-V.LAST-DATE-UPD(EM=9999-99-99) TO v_A10
   COMPRESS 'Email' v_A EML-V.CONTACT-ID  
       EMAIL-ADDRESS  
      'Pref:' EMAIL-PREFERRED-IND  
      'Undeliv:' EMAIL-UNDELIV 
                 v_A10
                 EML-V.LAST-USER-UPD
         INTO v_A 
   PERFORM sbr_ADD 
 END-FIND 
END-IF 
  
IF p_CI <> 0    
   v_A := '<br/> <b>--- Addreses  ---</b>' 
   v_AA := ' '
   PERFORM sbr_ADD 
 
 FIND AR-V  WITH AR-V.CONTACT-ID = p_CI 
    v_POOL-ID := AR-V.ADDRESS-POOL-ID 
    AR_F. 
     FIND AR-V  WITH AR-V.ADDRESS-POOL-ID = v_POOL-ID 
     IF *NUMBER(AR_F.) > 1  
         v_AA := 'Shared.'  
     END-IF 
   MOVE EDITED *COUNTER (EM=Z9';') TO v_A 
   COMPRESS v_AA 'Addr Relation:' v_A 'CI:' AR-V.CONTACT-ID '==> Pool ID:' AR-V.ADDRESS-POOL-ID INTO v_A 
   PERFORM sbr_ADD 
   END-FIND 
  END-FIND 
 
  RESET #PDA-ADDRESS 
  #ADD-TYPE := 'P' 
  #ADD-OLD-FORMAT := 'N' 
  #ADD-CONTACT-ID    := p_CI  
  CALLNAT 'ADDN0001' #PDA-ADDRESS  
  IF #ADD-LINE-1 <> " " 
     COMPRESS '*  <b>Primary</b> - Pool' '<br/>' #ADD-LINE-1 '<br/>' #ADD-LINE-2 '<br/>' #ADD-LINE-3  
     '[ Source code:' #ADD-ADDRESS-SOURCE 'Return mail:' #ADD-RETURNED-MAIL  
     'Valid:' #ADD-VALID-IND 'Protect:' #ADD-PREVENT-OVER-IND '] Last D:' #ADD-LAST-DATE-UPD  
     'Last U:' #ADD-LAST-USER-UPD INTO v_A 
     PERFORM sbr_ADD 
   END-IF 
 
  RESET #PDA-ADDRESS 
  #ADD-TYPE := 'S' 
  #ADD-OLD-FORMAT := 'N' 
  #ADD-CONTACT-ID    := p_CI  
  CALLNAT 'ADDN0001' #PDA-ADDRESS  
  IF #ADD-LINE-1 <> " " 
  COMPRESS  '*  <b>Seconday</b> - Pool' '<br/>' #ADD-LINE-1 '<br/>' #ADD-LINE-2 '<br/>' #ADD-LINE-3  
  '[ Source code:' #ADD-ADDRESS-SOURCE 'Return mail:' #ADD-RETURNED-MAIL 
  'Valid:' #ADD-VALID-IND '] Last D:' #ADD-LAST-DATE-UPD  'Last U:'  #ADD-LAST-USER-UPD INTO v_A  
   PERFORM sbr_ADD 
  END-IF 
END-IF 
IF p_CN <> 0 
   COMPRESS FULL '<br/><b> --- FIR --- </b> ' 
   INTO v_A
   PERFORM sbr_ADD
   
  FIND FIR-V  WITH FIR-V.ID-NUMBER = p_CN
   COMPRESS 'CN:' FIR-V.ID-NUMBER 'CI:' CONTACT-ID-NUMBER 
            'Policies:' C*POLICY-TABLE 
            'Pol-2:' C*POL-TABLE-2 
            'Pol-3' C*POL-TABLE-3  
            'Doc:' C*DOCUMENT
   INTO v_A
   PERFORM sbr_ADD
   COMPRESS FULL  '     '
                       'Dep Ci'
                       ' St'
                       'Name           '
                       'Dob       ' 
                       'SSN        '  
                       'bc  ' 
                       'Remark'  
                        INTO v_A  
   PERFORM sbr_ADD 
   FOR #i = 1 TO C*DEPENDENT-TABLE 
    MOVE EDITED #i (EM=Z9')') TO v_A 
    MOVE EDITED DEPN-CONTACT-ID(#i) (EM=ZZZZZZZ9) TO v_A8_1 
    MOVE EDITED DEPN-DATE-OF-BIRTH(#i) (EM=9999-99-99) TO v_AAA 
    COMPRESS FULL  v_A  V_A8_1   INTO v_A
    COMPRESS FULL  '[' DEPN-STATUS-CD(#i) ']' INTO v_AA LEAVING NO SPACE 
    COMPRESS FULL v_A v_AA  DEPN-NAME(#i) v_AAA DEPN-SSN(#i) 
           '['  DEPN-BIRTH-CD(#i) ']' DEPN-REMARKS(#i)  INTo v_A            
 
    PERFORM sbr_ADD 
   END-FOR 
 END-FIND 
END-IF  
 
IF p_CN <> 0 
   v_A := '<b>--- Services ---</b> ' 
   PERFORM sbr_ADD 
 
  FIND SMP-V  WITH SMP-V.SVC-ID-NUMBER = p_CN 
     COMPRESS '-Services CN:' SMP-V.SVC-ID-NUMBER  
            'Plan:' SVC-PLAN 'Status:' SVC-STATUS  
            'Paid to:' SVC-PAID-TO-DATE  
            'Entered:' SVC-DATE-ENTERED     
            'Terminated:' SVC-TERMINATION-DATE     
INTO v_A  
   PERFORM sbr_ADD 
 END-FIND 
END-IF  
 
  COMPRESS '<br/><b> --- AT Record found by Status SSN:' ST-V.SSN '(Contact SSN is' CNT-V.SSN ')</b>' INTO v_A 
  PERFORM sbr_ADD  
   
 FIND AT-V WITH SSN = ST-V.SSN 
   COMPRESS  NAME 'Intro Flag:' INTRO-FLAG 'Plan:' PLAN  INTO v_A 
   PERFORM sbr_ADD          
   COMPRESS 'AT ID:' ID-NUMBER 'CN:' MEMBER-CN  'CI' AT-V.CONTACT-ID 'SSN:' SSN  'AT Status:' AT-STATUS 
           'Action Flag:' ACTION-FLAG  '<br/>' 'GUID:' AT-GUID
   INTO v_A 
   PERFORM sbr_ADD          
 END-FIND 
 v_A := " " PERFORM   sbr_ADD 
 
   FSSN. 
   FIND NUMBER CNT-V WITH SSN = ST-V.SSN 
   MOVE EDITED *NUMBER(FSSN.)(EM=9) TO v_A10 
   IF *NUMBER(FSSN.) > 1  
      COMPRESS '<b><font color="red">' '--- SSN Links ---' v_A10 '</font> </b>' INTO v_A 
   ELSE 
      COMPRESS '<b>' '--- SSN Links ---' '</b>' v_A10 INTO v_A 
   END-IF    
   PERFORM sbr_ADD 
   FIND CNT-V WITH SSN = ST-V.SSN 
     COMPRESS FULL '--Contact' 'SSN:' CNT-V.SSN 'CN:' CNT-V.ID-NUMBER 'CI:' CNT-V.CONTACT-ID INTO v_A     
     PERFORM sbr_ADD 
   END-FIND   
   FIND ST-V WITH SSN = ST-V.SSN 
      COMPRESS FULL '--Status' ' SSN:' ST-V.SSN 'CN:' ST-V.ID-NUMBER 'CI:' ST-V.MEMBER-CONTACT-ID INTO v_A     
      PERFORM sbr_ADD 
   END-FIND   
 
  IF p_CI <> 0 
    v_A := " <br/><b>--- STATUS File CI Links ---</b>" 
    PERFORM sbr_ADD 
    v_A := ' ' 
    FIND ST-V WITH MEMBER-CONTACT-ID = p_CI 
       COMPRESS ' Member CN:' ID-NUMBER  INTO v_A 
    END-FIND    
    FIND ST-V WITH SPOUSE-CONTACT-ID = p_CI 
       COMPRESS v_A ' Spouse CN:' ID-NUMBER  INTO v_A 
    END-FIND    
    FIND ST-V WITH FORMER-SPOUSE-CONTACT-ID = p_CI 
       COMPRESS v_A ' Former Spouse CN:' ID-NUMBER  INTO v_A 
    END-FIND 
        FIND ST-V WITH FORMER-HUSBAND-CONTACT-ID = p_CI 
       COMPRESS v_A ' Former Husband CN:' ID-NUMBER  INTO v_A 
    END-FIND 
    PERFORM sbr_ADD 
 END-IF

   COMPRESS " <br/><b>--- Contact Policies Relations ---</b>" 'Ci:' p_CI INTO v_A
   PERFORM sbr_ADD
   v_A := ' #  Contact  Policy       Relation    (#)    Refernce '
   PERFORM sbr_ADD
   FIND POL-V WITH POL-V.CONTACT-ID =  p_Ci
       COMPRESS '(' RELATION ')' INTO v_A LEAVING NO SPACE
       MOVE EDITED *COUNTER (EM=Z9')') TO v_AA 
       v_A12 := POL-V.POLICY-NUMBER
       COMPRESS FULL v_AA  POL-V.CONTACT-ID v_A12 rel(RELATION) v_A '  ' REFERENCE-NUMBER INTO v_A
       PERFORM sbr_ADD 
   END-FIND
   COMPRESS " <br/><b>--- Contact Policies Relations ---</b>" 'CN:' p_CN INTO v_A
   PERFORM sbr_ADD
   v_A := ' #  Contact  Policy       Relation    (#)    Refernce '
   PERFORM sbr_ADD
   FIND POL-V WITH REFERENCE-NUMBER = p_CN
       COMPRESS '(' RELATION ')' INTO v_A LEAVING NO SPACE
       MOVE EDITED *COUNTER (EM=Z9')') TO v_AA 
       v_A12 := POL-V.POLICY-NUMBER
       COMPRESS FULL v_AA  POL-V.CONTACT-ID v_A12 rel(RELATION) v_A '  ' REFERENCE-NUMBER INTO v_A
       PERFORM sbr_ADD 
   END-FIND
  
*     v_A := "--- The End --- </pre>" 
*     PERFORM sbr_ADD 
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =  
DEFINE sbr_ADD 
  #x:= #x + 1 
  EXPAND ARRAY p_Out TO (1:#x) 
  COMPRESS v_A INTO p_Out(#x)                        
END-SUBROUTINE 
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =  
DEFINE sbr_IF-BENE 
   v_Bene_Pol := ' '
   FIND POL-V WITH POL-V.CONTACT-ID =  bene_CI
     WHERE RELATION = 5 OR = 6 OR = 7 OR = 11
       COMPRESS POL-V.POLICY-NUMBER INTO v_Bene_Pol
   END-FIND
END-SUBROUTINE 
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =  
END
