* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA
PARAMETER
1 p_BeneEZ 
  2 p_CN           (N6)
  2 p_C/Bene       (N1)
  2 p_Bene(6)
    3 Lst-Upd      (N8)   
    3 p_Lay        (N1)
    3 p_C/Iss-Date (N1) 
    3 p_Iss-Date   (N8/7)  /* YYYYMMDD
    3 p_Split      (N1/7)
  3 p_INFO (2)
   4 p_Type        (A1)
   4 p_Share       (A1)
   4 p_C/CI        (N1)
   4 p_CI          (N8/5)
   4 p_C/Text      (n1)
   4 p_Text        (A60/5)
   2 p_RC          (N4)
   2 p_Msg         (A) DYNAMIC  

LOCAL
*
1 BNF-V VIEW OF A-BENE-FINAL
 2 ID-NUMBER          (N6) 
  2 NOTE-DATE          (N8/60) /* LET P/A = PRIMARY/ALTERNATE DESING 
  2 REDEFINE NOTE-DATE         /* let L = linked policies 
   3 BENE-DATES-6-POLS (6) 
    4 BENE-ISS-DT        (N6/7)   /* (D, L) 
    4 BENE-SPLIT-IND     (N1/7)   /* (D, L) 
    4 BENE-LST-UPD       (N8)     /* (D) 
    4 BENE-STRT-PEND     (N8)     /* (D) 
    4 BENE-FOL-UP-IND    (N1)     /* (D) 
    4 BENE-OWN-TRNSF-DT  (N8)     /* (D) 
    4 BENE-SUC-OWN       (N1)     /* (D) 
    4 BENE-NEW-LAYOUT    (N1)     /* (D) 
    4 BENE-FIL1          (N4) 
  2 NOTE-CLERK-ID      (A3/60) 
  2 REDEFINE NOTE-CLERK-ID 
   3 BENE-MISC-6-POLS  (6) 
    4 BENE-STATUS      (A1)     /* (D) 
    4 BENE-RESTRICT    (A1)     /* (D) 
    4 BENE-MISC-PRIM-SEC (2) 
     5 BENE-TYPE        (A1)    /* (D, P/A) 
     5 BENE-SHARE-TYP   (A1) 
     5 BENE-MISC-FIL-2  (A12) 

 2 BENE-CONTACT-ID         (N8/60)
 2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
  2 NOTE-TEXT          (A60/60) 
  2 REDEFINE NOTE-TEXT 
   3 BENE-TEXTS-6-POLS (6) 
    4 BENE-TEXT-PRIM-SEC (2) 
     5 BENE-TEXT-OCC    (5)    /* (D, P/A, TEXT-LINE) 
      6 BENE-TEXT        (A60) 
      6 REDEFINE BENE-TEXT 
       7 BENE-TEXT-1ST-25 (A25) 
       7 BENE-TEXT-LST-35 (A35) 
*
1 #i          (I2)
1 #j          (I2)
1 #k          (I2)

END-DEFINE

ON ERROR
  p_RC := *ERROR-NR
  COMPRESS *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR INTO p_Msg
  ESCAPE ROUTINE
END-ERROR
  FIND(1) BNF-V WITH ID-NUMBER = p_CN
    FOR #i = 1 TO 6 
      IF BENE-TYPE(#i,1) = ' ' ESCAPE TOP END-IF
         p_C/Bene := p_C/Bene + 1
         Lst-Upd(#i) := BENE-LST-UPD(#i)
         p_Lay(#i) :=  BENE-NEW-LAYOUT(#i)
      FOR #k = 1 TO 7
        IF BENE-ISS-DT(#i,#k) <> 0
            IF BENE-ISS-DT(#i,#k) < 350101
               p_Iss-Date (#i,#k) := BENE-ISS-DT(#i,#k) + 20000000
            ELSE    
              p_Iss-Date (#i,#k) := BENE-ISS-DT(#i,#k) + 19000000
            END-IF  
              p_C/Iss-Date(#i) := p_C/Iss-Date(#i) + 1
              p_Split (#i,#k)  := BENE-SPLIT-IND(#i,#k)
          END-IF
      END-FOR
    FOR #j = 1 TO 2 
      p_Type(#i,#j)   := BENE-TYPE(#i,#j)
      p_Share(#i,#j)  := BENE-SHARE-TYP(#i,#j)
      FOR #k = 1 TO 5      
          IF  BENE-CONTACT-ID-R(#i,#j,#k) <> 0 
             p_CI(#i,#j,#k) := BENE-CONTACT-ID-R(#i,#j,#k)
             p_C/CI(#i,#j) :=  p_C/CI(#i,#j) + 1
          END-IF   
       END-FOR
       FOR #k = 1 TO 5      
          IF  BENE-TEXT(#i,#j,#k) <> ' '
            p_Text(#i,#j,#k) :=  BENE-TEXT(#i,#j,#k)
            p_C/Text(#i,#j)  :=  p_C/Text(#i,#j) + 1
          END-IF
       END-FOR
    END-FOR
    END-FOR
END-FIND
END
