* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center/Term Refund Notification
* Object name - MBCN0002
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    02162011       Initial Creation
********************************************************
*
DEFINE DATA PARAMETER
*
1 REQUEST-DATA      (A) DYNAMIC
1 RESPONSE-DATA     (A) DYNAMIC         /* Out
*
LOCAL USING OBJLSTAT
LOCAL USING PARSER-X
LOCAL
1 XML_PARSER_XPATH             (A) DYNAMIC
1 XML_PARSER_XPATH_TYPE        (A1)
1 XML_PARSER_CONTENT           (A) DYNAMIC
1 XML_PARSER_CONTENT_IS_EMPTY  (L)
1 XML_PARSER_INPUT   (A) DYNAMIC
1 XML_PARSER_ERROR_TEXT        (A253)
1 XML_PARSER_RESPONSE          (I2)
1 TermRefundInfo
  2 CN (N6)
  2 TermRefundGroup
    3 TermRefundAmount  (A) DYNAMIC
    3 TermRefundYear  (A) DYNAMIC
*
1 #value       (A) DYNAMIC
1 #I (I2)
*
END-DEFINE
*
XML_PARSER_INPUT := REQUEST-DATA
INCLUDE PARSER_X 'XML_PARSER_INPUT' /* XML file to be parsed
  'XML_PARSER_XPATH'                /* XPATH to represent element...
  'XML_PARSER_XPATH_TYPE'           /* Type of callback
  'XML_PARSER_CONTENT'              /* Content of element found
  'XML_PARSER_CONTENT_IS_EMPTY'     /* Is TRUE if element is empty
  'XML_PARSER_ERROR_TEXT'           /* error Message
  'XML_PARSER_RESPONSE'             /* Error NR; 0 = OK
*
************************** CALLBACK HANDLER
DEFINE SUBROUTINE CALLBACK
**************************
*
DECIDE ON FIRST XML_PARSER_XPATH
  VALUE 'TermRefundInfo'
    RESET TermRefundInfo
  VALUE 'TermRefundInfo/CN/$'
    #value := *TRIM(XML_PARSER_CONTENT)
    IF #value IS (N6) THEN
      TermRefundInfo.CN := VAL(#value)
    END-IF
  NONE VALUE
    IGNORE
END-DECIDE
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE PARSER_ERROR
******************************
COMPRESS XML_PARSER_RESPONSE XML_PARSER_ERROR_TEXT INTO RESPONSE-DATA
ESCAPE ROUTINE
END-SUBROUTINE
*
FIND(1) ST-V WITH ID-NUMBER = TermRefundInfo.CN
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  IF C*OUT-TERM-REF-GROUP <> 0
    FOR #I = 1 TO C*OUT-TERM-REF-GROUP
      TermRefundYear := ST-V.OUT-TERM-REF-YEAR(#I)
      MOVE EDITED ST-V.OUT-TERM-REF-AMOUNT(#I) (EM=ZZZZZZ9.99) TO TermRefundAmount
      COMPRESS '$' *TRIM(TermRefundAmount) INTO TermRefundAmount LEAVING NO
      COMPRESS RESPONSE-DATA
        '<TermRefundGroup>' '<TermRefundAmount>' TermRefundAmount '</TermRefundAmount>'
        '<TermRefundYear>' TermRefundYear '</TermRefundYear>' '</TermRefundGroup>'
        INTO RESPONSE-DATA LEAVING NO
    END-FOR
  END-IF
END-FIND
*
COMPRESS   '<TermRefundInfo>' '<CN>' TermRefundInfo.CN '</CN>'
  RESPONSE-DATA INTO RESPONSE-DATA LEAVING NO
COMPRESS   RESPONSE-DATA '</TermRefundInfo>'
  INTO RESPONSE-DATA LEAVING NO
*
END
