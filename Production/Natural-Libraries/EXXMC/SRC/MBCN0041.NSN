* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online/AUC
* Phone Information
* Get Single or Multiple Phones
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    04182012       Initial Creation
********************************************************
*
DEFINE DATA 
PARAMETER USING MBCA0004
LOCAL USING OBJACPHN
LOCAL USING P_PHONE
LOCAL
*
1 #HIST-KEY         (A) DYNAMIC CONST <'HIST-KEY'>
1 #GET              (A) DYNAMIC CONST <'GET'>
1 #InvalidRequest   (A) DYNAMIC CONST <'-1'>
1 #DOMESTIC         (A1) CONST <'D'>
1 #INTERNATIONAL    (A1) CONST <'I'>
1 #GET-PHONE-ID     (N6)
1 #I                (I2)
1 #II               (I2)
*
END-DEFINE
*
C@PhoneInfo := 1
IF Phone.ContactID = 0 
  Phone.ContactID := F-GET-CONTACT-ID(<Phone.CN>)
END-IF 
EXPAND ARRAY PhoneInfo to (*:C@PhoneInfo)
DECIDE FOR FIRST CONDITION
  WHEN Phone.PhoneID(1) <> 0
    PERFORM GET-SINGLE-PHONE
  WHEN Phone.PhoneID(1) = 0 and Phone.ContactID NE 0
    PERFORM GET-ALL-PHONES
  WHEN NONE
    Phone.ErrorCode := #InvalidRequest
    ESCAPE ROUTINE
END-DECIDE
*
**********************************
DEFINE SUBROUTINE GET-SINGLE-PHONE
**********************************
*
PARM-PHONE.#ACTION    := #GET
PARM-PHONE.PHONE-ID   := Phone.PhoneId(1)
*
CALLNAT 'OBJNCPHN' PARM-PHONE
IF PARM-PHONE.#RETURN-CODE NE ' '
  Phone.ErrorCode := PARM-PHONE.#RETURN-CODE
  ESCAPE ROUTINE
END-IF
PERFORM POPULATE-PHONE-FIELDS
*
END-SUBROUTINE
*
********************************
DEFINE SUBROUTINE GET-ALL-PHONES
********************************
*
CALLNAT 'GET_PHM' Phone.ContactId #GET-PHONE-ID #PHONE-ARRAY(*)
C@PhoneInfo := *OCCURRENCE (#PHONE-ARRAY.PHONE-ID)
IF C@PhoneInfo = 0
  ESCAPE MODULE
END-IF
EXPAND ARRAY PhoneInfo to (*:C@PhoneInfo)
FOR #I = 1 TO C@PhoneInfo
  Phone.PhoneID(#I)            := #PHONE-ARRAY.PHONE-ID(#I)
  Phone.LastUpdatedDate(#I)    := #PHONE-ARRAY.LAST-DATE-UPD(#I)
  Phone.LastUpdatedTime(#I)    := #PHONE-ARRAY.LAST-TIME-UPD(#I)
  Phone.LastUpdatedUser(#I)    := #PHONE-ARRAY.LAST-USER-UPD(#I)
  Phone.PreferredInd(#I)  := #PHONE-ARRAY.PREFERRED-PHONE-IND(#I)
  Phone.PhoneType(#I)          := #PHONE-ARRAY.PHONE-TYPE-CODE(#I)
  Phone.Type(#I)               := #PHONE-ARRAY.TYPE(#I)
  IF #PHONE-ARRAY.TYPE(#I) = #DOMESTIC
    Phone.AreaCode(#I)         := #PHONE-ARRAY.AREA-CODE(#I)    
    Phone.DialNumber(#I)       := #PHONE-ARRAY.DIAL-NUMBER(#I)  
  ELSE
    Phone.AreaCode(#I)         := #PHONE-ARRAY.INTL-ACCESS-CODE(#I) 
    Phone.DialNumber(#I)       := #PHONE-ARRAY.INTL-DIAL-NUMBER(#I) 
  END-IF  
  Phone.Extension(#I)          := #PHONE-ARRAY.EXT(#I)
  Phone.BestCallTime(#I)       := #PHONE-ARRAY.BEST-TIME-TO-CALL(#I)
  Phone.InvalidInd(#I)         := #PHONE-ARRAY.INVALID-IND(#I) 
* Phone.StartDate(#I)          := #PHONE-ARRAY.START-DATE(#I)
* Phone.EndDate(#I)            := #PHONE-ARRAY.END-DATE(#I)
  FOR #II = 1 TO 3
    COMPRESS Phone.Remarks(#I) #PHONE-ARRAY.REMARKS(#I,#II) INTO Phone.Remarks(#I)
  END-FOR
END-FOR
*
END-SUBROUTINE
*
****************************
DEFINE POPULATE-PHONE-FIELDS
****************************
*
FOR #I = 1 TO C@PhoneInfo
  Phone.LastUpdatedDate(#I)       := PARM-PHONE.LAST-DATE-UPD
  Phone.LastUpdatedTime(#I)       := PARM-PHONE.LAST-TIME-UPD
  Phone.LastUpdatedUser(#I)       := PARM-PHONE.LAST-USER-UPD
  Phone.PreferredInd(#I)     := PARM-PHONE.PREFERRED-PHONE-IND
  Phone.PhoneType(#I)             := PARM-PHONE.PHONE-TYPE-CODE
  Phone.Type(#I)                  := PARM-PHONE.TYPE
  IF PARM-PHONE.TYPE = #DOMESTIC
    Phone.AreaCode(#I)            := PARM-PHONE.AREA-CODE
    Phone.DialNumber(#I)          := PARM-PHONE.DIAL-NUMBER
  ELSE
    Phone.AreaCode(#I)            := PARM-PHONE.INTL-ACCESS-CODE
    Phone.DialNumber(#I)          := PARM-PHONE.INTL-DIAL-NUMBER
  END-IF  
  Phone.Extension(#I)             := PARM-PHONE.EXT
  Phone.BestCallTime(#I)          := PARM-PHONE.BEST-TIME-TO-CALL
  Phone.InvalidInd(#I)            := PARM-PHONE.INVALID-IND 
* Phone.StartDate(#I)             := PARM-PHONE.START-DATE
* Phone.EndDate(#I)               := PARM-PHONE.END-DATE
  FOR #II = 1 TO 3
    COMPRESS Phone.Remarks(#I) PARM-PHONE.REMARKS(#II) INTO Phone.Remarks(#I)
  END-FOR
END-FOR
END-SUBROUTINE
END
