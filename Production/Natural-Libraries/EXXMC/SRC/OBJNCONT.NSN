* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* A-CONTACTS - Object Module
* OBJNCONT - Subprogram to perform A-CONTACTS
* access functions.
* * * * * * * * * * * * * * * * * * * * * * * *
***********************************************
* MODIFICATION LOG
***********************************************
* USER   DATE      TAG  REASON
* YAK    09152010  YAK  Initial Creation
* * * * * * * * * * * * * * * * * * * * * * * *
*
DEFINE DATA
PARAMETER USING OBJACONT
*
LOCAL USING OBJLCONT
LOCAL USING ERROR_L
LOCAL USING ERROR_LN
LOCAL
1 #CONTACT           (A) DYNAMIC CONST <'MASTERCO'>
1 #DELETE-FLAG       (A) DYNAMIC CONST <'Y'>
1 #SSN-N             (N9)
1 REDEFINE #SSN-N
  2 #SSN-5           (N5) 
  2 #SSN-LAST4       (N4) 
END-DEFINE
*
* Main processing
*
DECIDE ON FIRST VALUE #ACTION
 VALUE 'ADD'
   PERFORM ADD-RECORD
 VALUE 'UPDATE'
   IF ISN = 0
     PERFORM UPDATE-RECORD-BY-KEY
   ELSE
     PERFORM UPDATE-RECORD-BY-ISN
   END-IF
 VALUE 'GET-MIL'
   PERFORM GET-MIL-INFO    
 VALUE 'UPDATE-MIL-AUC'
   PERFORM UPDATE-MIL-INFO-AUC  
 VALUE 'UPDATE-MIL-MC'
   PERFORM UPDATE-MIL-INFO-MC    
 VALUE 'DELETE'
   PERFORM DELETE-RECORD
 VALUE 'DELETE-DEP'
   PERFORM DELETE-DEPENDENT-RECORD
 VALUE 'GET'
   PERFORM GET-RECORD
 VALUE 'GET-BY-CN'
   PERFORM GET-RECORD-BY-CN
 VALUE 'CHECK-BY-CN'
   PERFORM CHECK-BY-CN
 VALUE 'CHECK-BY-FIELDS'
   PERFORM CHECK-BY-FIELDS
 VALUE 'CHECK-ORGANIZATION'
   PERFORM CHECK-ORGANIZATION  
 VALUE 'HIST-KEY'                        /* Histogram - check existence
   PERFORM HIST-KEY                      /* get *number
 NONE
   IGNORE
END-DECIDE
*
****************************
DEFINE SUBROUTINE ADD-RECORD
****************************
*
MOVE BY NAME PARM-CONTACT TO CT-V
* Get next contact id
CT-V.CONTACT-ID      := F-GET-NEXT-ID(<#CONTACT>)
CT-V.CONTACT-TYPE    := PARM-CONTACT.CONTACT-TYPE 
CT-V.BIRTH-CERT-CODE := PARM-CONTACT.BIRTH-CERT-CODE
* WRITE WORK FILE #LOG VARIABLE 'ADD CONTACT ID = ' CT-V.CONTACT-ID
 STORE CT-V
PARM-CONTACT.CONTACT-ID := CT-V.CONTACT-ID
*
END-SUBROUTINE
*
*******************************
DEFINE SUBROUTINE DELETE-RECORD
*******************************
*
FIND-D.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
 CT-V.DELETE-FLAG   := #DELETE-FLAG
 CT-V.LAST-DATE-UPD := PARM-CONTACT.LAST-DATE-UPD
 CT-V.LAST-TIME-UPD := PARM-CONTACT.LAST-TIME-UPD
 CT-V.LAST-USER-UPD := PARM-CONTACT.LAST-USER-UPD
 UPDATE (FIND-D.)
* WRITE WORK FILE #LOG VARIABLE 'DELETE CONTACT ID = ' CT-V.CONTACT-ID
END-FIND
*
END-SUBROUTINE
*
*****************************************
DEFINE SUBROUTINE DELETE-DEPENDENT-RECORD
*****************************************
*
FIND-DP.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
 IF CT-V.ID-NUMBER NE 0
   PARM-CONTACT.#RETURN-CODE := #CONTACT-HAS-CN
*  WRITE WORK FILE #LOG VARIABLE 'CONTACT ID = ' CT-V.CONTACT-ID ' HAS CN'
   ESCAPE ROUTINE
 END-IF
 CT-V.DELETE-FLAG   := #DELETE-FLAG
 CT-V.LAST-DATE-UPD := PARM-CONTACT.LAST-DATE-UPD
 CT-V.LAST-TIME-UPD := PARM-CONTACT.LAST-TIME-UPD
 CT-V.LAST-USER-UPD := PARM-CONTACT.LAST-USER-UPD
 UPDATE (FIND-DP.)
* WRITE WORK FILE #LOG VARIABLE 'DELETE CONTACT ID = ' CT-V.CONTACT-ID
END-FIND
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-KEY
**************************************
*
FIND-U.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
* SSN is not updatable from Member Center
* Update function does not pass SSN back from Member Center
* Needed for syncronization with Status/Fir files
 PARM-CONTACT.SSN := CT-V.SSN
 PARM-CONTACT.BIRTH-CERT-CODE := CT-V.BIRTH-CERT-CODE
 MOVE BY NAME PARM-CONTACT.MBC_UPDATE TO CT-V
 UPDATE (FIND-U.)
* WRITE WORK FILE #LOG VARIABLE 'UPDATE CONTACT ID = ' CT-V.CONTACT-ID
END-FIND
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE GET-MIL-INFO
******************************
*
FIND-G.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
 PARM-CONTACT.RANK-ID           := CT-V.RANK-ID
 PARM-CONTACT.PREV-RANK-ID      := CT-V.PREV-RANK-ID 
 PARM-CONTACT.RANK-PROTECT      := CT-V.RANK-PROTECT
 PARM-CONTACT.RANK-PROTECT-DATE := CT-V.RANK-PROTECT-DATE
 PARM-CONTACT.MILITARY-STATUS   := CT-V.MILITARY-STATUS
 PARM-CONTACT.SPECIAL-PAY-IND   := CT-V.SPECIAL-PAY-IND
 PARM-CONTACT.LAST-DATE-UPD     := CT-V.LAST-DATE-UPD
 PARM-CONTACT.LAST-TIME-UPD     := CT-V.LAST-TIME-UPD
 PARM-CONTACT.LAST-USER-UPD     := CT-V.LAST-USER-UPD
END-FIND
*
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE UPDATE-MIL-INFO-AUC
*************************************
*
U1.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
 IF CT-V.RANK-ID <> PARM-CONTACT.RANK-ID OR
     CT-V.PREV-RANK-ID      <> PARM-CONTACT.PREV-RANK-ID OR
     CT-V.RANK-PROTECT      <> PARM-CONTACT.RANK-PROTECT OR
     CT-V.RANK-PROTECT-DATE <> PARM-CONTACT.RANK-PROTECT-DATE OR
     CT-V.MILITARY-STATUS   <> PARM-CONTACT.MILITARY-STATUS OR
     CT-V.SPECIAL-PAY-IND   <> PARM-CONTACT.SPECIAL-PAY-IND
    IF CT-V.RANK-ID NE PARM-CONTACT.RANK-ID
      CT-V.PREV-RANK-ID := PARM-CONTACT.PREV-RANK-ID := CT-V.RANK-ID
      CT-V.RANK-ID            := PARM-CONTACT.RANK-ID      
      CT-V.LAST-RANK-DATE-UPD := PARM-CONTACT.LAST-DATE-UPD
      CT-V.LAST-RANK-USER-UPD := PARM-CONTACT.LAST-USER-UPD
    ELSE
      CT-V.PREV-RANK-ID := PARM-CONTACT.PREV-RANK-ID
    END-IF
    CT-V.RANK-PROTECT      := PARM-CONTACT.RANK-PROTECT
    CT-V.RANK-PROTECT-DATE := PARM-CONTACT.RANK-PROTECT-DATE 
    CT-V.MILITARY-STATUS   := PARM-CONTACT.MILITARY-STATUS
    CT-V.SPECIAL-PAY-IND   := PARM-CONTACT.SPECIAL-PAY-IND 
    CT-V.LAST-DATE-UPD     := PARM-CONTACT.LAST-DATE-UPD
    CT-V.LAST-TIME-UPD     := PARM-CONTACT.LAST-TIME-UPD
    CT-V.LAST-USER-UPD     := PARM-CONTACT.LAST-USER-UPD
    UPDATE (U1.)
  END-IF  
END-FIND
*
END-SUBROUTINE
*
************************************
DEFINE SUBROUTINE UPDATE-MIL-INFO-MC
************************************
*
U2.
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
  IF CT-V.RANK-ID <> PARM-CONTACT.RANK-ID OR
      CT-V.MILITARY-STATUS <> PARM-CONTACT.MILITARY-STATUS
    IF CT-V.RANK-ID NE PARM-CONTACT.RANK-ID
      CT-V.PREV-RANK-ID            := CT-V.RANK-ID
      CT-V.RANK-ID                 := PARM-CONTACT.RANK-ID
      CT-V.LAST-RANK-DATE-UPD      := PARM-CONTACT.LAST-DATE-UPD
      CT-V.LAST-RANK-USER-UPD      := PARM-CONTACT.LAST-USER-UPD
    END-IF
    CT-V.MILITARY-STATUS           := PARM-CONTACT.MILITARY-STATUS
    CT-V.LAST-DATE-UPD             := PARM-CONTACT.LAST-DATE-UPD
    CT-V.LAST-TIME-UPD             := PARM-CONTACT.LAST-TIME-UPD
    CT-V.LAST-USER-UPD             := PARM-CONTACT.LAST-USER-UPD
    UPDATE (U2.)
  END-IF
END-FIND
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-ISN
**************************************
*
GT-U. GET CT-V PARM-CONTACT.ISN
* SSN is not updatable from Member Center
* Update function does not pass SSN back from Member Center
* Needed for syncronization with Status/Fir files
PARM-CONTACT.SSN := CT-V.SSN
PARM-CONTACT.BIRTH-CERT-CODE := CT-V.BIRTH-CERT-CODE
MOVE BY NAME PARM-CONTACT.MBC_UPDATE TO CT-V
UPDATE (GT-U.)
* WRITE WORK FILE #LOG VARIABLE 'UPDATE CONTACT ID = ' CT-V.CONTACT-ID
*
END-SUBROUTINE
*
**************************
DEFINE SUBROUTINE HIST-KEY
**************************
*
FIND NUMBER CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE GET-RECORD
****************************
*
FIND(1) CT-V WITH CONTACT-ID = PARM-CONTACT.CONTACT-ID
  IF NO RECORDS FOUND
    PARM-CONTACT.#RETURN-CODE := #CONTACT-NOT-FOUND
  END-NOREC
  IF CT-V.DELETE-FLAG = 'Y'
    PARM-CONTACT.#RETURN-CODE := #CONTACT-NOT-FOUND
  ELSE  
    ISN := *ISN
    MOVE BY NAME CT-V TO PARM-CONTACT
  END-IF  
END-FIND
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE GET-RECORD-BY-CN
**********************************
*
FIND(1) CT-V WITH ID-NUMBER = PARM-CONTACT.ID-NUMBER
  IF NO RECORDS FOUND
    PARM-CONTACT.#RETURN-CODE := #CONTACT-WITH-CN-NOT-FOUND
  END-NOREC
  ISN    := *ISN
  MOVE BY NAME CT-V TO PARM-CONTACT
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE CHECK-BY-CN
*****************************
*
FIND(1) CT-V WITH ID-NUMBER = PARM-CONTACT.ID-NUMBER
  IF NO RECORDS FOUND
    PARM-CONTACT.#RETURN-CODE := #CONTACT-WITH-CN-NOT-FOUND
  END-NOREC
  #SSN-N := CT-V.SSN  
  IF #SSN-LAST4 = PARM-CONTACT.SSN AND 
    ((PARM-CONTACT.LAST-NAME = CT-V.LAST-NAME AND PARM-CONTACT.DATE-OF-BIRTH = CT-V.DATE-OF-BIRTH) OR
     (CT-V.CONTACT-TYPE = 'O' AND PARM-CONTACT.FIRM-ORGANIZATION-NAME = CT-V.FIRM-ORGANIZATION-NAME))
    ISN := *ISN
    PARM-CONTACT.ID-NUMBER := CT-V.ID-NUMBER    
    ESCAPE ROUTINE
  END-IF
  #RETURN-CODE := #VALIDATION-FAILED
END-FIND
END-SUBROUTINE
*
*********************************
DEFINE SUBROUTINE CHECK-BY-FIELDS
*********************************
*
FIND CT-V WITH DATE-OF-BIRTH = PARM-CONTACT.DATE-OF-BIRTH
  IF NO RECORDS FOUND
    PARM-CONTACT.#RETURN-CODE := #CONTACT-WITH-DOB-NOT-FOUND
  END-NOREC  
  #SSN-N := CT-V.SSN
  IF #SSN-LAST4 = PARM-CONTACT.SSN AND PARM-CONTACT.LAST-NAME = CT-V.LAST-NAME AND CT-V.ID-NUMBER <> 0
    ISN := *ISN
    PARM-CONTACT.ID-NUMBER := CT-V.ID-NUMBER
    RESET #RETURN-CODE
    ESCAPE ROUTINE
  END-IF
  #RETURN-CODE := #VALIDATION-FAILED
END-FIND
END-SUBROUTINE
*
************************************
DEFINE SUBROUTINE CHECK-ORGANIZATION
************************************
*
FIND CT-V WITH FIRM-ORGANIZATION-NAME = PARM-CONTACT.FIRM-ORGANIZATION-NAME
  IF NO RECORDS FOUND
    PARM-CONTACT.#RETURN-CODE := #ORGANIZATION-NOT-FOUND
  END-NOREC  
  #SSN-N := CT-V.SSN
  IF #SSN-LAST4 = PARM-CONTACT.SSN
    ISN := *ISN
    PARM-CONTACT.ID-NUMBER := CT-V.ID-NUMBER
    RESET #RETURN-CODE
    ESCAPE ROUTINE
  END-IF
  #RETURN-CODE := #VALIDATION-FAILED
END-FIND
END-SUBROUTINE
END
