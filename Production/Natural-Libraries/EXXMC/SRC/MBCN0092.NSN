* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online
* Member Benefits/Financial Information
* Transaction Logic
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    04042012       Initial Creation
********************************************************
*
DEFINE DATA
PARAMETER USING MBCAFIN1
LOCAL USING GPRATR01
*
LOCAL
1 FUNCTION_CODE     (A) DYNAMIC CONST <'FAS'>
1 #ContactID        (N8)
1 #INV-TYPE-NAME    (A) DYNAMIC
1 #TRAN-LINE        (A/*) DYNAMIC
1 #I                (I2)
1 #N                (I2)
1 #LOOK-UP-KEY      (A7) CONST <'CODE090'>
1 #LOOK-UP-VALUE    (A) DYNAMIC
1 #WORK             (A) DYNAMIC
*
END-DEFINE
*
#N := *OCCURRENCE(FIN-REQUEST.INV-NAME)
EXPAND ARRAY #TRAN-LINE TO (*:#N)
EXPAND ARRAY BEFORE_IMAGE.INV-LINE TO (*:#N)
EXPAND ARRAY AFTER_IMAGE.INV-LINE TO (*:#N)
DECIDE FOR FIRST CONDITION
  WHEN FIN-REQUEST.ACTION-TYPE = 'ADD' AND FIN-REQUEST.INVESTMENT-GROUP = 'GEN'
* INV-NAME INV-BALANCE INV-RATE INV-MAT-DT INV-DEBT
    RESET BEFORE_IMAGE
    PERFORM GET-GEN-LINE
    AFTER_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'ADD' AND FIN-REQUEST.INVESTMENT-GROUP = 'E-BONDS'
    RESET BEFORE_IMAGE
    PERFORM GET-EBOND-LINE
    AFTER_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'ADD' AND FIN-REQUEST.INVESTMENT-GROUP = 'MSB'
    RESET BEFORE_IMAGE
    PERFORM GET-MSB-LINE
    AFTER_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'DELETE' AND FIN-REQUEST.INVESTMENT-GROUP = 'GEN'
    RESET AFTER_IMAGE
    PERFORM GET-GEN-LINE
    BEFORE_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'DELETE' AND FIN-REQUEST.INVESTMENT-GROUP = 'E-BONDS'
    RESET AFTER_IMAGE
    PERFORM GET-EBOND-LINE
    BEFORE_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'DELETE' AND FIN-REQUEST.INVESTMENT-GROUP = 'MSB'
    RESET AFTER_IMAGE
    PERFORM GET-MSB-LINE
    BEFORE_IMAGE.INV-LINE(*) := #TRAN-LINE(*)
  WHEN FIN-REQUEST.ACTION-TYPE = 'UPDATE' AND FIN-REQUEST.INVESTMENT-GROUP = 'GEN'
    PERFORM GET-GEN-LINE
    AFTER_IMAGE.INV-LINE(1) := #TRAN-LINE(1)
    BEFORE_IMAGE.INV-LINE(1) := #TRAN-LINE(2)
  WHEN FIN-REQUEST.ACTION-TYPE = 'UPDATE' AND FIN-REQUEST.INVESTMENT-GROUP = 'E-BONDS'
    PERFORM GET-EBOND-LINE
    AFTER_IMAGE.INV-LINE(1) := #TRAN-LINE(1)
    BEFORE_IMAGE.INV-LINE(1) := #TRAN-LINE(2)
  WHEN FIN-REQUEST.ACTION-TYPE = 'UPDATE' AND FIN-REQUEST.INVESTMENT-GROUP = 'MSB'
    PERFORM GET-MSB-LINE
    AFTER_IMAGE.INV-LINE(1) := #TRAN-LINE(1)
    BEFORE_IMAGE.INV-LINE(1) := #TRAN-LINE(2)
  WHEN NONE
    IGNORE
END-DECIDE
*
PERFORM WRITE-TRANSACTION-LOG
*
**=================
DEFINE GET-GEN-LINE
**=================
FOR #I = 1 TO #N
  #LOOK-UP-VALUE := FIN-REQUEST.INV-TYPE(#I)
  #INV-TYPE-NAME := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,#LOOK-UP-VALUE>)
  COMPRESS FIN-REQUEST.INV-TYPE(#I) #INV-TYPE-NAME FIN-REQUEST.INV-NAME(#I) 
    'Balance $' FIN-REQUEST.INV-BALANCE(#I) INTO #TRAN-LINE(#I)
  IF FIN-REQUEST.INV-RATE(#I) <> 0
    MOVE EDITED FIN-REQUEST.INV-RATE(#I) (EM=Z9.99) TO #WORK
    COMPRESS #WORK '%' INTO #WORK LEAVING NO
    COMPRESS #TRAN-LINE(#I) 'with rate' *TRIM(#WORK) INTO #TRAN-LINE(#I)
  END-IF
  IF FIN-REQUEST.INV-MAT-DT(#I) <> 0
    #WORK := FUNCTION-CALENDAR(<FIN-REQUEST.INV-MAT-DT(#I),'E'>)
    COMPRESS #TRAN-LINE(#I) 'Mature on' #WORK INTO #TRAN-LINE(#I)
  END-IF
  IF FIN-REQUEST.INV-DEBT(#I) <> 0
    COMPRESS #TRAN-LINE(#I) 'Debt $' FIN-REQUEST.INV-DEBT(#I) INTO #TRAN-LINE(#I)
  END-IF
END-FOR
END-SUBROUTINE
**===================
DEFINE GET-EBOND-LINE
**===================
* INV-E-FACE INV-E-PUR-DT INV-E-END-DT INV-E-QUANTITY
FOR #I = 1 TO #N
  #LOOK-UP-VALUE := FIN-REQUEST.INV-TYPE(#I)
  #INV-TYPE-NAME := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,#LOOK-UP-VALUE>)
  COMPRESS FIN-REQUEST.INV-TYPE(#I) #INV-TYPE-NAME FIN-REQUEST.INV-NAME(#I) 
    'Face Amount $' FIN-REQUEST.INV-E-FACE(#I) INTO #TRAN-LINE(#I)
  IF FIN-REQUEST.INV-E-PUR-DT(#I) <> 0
    #WORK := FUNCTION-CALENDAR(<FIN-REQUEST.INV-E-PUR-DT(#I),'E'>)
    COMPRESS #TRAN-LINE(#I) 'Pur-Date' #WORK INTO #TRAN-LINE(#I)
  END-IF
  IF FIN-REQUEST.INV-E-END-DT(#I) <> 0
    #WORK := FUNCTION-CALENDAR(<FIN-REQUEST.INV-E-END-DT(#I),'E'>)
    COMPRESS #TRAN-LINE(#I) 'End Date' #WORK INTO #TRAN-LINE(#I)
  END-IF
  IF FIN-REQUEST.INV-E-QUANTITY(#I) <> 0
    COMPRESS #TRAN-LINE(#I) 'Quantity' FIN-REQUEST.INV-E-QUANTITY(#I) INTO #TRAN-LINE(#I)
  END-IF
END-FOR
END-SUBROUTINE
**=================
DEFINE GET-MSB-LINE
**=================
* INV-MSB-TICK-SYM INV-MSB-TICK-NAME INV-MSB-UNITS
FOR #I = 1 TO #N
  #LOOK-UP-VALUE := FIN-REQUEST.INV-TYPE(#I)
  #INV-TYPE-NAME := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,#LOOK-UP-VALUE>)
  COMPRESS FIN-REQUEST.INV-TYPE(#I) #INV-TYPE-NAME FIN-REQUEST.INV-MSB-TICK-SYM(#I) 
    'Quantity'INTO #TRAN-LINE(#I)
  MOVE EDITED FIN-REQUEST.INV-MSB-UNITS(#I)(EM=ZZZZZ9.999) TO #WORK      
  IF FIN-REQUEST.INV-TYPE(#I) = 'PG' OR = 'PP' OR = 'PS'
*    MOVE EDITED FIN-REQUEST.INV-MSB-UNITS(#I)(EM=ZZZZZ9.99) TO #WORK
    COMPRESS #TRAN-LINE(#I) *TRIM(#WORK) 'oz' INTO #TRAN-LINE(#I) 
  ELSE
*    MOVE EDITED FIN-REQUEST.INV-MSB-UNITS(#I)(EM=ZZZZZ9) TO #WORK    
    COMPRESS #TRAN-LINE(#I) *TRIM(#WORK) 'sh' INTO #TRAN-LINE(#I) 
  END-IF
END-FOR
END-SUBROUTINE
*
***************************************
DEFINE SUBROUTINE WRITE-TRANSACTION-LOG
***************************************
* Values for Transaction Log
#ContactID    := F-GET-CONTACT-ID(<FIN-REQUEST.PARM-CN>)
TR_ID_NUMBER  := FIN-REQUEST.PARM-CN
TR_NAME       := FUNCTION-CONTACT-NAME(<#ContactID>)
TR_CLERK      := FIN-REQUEST.USER-ID
CALLNAT 'GPRNTR01' FUNCTION_CODE TRAN_LOG_HEADER BEFORE_IMAGE AFTER_IMAGE
END-SUBROUTINE
END
