* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* A-CONTACT-PHONE - Object Module
* OBJNCPHN - Subprogram to perform A-CONTACT-PHONE
* access functions.
* * * * * * * * * * * * * * * * * * * * * * * *
***********************************************
* MODIFICATION LOG
***********************************************
* USER   DATE      TAG  REASON
* YAK    09222010  YAK  Initial Creation
* * * * * * * * * * * * * * * * * * * * * * * *
*
DEFINE DATA
PARAMETER USING OBJACPHN
*
LOCAL USING OBJLCPHN
LOCAL USING ERROR_L
LOCAL
1 #PHONE               (A) DYNAMIC CONST <'PHONEID'>
1 #SOURCE-AUC          (A) DYNAMIC CONST <'U'>
END-DEFINE
*
* Main processing
*
DECIDE ON FIRST VALUE #ACTION
  VALUE 'ADD'
    PERFORM ADD-RECORD
  VALUE 'UPDATE'
    IF ISN = 0
      PERFORM UPDATE-RECORD-BY-KEY
    ELSE
      PERFORM UPDATE-RECORD-BY-ISN
    END-IF
  VALUE 'DELETE'
    IF ISN = 0
      PERFORM DELETE-RECORD-BY-KEY
    ELSE
      PERFORM DELETE-RECORD-BY-ISN
    END-IF
  VALUE 'GET'
    PERFORM GET-RECORD
  VALUE 'GET-PREFERRED'
    PERFORM GET-PREFERRED-RECORD
  VALUE 'RESET-PREFERRED'
    PERFORM RESET-PREFERRED-IND
  VALUE 'HIST-KEY'                        /* Histogram - check existence
    PERFORM HIST-KEY                      /* get *number
  VALUE 'OAUPDATE'
    PERFORM ONLINEAPP-UPDATE
  NONE
    IGNORE
END-DECIDE
*
/*
DEFINE ONLINEAPP-UPDATE
/*     ----------------
SOLA.
FIND PHONE-V WITH CONTACT-ID = PARM-PHONE.CONTACT-ID
  IF NO RECORDS FOUND
    PERFORM ADD-RECORD
    ESCAPE BOTTOM
  END-NOREC
  IF PHONE-V.TYPE = 'D'
      AND PHONE-V.DIAL-NUMBER = PARM-PHONE.DIAL-NUMBER
    ISN := *ISN (SOLA.)
    PERFORM UPDATE-RECORD-BY-ISN
    ESCAPE BOTTOM
  END-IF
  IF PHONE-V.TYPE = 'I'
      AND PHONE-V.INTL-DIAL-NUMBER = PARM-PHONE.INTL-DIAL-NUMBER
    ISN := *ISN (SOLA.)
    PERFORM UPDATE-RECORD-BY-ISN
    ESCAPE BOTTOM
  END-IF
END-FIND
IF ISN = 0
  PERFORM ADD-RECORD
END-IF
IF PARM-PHONE.PREFERRED-PHONE-IND = 'Y'
  FIND PHONE-V WITH CONTACT-ID = PARM-PHONE.CONTACT-ID
    IF *ISN(0084) = ISN
      ESCAPE TOP
    END-IF
    RESET PHONE-V.PREFERRED-PHONE-IND
    UPDATE
  END-FIND
END-IF
END-SUBROUTINE
/*
****************************
DEFINE SUBROUTINE ADD-RECORD
****************************
*
MOVE BY NAME PARM-PHONE TO PHONE-V
* Get next pool id
PHONE-V.PHONE-ID := F-GET-NEXT-ID(<#PHONE>)
* WRITE WORK FILE #LOG VARIABLE 'ADD PHONE ID = ' PHONE-V.PHONE-ID
NEWPH.
STORE PHONE-V
PARM-PHONE.PHONE-ID := PHONE-V.PHONE-ID
ISN := *ISN(NEWPH.)
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-ISN
**************************************
*
GT-U. GET PHONE-V ISN
IF PARM-PHONE.PHONE-SOURCE = #SOURCE-AUC
  MOVE BY NAME PARM-PHONE TO PHONE-V
ELSE
* Do not use MOVE BY NAME - only certain fields can be updated from Member Center
  PHONE-V.PHONE-TYPE-CODE     := PARM-PHONE.PHONE-TYPE-CODE
  PHONE-V.TYPE                := PARM-PHONE.TYPE
  PHONE-V.AREA-CODE           := PARM-PHONE.AREA-CODE
  PHONE-V.DIAL-NUMBER         := PARM-PHONE.DIAL-NUMBER
  PHONE-V.EXT                 := PARM-PHONE.EXT
  PHONE-V.INTL-ACCESS-CODE    := PARM-PHONE.INTL-ACCESS-CODE
  PHONE-V.INTL-DIAL-NUMBER    := PARM-PHONE.INTL-DIAL-NUMBER
  PHONE-V.PREFERRED-PHONE-IND := PARM-PHONE.PREFERRED-PHONE-IND
  PHONE-V.INVALID-IND         := PARM-PHONE.INVALID-IND
  PHONE-V.PHONE-SOURCE        := PARM-PHONE.PHONE-SOURCE
  PHONE-V.LAST-DATE-UPD       := PARM-PHONE.LAST-DATE-UPD
  PHONE-V.LAST-TIME-UPD       := PARM-PHONE.LAST-TIME-UPD
  PHONE-V.LAST-USER-UPD       := PARM-PHONE.LAST-USER-UPD
END-IF
* WRITE WORK FILE #LOG VARIABLE 'UPDATE PHONE ID = ' PHONE-V.PHONE-ID
UPDATE (GT-U.)
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE DELETE-RECORD-BY-ISN
**************************************
ON ERROR
  IF *ERROR-NR = 3113
    #RETURN-CODE := #ISN-DOES-NOT-EXIST
    ESCAPE ROUTINE
  END-IF
END-ERROR
*
GT-D. GET PHONE-V ISN
DELETE (GT-D.)
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE DELETE-RECORD-BY-KEY
**************************************
*
FIND-D.
FIND(1) PHONE-V WITH PHONE-ID = PARM-PHONE.PHONE-ID
  DELETE (FIND-D.)
* WRITE WORK FILE #LOG VARIABLE 'DELETE PHONE ID = ' PHONE-V.PHONE-ID
END-FIND
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-KEY
**************************************
*
FIND-U.
FIND(1) PHONE-V WITH PHONE-ID = PARM-PHONE.PHONE-ID
  IF PARM-PHONE.PHONE-SOURCE = #SOURCE-AUC
    MOVE BY NAME PARM-PHONE TO PHONE-V
  ELSE
* Do not use MOVE BY NAME - only certain fields can be updated from Member Center
    PHONE-V.PHONE-TYPE-CODE     := PARM-PHONE.PHONE-TYPE-CODE
    PHONE-V.TYPE                := PARM-PHONE.TYPE
    PHONE-V.AREA-CODE           := PARM-PHONE.AREA-CODE
    PHONE-V.DIAL-NUMBER         := PARM-PHONE.DIAL-NUMBER
    PHONE-V.EXT                 := PARM-PHONE.EXT
    PHONE-V.INTL-ACCESS-CODE    := PARM-PHONE.INTL-ACCESS-CODE
    PHONE-V.INTL-DIAL-NUMBER    := PARM-PHONE.INTL-DIAL-NUMBER
    PHONE-V.PREFERRED-PHONE-IND := PARM-PHONE.PREFERRED-PHONE-IND
    PHONE-V.INVALID-IND         := PARM-PHONE.INVALID-IND
    PHONE-V.PHONE-SOURCE        := PARM-PHONE.PHONE-SOURCE
    PHONE-V.LAST-DATE-UPD       := PARM-PHONE.LAST-DATE-UPD
    PHONE-V.LAST-TIME-UPD       := PARM-PHONE.LAST-TIME-UPD
    PHONE-V.LAST-USER-UPD       := PARM-PHONE.LAST-USER-UPD
  END-IF
  UPDATE (FIND-U.)
* WRITE WORK FILE #LOG VARIABLE 'UPDATE PHONE ID = ' PHONE-V.PHONE-ID
END-FIND
*
END-SUBROUTINE
*
**************************
DEFINE SUBROUTINE HIST-KEY
**************************
*
FIND NUMBER PHONE-V WITH PHONE-ID = PARM-PHONE.PHONE-ID
IF *NUMBER = 0
  #RETURN-CODE := #PHONE-ID-NOT-FOUND
ELSE
  #NUMBER-OF-RECORDS := *NUMBER  
END-IF
*
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE GET-RECORD
****************************
*
FIND(1) PHONE-V WITH PHONE-ID = PARM-PHONE.PHONE-ID
  IF NO RECORDS FOUND
    PARM-PHONE.#RETURN-CODE := #PHONE-ID-NOT-FOUND
  END-NOREC
  ISN    := *ISN
  MOVE BY NAME PHONE-V TO PARM-PHONE
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE GET-PREFERRED-RECORD
**************************************
*
FIND PHONE-V WITH CONTACT-ID = PARM-PHONE.CONTACT-ID
  IF NO RECORDS FOUND
    PARM-PHONE.#RETURN-CODE := #PHONE-CONTACT-ID-NOT-FOUND
  END-NOREC
  ACCEPT IF PHONE-V.PREFERRED-PHONE-IND = PARM-PHONE.PREFERRED-PHONE-IND
  ISN    := *ISN
  MOVE BY NAME PHONE-V TO PARM-PHONE
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE RESET-PREFERRED-IND
*************************************
*
FIND-R.
FIND PHONE-V WITH PHONE-ID = PARM-PHONE.PHONE-ID
  PHONE-V.PREFERRED-PHONE-IND := PARM-PHONE.PREFERRED-PHONE-IND
  UPDATE (FIND-R.)  
END-FIND
END-SUBROUTINE
END
