* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* A-WEB-MEMBER-INFO - Object Module
* OBJNWEBU - Subprogram to perform A-WEB-MEMBER-INFO
* access functions.
***********************************************
* MODIFICATION LOG
***********************************************
* USER   DATE      TAG  REASON
* YAK    11142013  YAK  Initial Creation
***********************************************
*
DEFINE DATA
PARAMETER USING OBJAWEBU
*
LOCAL USING OBJLWEBU
LOCAL USING ERROR_LN
LOCAL
1 #ISN (P9)
END-DEFINE
*
* Main processing
*
DECIDE ON FIRST VALUE #ACTION
  VALUE 'AddRecord'
    PERFORM ADD-RECORD
  VALUE 'Update'
    IF PARM-WEB-ACCOUNT-INFO.ISN <> 0
      PERFORM UPDATE-RECORD-BY-ISN
    ELSE
      PERFORM UPDATE-RECORD
    END-IF
  VALUE 'MemberLogin'
    PERFORM MEMBER-LOGIN
  VALUE 'UpdatePO'
    PERFORM UPDATE-PAPERLESS-OPTIONS
  VALUE 'UpdatePassword'
    PERFORM UPDATE-PASSWORD
  VALUE 'UpdateUserName'
    PERFORM UPDATE-USER-NAME
  VALUE 'UpdateDALAccessDate'
    PERFORM UPDATE-DAL-ACCESS-DATE
  VALUE 'UpdateSecurityQuestions'
    PERFORM UPDATE-SECURITY-QUESTIONS
  VALUE 'CheckUserID'
    PERFORM CHECK-USER-NAME
  VALUE 'Delete'
    PERFORM DELETE
  VALUE 'GET'
    PERFORM GET-RECORD
  VALUE 'HistKey'                         /* Histogram - check existence
    PERFORM HIST-KEY                      /* get *number
  NONE
    IGNORE
END-DECIDE
*
****************************
DEFINE SUBROUTINE ADD-RECORD
****************************
* Add new record
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  #ISN := *ISN
END-FIND
IF #ISN <> 0
  GT1.
  GET WEB-ACCOUNT-INFO #ISN
END-IF
MOVE BY NAME PARM-WEB-ACCOUNT-INFO TO WEB-ACCOUNT-INFO
IF #ISN = 0
  STORE WEB-ACCOUNT-INFO
  WRITE WORK FILE 1 VARIABLE 'ADD New account' WEB-ACCOUNT-INFO.MEMBER-CN  'to WEB-AACOUNT-INFO file'
ELSE
  UPDATE (GT1.)
  WRITE WORK FILE 1 VARIABLE 'Update account' WEB-ACCOUNT-INFO.MEMBER-CN  'on WEB-AACOUNT-INFO file'
END-IF
*
END-SUBROUTINE
*
*******************************
DEFINE SUBROUTINE UPDATE-RECORD
*******************************
* Update Record
FIND-U.
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  MOVE BY NAME PARM-WEB-ACCOUNT-INFO TO WEB-ACCOUNT-INFO
  ISN := *ISN
  UPDATE (FIND-U.)
  WRITE WORK FILE 1 VARIABLE 'UPDATE WEB-ACCOUNT-INFO FOR MEMBER-CN ' WEB-ACCOUNT-INFO.MEMBER-CN
END-FIND
IF ISN = 0
  PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
END-IF
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-ISN
**************************************
* Update by ISN
GET-U.
GET WEB-ACCOUNT-INFO PARM-WEB-ACCOUNT-INFO.ISN
MOVE BY NAME PARM-WEB-ACCOUNT-INFO TO WEB-ACCOUNT-INFO
UPDATE (GET-U.)
WRITE WORK FILE 1 VARIABLE 'UPDATE WEB-ACCOUNT-INFO for MEMBER-CN ' WEB-ACCOUNT-INFO.MEMBER-CN
END-SUBROUTINE
*
******************************************
DEFINE SUBROUTINE UPDATE-PAPERLESS-OPTIONS
******************************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  WEB-ACCOUNT-INFO.ANNS-DELIVERY-FLAG  := PARM-WEB-ACCOUNT-INFO.ANNS-DELIVERY-FLAG
  WEB-ACCOUNT-INFO.BILL-DELIVERY-FLAG  := PARM-WEB-ACCOUNT-INFO.BILL-DELIVERY-FLAG
  WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE := PARM-WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE  
  WEB-ACCOUNT-INFO.DATE-MODIFIED       := PARM-WEB-ACCOUNT-INFO.DATE-MODIFIED  
  UPDATE
END-FIND
*
END-SUBROUTINE

*********************************
DEFINE SUBROUTINE UPDATE-PASSWORD
*********************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  WEB-ACCOUNT-INFO.WEB-USER-PASSWORD    := PARM-WEB-ACCOUNT-INFO.WEB-USER-PASSWORD
  WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE  := PARM-WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE
  WEB-ACCOUNT-INFO.DATE-MODIFIED        := PARM-WEB-ACCOUNT-INFO.DATE-MODIFIED  
  UPDATE
END-FIND
*
END-SUBROUTINE

**********************************
DEFINE SUBROUTINE UPDATE-USER-NAME
**********************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  WEB-ACCOUNT-INFO.WEB-USER-NAME       := PARM-WEB-ACCOUNT-INFO.WEB-USER-NAME
  WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE := PARM-WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE
  WEB-ACCOUNT-INFO.DATE-MODIFIED       := PARM-WEB-ACCOUNT-INFO.DATE-MODIFIED
  UPDATE
END-FIND
*
END-SUBROUTINE

*******************************************
DEFINE SUBROUTINE UPDATE-SECURITY-QUESTIONS
*******************************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  WEB-ACCOUNT-INFO.SECURITY-QUESTION(*) := PARM-WEB-ACCOUNT-INFO.SECURITY-QUESTION(*)
  WEB-ACCOUNT-INFO.SECURITY-ANSWER(*)   := PARM-WEB-ACCOUNT-INFO.SECURITY-ANSWER(*)
  WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE  := PARM-WEB-ACCOUNT-INFO.PROFILE-UPDATE-DATE  
  WEB-ACCOUNT-INFO.DATE-MODIFIED        := PARM-WEB-ACCOUNT-INFO.DATE-MODIFIED
  UPDATE
END-FIND
*
END-SUBROUTINE

************************
DEFINE SUBROUTINE DELETE
************************
* Delete record from WEB-ACCOUNT-INFO file
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  DELETE
  WRITE WORK FILE 1 VARIABLE 'DELETE record from file with' WEB-ACCOUNT-INFO.MEMBER-CN
END-FIND
*
END-SUBROUTINE
*
**************************
DEFINE SUBROUTINE HIST-KEY
**************************
*
FIND NUMBER WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
#NUMBER-OF-RECORDS := *NUMBER
*
END-SUBROUTINE
*
*********************************
DEFINE SUBROUTINE CHECK-USER-NAME
*********************************
*
FIND NUMBER WEB-ACCOUNT-INFO WITH WEB-USER-NAME = PARM-WEB-ACCOUNT-INFO.WEB-USER-NAME
#NUMBER-OF-RECORDS := *NUMBER
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE MEMBER-LOGIN
******************************
*
DECIDE FOR FIRST CONDITION
  WHEN PARM-WEB-ACCOUNT-INFO.WEB-USER-NAME <> ' '
    PERFORM MEMBER-LOGIN-USER-NAME
  WHEN PARM-WEB-ACCOUNT-INFO.MEMBER-CN <> 0
    PERFORM MEMBER-LOGIN-CN
  WHEN NONE
    #RETURN-CODE := #LOGIN-INCORRECT
    ESCAPE ROUTINE
END-DECIDE
*
END-SUBROUTINE
*
****************************************
DEFINE SUBROUTINE MEMBER-LOGIN-USER-NAME
****************************************
*
FIND(1) WEB-ACCOUNT-INFO WITH WEB-USER-NAME = PARM-WEB-ACCOUNT-INFO.WEB-USER-NAME
  IF NO RECORDS FOUND
    #RETURN-CODE := #USER-NAME-NOT-EXIST
    ESCAPE ROUTINE
  END-NOREC
  PARM-WEB-ACCOUNT-INFO.MEMBER-CN := WEB-ACCOUNT-INFO.MEMBER-CN
  IF PARM-WEB-ACCOUNT-INFO.WEB-USER-PASSWORD = *TRIM(WEB-ACCOUNT-INFO.WEB-USER-PASSWORD)
    IGNORE
  ELSE
    #RETURN-CODE := #LOGIN-INCORRECT
    ESCAPE ROUTINE
  END-IF
END-FIND
END-SUBROUTINE
*
*********************************
DEFINE SUBROUTINE MEMBER-LOGIN-CN
*********************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    #RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  IF WEB-ACCOUNT-INFO.REGISTERED-FLAG <> 'Y'
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-IF  
  PARM-WEB-ACCOUNT-INFO.WEB-USER-NAME := WEB-ACCOUNT-INFO.WEB-USER-NAME
  IF PARM-WEB-ACCOUNT-INFO.WEB-USER-PASSWORD = *TRIM(WEB-ACCOUNT-INFO.WEB-USER-PASSWORD)
    IGNORE
  ELSE
    #RETURN-CODE := #LOGIN-INCORRECT
    ESCAPE ROUTINE
  END-IF
END-FIND
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE GET-RECORD
****************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  PARM-WEB-ACCOUNT-INFO.ISN := *ISN
  MOVE BY NAME WEB-ACCOUNT-INFO TO PARM-WEB-ACCOUNT-INFO
END-FIND
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE GET-DAL-ACCESS-DATE
*************************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  PARM-WEB-ACCOUNT-INFO.DAL-ACCESS-DATE := WEB-ACCOUNT-INFO.DAL-ACCESS-DATE
END-FIND
END-SUBROUTINE
*
****************************************
DEFINE SUBROUTINE UPDATE-DAL-ACCESS-DATE
****************************************
*
FIND(1) WEB-ACCOUNT-INFO WITH MEMBER-CN = PARM-WEB-ACCOUNT-INFO.MEMBER-CN
  IF NO RECORDS FOUND
    PARM-WEB-ACCOUNT-INFO.#RETURN-CODE := #ID-NOT-REGISTERED
    ESCAPE ROUTINE
  END-NOREC
  WEB-ACCOUNT-INFO.DAL-ACCESS-DATE := PARM-WEB-ACCOUNT-INFO.DAL-ACCESS-DATE
  UPDATE
END-FIND
END-SUBROUTINE
*
END
