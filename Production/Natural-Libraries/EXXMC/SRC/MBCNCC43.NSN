* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online / MC-III
* ManageCreditCards
* MBCNCC43 - Checks policies paid by CN before deleting Credit Card
* Called from MBCNCC40
************************************************************
*        MODIFICATION LOG
************************************************************
* USER   DATE      TAG  REASON
* YAK    05252017       Initial Creation
************************************************************
*
DEFINE DATA
PARAMETER USING MBCACC41
PARAMETER
1 #ErrorCode  (A3)
LOCAL USING GPRA006N
LOCAL USING ERROR_LN
LOCAL
1 #Payer     (N2)CONST <8>
1 #CC-MODE   (A1/4) CONST <'0','1','2','4'>
1 #CC-LOAN-MODE (A1) CONST <'8'>
1 C@Polices  (I2)
1 #I         (I2)
1 #CCNumber    (A) DYNAMIC
1 #DefaultCard (A) DYNAMIC
*
END-DEFINE
*
#DefaultCard := F-CC-ACCOUNT(<CreditCard.AccountCN>)
SEPARATE #DefaultCard INTO #CCNumber IGNORE WITH DELIMITER '/'
IF #CCNumber = CreditCard.CardNumber
  PARM-CN := CreditCard.AccountCN
  CALLNAT 'GPRN006N' PARM-CN PARM-MEMBER-CN PARM-POLICIES(*) #Payer
  C@Polices := *OCCURRENCE(PARM-POLICIES.ID-NUMBER)
  FOR #I = 1 TO C@Polices
    IF (#CC-MODE(*) = SCAN(PARM-POLICIES.POLICY-MODE(#I)) OR PARM-POLICIES.LOAN-PAY-MODE(#I,*) = #CC-LOAN-MODE)
      AND PARM-POLICIES.PAYER-CN(#I) = CreditCard.AccountCN
      #ErrorCode := #USED-FOR-RECURRING-PAYMENTS
      ESCAPE MODULE
    END-IF
  END-FOR
END-IF
*
END
