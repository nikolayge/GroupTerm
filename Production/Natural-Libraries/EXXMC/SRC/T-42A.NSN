* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* find files (work files and reports) in ESP scripts using full or partial name
* Crated : Boris Zaslaskiy 10-07-2011
* ___________________________________________________

 DEFINE DATA
 PARAMETER
 1 p_T-42A  
  2 p_File (A)  DYNAMIC
  2 p_WF   (A)  DYNAMIC
  2 p_RP   (A)  DYNAMIC
  2 p_Scan (A)  DYNAMIC
  2 p_Out  (A/*) DYNAMIC
 LOCAL
*
 1 ESP-SCL-DET VIEW OF ESP-SCL-DET
   2 SCL-USER (A8)
   2 SCL-NAME (A8)
   2 SCL-STEP-NO (N4.0)
   2 C*PARAMETERS (N3)              /*   */
   2 PARAMETERS (A80/1:90)
*
 1 TEXT-V  VIEW OF ESP-SCL-DET-TEXT
  2 SCL-USER                      (A8)
  2 SCL-NAME                      (A8)
  2 SCL-STEP-NO                   (N4.0)
  2 WF-NO                         (N2.0)
  2 WF-NAME                       (A50)
  2 WF-DISP                       (A1)
  2 REP-NO                        (N2.0)
  2 REPORTS(1)                        
   3 REPORT-NAME                  (A50)

*
     
 1 #i    (I2)
 1 #k    (I2)
 1 v_A   (A)    DYNAMIC
 1 v_A4  (A4)
 1 v_A4_1  (A4)
 1 v_A2  (A2)
 1 v_A50 (A50)
 1 #x    (I2)
 1 b@    (A4) INIT <'<b>'>
 1 b/    (A5) INIT <'</b>'>
 1 v_X   (A40/2) INIT <'<span style="background-color:#DBFFFF">',
                       '<span style="background-color:#FFFFDB">'>
 1 #j    (N1)
 1 v_ToUpperCase   (A) DYNAMIC
 END-DEFINE
 p_File := *TRANSLATE(p_File, UPPER)
 #k := 5
*  v_A := '<pre>'
*  PERFORM sbr_ADD
 COMPRESS FULL b@ "Type:    File Name {disp}                                : User     : Name     : Step : f#" b/ INTO v_A
          PERFORM sbr_ADD
 COMPRESS FULL b@ "----:----------------------------------------------------:----------:----------:------:---" b/ INTO v_A
          PERFORM sbr_ADD          
 READ TEXT-V
   IF p_WF = "Y"
       v_ToUpperCase := *TRANSLATE(TEXT-V.WF-NAME, UPPER)
       IF   ( ( p_Scan <> "Y" ) AND ( v_ToUpperCase = p_File ))
         OR ( ( p_Scan =  "Y" ) AND ( v_ToUpperCase = SCAN p_File ))

          COMPRESS TEXT-V.WF-NAME ' {' TEXT-V.WF-DISP '}' INTO v_A50 LEAVING NO SPACE
          MOVE EDITED TEXT-V.SCL-STEP-NO (EM=ZZZ9) TO v_A4
          MOVE EDITED TEXT-V.WF-NO   (EM=Z9) TO v_A2
          #k := #k + 1
          #j := (#k / 2) * 2 - #k + 2
          COMPRESS FULL v_X(#j)
                '<b style="color:#06C">WF  </b>:' v_A50 ":" TEXT-V.SCL-USER  ":" TEXT-V.SCL-NAME  ":" v_A4 ":" v_A2  '</span>' INTO v_A
          PERFORM sbr_ADD
     END-IF
  END-IF  
* ---------------------------------- Reports --------------------------------------  
  IF p_RP = "Y"  
       v_ToUpperCase := *TRANSLATE(TEXT-V.REPORT-NAME(1), UPPER)

       IF   (( p_Scan <> "Y" ) AND ( v_ToUpperCase = p_File ))
         OR (( p_Scan =  "Y" ) AND ( v_ToUpperCase = SCAN p_File ))
         
          MOVE EDITED TEXT-V.SCL-STEP-NO (EM=ZZZ9) TO v_A4
          MOVE EDITED TEXT-V.REP-NO      (EM=Z9) TO v_A2
          #k := #k + 1
          #j := (#k / 2) * 2 - #k + 2
          COMPRESS FULL v_X(#j)
                '<b style="color:#C60">RP  </b>:' TEXT-V.REPORT-NAME(1) ":" TEXT-V.SCL-USER  ":" TEXT-V.SCL-NAME  ":" v_A4 ":" v_A2 '</span>' INTO v_A
         PERFORM sbr_ADD
       END-IF
   
 END-IF
 END-READ
* -------------------------------------Parameters ----------------------------------- 
 COMPRESS FULL b@ "Type:    Parameter value                                                               : User     : Name     : Step" b/ INTO v_A
 PERFORM sbr_ADD
 COMPRESS FULL b@ "----:----------------------------------------------------------------------------------:----------:----------:------" b/ INTO v_A
 PERFORM sbr_ADD 
 
 READ ESP-SCL-DET
  FOR #i = 1 TO C*PARAMETERS
       v_ToUpperCase := *TRANSLATE(ESP-SCL-DET.PARAMETERS(#i), UPPER)
       IF   (( p_Scan <> "Y" ) AND ( v_ToUpperCase = p_File ))
         OR (( p_Scan =  "Y" ) AND ( v_ToUpperCase = SCAN p_File ))
          MOVE EDITED ESP-SCL-DET.SCL-STEP-NO (EM=9999) TO v_A4
          #k := #k + 1
          #j := (#k / 2) * 2 - #k + 2
          COMPRESS FULL v_X(#j)
            '<b style="color:#4C4">PARM</b>:' PARAMETERS(#i) ":" ESP-SCL-DET.SCL-USER  ":" ESP-SCL-DET.SCL-NAME  ":" v_A4 '</span>' INTO v_A
         PERFORM sbr_ADD
       END-IF
   END-FOR
 
 END-READ
*  v_A := '</pre>'
*  PERFORM sbr_ADD

* ---------------------------------- ADD --------------------------------------   
 DEFINE sbr_ADD 
   #x:= #x + 1
   EXPAND ARRAY p_Out TO (1:#x) 
   MOVE EDITED #x (EM=ZZ9')') TO v_A4_1
   COMPRESS v_A4_1 v_A INTO p_Out(#x)
   RESET v_A
 END-SUBROUTINE 
END
