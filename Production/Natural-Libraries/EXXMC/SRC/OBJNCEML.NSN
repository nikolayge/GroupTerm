* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* A-CONTACT-EMAIL - Object Module
* OBJNCEML - Subprogram to perform A-CONTACT-EMAIL
* access functions.
*
***********************************************
* MODIFICATION LOG
***********************************************
* USER   DATE      TAG  REASON
* YAK    09222010  YAK  Initial Creation
* YAK    08202014  YK1  Save email addresses in lower case
*                       to be used for search 
***********************************************
*
DEFINE DATA
PARAMETER USING OBJACEML
*
LOCAL USING OBJLCEML
LOCAL USING ERROR_L
LOCAL
1 #EMAIL               (A) DYNAMIC CONST <'EMAILID'>
1 #SOURCE-AUC          (A) DYNAMIC CONST <'U'>
END-DEFINE
*
* Main processing
* YK1 - Convert email address to lower case
PARM-EMAIL.EMAIL-ADDRESS := *TRANSLATE(PARM-EMAIL.EMAIL-ADDRESS,LOWER)
*
DECIDE ON FIRST VALUE #ACTION
  VALUE 'ADD'
    PERFORM ADD-RECORD
  VALUE 'UPDATE'
    IF ISN = 0
      PERFORM UPDATE-RECORD-BY-KEY
    ELSE
      PERFORM UPDATE-RECORD-BY-ISN
    END-IF
  VALUE 'DELETE'
    IF ISN = 0
      PERFORM DELETE-RECORD-BY-KEY
    ELSE
      PERFORM DELETE-RECORD-BY-ISN
    END-IF
  VALUE 'GET'
    PERFORM GET-RECORD
  VALUE 'GET-PREFERRED'
    PERFORM GET-PREFERRED-RECORD
  VALUE 'RESET-PREFERRED'
    PERFORM RESET-PREFERRED-IND
  VALUE 'HIST-KEY'                        /* Histogram - check existence
    PERFORM HIST-KEY                      /* get *number
  NONE
    IGNORE
END-DECIDE
*
****************************
DEFINE SUBROUTINE ADD-RECORD
****************************
*
MOVE BY NAME PARM-EMAIL TO EMAIL-V
* Get next pool id
EMAIL-V.EMAIL-ID := F-GET-NEXT-ID(<#EMAIL>)
* WRITE WORK FILE #LOG VARIABLE 'ADD EMAIL ID = ' EMAIL-V.EMAIL-ID
STORE EMAIL-V
PARM-EMAIL.EMAIL-ID := EMAIL-V.EMAIL-ID
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-ISN
**************************************
*
GT-U. GET EMAIL-V ISN
IF PARM-EMAIL.EMAIL-SOURCE = #SOURCE-AUC
  MOVE BY NAME PARM-EMAIL TO EMAIL-V
ELSE
* Do not use MOVE BY NAME - only certain fields can be updated from Member Center
  EMAIL-V.EMAIL-ADDRESS        := PARM-EMAIL.EMAIL-ADDRESS
  EMAIL-V.EMAIL-UNDELIV        := PARM-EMAIL.EMAIL-UNDELIV
  EMAIL-V.EMAIL-PREFERRED-IND  := PARM-EMAIL.EMAIL-PREFERRED-IND
  EMAIL-V.EMAIL-SOURCE         := PARM-EMAIL.EMAIL-SOURCE
  EMAIL-V.REMARKS(*)           := PARM-EMAIL.REMARKS(*)
  EMAIL-V.LAST-DATE-UPD        := PARM-EMAIL.LAST-DATE-UPD
  EMAIL-V.LAST-TIME-UPD        := PARM-EMAIL.LAST-TIME-UPD
  EMAIL-V.LAST-USER-UPD        := PARM-EMAIL.LAST-USER-UPD
END-IF
UPDATE (GT-U.)
* WRITE WORK FILE #LOG VARIABLE 'UPDATE EMAIL ID = ' EMAIL-V.EMAIL-ID
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE DELETE-RECORD-BY-ISN
**************************************
ON ERROR
  IF *ERROR-NR = 3113
    #RETURN-CODE := #ISN-DOES-NOT-EXIST
    ESCAPE ROUTINE
  END-IF
END-ERROR
*
GT-D. GET EMAIL-V ISN
DELETE (GT-D.)
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE DELETE-RECORD-BY-KEY
**************************************
*
FIND-D.
FIND(1) EMAIL-V WITH EMAIL-ID = PARM-EMAIL.EMAIL-ID
  DELETE (FIND-D.)
* WRITE WORK FILE #LOG VARIABLE 'DELETE EMAIL ID = ' EMAIL-V.EMAIL-ID
END-FIND
*
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE UPDATE-RECORD-BY-KEY
**************************************
*
FIND-U.
FIND(1) EMAIL-V WITH EMAIL-ID = PARM-EMAIL.EMAIL-ID
  IF PARM-EMAIL.EMAIL-SOURCE = #SOURCE-AUC
    MOVE BY NAME PARM-EMAIL TO EMAIL-V
  ELSE
* Do not use MOVE BY NAME - only certain fields can be updated from Member Center
    EMAIL-V.EMAIL-ADDRESS        := PARM-EMAIL.EMAIL-ADDRESS
    EMAIL-V.EMAIL-UNDELIV        := PARM-EMAIL.EMAIL-UNDELIV
    EMAIL-V.EMAIL-PREFERRED-IND  := PARM-EMAIL.EMAIL-PREFERRED-IND
    EMAIL-V.EMAIL-SOURCE         := PARM-EMAIL.EMAIL-SOURCE
    EMAIL-V.REMARKS(*)           := PARM-EMAIL.REMARKS(*)
    EMAIL-V.LAST-DATE-UPD        := PARM-EMAIL.LAST-DATE-UPD
    EMAIL-V.LAST-TIME-UPD        := PARM-EMAIL.LAST-TIME-UPD
    EMAIL-V.LAST-USER-UPD        := PARM-EMAIL.LAST-USER-UPD
  END-IF
  UPDATE (FIND-U.)
* WRITE WORK FILE #LOG VARIABLE 'UPDATE EMAIL ID = ' EMAIL-V.EMAIL-ID
END-FIND
*
END-SUBROUTINE
*
**************************
DEFINE SUBROUTINE HIST-KEY
**************************
*
FIND NUMBER EMAIL-V WITH CONTACT-ID = PARM-EMAIL.CONTACT-ID
IF *NUMBER = 0
  #RETURN-CODE := #EMAIL-CONTACT-ID-NOT-FOUND
ELSE
  #NUMBER-OF-RECORDS := *NUMBER
END-IF
*
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE GET-RECORD
****************************
*
FIND(1) EMAIL-V WITH EMAIL-ID = PARM-EMAIL.EMAIL-ID
  IF NO RECORDS FOUND
    PARM-EMAIL.#RETURN-CODE := #EMAIL-ID-NOT-FOUND
  END-NOREC
  ISN    := *ISN
  MOVE BY NAME EMAIL-V TO PARM-EMAIL
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE GET-PREFERRED-RECORD
**************************************
*
FIND EMAIL-V WITH CONTACT-ID = PARM-EMAIL.CONTACT-ID
  IF NO RECORDS FOUND
    PARM-EMAIL.#RETURN-CODE := #EMAIL-CONTACT-ID-NOT-FOUND
  END-NOREC
  ACCEPT IF EMAIL-V.EMAIL-PREFERRED-IND = PARM-EMAIL.EMAIL-PREFERRED-IND
  ISN    := *ISN
  MOVE BY NAME EMAIL-V TO PARM-EMAIL
  ESCAPE ROUTINE
END-FIND
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE RESET-PREFERRED-IND
*************************************
*
FIND-R.
FIND(1) EMAIL-V WITH EMAIL-ID = PARM-EMAIL.EMAIL-ID
  EMAIL-V.EMAIL-PREFERRED-IND := PARM-EMAIL.EMAIL-PREFERRED-IND
  UPDATE (FIND-R.)
END-FIND
END-SUBROUTINE
END
