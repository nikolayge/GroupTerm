* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* T-XNAME Interface to T-ANAME
* BZ 07/12/2017
* Interpret requested function
* Validate Input Name and CN/CI
* Return request string for testing
DEFINE DATA PARAMETER
 1 r_ID            (A) Dynamic
 1 r_IN            (A) Dynamic
 1 r_OUT           (A) Dynamic
LOCAL
 1 p_TN
   2 p_CN      (N8)
   2 p_Ex_Name (A)   DYNAMIC 
   2 p_Mod     (A)   DYNAMIC
   2 p_Opt     (A)   DYNAMIC
   2 Text      (A/*) DYNAMIC

 1 #x          (N5) 
 1 #i          (I2)
 1 CN_A         (A) DYNAMIC 
 1 br           (A6) INIT <'<br />'>
*
 1 Vers          (A) DYNAMIC INIT <'[ Ver 3.2 07/31/2017 ]'> 
END-DEFINE
 
 SEPARATE r_IN INTO CN_A p_Ex_Name p_Opt WITH DELIMITER ':'
 p_Ex_Name := *TRIM(p_Ex_Name)
 CN_A := *TRIM(CN_A)
 p_Opt := *TRIM(p_Opt)
 IF CN_A = '999999' OR p_Ex_Name = '999999'
   COMPRESS r_In Vers INTO r_Out 
   ESCAPE ROUTINE
 END-IF
 DECIDE FOR FIRST CONDITION
    WHEN r_ID = SCAN("Name_Get") 
         PERFORM sbr_Test_Name 
         p_Mod := 'Get'
    WHEN r_ID = SCAN("Name_CN_Get")
         PERFORM sbr_Test_CN
         p_Mod := 'Get_CN'
    WHEN r_ID = SCAN("Name_Upd")
         p_Mod := 'Upd'
         PERFORM sbr_Test_Name
    WHEN r_ID = SCAN("Name_CN_Upd")
         PERFORM sbr_Test_CN 
         PERFORM sbr_Test_Name 
         p_Mod := 'Upd_CN' 
    WHEN r_ID = SCAN("Name_CI_Upd")
         PERFORM sbr_Test_CI 
         PERFORM sbr_Test_Name 
         p_Mod := 'Upd_CI' 
    WHEN r_ID = SCAN("Name_View")
         p_Mod := 'View'
    WHEN ANY CALLNAT 'T-ANAME' p_TN
    WHEN NONE 
     COMPRESS 'Unknown request' r_ID INTO r_OUT
     ESCAPE ROUTINE
 END-DECIDE

  IF *OCCURRENCE(TEXT)<> 0
    #x := *UBOUND(TEXT(*))
    r_OUT := '<pre>'
    FOR #i = 1 TO #x
      COMPRESS r_OUT br TEXT(#i)  INTO r_OUT
*      COMPRESS r_OUT br TEXT(#i) '[' *LENGTH(TEXT(#i)) ']'  INTO r_OUT
    END-FOR
     COMPRESS r_OUT '</pre>' INTO r_OUT
  ELSE 
    COMPRESS 'Result is blank for' '[' p_Ex_Name ']' 'CN:' '[' p_CN ']' r_ID INTO r_OUT
  END-IF
* --------------------------------------------------------
DEFINE sbr_Test_CN
*      -----------
  IF NOT CN_A IS (N6)
     r_OUT:= 'CN format should be NNNNNN'
     ESCAPE MODULE
  END-IF  
  p_CN := val(CN_A)
  IF p_CN = 0
     r_OUT:= 'CN is 0'
     ESCAPE MODULE
  END-IF  
END-SUBROUTINE
* --------------------------------------------------------
DEFINE sbr_Test_CI
*      -----------
  IF NOT CN_A IS (N8)
     r_OUT:= 'CI format should be NNNNNNNN'
     ESCAPE MODULE
  END-IF  
  p_CN := val(CN_A)
  IF p_CN = 0
     r_OUT:= 'CI is 0'
     ESCAPE MODULE
  END-IF  
END-SUBROUTINE
* --------------------------------------------------------
DEFINE sbr_Test_Name
*      -------------
  IF p_Ex_Name = ' '
     IF p_Opt <> 'Del' OR p_Mod = 'Upd'
       r_OUT:= 'Name is blank'
       ESCAPE MODULE
     END-IF  
  END-IF   
END-SUBROUTINE
END
