* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID F-DEF-PR   Defaul Prefix 
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20110616   None    Program written.                            *
* YAK   20110810           Clone for Member Center                     *
*                          remove extra logic - no file access needed  * 
*                                                                      *
* 1. If Gender CD (Contact) is "F". and Intro Flag (Status) is "S",
*    then Prefix (Contact) should be "MRS"
* 2. If Gender CD (Contact) is "M" then Prefix (Contact) should be "MR"
* 3. If Gender CD (Contact) is "F". and Date of Marriage (Status) is not <blank>
*    then Prefix (Contact) should be "MRS"
* 4. If Gender CD (Contact) is "F"
*    IF age (Contact) is less than 16 years then Prefix (Contact) should be "MISS"
*    ELSE then Prefix (Contact) should be "MS"
************************************************************************
*
DEFINE FUNCTION F-DEF-PREFIX RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #MEMBER-CONTACT-ID  (N8)
  1 #REL                (A1)
  1 #LAST-NAME          (A25)
  1 #GENDER-CD          (A1)
  1 #DATE-OF-BIRTH      (N8)
  LOCAL
  1 SPOUSE VIEW OF A-CONTACTS
    2 LAST-NAME
  1 #AGE (N8)
  1 #16-YEARS-OLD (N6) CONSTANT<160000>
  END-DEFINE
*
RESET F-DEF-PREFIX
DECIDE ON FIRST VALUE OF #GENDER-CD
  VALUE 'M'
    F-DEF-PREFIX := 'MR'
  VALUE 'F'
    IF #DATE-OF-BIRTH > 0
      #AGE := *DATN - #DATE-OF-BIRTH
    END-IF
    DECIDE FOR FIRST CONDITION
      WHEN #REL = 'S'
        FIND (1) SPOUSE WITH CONTACT-ID = #MEMBER-CONTACT-ID
        END-FIND
        IF SPOUSE.LAST-NAME = #LAST-NAME
          F-DEF-PREFIX := 'MRS'
        ELSE
          F-DEF-PREFIX := 'MS'
        END-IF
      WHEN #REL = 'X'
        F-DEF-PREFIX := 'MS'
      WHEN #AGE > 0 AND #AGE < #16-YEARS-OLD
        F-DEF-PREFIX := 'MISS'
      WHEN NONE
        F-DEF-PREFIX := 'MS'
    END-DECIDE
  NONE VALUE IGNORE
END-DECIDE
END-FUNCTION
END
