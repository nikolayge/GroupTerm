* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online
* Get calculated values for policy
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    11122010       Initial Creation
* YAK    03242011  YK1  Adjust death benefit calculation
*                       Get this value from "CVALUSXX"
* YAK    03242011  YK2  Adjust cash value calculation
*                       Get this value from "CVALUSXX"
********************************************************
*
DEFINE DATA
PARAMETER USING MBCA0072
LOCAL USING OBJLPINF
LOCAL
1 #SUFF                   (A3)
* 1 #CSVALU               (N7.2/9)
1 #CVALUE                 (N7.2/9)
1 #LVALUE                 (N7.2/9)
1 #MCSV                   (N7.2/9)
1 #ANN-PREMIUM            (N7.2/9)
1 #TPP                    (N7.2/9)
1 #FACE-AMT               (N7.2/9)
*
1 #DAILY-INT              (N7.2)
1 #DATN                   (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY  (N4)
  2 #DATN-MM    (N2)
  2 #DATN-DD    (N2)
1 REDEFINE #DATN
  2 #DAT-A      (A8)  
1 #DATX-1       (D)  
1 #DATX-2       (D)  
1 #DIFF         (N3)
END-DEFINE
*
* Calculate estimated loan interest at loan anniversary
IF #DATE-N <> 0
  PERFORM ESTIMATE-FUTURE-INTEREST
  ESCAPE ROUTINE
END-IF
*
CALLNAT 'CVALUSXX'
  #ID-NUMBER
  #SUFF
  #DATN
  PDA-CALCULATED-VALUES.#LOAN-BAL   (*)
  PDA-CALCULATED-VALUES.#LOAN-BAL-2 (*)
  PDA-CALCULATED-VALUES.#ACC-INT    (*)
  PDA-CALCULATED-VALUES.#ACC-INT-2  (*)
  PDA-CALCULATED-VALUES.#CURR-CASH-VALUE (*)                         /* YK2
* #CSVALU     (1:9)                                                  /* YK2
  #CVALUE     (1:9)
  #LVALUE     (1:9)
  #MCSV       (1:9)
  PDA-CALCULATED-VALUES.#CURR-DEATH-BENEFIT (*)                      /* YK1
* #DB         (1:9)                                                  /* YK1
  #ANN-PREMIUM  (1:9)
  #FACE-AMOUNT (1:9)
  #TPP        (1:9)
*
******************************************
DEFINE SUBROUTINE ESTIMATE-FUTURE-INTEREST
******************************************
*
#DATN := #DATE-N
#DATX-1 := *DATX
MOVE EDITED #DAT-A TO #DATX-2 (EM=YYYYMMDD)
#DIFF := #DATX-2 - #DATX-1
COMPUTE ROUNDED #DAILY-INT = #X-LOAN-AMOUNT * (#X-INTEREST-RATE / 365) / 100
DECIDE FOR FIRST CONDITION
  WHEN #X-LOAN-AMOUNT = 0
    #INTEREST := #X-ACCUM-INTEREST + #X-NXT-YR-ACCUM-INT
  WHEN NONE
    COMPUTE ROUNDED #INTEREST = (#DIFF * #DAILY-INT) + #X-ACCUM-INTEREST + #X-NXT-YR-ACCUM-INT
END-DECIDE
*
END-SUBROUTINE
END
