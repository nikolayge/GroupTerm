* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Report records on Hold
* ______________________________________________________________________________

DEFINE DATA
PARAMETER
 1 p_T-51A  
  2 p_Out (A/*) DYNAMIC
 LOCAL
1 TERM-V VIEW OF A-TERMINALS
  2 TERMINAL-ID
  2 ACTIVE-DIRECTORY-ID     (A50)
  2 CLERK-ID                (A3)
  2 LST-CMND                (A2)
  2 LST-CMND-DT             (N8)
  2 LST-CMND-TM             (N7)
  2 LST-CMND-ID             (N6)
  2 INIT-USER               (A8)   
  2 COMMENTS                (A20)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
*  
1 FIR-V VIEW OF A-FIRS
  2 ID-NUMBER
*  
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER
* 
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 MEMBER-CN

1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 CONTACT-ID  

1 RX-V VIEW OF RX-COMM
 2  RX-AT
    
*    
1 v_cmd0              (A)   DYNAMIC 
1 v_cmd1              (A)   DYNAMIC INIT <"adaopr dbid=@DBN di=hq | egrep '(@NODE)|(Hold)' > @FILE1" >
1 v_cmd2              (A)   DYNAMIC INIT <"adarep dbid=@DBN file=@ADAFILE | egrep '(Database)' > @FILE2" >
1 v_Node              (A6)
1 v_DBN               (A3)
1 v_DT                (A10)
1 v_TM                (A8)
1 v_A200              (A200) 
1 v_A100              (A100) 
1 f_Name11              (A)   DYNAMIC INIT <'/usr/SAG/AAFMAA/batch/workfiles/TEMP/wf11_@PID.txt'>
1 f_Name12              (A)   DYNAMIC INIT <'/usr/SAG/AAFMAA/batch/workfiles/TEMP/wf12_@PID.txt'>
1 v_In                (A) DYNAMIC
1 v_In9               (A/9) DYNAMIC
1 v_Login             (A) DYNAMIC
1 #i                  (I2)  
1 #j                  (I2)  
1 nl                  (A4)        CONST  <'<br>'>
*
1 v_A                 (A)  DYNAMIC
1 v_A1                (A)  DYNAMIC
1 #x         (I2)
1 rc  (I4)
1 v_File              (A3)
1 v_ISN               (P7)
END-DEFINE
* _________________________________________________________________________
 v_A1 := *PID
 v_A1 := *TRIM(v_A1)
 EXAMINE f_Name11  FOR '@PID'  REPLACE v_A1
 EXAMINE f_Name12  FOR '@PID'  REPLACE v_A1
 
 COMPRESS "<pre>"  INTO v_A
 PERFORM sbr_ADD                       /* 0
 DEFINE WORK FILE 11 f_Name11 /* ATTRIBUTES 'DELETE'
 v_Node := *HOSTNAME
 v_DBN := GET_DBN(<>)
 EXAMINE v_cmd1 FOR '@FILE1' REPLACE f_Name11
 EXAMINE v_cmd1 FOR '@NODE' REPLACE v_Node
 EXAMINE v_cmd1 FOR '@DBN'  REPLACE v_DBN
 CALL 'shcmd' v_cmd1
 #j := 0
 READ WORK FILE 11 v_A200
   
    #j := #j + 1
    v_In := *TRIM(v_A200)
    IF v_In = SCAN('Database') OR v_In = ' ' 
*      v_A := v_In
*      PERFORM sbr_ADD                       /* 0
      ESCAPE TOP 
    END-IF

   EXAMINE FULL v_In FOR ' ' REPLACE '#'
   REPEAT 
     EXAMINE v_In FOR '##' REPLACE '#' GIVING NUMBER #i
     IF #i = 0 ESCAPE BOTTOM END-IF
   END-REPEAT
  RESET v_In9(*)
  SEPARATE v_In INTO v_In9(*) WITH DELIMITERS '#'
  IF v_In9(8) = ' '  
      v_In9(8) := v_In9(7) 
      v_In9(7) := v_In9(6) 
      v_In9(6) := v_In9(5) 
  END-IF
  IF v_In9(6) <> MASK (99) OR v_In9(6) = ' ' 
    COMPRESS 'Error in parameters:'
      '1:' v_In9(1) '2:' v_In9(2) '3:' v_In9(3) '4:' v_In9(4) 
      '5:' v_In9(5) '6:' v_In9(6) '7:' v_In9(7) '8:' v_In9(8) INTO v_A
    PERFORM sbr_ADD 
    ESCAPE TOP  
  END-IF  
  v_cmd0 := v_cmd2
  DEFINE WORK FILE 12 f_Name12   /* ATTRIBUTES 'DELETE'
  EXAMINE v_cmd0 FOR '@FILE2'   REPLACE f_Name12
  EXAMINE v_cmd0 FOR '@NODE'    REPLACE v_Node
  EXAMINE v_cmd0 FOR '@DBN'     REPLACE v_DBN
  EXAMINE v_cmd0 FOR '@ADAFILE' REPLACE v_In9(6)
*  PRINT v_cmd0
  CALL 'shcmd' v_cmd0
  v_A100 := ' '
  COMPRESS 'test -f' f_Name12 INTO v_cmd0
  CALL 'shcmd' v_cmd0
  rc := RET('shcmd')
  IF rc = 0 
    COMPRESS 'test -s' f_Name12 INTO v_cmd0
    CALL 'shcmd' v_cmd0
    rc := RET('shcmd')
  END-IF
  IF rc = 0 
    READ WORK FILE 12 ONCE v_A100
  ELSE
    COMPRESS 'Error resolving file number [' v_In9(6) ']. Repeat ...' INTO v_A100
  END-IF
  v_A := ' - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -'    
  PERFORM sbr_ADD                              /* 1  
  v_A := v_A100
  EXAMINE v_A FOR '(' DELETE
  EXAMINE FULL v_A FOR '    )' DELETE
*  EXAMINE v_A FOR '      ' DELETE
  PERFORM sbr_ADD                              /* 2  
  COMPRESS 'Id:' v_In9(1) 'Login:' v_In9(3) 'ES Id:' v_In9(4) 'File:' v_In9(6) 'ISN:'  v_In9(7) INTO v_A
  PERFORM sbr_ADD                              
  CLOSE WORK FILE 12
  EXAMINE v_In9(7) FOR ',' DELETE
  IF v_In9(7) IS (N7)
     v_File := *TRIM(v_In9(6))
     v_ISN  := val(v_In9(7))
     DECIDE ON FIRST VALUE OF v_File
       VALUE '27 '
            GET CNT-V v_ISN
            COMPRESS '<b>' 'CONTACTS file,' ' CN:' CNT-V.ID-NUMBER 'CI:' CNT-V.CONTACT-ID '</b>'INTO v_A
            PERFORM sbr_ADD                

       VALUE '38 '
            GET NT-V v_ISN
            COMPRESS '<b>' 'NOTES file,' ' CN:' NT-V.ID-NUMBER '</b>'INTO v_A
            PERFORM sbr_ADD                
        VALUE '40 '
            GET ST-V v_ISN
            COMPRESS '<b>' 'STATUS file,' ' CN:' ST-V.ID-NUMBER 'CI:' MEMBER-CONTACT-ID '</b>' INTO v_A
            PERFORM sbr_ADD
        VALUE '42 '
            GET FIR-V v_ISN
            COMPRESS '<b>' 'FIRS file,' ' CN:' FIR-V.ID-NUMBER '</b>' INTO v_A
            PERFORM sbr_ADD                
       VALUE '46 '
            GET AT-V v_ISN
            COMPRESS '<b>' 'AT file,' ' AT:' AT-V.ID-NUMBER 'CN:' AT-V.MEMBER-CN '</b>' INTO v_A
            PERFORM sbr_ADD                
       VALUE '92 '
            GET RX-V v_ISN
            COMPRESS '<b>' 'RX file,' ' AT:' RX-V.RX-AT  '</b>' INTO v_A
            PERFORM sbr_ADD                
       NONE 
            COMPRESS '-- add file! <' v_File '>' INTO v_A
            PERFORM sbr_ADD           IGNORE
     END-DECIDE
  ELSE 
  COMPRESS '[' v_In9(7) 'is not N7' ']' INTO v_A
  PERFORM sbr_ADD  
  END-IF   
  v_Login := *TRANSLATE (v_In9(3),UPPER)
  FIND(1) TERM-V WITH TERMINAL-ID = v_Login  
    IF NO RECORD FOUND
       COMPRESS 'No TERMINALS records found for' v_Login INTO v_A
       PERFORM sbr_ADD                        /* 3
       ESCAPE BOTTOM
    END-NOREC 
     MOVE EDITED LST-CMND-DT (EM=9999-99-99) TO v_DT
     LST-CMND-TM := LST-CMND-TM / 10
     MOVE EDITED LST-CMND-TM (EM=99:99:99) TO v_TM
     
     COMPRESS 'TERMINALS Record:' nl 
        ' Login:' '<b>' v_In9(3) '</b>' 'Clerk ID:' '<b>' CLERK-ID '</b>' 'Name:' '<b>' COMMENTS '</b>' nl
        ' Command:' '<b>' LST-CMND '</b>' 'Date:' '<b>' v_DT '</b>' 'Time:' '<b>' v_TM '</b>' 'CN/AT:' '<b>' LST-CMND-ID '</b>'
         INTO v_A
         PERFORM sbr_ADD                     /* 4
  END-FIND       
END-WORK
    IF #j < 2 
       v_A :=  'Queue is empty. No Records on Hold' 
       PERFORM sbr_ADD                      /*  5
    END-IF

  COMPRESS "<pre>"    INTO v_A              /* 6
  PERFORM sbr_ADD

DEFINE sbr_ADD
  #x:= #x + 1
  EXPAND ARRAY p_Out TO (1:#x)
  COMPRESS v_A INTO p_Out(#x)
* PRINT NOTITLE '-' v_A                         
END-SUBROUTINE

END
