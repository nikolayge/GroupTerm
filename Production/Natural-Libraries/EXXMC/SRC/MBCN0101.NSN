* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member Center Online
* MBCN0101 - Add external Life Insurance company name to Gen Table
*            when added from Digital Vault Admin Tool
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    07242016       Initial Creation
********************************************************
*
DEFINE DATA PARAMETER
*
1 REQUEST-DATA      (A) DYNAMIC
1 RESPONSE-DATA     (A) DYNAMIC
*
LOCAL USING PARSER-X
LOCAL USING ERROR_L
LOCAL USING OBJLGENT
LOCAL
1 XML_PARSER_XPATH             (A) DYNAMIC
1 XML_PARSER_XPATH_TYPE        (A1)
1 XML_PARSER_CONTENT           (A) DYNAMIC
1 XML_PARSER_CONTENT_IS_EMPTY  (L)
1 XML_PARSER_INPUT   (A) DYNAMIC
1 XML_PARSER_ERROR_TEXT        (A253)
1 XML_PARSER_RESPONSE          (I2)
1 XML          (A) DYNAMIC
1 #ErrorCode   (A3) INIT <'0'>
1 #ErrorDescription (A) DYNAMIC
1 #LookUpKey   (A4) CONST <'EXTL'>
1 #Key         (A7)
1 #ISN         (P9)
*
1 InsuranceCompany
  2 ActionType  (A) DYNAMIC
  2 User        (A) DYNAMIC
  2 CompanyId   (A) DYNAMIC
  2 CompanyName (A) DYNAMIC
*
END-DEFINE
*
XML_PARSER_INPUT := REQUEST-DATA
INCLUDE PARSER_X 'XML_PARSER_INPUT' /* XML file to be parsed
  'XML_PARSER_XPATH'                /* XPATH to represent element...
  'XML_PARSER_XPATH_TYPE'           /* Type of callback
  'XML_PARSER_CONTENT'              /* Content of element found
  'XML_PARSER_CONTENT_IS_EMPTY'     /* Is TRUE if element is empty
  'XML_PARSER_ERROR_TEXT'           /* error Message
  'XML_PARSER_RESPONSE'             /* Error NR; 0 = OK
*
* Call Business Logic
*
DECIDE FOR FIRST CONDITION
  WHEN InsuranceCompany.ActionType = '1'
    PERFORM ADD-COMPANY
  WHEN NONE
    IGNORE
END-DECIDE
*
END TRANSACTION
PERFORM COMPOSE-XML
RESPONSE-DATA := XML
*
************************** CALLBACK HANDLER
DEFINE SUBROUTINE CALLBACK
**************************
*
DECIDE ON FIRST XML_PARSER_XPATH
  VALUE 'InsuranceCompany'
    Ignore
  VALUE 'InsuranceCompany/ActionType/$'
    InsuranceCompany.ActionType := *TRIM(XML_PARSER_CONTENT)
  VALUE 'InsuranceCompany/User/$'
    InsuranceCompany.User := *TRIM(XML_PARSER_CONTENT)
  VALUE 'InsuranceCompany/CompanyID/$'
    InsuranceCompany.CompanyId := *TRIM(XML_PARSER_CONTENT)
  VALUE 'InsuranceCompany/CompanyName/$'
    InsuranceCompany.CompanyName := *TRIM(XML_PARSER_CONTENT)
  NONE VALUE
    IGNORE
END-DECIDE
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE ADD-COMPANY
*****************************
*
COMPRESS #LookUpKey InsuranceCompany.CompanyId INTO #Key LEAVING NO
FIND(1) GEN-TABLE-V WITH SEC-KEY = #Key
  #ISN := *ISN
END-FIND
GEN-SUB-KEY := GEN-ALPHA-VALUE := *TRIM(InsuranceCompany.CompanyId)
GEN-SECURITY-CODE := 99
GEN-DESCRIPTION := ADDL-INFO := *TRIM(InsuranceCompany.CompanyName)
EFFECTIVE-DATE := LAST-DATE-UPDATE := *DATN
END-DATE := 20991231
OWNER-ID := 'YAK'
ALT-OWNER-ID := 'BZ'
LAST-USER-UPDATE := FUNCTION-WEB-USER(<InsuranceCompany.User>)(1)
IF #ISN = 0
  STORE GEN-TABLE-V
ELSE
  GET GEN-TABLE-V #ISN
  UPDATE
END-IF
*
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE COMPOSE-XML
*****************************
*
IF #ErrorCode <> '0'
  #ErrorDescription := F-ERROR-DESCRIPTION(<#ErrorCode>)
END-IF
*
COMPRESS
  '<InsuranceCompany>'
  '<ErrorCode>' #ErrorCode '</ErrorCode>'
  '<ErrorDescription>' #ErrorDescription '</ErrorDescription>'
  '</InsuranceCompany>'
  INTO XML LEAVING NO
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE PARSER_ERROR
******************************
COMPRESS XML_PARSER_RESPONSE XML_PARSER_ERROR_TEXT INTO RESPONSE-DATA
ESCAPE ROUTINE
END-SUBROUTINE
*
END
