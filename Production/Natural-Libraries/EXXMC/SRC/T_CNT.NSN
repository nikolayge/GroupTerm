* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* T_CNT Generic process through EntireX
*
DEFINE DATA PARAMETER
1 r_ID              (A) Dynamic
1 r_IN              (A) Dynamic
1 r_OUT             (A) Dynamic        /* Out
*
LOCAL USING p_PARMS
LOCAL USING USR1025L
LOCAL
1 f_Log             (A) DYNAMIC INIT <"/usr/SAG/tmp/T_LOG.TXT">
1 v_Log             (A2400)
1 nl                (A4)        CONST  <'<br>'>
1 #i                (N1)
1 e3009             (N1)        INIT <0>
END-DEFINE
*
 DEFINE WORK FILE 1 f_LOG TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
 COMPRESS '--' *DATX *TIMX INTO v_Log 
 WRITE WORK 1 VARIABLE v_Log
 ON ERROR
    BACKOUT TRANSACTION
    CALLNAT 'USR2001N' p_PARMS  /* Get the actual error line details
    IF P_ERROR_NUM = 3009 AND e3009 = 0
          e3009 := e3009 + 1
          r_OUT := ' '
          PERFORM sbr_RUN
    ELSE
      COMPRESS '  *' *PROGRAM P_ERROR_PROGRAM P_ERROR_LINE P_ERROR_NUM 'RPC223' INTO v_Log
      WRITE WORK FILE 1 VARIABLE v_Log
      CLOSE WORK FILE 1
      COMPRESS 'Error:' v_Log nl
             r_ID  nl 
             r_IN  nl
             r_OUT  
             INTO r_OUT
    END-IF
    ESCAPE ROUTINE
 END-ERROR
 PERFORM sbr_RUN
* ---------------------------------------------------------------------------------------------------------
DEFINE sbr_RUN
*      -------
 DECIDE FOR EVERY CONDITION
    WHEN r_ID = '?'  COMPRESS *ETID 'Verified' INTO r_Out
    WHEN r_ID = SCAN("info")
       CALLNAT 'USR1025N'  USR1025L
       COMPRESS FULL '1' STEPLIB-ID(1) DBID(1) FNR(1) nl INTO r_OUT
       FOR #i=2 TO 8
          IF STEPLIB-ID(#i) <> ' '
              COMPRESS FULL r_Out #i STEPLIB-ID(#i) DBID(#i) FNR(#i) nl INTO r_OUT
          END-IF
       END-FOR
       COMPRESS "RPC: <b>Name=</b>" *ETID "<b>Id=</b>" *Pid "<b>Parm=</b>" *PARM-USER "<b>Pgm=</b>" *PROGRAM nl 
                "HOST:" *HOSTNAME "User:" *NET-USER  "Nat-ver:" *NATVERS nl 'DB:' GET_DBN(<>)  nl r_OUT INTO r_OUT
    WHEN r_ID = SCAN("CN/CI at glance")
       CALLNAT 'T-X21' r_ID r_IN r_OUT
    WHEN    r_ID = SCAN("CN/CI details")
       CALLNAT 'T-X22' r_ID r_IN r_OUT
    WHEN    r_ID = SCAN("Web Account")
       CALLNAT 'T-X23' r_ID r_IN r_OUT 
    WHEN    r_ID = SCAN("SSN")
       CALLNAT 'T-X24' r_ID r_IN r_OUT 
    WHEN r_ID = SCAN("ESP Programs")
       CALLNAT 'T-X41' r_ID r_IN r_OUT
    WHEN r_ID = SCAN("ESP Files")
       CALLNAT 'T-X42' r_ID r_IN r_OUT  
    WHEN r_ID = SCAN("ESP Script")
       CALLNAT 'T-X43' r_ID r_IN r_OUT             
    WHEN    r_ID = SCAN("Command")
       CALLNAT 'T-X31' r_ID r_IN r_OUT
    WHEN    r_ID = SCAN("Hold Queue")
       CALLNAT 'T-X51' r_ID r_IN r_OUT   
* Apps
    WHEN    r_ID = MASK('Name')
        CALLNAT 'T-XNAME' r_ID r_IN r_OUT
    WHEN    r_ID = SCAN("Get W4")
       CALLNAT 'T-XW4' r_ID r_IN r_OUT
    WHEN r_ID = SCAN("ap4AT")
      CALLNAT 'T-XAP4AT' r_ID r_IN r_OUT
* EXCEL
    WHEN    r_ID = "Excel"
       CALLNAT 'T-X61' r_ID r_IN r_OUT          
    WHEN NONE COMPRESS 'Error:' r_ID  "is unknown command" INTO r_OUT
  END-DECIDE 
 COMPRESS '  ' r_ID r_IN r_OUT INTO v_Log
 WRITE WORK FILE 1 VARIABLE v_Log
 CLOSE WORK FILE 1
END-SUBROUTINE 
END
