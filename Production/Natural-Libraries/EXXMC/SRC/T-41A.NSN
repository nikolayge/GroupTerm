* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* find scripts using specified program 
* Crated : Boris Zaslaskiy 10-07-2009 
* ___________________________________________________ 
 
DEFINE DATA 
PARAMETER  
 
1 p_T-41A  
  2 p_PGM   (A8)
  2 p_USER  (A8)
  2 p_Out   (A/*) DYNAMIC 
   
 LOCAL 
 1 ESD VIEW OF ESP-SCL-DET 
   2 SCL-USER (A8) 
   2 SCL-NAME (A8) 
   2 SCL-TYPE (A8) 
   2 SCL-STEP-NO (N4.0) 
   2 STEP-NAME (A8) 
   2 LIBRARY (A8) 
   2 PROGRAM (A8)
   
 1 ES VIEW OF ESP-SCL
   2 SCL-USER (A8)  /* D
   2 SCL-NAME (A8)  /* D
   2 SCL-TYPE (A8)
   2 SCL-DESCRIPTION (A50)
   2 JOB-CLASS (A1)
   2 JOB-DISP (A1)
   2 COND-CODE (N4.0)
   2 COND-CODE-TEST (A2)
   2 UPDATE-ON (A1)
   2 AUDIT-USER-ID (A8)
   2 AUDIT-TIMESTAMP (T)    
1 v_A        (A)  DYNAMIC 
1 v_A1        (A)  DYNAMIC
1 v_A2        (A)  DYNAMIC 
1 #x         (I2) 
END-DEFINE 

p_PGM  := *TRANSLATE(p_PGM, UPPER)
p_USER := *TRANSLATE(p_USER, UPPER)
R.
READ ESD WHERE PROGRAM = p_PGM
   IF p_USER <> ' ' AND p_USER <>  SCL-USER ESCAPE TOP END-IF
   FIND (1) ES WITH ES.SCL-NAME = ESD.SCL-NAME
   WHERE ES.SCL-USER = ESD.SCL-USER
     IF NO RECORD FOUND
        v_A1 := "** No ESP-SCL **"
        ESCAPE BOTTOM
     END-NOREC
     COMPRESS '[' ES.SCL-USER ',' ES.SCL-NAME ',' ES.SCL-TYPE ']' INTO v_A2 LEAVING NO SPACE
     MOVE EDITED AUDIT-TIMESTAMP (EM=MM/DD/YYYY) TO v_A1 
     COMPRESS AUDIT-USER-ID v_A1 SCL-DESCRIPTION INTO v_A1
   END-FIND
   COMPRESS FULL "Pgm:" p_PGM  " User:" ESD.SCL-USER  " Name:" ESD.SCL-NAME  " Step N:" ESD.SCL-STEP-NO v_A1 v_A2 INTO v_A
   PERFORM sbr_ADD 
END-READ
IF *COUNTER(R.) = 0 
   COMPRESS FULL "Pgm:" p_PGM  "was not found" INTO v_A 
   PERFORM sbr_ADD 
END-IF
 
DEFINE sbr_ADD 
  #x:= #x + 1 
  EXPAND ARRAY p_Out TO (1:#x) 
  COMPRESS v_A INTO p_Out(#x)                        
END-SUBROUTINE 
END 
