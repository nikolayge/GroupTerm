* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: T-22A
* Get contact details
* CN 151501     
* CI     377442 
DEFINE DATA
PARAMETER
1 p_T-21 
  2 p_CN (N6)
  2 p_CI (N8)
  2 p_Out  (A/*) DYNAMIC
LOCAL
* ----------------------------------------------- Contact
1 CNT-V VIEW OF A-CONTACTS
  2  CONTACT-ID                        (N8.0)   /*  N D 
  2  CONTACT-STATUS                    (A1)     /*  F   Such as B,D, H,I,J
  2  CONTACT-TYPE                      (A1)     /*  N   
  2  ID-NUMBER                         (N6.0)   /*  D 
  2  RANK-ID                           (N6.0)   /*  N D
  2 MILITARY-STATUS
  2  FULL-NAME                                     
   3  FIRST-NAME                        (A25)    /*  N   
   3  MIDDLE-NAME                       (A25)    /*  N   
   3  LAST-NAME                         (A25)    /*  N   
   3  SUFFIX                            (A10)    /*  N   
  2  GENDER-CD                         (A1)     /*  N   
  2  DATE-OF-BIRTH                     (N8.0)   /*  N D 
  2  SSN                               (N9.0)   /*  N D 
  2  DATE-OF-DEATH                     (N8.0)   /*  N D 
  2  BIRTH-CERT-CODE                   (A1)     /*  N   
  2  DEATH-CERT-CODE                   (A1)     /*  N  
  2  FIRM-ORGANIZATION-NAME            (A75)    /*  N D 
   2 LAST-USER-UPD       
   2 LAST-DATE-UPD   
*
1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 REDEFINE ID-NUMBER
       3 CN-A1           (A1)
       3 CN_A5           (A5)
    2 MEMBER-CONTACT-ID
    2 STATUS-CODE
    2 NAME
    2 SSN
    2 DATE-OF-DEATH
    2 DATE-OF-MARRIAGE
    2 DATE-RETIRED
    2 RANK             
    2 MILITARY-STATUS   
    2 MILITARY-SERVICE
    2 SPOUSE-CONTACT-ID
    2 STATUS-CODE-SP
    2 NAME-SP     
    2 SSN-SP
    2 DATE-OF-DIVORCE
    2 FORMER-SPOUSE-CONTACT-ID
    2 FORMER-SPOUSE-NAME
    2 FORMER-SPOUSE-STATUS        
    2 FORMER-SPOUSE-DATE-OF-MARRIAGE   
    2 FORMER-SPOUSE-DATE-OF-DIVORCE  
    2 FORMER-SPOUSE-SSN       
    2 FORMER-HUSBAND-CONTACT-ID
    2 C*LINKED-CN
    2 LINKED-CN (N6/30)
    2 PROCESS-IND
    2 INTRO-FLAG
    2 MEMBER-FLAG
    2 WIDOW-FLAG
    2 DUES-BALANCE
    2 NUMBER-MEMBERSHIPS
    2 SUSPEND-CODE
    2 C*ACTIVE-SEGMENTS
    2 ACTIVE-SEGMENTS (9)      
      3  STATUS                (A1)
      3  PLAN                  (A3)
      3  PAY-STATUS            (A2)
      3  MODE                  (A1)
      3  ISSUE-DATE            (N8)
      3  REDEFINE ISSUE-DATE
         4 ISSUE-DATE_A        (A8) 
      3  POLICY-ID             (A16)        
      3  PAID-UP-DATE          (N8)   
      3  PAID-TO-DATE          (N8)
      3  FACE-AMOUNT           (P7.2)
      3  CASH-VALUE            (P7.2)
      3  DEATH-BENEFIT         (N7.2)
      3  NXT-MON-DEATH-BENEFIT (N7.2)
      3  X-LOAN-AMOUNT         (P5.2)
      3  X-LOAN-AMOUNT-2       (P7.2)
      3  NUM-MBRSHIPS       
      3 CONV-DURATION         (N3)
*
1 STL-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 MEMBER-CONTACT-ID
    2 SPOUSE-CONTACT-ID
    2 STATUS-CODE-SP
    2 FORMER-SPOUSE-CONTACT-ID
    2 FORMER-HUSBAND-CONTACT-ID
    2 C*LINKED-CN
    2 LINKED-CN (N6/30)
    2 PROCESS-IND
    2 INTRO-FLAG
    2 MEMBER-FLAG
    2 WIDOW-FLAG
*    
 1 O9-V VIEW OF OVER-9-LINKS
  2 PRIME-CN         (N6)
  2 C*SECONDARY-CN   
  2 SECONDARY-CN     (N6/40)
        
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS
   2 CONTACT-1-ID    
   2 ROLE-1-CD       
   2 CONTACT-2-ID    
   2 ROLE-2-CD
   2 LAST-USER-UPD       
   2 LAST-DATE-UPD       
*************
 1 PH-V  VIEW OF A-CONTACT-PHONE
    2 PHONE-ID
    2 CONTACT-ID
*
  1 EML-V VIEW of A-CONTACT-EMAIL
    2 EMAIL-ID   
    2 CONTACT-ID   
*
 1 AR-V VIEW OF A-ADDRESS-RELATIONSHIP
   2 CONTACT-ID        (N8)
   2 ADDRESS-POOL-ID   (N8)
*
 1 POOL-V VIEW OF A-ADDRESS-POOL
   2 ADDRESS-POOL-ID   (N8)
*
 1 FIR-V VIEW OF A-FIRS
  2 ID-NUMBER
  2 MEMBER-FLAG
  2 WIDOW-FLAG
  2 C*DEPENDENT-TABLE 
  2 DEPENDENT-TABLE (20)
    3 DEPN-CONTACT-ID
    3 DEPN-NAME        
    3 DEPN-SSN            
    3 DEPN-DATE-OF-BIRTH  
    3 DEPN-STATUS-CD   
    3 DEPN-BIRTH-CD    
    3 DEPN-REMARKS     
*
 1 SMP-V VIEW OF A-SVC-MEMBER
  2 SVC-ID-NUMBER      (N6)    
*
1 POL-V VIEW OF A-POLICY-CONTACTS
   2 CONTACT-ID 
   2 POLICY-NUMBER           (A16)  /* D
   2 RELATION                (N2) 
   2 REFERENCE-NUMBER        (N6)
*
1 rel(A11/0:11) INIT <'Code is 0', /*  0
                    'Insured',         /*  1
                    'Owner Prime',     /*  2
                    'Member',          /*  3  
                    'Owner Scs',       /*  4
                    'Bene Prime',      /*  5 
                    'Bene Unf',        /*  6 
                    'Bene Ctg',       /*  7 
                    'Payor',           /*  8
                    'Linked',          /*  9
                    'Owner Co',        /*  10
                    'Bene Ctg U'>      /* 11
*                    
1 v_Bene_Pol (A)   DYNAMIC
1 bene_CI    (N8)
1 c_Owner    (N2)
1 v_Owners   (A)   DYNAMIC 
1 Owner_CN   (N6)
*
1 v_POOL-ID  (N8)
1  dm               (A1)    INIT <H'09'>
*
1 #i         (I2)
1 #j         (I2)
1 #k         (I2)
1 #x         (I2)
1 #indx      (N1)
1 #indx#j     (N1)
1 #num       (N1)
1 v_A        (A)  DYNAMIC
1 v_AA       (A)  DYNAMIC
1 v_AAA      (A)  DYNAMIC
1 v_A4       (A4)
1 v_A8_1       (A8)
1 v_A8_2       (A8)
1 v_A10        (A10)
1 v_A12        (A12)  
1 v_A16        (A16)
END-DEFINE
* ON ERROR
*   COMPRESS "CN:" ST-V.ID-NUMBER "Pgm:" *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR INTO v_A
*   PERFORM sbr_ADD
*   ESCAPE ROUTINE
* END-ERROR
* *
 v_A := '<pre>' 
 PERFORM sbr_ADD

 IF p_CN <> 0
    FST.
    FIND NUMBER ST-V WITH ID-NUMBER = p_CN
    IF *NUMBER(FST.) > 1
       COMPRESS '<b><font color="red">(!)</font></b>' 'Status CN=' p_CN '<b><font color="red">has '*NUMBER(FST.)' duplicate</font></b>' INTO v_A
       PERFORM sbr_ADD
    END-IF
    FIND  ST-V WITH ID-NUMBER = p_CN
         IF NO RECORD
           COMPRESS p_CN 'No Status' INTO v_A
           PERFORM sbr_ADD
           ESCAPE BOTTOM
         END-NOREC
       COMPRESS 
                '<b style="color:#00F">STATUS  Mem</b> CN :' ST-V.ID-NUMBER 'CI:' MEMBER-CONTACT-ID 'isn:' *ISN INTO v_A
       COMPRESS  'Flags:' 'm='  MEMBER-FLAG 
                          ',w=' WIDOW-FLAG 
                          ',pi=' PROCESS-IND 
                          ',if=' INTRO-FLAG 
                          ',#=' NUMBER-MEMBERSHIPS
                          ',sus=' SUSPEND-CODE
          INTO v_AA LEAVING NO SPACE 
        COMPRESS v_A v_AA INTO V_A
        PERFORM sbr_ADD
        v_A := '                                       <b>   SSN     : DoD/Divorce: contact DoB</b>'
        PERFORM sbr_ADD
        COMPRESS FULL '           ' NAME INTO v_A
        MOVE EDITED ST-V.SSN (EM=999-99-9999) TO v_AA
        COMPRESS FULL v_A v_AA ':' INTO V_A
        MOVE EDITED ST-V.DATE-OF-DEATH (EM=9999-99-99)  TO v_AA
        COMPRESS v_A v_AA ':' INTO V_A
        COMPRESS v_A 'rank:' ST-V.RANK 'Mil:serv:' ST-V.MILITARY-SERVICE 'mil.st:' ST-V.MILITARY-STATUS INTO V_A
        MOVE EDITED ST-V.DATE-RETIRED(EM=9999-99-99)  TO v_AA
        COMPRESS v_A 'DoR:' v_AA INTO V_A
        MOVE EDITED ST-V.DATE-OF-MARRIAGE (EM=9999-99-99) TO v_AA
        COMPRESS v_A 'DoM:' v_AA INTO V_A
        PERFORM sbr_ADD
        
        COMPRESS FULL '<b>        SP </b>' NAME-SP  INTO v_A

        MOVE EDITED SSN-SP (EM=999-99-9999) TO v_AA
        COMPRESS FULL v_A  v_AA ':' INTO V_A

        MOVE EDITED DATE-OF-DIVORCE (EM=9999-99-99) TO v_AA
        COMPRESS v_A v_AA ':' STATUS-CODE-SP INTO V_A

        MOVE EDITED DATE-OF-MARRIAGE (EM=9999-99-99) TO v_AA
        COMPRESS  v_A 'DoM:' v_AA 'CI:' SPOUSE-CONTACT-ID INTO V_A

        PERFORM sbr_ADD
     IF FORMER-SPOUSE-NAME <> " "
        COMPRESS FULL '<b>       FSP </b>' FORMER-SPOUSE-NAME  INTO v_A
        MOVE EDITED FORMER-SPOUSE-SSN (EM=999-99-9999) TO v_AA
        COMPRESS FULL v_A v_AA ':' INTO V_A
*         MOVE '          ' TO v_AA
*         COMPRESS  v_A  v_AA  ':' INTO V_A
        MOVE EDITED FORMER-SPOUSE-DATE-OF-DIVORCE (EM=9999-99-99) TO v_AA
        COMPRESS v_A  v_AA FORMER-SPOUSE-STATUS INTO V_A
        MOVE EDITED FORMER-SPOUSE-DATE-OF-MARRIAGE (EM=9999-99-99) TO v_AA
        COMPRESS  v_A 'DoM:' v_AA 'CI:' FORMER-SPOUSE-CONTACT-ID INTO V_A
        PERFORM sbr_ADD
      END-IF
 
        IF p_CI = 0   p_CI   := MEMBER-CONTACT-ID END-IF
        
        FIND O9-V WITH PRIME-CN = p_CN OR SECONDARY-CN = p_CN
           COMPRESS 'Prime CN' PRIME-CN 'has 9+ policy. In' C*SECONDARY-CN 'Secondary CN'  INTO v_A  
           PERFORM sbr_ADD
           COMPRESS PRIME-CN 'Has 9+ policies' INTO v_A  
           v_A := ' '   
           FOR #i = 1 TO C*SECONDARY-CN 
             COMPRESS  v_A '|' SECONDARY-CN(#i) INTO v_A
           END-FOR 
             COMPRESS  PRIME-CN '=>' v_A '|' INTO v_A        
             PERFORM sbr_ADD
        END-FIND
        
        FOR #i =1 TO ST-V.C*LINKED-CN
           IF #i = 1 
               MOVE EDITED ST-V.C*LINKED-CN (EM=Z9) TO v_A
               COMPRESS FULL '  -- Has' v_A 'Links --' INTO v_A
               PERFORM sbr_ADD
           END-IF                
           IF ST-V.LINKED-CN(#i) >= 500000
               COMPRESS 
               '  AT Link :' ST-V.LINKED-CN(#i) 
                 INTO v_A
                 PERFORM sbr_ADD
            ELSE  
            FIND (1) STL-V WITH ID-NUMBER = ST-V.LINKED-CN(#i) 
               IF NO RECORD FOUND 
                 COMPRESS 
                         '  Link CN :' ST-V.LINKED-CN(#i) 
                         'Not Found'   
                 INTO v_A
                 PERFORM sbr_ADD
                 ESCAPE BOTTOM
               END-NOREC              

              v_AA := ' '
              FOR #j = 1 TO STL-V.C*LINKED-CN
                  IF #j <= 15 
                      IF STL-V.LINKED-CN(#j) <> 0
                        COMPRESS v_AA LINKED-CN(#j) INTO v_AA
                      END-IF          
                  ELSE
                    COMPRESS v_AA '[' #j ']' INTO v_AA LEAVING NO SPACE
                 END-IF
              END-FOR
              COMPRESS 
                      '  Linked' '[' STL-V.C*LINKED-CN ']' 'CNs:' STL-V.ID-NUMBER 
                      '(' v_AA ')'
                      'Mem CI:' STL-V.MEMBER-CONTACT-ID 
              INTO v_A
              COMPRESS  'Flags::' 'm=' STL-V.MEMBER-FLAG 
                                  ',w=' STL-V.WIDOW-FLAG 
                                  ',p=' STL-V.PROCESS-IND 
                                  ',i=' STL-V.INTRO-FLAG 
                  INTO v_AA LEAVING NO SPACE 
               COMPRESS v_A v_AA  INTO v_A
               PERFORM sbr_ADD
            END-FIND 
          END-IF  
        END-FOR
    END-FIND
 END-IF

   IF p_CN <> 0
         FNCN.
         FIND NUMBER CNT-V WITH ID-NUMBER = p_CN
         IF *NUMBER(FNCN.) > 1
            V_A := ' '  PERFORM sbr_ADD
            COMPRESS '<b><font color="red">(!)</font></b>' 'Contact CN=' p_CN '<b><font color="red">has' *NUMBER(FNCN.) 'duplicates</font></b>' INTO v_A
            PERFORM sbr_ADD
            FIND CNT-V WITH ID-NUMBER = p_CN
               COMPRESS '(!)' 'CN=' ID-NUMBER 'CI=' CONTACT-ID 'SSN=' SSN 'DoB=' DATE-OF-BIRTH FIRST-NAME LAST-NAME  INTO v_A
               PERFORM sbr_ADD
            END-FIND
             
         END-IF    
    END-IF 
 
   IF p_CI <> 0
      FNCI.
      FIND NUMBER CNT-V WITH CONTACT-ID = p_CI
      IF *NUMBER(FNCI.) > 1
          COMPRESS '<b><font color="red">(!)</font></b>' 'Contact CI=' p_CI '<b><font color="red">has' *NUMBER(FNCI.)  'duplicates</font></b>' INTO v_A
      ELSE 
          v_A := '.'   
      END-IF    
      PERFORM sbr_ADD
      
      FIND CNT-V WITH CONTACT-ID = p_CI
           IF NO RECORD
              COMPRESS p_CI 'No Contact' INTO v_A
              PERFORM sbr_ADD
              ESCAPE BOTTOM
           END-NOREC
           COMPRESS LAST-NAME FIRST-NAME MIDDLE-NAME SUFFIX INTO NAME
           COMPRESS FULL '<b style="color:#00F">CONTACT Mem</b>' NAME  INTO v_A
           MOVE EDITED CNT-V.SSN (EM=999-99-9999) TO v_AA
           COMPRESS  FULL v_A v_AA INTO V_A
           MOVE EDITED CNT-V.DATE-OF-DEATH (EM=9999-99-99) TO v_AA
           COMPRESS v_A ':' v_AA INTO V_A
           MOVE EDITED CNT-V.DATE-OF-BIRTH (EM=9999-99-99) TO v_AA
           COMPRESS v_A ':' v_AA INTO V_A
           COMPRESS v_A 'rank id:' CNT-V.RANK-ID 'mil.st:' CNT-V.MILITARY-STATUS   INTO V_A
           MOVE EDITED CNT-V.LAST-DATE-UPD(EM=9999-99-99) TO v_A10 
           COMPRESS v_A  '<small><i>' CNT-V.LAST-USER-UPD v_A10 '</small></i>' INTO V_A
           PERFORM sbr_ADD
           IF CONTACT-TYPE = 'O'
             COMPRESS  FULL 'Org. Name  ' FIRM-ORGANIZATION-NAME INTO v_A
             PERFORM sbr_ADD
           END-IF  
           bene_CI := p_CI
           PERFORM sbr_IF-BENE
            IF v_Bene_Pol <> " "
               COMPRESS  'Beneficiary of' v_Bene_Pol  INTO v_A
               PERFORM sbr_ADD
            END-IF
                
           IF p_CN = 0 p_CN := CNT-V.ID-NUMBER END-IF
 
    END-FIND

    FIND  CR-V WITH CONTACT-1-ID = p_CI
         IF NO RECORD
           COMPRESS p_CI 'No Rel' INTO v_A
           PERFORM sbr_ADD
           ESCAPE BOTTOM
         END-NOREC
         IF *COUNTER = 1 COMPRESS '<b>Relations</b>' INTO v_AA 
         ELSE v_AA := '         ' END-IF 
         MOVE EDITED CONTACT-1-ID (EM=ZZZZZZZ9) TO v_A8_1
         MOVE EDITED CONTACT-2-ID (EM=ZZZZZZZ9) TO v_A8_2
         MOVE EDITED *COUNTER (EM=Z9')')        TO v_A
         bene_CI := CONTACT-2-ID
         PERFORM sbr_IF-BENE
         IF v_Bene_Pol <> " "
            COMPRESS  '<b>Benef. of</b>' v_Bene_Pol INTO v_Bene_Pol
            
         END-IF 
         MOVE EDITED CR-V.LAST-DATE-UPD(EM=9999-99-99) TO v_A10
         COMPRESS "<small><i>" CR-V.LAST-USER-UPD v_A10 "</small></i>" INTO v_AAA
         COMPRESS FULL
                v_AA v_A ROLE-1-CD ROLE-2-CD v_A8_2 v_AAA v_Bene_Pol
         INTO v_A 
         PERFORM sbr_ADD
         FIND CNT-V WITH CONTACT-ID = CONTACT-2-ID
           IF NO RECORD
              COMPRESS 'No Contact for CI=' CONTACT-2-ID INTO v_A
              PERFORM sbr_ADD
              ESCAPE BOTTOM
           END-NOREC
           COMPRESS LAST-NAME FIRST-NAME MIDDLE-NAME SUFFIX INTO NAME
           MOVE EDITED CONTACT-ID(EM=ZZZZZZZZ)  TO v_A8_1
           MOVE RIGHT v_A8_1 TO  v_A8_1
           COMPRESS FULL  '<b>--</b>' v_A8_1  NAME INTO v_A 
           MOVE EDITED CNT-V.SSN (EM=999-99-9999) TO v_AA
           COMPRESS  FULL v_A v_AA INTO V_A
           MOVE EDITED CNT-V.DATE-OF-DEATH (EM=9999-99-99) TO v_AA
           COMPRESS v_A ':' v_AA INTO V_A
           MOVE EDITED CNT-V.DATE-OF-BIRTH (EM=9999-99-99) TO v_AA
           COMPRESS v_A ':' v_AA INTO V_A
           MOVE EDITED CNT-V.LAST-DATE-UPD(EM=9999-99-99) TO v_A10
           COMPRESS v_A '<small><i>' CNT-V.LAST-USER-UPD v_A10 '</small></i>' INTO v_A
           COMPRESS v_A '<b style="color:#44E" >' 'cn:' CNT-V.ID-NUMBER '</b>' INTO v_A
           PERFORM sbr_ADD
        END-FIND
         
    END-FIND
    FIND  CR-V WITH CONTACT-2-ID = p_CI
         IF NO RECORD
            COMPRESS 'No Reverse Relations for' p_CI INTO v_A
            PERFORM sbr_ADD
            ESCAPE BOTTOM
         END-NOREC
         COMPRESS
            'Reverse Rel'
            CONTACT-1-ID
            ROLE-1-CD
            CONTACT-2-ID
            ROLE-2-CD
            CR-V.LAST-USER-UPD
            CR-V.LAST-DATE-UPD 
         INTO v_A 
         PERFORM sbr_ADD
    END-FIND
 END-IF
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - FIR
IF p_CN <> 0
  v_A := "." PERFORM sbr_ADD
  FIND FIR-V  WITH FIR-V.ID-NUMBER = p_CN
   COMPRESS '[' MEMBER-FLAG '-' WIDOW-FLAG ']' INTO V_AA LEAVING NO SPACE
   COMPRESS '<b style="color:#33F">FIR</b> ' FIR-V.ID-NUMBER  v_AA 'CI  Dep. Name           SSN      DoB    Sert  Status  Remarks'     INTO v_A
   PERFORM sbr_ADD
   FOR #i = 1 TO C*DEPENDENT-TABLE
    MOVE EDITED #i (EM=Z9')') TO v_A
    MOVE EDITED DEPN-CONTACT-ID(#i) (EM=ZZZZZZZ9) TO v_A8_1
    COMPRESS FULL '     ' v_A  V_A8_1       INTO v_A 
    COMPRESS FULL v_A    
              DEPN-NAME(#i)       
              DEPN-SSN(#i) 
              DEPN-DATE-OF-BIRTH(#i) '  '
              DEPN-BIRTH-CD(#i)      '  '
              DEPN-STATUS-CD(#i) 
              DEPN-REMARKS(#i)
    INTO v_A              
    PERFORM sbr_ADD
   END-FOR
 END-FIND
END-IF 
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - Other
IF p_CI <> 0  
 v_A := "." PERFORM sbr_ADD
 PH#.
 FIND NUMBER PH-V  WITH PH-V.CONTACT-ID = p_CI
 MOVE EDITED *NUMBER(PH#.) (EM=Z9) TO v_AA
 COMPRESS  'Phones' v_AA  INTO v_A

 EML#.
 FIND NUMBER EML-V  WITH EML-V.CONTACT-ID = p_CI
 MOVE EDITED *NUMBER(EML#.) (EM=Z9) TO v_AA
 COMPRESS FULL v_A '  Emails' v_AA INTO v_A

 PERFORM sbr_ADD
END-IF
 
IF p_CI <> 0   
 FIND AR-V  WITH AR-V.CONTACT-ID = p_CI
    MOVE EDITED *COUNTER (EM=Z9')') TO v_A
    v_POOL-ID := AR-V.ADDRESS-POOL-ID
    FIND (1) POOL-V  WITH POOL-V.ADDRESS-POOL-ID = v_POOL-ID
         IF NO RECORD 
            COMPRESS v_A 'Address Pool:' v_POOL-ID 'Not found' INTO v_A
         END-NOREC
         COMPRESS v_A 'Address Pool:' AR-V.ADDRESS-POOL-ID INTO v_A
    END-FIND
 
    AR_F.
     FIND  AR-V  WITH AR-V.ADDRESS-POOL-ID = v_POOL-ID
     IF *NUMBER(AR_F.) > 1
       IF *COUNTER = 1
         COMPRESS v_A 'Shared by' *NUMBER(AR_F.) 'contacts' INTO v_A 
       END-IF
       IF AR-V.CONTACT-ID = p_CI
          COMPRESS v_A '<b style="color:#08F">' AR-V.CONTACT-ID '</b>' INTO v_A
       ELSE
          COMPRESS v_A AR-V.CONTACT-ID INTO v_A
       END-IF
     END-IF  
     END-FIND
    PERFORM sbr_ADD
 END-FIND
END-IF

IF p_CN <> 0
  FIND SMP-V  WITH SMP-V.SVC-ID-NUMBER = p_CN
   COMPRESS 'SERV' 'CN:' SMP-V.SVC-ID-NUMBER INTO v_A 
   PERFORM sbr_ADD
 END-FIND
END-IF 

v_A := "." PERFORM   sbr_ADD
PERFORM sbr_Policy

*  v_A := '<pre>' 
*  PERFORM sbr_ADD
* 
*  IF p_CI <> 0  
*    v_A := " <br/><b>--- Contact Policy Relations ---</b>" 
*    PERFORM sbr_ADD 
*    v_A := ' #  Policy       Relation    (#)    Refernce '
*    PERFORM sbr_ADD
*    FIND POL-V WITH POL-V.CONTACT-ID =  p_Ci SORTED BY POL-V.POLICY-NUMBER
*        COMPRESS '(' RELATION ')' INTO v_A LEAVING NO SPACE
*        MOVE EDITED *COUNTER (EM=Z9')') TO v_AA 
*        v_A12 := POL-V.POLICY-NUMBER
*        COMPRESS FULL v_AA v_A12 rel(RELATION) v_A '  ' REFERENCE-NUMBER INTO v_A
*        PERFORM sbr_ADD 
*    END-FIND
*  END-IF
* ----------------------------------------------------------------------------------------
DEFINE sbr_ADD
  #x:= #x + 1
  EXPAND ARRAY p_Out TO (1:#x)
  COMPRESS v_A INTO p_Out(#x) 
  v_A := ' '                      
END-SUBROUTINE
* ----------------------------------------------------------------------------------------
DEFINE sbr_Policy
  v_A := '<b style="color:#66F">Policy  Policy number/NM    Plan   St  Mod Pay-St Issued       Paid Up      Paid To    Face Amnt     Cash Val    Loan Fix     Loan Var     Death-Bene   Next-Mo-DB   Conv-D Deus-Bal</b>'
  PERFORM sbr_ADD   
  FOR #i = 1 TO C*ACTIVE-SEGMENTS /* 9
*    IF PLAN(#i) <> " "
      PERFORM sbr_Owner
      MOVE EDITED #i (EM=Z9')') TO v_A
      MOVE EDITED ISSUE-DATE(#i) (EM=9999-99-99)  TO v_A10 
      COMPRESS POLICY-ID(#i) '/' NUM-MBRSHIPS(#i) INTO v_A16 LEAVING NO SPACE
      COMPRESS FULL '<b>'v_A '   ' v_A16 '</b>' ' ' PLAN(#i) ' ' STATUS(#i) ' '  MODE(#i) ' ' PAY-STATUS(#i) '   ' v_A10 INTO v_A
      MOVE EDITED PAID-UP-DATE(#i) (EM=9999-99-99)  TO v_A10 
      COMPRESS FULL v_A ' ' v_A10  INTO v_A
      MOVE EDITED PAID-TO-DATE(#i) (EM=9999-99-99)  TO v_A10 
      COMPRESS FULL v_A ' ' v_A10  INTO v_A
      MOVE EDITED FACE-AMOUNT(#i) (EM=Z,ZZZ,ZZ9)  TO v_A12 
      COMPRESS  FULL v_A  v_A12  INTO v_A
      MOVE EDITED CASH-VALUE(#i) (EM=Z,ZZZ,ZZZ.99)  TO v_A12 
      COMPRESS  v_A ' ' v_A12  INTO v_A

      MOVE EDITED X-LOAN-AMOUNT(#i) (EM=ZZ,ZZZ.99)  TO v_A10 
      COMPRESS  FULL v_A ' ' v_A10  INTO v_A
      MOVE EDITED X-LOAN-AMOUNT-2(#i) (EM=ZZ,ZZZ.99)  TO v_A10 
      COMPRESS  FULL v_A ' ' v_A10  INTO v_A

      MOVE EDITED DEATH-BENEFIT(#i)(EM=Z,ZZZ,ZZZ.99)  TO v_A12
      COMPRESS  FULL v_A  v_A12   INTO v_A

      MOVE EDITED NXT-MON-DEATH-BENEFIT(#i) (EM=Z,ZZZ,ZZZ.99)  TO v_A12
      COMPRESS  FULL v_A  v_A12   INTO v_A

/*  Not in segment        
      MOVE EDITED DUES-BALANCE (EM=Z,ZZZ,ZZZ.99)  TO v_A10
      COMPRESS  FULL v_A  v_A10 '   ' CONV-DURATION(#i) INTO v_A
/*
      PERFORM sbr_ADD
       v_A := " " 
            IF Owner_CN <> ST-V.ID-NUMBER
                COMPRESS '<b style="color:#B00">' 'Pol-Rel:' Owner_CN '</b>' INTO v_AA
            ELSE  
                COMPRESS '<b style="color:#080">' 'Pol-Rel:' Owner_CN '</b>' INTO v_AA            
            END-IF 
            COMPRESS v_A v_AA INTO v_A
*       IF v_A <> ' '
          COMPRESS FULL '        Owner CN'  v_A INTO v_A
          IF c_Owner > 1 
            COMPRESS v_A 'CoOwners:' v_Owners INTO v_A
          END-IF
          PERFORM sbr_ADD
          PERFORM sbr_Pol_Rel
*       END-IF
*    END-IF
  END-FOR
END-SUBROUTINE
* ----------------------------------------------------------------------------------------
DEFINE sbr_IF-BENE
   v_Bene_Pol := ' '
   FIND POL-V WITH POL-V.CONTACT-ID =  bene_CI
     IF RELATION = 5 OR = 6 OR = 7 OR = 11
       COMPRESS POL-V.POLICY-NUMBER INTO v_Bene_Pol
     END-IF
   END-FIND  
END-SUBROUTINE 
* ----------------------------------------------------------------------------------------
DEFINE sbr_Owner
   c_Owner := 0 v_Owners := ' '
   FIND POL-V WITH POL-V.POLICY-NUMBER = POLICY-ID(#i)
     IF RELATION = 2 OR = 4 OR = 9

       IF POL-V.REFERENCE-NUMBER <> ST-V.ID-NUMBER
         IF RELATION = 9 
           COMPRESS v_Owners ' Lnk:'POL-V.REFERENCE-NUMBER  INTO v_Owners
         ELSE
           COMPRESS v_Owners POL-V.REFERENCE-NUMBER INTO v_Owners
         END-IF  
       END-IF
       c_Owner := c_Owner + 1
     END-IF

     IF RELATION = 2
       Owner_CN := POL-V.REFERENCE-NUMBER
     END-IF
   END-FIND
END-SUBROUTINE 
* ----------------------------------------------------------------------------------------
DEFINE sbr_Pol_Rel
   v_A := "<b>        Relations</b>" 
   PERFORM sbr_ADD 
   v_A := '        #   Contact    Relation    (#)    Reference '
   PERFORM sbr_ADD
   FIND POL-V WITH POL-V.POLICY-NUMBER = POLICY-ID(#i)
       COMPRESS '(' RELATION ')' INTO v_A4 LEAVING NO SPACE
       MOVE EDITED *COUNTER (EM=Z9'.') TO v_AA
       MOVE EDITED POL-V.CONTACT-ID (EM=Z(8)) TO v_A8_1 
        COMPRESS FULL '      ' v_AA v_A8_1 '  ' rel(RELATION) v_A4 '  ' REFERENCE-NUMBER INTO v_A
*        COMPRESS FULL '      ' v_AA v_A8_1 '  ' RELATION v_A4 '  ' REFERENCE-NUMBER INTO v_A
       PERFORM sbr_ADD 
   END-FIND
END-SUBROUTINE 


END
