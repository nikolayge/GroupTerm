* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Web Application Online
* Member Information module
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    03092009       Initial Creation
* NGG    20131009  NG1 Address normalization
********************************************************
*
DEFINE DATA PARAMETER
*
1 REQUEST-DATA      (A1024)
1 RESPONSE-DATA     (A1024)        /* Out
1 RETURN-CODE       (A4)           /* Out
1 RETURN-MESSAGE    (A100)         /* Out
*
LOCAL USING OBJLSTAT
LOCAL USING OAPLMINF
LOCAL USING ADDA0001               /* NG1
LOCAL
1 #CN         (A) DYNAMIC
1 #SPLIT      (A1)   INIT <'S'>
1 #STREET     (A25)
1 #STREET-2   (A25)
1 #ERR-MSG    (A60)
1 #HOME-PHONE-NBR (N10)
1 #WORK-PHONE-NBR (N10)
1 #EXX-DELIMITER (A1) INIT <'^'>
1 #MEMBER-KEY    (A7)
1 REDEFINE #MEMBER-KEY
  2 #ID-NUMBER   (N6)
  2 #MEMBER-FLAG (A1)
1 #NOT-FOUND     (L)
1 ERROR-LOG      (A100)
*
END-DEFINE
*
#CN    := *TRIM(REQUEST-DATA)
#ID-NUMBER := VAL(#CN)
*
READ(1) ST-V WITH ID-NUMBER = #ID-NUMBER
  IF ST-V.ID-NUMBER NE #ID-NUMBER
    #NOT-FOUND := TRUE
    ESCAPE BOTTOM
  END-IF
*  SSN
*  NAME
*  Intro-flag
*  Military-Service
*  Military-Status
*  Rank
*  Zip-code
*  S-zip-code
*  Internet-Addr
*  Home-tele-intr
*  Work-tele-intr
*  Sex
*  DOB
  MOVE BY NAME ST-V TO MEMBER-INFORMATION
END-READ
IF #NOT-FOUND
  RETURN-MESSAGE := 'fail'
  RETURN-CODE    := '0001'
  RESET RESPONSE-DATA
  ESCAPE ROUTINE IMMEDIATE
END-IF
*
IF MEMBER-INFORMATION.INTRO-FLAG = ' '
  MEMBER-INFORMATION.INTRO-FLAG := 'M'
END-IF
IF MEMBER-INFORMATION.MILITARY-STATUS = ' '
  MEMBER-INFORMATION.MILITARY-STATUS := 'N'
END-IF
HOME-PHONE := FUNCTION-DEFAULT-PHONE (< ST-V.MEMBER-CONTACT-ID >)
WORK-PHONE := FUNCTION-DEFAULT-WORK  (< ST-V.MEMBER-CONTACT-ID >)
*
RESET #PDA-ADDRESS
MOVE ST-V.MEMBER-CONTACT-ID TO #ADD-CONTACT-ID
MOVE 'P' TO #ADD-TYPE
CALLNAT 'ADDN0001' #PDA-ADDRESS
STREET := #ADD-LINE-1
CITY   := #ADD-CITY
STATE  := #ADD-STATE
*
IF ST-V.S-ADDRESS-IND = 'Y'
  RESET #PDA-ADDRESS
  MOVE ST-V.MEMBER-CONTACT-ID TO #ADD-CONTACT-ID
  MOVE 'S' TO #ADD-TYPE
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  S-STREET := #ADD-LINE-1
  S-CITY   := #ADD-CITY
  S-STATE  := #ADD-STATE
*
END-IF
*
RETURN-MESSAGE := 'success'
RETURN-CODE    := '0000'
COMPRESS MEMBER-INFORMATION.SSN #EXX-DELIMITER MEMBER-INFORMATION.NAME #EXX-DELIMITER
  MEMBER-INFORMATION.MILITARY-SERVICE #EXX-DELIMITER
  MEMBER-INFORMATION.MILITARY-STATUS #EXX-DELIMITER
  MEMBER-INFORMATION.RANK #EXX-DELIMITER STREET #EXX-DELIMITER
  CITY #EXX-DELIMITER STATE #EXX-DELIMITER MEMBER-INFORMATION.ZIP-CODE #EXX-DELIMITER
  S-STREET #EXX-DELIMITER S-CITY #EXX-DELIMITER S-STATE #EXX-DELIMITER
  MEMBER-INFORMATION.S-ZIP-CODE #EXX-DELIMITER MEMBER-INFORMATION.INTERNET-ADDR #EXX-DELIMITER
  MEMBER-INFORMATION.HOME-PHONE #EXX-DELIMITER MEMBER-INFORMATION.WORK-PHONE
  #EXX-DELIMITER MEMBER-INFORMATION.SEX #EXX-DELIMITER MEMBER-INFORMATION.DATE-OF-BIRTH
  #EXX-DELIMITER MEMBER-INFORMATION.INTRO-FLAG
  INTO RESPONSE-DATA LEAVING NO
END
