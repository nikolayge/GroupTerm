* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram CQRNQQ.
/**
/** :author nguentchev
/* CALLNAT 'CQRNQQ' CustomerQuote
DEFINE DATA
PARAMETER
1 #RELATION          (A1)
1 #GENDER            (A1)
1 #NICOTINE          (A1)
1 #AGE               (N2) 1 REDEFINE #AGE
  2 #AGE-A           (A2)
1 #AGE-DAYS          (N3.2)
PARAMETER USING CQRA001
LOCAL
1 PROD-V VIEW OF A-PRODUCTS
  2 PLANCODE (A3)
  2 NAME (A50)
  2 DESCRIPTION (A120)
  2 DURATION (A50)
  2 RIDER (A1)
  2 RATECLASS (A2/1:3)
  2 EFFECTIVE-DATE (N8)
  2 END-DATE (N8)
  2 CATEGORY (A20)               /* Insurance or Product
  2 PAYMENT-MODE-CODES (A1/1:10) /* Q,G,...
  2 PAYMENT-MODES (A30/1:10)     /* Allotment, EaziPay,...
  2 FACE-AMT-TYPE (A10)          /* Fixed or Values
  2 FACE-AMT-VALUES (N7.2/1:7)   /* Min,Max or List of Face amounts
*
1 #INSURANCE (A20) CONST <'Insurance'>
1 #SERVICE   (A20) CONST <'Service'>
1 #CX       (I2)
1 #INDEX    (I2)
END-DEFINE
*
RESET #INDEX
READ PROD-V
  IF CustomerQuote.QuoteType = 1
      AND PROD-V.CATEGORY NE #INSURANCE
    ESCAPE TOP
  END-IF
  IF CustomerQuote.QuoteType = 2
      AND PROD-V.CATEGORY NE #SERVICE
    ESCAPE TOP
  END-IF
  ADD 1 TO #INDEX
  EXPAND ARRAY CustomerQuote.Product TO (1:#INDEX)
  CustomerQuote.Code            (#INDEX) := PROD-V.PLANCODE
  CustomerQuote.Name            (#INDEX) := PROD-V.NAME
  CustomerQuote.Description     (#INDEX) := PROD-V.DESCRIPTION
  CustomerQuote.PremiumDuration (#INDEX) := PROD-V.DURATION
  CustomerQuote.ProductType     (#INDEX) := PROD-V.CATEGORY
  DECIDE ON FIRST VALUE OF CustomerQuote.Code (#INDEX)
    VALUE 'RT'
      CustomerQuote.Term      (#INDEX) := 5
      CustomerQuote.Renewable (#INDEX) := true
    VALUE 'LT2'
      CustomerQuote.Term      (#INDEX) := 5
      CustomerQuote.Renewable (#INDEX) := true
      DECIDE ON FIRST VALUE OF PROD-V.DURATION
        VALUE 'Super Select'
          CustomerQuote.LevelTermType (#INDEX) := 'SS'
        VALUE 'Select'
          CustomerQuote.LevelTermType (#INDEX) := 'SE'
        NONE VALUE
          CustomerQuote.LevelTermType (#INDEX) := 'ST'
      END-DECIDE
    NONE VALUE
      IGNORE
  END-DECIDE
  #CX := 1
  CustomerQuote.C@QuotePackage(#INDEX) := #CX
  EXPAND ARRAY CustomerQuote.QuotePackage TO (*,1:#CX)
  CustomerQuote.FaceAmount ( #INDEX, #CX ) := QuickQuoteAmount
  CustomerQuote.C@Product := #INDEX 
END-READ
*
END
