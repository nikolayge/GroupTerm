* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Pass Contact data to Sales CRM
************************************************************************
* NAME         : MBRDATA                                               *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Sends Contact Relationship Container                  *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20100128      Initial Creation
* NGG    20100128  NG1 Add optional parameter
* NGG    20120628  NG2 Use ContactId instead ID-NUMBER
* NGG    20180201  NG3 Use ContactId instead ID-NUMBER
*********************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID       (N8)                       /* NG2
1 #REQUEST          (A) DYNAMIC OPTIONAL       /* NG1
1 RESPONSE-DATA     (A) DYNAMIC OPTIONAL
LOCAL
1 BLANK             (A1) CONST <' '>
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)            /* D
  2 ID-NUMBER (N6)             /* D
  2 CRM-CONTACT-GUI
*
1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID                      (N8.0) /* Descriptor
  2 ROLE-1-CD                         (A1)
  2 CONTACT-2-ID                      (N8.0) /* Descriptor
  2 ROLE-2-CD                         (A1)
  2 LAST-USER-UPD                     (A8)
  2 ACCESS-AUTH-SVC                   (A1)   /* Insurance IAS database
  2 ACCESS-AUTH-INS                   (A1)   /* Services  SAS database
*
1 #ERROR-CODE         (A2)
1 XML_SERIALZE_OUTPUT (A) DYNAMIC
1 #CSV-LINE           (A) DYNAMIC
1 #CRM-Result         (A) DYNAMIC
1 #Contact            (I4) CONST <2>
1 #Relations          (I4) CONST <3>
1 #ERROR-LOG          (I4) CONST <3>
*
1 #ISN                (P8)
1 #J                  (I4)
1 #N                  (I4)
1 #CR                 (N8/99)
1 #CR-TOP             (I4)
1 #CURRENT-CONTACT    (N8)
*
END-DEFINE
*                                                 NG1    /\/\/\
IF #REQUEST SPECIFIED
    AND #REQUEST = 'FAMILY'
  PERFORM PASS-MEMBER-ALL
ELSE
  CALLNAT 'MBRCNTC'
    #CONTACT-ID
    XML_SERIALZE_OUTPUT
    #CSV-LINE
    #CRM-Result
    #Contact
*   CALLNAT 'MBRDATA'
*     #ISN
*     XML_SERIALZE_OUTPUT
*     #CSV-LINE
*     #CRM-Result
*     #Contact
*     #CONTACT-ID                                 /* NG2
  IF RESPONSE-DATA SPECIFIED
    RESPONSE-DATA := #CRM-Result
  END-IF
END-IF
*                                                 NG1    /\/\/\
DEFINE PASS-MEMBER-ALL
/*     ===============
PERFORM COLLECT-REL
CALLNAT 'MBRREL'
  #CONTACT-ID
  XML_SERIALZE_OUTPUT
  #CSV-LINE
  #CRM-Result
  #Relations
IF RESPONSE-DATA SPECIFIED
  RESPONSE-DATA := #CRM-Result
END-IF
END-SUBROUTINE
*
DEFINE COLLECT-REL
/*     ===========
RESET #CR-TOP
*
FIND CNT-REL WITH CONTACT-2-ID = #CONTACT-ID
  ADD 1 TO #CR-TOP
  #CR (#CR-TOP) := CNT-REL.CONTACT-1-ID
END-FIND
*
FIND CNT-REL WITH CONTACT-1-ID = #CONTACT-ID
  ADD 1 TO #CR-TOP
  #CR (#CR-TOP) := CNT-REL.CONTACT-2-ID
END-FIND
*
ASSIGN #CURRENT-CONTACT = #CONTACT-ID
CALLNAT 'MBRCNTC'
  #CURRENT-CONTACT
  XML_SERIALZE_OUTPUT
  #CSV-LINE
  #CRM-Result
  #Contact
*
FOR #J 1 TO #CR-TOP
  ASSIGN #CURRENT-CONTACT = #CR (#J)
  CALLNAT 'MBRCNTC'
    #CURRENT-CONTACT
    XML_SERIALZE_OUTPUT
    #CSV-LINE
    #CRM-Result
    #Contact
END-FOR
*
END-SUBROUTINE
/*
ON ERROR
  RESET #ERROR-CODE
  CALLNAT 'CRMLOG' #ERROR-CODE #CRM-Result
    #ERROR-LOG
  IF RESPONSE-DATA SPECIFIED
    RESPONSE-DATA := #CRM-Result
  END-IF
  ESCAPE ROUTINE
END-ERROR
/*
END
