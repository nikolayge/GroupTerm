* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* FUNCTION OBJECT: MBRFPN
* Returns product name
*
DEFINE FUNCTION FUNCTION-PRODUCT-CRM RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #Product-code     (A3) 1 REDEFINE #Product-code
    2 #PLAN-1   (A1)
    2 #PLAN-2   (A2)
  LOCAL
  1 #L    (I2)
  1 #C    (I2)
  1 #LTII   (A1/6) CONST < 'W','V','U','Q','R','S' >
  1 #LTII-TEXT (A/6) DYNAMIC CONST
    <
    'Level Term II @@ year term (Super Select)',
    'Level Term II @@ year term (Select)',
    'Level Term II @@ year term (Standard)',
    'Level Term II+ @@ year term (Super Select)',
    'Level Term II+ @@ year term (Select)',
    'Level Term II+ @@ year term (Standard)'
    >
  1 #PCodes (A3/15) CONST <'LT','RT','GPL','OL','30P','20P','7P','SP','EZ','GP2'  >
  1 #Product (A/15) DYNAMIC CONST
    <
    'Level Term I To Age 50',
    'Five-Year Renewable Term',
    'Generations Plus',
    'Value-Added Whole Life Pay for life',
    'Value-Added Whole Life Pay for 30 years',
    'Value-Added Whole Life Pay for 20 years',
    'Value-Added Whole Life Pay for 7 years',
    'Value-Added Whole Life Single Payment',
    'EZ Term',
    'Generations Plus 2'
    >
  END-DEFINE
*
IF #PLAN-1 = #LTII (*) AND #PLAN-2 IS (N2)
  EXAMINE #LTII (*) FOR #PLAN-1 INDEX #L
  FUNCTION-PRODUCT-CRM := #LTII-TEXT (#L)
  EXAMINE FUNCTION-PRODUCT-CRM FOR '@@' REPLACE WITH #PLAN-2
ELSE
  EXAMINE #PCodes (*) FOR #Product-code INDEX #L
  IF #L > 0
    FUNCTION-PRODUCT-CRM := #Product (#L)
  ELSE
    FUNCTION-PRODUCT-CRM := 'Unsupported'
  END-IF
END-IF
END-FUNCTION
END
