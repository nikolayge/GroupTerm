* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
LOCAL
* 1 SERVICEREQUEST
*   2 CONTACTID (N8)
*   2 C@SERVICE (I2)
*   2 SERVICE (1:*)
*     3 PLAN (A3)
*     3 MODE (A1)
*     3 TRACKINGID (N6)
*     3 SERVICE-ID (A20)
*     3 CRM-SERVICE-ID (A50)
1 AP-TRACKING VIEW OF A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 CONTACT-ID     (N8)
  2 DATE-RECEIVED  (N8)
  2 USER-SOURCE    (A3)
  2 AT-STATUS      (A2)
  2 INTRO-FLAG     (A1)
  2 SMOKER-FLAG    (A1)
  2 INS-ONLY       (A1)
  2 MIL-SERVICE    (A3)
  2 MIL-STATUS     (A1)
  2 NAME           (A25)
  2 RANK           (A3)
  2 SSN            (N9)
  2 DATE-OF-BIRTH  (N8)
  2 SEX            (A1)
  2 MEMBER-CN      (A6) 2 REDEFINE MEMBER-CN
    3 MEMBER-CN-N  (N6)
  2 REFERING-ID-NUMBER (N6/1)
  2 NOTES          (A75/40) 2 REDEFINE NOTES
    3 NOTES-A (I1/3000)
  2 ST-NOTE-USER   (A3/1)
  2 ST-NOTE-STATUS (A2/1)
  2 ST-NOTE-DATE (N8/1) 2 REDEFINE ST-NOTE-DATE
    3 ST-NOTE-DATE-A (A8)
  2 APPL-SOURCE    (A7)
  2 CAP-IND        (A1)
  2 CRM-APPL-ID    (A50)
*
1 #AT         (N6)
1 #AT-ISN     (P8)
1 #STATUS-ISN (P8)
1 #ERROR-DESCRIPTION (A) DYNAMIC
1 RESPONSE-DATA      (A) DYNAMIC
1 MESSAGE-ID         (A) DYNAMIC
1 #ERROR-CODE        (A2)
1 #ERROR-LOG         (I4) CONST <3>
1 #REQUEST                   (I4)
1 STATUS VIEW OF A-STATUS
END-DEFINE
*
INPUT #AT
IF #AT = 0
  ESCAPE ROUTINE
END-IF
*
RESET #AT-ISN
FIND AP-TRACKING WITH ID-NUMBER = #AT
  #AT-ISN := *ISN (0065)
END-FIND
*
IF #AT-ISN > 0
  CALLNAT 'GOATN001'
    AP-TRACKING.CONTACT-ID
    AP-TRACKING.ID-NUMBER
    #ERROR-DESCRIPTION
  IF #ERROR-DESCRIPTION EQ ' '
    END TRANSACTION           /* PHASE 2 TRANSACTION = SYNCH PROCESS
    GET AP-TRACKING #AT-ISN   /* NG4 START
    IF AP-TRACKING.INTRO-FLAG = 'S' OR= 'K' OR= 'G'
      FIND (1) STATUS WITH ID-NUMBER = MEMBER-CN-N
        #STATUS-ISN := *ISN (0078)
      END-FIND
      CALLNAT 'MBRNEWAI'
        #AT-ISN
        #STATUS-ISN
    ELSE
      CALLNAT 'MBRNEWAI'
        #AT-ISN
        #STATUS-ISN
        RESPONSE-DATA
    END-IF                     /* NG4 END
  ELSE
    BACKOUT TRANSACTION
    PRINT 'BACKOUT UPDATES' #AT
    CALLNAT 'CRMLOG' #ERROR-CODE #ERROR-DESCRIPTION
      #ERROR-LOG
      MESSAGE-ID
  END-IF
END-IF
RESPONSE-DATA := AP-TRACKING.ID-NUMBER
PRINT RESPONSE-DATA
END
