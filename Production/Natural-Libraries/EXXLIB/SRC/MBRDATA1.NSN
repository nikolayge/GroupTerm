* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Pass Member data to Sales CRM as an XML message
************************************************************************
* NAME         : MBRDATA1                                               *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Returns Contact data elements                         *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20110506       Initial Creation
* NGG    20111104       Rewriten - Up to 6 phones are synched with CRM
* NGG    20120214  NG3  Leading zeros bug
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID           (N8)
1 #PHONES   (1:6)             /* Cell Home Work Fax Other Pager
  2 PHONE-TYPE-CODE     (A1)
  2 INTL-FLAG           (A1)
  2 PREFERRED-PHONE-IND (A1)
  2 DIAL-NUMBER         (A) DYNAMIC
  2 EXT                 (A6)
  2 ALL-REMARKS         (A) DYNAMIC
*
LOCAL
1 PHONES VIEW OF A-CONTACT-PHONE
  2 CONTACT-ID       (N8)
  2 PHONE-TYPE-CODE  (A1)
  2 INTL-ACCESS-CODE (N3.0)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE        (N3.0)
  2 DIAL-NUMBER      (N7.0) 2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3 (N3)
    3 DIAL-NUMBER-4 (N4)
  2 EXT              (N6.0)
  2 PREFERRED-PHONE-IND (A1)
  2 REMARKS             (A70/3)
*
1 #AREA-CODE      (A3)                 /* NG3
1 #DIAL-NUMBER-3  (A3)                 /* NG3
1 #DIAL-NUMBER-4  (A4)                 /* NG3
1 #CELL    (A1)   INIT <'C'>           /* NG2 Start
1 #HOME    (A1)   INIT <'H'>
1 #BUSINESS(A1)   INIT <'B'>
1 #FAX    (A1/3)  INIT <'A','F','X'>
1 #OTHER   (A1)   INIT <'O'>
1 #CARPHN  (A1/2) INIT <'M','P'>       /* NG2 End
*
1 #TAB       (A1)  CONST <H'09'>
1 #DASH      (A1)  CONST <'-'>
1 BLANK      (A1)  CONST <' '>
1 #N         (I4)
*
END-DEFINE
*
RESET #PHONES (*)
FIND PHONES WITH CONTACT-ID = #CONTACT-ID
  DECIDE FOR FIRST CONDITION
    WHEN PHONES.PHONE-TYPE-CODE = #CELL
      #N := 1
      PERFORM COLLECT-DATA
    WHEN PHONES.PHONE-TYPE-CODE = #HOME
      #N := 2
      PERFORM COLLECT-DATA
    WHEN PHONES.PHONE-TYPE-CODE = #BUSINESS
      #N := 3
      PERFORM COLLECT-DATA
    WHEN PHONES.PHONE-TYPE-CODE = #FAX (*)
      #N := 4
      PERFORM COLLECT-DATA
    WHEN PHONES.PHONE-TYPE-CODE = #OTHER
      #N := 5
      PERFORM COLLECT-DATA
    WHEN PHONES.PHONE-TYPE-CODE = #CARPHN (*)
      #N := 6
      PERFORM COLLECT-DATA
    WHEN NONE IGNORE
  END-DECIDE
END-FIND
*
DEFINE COLLECT-DATA
/*     ============
IF PHONES.PREFERRED-PHONE-IND = 'Y'
    OR #PHONES.DIAL-NUMBER (#N) = BLANK
  RESET #PHONES (#N)
  MOVE BY NAME PHONES TO #PHONES (#N)
  IF AREA-CODE > 0
    MOVE EDITED PHONES.AREA-CODE (EM=999) TO #AREA-CODE
    MOVE EDITED PHONES.DIAL-NUMBER-3 (EM=999) TO #DIAL-NUMBER-3
    MOVE EDITED PHONES.DIAL-NUMBER-4 (EM=9999) TO #DIAL-NUMBER-4
    COMPRESS #AREA-CODE #DIAL-NUMBER-3 #DIAL-NUMBER-4
      INTO #PHONES.DIAL-NUMBER (#N) WITH DELIMITER #DASH
    #PHONES.INTL-FLAG (#N) := 'N'
  ELSE
    #PHONES.DIAL-NUMBER (#N) := INTL-DIAL-NUMBER
    #PHONES.INTL-FLAG   (#N) := 'Y'
  END-IF
  IF PHONES.EXT = 0
    RESET #PHONES.EXT (#N)
  END-IF
  COMPRESS PHONES.REMARKS (1:3) INTO  #PHONES.ALL-REMARKS (#N)
END-IF
END-SUBROUTINE
*
END
