* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*     !!!!!!! ******
*
*     SCAN for PRINT before deployment
*
* CRM Admin interface : Pass Member data to Sales CRM
************************************************************************
* NAME         : MBRNEW                                                *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Pass member ID to the CRM                             *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20100128      Initial Creation
* NGG    20100715   NG1 Changes to accept all P* AT Statuses
* NGG    20110901   NG2 Changes to accept SD Status
* NGG    20120201   NG3 Changes to accept any status starts with a P, U, and W, AND A
* NGG    20120201   NG4 Translate Admin PR to CRM PW
* NGG    20170605   NG5 Synch all AT status codes
* NGG    08/07/2017 NG6 Online app changes                             
*********************************************************

* <Customers>
* <Customer>
* <TrackingID>123456</TrackingID>
* <Id>5e35dcde-c440-df11-9cfb-000d5670a0b3</Id>
* <CN>34567</CN>
* <ContactID>123456</ContactID>          New node
* <PolicyStatus>PR</PolicyStatus>
* </Customer>
* </Customers>


DEFINE DATA
PARAMETER
1 #AT-NUMBER          (N6)
1 #CN                 (N6)
1 #INTRO-FLAG         (A1)
1 #PROCESS-IND        (A1)
1 #CRM-APPLICATION-ID (A) DYNAMIC
1 #CRM-Result         (A) DYNAMIC

LOCAL
1 XML_SERIALZE_OUTPUT (A)  DYNAMIC
1 BLANK               (A1) CONST <' '>
1 #NEW-MEMBER         (I4) CONST <4>  /* CODE OF THE CRM FUNCTION
1 #LINKED-TO          (A7)
1 APPLICATION view of A-APPL-TRACKING
  2 AT-STATUS         (A2) 2 REDEFINE AT-STATUS
    3 AT-STATUS-1     (A1)

1 STATUS VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID

1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
*
1 #ERROR-TIME             (A19)
END-DEFINE
*
* ASSIGN XML_SERIALZE_OUTPUT = '<?xml version="1.0"?>'
RESET XML_SERIALZE_OUTPUT
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Customers'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Customer'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
PERFORM COLLECT-DATA
*
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Customer'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '</Customers'
  '>' INTO XML_SERIALZE_OUTPUT LEAVING NO
*
**IF AT-STATUS-1 = 'A' OR= 'P' OR= 'U' OR= 'W'       /* NG3 NG5
IF #CRM-APPLICATION-ID NE BLANK                      /* NG5
  CALLNAT 'CRMSYNC' XML_SERIALZE_OUTPUT (AD=O) #NEW-MEMBER (AD=O)
    #CRM-Result (AD=M)
END-IF
*
DEFINE COLLECT-DATA
/*     ============
ASSIGN #LINKED-TO = #CN
/*    REMOVED TO TEST ONLINE APP   NG6
* IF #PROCESS-IND = 'C'
*     AND #INTRO-FLAG = 'A'
*   COMPRESS #LINKED-TO 'P' INTO #LINKED-TO LEAVING NO
* END-IF

FIND APPLICATION WITH ID-NUMBER = #AT-NUMBER
END-FIND

FIND STATUS WITH ID-NUMBER = #CN
END-FIND

IF AT-STATUS = 'PR'                   /* NG1
    OR AT-STATUS = 'SD'               /* NG2
ASSIGN AT-STATUS = 'PW'             /* TRANSLATE PR TO PREVENT CRM CONFUSION  NG4
END-IF

COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<TrackingID>' #AT-NUMBER '</TrackingID>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<Id>' #CRM-APPLICATION-ID '</Id>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<CN>' #LINKED-TO '</CN>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
*                   <ContactID>123456</ContactID>
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<ContactID>' MEMBER-CONTACT-ID '</ContactID>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO
COMPRESS NUMERIC XML_SERIALZE_OUTPUT '<PolicyStatus>' AT-STATUS '</PolicyStatus>'
  INTO XML_SERIALZE_OUTPUT LEAVING NO

END-SUBROUTINE

ON ERROR
/*
  CALLNAT 'USR2001N' P_PARMS
/*
  MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
    TO #ERROR-TIME
/*
  COMPRESS
    'Error number:' P_ERROR_NUM
    'Error class:' P_ERROR_CLASS
    'Error line:' P_ERROR_LINE
    'Error program:' P_ERROR_PROGRAM
    'Error program type:' P_ERROR_PGM_TYPE
    'Error program type long:' P_ERROR_PGM_TYPE_LONG
    'Error program level:' P_ERROR_LEVEL
    'Language at error time:' P_ERROR_LANG
    'Error library:' P_ERROR_LIB
    'DBID:' P_ERROR_DBID 'FNR:' P_ERROR_FNR
    'Error type:'  P_ERROR_TYPE
    'Error time:' #ERROR-TIME
    INTO #CRM-Result

  ESCAPE ROUTINE

END-ERROR

END
