* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**@AUTHOR: TRAVIS TINGER
**   DATE:  07-29-2014
**************************
DEFINE DATA
PARAMETER USING AUCAPORG
PARAMETER
*
1 SEARCH-ISNS                  (P10/1:*)
1 ADDRESS-FLAGS                (A1/1:*)
1 EMAIL-FLAGS                  (N8/1:*)
1 PHONE-FLAGS                  (N8/1:*)
LOCAL USING ADDA0001
LOCAL
1 i              (I4)
1 PHONE-7        (N7)
1 REDEFINE PHONE-7
  2 PHONE-3      (A3)
  2 PHONE-4      (A4)
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 ID-NUMBER
  2 FULL-NAME
    3 FIRST-NAME (A25)
    3 MIDDLE-NAME (A25)
    3 LAST-NAME (A25)
    3 SUFFIX (A10)
  2 DATE-OF-BIRTH (N8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8)
  2 FIRM-ESTABLISHED-DATE
  2 FIRM-ORGANIZATION-NAME
*
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID (N8)
  2 CONTACT-ID (N8)
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
*
1 PHONE-V VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
*
END-DEFINE
*
FOR i 1 TO C@Organization
  GET CONTACT SEARCH-ISNS(i)
  CN(i)                    := ID-NUMBER
  ContactId (I)            := CONTACT-ID
  COMPRESS FULL CONTACT.SSN INTO PageOrganization.TIN(i)
  COMPRESS FIRST-NAME LAST-NAME INTO POCName(i)
  POCName(i)  := *TRIM(POCName(i))
  EstablishedDate(i)        := FIRM-ESTABLISHED-DATE
  OrganizationName(i)        := *TRIM(FIRM-ORGANIZATION-NAME)

  IF EMAIL-FLAGS(i) = 0
    POCEmail(i)               := FUNCTION-PREFFERED-EMAIL(< CONTACT-ID >)
    POCEmail(i)               := *TRIM(POCEmail(i))
  ELSE
    FIND(1) EMAIL-V WITH EMAIL-V.EMAIL-ID = EMAIL-FLAGS(i)
      POCEmail(i)              := *TRIM(EMAIL-ADDRESS)
    END-FIND
  END-IF
  RESET #PDA-ADDRESS
  #ADD-TYPE        := ADDRESS-FLAGS(i)
  #ADD-CONTACT-ID  := CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  Address1(i)   := *TRIM(#ADD-LINE-1)
  Address2(i)   := *TRIM(#ADD-LINE-2)
  Address3(i)   := *TRIM(#ADD-LINE-3)
*
  IF PHONE-FLAGS(i) = 0
    POCPhone(i)                := *TRIM(F-PREF-PHONE(<CONTACT-ID>))
  ELSE
    FIND(1) PHONE-V WITH PHONE-ID = PHONE-FLAGS(i)
     PHONE-7  := DIAL-NUMBER
     COMPRESS AREA-CODE '-' PHONE-3 '-' PHONE-4 INTO POCPhone(i) LEAVING NO
    END-FIND
  END-IF
*  
END-FOR







END
