* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
LOCAL
1 #IN-REC  (A80) 1 REDEFINE #IN-REC
  2 #CI  (N8)
1 #XML                      (A) DYNAMIC
1 #LINE                     (A) DYNAMIC
1 #RESPONSE                 (A) DYNAMIC
1 #CRM-REQUEST              (I4)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
1 #ISN                      (P8)
1 #CONTACT-ID               (N8)
1 BLANK                     (A1) CONST <' '>
1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
1 #ERROR-TIME             (A19)
1 J                       (I4)
END-DEFINE
*
DEFINE WORK FILE 1 "/usr/SAG/tmp/Quotes.TXT" TYPE "ASCII"
ASSIGN #CRM-REQUEST = 2
*
READ WORK 1 #IN-REC
*  MOVE RIGHT #IN-REC TO #IN-REC
  ADD 1 TO J
  FIND ST-V WITH MEMBER-CONTACT-ID = #CI
    IF NO RECORDS FOUND
      RESET #ISN
      #CONTACT-ID := #CI
      CALLNAT 'MBRDATA'
        #ISN
        #XML
        #LINE
        #RESPONSE
        #CRM-REQUEST
        #CONTACT-ID
      ESCAPE BOTTOM
    END-NOREC
    #ISN := *ISN
    CALLNAT 'MBRDATA'
      #ISN
      #XML
      #LINE
      #RESPONSE
      #CRM-REQUEST
  END-FIND
  IF J > 100
    PRINT #CI #ISN
    RESET J
  END-IF
END-WORK
*
ON ERROR
  CALLNAT 'USR2001N' P_PARMS
/*
  MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
    TO #ERROR-TIME
/*
  COMPRESS
    'Error number:'  P_ERROR_NUM
    'Error class:'   P_ERROR_CLASS
    'Error line:'    P_ERROR_LINE
    'Error program:' P_ERROR_PROGRAM
    'Error program type:'      P_ERROR_PGM_TYPE
    'Error program type long:' P_ERROR_PGM_TYPE_LONG
    'Error program level:'     P_ERROR_LEVEL
    'Language at error time:'  P_ERROR_LANG
    'Error library:'           P_ERROR_LIB
    'DBID:' P_ERROR_DBID
    'FNR:'  P_ERROR_FNR
    'Error type:' P_ERROR_TYPE
    'Error time:' #ERROR-TIME
    INTO #LINE
  PRINT 'Input CN:' #CI
    / #LINE
END-ERROR

END
