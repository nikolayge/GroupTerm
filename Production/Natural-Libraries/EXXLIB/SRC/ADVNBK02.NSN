* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Call to Address verification WebService
* Called from CG process
*
*****************************************************************
*        MODIFICATION LOG
*****************************************************************
* USER   DATE      TAG  REASON
* YAK    01102010       Initial Creation
* NGG    08252010       Clone ADVN0001 to match the new address
*                       structure
*****************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID      (N8)
1 #URBANIZATION    (A50)
1 #STREET          (A50)
1 #UNIT            (A25)
1 #CITY            (A50)
1 #STATE-CD        (A2)
1 #ZIP             (A10)
1 #CASS-FLAG       (A1)
1 #CHG-FLAG        (A1)
1 #DUMP            (A) DYNAMIC
LOCAL USING ADVA0002
local
1 #CORR-STREET          (A50)
1 #CORR-UNIT            (A25)
1 #CORR-CITY            (A50)
1 #CORR-STATE-CD        (A2)
1 #CORR-ZIP             (A10)

1 #NO-MATCH-FOUND-CODE   (A3)  CONST <'-99'>                                    /* YK1
1 #ADDR-TITLE            (A19)
1 #ERROR-DESC-1          (A70)
1 #ERROR-DESC-2          (A70)
1 #ERROR-DESC-3          (A70)

1 #TEXT                  (A78)
1 #REJECTED              (L)
1 #ACCEPTED              (L)
1 #NO-MATCH-FOUND        (L)
1 #EXCEEDED-LENGTH       (L)
1 #NO-ZIP4-FOUND         (L)
1 #ERROR-MSG             (A) DYNAMIC
1 #NO-ZIP4-ACCEPTED-MSG  (A) DYNAMIC INIT <'No ZIP+4 returned - accepted '>
1 #NO-ZIP4-REJECTED-MSG  (A) DYNAMIC INIT <'No ZIP+4 returned - rejected '>
1 #REJECTED-MSG          (A) DYNAMIC INIT <'Address change rejected by user'>
1 #NO-MATCH-FOUND-MSG    (A) DYNAMIC INIT <'No match found'>
1 #PROCESS-TYPE          (A) DYNAMIC INIT <'AddressValidation'>
1 #USER-ID               (A8)
1 #SearchesLeft          (A10)


1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER  (N6)
  2 CONTACT-ID (N8)
1 RELATED VIEW OF A-CONTACTS
  2 ID-NUMBER  (N6)
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD

1 #ADD-MESSAGE           (A) DYNAMIC
1 #LOG-MESSAGE           (A) DYNAMIC
1 #MILITARY              (A2)
1 #K                     (I4)
1 #L                     (I4)
1 BLANK                  (A1) CONST <' '>

END-DEFINE
escape routine
* do not send empty addresses for verification
IF #STREET = ' ' and #CITY = ' ' and #STATE-CD = ' '
    and #ZIP = ' '
   ESCAPE ROUTINE
END-IF
RESET #CASS-FLAG
username      := 'AAFMAA'
password      := '1879'
ca_codes      := 'Ry'
delivery_line_1 := #STREET
delivery_line_2 := #UNIT
urbanization    := #URBANIZATION
compress #CITY #STATE-CD #ZIP into city_state_zip

Callnat 'WSCORREC'
  username (AD=O) password (AD=O) firmname (AD=O)
  urbanization (AD=O)
  delivery_line_1 (AD=O)
  delivery_line_2 (AD=O)
  city_state_zip (AD=O)
  ca_codes (AD=O) ca_filler (AD=O) batchname (AD=O)
  StreetNumber (AD=A) PreDirectional (AD=A) StreetName (AD=A) StreetSuffix (AD=A)
  PostDirectional (AD=A) SecondaryDesignation (AD=A) SecondaryNumber (AD=A)
  City (AD=A) State (AD=A) ZipAddon (AD=A) Zip (AD=A) Addon (AD=A) LOTNumber (AD=A)
  DPCCheckdigit (AD=A) RecordType (AD=A) DeliveryLine1 (AD=A) DeliveryLine2 (AD=A)
  LastLine (AD=A) LACS (AD=A) CarrierRoute (AD=A) PMBDesignator (AD=A) FirmRecipient (AD=A)
  Urbanization_r (AD=A) CountyName (AD=A) CountyNumber (AD=A) FILLER (AD=A)
  GeoTLID (AD=A) GeoMatchFlag (AD=A) GeoTract (AD=A) GeoBlock (AD=A) GeofLat (AD=A)
  GeotLat (AD=A) GeofLong (AD=A) GeotLong (AD=A) GeoAddonEnd (AD=A) GeoAddonStart (AD=A)
  GeoRetCode (AD=A) GeoErrCodes (AD=A) ReturnCodes (AD=A) ErrorCodes (AD=A)
  ErrorDesc (AD=A) SearchesLeft (AD=A)

RESET #MILITARY
#K := *LENGTH ( ErrorCodes )
#L := 1
REPEAT
  UNTIL #L >= #K
  IF SUBSTRING ( ErrorCodes,#L,2) = '13'
    #MILITARY := '13'
  END-IF
  ADD 2 TO #L
END-REPEAT

COMPRESS
  '<StreetNumber>' StreetNumber
  '<PreDirectional>' PreDirectional
  '<StreetName>' StreetName
  '<StreetSuffix>' StreetSuffix
  '<PostDirectional>' PostDirectional
  '<SecondaryDesignation>' SecondaryDesignation
  '<State>' State
  '<SecondaryNumber>' SecondaryNumber
  '<City>' City
  '<ZipAddon>' ZipAddon
  '<Zip>' Zip
  '<Addon>' Addon
  '<LOTNumber>' LOTNumber
  '<DPCCheckdigit>' DPCCheckdigit
  '<RecordType>' RecordType
  '<DeliveryLine1>' DeliveryLine1
  '<DeliveryLine2>' DeliveryLine2
  '<LastLine>' LastLine
  '<LACS>' LACS
  '<CarrierRoute>' CarrierRoute
  '<PMBDesignator>' PMBDesignator
  '<FirmRecipient>' FirmRecipient
  '<Urbanization_r>' Urbanization_r
  '<CountyName>' CountyName
  '<CountyNumber>' CountyNumber
  '<ErrorCodes>' ErrorCodes
  '<Military>' #MILITARY
  '<FILLER>' FILLER
  INTO #DUMP

DEFINE WINDOW ADDRESS-UPDATE
  SIZE 18 * 80
  BASE 4 / 4
  TITLE #ADDR-TITLE
  FRAMED ON
*
DEFINE WINDOW USER-MESSAGE
  SIZE 5 * 50
  BASE 8 / 15
  TITLE #ADDR-TITLE
  FRAMED ON

IF ReturnCodes = #NO-MATCH-FOUND-CODE
* Returned address is the same as sent address
  #NO-MATCH-FOUND := TRUE
  PERFORM WRITE-LOG
* Display information window
  SET WINDOW 'USER-MESSAGE'
  INPUT WINDOW = 'USER-MESSAGE' WITH TEXT
    '  ***           No Match Found        ***'
    '  ***             No changes          ***'
  #CHG-FLAG    := 'N'
  #CASS-FLAG   := 'F'
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF

IF ZipAddon = ' '
  IF Addon = ' ' THEN Addon := '0000'
  END-IF
  COMPRESS Zip Addon into ZipAddon with '-'
  #NO-ZIP4-FOUND := TRUE
END-IF

ASSIGN #SearchesLeft := SearchesLeft

IF RecordType = 'P' OR= 'R'
    OR #MILITARY = '13'
  #CORR-STREET := DeliveryLine1
ELSE
  COMPRESS
    StreetNumber
    PreDirectional
    StreetName
    StreetSuffix
    PostDirectional
    INTO #CORR-STREET
END-IF
IF SecondaryDesignation EQ BLANK
    AND SecondaryNumber NE BLANK
  COMPRESS
    '#'
    SecondaryNumber
    PMBDesignator
    INTO #CORR-UNIT
ELSE
  COMPRESS
    SecondaryDesignation
    SecondaryNumber
    PMBDesignator
    INTO #CORR-UNIT
END-IF
IF #STREET NE #CORR-STREET
    OR #UNIT NE #CORR-UNIT
    OR #CITY NE City
    OR #STATE-CD NE State
    OR #ZIP NE ZipAddon
*
  #CORR-CITY      := City
  #CORR-STATE-CD  := State
  #CORR-ZIP       := ZipAddon
  #ERROR-DESC-1   := ErrorDesc
*
  if *length(ErrorDesc) > 70
    #ERROR-DESC-2  := substr(ErrorDesc,71)
  end-if
  if *length(ErrorDesc) > 140
    #ERROR-DESC-3  := substr(ErrorDesc,140)
  end-if
  #CHG-FLAG    := 'Y'
  #CASS-FLAG   := 'N'
ELSE
  #CHG-FLAG    := 'N'
  #CASS-FLAG   := 'Y'
*    Display confirmation window
  SET WINDOW 'USER-MESSAGE'
  INPUT WINDOW = 'USER-MESSAGE' WITH TEXT
    '  ***      Address is CASS certified  ***'
    '  ***             No changes          ***'
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF

SET WINDOW 'ADDRESS-UPDATE'
INPUT WITH TEXT #TEXT USING MAP 'ADVM0002'
DECIDE FOR FIRST CONDITION
  WHEN #CHG-FLAG = 'N'                 /* Corrections were not accepted by user
    #REJECTED   := TRUE
  WHEN #CHG-FLAG = 'Y'                 /* Corrections accepted by user
    #ACCEPTED   := TRUE
    #STREET     := #CORR-STREET
    #UNIT       := #CORR-UNIT
    #CITY       := #CORR-CITY
    #STATE-CD   := #CORR-STATE-CD
    #ZIP        := #CORR-ZIP
    #CASS-FLAG   := 'Y'
  WHEN ANY
    PERFORM WRITE-LOG
    SET WINDOW OFF
    ESCAPE ROUTINE
  WHEN NONE                                  /* There are corrections, user selection Y/N required
    #TEXT := 'Please, select "Y" OR "N"'
    REINPUT WITH #TEXT MARK *#CHG-FLAG
END-DECIDE

***************************
DEFINE SUBROUTINE WRITE-LOG    /* YK2
***************************
*
RESET #ERROR-MSG #ADD-MESSAGE
FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
END-FIND
IF CONTACT.ID-NUMBER = 0                                   /* NG1 add Linked CN
  FIND CONTACT-REL WITH CONTACT-2-ID = CONTACT.CONTACT-ID
    FIND RELATED WITH CONTACT-ID = CONTACT-1-ID
      IF RELATED.ID-NUMBER > 0
        #ADD-MESSAGE := FUNCTION-CONTACT-NAME-2 (<CONTACT.CONTACT-ID>)
        #ERROR-MSG := FUNCTION-DISP-RL-DESC(<CONTACT-REL.ROLE-2-CD>)
        COMPRESS 'See CN:' RELATED.ID-NUMBER
          #ERROR-MSG
          #ADD-MESSAGE
          'Contact ID'
          CONTACT.CONTACT-ID
          TO #ADD-MESSAGE
        RESET #ERROR-MSG
        ESCAPE BOTTOM (0277)
      END-IF
    END-FIND
  END-FIND
END-IF

DECIDE FOR FIRST CONDITION
  WHEN #ACCEPTED AND #NO-ZIP4-FOUND
    #ERROR-MSG := #NO-ZIP4-ACCEPTED-MSG
  WHEN #REJECTED AND #NO-ZIP4-FOUND
    #ERROR-MSG := #NO-ZIP4-REJECTED-MSG
  WHEN #REJECTED
    #ERROR-MSG := #REJECTED-MSG
  WHEN #NO-MATCH-FOUND
    #ERROR-MSG := #NO-MATCH-FOUND-MSG
  WHEN NONE
    IGNORE
END-DECIDE
IF #ERROR-MSG NE ' '
  #USER-ID := *USER
  IF CONTACT.ID-NUMBER = 0
    COMPRESS *DATU FUNCTION-CLERK(<#USER-ID>) '*' #STREET #UNIT #CITY #STATE-CD #ZIP
      '-' #ERROR-MSG '-' #ADD-MESSAGE INTO #LOG-MESSAGE
  ELSE
    COMPRESS *DATU FUNCTION-CLERK(<#USER-ID>) CONTACT.ID-NUMBER #STREET #UNIT #CITY
      #STATE-CD #ZIP '-' #ERROR-MSG INTO #LOG-MESSAGE
  END-IF
  CALLNAT 'GPRN0005' #PROCESS-TYPE #LOG-MESSAGE
END-IF
*
END-SUBROUTINE

END
