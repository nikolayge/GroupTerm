* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* CRM Admin interface : Get Clerk ID
************************************************************************
* NAME         : MBRCLERK                                              *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Get Clerk ID from UserCode tag in CRM XML message     *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20111011      Initial Creation
DEFINE DATA
PARAMETER
1 XML_PARSER_INPUT             (A) DYNAMIC
1 #CLERK-ID                    (A3)
LOCAL USING PARSER-X           /* parser internal data - do not change
LOCAL
1 #GEN-KEY           (A4)
1 GEN-TABLE-V VIEW OF A-GEN-TABLE
  2 GEN-KEY (A4)
  2 GEN-SUB-KEY (A3)
  2 GEN-ALPHA-VALUE (A50)
1 #TOKENS                      (A/9) DYNAMIC
1 XML_PARSER_ERROR_TEXT        (A253)
1 XML_PARSER_RESPONSE          (I4)
1 XML_PARSER_XPATH             (A) DYNAMIC
1 XML_PARSER_XPATH_TYPE        (A1)
1 XML_PARSER_CONTENT           (A) DYNAMIC
1 XML_PARSER_CONTENT_IS_EMPTY  (L)
END-DEFINE
*
RESET #CLERK-ID
INCLUDE PARSER_X 'XML_PARSER_INPUT' /* XML file to be parsed
  'XML_PARSER_XPATH'                /* XPATH to represent element...
  'XML_PARSER_XPATH_TYPE'           /* Type of callback
  'XML_PARSER_CONTENT'              /* Content of element found
  'XML_PARSER_CONTENT_IS_EMPTY'     /* Is TRUE if element is empty
  'XML_PARSER_ERROR_TEXT'           /* error Message
  'XML_PARSER_RESPONSE'             /* Error NR; 0 = OK
*
DEFINE SUBROUTINE CALLBACK
/*                ========
IF XML_PARSER_CONTENT_IS_EMPTY THEN
  IGNORE
ELSE
  IF #CLERK-ID NE ' '
    IGNORE
  ELSE
    XML_PARSER_CONTENT := *TRIM (XML_PARSER_CONTENT)
    MOVE *TRANSLATE(XML_PARSER_CONTENT, UPPER) TO XML_PARSER_CONTENT
    SEPARATE XML_PARSER_XPATH INTO #TOKENS (*) IGNORE WITH DELIMITERS '/'
    IF  #TOKENS (3) = 'UserCode'
      #GEN-KEY := 'WEBU'
      FIND GEN-TABLE-V WITH GEN-KEY = #GEN-KEY
        ACCEPT IF *TRANSLATE(GEN-ALPHA-VALUE,UPPER)= XML_PARSER_CONTENT
        #CLERK-ID := GEN-SUB-KEY
        ESCAPE BOTTOM
      END-FIND
    END-IF
  END-IF
END-IF
END-SUBROUTINE
/*
DEFINE SUBROUTINE PARSER_ERROR
/*                ============
RESET #CLERK-ID
ESCAPE ROUTINE
END-SUBROUTINE
/*
END

