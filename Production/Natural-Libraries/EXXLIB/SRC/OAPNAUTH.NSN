* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Web Application Online
* Authentication module
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    03012009       Initial Creation
********************************************************
*
DEFINE DATA PARAMETER
*
1 REQUEST-DATA      (A1024)
1 RESPONSE-DATA     (A1024)        /* Out
1 RETURN-CODE       (A4)           /* Out
1 RETURN-MESSAGE    (A100)         /* Out
*
LOCAL USING OBJLSTAT
LOCAL USING OBJLAPTR
LOCAL
1 #CN            (A) DYNAMIC
1 #SSN-4         (A) DYNAMIC
1 #DOB           (A) DYNAMIC
1 #EXX-DELIMITER (A1) INIT <'^'>
1 #MEMBER-KEY    (A7)
1 REDEFINE #MEMBER-KEY
  2 #ID-NUMBER   (N6)
  2 #MEMBER-FLAG (A1)
1 #NOT-FOUND     (L) 
1 #ST-SSN        (N9)
1 REDEFINE #ST-SSN
  2 #ST-SSN-A    (A9)
1 #ST-DOB        (N8)
1 REDEFINE #ST-DOB
  2 #ST-DOB-A    (A8)
1 ERROR-LOG      (A100)
1 #WEB-USER      (A8)
*
END-DEFINE
*
#WEB-USER := *USER
SEPARATE REQUEST-DATA INTO #CN #SSN-4 #DOB IGNORE WITH DELIMITER #EXX-DELIMITER
*
#CN    := *TRIM(#CN)
#SSN-4 := *TRIM(#SSN-4)
#DOB   := *TRIM(#DOB)
#ID-NUMBER := VAL(#CN)
READ(1) ST-V WITH MEMBER-KEY = #MEMBER-KEY
  IF ID-NUMBER NE #ID-NUMBER
     #NOT-FOUND := TRUE
     ESCAPE BOTTOM
  END-IF
     #ST-SSN := SSN
     #ST-DOB := DATE-OF-BIRTH
     IF SUBSTR(#ST-SSN-A,6,4) = #SSN-4  AND #DOB = #ST-DOB-A
        IGNORE
     ELSE
         #NOT-FOUND := TRUE
     END-IF
END-READ
*
IF #NOT-FOUND OR ST-V.ID-NUMBER = 0
  RETURN-MESSAGE := 'fail'
  RETURN-CODE    := '0001'
ELSE
  RETURN-MESSAGE := 'success'
  RETURN-CODE    := '0000'
END-IF
END

