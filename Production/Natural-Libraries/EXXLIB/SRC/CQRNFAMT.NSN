* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* CQRNFAMT
*
DEFINE DATA
PARAMETER
1 #PLAN        (A) DYNAMIC
1 QUOTEPACKAGE (I4)
1 FACEAMOUNT   (N22.7/1,1:V)
1 ERRORDESC    (A) DYNAMIC
LOCAL
1 PROD-V VIEW OF A-PRODUCTS
  2 PLANCODE (A3)
  2 NAME (A50)
  2 DESCRIPTION (A120)
  2 DURATION (A50)
  2 RIDER (A1)
  2 RATECLASS (A2/1:3)
  2 EFFECTIVE-DATE (N8)
  2 END-DATE (N8)
  2 CATEGORY (A20)               /*Insurance or Product
  2 PAYMENT-MODE-CODES (A1/1:10) /* Q,G,...
  2 PAYMENT-MODES (A30/1:10) 
  2 FACE-AMT-TYPE (A10)          /*Fixed or Values
  2 FACE-AMT-VALUES (N7.2/1:7) 
1 J     (I4)
1 #WHOLE               (A3/5)   CONST <'OL','30P','20P','7P','SP'>
*
1 #FaceAMTError_GPL    (A) DYNAMIC CONST
  <'We''re sorry, GPL policy is only available for $10,000, $15,000, $20,000, $25,000 and $30,000.'>
1 #FaceAMTError_GP2    (A) DYNAMIC CONST
  <'We''re sorry, GPL policy is only available for $10,000, $15,000, $20,000, $25,000, $30,000, $40,000 and $50,000.'>
1 #FaceAMTError_EZ       (A) DYNAMIC CONST
  <'We''re sorry, EZ Term is available only in the following face amounts: $10,000, $25,000, $50,000, $75,000 and $100,000'>
1 #FaceAMTError_WL    (A) DYNAMIC CONST
  <'We''re sorry, whole life policies are only available for $10,000 or more.'>
1 #FaceAMTError_GNR    (A) DYNAMIC CONST
  <'We''re sorry, this policy is only available for $50,000 or more.'>
1 #FaceAMTError_LT    (A) DYNAMIC CONST
  <'We''re sorry, Level Term I is available only up to the face amount of $800,000.'>
1 #FaceAMTError_LT2   (A) DYNAMIC CONST
  <'We''re sorry, Level Term II is available only up to the face amount of $800,000.'>
1 #FaceAMTError_RT   (A) DYNAMIC CONST
  <'We''re sorry, Five year Renewable Term is available only up to the face amount of $800,000.'>
*
END-DEFINE
*
READ PROD-V
  IF #PLAN = PLANCODE
    FOR J 1 TO QUOTEPACKAGE
      IF FACEAMOUNT (1,J) NE 0
        IF FACE-AMT-TYPE = 'Fixed'
          IF NOT FACEAMOUNT (1,J) = FACE-AMT-VALUES (*)
            PERFORM ERROR-MESSAGE
            ESCAPE BOTTOM
          END-IF
        ELSE
          IF FACEAMOUNT (1,J) < FACE-AMT-VALUES (1)        /* Minimum
              OR FACEAMOUNT (1,J) > FACE-AMT-VALUES (2)    /* Maximum
            PERFORM ERROR-MESSAGE
            ESCAPE BOTTOM
          END-IF
        END-IF
      END-IF
    END-FOR
    ESCAPE BOTTOM
  END-IF
END-READ
*
DEFINE ERROR-MESSAGE
DECIDE FOR FIRST CONDITION
  WHEN #PLAN = 'GP2'
    ASSIGN ERRORDESC = #FaceAMTError_GP2
  WHEN #PLAN = 'RT'
    ASSIGN ERRORDESC = #FaceAMTError_RT
  WHEN #PLAN = 'EZ'
    ASSIGN ERRORDESC = #FaceAMTError_EZ
  WHEN #PLAN = 'LT'
    ASSIGN ERRORDESC = #FaceAMTError_LT
  WHEN #PLAN = 'LT2'
    ASSIGN ERRORDESC = #FaceAMTError_LT2
  WHEN #PLAN = #WHOLE (*)
    ASSIGN ERRORDESC = #FaceAMTError_WL
  WHEN NONE
    ASSIGN ERRORDESC = #FaceAMTError_GNR
END-DECIDE
END-SUBROUTINE
*
END
