* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA
PARAMETER USING AUCASORG
PARAMETER
1 SEARCH-ISNS             (P10/1:*)
1 ADDRESS-FLAGS           (A1/1:*)
1 EMAIL-FLAGS             (N8/1:*)
1 PHONE-FLAGS             (N8/1:*)
*
LOCAL
1 LETTER               (A1)   CONSTANT<']'>
1 BLANK                (A1)   CONSTANT<' '>
1 SSN-MAX-DIGITS       (N9.0) CONSTANT<9>
1 ORGANIZATION         (A1)   CONSTANT<'O'>
1 CONTACT-SET-EXISTS   (L)
1 ADDRESS-SET-EXISTS   (L)
1 i                    (I4)
*
1 #ZIP-CODE      (N9)
1 REDEFINE #ZIP-CODE
  2 #ZIP-4           (A4)
  2 #ZIP-5           (A5)
1 ZIP-WITH-ZEROS      (N9)
1 REDEFINE ZIP-WITH-ZEROS
  2 ZIP-WITH-ZEROS-A   (A9)
1 ZIP-WITH-NINES      (N9)
1 REDEFINE ZIP-WITH-NINES
  2 ZIP-WITH-NINES-A   (A9)
*
1 HOLD-ISN                (P10)
1 UPPER-BOUND             (I4) INIT <5000>
1 CURRENT-INDEX           (I4) INIT <1>
*
1 #SSN-NUMBER             (N9.0)
1 REDEFINE #SSN-NUMBER
  2 #SSN-STRING           (A9)
1 #SSN-NUMBER-MAX         (N9.0)
1 REDEFINE #SSN-NUMBER-MAX
  2 #SSN-STRING-MAX       (A9)
1 HOLD-TIN                (N9)
*
1 #IN-PHONE           (N10)
1 REDEFINE #IN-PHONE
  2 #AREA-CODE   (N3)
  2 #DIAL-NUMBER (N7)
1 SSN-TEXT                (A9)
1 DYNAMIC-SSN             (A) DYNAMIC
1 SSN-LENGTH              (I2)
1 TRAILING-NINES          (A9)
1 TRAILING-ZEROS          (A9)
*
1 NAME-LENGTH          (I4)
1 NAME-WITH-TRAIL      (A25)
1 ORG-NAME-LENGTH      (I4)
1 ORG-NAME-WITH-TRAIL  (A75)
1 EMAIL-LENGTH         (I4)
1 EMAIL-WITH-TRAIL     (A70)
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 CONTACT-STATUS (A1)
  2 CONTACT-TYPE (A1)
  2 ID-NUMBER (N6)
  2 LAST-TIME-UPD (N7)
  2 FIRM-ORGANIZATION-NAME (A75)
  2 FIRM-ESTABLISHED-DATE (N8)
  2 FIRM-CERT-CODE (A1)
  2 FIRST-NAME
  2 LAST-NAME
*
1 PHONE-V VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
*
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID (N8)
  2 CONTACT-ID (N8)
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
*
1 ADDRESS-V VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID (N8)
  2 CITY (A50)
  2 STATE-CODE (A2)
  2 ZIP-CODE (N9)
*
1 ADDRESS-RELATIONSHIP VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID (N8)
  2 ADDRESS-POOL-ID (N8)
  2 ADDR-TYPE-CD
*
END-DEFINE


EXPAND ARRAY SEARCH-ISNS    TO (1:UPPER-BOUND) /* STARTS AT 5000
EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND) 
EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND) 
EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND) 

DECIDE FOR EVERY CONDITION
  WHEN SearchOrganization.TIN                    NE BLANK          PERFORM FIND-BY-TIN
  WHEN SearchOrganization.EstablishedDate        NE 0              PERFORM FIND-BY-ESTABLISHED-DATE
  WHEN SearchOrganization.OrganizationName       NE BLANK          PERFORM FIND-BY-ORGANIZATION-NAME
  WHEN SearchOrganization.POCLastName            NE BLANK          PERFORM FIND-BY-LAST-NAME
  WHEN SearchOrganization.POCFirstName           NE BLANK          PERFORM FIND-BY-FIRST-NAME
  WHEN SearchOrganization.ZIP                    NE 0              PERFORM FIND-BY-ZIP-CODE
  WHEN SearchOrganization.City                   NE BLANK          PERFORM FIND-BY-CITY
  WHEN SearchOrganization.State                  NE BLANK          PERFORM FIND-BY-STATE
  WHEN CONTACT-SET-EXISTS OR ADDRESS-SET-EXISTS                    PERFORM ADD-TO-ISN-ARRAY
  WHEN SearchOrganization.POCPhone               NE 0              PERFORM FIND-BY-PHONE-NUMBER
  WHEN SearchOrganization.POCeMail               NE BLANK          PERFORM FIND-BY-EMAIL-ADDRESS
  WHEN NONE
    IGNORE
END-DECIDE
*
TotalFound := CURRENT-INDEX - 1
*
IF CONTACT-SET-EXISTS
  RELEASE SET 'CONTACT-SET'
END-IF
IF ADDRESS-SET-EXISTS
  RELEASE SET 'ADDRESS-SET'
END-IF
*
**============================
DEFINE SUBROUTINE FIND-BY-TIN
**===========================
HOLD-TIN := VAL(SearchOrganization.TIN)
*
TIN.
FIND NUMBER CONTACT WITH SSN = HOLD-TIN AND SSN > 1
  RETAIN AS 'CONTACT-SET'
*
IF *NUMBER( TIN.) = 0 THEN
  PERFORM ADD-TRAILING-NUMS
  FIND NUMBER CONTACT WITH SSN >= #SSN-NUMBER AND SSN <= #SSN-NUMBER-MAX
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============

**============================
DEFINE SUBROUTINE FIND-BY-ESTABLISHED-DATE
**===========================
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET'
    AND FIRM-ESTABLISHED-DATE = SearchOrganization.EstablishedDate
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH FIRM-ESTABLISHED-DATE = SearchOrganization.EstablishedDate
    RETAIN AS 'CONTACT-SET'
  IGNORE
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============


**==================================
DEFINE SUBROUTINE ADD-TRAILING-NUMS
**==================================
DYNAMIC-SSN := *TRIM(SearchOrganization.TIN)
#SSN-STRING := SearchOrganization.TIN
SSN-LENGTH  := *LENGTH(SearchOrganization.TIN)
RESET i
FOR i SSN-LENGTH TO SSN-MAX-DIGITS
  COMPRESS '0' TRAILING-ZEROS INTO TRAILING-ZEROS LEAVING NO
  COMPRESS '9' TRAILING-NINES INTO TRAILING-NINES LEAVING NO
END-FOR
*
COMPRESS  #SSN-STRING TRAILING-NINES    INTO #SSN-STRING-MAX   LEAVING NO
COMPRESS  #SSN-STRING TRAILING-ZEROS    INTO #SSN-STRING       LEAVING NO
**============
END-SUBROUTINE
**============


**============================
DEFINE SUBROUTINE FIND-BY-ORGANIZATION-NAME
**============================
RESET ORG-NAME-LENGTH ORG-NAME-WITH-TRAIL
ORG-NAME-LENGTH           := *LENGTH(OrganizationName)
ORG-NAME-WITH-TRAIL       := SearchOrganization.OrganizationName
*
FOR i ORG-NAME-LENGTH TO 75
  COMPRESS ORG-NAME-WITH-TRAIL '[' INTO ORG-NAME-WITH-TRAIL LEAVING NO
END-FOR
*
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET'
    AND FIRM-ORGANIZATION-NAME = OrganizationName THRU ORG-NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH FIRM-ORGANIZATION-NAME = OrganizationName THRU ORG-NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
END-IF
*
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============



**============================
DEFINE SUBROUTINE FIND-BY-LAST-NAME
**===========================
RESET NAME-LENGTH NAME-WITH-TRAIL
NAME-LENGTH     := *LENGTH(POCLastName)
NAME-WITH-TRAIL := POCLastName
*
FOR i NAME-LENGTH TO 25
  COMPRESS NAME-WITH-TRAIL '[' INTO NAME-WITH-TRAIL LEAVING NO
END-FOR
*
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND LAST-NAME = POCLastName THRU NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH LAST-NAME = POCLastName THRU NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============

**============================
DEFINE SUBROUTINE FIND-BY-FIRST-NAME
**===========================
RESET NAME-LENGTH NAME-WITH-TRAIL
NAME-LENGTH := *LENGTH(POCFirstName)
NAME-WITH-TRAIL := POCFirstName
*
FOR i NAME-LENGTH TO 25
  COMPRESS NAME-WITH-TRAIL '[' INTO NAME-WITH-TRAIL LEAVING NO   /* '[' is character after Z
END-FOR
*
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND FIRST-NAME = POCFirstName THRU NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH FIRST-NAME = POCFirstName THRU NAME-WITH-TRAIL
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============

**================================
DEFINE SUBROUTINE FIND-BY-ZIP-CODE
**================================
#ZIP-CODE := ZIP
IF #ZIP-CODE > 99999  /* CHECKING FOR 9 DIGIT ZIP-CODE
  FIND NUMBER ADDRESS-V WITH ZIP-CODE = #ZIP-CODE
    RETAIN AS 'ADDRESS-SET'
ELSE
  COMPRESS #ZIP-5 '0000' INTO ZIP-WITH-ZEROS-A LEAVING NO
  COMPRESS #ZIP-5 '9999' INTO ZIP-WITH-NINES-A LEAVING NO
  FIND NUMBER ADDRESS-V WITH ZIP-CODE >= ZIP-WITH-ZEROS AND ZIP-CODE <= ZIP-WITH-NINES
    RETAIN AS 'ADDRESS-SET'
END-IF
ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============



**============================
DEFINE SUBROUTINE FIND-BY-CITY
**============================
IF ADDRESS-SET-EXISTS
  FIND NUMBER ADDRESS-V WITH 'ADDRESS-SET' AND CITY = SearchOrganization.City
    RETAIN AS 'ADDRESS-SET'
ELSE
  FIND NUMBER ADDRESS-v WITH CITY = SearchOrganization.City
    RETAIN AS 'ADDRESS-SET'
END-IF
ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============


**=============================
DEFINE SUBROUTINE FIND-BY-STATE
**=============================
IF ADDRESS-SET-EXISTS
  FIND NUMBER ADDRESS-V WITH 'ADDRESS-SET' AND STATE-CODE = State
    RETAIN AS 'ADDRESS-SET'
ELSE
  FIND NUMBER ADDRESS-V WITH STATE-CODE = State
    RETAIN AS 'ADDRESS-SET'
END-IF
ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============

**=================================
DEFINE SUBROUTINE ADD-TO-ISN-ARRAY
**=================================
IF SearchOrganization.POCPhone NE 0        
  ESCAPE ROUTINE
END-IF
*      
IF SearchOrganization.POCeMail NE BLANK
  ESCAPE ROUTINE
END-IF
*
IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS
  FIND CONTACT WITH 'CONTACT-SET'
    ACCEPT IF CONTACT-TYPE = ORGANIZATION
    RESET HOLD-ISN
    HOLD-ISN := *ISN
    FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = CONTACT.CONTACT-ID
      FIND(1) ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
        PERFORM MANAGE-ARRAY
        SEARCH-ISNS    (CURRENT-INDEX) := HOLD-ISN
        ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
        ADD 1 TO CURRENT-INDEX
      END-FIND
    END-FIND
  END-FIND
ELSE
  IF CONTACT-SET-EXISTS
    FIND CONTACT WITH 'CONTACT-SET'
      ACCEPT IF CONTACT-TYPE = ORGANIZATION
      PERFORM MANAGE-ARRAY
      SEARCH-ISNS    (CURRENT-INDEX)  := *ISN
      ADDRESS-FLAGS  (CURRENT-INDEX)  := 'P'
      ADD 1 TO CURRENT-INDEX
    END-FIND
  ELSE
    IF ADDRESS-SET-EXISTS
      FIND ADDRESS-V WITH 'ADDRESS-SET'
        FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID = ADDRESS-V.ADDRESS-POOL-ID
          FIND CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
            ACCEPT IF CONTACT-TYPE = ORGANIZATION
            PERFORM MANAGE-ARRAY
            SEARCH-ISNS(CURRENT-INDEX)     := *ISN
            ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
            ADD 1 TO CURRENT-INDEX
          END-FIND
        END-FIND
      END-FIND
    END-IF
  END-IF
END-IF
**============
END-SUBROUTINE
**============

**====================================
DEFINE SUBROUTINE FIND-BY-PHONE-NUMBER
**====================================
#IN-PHONE  :=  POCPhone
PERFORM CLEAN-ARRAY
*
FIND PHONE-V WITH AREA-CODE = #AREA-CODE
  ACCEPT IF #DIAL-NUMBER = DIAL-NUMBER
  FIND(1) CONTACT WITH CONTACT.CONTACT-ID = PHONE-V.CONTACT-ID
    ACCEPT IF CONTACT-TYPE = ORGANIZATION
    PERFORM MANAGE-ARRAY
    SEARCH-ISNS(CURRENT-INDEX)  := *ISN
    PHONE-FLAGS(CURRENT-INDEX)  := PHONE-ID
    ADD 1 TO CURRENT-INDEX
  END-FIND
END-FIND
**============
END-SUBROUTINE
**============



**=====================================
DEFINE SUBROUTINE FIND-BY-EMAIL-ADDRESS
**=====================================
PERFORM CLEAN-ARRAY
RESET EMAIL-LENGTH EMAIL-WITH-TRAIL
EXAMINE POCEmail TRANSLATE INTO LOWER CASE
EMAIL-LENGTH     := *LENGTH(POCEmail)
EMAIL-WITH-TRAIL := POCEmail
*
FOR i EMAIL-LENGTH TO 70
  COMPRESS EMAIL-WITH-TRAIL '{' INTO EMAIL-WITH-TRAIL LEAVING NO
END-FOR
*
IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS THEN
  FIND EMAIL-V WITH EMAIL-ADDRESS = SearchOrganization.POCEmail THRU EMAIL-WITH-TRAIL
    FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
      FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
        FIND(1) CONTACT WITH 'CONTACT-SET' AND CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
          ACCEPT IF CONTACT-TYPE = ORGANIZATION
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS(CURRENT-INDEX) := *ISN
          ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
          EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    END-FIND
  END-FIND
ELSE IF CONTACT-SET-EXISTS
    FIND  CONTACT WITH 'CONTACT-SET'
      ACCEPT IF CONTACT-TYPE = ORGANIZATION
      RESET HOLD-ISN
      HOLD-ISN := *ISN
      FIND(1) EMAIL-V WITH EMAIL-V.CONTACT-ID = CONTACT.CONTACT-ID
          AND EMAIL-ADDRESS = SearchOrganization.POCEmail THRU EMAIL-WITH-TRAIL
        PERFORM MANAGE-ARRAY
        SEARCH-ISNS    (CURRENT-INDEX) := HOLD-ISN
        ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
        EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
        ADD 1 TO CURRENT-INDEX
      END-FIND
    END-FIND
  ELSE IF ADDRESS-SET-EXISTS
      FIND EMAIL-V WITH EMAIL-ADDRESS = SearchOrganization.POCEmail THRU EMAIL-WITH-TRAIL
        FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
          FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
            FIND(1) CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
              ACCEPT IF CONTACT-TYPE = ORGANIZATION
              PERFORM MANAGE-ARRAY
              SEARCH-ISNS    (CURRENT-INDEX) := *ISN
              ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
              EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
              ADD 1 TO CURRENT-INDEX
            END-FIND
          END-FIND
        END-FIND
      END-FIND
    ELSE
      FIND EMAIL-V WITH EMAIL-ADDRESS = SearchOrganization.POCEmail THRU EMAIL-WITH-TRAIL
        FIND(1) CONTACT WITH CONTACT.CONTACT-ID = EMAIL-V.CONTACT-ID
          ACCEPT IF CONTACT-TYPE = ORGANIZATION
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS(CURRENT-INDEX)     := *ISN
          ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
          EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    END-IF
  END-IF
END-IF
**============
END-SUBROUTINE
**============

**============================
DEFINE SUBROUTINE MANAGE-ARRAY
**============================
IF CURRENT-INDEX >= UPPER-BOUND
  ADD 5000 TO UPPER-BOUND
  EXPAND ARRAY SEARCH-ISNS    TO (1:UPPER-BOUND)
  EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND)
  EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND)
  EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND)
ELSE
  ESCAPE ROUTINE
END-IF
**============
END-SUBROUTINE
**============

**============================
DEFINE SUBROUTINE CLEAN-ARRAY
**============================
RESET INITIAL UPPER-BOUND CURRENT-INDEX
RESET SEARCH-ISNS(*) ADDRESS-FLAGS(*) EMAIL-FLAGS(*)
EXPAND ARRAY SEARCH-ISNS    TO (1:UPPER-BOUND)
EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND) /* STARTS AT 5000
EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND)
EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND)
**============
END-SUBROUTINE
**============

*
END
