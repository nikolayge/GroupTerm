* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Pass admin contact data back to CRM
*****************************************************************************
* NAME         : MBRSASB                                                    *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                         *
* Program ID   : MBRSASA                                                    *
* DESCRIPTION  : Pass back Contacts and relationships for Gift of membership*
*                BASED ON CRM XML message                                   *
*****************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG          REASON
* NGG    20110501            Initial Creation
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID-NBR         (I4)
1 #CONTACT-ID-IN          (N8/1:V)
1 #CONTACT-ID-CRM         (A/1:V)  DYNAMIC
1 #ERROR-CODE             (A2)
1 #ERROR-DESCRIPTION      (A) DYNAMIC
*
LOCAL
1 #STATUS-ISN             (P8)
1 #XML                    (A) DYNAMIC
1 #LINE                   (A) DYNAMIC
1 #CRM-Request            (I4)
1 #J                      (I4)
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-REMARKS       (A75/1)
1 #Natural-error          (A2) CONST <'-3'>
1 #LOG-ERROR              (A1000)
1 #EFFECTIVE-DATE-A       (A8)
1 #TRL-FILE-NAME          (A)  DYNAMIC
*
END-DEFINE
*
MOVE EDITED *DATX (EM=YYYYMMDD) TO #EFFECTIVE-DATE-A
assign #TRL-FILE-NAME = '/usr/SAG/tmp/'
compress #TRL-FILE-NAME 'XMLerror6974' #EFFECTIVE-DATE-A '.txt' into
  #TRL-FILE-NAME leaving no
DEFINE WORK FILE 4 #TRL-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
FOR #J 1 TO #CONTACT-ID-NBR
  FIND (1) CONTACT WITH CONTACT-ID = #CONTACT-ID-IN ( #J )
    COMPRESS 'CRMID:' #CONTACT-ID-CRM ( #J ) INTO
      CONTACT-REMARKS (1)
    UPDATE
  END-FIND
END-FOR
*
* RESET #ERROR-DESCRIPTION
* ESCAPE ROUTINE /* SKIP AUTO SYNCH NG1
*
ASSIGN #CRM-Request = 2   /* Contacts - if #CRM-Request = 0 no synch message will be send
*
RESET #ERROR-DESCRIPTION
FOR #J 1 TO #CONTACT-ID-NBR   /* Pass back Admin contacts
  CALLNAT 'MBRDATA'
    #STATUS-ISN
    #XML
    #LINE
    #ERROR-DESCRIPTION
    #CRM-Request
    #CONTACT-ID-IN  ( #J )
    #CONTACT-ID-CRM ( #J )
  IF NOT #ERROR-DESCRIPTION EQ ' ' OR= 'Succeeded'
    #LOG-ERROR := #ERROR-DESCRIPTION
    WRITE WORK 4 #LOG-ERROR
    RESET #ERROR-DESCRIPTION
  ELSE
    RESET #ERROR-DESCRIPTION
  END-IF
END-FOR
*
END
