* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Store Application Data
************************************************************************
* NAME         : MBRNAPPL                                                *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Return Quotes for Given Product/AmountsBenefits       *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20100128      Initial Creation
* NGG    09302010  NG2 Populate Linked CN with Application ID
* NGG    08102011  NG3 Update logic is replaced to create contact records
* NGG    20110901  NG4 Synch with CRM with contact ID handshake
* NGG    20120131  NG5 Premier services changes
* NGG    20120703  NG6 Ticket 1276 - Duplicate AT records
* YAK    20170626  YK1 Online App enhancement (store credit cards)
*************************************************************************

DEFINE DATA PARAMETER
1 INPUT-DATA          (A) Dynamic        /* In
1 RESPONSE-DATA       (A) Dynamic        /* Out
1 MESSAGE-ID          (A) Dynamic        /* Out
local
1 Application-Tracking /* view of A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 CONTACT-ID     (N8)
  2 DATE-RECEIVED  (N8)
  2 USER-SOURCE    (A3)
  2 AT-STATUS      (A2)
  2 INTRO-FLAG     (A1)
  2 SMOKER-FLAG    (A1)
  2 INS-ONLY       (A1)
  2 MIL-SERVICE    (A3)
  2 NAME           (A25)
  2 RANK           (A3)
  2 MEMBER-CN      (A6)
  2 REFERING-ID-NUMBER (N6/1)
  2 NOTES          (A75/40)
  2 APPL-SOURCE    (A7)
  2 CAP-IND        (A1)
  2 CRM-APPL-ID    (A50)
  2 PLAN           (A3)
  2 FROM-ONLINE-APP (A1)       /* YK1
*
1 AP-Tracking view of A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 CONTACT-ID     (N8)
  2 DATE-RECEIVED  (N8)
  2 USER-SOURCE    (A3)
  2 AT-STATUS      (A2)
  2 INTRO-FLAG     (A1)
  2 SMOKER-FLAG    (A1)
  2 INS-ONLY       (A1)
  2 MIL-SERVICE    (A3)
  2 MIL-STATUS     (A1)
  2 NAME           (A25)
  2 RANK           (A3)
  2 SSN            (N9)
  2 DATE-OF-BIRTH  (N8)
  2 SEX            (A1)
  2 MEMBER-CN      (A6) 2 REDEFINE MEMBER-CN
    3 MEMBER-CN-N  (N6)
  2 REFERING-ID-NUMBER (N6/1)
  2 NOTES          (A75/40) 2 REDEFINE NOTES
    3 NOTES-A (I1/3000)
  2 ST-NOTE-USER   (A3/1)
  2 ST-NOTE-STATUS (A2/1)
  2 ST-NOTE-DATE (N8/1) 2 redefine ST-NOTE-DATE
    3 ST-NOTE-DATE-A (A8)
  2 APPL-SOURCE    (A7)
  2 CAP-IND        (A1)
  2 CRM-APPL-ID    (A50)
  2 PLAN           (A3)
*
1 AT-EXIST view of A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 AT-STATUS      (A2)
  2 DATE-OF-BIRTH  (N8)
*
1 STATUS VIEW OF A-STATUS
  2 LINKED-CN      (N6/30)
*
1 #AT-NUMBER        (N6)
1 #AT-LAST          (A) DYNAMIC
1 #AT-FIRST         (A) DYNAMIC
1 #AT-MIDDLE        (A) DYNAMIC
1 #AT-EXIST         (L)
1 #ERROR-CODE        (A2)
1 #ERROR-DESCRIPTION (A) DYNAMIC
1 #MESSAGE-LOG      (I4) CONST <2>
1 #ERROR-LOG        (I4) CONST <3>
1 BLANK             (A1) CONST <' '>
1 #Timeout          (A2) CONST <'-2'>
1 #Natural-error    (A2) CONST <'-3'>
1 #No-AT#-available (A2) CONST <'-5'>
1 #No-AT#-found     (A2) CONST <'-6'>
1 #Duplicate-AT     (A2) CONST <'-8'>
1 #CRM-Status       (A2) CONST <'SD'>    /* CONST <'SD'>  CONST 'PR' back to SD
1 #DATE-A           (A10)
1 #J                (I4)
1 #UPDATE           (L/12)
1 #AT-ISN           (P8)
1 #STATUS-ISN       (P8)                 /* NG4
1 #TRACKING-ID      (A) DYNAMIC
1 #P-SERVICE-IND             (A1)        /* NG5
1 #REQUEST                   (I4)        /* NG5
1 #ERRORS                    (L)         /* NG5
1 #SVC-M-RATE                (A7)        /* NG5
LOCAL USING GPRL0EML
LOCAL USING MBRASVC                      /* NG5
END-DEFINE
*
RESET RESPONSE-DATA
  #AT-LAST #AT-FIRST #AT-MIDDLE
  #UPDATE (*)
*
CALLNAT 'MBRAPPL' INPUT-DATA RESPONSE-DATA Application-Tracking
  #UPDATE (*)
  ServiceRequest                         /* NG5
MESSAGE-ID := Application-Tracking.CRM-APPL-ID
*
IF SUBSTR (RESPONSE-DATA,1,10) = 'Failed....'
  MOVE SUBSTR ( RESPONSE-DATA,11 ) TO RESPONSE-DATA
  ESCAPE ROUTINE
END-IF
*     NG6 Start
FIND AT-EXIST WITH CRM-APPL-ID = Application-Tracking.CRM-APPL-ID
  IF NO RECORDS
    ESCAPE BOTTOM
  END-NOREC
  RESPONSE-DATA := 'Duplicate CRM Quote GUI'
  COMPRESS #Duplicate-AT RESPONSE-DATA Application-Tracking.CRM-APPL-ID
    INTO RESPONSE-DATA WITH DELIMITER ':'
  ESCAPE ROUTINE
END-FIND
*     NG6 End
MOVE SUBSTR ( RESPONSE-DATA,11 ) TO RESPONSE-DATA
MOVE BY NAME Application-Tracking TO AP-Tracking
CALLNAT 'C1014SAT' AP-Tracking.ID-NUMBER
IF AP-Tracking.ID-NUMBER = 0
  RESPONSE-DATA := 'There is no AT record tracking number available'
  COMPRESS #No-AT#-available RESPONSE-DATA Application-Tracking.ID-NUMBER
    INTO RESPONSE-DATA WITH DELIMITER ':'
  ESCAPE ROUTINE
ELSE
  AP-Tracking.AT-STATUS   := #CRM-Status
  AP-Tracking.ST-NOTE-STATUS (1) := #CRM-Status
  AP-Tracking.ST-NOTE-USER   (1) := Application-Tracking.USER-SOURCE
  MOVE EDITED *DATX (EM=YYYYMMDD) TO ST-NOTE-DATE-A
  FOR #J 1 TO 3000
    IF AP-Tracking.NOTES-A (#J) < 32 OR AP-Tracking.NOTES-A (#J) > 127
      ASSIGN NOTES-A (#J) = 32
    END-IF
  END-FOR
  IF AP-Tracking.MEMBER-CN NE BLANK
    FIND STATUS WITH ID-NUMBER = AP-Tracking.MEMBER-CN-N
      IF NO RECORDS FOUND
        RESET AP-Tracking.MEMBER-CN
      END-NOREC
    END-FIND
  END-IF
  IF AP-Tracking.REFERING-ID-NUMBER (1) NE 0
    FIND STATUS WITH ID-NUMBER = AP-Tracking.REFERING-ID-NUMBER (1)
      IF NO RECORDS FOUND
        RESET AP-Tracking.REFERING-ID-NUMBER (1)
        ESCAPE BOTTOM
      END-NOREC
      IF AP-Tracking.INTRO-FLAG = 'S' OR = 'K' OR = 'G'  /* NG2
        FOR #J 1 TO 30
          IF STATUS.LINKED-CN ( #J ) = 0
            STATUS.LINKED-CN ( #J ) := AP-Tracking.ID-NUMBER
            UPDATE (0168)
            ESCAPE BOTTOM
          END-IF
        END-FOR
      END-IF
    END-FIND
  END-IF
  STORE-AT.
  STORE AP-Tracking
  #AT-ISN := *ISN ( STORE-AT. )
  ASSIGN #UPDATE (10) = TRUE
END-IF
*
IF #UPDATE (*)
  END TRANSACTION             /* Phase 1 transaction = CRM request
/* SYNCH AT RECORD WITH CONTACT DATA and create Status
  CALLNAT 'GOATN001'
    AP-Tracking.CONTACT-ID
    AP-Tracking.ID-NUMBER
    Application-Tracking.FROM-ONLINE-APP     /* YK1
    #ERROR-DESCRIPTION
  IF #ERROR-DESCRIPTION EQ BLANK
    END TRANSACTION           /* Phase 2 transaction = Synch process
    IF SERVICE-ID (1) = 'TRUE'  /* NG5
      #REQUEST := 2
      #P-SERVICE-IND := 'Y'
      CALLNAT 'C1011NAT'
        AP-Tracking.ID-NUMBER
        #P-SERVICE-IND
        #REQUEST                /* 1=GET, 2=UPDATE 3=CHANGE
        #ERROR-DESCRIPTION
        #ERRORS
        #SVC-M-RATE
        CRM-SERVICE-ID (1)
      END TRANSACTION           /* Phase 3 transaction = premier services
    END-IF
    GET AP-Tracking #AT-ISN   /* NG4 Start
    IF AP-Tracking.INTRO-FLAG = 'S' OR= 'K' OR= 'G'
      FIND (1) STATUS WITH ID-NUMBER = MEMBER-CN-N
        #STATUS-ISN := *ISN (0215)
      END-FIND
      CALLNAT 'MBRNEWAI'
        #AT-ISN
        #STATUS-ISN
    ELSE
      CALLNAT 'MBRNEWAI'
        #AT-ISN
        #STATUS-ISN
        RESPONSE-DATA
    END-IF                     /* NG4 End
  ELSE
    BACKOUT TRANSACTION
    CALLNAT 'CRMLOG' #ERROR-CODE #ERROR-DESCRIPTION
      #ERROR-LOG
      MESSAGE-ID
  END-IF
  RESPONSE-DATA := AP-Tracking.ID-NUMBER
ELSE
  BACKOUT TRANSACTION
END-IF
/*
ON ERROR
  BACKOUT TRANSACTION
  RESET #ERROR-CODE
  CALLNAT 'CRMLOG' #ERROR-CODE #ERROR-DESCRIPTION
    #ERROR-LOG
    MESSAGE-ID
  PERFORM FORMAT-ERROR
  ESCAPE ROUTINE
END-ERROR
*
DEFINE FORMAT-ERROR
/*     ============
BACKOUT TRANSACTION
COMPRESS #ERROR-CODE #ERROR-DESCRIPTION INTO RESPONSE-DATA WITH DELIMITERS ':'
END-SUBROUTINE
*
END
