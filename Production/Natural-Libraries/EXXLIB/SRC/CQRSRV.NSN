* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram CQRSRV.
/**
/** :author nguentchev
/* TODO: Enter your code here
* DESCRIPTION  : Customer Quote Request                                *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20121101       Initial Creation
*
DEFINE DATA
PARAMETER
1 FUNCTION-NAME       (A) Dynamic        /* In
1 INPUT-DATA          (A) Dynamic        /* In
1 RESPONSE-DATA       (A) Dynamic        /* Out
*
LOCAL
1 #MESSAGE            (A) DYNAMIC
1 #MESSAGE-ID         (A) DYNAMIC
1 #ERROR-CODE         (I4)
*   CQRNPRS
1 #QuoteRequest       (A25) CONST <'QuoteRequest'>
1 #QuickQuoteRequest  (A25) CONST <'QuickQuoteRequest'>

1 #ErrorTag           (A) DYNAMIC
1 #Unsupported        (A2) CONST <'-1'>
1 #Timeout            (A2) CONST <'-2'>
1 #Natural-error      (A2) CONST <'-3'>
1 #MESSAGE-LOG        (I4) CONST <2>
1 #ERROR-LOG          (I4) CONST <3>
1 #REQUEST-LOG        (I4) CONST <4>
1 #UPDATE-LOG         (I4) CONST <5>
*
1 #CustomerQuote      (A) DYNAMIC CONST <'CustomerQuote'>
1 #QQREQUEST          (L) 
1 #J         (I4)
END-DEFINE
*
ON ERROR
  IF *ERROR-NR = 3009
    CALLNAT 'CQRSRV' FUNCTION-NAME INPUT-DATA RESPONSE-DATA
  ELSE
    COMPRESS *DATN *TIMN FUNCTION-NAME INPUT-DATA INTO #MESSAGE
    CALLNAT 'CQRLOG' #ERROR-CODE #MESSAGE
      #ERROR-LOG #ErrorTag
  END-IF
  RESPONSE-DATA := #MESSAGE
  ESCAPE ROUTINE
END-ERROR
*
COMPRESS *DATN *TIMN FUNCTION-NAME INPUT-DATA INTO #MESSAGE
CALLNAT 'CQRLOG' #ERROR-CODE #MESSAGE #REQUEST-LOG
*
* Parse CUSTOMER-QUOTE-REQUEST
*
DECIDE ON FIRST VALUE OF FUNCTION-NAME
  VALUE #QuoteRequest
    #ErrorTag := #CustomerQuote
    CALLNAT 'CQRN0001' INPUT-DATA RESPONSE-DATA
  VALUE #QuickQuoteRequest
    #ErrorTag := #CustomerQuote
    #QQREQUEST := TRUE
    CALLNAT 'CQRN0001' INPUT-DATA RESPONSE-DATA #QQREQUEST
  NONE VALUE
    #ErrorTag := 'CustomerQuote'
    #ERROR-CODE := -1
    #MESSAGE := 'Unsupported request'
    CALLNAT 'CQRLOG' #ERROR-CODE #MESSAGE
      #ERROR-LOG #ErrorTag
    RESPONSE-DATA := #MESSAGE
END-DECIDE
*
#J := *LENGTH ( RESPONSE-DATA )
IF #J > 0
  COMPRESS *DATN *TIMN FUNCTION-NAME RESPONSE-DATA INTO #MESSAGE
  CALLNAT 'CQRLOG' #ERROR-CODE #MESSAGE #MESSAGE-LOG
END-IF
*
END
