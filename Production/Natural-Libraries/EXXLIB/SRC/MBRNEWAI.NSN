* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Pass Member data to Sales CRM
************************************************************************
* NAME         : MBRNEWAI                                              *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Pass member ID to the CRM                             *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20100128      Initial Creation
* NGG    20111010   NG1 PASS SYNCH MESSAGE WITH RELATIONS
* NGG    08/07/2017 NG3 Online app changes
*********************************************************
DEFINE DATA
PARAMETER
1 #APPL-ISN          (P8)
1 #STATUS-ISN        (P8)
1 #XML-TEMPLATE      (A) DYNAMIC OPTIONAL
LOCAL
1 #CRM-Result        (A)  DYNAMIC
1 APPL-TRACK-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER        (N6)
  2 MEMBER-CN        (A6)
  2 CRM-APPL-ID      (A50)
1 NEW-V VIEW OF A-STATUS
  2 ID-NUMBER    (N6)
  2 INTRO-FLAG   (A1)
  2 PROCESS-IND  (A1)
1 #CRM-APPLICATION-ID  (A) DYNAMIC
1 #TRACKING-ID   (A) DYNAMIC
1 #NEW-MEMBER    (I4) CONST <4>  /* CODE OF THE CRM FUNCTION
END-DEFINE
*
GET APPL-TRACK-V #APPL-ISN
*                           NG1 Start
IF #XML-TEMPLATE SPECIFIED
  COMPRESS '<TrackingID>' APPL-TRACK-V.ID-NUMBER '</TrackingID>'
    INTO #TRACKING-ID LEAVING NO
  EXAMINE #XML-TEMPLATE FOR '<TrackingID>@@@@@@</TrackingID>'
    REPLACE WITH #TRACKING-ID
*  COMPRESS '<CN>' APPL-TRACK-V.MEMBER-CN 'P' '</CN>'  /* NG3
  COMPRESS '<CN>' APPL-TRACK-V.MEMBER-CN  '</CN>'      /* NG3
    INTO #TRACKING-ID LEAVING NO
  EXAMINE #XML-TEMPLATE FOR '<CN>@----@</CN>'
    REPLACE WITH #TRACKING-ID
  CALLNAT 'CRMSYNC' #XML-TEMPLATE (AD=O) #NEW-MEMBER (AD=O) #CRM-Result (AD=M)
ELSE                 /*    NG1 end
  MOVE APPL-TRACK-V.CRM-APPL-ID TO #CRM-APPLICATION-ID
  GET NEW-V #STATUS-ISN
  CALLNAT 'MBRNEW'
    APPL-TRACK-V.ID-NUMBER
    NEW-V.ID-NUMBER
    NEW-V.INTRO-FLAG
    NEW-V.PROCESS-IND
    #CRM-APPLICATION-ID
    #CRM-Result
END-IF
*
END
