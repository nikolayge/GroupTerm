* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**@AUTHOR: TRAVIS TINGER
**   DATE:  07-29-2014
**************************
DEFINE DATA
PARAMETER USING AUCAPORG
PARAMETER
*
1 SEARCH-ISNS             (P10/1:*)
1 FileName               (A) DYNAMIC
LOCAL USING ADDA0001
LOCAL
1 i              (I4)
1 MSG            (A) DYNAMIC
1 USER-NAME      (A) DYNAMIC
1 DIRECTORY      (A) DYNAMIC INIT<'/usr/SAG/tmp/AUC/'>
*
1 p_FTP_PS
  2 p_PARM     (A)  DYNAMIC
  2 p_MSG      (A)  DYNAMIC
  2 p_RC       (N3)
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 ID-NUMBER
  2 FULL-NAME
    3 FIRST-NAME (A25)
    3 MIDDLE-NAME (A25)
    3 LAST-NAME (A25)
    3 SUFFIX (A10)
  2 DATE-OF-BIRTH (N8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8)
  2 FIRM-ESTABLISHED-DATE
  2 FIRM-ORGANIZATION-NAME
*
1 OUTPUT-FIELDS
  2 #ID-NUMBER           (N6)
  2 #CONTACT-ID          (N8)
  2 #SSN                 (A11)
  2 #ORGANIZATION-NAME   (A70)
  2 #FIRST-NAME          (A25)
  2 #LAST-NAME           (A25)
  2 #ESTABLISHED-DATE    (A10)
  2 #DATE-OF-BIRTH       (A10)
  2 #POC-PHONE           (A20)
  2 #POC-EMAIL           (A70)
  2 #ADDRESS-1           (A75)
  2 #ADDRESS-2           (A75)
  2 #ADDRESS-3           (A75)

1 TAB                  (A1) CONSTANT <H'09'>
1 FILE-NAME            (A)  DYNAMIC
1 #OUTPUT              (A1000)
*
1 SSN-REDEFINED          (A9)
1 REDEFINE SSN-REDEFINED
  2 XXX                  (A3)
  2 XX                   (A2)
  2 XXXX                 (A4)
*
1 DATE-YYYYMMDD          (N10)
1 REDEFINE DATE-YYYYMMDD
  2 YYYY                 (A4)
  2 MM                   (A2)
  2 DD                   (A2)
1 COLUMN-HEADERS
  2 #H-ID-NUMBER          (A)   DYNAMIC CONSTANT <'CN'>
  2 #H-CONTACT-ID         (A)   DYNAMIC CONSTANT <'Member Contact ID'>
  2 #H-SSN                (A)   DYNAMIC CONSTANT <'Tax ID / SSN'>
  2 #H-ORGANIZATION-NAME  (A)   DYNAMIC CONSTANT <'Organization Name'>
  2 #H-FIRST-NAME         (A)   DYNAMIC CONSTANT <'POC First Name'>
  2 #H-LAST-NAME          (A)   DYNAMIC CONSTANT <'POC Last Name'>
  2 #H-ESTABLISHED-DATE   (A)   DYNAMIC CONSTANT <'Established Date'>
  2 #H-DOB                (A)   DYNAMIC CONSTANT <'Date of Birth'>
  2 #H-POC-PHONE          (A)   DYNAMIC CONSTANT <'POC Phone Number'>
  2 #H-POC-EMAIL          (A)   DYNAMIC CONSTANT <'POC Email'>
  2 #H-ADD-1              (A)   DYNAMIC CONSTANT <'Address line 1'>
  2 #H-ADD-2              (A)   DYNAMIC CONSTANT <'Address line 2'>
  2 #H-ADD-3              (A)   DYNAMIC CONSTANT <'Address line 3'>
*
1 #NO-ADVERTISING-BES     (A)   DYNAMIC CONSTANT <'B/E/S (No Advertising)'>
1 #NO-EMAIL               (A)   DYNAMIC CONSTANT <'No Email'>
1 #NO-ANNUAL-STATEMENTS   (A)   DYNAMIC CONSTANT <'No Annual Statements'>
1 #NO-SALES-ADVERTISING   (A)   DYNAMIC CONSTANT <'No Sales Advertising'>
END-DEFINE
*
USER-NAME  := FileName
COMPRESS '/usr/SAG/tmp/AUC/' FileName '_Search.txt' INTO FILE-NAME LEAVING NO
*
DEFINE WORK FILE 1 FILE-NAME TYPE 'ASCII-COMPRESSED'
*
COMPRESS COLUMN-HEADERS INTO #OUTPUT WITH DELIMITER TAB
WRITE WORK 1 #OUTPUT
*
FOR i 1 TO PageOrganization.TotalFound
  GET CONTACT SEARCH-ISNS(i)
  #ID-NUMBER             := ID-NUMBER
  #CONTACT-ID            := CONTACT-ID
*
  RESET SSN-REDEFINED
  SSN-REDEFINED          := CONTACT.SSN
  COMPRESS XXX '-' XX '-' XXXX INTO #SSN LEAVING NO
*
  #ORGANIZATION-NAME     := *TRIM(FIRM-ORGANIZATION-NAME)
  #FIRST-NAME            := *TRIM(FIRST-NAME)
  #LAST-NAME             := *TRIM(LAST-NAME)
*
  RESET DATE-YYYYMMDD
  DATE-YYYYMMDD         := FIRM-ESTABLISHED-DATE
  COMPRESS MM '/' DD '/' YYYY INTO #ESTABLISHED-DATE LEAVING NO
*
  RESET DATE-YYYYMMDD
  DATE-YYYYMMDD          := DATE-OF-BIRTH
  COMPRESS MM '/' DD '/' YYYY INTO #DATE-OF-BIRTH LEAVING NO
*
  #POC-PHONE             := *TRIM(F-PREF-PHONE(<CONTACT-ID>))
  IF #POC-PHONE  = ' '
    #POC-PHONE   := 'n/a'
  ELSE
    #POC-PHONE           := *TRIM(F-PREF-PHONE(<CONTACT-ID>))
  END-IF
*
  #POC-EMAIL             := FUNCTION-PREFFERED-EMAIL(< CONTACT-ID >)
  IF #POC-EMAIL = ' '
    #POC-PHONE   := 'n/a'
  ELSE
    #POC-EMAIL           := *TRIM(#POC-EMAIL)
  END-IF
*
  RESET #PDA-ADDRESS
  #ADD-TYPE        := 'P'
  #ADD-CONTACT-ID  := CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
*
  #ADDRESS-1             := *TRIM(#ADD-LINE-1)
  #ADDRESS-2             := *TRIM(#ADD-LINE-2)
  #ADDRESS-3             := *TRIM(#ADD-LINE-3)
*
  COMPRESS OUTPUT-FIELDS INTO #OUTPUT WITH DELIMITER TAB
  WRITE WORK 1 #OUTPUT
  RESET OUTPUT-FIELDS #OUTPUT CONTACT
END-FOR
*
CLOSE WORK FILE 1
*
COMPRESS DIRECTORY ','USER-NAME '_Search.txt , /ftp/AUC_SearchResults , 'USER-NAME '_Search.txt' INTO p_PARM LEAVING NO
CALLNAT 'FTP-PS' p_FTP_PS
*
END
