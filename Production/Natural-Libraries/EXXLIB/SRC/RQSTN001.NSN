* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Request from SSRS - List of policies/emails to be sent
* Object name - RQSTN001
***********************************************************************
*        MODIFICATION LOG
***********************************************************************
* USER   DATE      TAG  REASON
* YAK    08032010       Initial Creation
* YAK    04152018       Rewritten to create list of emails by template name 
***********************************************************************
*
DEFINE DATA PARAMETER
*
1 REQUEST-DATA      (A) DYNAMIC
1 RESPONSE-DATA     (A) DYNAMIC         /* Out
*
LOCAL
1 LT-V VIEW OF A-LETTERS
  2 LAST-LOAD-DATE
  2 INSURED-CN
  2 POLICY-NUMBER
  2 TEMPLATE-NAME
  2 EMAIL-ADDRESS
  2 DATE-SENT
  2 IS-SENT
*
1 #ErrorMessage                       (A) DYNAMIC
1 #TEMPLATE-NAME                      (A) DYNAMIC
1 XML                                 (A) DYNAMIC
*
END-DEFINE
*
XML := REQUEST-DATA
EXAMINE XML FOR '<EmailList>'  DELETE
EXAMINE XML FOR '</EmailList>' DELETE
#TEMPLATE-NAME := *TRIM(XML)
*
XML := '<EmailList>'
FIND LT-V WITH TEMPLATE-NAME = #TEMPLATE-NAME
  ACCEPT IF IS-SENT = ' '
  IF LT-V.EMAIL-ADDRESS <> ' '
    COMPRESS XML '<EmailItem>'
      '<PolicyNumber>' LT-V.POLICY-NUMBER '</PolicyNumber>'
      '<EmailAddress><![CDATA[' LT-V.EMAIL-ADDRESS ']]></EmailAddress>'
      '</EmailItem>' INTO XML LEAVING NO
  END-IF
END-FIND
COMPRESS XML '</EmailList>' INTO XML LEAVING NO
RESPONSE-DATA := XML
*
END
