* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Write log file to create a report for Address verification
* Called from AUC/Address process
*
*****************************************************************
*        MODIFICATION LOG
*****************************************************************
* USER   DATE      TAG  REASON
* YAK    06122012       Initial Creation
*****************************************************************
*
DEFINE DATA
PARAMETER
1 #ID-NUMBER       (N6)
1 #RESPONSE        (A) DYNAMIC
1 #USER-ID         (A8)
1 #STREET          (A75)
1 #UNIT            (A25)
1 #CITY            (A50)
1 #STATE-CD        (A2)
1 #ZIP             (N9)
LOCAL
1 #RETURN-CODE           (A) DYNAMIC
1 #ERROR-CODE            (A) DYNAMIC
1 #NO-MATCH-FOUND-CD     (A) DYNAMIC CONST <'-99'> 
1 #NO-EXACT-MATCH        (A) DYNAMIC CONST <'-1'>
1 #ERROR-MSG             (A) DYNAMIC
1 #NO-ZIP4-MSG           (A) DYNAMIC INIT <'No ZIP+4 returned'>
1 #REJECTED-MSG          (A) DYNAMIC INIT <'Address change rejected by user'>
1 #NO-MATCH-FOUND-MSG    (A) DYNAMIC INIT <'No match found'>
1 #PROCESS-TYPE          (A) DYNAMIC INIT <'AddressValidation'>
1 #LOG-MESSAGE           (A) DYNAMIC
1 #I                     (I2) 
1 #ZIP-A                 (A) DYNAMIC
1 #ZIP-N                 (N9)
1 REDEFINE #ZIP-N
  2 #ZIP-5               (N5)  
  2 #ZIP-4               (N4)    
END-DEFINE
*
SEPARATE #RESPONSE INTO #RETURN-CODE #ERROR-CODE WITH DELIMITER ':'
DECIDE FOR FIRST CONDITION
  WHEN #RETURN-CODE = #NO-MATCH-FOUND-CD
    #ERROR-MSG := #NO-MATCH-FOUND-MSG
  WHEN #RETURN-CODE = #NO-EXACT-MATCH
    EXAMINE #ERROR-CODE FOR '07' NUMBER #I 
    IF #I <> 0 
      #ERROR-MSG := #NO-ZIP4-MSG
    ELSE  
      #ERROR-MSG := #REJECTED-MSG
    END-IF  
  WHEN NONE
    #ERROR-MSG := #REJECTED-MSG
END-DECIDE
*
IF #ERROR-MSG NE ' '
  #ZIP-N := #ZIP
  IF #ZIP-4 <> 0
    COMPRESS FULL #ZIP-5 '-' #ZIP-4 INTO #ZIP-A 
  ELSE
    MOVE EDITED #ZIP-5 (EM=99999) TO #ZIP-A
  END-IF
  COMPRESS *DATU FUNCTION-NAME(<#USER-ID>) #ID-NUMBER #STREET #UNIT #CITY
      #STATE-CD #ZIP-A '-' #ERROR-MSG INTO #LOG-MESSAGE
  CALLNAT 'GPRN0005' #PROCESS-TYPE #LOG-MESSAGE
END-IF
END
