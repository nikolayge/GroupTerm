* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* DESCRIPTION
*   XML Parser implementationfor file:
*   I:\git\MSS\MembershipStatistics\Natural-Libraries\MS\SearchInd.xsd
*   using PARSE XML statement and datastructure 'AUCASI02'
*
* ----------------------------------------------------------------------
*
DEFINE DATA
PARAMETER USING AUCASIND
*
PARAMETER
1 SEARCH-ISNS             (P10/1:*)
1 ADDRESS-FLAGS           (A1/1:*)
1 EMAIL-FLAGS             (N8/1:*)
1 PHONE-FLAGS             (N8/1:*)
*
LOCAL

1 BLANK                   (A1)CONSTANT <' '>
1 TRAILING-NINES          (A9)
1 TRAILING-ZEROS          (A9)
1 SSN-MAX-DIGITS          (I2)INIT <9>
1 CONTACT-SET-EXISTS      (L) INIT <FALSE>
1 INDIVIDUAL              (A1)CONSTANT <'I'>
1 i                       (I4)
1 cha   (a1) constant <H'8A'>
1 #IN-PHONE           (N10)
1 REDEFINE #IN-PHONE
  2 #AREA-CODE   (N3)
  2 #DIAL-NUMBER (N7)
*
1 #ZIP-CODE      (N9)
1 REDEFINE #ZIP-CODE
  2 #ZIP-4           (A4)
  2 #ZIP-5           (A5)
*
1 NAME-LENGTH          (I4)
1 NAME-WITH-9S        (A25)
1 EMAIL-LENGTH         (I4)
1 EMAIL-WITH-9S       (A70)
1 HOLD-SSN             (N9)
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 ID-NUMBER
  2 CONTACT-TYPE
  2 FULL-NAME
    3 FIRST-NAME (A25)
    3 MIDDLE-NAME (A25)
    3 LAST-NAME (A25)
    3 SUFFIX (A10)
  2 DATE-OF-BIRTH (N8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8)
*
1 PHONE-V VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
*
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID (N8)
  2 CONTACT-ID (N8)
  2 EMAIL-ADDRESS (A70)
*
1 ADDRESS-V VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID (N8)
  2 CITY (A50)
  2 STATE-CODE (A2)
  2 ZIP-CODE (N9)
*
1 ADDRESS-RELATIONSHIP VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID (N8)
  2 ADDR-TYPE-CD
  2 ADDRESS-POOL-ID (N8)
*
1 SSN-NOT-FOUND(L)
1 LAST-FOUR-DIGITS(N4)
1 #SSN-NUMBER             (N9.0)
1 REDEFINE #SSN-NUMBER
  2 #SSN-STRING           (A9)
1 #SSN-NUMBER-MAX         (N9.0)
1 REDEFINE #SSN-NUMBER-MAX
  2 #SSN-STRING-MAX       (A9)
1 SSN-TEXT                (A9)
1 DYNAMIC-SSN             (A) DYNAMIC
1 SSN-LENGTH              (I2)
1 ADDRESS-SET-EXISTS      (L) INIT <FALSE>
1 NEXT-LETTER             (A1)
1 FIRST-LETTER            (A1)
1 #PARAMETER              (A25)
*
1 HOLD-ISN                (P10)
1 HOLD-ADD-ISN            (P10)
1 UPPER-BOUND             (I4) INIT <5000>
1 CURRENT-INDEX           (I4) INIT <1>
*
1 ZIP-WITH-ZEROS      (N9)
1 REDEFINE ZIP-WITH-ZEROS
  2 ZIP-WITH-ZEROS-A   (A9)
1 ZIP-WITH-NINES      (N9)
1 REDEFINE ZIP-WITH-NINES
  2 ZIP-WITH-NINES-A   (A9)
1 SEARCH-RESULTS       (A) DYNAMIC
1 NUMBER-OF-RESULTS    (I4)
END-DEFINE
*
EXPAND ARRAY SEARCH-ISNS    TO (1:UPPER-BOUND) /* STARTS AT 5000
EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND) /* STARTS AT 1000
EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND) /* STARTS AT 1000
EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND) /* STARTS AT 1000


DECIDE FOR EVERY CONDITION
  WHEN SearchIndividual.SSN                NE BLANK                PERFORM FIND-BY-SSN
  WHEN SearchIndividual.DateOfDeath        NE 0                    PERFORM FIND-BY-DOD
  WHEN SearchIndividual.DateOfBirth        NE 0                    PERFORM FIND-BY-DOB
  WHEN SearchIndividual.LastName           NE BLANK                PERFORM FIND-BY-LAST-NAME
  WHEN SearchIndividual.FirstName          NE BLANK                PERFORM FIND-BY-FIRST-NAME
  WHEN SearchIndividual.ZIP                NE 0                    PERFORM FIND-BY-ZIP-CODE
  WHEN SearchIndividual.City               NE BLANK                PERFORM FIND-BY-CITY
  WHEN SearchIndividual.State              NE BLANK                PERFORM FIND-BY-STATE
  WHEN CONTACT-SET-EXISTS OR ADDRESS-SET-EXISTS                    PERFORM ADD-TO-ISN-ARRAY
  WHEN SearchIndividual.Email              NE BLANK                PERFORM FIND-BY-EMAIL-ADDRESS
  WHEN SearchIndividual.Phone              NE 0                    PERFORM FIND-BY-PHONE-NUMBER
  WHEN NONE
    IGNORE
END-DECIDE
*
SearchIndividual.TotalFound :=   CURRENT-INDEX - 1            /* UPPER-BOUND?99
*
IF CONTACT-SET-EXISTS
  RELEASE SET 'CONTACT-SET'
END-IF
IF ADDRESS-SET-EXISTS
  RELEASE SET 'ADDRESS-SET'
END-IF

**

**============================
DEFINE SUBROUTINE FIND-BY-SSN
**===========================
HOLD-SSN   := VAL(SearchIndividual.SSN)
*
SSN.
FIND NUMBER CONTACT WITH SSN = HOLD-SSN
  RETAIN AS 'CONTACT-SET'
*
IF *NUMBER( SSN.) = 0 THEN
  PERFORM ADD-TRAILING-NUMS
  FIND NUMBER CONTACT WITH SSN >= #SSN-NUMBER AND SSN <= #SSN-NUMBER-MAX
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============



**==================================
DEFINE SUBROUTINE ADD-TRAILING-NUMS
**==================================
DYNAMIC-SSN := *TRIM(SearchIndividual.SSN)
#SSN-STRING := SearchIndividual.SSN
SSN-LENGTH  := *LENGTH(SearchIndividual.SSN)
RESET i
FOR i SSN-LENGTH TO SSN-MAX-DIGITS
  COMPRESS '0' TRAILING-ZEROS INTO TRAILING-ZEROS LEAVING NO
  COMPRESS '9' TRAILING-NINES INTO TRAILING-NINES LEAVING NO
END-FOR
*
COMPRESS  #SSN-STRING TRAILING-NINES    INTO #SSN-STRING-MAX   LEAVING NO
COMPRESS  #SSN-STRING TRAILING-ZEROS    INTO #SSN-STRING       LEAVING NO
**============
END-SUBROUTINE
**============

**===========================
DEFINE SUBROUTINE FIND-BY-DOD
**===========================
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND DATE-OF-DEATH = SearchIndividual.DateOfDeath
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH DATE-OF-DEATH = SearchIndividual.DateOfDeath
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============



**===========================
DEFINE SUBROUTINE FIND-BY-DOB
**===========================
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND DATE-OF-BIRTH = SearchIndividual.DateOfBirth
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH  DATE-OF-BIRTH = SearchIndividual.DateOfBirth
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============



**=================================
DEFINE SUBROUTINE FIND-BY-LAST-NAME
**=================================
RESET NAME-LENGTH NAME-WITH-9S
NAME-LENGTH := *LENGTH(LastName)
NAME-WITH-9S := LastName
*
FOR i NAME-LENGTH TO 25
  COMPRESS NAME-WITH-9S '[' INTO NAME-WITH-9S LEAVING NO
END-FOR
*
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND LAST-NAME = LastName THRU NAME-WITH-9S
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH LAST-NAME = LastName THRU NAME-WITH-9S
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE
**============
END-SUBROUTINE
**============


**==================================
DEFINE SUBROUTINE FIND-BY-FIRST-NAME
**==================================
RESET NAME-LENGTH NAME-WITH-9S
NAME-LENGTH := *LENGTH(FirstName)
NAME-WITH-9S := FirstName
*
FOR i NAME-LENGTH TO 25
  COMPRESS NAME-WITH-9S '[' INTO NAME-WITH-9S LEAVING NO
END-FOR
*
IF CONTACT-SET-EXISTS
  FIND NUMBER CONTACT WITH 'CONTACT-SET' AND FIRST-NAME = FirstName THRU NAME-WITH-9S
    RETAIN AS 'CONTACT-SET'
ELSE
  FIND NUMBER CONTACT WITH FIRST-NAME = FirstName THRU NAME-WITH-9S
    RETAIN AS 'CONTACT-SET'
END-IF
CONTACT-SET-EXISTS   := TRUE

**============
END-SUBROUTINE
**============


**=================================
DEFINE SUBROUTINE ADD-TO-ISN-ARRAY
**=================================
IF MembersOnly THEN
  IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS
    FIND CONTACT WITH 'CONTACT-SET' AND CONTACT.ID-NUMBER >= 1900
      ACCEPT IF CONTACT-TYPE = INDIVIDUAL
      RESET HOLD-ISN
      HOLD-ISN := *ISN
      FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = CONTACT.CONTACT-ID
        FIND(1) ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS    (CURRENT-INDEX) := HOLD-ISN
          ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    END-FIND
  ELSE
    IF CONTACT-SET-EXISTS
      FIND CONTACT WITH 'CONTACT-SET' AND CONTACT.ID-NUMBER >= 1900
        ACCEPT IF CONTACT-TYPE = INDIVIDUAL
        PERFORM MANAGE-ARRAY
        SEARCH-ISNS    (CURRENT-INDEX)  := *ISN
        ADDRESS-FLAGS  (CURRENT-INDEX)  := 'P'
        ADD 1 TO CURRENT-INDEX
      END-FIND
    ELSE
      IF ADDRESS-SET-EXISTS
        FIND ADDRESS-V WITH 'ADDRESS-SET'
          FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID = ADDRESS-V.ADDRESS-POOL-ID
            FIND CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID AND CONTACT.ID-NUMBER >= 1900
              ACCEPT IF CONTACT-TYPE = INDIVIDUAL
              PERFORM MANAGE-ARRAY
              SEARCH-ISNS(CURRENT-INDEX)     := *ISN
              ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
              ADD 1 TO CURRENT-INDEX
            END-FIND
          END-FIND
        END-FIND
      END-IF
    END-IF
  END-IF
ELSE
  IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS
    FIND CONTACT WITH 'CONTACT-SET'
      IF CONTACT.ID-NUMBER > 0 AND CONTACT.ID-NUMBER <= 1900
        ESCAPE TOP
      END-IF
      ACCEPT IF CONTACT-TYPE = INDIVIDUAL
      RESET HOLD-ISN
      HOLD-ISN := *ISN
      FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = CONTACT.CONTACT-ID
        FIND(1) ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS    (CURRENT-INDEX)  := HOLD-ISN
          ADDRESS-FLAGS  (CURRENT-INDEX)  := ADDR-TYPE-CD
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    END-FIND
  ELSE
    IF CONTACT-SET-EXISTS
      FIND CONTACT WITH 'CONTACT-SET'
        IF CONTACT.ID-NUMBER > 0 AND CONTACT.ID-NUMBER <= 1900
          ESCAPE TOP
        END-IF
        ACCEPT IF CONTACT-TYPE = INDIVIDUAL
        PERFORM MANAGE-ARRAY
        SEARCH-ISNS(CURRENT-INDEX) := *ISN
        ADDRESS-FLAGS  (CURRENT-INDEX)  := 'P'
        ADD 1 TO CURRENT-INDEX
      END-FIND
    ELSE
      IF ADDRESS-SET-EXISTS
        FIND ADDRESS-V WITH 'ADDRESS-SET'
          FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID = ADDRESS-V.ADDRESS-POOL-ID
            FIND CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
              IF CONTACT.ID-NUMBER > 0 AND CONTACT.ID-NUMBER <= 1900
                ESCAPE TOP
              END-IF
              ACCEPT IF CONTACT-TYPE = INDIVIDUAL
              PERFORM MANAGE-ARRAY
              SEARCH-ISNS    (CURRENT-INDEX) := *ISN
              ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
              ADD 1 TO CURRENT-INDEX
            END-FIND
          END-FIND
        END-FIND
      END-IF
    END-IF
  END-IF
END-IF
**============
END-SUBROUTINE
**============

**====================================
DEFINE SUBROUTINE FIND-BY-PHONE-NUMBER
**====================================
#IN-PHONE  :=  Phone
PERFORM CLEAN-ARRAY
*
FIND PHONE-V WITH AREA-CODE = #AREA-CODE
  ACCEPT IF #DIAL-NUMBER = DIAL-NUMBER
  FIND(1) CONTACT WITH CONTACT.CONTACT-ID = PHONE-V.CONTACT-ID
    ACCEPT IF CONTACT-TYPE = INDIVIDUAL
    PERFORM MANAGE-ARRAY
    SEARCH-ISNS(CURRENT-INDEX) := *ISN
    PHONE-FLAGS(CURRENT-INDEX) := PHONE-ID
    ADD 1 TO CURRENT-INDEX
  END-FIND
END-FIND
**============
END-SUBROUTINE
**============



**=====================================
DEFINE SUBROUTINE FIND-BY-EMAIL-ADDRESS
**=====================================
PERFORM CLEAN-ARRAY
RESET EMAIL-LENGTH EMAIL-WITH-9S
EXAMINE Email TRANSLATE INTO LOWER CASE
EMAIL-LENGTH := *LENGTH(Email)
EMAIL-WITH-9S := Email
*
FOR i EMAIL-LENGTH TO 70
  COMPRESS EMAIL-WITH-9S '{' INTO EMAIL-WITH-9S LEAVING NO
END-FOR
*
IF MembersOnly
  IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS THEN
    FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
      FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
        FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
          FIND(1) CONTACT WITH 'CONTACT-SET' AND CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
              AND CONTACT.ID-NUMBER > 1900
            PERFORM MANAGE-ARRAY
            SEARCH-ISNS(CURRENT-INDEX) := *ISN
            ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
            EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
            ADD 1 TO CURRENT-INDEX
          END-FIND
        END-FIND
      END-FIND
    END-FIND
  ELSE IF CONTACT-SET-EXISTS
      FIND  CONTACT WITH 'CONTACT-SET' AND CONTACT.ID-NUMBER > 1900
        ACCEPT IF CONTACT-TYPE = INDIVIDUAL
        RESET HOLD-ISN #PARAMETER
        HOLD-ISN := *ISN
        FIND(1) EMAIL-V WITH EMAIL-V.CONTACT-ID = CONTACT.CONTACT-ID
            AND EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS    (CURRENT-INDEX) := HOLD-ISN
          ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
          EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    ELSE IF ADDRESS-SET-EXISTS
        FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
            FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
              FIND(1) CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID AND CONTACT.ID-NUMBER > 1900
                ACCEPT IF CONTACT-TYPE = INDIVIDUAL
                PERFORM MANAGE-ARRAY
                SEARCH-ISNS    (CURRENT-INDEX) := *ISN
                ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
                EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
                ADD 1 TO CURRENT-INDEX
              END-FIND
            END-FIND
          END-FIND
        END-FIND
      ELSE
        FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          FIND(1) CONTACT WITH CONTACT.CONTACT-ID = EMAIL-V.CONTACT-ID AND CONTACT.ID-NUMBER > 1900
            ACCEPT IF CONTACT-TYPE = INDIVIDUAL
            PERFORM MANAGE-ARRAY
            SEARCH-ISNS(CURRENT-INDEX)     := *ISN
            ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
            EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
            ADD 1 TO CURRENT-INDEX
          END-FIND
        END-FIND
      END-IF
    END-IF
  END-IF
ELSE
  IF CONTACT-SET-EXISTS AND ADDRESS-SET-EXISTS THEN
    FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
      FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
        FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
          FIND(1) CONTACT WITH 'CONTACT-SET' AND CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
            PERFORM MANAGE-ARRAY
            SEARCH-ISNS(CURRENT-INDEX) := *ISN
            ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
            EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
            ADD 1 TO CURRENT-INDEX
          END-FIND
        END-FIND
      END-FIND
    END-FIND
  ELSE IF CONTACT-SET-EXISTS
      FIND  CONTACT WITH 'CONTACT-SET'
        ACCEPT IF CONTACT-TYPE = INDIVIDUAL
        RESET HOLD-ISN #PARAMETER
        HOLD-ISN := *ISN
        FIND(1) EMAIL-V WITH EMAIL-V.CONTACT-ID = CONTACT.CONTACT-ID
            AND EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          PERFORM MANAGE-ARRAY
          SEARCH-ISNS    (CURRENT-INDEX) := HOLD-ISN
          ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
          EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
          ADD 1 TO CURRENT-INDEX
        END-FIND
      END-FIND
    ELSE IF ADDRESS-SET-EXISTS
        FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          FIND ADDRESS-RELATIONSHIP WITH ADDRESS-RELATIONSHIP.CONTACT-ID = EMAIL-V.CONTACT-ID
            FIND ADDRESS-V WITH 'ADDRESS-SET' AND ADDRESS-V.ADDRESS-POOL-ID = ADDRESS-RELATIONSHIP.ADDRESS-POOL-ID
              FIND(1) CONTACT WITH CONTACT.CONTACT-ID = ADDRESS-RELATIONSHIP.CONTACT-ID
                ACCEPT IF CONTACT-TYPE = INDIVIDUAL
                PERFORM MANAGE-ARRAY
                SEARCH-ISNS    (CURRENT-INDEX) := *ISN
                ADDRESS-FLAGS  (CURRENT-INDEX) := ADDR-TYPE-CD
                EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
                ADD 1 TO CURRENT-INDEX
              END-FIND
            END-FIND
          END-FIND
        END-FIND
      ELSE
        FIND EMAIL-V WITH EMAIL-ADDRESS = SearchIndividual.Email THRU EMAIL-WITH-9S
          FIND(1) CONTACT WITH CONTACT.CONTACT-ID = EMAIL-V.CONTACT-ID
            ACCEPT IF CONTACT-TYPE = INDIVIDUAL
            PERFORM MANAGE-ARRAY
            SEARCH-ISNS(CURRENT-INDEX)     := *ISN
            ADDRESS-FLAGS  (CURRENT-INDEX) := 'P'
            EMAIL-FLAGS    (CURRENT-INDEX) := EMAIL-ID
            ADD 1 TO CURRENT-INDEX
          END-FIND
        END-FIND
      END-IF
    END-IF
  END-IF
END-IF
**============
END-SUBROUTINE
**============





**================================
DEFINE SUBROUTINE FIND-BY-ZIP-CODE
**================================
#ZIP-CODE := ZIP
IF #ZIP-CODE > 99999  /* CHECKING FOR 9 DIGIT ZIP-CODE
  FIND NUMBER ADDRESS-V WITH ZIP-CODE = #ZIP-CODE
    RETAIN AS 'ADDRESS-SET'
ELSE
  COMPRESS #ZIP-5 '0000' INTO ZIP-WITH-ZEROS-A LEAVING NO
  COMPRESS #ZIP-5 '9999' INTO ZIP-WITH-NINES-A LEAVING NO
  FIND NUMBER ADDRESS-V WITH ZIP-CODE >= ZIP-WITH-ZEROS AND ZIP-CODE <= ZIP-WITH-NINES
    RETAIN AS 'ADDRESS-SET'
END-IF
ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============



**============================
DEFINE SUBROUTINE FIND-BY-CITY
**============================
IF ADDRESS-SET-EXISTS
  FIND NUMBER ADDRESS-V WITH 'ADDRESS-SET' AND CITY = SearchIndividual.City
    RETAIN AS 'ADDRESS-SET'
ELSE
  FIND NUMBER ADDRESS-v WITH CITY = SearchIndividual.City
    RETAIN AS 'ADDRESS-SET'
END-IF
ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============


**=============================
DEFINE SUBROUTINE FIND-BY-STATE
**=============================
IF ADDRESS-SET-EXISTS
  FIND NUMBER ADDRESS-V WITH 'ADDRESS-SET' AND STATE-CODE = State
    RETAIN AS 'ADDRESS-SET'
ELSE
  FIND NUMBER ADDRESS-V WITH STATE-CODE = State
    RETAIN AS 'ADDRESS-SET'
END-IF

ADDRESS-SET-EXISTS := TRUE
**============
END-SUBROUTINE
**============




**============================
DEFINE SUBROUTINE MANAGE-ARRAY
**============================
IF CURRENT-INDEX >= UPPER-BOUND
  ADD 5000 TO UPPER-BOUND
  EXPAND ARRAY SEARCH-ISNS TO (1:UPPER-BOUND)
  EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND)
  EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND)
  EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND)
ELSE
  ESCAPE ROUTINE
END-IF
IGNORE
**============
END-SUBROUTINE
**============

**============================
DEFINE SUBROUTINE CLEAN-ARRAY
**============================
RESET INITIAL UPPER-BOUND CURRENT-INDEX
RESET SEARCH-ISNS(*) ADDRESS-FLAGS(*) EMAIL-FLAGS(*) PHONE-FLAGS(*)
EXPAND ARRAY SEARCH-ISNS    TO (1:UPPER-BOUND)
EXPAND ARRAY PHONE-FLAGS    TO (1:UPPER-BOUND) /* STARTS AT 1000
EXPAND ARRAY ADDRESS-FLAGS  TO (1:UPPER-BOUND) /* STARTS AT 1000
EXPAND ARRAY EMAIL-FLAGS    TO (1:UPPER-BOUND) /* STARTS AT 1000
**============
END-SUBROUTINE
**============


END
