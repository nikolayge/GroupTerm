* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Web Application Online
* NATSERV2 - EntireX Driver module (8k - 8k)
* Call Business Object module based on ProcessName
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    03012009       Initial Creation
********************************************************
*
DEFINE DATA PARAMETER
1 PROCESS-NAME      (A8)
1 REQUEST-DATA      (A8192)
1 RESPONSE-DATA     (A8192)        /* Out
1 RETURN-CODE       (A4)           /* Out
1 RETURN-MESSAGE    (A100)         /* Out
*
LOCAL
1 ERROR-LOG     (A100)
1 #RESPONSE-FILE-NAME (A) DYNAMIC
1 #DATE         (N8)
1 #TIME         (N7)
1 #DEL          (A1) INIT<':'>
1 #LOG-FILE     (A) Dynamic INIT <'/usr/SAG/tmp/natserv2_'>
END-DEFINE
#DATE := *DATN
#TIME := *TIMN
*
* DEFINE WORK FILE 1 "/usr/SAG/tmp/RPC223_2.log" TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
COMPRESS #LOG-FILE #DATE '.log' INTO #LOG-FILE LEAVING NO
DEFINE WORK FILE 1 #LOG-FILE TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
ON ERROR
  COMPRESS *ERROR-NR *ERROR-LINE *ERROR-TA *PROGRAM *DATN *TIME INTO ERROR-LOG
  WRITE WORK FILE 1 VARIABLE ERROR-LOG
  CLOSE WORK FILE 1
  BACKOUT TRANSACTION
  ESCAPE ROUTINE
END-ERROR
*
WRITE WORK FILE 1 VARIABLE #DATE #DEL #TIME #DEL PROCESS-NAME #DEL REQUEST-DATA
DECIDE ON FIRST VALUE PROCESS-NAME
  VALUE 'WCCPINFO'
     CALLNAT 'WCCNCC10' REQUEST-DATA RESPONSE-DATA RETURN-CODE RETURN-MESSAGE
  VALUE 'WCCPAPPL'
     CALLNAT 'WCCNCC20' REQUEST-DATA RESPONSE-DATA RETURN-CODE RETURN-MESSAGE
  VALUE 'CCARDRUN'
     CALLNAT 'WCCNCC40' PROCESS-NAME
  NONE VALUE
     IGNORE
END-DECIDE
WRITE WORK FILE 1 VARIABLE #DATE #DEL #TIME #DEL PROCESS-NAME #DEL RESPONSE-DATA
WRITE WORK FILE 1 VARIABLE #DATE #DEL #TIME #DEL RETURN-CODE  #DEL RETURN-MESSAGE
CLOSE WORK FILE 1
*
END
