* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
define data
parameter
1 #MAIN-NAME       (A25)
1 #ISN             (P8)
1 #UPDATED        (L)
LOCAL
1 #CLERK           (A20)
1 UpdateNotes view of A-CONTACT-NOTES
  2 USER-COMPANY   (A1)
  2 USER-ID        (A3)
  2 CONTACT-ID     (N8) /* D
  2 NOTE-TIME      (N7)
  2 NOTE-DATE      (N8) 2 REDEFINE NOTE-DATE
    3 NOTE-DATE-A  (A8)
  2 C*NOTE-TEXT
  2 NOTE-TEXT      (A78/18) 2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-S  (18)
      4 NOTE-LINE   (A60)
      4 NOTE-FUTURE (A18)
  2 LAST-UPD-USER
  2 LAST-UPD-DATE 2 redefine LAST-UPD-DATE
    3 LAST-UPD-DATE-A (A8)
  2 LAST-UPD-TIME

1 #NOTE-DATE     (A10)
1 #D             (D)
1 #J             (I4)
1 #L             (I4)
1 #NOTE-DATE-A   (A8) 1 REDEFINE #NOTE-DATE-A
  2 #NOTE-DATE-N (N8)
1 #NOTE-TIME-A   (A7) 1 REDEFINE #NOTE-TIME-A
  2 #NOTE-TIME-N (N7)
1 #UPD-DATE      (A10)
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS

1 #Updated-message (A40) CONST <'Transaction complete.'>
1 #UPD-BY          (A45)
end-define

* DEFINE WINDOW Contact-Note
*  SIZE 24 * 66
*  BASE 1/10
*  FRAMED ON POSITION SYMBOL AUTO

SET KEY PF3 NAMED 'Exit'
SET KEY ENTR NAMED 'Updt'
RESET #UPDATED

IF #ISN = 0 ESCAPE ROUTINE END-IF
GET-LBL.
GET UpdateNotes #ISN
IF *ISN (GET-LBL.) = 0
  ESCAPE ROUTINE
END-IF

MOVE EDITED NOTE-DATE-A TO #D (EM=YYYYMMDD)
MOVE EDITED #D (EM=MM-DD-YYYY) TO #NOTE-DATE

FIND (1) TERM WITH CLERK-ID = UpdateNotes.USER-ID
  MOVE COMMENTS TO #CLERK
END-FIND

IF UpdateNotes.LAST-UPD-USER NE ' '
  MOVE EDITED LAST-UPD-DATE-A TO #D (EM=YYYYMMDD)
  MOVE EDITED #D (EM=MM-DD-YYYY) TO #UPD-DATE
  FIND (1) TERM WITH TERMINAL-ID = UpdateNotes.LAST-UPD-USER
    COMPRESS 'UPDATED ON' #UPD-DATE 'BY' COMMENTS INTO #UPD-BY
  END-FIND
ELSE
  RESET #UPD-BY
END-IF

* INPUT WINDOW = 'Contact-Note' USING MAP 'NOTE4MXX'
INPUT USING MAP 'NOTE4MXX'
REPEAT UNTIL *PF-KEY = 'PF3'
  PERFORM COMPRESS-NOTE
  ASSIGN #UPDATED = TRUE
  INPUT WITH TEXT #Updated-message USING MAP 'NOTE4MXX'
END-REPEAT

IF #UPDATED
  MOVE EDITED *TIMX (EM=HHIISST)  TO #NOTE-TIME-A
  MOVE EDITED *DATX (EM=YYYYMMDD) TO #NOTE-DATE-A
  ASSIGN UpdateNotes.LAST-UPD-USER = *INIT-USER
  ASSIGN UpdateNotes.LAST-UPD-DATE = #NOTE-DATE-N
  ASSIGN UpdateNotes.LAST-UPD-TIME = #NOTE-TIME-N
  UPDATE (GET-LBL.)
END-IF

DEFINE COMPRESS-NOTE
/*     =============
FOR #J 1 TO 18
  IF NOTE-LINE  (#J) = ' '
    #L := #J + 1
    REPEAT UNTIL #L > 18
      IF NOTE-LINE  (#L) NE ' '
        ASSIGN NOTE-LINE  (#J) = NOTE-LINE  (#L)
        RESET NOTE-LINE  (#L)
        ESCAPE BOTTOM
      END-IF
      ADD 1 TO #L
    END-REPEAT
  END-IF
END-FOR
END-SUBROUTINE

END
