* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* CRM Admin interface : Server switch
************************************************************************
* NAME         : MBRSTAT                                               *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Return Quotes for Given Product/AmountsBenefits       *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20120131      Initial Creation
**********************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID      (N8)
1 #STATUS-CD       (A1)
1 #SVC-CD          (A1)
1 #NUMBER-MEMBERSHIPS (P1)
1 #SVC-STATUS      (A1)
1 #SVC-CODE        (A3) OPTIONAL
LOCAL
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER   (N6)
  2 INTRO-FLAG  (A1)
  2 PROCESS-IND (A1)
  2 MILITARY-SERVICE (A3)
  2 NUMBER-MEMBERSHIPS (P1)
1 SVC-MEMBER-V VIEW OF A-SVC-MEMBER
  2 SVC-STATUS  (A1)
1 CNT-V VIEW OF A-CONTACTS
  2 DATE-OF-DEATH
1 #SVC-CODE-W   (A3)
END-DEFINE
*
RESET #STATUS-CD #SVC-CD #SVC-STATUS
*
FIND (1) CNT-V WITH CONTACT-ID = #CONTACT-ID
  IF DATE-OF-DEATH > 0
    #SVC-STATUS := '0'
    ESCAPE ROUTINE
  END-IF
END-FIND
IF #SVC-CODE SPECIFIED
  #SVC-CODE-W := #SVC-CODE
ELSE
  #SVC-CODE-W := 'PSM'
END-IF
FIND (1) ST-V WITH MEMBER-CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = ST-V.ID-NUMBER
      AND SVC-PLAN = #SVC-CODE-W
    IF SVC-MEMBER-V.SVC-STATUS = 'A' OR= 'P'
      #SVC-STATUS := SVC-MEMBER-V.SVC-STATUS
      ESCAPE BOTTOM
    END-IF
  END-FIND
*
  #NUMBER-MEMBERSHIPS := ST-V.NUMBER-MEMBERSHIPS
  IF PROCESS-IND = 'C'
    IF INTRO-FLAG = 'A'                     /* Pending application
      MOVE 'A' TO #STATUS-CD                /* Applicant
    ELSE
      MOVE 'O' TO #STATUS-CD                /* Owner
    END-IF
  ELSE
    IF INTRO-FLAG = 'S'
      MOVE 'S' TO #STATUS-CD                 /* Spouse
    ELSE
      IF INTRO-FLAG = 'K'
        MOVE 'C' TO #STATUS-CD               /* Child
      ELSE
        IF INTRO-FLAG = 'G'
          MOVE 'G' TO #STATUS-CD             /* Grandchild
        ELSE
          IF MILITARY-SERVICE = 'EMP'
            MOVE 'E' TO #STATUS-CD           /* Employee
          ELSE
/* Member (in other words, if they are not a contact, spouse, child, grandchild or employee,
/* they are a member)
            MOVE 'M' TO #STATUS-CD           /* Member
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF PROCESS-IND = 'G'
    MOVE 'G' TO #SVC-CD                      /* Grandfathered
  ELSE
    IF PROCESS-IND = 'D'
      MOVE 'D' TO #SVC-CD                    /* De-Grandfathered
    ELSE
      IF PROCESS-IND = 'O'                   /* Opt-Out
        MOVE 'O' TO #SVC-CD
      ELSE
        IF  (INTRO-FLAG = 'S' OR = 'K' OR = 'G')
            OR (MILITARY-SERVICE  = 'EMP')
            OR (PROCESS-IND = 'C')
          MOVE 'N' TO #SVC-CD                /* None
        ELSE
          MOVE 'I' TO #SVC-CD                /* Insurance-Plus
        END-IF
      END-IF
    END-IF
  END-IF
END-FIND
END
