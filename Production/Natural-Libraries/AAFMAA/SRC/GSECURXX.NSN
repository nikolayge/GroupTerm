* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID : GSECURXX
*
* Security Pgm - Review Member selected vs. User Updating
*              - If the member selected contains the same SSN as the
*                user, or it is a linked to a member with the same SSN
*                as the user, the program will return a security error.
DEFINE DATA
*
PARAMETER
1 #ID-NUMBER              (N6)
1 REDEFINE #ID-NUMBER
 2 #ID-NUMBER-A           (A6)
1 #USER-ID                (A3)
1 #SECURITY-ERROR         (L)
*
LOCAL
*
1 TRMS-V VIEW OF A-TERMINALS
 2 CLERK-ID               (A3)
 2 SSN                    (N9)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER              (N6)
 2 SSN                    (N9)
 2 LINKED-CN              (N6/30)
 2 REDEFINE LINKED-CN
  3 #LINKED-CN-A          (A6/30)
*
1 #SSN-TO-CHECK           (N9)
1 #ISN                    (P8)
1 #I1                     (I2)
*
END-DEFINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
RESET #SSN-TO-CHECK #ISN
MOVE FALSE TO #SECURITY-ERROR
*
FIND (1) TRMS-V WITH CLERK-ID = #USER-ID
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
*
  MOVE TRMS-V.SSN TO #SSN-TO-CHECK
END-FIND
*
IF #SSN-TO-CHECK GT 0
  FIND (1) ST-V WITH MEMBER-KEY = #ID-NUMBER-A
    IF NO RECORDS FOUND
      ESCAPE ROUTINE
    END-NOREC
*
    MOVE *ISN TO #ISN
  END-FIND
*
  GET ST-V #ISN
*
  IF #SSN-TO-CHECK = ST-V.SSN    /* mbr updating own record
    MOVE TRUE TO #SECURITY-ERROR
  ELSE
    FOR #I1 1 TO 30
      IF ST-V.LINKED-CN (#I1) = 0
        ESCAPE BOTTOM
      END-IF
*
      FIND (1) ST-V WITH MEMBER-KEY = #LINKED-CN-A (#I1)
        IF ST-V.SSN = #SSN-TO-CHECK  /* mbr updating linked record
          MOVE TRUE TO #SECURITY-ERROR
        END-IF
      END-FIND
    END-FOR
  END-IF
END-IF
*
END
