* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: DNP0PRPT
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* BZ     12/21/2010  None    Program written                           *
* NGG    01/15/2011  NG1     Report adjustments
************************************************************************

DEFINE DATA
LOCAL

1 C-V        VIEW OF A-CONTACTS
  2 CONTACT-ID                      (N8)    /* D
  2 ID-NUMBER                       (N6)    /* D
  2 CONTACT-STATUS                  (A1)
  2 MILITARY-STATUS                 (A1)
  2 SSN-PROTECT                     (A1)
  2 SSN-PROTECT-DATE                (N8)
  2 PREFIX-TITLE                    (A25)
  2 FULL-NAME
    3 FIRST-NAME                    (A25)
    3 MIDDLE-NAME                   (A25)
    3 LAST-NAME                     (A25)
    3 SUFFIX                        (A10)
  2 GENDER-CD                       (A1)
  2 DATE-OF-BIRTH                   (N8)  /* D
  2 SSN                             (N9)  /* D
  2 DATE-OF-DEATH-REPORTED          (N8)
  2 DATE-OF-DEATH-STATUS-CODE       (A1)
  2 DATE-OF-DEATH-STATUS-DATE       (N8)
  2 REDEFINE DATE-OF-DEATH-STATUS-DATE
    3 DATE-OF-DEATH-STATUS-DATE_A               (A8)

  2 DEATH-NOTIFIED-BY-CONTACT-ID    (N8)
  2 WEBSITE-URL                     (A70)
  2 HOMICIDE-SUICIDE-ACCIDENT-IND   (A1)  /* D
  2 BIRTH-CERT-CODE                 (A1)
  2 DEATH-CERT-CODE                 (A1)
  2 BURIAL-LOC                      (A1)
  2 BURIAL-INFO                     (A35)
  2 PLACE-OF-DEATH                  (A50)
  2 CAUSE-OF-DEATH                  (A50)
  2 PLACE-OF-BIRTH                  (A50)
  2 DELETE-FLAG                     (A1)
  2 SPECIAL-PAY-IND                 (A1)
  2 LAST-DEATH-USER-UPD             (A8)
  2 LAST-DEATH-DATE-UPD             (N8) 2 REDEFINE LAST-DEATH-DATE-UPD
    3 LAST-DEATH-DATE-UPD-A         (A8)
  2 LAST-USER-UPD                   (A8)
  2 LAST-DATE-UPD                   (N8)
  2 LAST-TIME-UPD                   (N7)
  2 FIRM-ORGANIZATION-NAME          (A75)
  2 DEATH-DOCUMENT-VALIDATION-CODE  (A10)

1 dm          (A1)   INIT <H'09'>
1 Err-msg     (A12)
1 c_in        (N2)
1 c_upd       (N5)
1 c_Err_ST    (N5)
1 c_Err_FR    (N5)
*
1 v_Days          (N4)
1 v_RC            (N3)
1 v_D             (D)
1 v_D0            (D)
1 v_SPSDOD        (D)
1 v_SPSDOD-A      (A10)
1 v_UPDATED       (D)
1 v_A35           (A30)
1 v_Clerk         (A20)
1 v_Clerk_ID      (A3)
*
1 SPOUSE VIEW OF A-CONTACTS
  2 DATE-OF-DEATH                   (N8)  2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A               (A8)
*
1 STATUS VIEW OF A-STATUS
  2 INTRO-FLAG
  2 PROCESS-IND
  2 STATUS-CODE-SP
  2 SPOUSE-CONTACT-ID
* -------------------------------------------------  TERMINALS
END-DEFINE
ON ERROR
  WRITE *PROGRAM *ERROR-LINE *ERROR-NR /
    'DOD Status dt'  ID-NUMBER DATE-OF-DEATH-STATUS-DATE_A
END-ERROR
FORMAT (2) LS = 161 PS = 73
v_D0   := *DATX

WRITE (2) NOTITLE 'PROGRAM:' *PROGRAM 35T "AAFMAA Pending Death Report"
  82T 'DATE:' *DAT4U 'TIME:' *TIME (Al=8)
/* 3x 'DB:' v_DB
  /            20T     " Pending Death Date Confirmation - Status code 'Pending'"
  //
  '                                          PRC INT SPS Spouse     Reported   Days Updated              Last' /
  'CNT   CN            Name                  IND FLG STA DOD        DOD Date   Pend By Clerk             Updated' /
  '--- ------ ------------------------------ --- --- --- ---------- ---------- ---- -------------------- ----------'
* '12345...10....5...20....5...30....5...40....5...50....5...60....5...70....5...80....5...90....5..100....5..110..
F1.
FIND C-V WITH  DATE-OF-DEATH-STATUS-DATE <> 0 AND ID-NUMBER > 1900
    SORTED BY  DATE-OF-DEATH-STATUS-DATE DESCENDING
    WHERE DATE-OF-DEATH-STATUS-CODE = 'P'
  c_in := c_in + 1
  IF DATE-OF-DEATH-STATUS-DATE_A = MASK(YYYYMMDD)
    MOVE EDITED DATE-OF-DEATH-STATUS-DATE_A  TO  v_D (EM=YYYYMMDD)
    v_Days := v_D0 - v_D
  ELSE
    RESET v_Days
  END-IF
  v_A35 := EDIT_NAME_FMLS(<FULL-NAME>)
  FIND (1) STATUS WITH ID-NUMBER = C-V.ID-NUMBER
  END-FIND
  IF STATUS.SPOUSE-CONTACT-ID > 0
    FIND (1) SPOUSE WITH CONTACT-ID = STATUS.SPOUSE-CONTACT-ID
      IF DATE-OF-DEATH > 0
        MOVE EDITED DATE-OF-DEATH-A TO v_SPSDOD (EM=YYYYMMDD)
        MOVE EDITED v_SPSDOD ( EM=MM/DD/YYYY ) TO v_SPSDOD-A
      ELSE
        RESET v_SPSDOD-A
      END-IF
    END-FIND
  ELSE
    v_SPSDOD-A := 'No spouse'
  END-IF
  IF LAST-DEATH-DATE-UPD > 0
    MOVE EDITED LAST-DEATH-DATE-UPD-A TO v_UPDATED (EM=YYYYMMDD)
  ELSE
    RESET v_UPDATED
  END-IF
  v_Clerk_ID := LAST-DEATH-USER-UPD
  v_Clerk := FUNCTION-CLERK-NAME (<v_Clerk_ID>)
  WRITE (2)
    c_in
    ID-NUMBER (EM=9(6))
    v_A35
    PROCESS-IND
    INTRO-FLAG
    51T STATUS-CODE-SP
    55T v_SPSDOD    ( EM=MM/DD/YYYY )
    66T v_D         ( EM=MM/DD/YYYY )
    77T v_Days      ( EM=ZZZZ)
    82T v_Clerk
    103T v_UPDATED  ( EM=MM/DD/YYYY )
END-FIND

END
