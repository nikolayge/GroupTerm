* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram POSN0019.
/**
/** :author nguentchev
/* vxt 09/22/2017 vt1 /Ticket 5789/  
/*
DEFINE DATA
PARAMETER
1 #POL-NUM      (A16)
1 #PAYOR-CN     (A6)
1 #PAYOR-NAME   (A70)
1 #NOTC-ADDR-1  (A70)
1 #NOTC-ADDR-2  (A70)
1 #NOTC-ADDR-3  (A70)
1 #CITY         (A50)
1 #STATE        (A2)
1 #NOTC-ZIP-5   (A5)
1 #NOTC-ZIP-4   (A4)
/*
LOCAL USING POSAGET
LOCAL USING POSLROLE
LOCAL USING ADDA0001   /* Address parameter area ADDN0001
LOCAL
1 BLANK  (A1) CONST <' '>
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
*
1 #work-zip-code  (a9)            /* vt1 \/
1 redefine #work-zip-code
  2 #zip-code-5    (n5)
  2 #zip-code-4    (n4)           /* vt1 /\
*
END-DEFINE
/*
RESET #NUMBER-OF-RELATIONS
  #POLICY-RELATIONS (*)
  #ISNS(*)
#RELATIONSHIP-TYPE := PayorRole
CALLNAT 'POSNCNTS'  /* Get all contacts with role payor
  #POL-NUM
  #RELATIONSHIP-TYPE
  #NUMBER-OF-RELATIONS
  #POLICY-RELATIONS (*)
  #ISNS(*)
  #MSG
IF #MSG = BLANK
    AND #NUMBER-OF-RELATIONS > 0
/* Get payor name and address
  RESET #PDA-ADDRESS
  #ADD-CONTACT-ID := CONTACT-ID (1)
  #ADD-TYPE       := 'P'  /* Primary
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  #NOTC-ADDR-1  := #PDA-ADDRESS.#ADD-LINE-1
  #NOTC-ADDR-2  := #PDA-ADDRESS.#ADD-LINE-2
  #NOTC-ADDR-3  := #PDA-ADDRESS.#ADD-LINE-3
  #CITY         := #PDA-ADDRESS.#ADD-CITY
  #STATE        := #PDA-ADDRESS.#ADD-STATE
  reset #work-zip-code                                                              /* vt1 \/
  if #PDA-ADDRESS.#add-zip-code ne 0
    move right justified #PDA-ADDRESS.#add-zip-code to #work-zip-code
    examine #work-zip-code for ' ' replace '0'
    if #zip-code-5 NE 0
      move edited #zip-code-5 (em=99999) to #NOTC-ZIP-5
      if #zip-code-4 ge 0
        move edited  #zip-code-4(em=9999) to #NOTC-ZIP-4
      end-if
    else
      reset #NOTC-ZIP-5
    end-if
  else
    reset #NOTC-ZIP-4
    move '0' to #NOTC-ZIP-5
  end-if
*  #NOTC-ZIP-5   := #PDA-ADDRESS.#ADD-ZIP5
*  #NOTC-ZIP-4   := #PDA-ADDRESS.#ADD-ZIP4                                         /* vt1 /\
  #PAYOR-NAME   := FUNCTION-CONTACT-NAME-2 (< CONTACT-ID (1) >)
  FIND (1) ST-V WITH MEMBER-CONTACT-ID = CONTACT-ID (1)
    MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO #PAYOR-CN
  END-FIND
END-IF
END
