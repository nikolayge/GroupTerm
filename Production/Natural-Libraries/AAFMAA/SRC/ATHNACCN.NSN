* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************** 
* Program : ATHNACCN 
* 
* Function : To get the new GL account number 
* 
* vxt 04/05/2014  Initial creation 
**************************************************** 
define data parameter 
1 #id-number            (n6) 
1 #policy-number        (a16) 
1 #sg-category-1        (a2) 
1 #sg-category-2        (a2) 
1 #ACCOUNT-NUMBER       (a4) 
1 #account-description  (a25) 
* 
local 
1 st-v view of a-status 
  2 id-number 
  2 plan (9) 
* 
1 #k               (i2) 
1 #pol-num         (a16) 
1 #plan            (a3) 
end-define 
* 
#plan := #sg-category-2 
if #plan = ' ' 
  find st-v with st-v.id-number = #id-number 
    for #k 1 9 
      callnat 'GPLNMSXX' #pol-num #id-number #k 
      if #pol-num = #policy-number 
        move st-v.plan(#k) to #plan 
        escape bottom 
      end-if 
    end-for 
  end-find 
end-if 
* 
if #plan = 'VT' or  function-term-policy(<#plan>) 
  decide on first value of #plan 
    value 'LT' 
      if #SG-CATEGORY-1 = '1Y' 
        #ACCOUNT-NUMBER := 4021 
        #account-description := 'First Year Premiums-LT1' 
      else 
        if #SG-CATEGORY-1 = 'RN' 
          #ACCOUNT-NUMBER := 4031 
          #account-description := 'Renewals-LT' 
        end-if 
      end-if 
    value 'RT' 
      if #SG-CATEGORY-1 = '1Y' 
        #ACCOUNT-NUMBER := 4023 
        #account-description := 'First Year Premiums-RT' 
      else 
        if #SG-CATEGORY-1 = 'RN' 
          #ACCOUNT-NUMBER := 4033 
          #account-description := 'Renewals-RT' 
        end-if 
      end-if 
    value 'EZ' 
      if #SG-CATEGORY-1 = '1Y' 
        #ACCOUNT-NUMBER := 4024 
        #account-description := 'First Year Premiums-EZT' 
      else 
        if #SG-CATEGORY-1 = 'RN' 
          #ACCOUNT-NUMBER := 4034 
          #account-description := 'Renewals-EZT' 
        end-if 
      end-if 
    none value 
      if #SG-CATEGORY-1 = '1Y' 
        #ACCOUNT-NUMBER := 4022 
        #account-description := 'First Year Premiums-LT2' 
      else 
        if #SG-CATEGORY-1 = 'RN' 
          #ACCOUNT-NUMBER := 4032 
          #account-description := 'Renewals-LT2' 
        end-if 
      end-if 
  end-decide 
else 
  IF VAWL-TRUE(<#plan>) 
      or (#plan = mask(NN'F')) or ( #plan = mask(N'P')) 
    if #SG-CATEGORY-1 = '1Y' 
      #ACCOUNT-NUMBER := 4026 
      #account-description := 'First Year Premiums-WL' 
    else 
      if #SG-CATEGORY-1 = 'RN' 
        #ACCOUNT-NUMBER := 4036 
        #account-description := 'Renewals-WL' 
      end-if 
    end-if 
  else 
    decide on first value of #plan 
      value 'GPL' , 'GP2' 
        if #SG-CATEGORY-1 = '1Y' 
          #ACCOUNT-NUMBER := 4025 
          #account-description := 'First Year Premiums-GPL GP2' 
        else 
          if #SG-CATEGORY-1 = 'RN' 
            #ACCOUNT-NUMBER := 4035 
            #account-description := 'Renewals-GPL GP2' 
          end-if 
        end-if 
      value 'AL' 
        if #SG-CATEGORY-1 = '1Y' 
          #ACCOUNT-NUMBER := 4028 
          #account-description := 'First Year Premiums-AL' 
        end-if 
      value 'SO' 
        if #SG-CATEGORY-1 = 'SO' 
          #ACCOUNT-NUMBER := 4112 
          #account-description := 'Service Only' 
        end-if 
      none value 
        ignore 
    end-decide 
  end-if 
end-if 
* 
if #sg-category-1  = 'SP' and #sg-category-2 ne 'AL'
  #ACCOUNT-NUMBER := 4027 
  #account-description := 'First Year Premiums-WLSP' 
end-if 
* 
if #sg-category-1  = 'SP' and #sg-category-2 eq 'AL'
  #ACCOUNT-NUMBER := 4028
  #account-description := 'First Year Premiums-AL'
end-if 
*
end 
* 
