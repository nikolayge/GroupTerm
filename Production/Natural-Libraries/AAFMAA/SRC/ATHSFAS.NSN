* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHSFAS      *** CALC TOTAL ASSETS ***
*    CALLNAT'D BY ATHPFAS
DEFINE DATA
PARAMETER
1 #PASSED-SUFFIX         (A3)
1 #PASSED-FD-TYPE        (A2)
1 #PASSED-FIN-ISN        (P8)
1 #PASSED-ST-ISN         (P8)
1 #PASSED-ID-NUMBER      (N6)
1 #PASSED-TOT-INV        (N9)
1 #PASSED-INV            (N9/7)
1 #PASSED-WAR            (N3.2)
1 #PASSED-MTG-DEBT       (N7)
1 #PASSED-RET-INV        (N9)
*
LOCAL
1 ST-V VIEW OF A-STATUS
*  2 DATE-OF-BIRTH          /* vt 10/18/2015
*   2 DATE-OF-BIRTH-SP      /* vt 10/18/2015
  2 name                   /* vt 10/18/2015
*
1 TK-V VIEW OF A-TICKERS
  2 SYMBOL     (A7)
  2 UNIT-PRICE (P5.3)
*
1 #MAX-GEN        (N3) INIT <75>
1 #MAX-MSB        (N3) INIT <99>
1 FI-V VIEW OF A-INVESTMENTS
  2 ID-NUMBER            (N7)
*  INV-GEN-TBL
  2 C*INV-GEN-TBL
  2 INV-TYPE              (A3/75)
  2 INV-BALANCE           (P7/75)
  2 INV-RATE              (P2.3/75)
  2 INV-DEBT              (P7/75)
*  INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 INV-MSB-TYPE          (A3/99)
  2 INV-MSB-TICK-SYM      (A6/99)
  2 INV-MSB-UNITS         (P6.3/99)
*  INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 INV-E-TYPE            (A3/99)
  2 INV-E-FACE            (P5/99)
  2 INV-E-PUR-DT          (N8/99)
  2 INV-E-END-DT          (N8/99)
  2 C*INV-E-BONDS-TBL-2
  2 INV-E-TYPE-2          (A3/99)
  2 INV-E-FACE-2          (P5/99)
  2 INV-E-PUR-DT-2        (N8/99)
  2 INV-E-END-DT-2        (N8/99)
*
1 #REAL-RTN (A28) INIT <'000-010-050-060-020-080-000-'>
1 REDEFINE #REAL-RTN
  2 #REAL-RTN-OCC (7)
    3 #REAL-RETURN (N2.1)
    3 #REAL-FILL   (A1)
*
1 #TOT-MANAGED-ASSETS (N9)
1 #CSV                (P9)
1 #CURR-PCT         (P4.3)
1 #UNIT-PRICE       (P5.3)
1 #E-UNIT-RED-VAL   (P3.2)
1 #NUM-BONDS        (P2)
1 #E-NUM-BONDS      (P2)
1 #E-RED-VAL        (P7)
1 #E-TBL-LST-DT-UPDT (A11)
1 #E-TYPE           (A3)
1 #E-END-DT         (N8)
1 REDEFINE #E-END-DT
  2 #E-END-DT-YYYYMM (N6)
  2 #E-END-DT-DD     (N2)
1 #E-FACE           (P5)
1 #FIN-TOT          (P7)
1 #AGG-BALANCE      (P7)
1 #AMT-7            (P11)
1 #AMT              (P9.3)
1 #AMT-2            (P9.2)
1 #AMT-DISP         (A7)
*
1 #YYYYMMDD         (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY           (N4)
  2 #MM            (N2)
  2 #DD            (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM         (N6)
  2 #DD-X           (N2)
*
1 #YYYYMMDD-596     (N8)
1 REDEFINE #YYYYMMDD-596
  2 #YYYY-596       (N4)
  2 #MM-596        (N2)
  2 #DD-596        (N2)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #I4              (I2)
1 #SCX             (I2)
1 #FIN-ID-NUMBER   (N7)
1 #FIN-ISN         (P8)
1 #TICK-SYM  (A7)
1 REDEFINE #TICK-SYM
  2 #TICK-SYM-X (A1/7)
*
END-DEFINE
*
IF #PASSED-FIN-ISN NE 0
  GET FI-V #PASSED-FIN-ISN
END-IF
*
GET ST-V #PASSED-ST-ISN
*
MOVE *DATN TO #YYYYMMDD-596
SUBTRACT 59 FROM #YYYY-596
SUBTRACT 6 FROM #MM-596
*
IF #MM-596 LT 1
  SUBTRACT 12 FROM #MM-596
  SUBTRACT 1 FROM #YYYY-596
END-IF
*
FOR #I1 1 TO #MAX-GEN
  IF INV-TYPE (#I1) = ' '
    ESCAPE BOTTOM
  END-IF
*
  IF INV-TYPE (#I1) = 'CP'
    ADD INV-BALANCE (#I1) TO #FIN-TOT
    ESCAPE TOP
  END-IF
*
  IF INV-TYPE (#I1) = MASK ('C')
    ADD INV-BALANCE (#I1) TO #FIN-TOT
    ADD INV-BALANCE (#I1) TO #PASSED-INV (1)
*
    IF INV-TYPE (#I1) = 'CV'
      ADD INV-BALANCE (#I1) TO #CSV
    END-IF
  END-IF
*
  IF  (INV-TYPE (#I1) = MASK ('T'))
      OR (INV-TYPE (#I1) = MASK ('I'))
    ADD INV-BALANCE (#I1) TO #FIN-TOT
    ADD INV-BALANCE (#I1) TO #PASSED-INV (2)
  END-IF
*
  IF INV-TYPE (#I1) = 'BI'
    ADD INV-BALANCE (#I1) TO #FIN-TOT
    ADD INV-BALANCE (#I1) TO #PASSED-INV (5)
  END-IF
*
  IF INV-TYPE (#I1) = 'AG'
    COMPUTE ROUNDED #AMT = (INV-BALANCE (#I1)
      * (100 - INV-RATE (#I1)) / 100) - INV-DEBT (#I1)
    MOVE ROUNDED #AMT  TO #AGG-BALANCE
*
    ADD #AGG-BALANCE TO #FIN-TOT
    ADD #AGG-BALANCE TO #PASSED-INV (6)
  END-IF
*
  IF  (INV-TYPE (#I1) = MASK ('R'))
      OR (INV-TYPE (#I1) = MASK ('W'))
    ADD INV-BALANCE (#I1) TO #PASSED-RET-INV
*
    IF #PASSED-SUFFIX NE 'PRO'  /* do not include ret amts if profile
      IF INV-TYPE (#I1) = 'RC' OR = 'WC'
        ADD INV-BALANCE (#I1) TO #FIN-TOT
        ADD INV-BALANCE (#I1) TO #PASSED-INV (1)
      END-IF
*
      IF INV-TYPE (#I1) = 'RT' OR = 'WT'
        ADD INV-BALANCE (#I1) TO #FIN-TOT
        ADD INV-BALANCE (#I1) TO #PASSED-INV (2)
      END-IF
*
      IF INV-TYPE (#I1) = 'RI' OR = 'WI'
        ADD INV-BALANCE (#I1) TO #FIN-TOT
        ADD INV-BALANCE (#I1) TO #PASSED-INV (5)
      END-IF
*
      IF INV-TYPE (#I1) = 'RA' OR = 'WA'
        ADD INV-BALANCE (#I1) TO #FIN-TOT
        ADD INV-BALANCE (#I1) TO #PASSED-INV (6)
      END-IF
    END-IF
  END-IF
*
  IF INV-TYPE (#I1) = 'LR' OR = 'LS'
    ADD INV-DEBT (#I1) TO #PASSED-MTG-DEBT
  END-IF
END-FOR
*
IF (C*INV-MUTUAL-STOCKS-BONDS-TBL GT (#MAX-MSB - 5))
    AND (#PASSED-FIN-ISN NE 0)
  COMPUTE #FIN-ID-NUMBER = #PASSED-ID-NUMBER + 900000
  FIND (1) FI-V WITH ID-NUMBER = #FIN-ID-NUMBER
*   WRITE 'procesing 9xxxx'
    PERFORM ACCUM-MSB
  END-FIND
*
  GET FI-V #PASSED-FIN-ISN
END-IF
*
PERFORM ACCUM-MSB
*
FOR #I1 1 TO 198
  IF #I1 GT 99
    MOVE INV-E-TYPE-2   (#I1 - 99) TO #E-TYPE
    MOVE INV-E-PUR-DT-2 (#I1 - 99) TO #YYYYMMDD
    MOVE INV-E-FACE-2   (#I1 - 99) TO #E-FACE
    MOVE INV-E-END-DT-2 (#I1 - 99) TO #E-END-DT
  ELSE
    MOVE INV-E-TYPE   (#I1) TO #E-TYPE
    MOVE INV-E-PUR-DT (#I1) TO #YYYYMMDD
    MOVE INV-E-FACE   (#I1) TO #E-FACE
    MOVE INV-E-END-DT (#I1) TO #E-END-DT
  END-IF
*
  IF #E-TYPE = ' '
    ESCAPE BOTTOM
  END-IF
*
  REPEAT
    MOVE #DD TO #E-NUM-BONDS
    MOVE 1 TO #NUM-BONDS
    IF #E-NUM-BONDS GT 1
      MOVE #E-NUM-BONDS TO #NUM-BONDS
    END-IF
*
    CALLNAT 'D3255SXX' #E-TYPE
      #YYYYMMDD
      #E-FACE
      #E-RED-VAL
      #E-UNIT-RED-VAL
      #E-TBL-LST-DT-UPDT
      #NUM-BONDS
*
    ADD #E-RED-VAL TO #FIN-TOT
    ADD #E-RED-VAL TO #PASSED-INV (2)
*
    IF #E-END-DT NE 0
      ADD 1 TO #MM
      IF #MM GT 12
        MOVE 1 TO #MM
        ADD 1 TO #YYYY
      END-IF
*
      IF #YYYYMM GT #E-END-DT-YYYYMM
        ESCAPE BOTTOM
      END-IF
    ELSE
      ESCAPE BOTTOM
    END-IF
  END-REPEAT /*(0230)
END-FOR
*
MOVE #FIN-TOT TO #PASSED-TOT-INV
SUBTRACT #CSV FROM #PASSED-TOT-INV
*
FOR #I1 1 TO 7
  ADD #PASSED-INV (#I1) TO #TOT-MANAGED-ASSETS
END-FOR
*
FOR #I1 1 TO 7
  COMPUTE ROUNDED #CURR-PCT = #PASSED-INV (#I1) / #TOT-MANAGED-ASSETS
  MULTIPLY #CURR-PCT BY 100
  COMPUTE ROUNDED #PASSED-WAR = #PASSED-WAR +
    ((#CURR-PCT * #REAL-RETURN (#I1)) / 100)
END-FOR
* * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-MSB
* * * * * * * * * * * * * *
*
FOR #I1 1 TO #MAX-MSB
  IF INV-MSB-TYPE (#I1) = ' '
    ESCAPE BOTTOM
  END-IF
*
  MOVE INV-MSB-TICK-SYM (#I1) TO #TICK-SYM
  PERFORM CHG-@
  FIND (1) TK-V WITH SYMBOL = #TICK-SYM
    MOVE UNIT-PRICE TO #UNIT-PRICE
    COMPUTE ROUNDED #AMT-7 = INV-MSB-UNITS (#I1) * #UNIT-PRICE
*
    IF #AMT-7 GT 9999999
      MOVE 9999999 TO #AMT
    END-IF
*
    IF INV-MSB-TYPE (#I1) = 'RM' OR = 'RS' OR = 'RB' OR = 'RP'
        OR = 'WM' OR = 'WS' OR = 'WB' OR = 'WP'
      ADD #AMT-7 TO #PASSED-RET-INV
    END-IF
*
    IF #PASSED-SUFFIX = 'PRO'  /* do not include ret amts if profile
      IF INV-MSB-TYPE (#I1) = 'MF'
        ADD #AMT-7 TO #PASSED-INV (3)
        ADD #AMT-7 TO #FIN-TOT
      END-IF
*
      IF INV-MSB-TYPE (#I1) = 'ST'
        ADD #AMT-7 TO #PASSED-INV (4)
        ADD #AMT-7 TO #FIN-TOT
      END-IF
*
      IF INV-MSB-TYPE (#I1) = 'BF'
        ADD #AMT-7 TO #PASSED-INV (5)
        ADD #AMT-7 TO #FIN-TOT
      END-IF
*
      IF  (INV-MSB-TYPE (#I1) = MASK ('P'))
        ADD #AMT-7 TO #PASSED-INV (7)
        ADD #AMT-7 TO #FIN-TOT
      END-IF
    ELSE
      ADD #AMT-7 TO #FIN-TOT
*
      IF INV-MSB-TYPE (#I1) = 'MF' OR = 'RM' OR = 'WM'
        ADD #AMT-7 TO #PASSED-INV (3)
      END-IF
*
      IF INV-MSB-TYPE (#I1) = 'ST' OR = 'RS' OR = 'WS'
        ADD #AMT-7 TO #PASSED-INV (4)
      END-IF
*
      IF INV-MSB-TYPE (#I1) = 'BF' OR = 'RB' OR = 'WB'
        ADD #AMT-7 TO #PASSED-INV (5)
      END-IF
*
      IF  (INV-MSB-TYPE (#I1) = MASK ('P'))
          OR (INV-MSB-TYPE (#I1) = 'RP' OR = 'WP')
        ADD #AMT-7 TO #PASSED-INV (7)
      END-IF
    END-IF
  END-FIND
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * *
DEFINE SUBROUTINE CHG-@
* * * * * * * * * * * *
*
FOR #I4 1 TO 7
  IF #TICK-SYM-X (#I4) = '@'
    IF (#I4 LE 6) AND (#TICK-SYM-X (#I4 + 1) NE ' ')
      MOVE '_' TO #TICK-SYM-X (#I4)
    ELSE
      RESET #TICK-SYM-X (#I4)
    END-IF
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
