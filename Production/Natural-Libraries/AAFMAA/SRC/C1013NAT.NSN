* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*****************************************************************************
* Program : C1013NAT                                                        *
*                                                                           *  
* Function : Shows screen for selection of available intro flag values      *
*                                                                           *
* vxt 04/29/2016 Initial creation.                                           *
*****************************************************************************
define data parameter
1 #code              (a1)
LOCAL
1 #SCN-CODES         (A5/12) init<'BLANK','S','K','G','I','C','B','O','P','Q','F','A'>
1 #SELECT            (A1/12)
1 #select-cv         (c/12)
1 #I                 (I4)
1 #j                 (I4)
*
END-DEFINE
*
DEFINE WINDOW HELPWINDOW
  SIZE 22*42
  BASE 8/4
*
SET WINDOW 'HELPWINDOW'
SET KEY ENTR NAMED 'Slct'
SET KEY PF3  NAMED 'Retrn'
*
for #i 1 12
  move '_' to #select(#i)
end-for
*
INPUT USING MAP 'HELPIMAP'
REPEAT UNTIL *PF-KEY = 'PF3'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'ENTR'
      RESET #j
      FOR #I 1 TO 12
        if #SELECT ( #I ) NE ' ' and #select(#i) ne '_'
          ADD 1 TO #j
        END-IF
      END-FOR
      IF #j > 1
        REINPUT 'Please select only one code'
          MARK 1
      END-IF
      for #I 1 to 12
        if #SELECT ( #I ) NE ' ' and #select(#i) ne '_'
          if #scn-codes(#i) = 'BLANK'
            reset #code
          else
            #CODE := #scn-CODES ( #I )
          end-if
          ESCAPE BOTTOM
        end-if
      end-for
      escape bottom
    NONE VALUE IGNORE
  END-DECIDE
  INPUT USING MAP 'HELPIMAP'
END-REPEAT

SET WINDOW OFF

END
