* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPALT2 - collect all aafmma allotment files and sort
*
DEFINE DATA
LOCAL
*
1 ALOT-FILE-IN    (A86)
1 REDEFINE ALOT-FILE-IN    /* air force active - ongoing credit record
 2 FAC-CODE        (A1)   /* "1" ongoing credit
 2 FAC-SSN         (A9)
 2 FAC-NAME        (A20)
 2 FAC-ACCT-NUM    (A17)
 2 FAC-FILL-1      (A5)
 2 FAC-AMOUNT      (N4.2)
 2 FAC-FILL-2      (A1)
 2 FAC-EFF-DATE-YY (N2)    /* YY
 2 FAC-EFF-DATE-MM (N2)    /* MM
 2 FAC-FILL-3      (A17)
 2 FAC-FILL-4      (A06)
1 REDEFINE ALOT-FILE-IN    /* army active - ongoing credits
 2 AAC-CODE        (A03)    /* "622" ongoing credit
 2 AAC-FILL-1      (A26)
 2 AAC-AMOUNT      (N08.2)
 2 AAC-SSN         (A09)
 2 AAC-FILL-2      (A06)
 2 AAC-NAME        (A22)
 2 AAC-FILL-3      (A06)
 2 AAC-EFF-DATE-YY (N02)    /* YY
 2 AAC-EFF-DATE-MM (N02)    /* MM
1 REDEFINE ALOT-FILE-IN    /* retired - ongoing credits
 2 RC-FILL-1      (A01)
 2 RC-NAME        (A19)
 2 RC-FILL-2      (A21)
 2 RC-EFF-DATE-MM (N2)
 2 RC-FILL-3      (A1)
 2 RC-EFF-DATE-YY (N2)
 2 RC-FILL-4      (N7)
 2 RC-AMT-1       (A04)
 2 RC-FILL-5      (A01)
 2 RC-AMT-2       (A02)
 2 RC-FILL-6      (A02)
 2 RC-SSN-1       (A03)
 2 RC-FILL-7      (A01)
 2 RC-SSN-2       (A02)
 2 RC-FILL-8      (A01)
 2 RC-SSN-3       (A04)
 2 RC-FILL-9      (A01)
 2 RC-SERV        (A01)
 2 RC-STATUS      (A01)
 2 RC-FILL-10     (A02)
 2 RC-CODE        (A02)
*
1 ALOT-REC     (A45)
1 REDEFINE ALOT-REC
 2 AL-SSN-A    (A09)
 2 REDEFINE AL-SSN-A
  3 AL-SSN     (N09)
 2 AL-SERV     (A01)
 2 AL-STATUS   (A01)
 2 AL-AMOUNT-A (A06)
 2 REDEFINE AL-AMOUNT-A
  3 AL-AMOUNT  (N4.2)
 2 AL-NAME     (A22)
 2 AL-EFF-YYYYMM (A6)
*
1 SORTED-ALOT-REC    (A45)
1 REDEFINE SORTED-ALOT-REC
 2 ALS-SSN-A    (A9)
 2 ALS-SERV     (A1)
 2 ALS-STATUS   (A1)
 2 ALS-AMOUNT   (N4.2)
 2 ALS-NAME     (A22)
 2 ALS-EFF-YYYYMM (A6)
*
1 #COUNTERS
 2 #RECS-READ        (P5)
 2 #RECS-SELECTED    (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #CNT-5            (P5)
 2 #CNT-6            (P5)
 2 #CNT-7            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY          (N4)
  2 #MM            (N2)
  2 #DD            (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM-A      (A6)
*
1 #AA-RECS      (N5)
1 #FA-RECS      (N5)
1 #XR-RECS      (N5)
1 #ALOT-RECS    (N5)
1 #LAST-SSN     (A9)
1 #LAST-AMT     (N4.2)
1 #B4           (N9)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'DFAS Allotment Exceptions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'DFAS Allotment Ongoing credits'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Duplicate DFAS allotments'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
*
READ WORK 2 RECORD ALOT-FILE-IN    /* aafmaa ARMY active
* IF #AA-RECS GT 10
*   ESCAPE TOP
* END-IF
*
  IF AAC-CODE NE '622'
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #AA-RECS
  MOVE AAC-SSN    TO AL-SSN-A
  MOVE 'A'        TO AL-SERV
  MOVE 'A'        TO AL-STATUS
  MOVE AAC-AMOUNT TO AL-AMOUNT
  MOVE AAC-NAME   TO AL-NAME
  MOVE AAC-EFF-DATE-MM TO #MM
*
  IF AAC-EFF-DATE-YY LT 50
    COMPUTE #YYYY = AAC-EFF-DATE-YY + 2000
  ELSE
    COMPUTE #YYYY = AAC-EFF-DATE-YY + 1900
  END-IF
  MOVE #YYYYMM-A  TO AL-EFF-YYYYMM
*
  WRITE WORK 5 ALOT-REC
  ADD 1 TO #ALOT-RECS
END-WORK
*
READ WORK 3 RECORD ALOT-FILE-IN    /* aafmaa AIR FORCE active
* IF #FA-RECS GT 10
*   ESCAPE TOP
* END-IF
*
  IF FAC-CODE NE '1'
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #FA-RECS
  MOVE FAC-SSN    TO AL-SSN-A
  MOVE 'F'        TO AL-SERV
  MOVE 'A'        TO AL-STATUS
  MOVE FAC-AMOUNT TO AL-AMOUNT
  MOVE FAC-NAME   TO AL-NAME
*
  IF FAC-EFF-DATE-YY LT 50
    COMPUTE #YYYY = FAC-EFF-DATE-YY + 2000
  ELSE
    COMPUTE #YYYY = FAC-EFF-DATE-YY + 1900
  END-IF
  MOVE #YYYYMM-A  TO AL-EFF-YYYYMM
*
  WRITE WORK 5 ALOT-REC
  ADD 1 TO #ALOT-RECS
END-WORK
*
READ WORK 4 RECORD ALOT-FILE-IN    /* aafmaa retired
* IF #XR-RECS GT 10
*   ESCAPE TOP
* END-IF
*
  IF RC-CODE NE '35'
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #XR-RECS
  COMPRESS RC-SSN-1 RC-SSN-2 RC-SSN-3 INTO AL-SSN-A LEAVING NO
  MOVE RC-SERV   TO AL-SERV
  MOVE RC-STATUS TO AL-STATUS
  COMPRESS RC-AMT-1 RC-AMT-2 INTO AL-AMOUNT-A LEAVING NO
  MOVE RC-NAME   TO AL-NAME
*
  IF RC-EFF-DATE-YY LT 50
    COMPUTE #YYYY = RC-EFF-DATE-YY + 2000
  ELSE
    COMPUTE #YYYY = RC-EFF-DATE-YY + 1900
  END-IF
  MOVE #YYYYMM-A  TO AL-EFF-YYYYMM
*
*
  WRITE WORK 5 ALOT-REC
  ADD 1 TO #ALOT-RECS
END-WORK
*
READ WORK 5 ALOT-REC
* below are cases where a non-member is paying for an AAFMAA member
*            non-member     AAFMAA Mbr
  MOVE AL-SSN TO #B4
  IF AL-SSN = 848423413 MOVE 248423413 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN END-IF  /* TRANSPOSED SSN
  IF AL-SSN = 480981133 MOVE 480981137 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN  END-IF /* transposed SSN
  IF AL-SSN = 516111395 MOVE 516111195 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN  END-IF /* transposed SSN
  IF AL-SSN = 509784352 MOVE 420805066 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN  END-IF /* spouse pays
  IF AL-SSN = 029421701 MOVE 524087921 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN  END-IF /* spouse pays
  IF AL-SSN = 004685683 MOVE 086387460 TO AL-SSN
    WRITE 'SSN' #B4 'switched to' AL-SSN  END-IF /* spouse pays
*
END-ALL
*
SORT BY AL-SSN-A
  USING AL-SERV AL-STATUS AL-AMOUNT AL-NAME AL-EFF-YYYYMM
*
  IF AL-SSN-A = #LAST-SSN
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-3 AL-SSN-A AL-AMOUNT #LAST-AMT
  END-IF
*
  MOVE AL-SSN-A  TO #LAST-SSN
  MOVE AL-AMOUNT TO #LAST-AMT
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2 AL-SSN-A AL-SERV AL-STATUS AL-AMOUNT AL-NAME
                     AL-EFF-YYYYMM
  MOVE AL-SSN-A    TO ALS-SSN-A
  MOVE AL-SERV     TO ALS-SERV
  MOVE AL-STATUS   TO ALS-STATUS
  MOVE AL-AMOUNT   TO ALS-AMOUNT
  MOVE AL-NAME     TO ALS-NAME
  MOVE AL-EFF-YYYYMM TO ALS-EFF-YYYYMM
  WRITE WORK 6 SORTED-ALOT-REC
END-SORT
*
WRITE 'tot army active alots     =' #AA-RECS
WRITE 'tot airforce active alots =' #FA-RECS
WRITE 'tot retired        alots ='  #XR-RECS
WRITE 'tot                alots ='  #ALOT-RECS
*
END
