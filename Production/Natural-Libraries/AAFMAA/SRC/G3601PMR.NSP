* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G3601PMR
* THIS PROG PROCESSES THE 'MR' MAILING Rqst UPDATE SCREEN (beneficiries)
* NGG  20121010   NG1  CN Expansion Project - beneficiary data are
*                      moved to new database files
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
*
1 #MR-MSG-NOTE-NUM (N6)
1 #MR-MSG-BEN      (A60/4)
1 #HOLD-MSG-BEN    (A60/4)
1 #MR-NT-REC-FND     (L)
1 #MR-MSG-FND-BEN    (L)
1 #MR-MSG-CHGED-BEN  (L)
1 #MANUAL            (L)
1 #NT-ISN            (P8)
* #NOTE-NUM          (P8)
*
1 #NOTE-REC-NOT-FOUND (L)
1 #NOTE-ISN           (P8)
*
* NT-V VIEW OF A-NOTES       /* NG1
1 NT-V VIEW OF A-BENE-DRAFT  /* NG1
 2 ID-NUMBER         (N6)    /* let D = bene designation (1-4)
 2 NOTE-LINE-NUM     (P1/40)
 2 NOTE-DATE          (N8/40)
 2 NOTE-CLERK-ID      (A3/40)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-4-POLS  (4)
   4 BENE-STATUS      (A1)
   4 BENE-RESTRICT    (A1)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)
    5 BENE-SHARE-TYP   (A1)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT  (A60/40)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXT-4-POLS    (4)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-LINES   (5)
     6 BENE-TEXT-1ST-25 (A25)
     6 BENE-TEXT-LST-35 (A35)
*
1 BENE-F VIEW OF A-BENE-FINAL    /* NG1
 2 ID-NUMBER         (N6)    /* let D = bene designation (1-4)
 2 NOTE-LINE-NUM     (P1/40)
 2 NOTE-DATE          (N8/40)
 2 NOTE-CLERK-ID      (A3/40)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-4-POLS  (4)
   4 BENE-STATUS      (A1)
   4 BENE-RESTRICT    (A1)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)
    5 BENE-SHARE-TYP   (A1)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT  (A60/40)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXT-4-POLS    (4)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-LINES   (5)
     6 BENE-TEXT-1ST-25 (A25)
     6 BENE-TEXT-LST-35 (A35)
*
1 #SCR-CV-X           (C)
1 #SCR-CV             (C/20)
*
1 #TRNSF-SUCC-IND     (A1)
1 #TRNSF-IND          (N1)
1 #SCR-BN-CV          (C/20)
1 #SCR-BN-MR-TYPE     (A1/20)
1 REDEFINE #SCR-BN-MR-TYPE
 2 #SCR-BN-MR-TYPE-2  (A1/20)
*
1 #SCR-BNP-MR-TYPE    (A1/20)
1 REDEFINE #SCR-BNP-MR-TYPE
 2 #SCR-BNP-MR-TYPE-2  (A1/20)
1 REDEFINE #SCR-BNP-MR-TYPE
 2 #SCR-BNP-MR-TYPE-3  (A1/20)
*
1 #BNP-TBL    /* this tbl is also in g3700pml
 2 #BNP-1    (A20) INIT <'PRIN Bene By law'>
 2 #BNP-2    (A20) INIT <'ALT Bene By law'>
 2 #BNP-3    (A20) INIT <'Full Name Needed'>
 2 #BNP-4    (A20) INIT <'Rel of Bene Needed'>
 2 #BNP-5    (A20) INIT <'Exact Intentions'>
 2 #BNP-6    (A20) INIT <'Unequal Distribution'>
 2 #BNP-7    (A20) INIT <'No Beneficiary'>
 2 #BNP-8    (A20) INIT <'In Accord W Tel Conv'>
 2 #BNP-9    (A20) INIT <'Owner Signature Reqd'>
 2 #BNP-10   (A20) INIT <'Successor Owner Reqd'>
 2 #BNP-11   (A20) INIT <'Both of Above'>
 2 #BNP-REST1 (A20/10)
1 REDEFINE #BNP-TBL
 2 #BNP-DESC (A20/20)
*
1 #MR-X            (I2)
1 #MR-BNP-X        (I2)
1 #NO-STORE        (L)
1 #NO-PENDING      (L)
1 #NO-FINAL        (L)
* 1 #NO-OWNER       (L)
1 #OWNER-IS-MBR   (L)
1 #SCR-ID-N         (N6)
1 REDEFINE #SCR-ID-N
 2 #SCR-ID-A        (A6)
1 #I             (I1)
1 #I1            (I1)
1 #I2            (I1)
1 #PROCESS-FLAG  (L) INIT<FALSE>
1 #TEXT          (A75)
1 #NAME          (A20)
1 #TYPE          (A3)
*
1 #DATN          (N08)
1 REDEFINE #DATN
  2 #DATE-CENT   (N02)
  2 #DATE-YY     (N02)
  2 #DATE-MM     (N02)
  2 #DATE-DD     (N02)
*
1 #TODAY-YYYYMMDD    (N8)
1 REDEFINE #TODAY-YYYYMMDD
 2 #TODAY-YYYY       (N4)
 2 REDEFINE #TODAY-YYYY
  3 #TODAY-CENT      (N2)
  3 #TODAY-YY        (N2)
 2 #TODAY-MMDD       (N4)
 2 REDEFINE #TODAY-MMDD
  3 #TODAY-MM         (N2)
  3 #TODAY-DD         (N2)
*
1 #CENT-1ST-GUESS  (P1) /* IF PREVIOUS CENTURY 1 ELSE 0
1 #CENT-CUTOFF-YRS (P3) /* NUM YRS PRIOR TO TODAY YYYY TO CUTOFF
1 #CUTOFF-YYYY
 2 #CUTOFF-CENT   (N2)
 2 #CUTOFF-YY     (N2)
1 REDEFINE #CUTOFF-YYYY
 2 #CUTOFF-YYYY-N (N4)
*
1 #WYYYYMMDD
 2 #WCENT    (N2)
 2 #WYY      (N2)
 2 #WMM      (N2)
 2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
 2 #WYYYYMMDD-N (N8)
1 REDEFINE #WYYYYMMDD
 2 #WYYYY-N   (N4)
 2 #WMMDD-N   (N4)
*
1 #WMMDDYY
 2 #WMM      (N2)
 2 #WDD      (N2)
 2 #WYY      (N2)
1 REDEFINE #WMMDDYY
 2 #WMMDDYY-A (A6)
*
1 MR-R01 VIEW OF REQUEST
  2 ID-NUMBER        (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A    (A6)
  2 MEMBER-NAME      (A20)
  2 CLERK-ID         (A3)
  2 ZIP-CODE         (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND         (N1)
   3 MSG-IND         (N1)
   3 RET-IND         (N1)
   3 FIN-IND         (N1)
   3 FAS-IND         (N1)
  2 RQST-TABLE       (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND         (A1)
   3 MAL-IND         (A1)
   3 BEN-SHO-IND     (A1)
   3 ACC-IND         (A1)
   3 ALI-IND         (A1)
   3 APP-IND         (A1)
   3 PIL-IND         (A1)
   3 CO-IND          (A1)
   3 VP-IND          (A1)
   3 MS-IND          (A1)
  2 REQUEST-DATE     (N4)
  2 ASSUM-RET-DATE   (N8)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
  2 MESSAGE          (A60/20)
*
1 ST-V VIEW OF A-STATUS
  2 NOTE-REC-IND     (A1)
*
1 MR-S01 VIEW OF A-STATUS
  2 ID-NUMBER        (N6)
  2 MEMBER-CONTACT-ID                          /* TT1
  2 WIDOW-FLAG       (A1)
  2 SUSPEND-CODE     (A1)
  2 INTRO-FLAG       (A1)
*   2 DATE-OF-BIRTH    (N8)
  2 NUMBER-MEMBERSHIPS (N1)
  2 RESTRICT-MBR-SW  (A1)
  2 NOTE-REC-IND     (A1)
  2 NAME             (A25)
  2 NAME-SP          (A25)
  2 STATUS-CODE-SP   (A1)
  2 RANK             (A3)
  2 HANDLE-FLAG      (A1)
*   2 ADDRESS-CHG-CODE (A1)
*   2 ADDRESS-1        (A25)
  2 S-ADDRESS-CODE   (A1)
  2 MILITARY-STATUS  (A1)
  2 MILITARY-SERVICE (A3)
  2 CHIEF-OF-STAFF-IND (A1)
  2 DATE-RETIRED     (N8)
  2 RSV-NG-20-YR-CERT-LTR-DATE  (N8)
  2 ACTIVE-DUTY-BASE-DATE (N8)
  2 BASE-PAY-EF-DATE      (N8)
  2 DATE-OF-RANK     (N8)
  2 REDEFINE DATE-OF-RANK
   3 DOR-CENT        (N2)
   3 DOR-YY          (N2)
   3 DOR-MM          (N2)
   3 DOR-DD          (N2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX         /* NATURAL ERROR ROUTINE
*
MOVE '*' TO #CGA-SCR-COMMAND
*
FIND (1) MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  FOR #I1 1 TO 4
    MOVE MR-R01.MESSAGE (#I1 + 10) TO #MR-MSG-BEN (#I1)
    MOVE MR-R01.MESSAGE (#I1 + 10) TO #HOLD-MSG-BEN (#I1)
  END-FOR
END-FIND
*
FIND (1) MR-S01 WITH MEMBER-KEY = #CGA-ID-G
  IF NO RECORD FOUND
    FIND (1) MR-S01 WITH WIDOW-KEY = #CGA-ID-G
    END-FIND
  END-NOREC
END-FIND
*
IF MR-S01.WIDOW-FLAG = 'Y'
  MOVE MR-S01.NAME-SP TO #NAME
ELSE
  MOVE MR-S01.NAME TO #NAME
END-IF
*
* BNx SHOULD BE INCLUDED IN LIST BELOW BUT IS NOT ON THE MENU
IF #CGA-SCR-SUFFIX = 'BNF'  OR = 'BNB' OR = 'BNA' OR = 'BNC' OR = 'BND'
  MOVE #CGA-SCR-SUFFIX TO #TYPE
ELSE
  IF #CGA-SCR-SUFFIX = 'BEN' OR = 'BNP'  /* these require bene menu
                  OR = 'BNT' OR = 'BNL' OR = 'BNN'
    DECIDE ON FIRST #CGA-SCR-SUFFIX
      VALUE 'BNT' MOVE 'X' TO #SCR-BN-MR-TYPE (6)
      VALUE 'BNL' MOVE 'X' TO #SCR-BN-MR-TYPE (7)
      VALUE 'BNN' MOVE 'X' TO #SCR-BN-MR-TYPE (8)
      NONE IGNORE
    END-DECIDE
*
    IF #CGA-SCR-SUFFIX = 'BNT' OR = 'BNL' OR = 'BNN'
      MOVE 'Enter Transfer Indicator' TO #TEXT
      RESET #CGA-SCR-SUFFIX
      INPUT TEXT #TEXT MARK *#TRNSF-IND USING MAP 'G3610MMR'
    ELSE
      MOVE 'SELECT TYPE OF "MR"' TO #TEXT
      RESET #CGA-SCR-SUFFIX
      INPUT TEXT #TEXT MARK *#SCR-BN-MR-TYPE (1) USING MAP 'G3610MMR'
    END-IF
*
    IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
    END-IF
*
    RESET #MR-X #MR-BNP-X #MR-MSG-CHGED-BEN
    FOR #I1 1 TO 4
      IF #MR-MSG-BEN (#I1) NE ' '
        MOVE TRUE TO #MR-MSG-FND-BEN
      END-IF
*
      IF #MR-MSG-BEN (#I1) NE #HOLD-MSG-BEN (#I1)
        MOVE TRUE TO #MR-MSG-CHGED-BEN
      END-IF
    END-FOR
*
    FOR #I1 1 TO 20
      IF #SCR-BN-MR-TYPE (#I1) NE ' '
        IF #MR-X NE 0
          RESET #MR-X
          REINPUT 'ONLY 1 TYPE CAN BE SELECTED'
          MARK *#SCR-BN-MR-TYPE (#I1)
        END-IF
*
        MOVE #I1 TO #MR-X
      END-IF
    END-FOR
*
    FOR #I1 1 TO 20
      IF #SCR-BNP-MR-TYPE (#I1) NE ' '
        IF  (#MR-X NE 0)
         OR (#MR-BNP-X NE 0)
          RESET #MR-X #MR-BNP-X
          REINPUT 'ONLY 1 TYPE CAN BE SELECTED'
          MARK *#SCR-BNP-MR-TYPE (#I1)
        END-IF
*
        MOVE #I1 TO #MR-BNP-X
      END-IF
    END-FOR
*
    IF (#MR-X = 0) AND (#MR-BNP-X = 0)
     AND (NOT #MR-MSG-CHGED-BEN)
      REINPUT 'Select an option'
      MARK *#SCR-BN-MR-TYPE (1)
    END-IF
*
    IF    (#MR-X = 6 OR = 8)
      AND (NOT #TRNSF-IND = 1 OR = 2 OR = 3 OR = 4)
      REINPUT 'Indicator must be 1/2/3/4'
      MARK *#TRNSF-IND
    END-IF
*
    IF    (#MR-X = 7)
      AND (NOT #TRNSF-IND = 1 OR = 2 OR = 3)
      REINPUT 'Indicator must be 1 or 2 OR 3'
      MARK *#TRNSF-IND
    END-IF
*
    IF    (#MR-X = 6 OR = 7 OR = 8)
      IF (NOT #TRNSF-SUCC-IND = 'Y' OR = 'N' OR = ' ')
        REINPUT 'Successor Owner Form must be Y/N/Blank'
        MARK *#TRNSF-SUCC-IND
      END-IF
*
      IF   (#MR-X = 6)
       AND (#TRNSF-SUCC-IND = 'Y')
        REINPUT 'Successor Owner Form not allowed for Trust Transfer'
        MARK *#TRNSF-SUCC-IND
      END-IF
    END-IF
*
    DECIDE ON FIRST #MR-X
      VALUE 1  MOVE 'BNF'  TO #TYPE
      VALUE 2  MOVE 'BNB'  TO #TYPE
      VALUE 3  MOVE 'BNA'  TO #TYPE
      VALUE 4  MOVE 'BNC'  TO #TYPE
      VALUE 5  MOVE 'BND'  TO #TYPE
      VALUE 6  MOVE 'BNT'  TO #TYPE
      VALUE 7  MOVE 'BNL'  TO #TYPE
      VALUE 8  MOVE 'BNN'  TO #TYPE
     NONE IGNORE
    END-DECIDE
*
    IF #MR-BNP-X NE 0
      MOVE 'BNP' TO #TYPE
    END-IF
  END-IF
END-IF
*
RESET #TEXT
*
MOVE *DATN TO #DATN
MOVE *DATN TO #TODAY-YYYYMMDD
MOVE #CGA-SCR-ID TO #SCR-ID-N
*
PERFORM PROCESS-LINE
*
IF   (#TEXT = ' ')
 AND (#TYPE = MASK ('BN'))
  COMPRESS #TYPE  '-Beneficiary Designation Requested' INTO #TEXT
END-IF
*
IF #MR-MSG-FND-BEN
  IF #TEXT = ' '
    MOVE 'Ben Message Processed' TO #TEXT
  ELSE
    COMPRESS #TEXT '(MSB)' INTO #TEXT
  END-IF
END-IF
*
COMPRESS #TEXT 'for' #NAME INTO #TEXT
*
MOVE (AD=P) TO #SCR-BN-CV (*)
MOVE 'MR' TO #CGA-SCR-COMMAND
*
INPUT TEXT #TEXT MARK *#CGA-SCR-COMMAND USING MAP 'G3610MMR'
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-LINE
* * * * * * * * * * * * * * * *
*
RESET #NO-STORE
*
IF   ((#TYPE = MASK ('BN')) AND (#TYPE NE 'BND')) /* bnd is okay
 AND (NOT #CGA-CLERK = 'LCB' OR = 'LCC' OR = 'GME' OR = 'BMG' OR = 'MAW'
    OR = 'KHB' OR = 'SKS' OR = 'ALB' OR = 'DRW' OR = 'CAD')
  MOVE 'REJECTED - Only Selected user"s can do BENE RQSTS' TO #TEXT
  ESCAPE ROUTINE
END-IF
*
IF #TYPE = 'BNF' OR = 'BNA' OR = 'BNC' OR = 'BNT' OR = 'BNL' OR = 'BNN'
  RESET #OWNER-IS-MBR
  MOVE TRUE TO #NO-PENDING
* COMPUTE #NOTE-NUM = #CGA-ID + 200000           NG1
  FIND (1) NT-V WITH ID-NUMBER = #CGA-ID     /*  NG1
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
*
    FOR #I1 1 TO 4
      IF NT-V.BENE-STATUS (#I1) = 'P'
        IF   (#TYPE = 'BNT')
         AND (NOT NT-V.BENE-TYPE (#I1, 1) = 'K' OR = 'P' OR = 'X')
          MOVE 'REJECTED - BNT only applies to TYP P/K/X' TO #TEXT
          ESCAPE ROUTINE
        END-IF
*
        IF   (#TYPE = 'BNN' OR = 'BNL')
         AND (NT-V.BENE-TYPE (#I1, 1) = 'K' OR = 'P')
          MOVE 'REJECTED - BNN/BNL does not apply to type P/K' TO #TEXT
          ESCAPE ROUTINE
        END-IF
*
        IF   (#TYPE = 'BNT' OR = 'BNL' OR = 'BNN')
         AND (NOT MR-S01.INTRO-FLAG = 'S' OR = 'K' OR = 'K')
         AND (NT-V.BENE-TEXT-LST-35 (#I1, 2, 5) = ' ')
           MOVE TRUE TO #OWNER-IS-MBR
        END-IF
*
        FOR #I2 1 TO 2
          IF NT-V.BENE-TYPE (#I1, #I2) = 'M'
            MOVE TRUE TO #MANUAL
            ESCAPE BOTTOM
          END-IF
        END-FOR
*
        RESET #NO-PENDING
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
END-IF
*
IF #TYPE = 'BNF' OR = 'BNN' OR = 'BNT' OR = 'BNL' /* pending is required
  IF #MANUAL
    MOVE 'REJECTED - Manual Designations exist' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF #NO-PENDING
    MOVE 'REJECTED - No pending beneficiaries to process' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF #OWNER-IS-MBR
     MOVE 'REJECTED - Non-MBR Owner is reqd for Trans of Ownership'
        TO  #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
* IF #TYPE = 'BNA' OR = 'BNC'  /* BNA removed 10/97 as per lcb
IF #TYPE = 'BNC'     /* pending is not allowed
  IF NOT #NO-PENDING
    MOVE 'REJECTED - Pending beneficiaries not allowed' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF #TYPE = 'BND' OR = 'BNA' OR = 'BNC'  /* finalized are req
  MOVE TRUE TO #NO-FINAL
* COMPUTE #NOTE-NUM = #CGA-ID + 400000                     NG1 Start
  FIND (1) BENE-F WITH ID-NUMBER = #CGA-ID
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
*
    FOR #I1 1 TO 4
      IF BENE-F.BENE-TYPE (#I1, 1) NE ' '
        FOR #I2 1 TO 2
          IF BENE-F.BENE-TYPE (#I1, #I2) = 'M'
            MOVE TRUE TO #MANUAL
            ESCAPE BOTTOM
          END-IF
        END-FOR
*
        RESET #NO-FINAL
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
*                                                          NG1 End
  IF #MANUAL
    MOVE 'REJECTED - Manual Designations exist' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF #NO-FINAL
    MOVE 'REJECTED - No final beneficiaries to process' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF (F-UNDEL-ADDRESS(<MR-S01.MEMBER-CONTACT-ID>))                 /* TT1
  MOVE 'REJECTED - no deliverable address' TO #TEXT
  ESCAPE ROUTINE
END-IF
*
IF (#TYPE = 'BNP') AND (#MR-BNP-X = 10 OR = 11)
 AND (NOT MR-S01.INTRO-FLAG = 'S' OR = 'K' OR = 'G')
  MOVE 'Only allowed on SPS/GEN Policy' TO #TEXT
  ESCAPE ROUTINE
END-IF
*
FIND (1) MR-R01 WITH ID-NUMBER   = #CGA-SCR-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  IF REQUEST-DATE = 9999
    MOVE 'EDS "MR" request run is in progress - try later' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
* allow re-requesting of ben types
*
  IF   (#TYPE = 'BNP' OR = 'BNN' OR = 'BNL' OR = 'BNT')
   AND ((MR-R01.RET-IND NE 0)
    OR  (MR-R01.APP-IND NE ' ')
    OR  (MR-R01.VP-IND NE ' '))
    MOVE 'Not allowed same day as "RET" or "APP" or "VP"' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  MOVE TRUE TO #NO-STORE
  ESCAPE BOTTOM
END-FIND /* (0342)
*
IF #NO-STORE
  FIND (1) MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
    PERFORM BUILD-MR-REC
    UPDATE
  END-FIND
ELSE
  PERFORM BUILD-MR-REC
  STORE MR-R01
END-IF
*
END TRANSACTION
*
END-SUBROUTINE /* (0405)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-MR-REC
* * * * * * * * * * * * * * * *
*
MOVE #CGA-SCR-ID TO MR-R01.ID-NUMBER
MOVE #NAME       TO MR-R01.MEMBER-NAME
MOVE #CGA-CLERK  TO MR-R01.CLERK-ID
MOVE #TODAY-MMDD  TO MR-R01.REQUEST-DATE
*
IF #TYPE = 'BND'    /* bene display only
  MOVE '1' TO MR-R01.BEN-SHO-IND
END-IF
*
IF #TYPE = 'BNF'    /*   bene form
  MOVE '1' TO MR-R01.BEN-IND
END-IF
*
IF #TYPE = 'BNB'    /* bene blank form
  MOVE '2' TO MR-R01.BEN-IND
END-IF
*
IF #TYPE = 'BNA'    /* addl bene blank form
  MOVE '3' TO MR-R01.BEN-IND
END-IF
*
IF #TYPE = 'BNC'    /* vhange bene blank form
  MOVE '4' TO MR-R01.BEN-IND
END-IF
*
IF #TYPE = 'BNP'    /* bene problem
  MOVE '5' TO MR-R01.BEN-IND
  MOVE #MR-BNP-X TO MR-R01.ASSUM-RET-DATE-3 /* save type of problem
END-IF
*
IF #TYPE = 'BNT'    /* bene transfer to trust
  MOVE '6' TO MR-R01.BEN-IND
  MOVE #TRNSF-IND TO MR-R01.ASSUM-RET-DATE-3 /* save type
*
  IF #TRNSF-SUCC-IND = 'Y'
    MOVE 1 TO MR-R01.ASSUM-RET-DATE-2
  END-IF
END-IF
*
IF #TYPE = 'BNL'    /* bene transfer with love
  MOVE '7' TO MR-R01.BEN-IND
  MOVE #TRNSF-IND TO MR-R01.ASSUM-RET-DATE-3 /* save type
*
  IF #TRNSF-SUCC-IND = 'Y'
    MOVE 1 TO MR-R01.ASSUM-RET-DATE-2
  END-IF
END-IF
*
IF #TYPE = 'BNN'    /* bene transfer with no love
  MOVE '8' TO MR-R01.BEN-IND
  MOVE #TRNSF-IND TO MR-R01.ASSUM-RET-DATE-3 /* save type
*
  IF #TRNSF-SUCC-IND = 'Y'
    MOVE 1 TO MR-R01.ASSUM-RET-DATE-2
  END-IF
END-IF
*
IF #MR-MSG-FND-BEN
  IF MR-R01.MSG-IND = 1
    MOVE 3 TO MR-R01.MSG-IND
  ELSE
    MOVE 2 TO MR-R01.MSG-IND
  END-IF
END-IF
*
IF #MR-MSG-FND-BEN
*
*   PM 05/2000 - FREE NOTE RECS 600000 - 700000
*
  FOR #I1 1 TO 4
    MOVE #MR-MSG-BEN (#I1) TO MR-R01.MESSAGE (#I1 + 10)
  END-FOR
ELSE
  RESET MR-R01.MESSAGE (11:14)
END-IF
*
END-SUBROUTINE /* (0570)
*
END
