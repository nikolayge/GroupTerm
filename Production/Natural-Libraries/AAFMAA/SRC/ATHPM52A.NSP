* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
** PROGRAM-ID     ATHPM52A    Widow extract reports
*
DEFINE DATA
LOCAL  USING GONERLXX                       /* SAG RH1 11292005 SS
LOCAL
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  09162005 DW1  Sort reports by CN
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
* SAG  12092005 RH1  SS - Modified the call to external object ABEND & *
*                    replaced with copycode GABNDCX6.                  *
************************************************************************
*
1 EXTRACT-RECORD
*************************************************************
*
* EXTRACT CODES:                                            *
*         A -  WIDOW TICKLER LETTERS - AGE 60               *
*         B -  MEMBER WITH DECEASED SPOUSE - AGE 60         *
*         C -  WIDOW TICKLER LETTERS - CHILD AGE 18         *
*         D -  WIDOW TICKLER LETTERS - YOUNG CHILD AGE 18   *
*         E -  WIDOW TICKLER LETTERS - AGE 62               *
*         F -  MEMBER WITH DECEASED SPOUSE - AGE 62         *
*         G -  WIDOW TICKLER LETTERS - AGE 65               *
*         H -  MEMBER WITH DECEASED SPOUSE - AGE 65         *
*************************************************************
  2 EXTRACT-CODE             (A1)
  2 EXTRACT-KEY              (A50)
*
  2 REDEFINE EXTRACT-KEY
    3 LETTER-TYPE            (A1)
    3 LETTER-CN              (A6)    /* DW1
    3 LETTER-NAME            (A25)
*
  2 REDEFINE EXTRACT-KEY
    3 DEP-LETTER-TYPE        (A1)
    3 DEP-LETTER-CN          (A6)    /* DW1
    3 DEP-LETTER-NAME        (A25)
*
  2 REDEFINE EXTRACT-KEY
    3 DEP-16-LETTER-TYPE     (A1)
    3 DEP-16-LETTER-CN          (A6)    /* DW1
    3 DEP-16-LETTER-NAME     (A25)
*
  2 EXTRACT-DATA             (A150)
*
  2 REDEFINE EXTRACT-DATA
    3 LET-ADDR1              (A25)
    3 LET-ADDR2              (A25)
    3 LET-ADDR3              (A25)
    3 LET-ZIP                (P9)
    3 LET-ID-NO              (A9)
    3 LET-DOB                (N8)
    3 REDEFINE LET-DOB
     4 LET-DOB-YYYY  (A4)
     4 LET-DOB-MM    (A2)
     4 LET-DOB-DD    (A2)
    3 LET-MEMB               (A25)
    3 LET-WIDOW-STATUS       (A1)
*
  2 REDEFINE EXTRACT-DATA
    3 DEP-LET-ADDR1          (A25)
    3 DEP-LET-ADDR2          (A25)
    3 DEP-LET-ADDR3          (A25)
    3 DEP-LET-ZIP            (P9)
    3 DEP-LET-ID-NO          (A6)
    3 DEP-LET-CHILD-ST       (A1)
    3 DEP-LET-FILLER         (A2)
    3 DEP-LET-DOB            (N8)
    3 REDEFINE DEP-LET-DOB
     4 DEP-LET-DOB-YYYY  (A4)
     4 DEP-LET-DOB-MM    (A2)
     4 DEP-LET-DOB-DD    (A2)
    3 DEP-LET-CHILD          (A15)
    3 DEP-LET-MEMB           (A25)
    3 DEP-LET-WIDOW-STATUS   (A1)
*
1 #NEWPAGE-M                 (L)
1 #NEWPAGE-W                 (L)
1 #REPORT-HEADING-W          (A22)
1 #REPORT-HEADING-60-W       (A22) INIT <'WIDOW/ER AGE 60 REPORT'>
1 #REPORT-HEADING-62-W       (A22) INIT <'WIDOW/ER AGE 62 REPORT'>
1 #REPORT-HEADING-65-W       (A22) INIT <'WIDOW/ER AGE 65 REPORT'>
1 #REPORT-HD-M     (A38)
1 #REPORT-HD-M-60  (A38) INIT <'MEMBER WITH DECEASED SPOUSE AGE 60 RPT'>
1 #REPORT-HD-M-62  (A38) INIT <'MEMBER WITH DECEASED SPOUSE AGE 62 RPT'>
1 #REPORT-HD-M-65  (A38) INIT <'MEMBER WITH DECEASED SPOUSE AGE 65 RPT'>
1 #LAST-EXTRACT-CODE         (A1)
1 #REPORT-TYPE   (A35)
1 #LAST-WID-LTR-TYPE-A (A1)
1 #LAST-WID-LTR-TYPE-B (A1)
1 #LAST-DEP-LTR-TYPE-18 (A1)
1 #LAST-DEP-LTR-TYPE-16 (A1)
1 #DATE-DISP   (A10)
1 #CNT-2    (N6)
1 #CNT-3    (N6)
1 #CNT-4    (N6)
1 #CNT-5    (N6)
*
1 ABEND-CODE                 (I4)
1 #ID-NUMBER-G               (A6)
1 REDEFINE #ID-NUMBER-G
  2 #ID-NUMBER               (N6)
*
*****************************************************************
*          L I N K A G E   S E C T I O N                        *
*****************************************************************
1 PARM-DATE      (A10)
1 REDEFINE PARM-DATE
  2 PARM-MONTH       (A2)
  2 PARM-FILL1       (A1)
  2 PARM-DAY         (A2)
  2 PARM-FILL2       (A1)
  2 PARM-YYYY        (A4)
*
END-DEFINE
*
*****************************************************************
*          P R O C E D U R E   D I V I S I O N                  *
*****************************************************************
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR    /* SAG RH1 11292005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*     MOVE +4095 TO ABEND-CODE
*     CALL 'ABEND' USING ABEND-CODE
INCLUDE GABNDCX3                                    /* SAG RH1 11292005 SS - END
END-ERROR
*
FORMAT PS=55 LS=133
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM 5X 'DATE:' *DAT4U
                  5X 'TIME:' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
   #REPORT-HEADING-W
    5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
   #REPORT-HD-M
    5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
   'WIDOW CHILD AGE 18 REPORT' 5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=133 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
   'WIDOW CHILD AGE 16 REPORT' 5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 3 ONCE RECORD PARM-DATE   /* DATE (09/01/1999)
*
IF PARM-DATE NE MASK(MM'/01/'YYYY)
*   WRITE 'PARM-DATE NOT VALID "MM/01/YYYY"'       /* SAG 12092005 RH1 SS - BEGIN
*   MOVE +10 TO ABEND-CODE
*   CALL 'ABEND' ABEND-CODE
  #BATCH-LITERAL-1 := 'PARM-DATE'
  #BATCH-LITERAL-4 := 'PARM-DATE NOT VALID "MM/01/YYYY"'
  INCLUDE GABNDCX6
  TERMINATE                                        /* SAG 12092005 RH1 SS - END
END-IF
*
READ WORK 1 RECORD EXTRACT-RECORD
*
END-ALL
*
SORT BY EXTRACT-CODE LETTER-TYPE EXTRACT-KEY
  USING EXTRACT-DATA
*
  IF EXTRACT-CODE = 'A' OR = 'E' OR = 'G'
    IF LETTER-TYPE = 'A'
      MOVE 'With SBP option' TO #REPORT-TYPE
    ELSE
      IF LETTER-TYPE = 'B'
        MOVE 'Without SBP option' TO #REPORT-TYPE
      ELSE
        MOVE 'With unknown SBP option' TO #REPORT-TYPE
      END-IF
    END-IF
*
    IF EXTRACT-CODE NE #LAST-EXTRACT-CODE
       RESET #CNT-2
       MOVE TRUE TO #NEWPAGE-W
       MOVE EXTRACT-CODE TO #LAST-EXTRACT-CODE
    END-IF
*
    IF EXTRACT-CODE = 'A'
       MOVE #REPORT-HEADING-60-W TO #REPORT-HEADING-W
    ELSE
       IF EXTRACT-CODE = 'E'
          MOVE #REPORT-HEADING-62-W TO #REPORT-HEADING-W
       ELSE
          MOVE #REPORT-HEADING-65-W TO #REPORT-HEADING-W
       END-IF
    END-IF
*
    IF LETTER-TYPE NE #LAST-WID-LTR-TYPE-A
       MOVE LETTER-TYPE TO #LAST-WID-LTR-TYPE-A
       MOVE TRUE TO #NEWPAGE-W
    END-IF
*
    IF #NEWPAGE-W
      NEWPAGE (2)
      WRITE (2) #REPORT-TYPE
      RESET #NEWPAGE-W
    END-IF
*
    COMPRESS LET-DOB-MM  LET-DOB-DD LET-DOB-YYYY
             INTO #DATE-DISP WITH '/'
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2
      'ID/NUMBER' LET-ID-NO
      'CD' EXTRACT-CODE
      'DATE OF/BIRTH' #DATE-DISP
      'ST/CD'     LET-WIDOW-STATUS
      'NAME'      LETTER-NAME
      'ADDRESS-1' LET-ADDR1
      'ADDRESS-2' LET-ADDR2
      'ZIP'       LET-ZIP
      'MEMBERS/NAME' LET-MEMB (EM=XXXXXXXX)
*
    IF LET-ADDR3 NE ' '
      WRITE (2) '     ADDRESS-3 = ' LET-ADDR3
    END-IF
  END-IF
*
  IF EXTRACT-CODE = 'B' OR = 'F' OR = 'H'
    IF LETTER-TYPE = 'A'
      MOVE 'With SBP option' TO #REPORT-TYPE
    ELSE
      IF LETTER-TYPE = 'B'
        MOVE 'Without SBP option' TO #REPORT-TYPE
      ELSE
        MOVE 'With unknown SBP option' TO #REPORT-TYPE
      END-IF
    END-IF
*
    IF EXTRACT-CODE NE #LAST-EXTRACT-CODE
       RESET #CNT-3
       MOVE EXTRACT-CODE TO #LAST-EXTRACT-CODE
       MOVE TRUE TO #NEWPAGE-M
    END-IF
*
    IF EXTRACT-CODE = 'B'
       MOVE #REPORT-HD-M-60 TO #REPORT-HD-M
    ELSE
       IF EXTRACT-CODE = 'F'
          MOVE #REPORT-HD-M-62 TO #REPORT-HD-M
       ELSE
          MOVE #REPORT-HD-M-65 TO #REPORT-HD-M
       END-IF
    END-IF
*
    IF LETTER-TYPE NE #LAST-WID-LTR-TYPE-B
       MOVE LETTER-TYPE TO #LAST-WID-LTR-TYPE-B
       MOVE TRUE TO #NEWPAGE-M
    END-IF
*
    IF #NEWPAGE-M
        NEWPAGE (3)
        WRITE (3) #REPORT-TYPE
        RESET #NEWPAGE-M
    END-IF
*
    COMPRESS LET-DOB-MM  LET-DOB-DD LET-DOB-YYYY
               INTO #DATE-DISP WITH '/'
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-3
      'ID/NUMBER' LET-ID-NO
      'CD' EXTRACT-CODE
      'Date of/Birth' #DATE-DISP
      'St/Cd'     LET-WIDOW-STATUS
      'Name'      LETTER-NAME
      'Address-1' LET-ADDR1
      'Address-2' LET-ADDR2
      'Zip'       LET-ZIP
      'MEMBERS/NAME' LET-MEMB (EM=XXXXXXXX)
*
    IF LET-ADDR3 NE ' '
      WRITE (3) '     Address-3 = ' LET-ADDR3
    END-IF
  END-IF
*
  IF EXTRACT-CODE = 'C'
    IF LETTER-TYPE = 'A'
      MOVE 'VA-DIC Granted' TO #REPORT-TYPE
    ELSE
      IF LETTER-TYPE = 'B'
        MOVE 'VA-DIC Denied' TO #REPORT-TYPE
      ELSE
        MOVE 'VA-DIC-Unknown' TO #REPORT-TYPE
      END-IF
    END-IF
*
    IF DEP-LETTER-TYPE NE #LAST-DEP-LTR-TYPE-18
      NEWPAGE (4)
      WRITE (4) #REPORT-TYPE
    END-IF
    MOVE DEP-LETTER-TYPE TO #LAST-DEP-LTR-TYPE-18
*
    COMPRESS DEP-LET-DOB-MM  DEP-LET-DOB-DD DEP-LET-DOB-YYYY
               INTO #DATE-DISP WITH '/'
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4
      'ID/NUMBER'  DEP-LET-ID-NO
      'Widow/Name' DEP-LETTER-NAME   (EM=XXXXXXXXXXXXX)
      'Wd/St'      DEP-LET-WIDOW-STATUS
      'Address-1'  DEP-LET-ADDR1
      'Address-2'  DEP-LET-ADDR2
      'Zip'        DEP-LET-ZIP
      'Childs/Name' DEP-LET-CHILD
      'Childs/Date of/Birth' #DATE-DISP
      'Mbr/Name'  DEP-LET-MEMB   (EM=XXXXXXXXXX)
*
    IF LET-ADDR3 NE ' '
      WRITE (4) '     Address-3 = ' LET-ADDR3
    END-IF
  END-IF
*
  IF EXTRACT-CODE = 'D'
    COMPRESS DEP-LET-DOB-MM  DEP-LET-DOB-DD DEP-LET-DOB-YYYY
               INTO #DATE-DISP WITH '/'
    ADD 1 TO #CNT-5
    DISPLAY (5) #CNT-5
      'ID/NUMBER'  DEP-LET-ID-NO
      'Widow/Name' DEP-LETTER-NAME (EM=XXXXXXXXXXXXX)
      'Wd/St'      DEP-LET-WIDOW-STATUS
      'Address-1'  DEP-LET-ADDR1
      'Address-2'  DEP-LET-ADDR2
      'Zip'        DEP-LET-ZIP
      'Childs/Name' DEP-LET-CHILD
      'Childs/Date of/Birth' #DATE-DISP
      'Mbr/Nane'  DEP-LET-MEMB   (EM=XXXXXXXXXX)
*
    IF LET-ADDR3 NE ' '
      WRITE (5) '     Address-3 = ' LET-ADDR3
    END-IF
  END-IF
*
END-SORT
*
END
