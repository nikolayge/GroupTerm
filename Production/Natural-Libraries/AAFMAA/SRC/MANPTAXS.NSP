* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*************************************************************************************
* PROGRAM-ID: MANPTAXS - Tax Summary Report for Annuitants                          *
*             Read Annuitants                                                       *
*             Find 1099 record with specified input year                            *
*             Build Tax summary report                                              *
*************************************************************************************
*                       MODIFICATION LOG                                            *
* USER     DATE          TAG       REASON                                           *
*                                                                                   *
* JT       11/12/2015              Creation                                         *
*                                                                                   *
*************************************************************************************
*
DEFINE DATA
*
LOCAL USING ADDA0001
LOCAL
*
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 TAXABLE (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0)
  2 REDEFINE START-DATE
    3 #YYYY-1 (A4)
    3 #MM-1   (A2)
    3 #DD-1   (A2)
  2 REDEFINE START-DATE
    3 #START-DATE-A (A8)
  2 ANNUITANT-NAME (A60)
*
1 C-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 CONTACT-STATUS
  2 CONTACT-TYPE
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 REDEFINE MIDDLE-NAME
    3 #MIDDLE-1 (A1/25)
  2 LAST-NAME
  2 RANK-ID
  2 DATE-OF-BIRTH
  2 REDEFINE DATE-OF-BIRTH
    3 #DATE-OF-BIRTH-YYYY (A4)
    3 REDEFINE #DATE-OF-BIRTH-YYYY
      4 #DATE-OF-BIRTH-YYYY-N (N4)
    3 #DATE-OF-BIRTH-MM   (A2)
    3 REDEFINE #DATE-OF-BIRTH-MM
      4 #DATE-OF-BIRTH-MM-N (N2)
    3 #DATE-OF-BIRTH-DD   (A2)
    3 REDEFINE #DATE-OF-BIRTH-DD
      4 #DATE-OF-BIRTH-DD-N (N2)
  2 SSN
  2 GENDER-CD
*
1 #SSN-N        (N09)
1 REDEFINE #SSN-N
  2 #SSN-A  (A09)
1 LETTER        (A) DYNAMIC
1 PRIMARY          (A1) CONST <'P'>
1 #ZIP-CODE-DISP   (A10)
1 #ANNU-ISN (P8)
*
1 #EXTRACT-RECORD  (A250)
*
1 #OUTPUT-RECORD-1                          (A500)
*
1 #YEAR-OUT                (A04)
1 #SSN-OUT                 (A09)
1 #TAX-IND-OUT             (A01)
1 #CN-OUT                  (A06)
1 #SETTLEMENT-OPTION-OUT              (A13)
1 #SETTLEMENT-OPTION-DESC-OUT (A20)    
1 #DISPLAY-NAME-OUT        (A) DYNAMIC                                                                    /* PM9
1 #INSURED-OUT             (A25)
1 #NAME-2-OUT              (A) DYNAMIC
1 #GROSS-AMT-OUT           (A15)
1 #TAX-AMT-OUT             (A15)
1 #NET-AMT-OUT             (A15)
1 #BANKRUPTCY-IND-OUT      (A01)
1 #DATE-ENT-OUT            (A10)
1 REDEFINE #DATE-ENT-OUT 
  2 #DATE-ENT-MM   (A2)
  2 #DATE-ENT1     (A1) 
  2 #DATE-ENT-DD   (A2)
  2 #DATE-ENT2     (A1) 
  2 #DATE-ENT-YYYY (A4)
1 #ADDR-1-OUT              (A75)
1 #ADDR-2-OUT              (A75)
1 #ADDR-3-OUT              (A75)
*
1 #GROSS         (N7.2)
1 #GROSS-TAXABLE (N7.2)
1 #GROSS-AMT     (N7.2)
1 #NET-AMT       (A15)
1 #TAX-AMT       (A15)
1 #PAID               (N7.2)
1 #WORK-NET-AMT       (N7.2)
1 #DATE-ENT-DISP      (A10)
1 #WORK-TAX-AMT       (N7.2)
1 #DOING-PRIOR-YEAR   (L)
1 #PRIOR-YEAR         (N4)
1 #DISPLAY-NAME       (A) DYNAMIC
1 #FORMATTED-NAME     (A) DYNAMIC
1 #SPLIT              (A1) INIT <'S'>
*
1 #JCL-YEAR          (N4)
1 #PAGE-HEADER
  2 #YEAR-HD                (A4)  CONST <'YEAR'>
  2 #SSN-HD                 (A6)  CONST <'SSN-HD'>
  2 #TAX-IND-HD             (A7)  CONST <'TAX-IND'>
  2 #CN-HD                  (A2)  CONST <'CN'>
  2 #SETTLEMENT-OPTION-HD   (A13) CONST <'SETTLE-OPTION'>
  2 #SETTLEMENT-OPTION-DESC-HD (A20) CONST <'SETTLE-OPTION-DESC'>
  2 #NAME-HD                (A4)  CONST <'NAME'>
  2 #INSURED-HD             (A7)  CONST <'INSURED'>
  2 #NAME-2-HD              (A6)  CONST <'NAME-2'>
  2 #GROSS-AMT-HD           (A20) CONST <'GROSS-AMT'>
  2 #TAXABLE-AMT-HD         (A11) CONST <'TAXABLE-AMT'>
  2 #NET-CALCED-AMT-HD      (A14) CONST <'NET-CALCED-AMT'>
  2 #BANKRUPT-IND-HD        (A12) CONST <'BANKRUPT-IND'>
  2 #DATE-ENT-HD            (A8)  CONST <'DATE-ENT'>
  2 #ADDR-1-HD              (A6)  CONST <'ADDR-1'>
  2 #ADDR-2-HD              (A6)  CONST <'ADDR-2'>
  2 #ADDR-3-HD              (A6)  CONST <'ADDR-3'>
*
END-DEFINE
* - - - - - - - - -
DEFINE WORK FILE 1 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/ANNU-TAX-SUMMARY.TXT' type 'ASCII-COMPRESSED'
DEFINE WORK FILE 3 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/ANNU-TAX-YEAR.TXT' type 'ASCII-COMPRESSED'
*
COMPRESS FULL
  #YEAR-HD #SSN-HD 
  #TAX-IND-HD #CN-HD #SETTLEMENT-OPTION-HD #SETTLEMENT-OPTION-DESC-HD
  #NAME-HD #INSURED-HD #NAME-2-HD #GROSS-AMT-HD #TAXABLE-AMT-HD
  #NET-CALCED-AMT-HD #BANKRUPT-IND-HD #DATE-ENT-HD
  #ADDR-1-HD #ADDR-2-HD #ADDR-3-HD
  INTO #OUTPUT-RECORD-1
  WITH DELIMITER H'09'
WRITE WORK 1 #OUTPUT-RECORD-1
* READ WORK 3 ONCE RECORD #JCL-YEAR
MOVE 2015 TO #JCL-YEAR
*
RD1. READ ANNUITANTS BY CONTACT-ID
*
RESET 
    #YEAR-OUT #SSN-OUT 
    #TAX-IND-OUT #CN-OUT     
    #SETTLEMENT-OPTION-OUT   #SETTLEMENT-OPTION-DESC-OUT
    #DISPLAY-NAME-OUT #INSURED-OUT #NAME-2-OUT #GROSS-AMT-OUT #TAX-AMT-OUT
    #NET-AMT-OUT #BANKRUPTCY-IND-OUT #DATE-ENT-OUT
    #ADDR-1-OUT #ADDR-2-OUT #ADDR-3-OUT
*
  IF ANNUITANTS.REFERENCE-CN > 0
      FIND C-V WITH C-V.CONTACT-ID = ANNUITANTS.CONTACT-ID
          IF NO RECORDS FOUND
             ESCAPE BOTTOM
          END-NOREC
*
  MOVE '-' TO #DATE-ENT1 #DATE-ENT2
  MOVE #JCL-YEAR                     TO #YEAR-OUT
  MOVE RIGHT C-V.SSN TO #SSN-A
  MOVE EDITED #SSN-N (EM=999999999)  TO #SSN-OUT
  MOVE C-V.CONTACT-TYPE              TO #TAX-IND-OUT
  MOVE ANNUITANTS.REFERENCE-CN       TO #CN-OUT
  MOVE ANNUITANTS.SETTLEMENT-OPTION  TO #SETTLEMENT-OPTION-OUT
  MOVE ANNUITANTS.#MM-1              TO #DATE-ENT-MM
  MOVE ANNUITANTS.#DD-1              TO #DATE-ENT-DD
  MOVE ANNUITANTS.#YYYY-1            TO #DATE-ENT-YYYY
*
   DECIDE ON FIRST VALUE OF ANNUITANTS.SETTLEMENT-OPTION 
     VALUE 'AL'
       MOVE 'ANNUITY Life'   TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'A1'
       MOVE 'ANNUITY Life/1' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'A2'
       MOVE 'ANNUITY Life/2' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'IO'
       MOVE 'Interest Only' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'L1'
       MOVE 'LA Certain 10 years' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'L2'
       MOVE 'LA Certain 15 years' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'L3'
       MOVE 'LA Certain 20 years' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'LA'
       MOVE 'Life Annuity' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'LT'
       MOVE 'Long Term Care' TO #SETTLEMENT-OPTION-DESC-OUT
     VALUE 'LS'
       MOVE 'Lump Sum' TO #SETTLEMENT-OPTION-DESC-OUT
     NONE
       IGNORE
   END-DECIDE    
*
 RESET #PDA-ADDRESS
  #ADD-CONTACT-ID := C-V.CONTACT-ID 
  #ADD-TYPE       := PRIMARY
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  #ADDR-1-OUT := #ADD-LINE-1
  #ADDR-2-OUT := #ADD-LINE-2
  #ADDR-3-OUT := #ADD-LINE-3
*
  #DISPLAY-NAME-OUT   := FUNCTION-CONTACT-NAME-3 (< C-V.CONTACT-ID >)  
*
  RESET #PAID #GROSS #GROSS-TAXABLE
  #ANNU-ISN := *ISN(0153)
  CALLNAT 'MANNGROS'
    #ANNU-ISN
    #PAID
    #GROSS
    #GROSS-TAXABLE
*
  MOVE EDITED #GROSS (EM=-ZZZZZZ9.99) TO #GROSS-AMT-OUT
  IF ANNUITANTS.TAXABLE > 0
     #WORK-NET-AMT := #GROSS-TAXABLE - ANNUITANTS.TAXABLE
     MOVE EDITED #WORK-NET-AMT (EM=-ZZZZZZ9.99) TO #NET-AMT-OUT
  ELSE
     MOVE EDITED #GROSS (EM=-ZZZZZZ9.99) TO #NET-AMT-OUT
  END-IF
  MOVE EDITED ANNUITANTS.TAXABLE        (EM=-ZZZZZZ9.99) TO #TAX-AMT
  MOVE LEFT #TAX-AMT   TO #TAX-AMT-OUT
*
  COMPRESS FULL
    #YEAR-OUT #SSN-OUT 
    #TAX-IND-OUT #CN-OUT     
    #SETTLEMENT-OPTION-OUT   #SETTLEMENT-OPTION-DESC-OUT
    #DISPLAY-NAME-OUT #INSURED-OUT #NAME-2-OUT #GROSS-AMT-OUT #TAX-AMT-OUT
    #NET-AMT-OUT #BANKRUPTCY-IND-OUT #DATE-ENT-OUT
    #ADDR-1-OUT #ADDR-2-OUT #ADDR-3-OUT
    INTO #OUTPUT-RECORD-1
    WITH DELIMITER H'09'
  WRITE WORK 1 #OUTPUT-RECORD-1
*
  END-FIND
END-IF 
END-READ     /* RD1.
* *
ON ERROR
  WRITE ANNUITANTS.REFERENCE-CN
    ANNUITANTS.SETTLEMENT-OPTION
    *ERROR-LINE
    C-V.#DATE-OF-BIRTH-YYYY
END-ERROR
*
END
