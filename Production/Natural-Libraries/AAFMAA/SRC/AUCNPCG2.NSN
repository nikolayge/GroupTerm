* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*******************************************************************
* AUCNPCG2 - Payer Role
* Called from AUCSRV / AUCNPCU1 / AUCNPCG1 - 'UpdatePolicyRoles' function
* Move Payment information from Insured record to Payer record
* by user request when initially Payer Role is set up and Payer
* does not have required payment information
*******************************************************************
*        MODIFICATION LOG
*******************************************************************
* USER   DATE      TAG  REASON
* YAK    06192017       Initial Creation EB&P III
* YAK    09072017  YK1  Correct Field assignments for saving Credit Card
*******************************************************************
*
DEFINE DATA
PARAMETER
1 #PolicyNumber (A16)
1 #UpdateUser (A) DYNAMIC
1 #InsuredContactID  (N8)
1 #PayerContactID  (N8)
1 #PayerCN  (N6)
1 #ErrorCode (A) DYNAMIC
*
LOCAL USING OBJACC01
LOCAL USING OBJABA01
LOCAL USING GPRATR06
LOCAL USING ERROR_LN
LOCAL
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 PAD-CODE
  2 POLICY-ID (9)
  2 MODE (9)
*
1 #I                (I2)
1 #DATN             (N8)
1 #TIMN             (N7)
1 #TRAN-CN          (N6)
1 #TRAN-CONTACT-ID  (N8)
1 FUNCTION_CODE     (A) DYNAMIC
1 CC_TR_FUNCTION    (A) DYNAMIC INIT <'CreditCardInfo'>
1 EZ_TR_FUNCTION    (A) DYNAMIC INIT <'EZPayInfo'>
1 #UPDATE-ID-NUMBER (A) DYNAMIC CONST <'UPDATE-ID-NUMBER'>
1 #MOVE-EZPAY-ACCOUNT (A) DYNAMIC CONST <'MOVE-EZPAY-ACCOUNT'>
*
1 #UPDATE-CLERK    (A3)
1 #UPDATE-USER     (A) DYNAMIC
*
1 #InsuredCN       (N6)
1 #PAY-MODE        (A1)
1 #LOAN-MODE       (A1/2)
* Payment Mode
1 #EZ-PAY          (A1) CONST <'E'>
1 #CC-MODE         (A1/4) CONST <'0','1','4','2'>
1 #CC-LOAN-MODE     (A1/2) CONST <'8','C'>
1 #EZPAY-LOAN-MODE  (A1/3) CONST <'3','4','E'>
*
END-DEFINE
*
#DATN := *DATN
#TIMN := *TIMN          /* YK1
#UPDATE-CLERK := FUNCTION-WEB-USER(<#UpdateUser>)(1)
#UPDATE-USER  := FUNCTION-WEB-USER(<#UpdateUser>)(2)
*
* Get Policy Payment Type
*
FIND(1) ST-V WITH MEMBER-CONTACT-ID = #InsuredContactID
  #InsuredCN := ST-V.ID-NUMBER
  EXAMINE FULL ST-V.POLICY-ID(*) FOR FULL #PolicyNumber INDEX #I
END-FIND
#PAY-MODE := F-POLICY-MODE(<#InsuredCN,#PolicyNumber>)
  F-LOAN-PAY-MODE(<#InsuredCN,#PolicyNumber,#LOAN-MODE(*)>)
*
DECIDE FOR EVERY CONDITION
  WHEN #PAY-MODE = #CC-MODE(*) OR #LOAN-MODE(1) = #CC-LOAN-MODE(*) OR #LOAN-MODE(2) = #CC-LOAN-MODE(*)
    PERFORM MOVE-CC-ACCOUNT
  WHEN #PAY-MODE = #EZ-PAY OR #LOAN-MODE(1) = #EZPAY-LOAN-MODE(*) OR #LOAN-MODE(2) = #EZPAY-LOAN-MODE(*)
    PERFORM MOVE-EZPAY-ACCOUNT
  WHEN NONE
    ESCAPE ROUTINE
END-DECIDE
*
IF #RESULT-CODE <> ' '
  #ErrorCode := #RESULT-CODE
END-IF
*
*********************************
DEFINE SUBROUTINE MOVE-CC-ACCOUNT
*********************************
*
PARM-CC-DATA.#PARM-ID-NUMBER  := #InsuredCN
PARM-CC-DATA.#ACTION          := #UPDATE-ID-NUMBER
PARM-CC-INFO.ID-NUMBER        := #PayerCN
PARM-CC-INFO.LAST-USER-UPDATE := #UPDATE-USER                     /* YK1
PARM-CC-INFO.LAST-DATE-UPDATE := #DATN
PARM-CC-INFO.LAST-TIME-UPDATE := #TIMN
CALLNAT 'OBJNCC01' PARM-CC-DATA PARM-CC-INFO
IF #RESULT-CODE = ' '
  PERFORM POPULATE-CC-BEFORE-AFTER-IMAGE
END-IF
*
END-SUBROUTINE
*
************************************
DEFINE SUBROUTINE MOVE-EZPAY-ACCOUNT
************************************
*
PARM-BA-DATA.#PARM-ID-NUMBER := #InsuredCN
PARM-BA-DATA.#NUMBER-OF-RECORDS := #I
PARM-BA-DATA.#ACTION         := #MOVE-EZPAY-ACCOUNT
PARM-BA-INFO.ID-NUMBER       := #PayerCN
PARM-BA-INFO.LAST-UPDATE-USER := #UPDATE-USER                     /* YK1
PARM-BA-INFO.LAST-UPDATE-DATE := #DATN
PARM-BA-INFO.LAST-UPDATE-TIME := #TIMN
CALLNAT 'OBJNBA01' PARM-BA-DATA PARM-BA-INFO
IF #RESULT-CODE = ' '
  PERFORM POPULATE-EZ-BEFORE-AFTER-IMAGE
END-IF
END-SUBROUTINE
*
************************************************
DEFINE SUBROUTINE POPULATE-CC-BEFORE-AFTER-IMAGE
************************************************
* Save transaction for Adding CC to Payer account
RESET BEFORE_IMAGE
AFTER_IMAGE.CC-NUMBER               := PARM-CC-INFO.CC-NUMBER
AFTER_IMAGE.CC-GUID                 := PARM-CC-INFO.CC-GUID
COMPRESS PARM-CC-INFO.CC-EXPIRATION-MONTH PARM-CC-INFO.CC-EXPIRATION-YEAR
  INTO AFTER_IMAGE.CC-EXPIRATION-DATE
AFTER_IMAGE.CCHOLDER-NAME           := PARM-CC-INFO.CCHOLDER-NAME
AFTER_IMAGE.CCHOLDER-ADDRESS-1      := PARM-CC-INFO.CCHOLDER-STREET-ADDR
AFTER_IMAGE.CCHOLDER-ADDRESS-2      := PARM-CC-INFO.CCHOLDER-ADDL-ADDR
IF CC-ADDR-FORMAT-CODE = '1'
  AFTER_IMAGE.CCHOLDER-ADDRESS-3    := PARM-CC-INFO.CCHOLDER-ZIP-CODE
ELSE
  COMPRESS PARM-CC-INFO.CCHOLDER-POSTAL-CODE PARM-CC-INFO.CCHOLDER-COUNTRY
    INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
END-IF
COMPRESS PARM-CC-INFO.CCHOLDER-CITY PARM-CC-INFO.CCHOLDER-STATE-CD AFTER_IMAGE.CCHOLDER-ADDRESS-3
  INTO AFTER_IMAGE.CCHOLDER-ADDRESS-3
AFTER_IMAGE.CC-ADDR-VALIDATION-CODE := PARM-CC-INFO.CC-ADDR-VALIDATION-CODE
AFTER_IMAGE.CC-DEFAULT-IND := PARM-CC-INFO.CC-DEFAULT-IND
IF AFTER_IMAGE.CC-DEFAULT-IND = ' '
  AFTER_IMAGE.CC-DEFAULT-IND := 'N'
END-IF
*
FUNCTION_CODE := CC_TR_FUNCTION
#TRAN-CN := #PayerCN
#TRAN-CONTACT-ID := #PayerContactID
PERFORM WRITE-TRANSACTION-LOG
* Save transaction for Removing CC from Insured account
#TRAN-CN := #InsuredCN
#TRAN-CONTACT-ID := #InsuredContactID
MOVE BY NAME AFTER_IMAGE TO BEFORE_IMAGE
BEFORE_IMAGE.DATE-LAST-UPDATE := PARM-CC-INFO.LAST-DATE-UPDATE
RESET AFTER_IMAGE
PERFORM WRITE-TRANSACTION-LOG
*
END-SUBROUTINE
*
************************************************
DEFINE SUBROUTINE POPULATE-EZ-BEFORE-AFTER-IMAGE
************************************************
* Save transaction for Adding EZ to Payer account
RESET BEFORE_IMAGE
AFTER_IMAGE.ROUTING-NUMBER  := PARM-BA-INFO.ROUTING-NUMBER
AFTER_IMAGE.ACCOUNT-NUMBER  := PARM-BA-INFO.ACCOUNT-NUMBER
AFTER_IMAGE.ACCOUNT-TYPE    := PARM-BA-INFO.ACCOUNT-TYPE
*
FUNCTION_CODE := EZ_TR_FUNCTION
#TRAN-CN := #PayerCN
#TRAN-CONTACT-ID := #PayerContactID
PERFORM WRITE-TRANSACTION-LOG
* Save transaction for Removing EZ from Insured account
* #TRAN-CN := #InsuredCN
* #TRAN-CONTACT-ID := #InsuredContactID
* MOVE BY NAME AFTER_IMAGE TO BEFORE_IMAGE
* BEFORE_IMAGE.DATE-LAST-UPDATE := PARM-CC-INFO.LAST-DATE-UPDATE
* RESET AFTER_IMAGE
* PERFORM WRITE-TRANSACTION-LOG
*
END-SUBROUTINE
*
***************************************
DEFINE SUBROUTINE WRITE-TRANSACTION-LOG
***************************************
*
TR_ID_NUMBER  := #TRAN-CN
TR_NAME       := FUNCTION-CONTACT-NAME(<#TRAN-CONTACT-ID>)
TR_CLERK      := #UPDATE-CLERK
CALLNAT 'GPRNTR06' FUNCTION_CODE TRAN_LOG_HEADER BEFORE_IMAGE AFTER_IMAGE
END-SUBROUTINE
*
END
