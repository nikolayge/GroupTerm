* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRNE008
* DESCRIPTION: Assemble Rank Information
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  11-15-2010  RE1  Creation
* NGG  01-28-2011  NG1  No Military Branch for civilians
* RSE  04/07/2011  RE2  Use function to check authorized users
* NGG  06/20/2011  NG2  Organization Contact
* RSE  06/27/2011  RE3  Add Prefix
* RSE  07/27/2011  RE4  Don't display military status for non-member
**************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 CONTACT-TYPE          /* NG2
  2 ID-NUMBER
  2 RANK-ID
  2 RANK-PROTECT
  2 MILITARY-STATUS
  2 PREFIX-TITLE          /* RE3
*
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 MILITARY-ID
  2 MILITARY-BRANCH
  2 RANK-ABBR
  2 PAY-GRADE
  2 RANK-DESCRIPTION
*
1 STATUS-V VIEW OF A-STATUS
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
  2 RANK
*
1 #PROTECT               (A7)
1 #MILITARY-BRANCH       (A50)
1 #CODE-NAME             (A) DYNAMIC
1 #CODE-PARM             (A10)
*
END-DEFINE
*
RESET #TEXT (*)
*
GET CONTACT-V #ISN
*
IF CONTACT-V.CONTACT-TYPE = 'O'                                   /* NG2
  #TEXT  (1) := FUNCTION-POC (< CONTACT-V.CONTACT-ID >)
  ESCAPE ROUTINE
END-IF
*
IF CONTACT-V.RANK-ID > 0
  FIND RANK-V WITH RANK-ID = CONTACT-V.RANK-ID
    IF NO RECORD FOUND
      ESCAPE ROUTINE
    END-NOREC
  END-FIND
END-IF
*
FIND STATUS-V WITH ID-NUMBER = CONTACT-V.ID-NUMBER
END-FIND
*
IF RANK-PROTECT = 'Y'
  MOVE 'PROTECT' TO #PROTECT
END-IF
*
IF CONTACT-V.RANK-ID > 0
  IF #PROTECT NE ' '
    COMPRESS MILITARY-BRANCH  PAY-GRADE RANK-ABBR RANK-DESCRIPTION '(' INTO #TEXT (1)
    COMPRESS #TEXT (1) 'PROTECT)' INTO #TEXT (1) LEAVING NO
  ELSE
    COMPRESS MILITARY-BRANCH PAY-GRADE RANK-ABBR RANK-DESCRIPTION INTO #TEXT (1)
  END-IF
ELSE
  READ RANK-V WHERE MILITARY-ID = MILITARY-SERVICE
    ESCAPE BOTTOM
  END-READ
  IF MILITARY-ID NE MILITARY-SERVICE
    RESET MILITARY-BRANCH                          /* NG1
  END-IF
  IF #PROTECT NE ' '
    COMPRESS MILITARY-BRANCH '*** MISSING RANK *** (' INTO #TEXT (1)
    COMPRESS #TEXT (1) 'PROTECT)' INTO #TEXT (1) LEAVING NO
  ELSE
    COMPRESS MILITARY-BRANCH '*** MISSING RANK ***' INTO #TEXT (1)
  END-IF
END-IF
*
COMPRESS #TEXT (1) 'PREFIX:' PREFIX-TITLE INTO #TEXT (1)      /* RE3
*
IF ID-NUMBER > 0    /* RE4
  #CODE-NAME   := 'RANK-STATUS'
  #CODE-PARM   := CONTACT-V.MILITARY-STATUS
  #TEXT (2) := FUNCTION-RANK-CODES(<#CODE-NAME,#CODE-PARM>)
END-IF
*
IF CONTACT-V.RANK-ID > 0 AND
    FUNCTION-AUTHORIZED(<*INIT-USER>)                  /* RE2
*    *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA'
  COMPRESS #TEXT  (1) '{'  INTO #TEXT  (1)
  COMPRESS #TEXT  (1) RANK-ID INTO #TEXT  (1) LEAVING NO
  COMPRESS #TEXT  (1) STATUS-V.RANK INTO #TEXT  (1)
  COMPRESS #TEXT  (1)  '}' INTO #TEXT  (1) LEAVING NO
END-IF

END
