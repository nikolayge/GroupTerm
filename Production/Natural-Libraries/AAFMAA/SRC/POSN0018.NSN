* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSN0018.
/**
/** :author nguentchev
/*
/* Get ownership transfer date and last update user from BD BENE
/*
DEFINE DATA
PARAMETER
1 #POLICY-NUMBER         (A16)
1 #CN                    (N6)
1 #OwnershipTransferDate (N8)
1 #OwnershipTransferUser (A8)
LOCAL
1 NT-V VIEW OF A-BENE-FINAL
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 NOTE-DATE  (N8/60) /* P/A = PRIN/ALT DESIG
  2 REDEFINE NOTE-DATE /* L = linked pols
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7) /* (D, L)
      4 BENE-SPLIT-IND    (N1/7) /* (D, L)
      4 BENE-LST-UPD      (N8)   /* (D)
      4 BENE-STRT-PEND    (N8)   /* (D)
      4 BENE-FOL-UP-IND   (N1)   /* (D)
      4 BENE-OWN-TRNSF-DT (N8)   /* (D)
      4 BENE-SUC-OWN      (N1)   /* (d)
      4 BENE-NEW-LAYOUT   (N1)   /* (D)/* was -setl-opt
      4 BENE-COM-DIS      (N2)   /* (D)/* Common disaster not used
      4 BENE-PER-STR      (N1)   /* (D)/* Per Stirpes not user
      4 BENE-FIL1         (A1)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)  /* (D)
      4 BENE-RESTRICT    (A1)  /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1) /* (D, P/A)
        5 BENE-SHARE-TYP   (A1) /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 LST-BD-FINALIZATION-USER (A3)
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9)
  2 SPLIT-IND     (9)
*
1 #WORK-DATE  (A6)
1 REDEFINE #WORK-DATE
  2 #WORK-YY   (A2)
  2 REDEFINE #WORK-YY
    3 #WORK-YY-N(N2)
  2 #WORK-REST (A4)
1 #WORK-8        (A8) 1 REDEFINE #WORK-8
  2 #WORK-8-N    (N8)
1 #I1   (I2)
1 #I3   (I2)
1 J     (I2)
END-DEFINE
*
RESET #OwnershipTransferDate
  #OwnershipTransferUser
FIND NT-V WITH ID-NUMBER = #CN
END-FIND
FIND POLICY WITH ID-NUMBER = NT-V.ID-NUMBER
END-FIND
FOR #I1 1 TO 6
  IF NT-V.BENE-ISS-DT (#I1, 1) EQ 0
    ESCAPE TOP
  END-IF
/*         Collect policy pool for current beneficiary CN
  FOR #I3 1 TO 7  /* Same Designation
    IF NT-V.BENE-ISS-DT (#I1,#I3) = 0
      ESCAPE TOP
    END-IF
    MOVE EDITED NT-V.BENE-ISS-DT (#I1,#I3) (EM=999999) TO #WORK-DATE
    IF #WORK-YY-N LT 16
      #WORK-8 := '20'
    ELSE
      #WORK-8 := '19'
    END-IF
    COMPRESS #WORK-8 #WORK-YY #WORK-REST INTO #WORK-8 LEAVING NO
    FOR J 1 TO POLICY.C*ACTIVE-SEGMENTS
      IF POLICY.ISSUE-DATE (J) = #WORK-8-N
          AND POLICY.SPLIT-IND (J) = NT-V.BENE-SPLIT-IND (#I1,#I3)
        IF #POLICY-NUMBER = POLICY.POLICY-ID (J)
          #OwnershipTransferDate := BENE-OWN-TRNSF-DT (#I1)
          #OwnershipTransferUser := LST-BD-FINALIZATION-USER
          ESCAPE ROUTINE
        END-IF
      END-IF
    END-FOR
  END-FOR
END-FOR
END
