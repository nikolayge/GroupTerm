* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram OBJNCCN0.
/**
/** :author nguentchev
/*
/* returns phone number according to the request type
/*
DEFINE DATA
PARAMETER
1 #CONTACT-ID    (N8)
1 #PHONE-TYPE    (A1)
1 #PHONE         (N20)
1 #INTL          (A1)
LOCAL
1 PHONE VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3   (A3)
    3 DIAL-NUMBER-4   (A4)
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
  2 INVALID-IND (A1)
1 #PN    (A) DYNAMIC
1 L      (I4)
END-DEFINE
*
RESET #PHONE #INTL
IF #CONTACT-ID <= 0
  ESCAPE ROUTINE
END-IF
#PN := '0'
FIND PHONE WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  IF PHONE-TYPE-CODE = #PHONE-TYPE
    IF TYPE = 'I'
      COMPRESS INTL-ACCESS-CODE INTL-DIAL-NUMBER INTO #PN LEAVING NO
      #INTL := 'Y'
    ELSE
      COMPRESS AREA-CODE DIAL-NUMBER INTO #PN LEAVING NO
    END-IF
    ESCAPE BOTTOM
  END-IF
END-FIND
*
L := *LENGTH ( #PN )
IF L <= 20
  #PHONE := VAL ( #PN )
END-IF
END
