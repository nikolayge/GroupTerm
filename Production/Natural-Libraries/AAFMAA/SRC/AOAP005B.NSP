* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP005B - PRT ben LETTERS FROM "MR" REQUEST FILE - 1st
*                      - ben-sho & ben-ind 1/2/3/4
* bz  06/09/2013  Rebranding (Search for AAFMAA)
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL
*
1 #SPS-GEN-LINE-1   (A30)
1 #SPS-GEN-LINE-2   (A15)
1 #FORMAT-CN        (A8)
1 #SCR-ZIP-BAR-CODE (A14)
*
1 #PARM-INPUT    (A36)
1 #PARM-CHOICE   (A12)
1 #PARM-CLERK    (A3)
1 #SCR-TBL    (A78/25)
1 REDEFINE #SCR-TBL
 2 #SCR-TBL-OCC-1 (25)
  3 #FST-4        (A4)
  3 #SCR-TBL-LST  (A74)
*
1 #MR-MSG-NOTE-NUM (P8)
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT (11:14)
*
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND       (N1)
   3 MSG-IND       (N1)
   3 RET-IND       (N1)
   3 FIN-IND       (N1)
   3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND       (A1)
   3 MAL-IND       (A1)
   3 BEN-SHO-IND   (A1)
   3 ACC-IND       (A1)
   3 CAP-IND       (A1)
   3 APP-IND       (A1)
   3 PIL-IND       (A1)
   3 CO-IND        (A1)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
     4 REQUEST-MM-N (N2)
    3 REQUEST-DD   (A2)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
  2 MESSAGE        (A60/20)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 REDEFINE #MM
     4 #MM-N  (N2)
    3 #DD     (A2)
*
1 #COUNTERS
  2 #BEN-RQST-CNT   (P5)
*
1 #FST-BEN  (L) INIT <TRUE>
1 #I1               (I2)
1 #I2               (I2)
1 #SC-X             (I2)
1 #ISN              (P8)
*
*
END-DEFINE
*
* * * * * * * * *
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
                  5X 'BEN LETTER DETAIL'
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
INPUT #PARM-INPUT                    /* TYPE OF MAIL-OUTS
MOVE #PARM-INPUT TO #PARM-CHOICE
*
IF #PARM-CHOICE = 'RQST'
  INPUT #PARM-CLERK                    /* CLERK
END-IF
*
INPUT #PARM-INPUT /* MEMBER DATE RANGE
INPUT #PARM-INPUT /* MEMBER AGE 62 DATE RANGE
INPUT #PARM-INPUT /* WIDOW DATE RANGE
*
IF #PARM-CHOICE = 'MBR-RQST-WID' OR = 'MBR-RQST' OR = 'RQST'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
* BEN LETTER REQUEST PROCESSING
* * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
* IF (#PARM-CLERK = '***') AND (REQUEST-DATE NE 9999) /* BYPASS IF
  IF (#PARM-CLERK = '***') AND (REQUEST-MM-N LT 50) /* BYPASS IF
    ESCAPE TOP                              /* entered after job started
  END-IF
*
  IF   ((FIR-IND NE 0) OR (FIN-IND NE 0))
   AND (BEN-SHO-IND NE ' ')
    RESET BEN-SHO-IND
  END-IF
*
  IF  (XX-R01.BEN-IND NE ' ')
   OR (XX-R01.BEN-SHO-IND NE ' ')
    ADD +1 TO #BEN-RQST-CNT
  ELSE
    ESCAPE TOP
  END-IF
*
  END TRANSACTION
END-ALL  /* (0121)
*
SORT BY ID-NUMBER
  USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE RQST-TABLE
                 ASSUM-RET-DATE-2 ASSUM-RET-DATE-3 MESSAGE (*)
*
  RESET *ISN #ISN
  FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
        IF NO RECORDS FOUND
          WRITE (2) 'MBR NOT FOUND' ST-V.ID-NUMBER
          ESCAPE BOTTOM
        END-NOREC
*
        MOVE *ISN TO #ISN
      END-FIND
    END-NOREC
*
    IF #ISN = 0
      MOVE *ISN TO #ISN
    END-IF
  END-FIND
*
  IF #ISN = 0
    ESCAPE TOP
  END-IF
*
  PERFORM PROCESS-ADDR
*
  MOVE XX-R01.ID-NUMBER TO #CGA-ID
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
  MOVE #ISN TO #CGA-ISN
*
  IF XX-R01.BEN-SHO-IND = '1'
    RESET #SCR-TBL (*)
    COMPRESS '  Attached is a copy of your current beneficiary'
             'designation.'                INTO #SCR-TBL-LST (1)
    IF #ZIP-CODE = 0
      NEWPAGE (6)
      WRITE (6) NOTITLE USING FORM 'AOAF005D' /* blank addr page
    ELSE
      NEWPAGE (4)
      WRITE (4) NOTITLE USING FORM 'AOAF005D' /* blank addr page
    END-IF
    MOVE 'BND' TO #CGA-SCR-SUFFIX
*
    FETCH RETURN 'E4700PBD'
  END-IF
*
  IF XX-R01.BEN-IND = '1' OR = '2' OR = '3' OR = '4'
*   IF #FST-BEN
*     WRITE (5) 'To skip a page'
*     RESET #FST-BEN
*   END-IF
*
    IF XX-R01.BEN-IND = '1'
      MOVE 'BNF' TO #CGA-SCR-SUFFIX
      RESET #SCR-TBL (*)
      COMPRESS '  A change of beneficiary form is enclosed which has'
               'been prepared as'            INTO #SCR-TBL-LST (1)
      COMPRESS 'requested and in accordance with our customary'
               'practices. The form should'  INTO #SCR-TBL-LST (2)
      COMPRESS 'be completed where indicated and returned to this'
               'office for processing.'      INTO #SCR-TBL-LST (3)
      COMPRESS 'Certificates of Membership and Insurance are not'
               'required for endorse-'       INTO #SCR-TBL-LST (4)
      COMPRESS 'ment. Duplicate(s) of new designation will be provided'
               'to be attached to'           INTO #SCR-TBL-LST (5)
      MOVE     'the Certificate(s) for a permanent record.'
                                               TO #SCR-TBL-LST (6)
      MOVE 6 TO #SC-X
    END-IF
*
    IF XX-R01.BEN-IND = '2'
      MOVE 'BNB' TO #CGA-SCR-SUFFIX
      RESET #SCR-TBL (*)
      COMPRESS '  As requested, a change of beneficiary form is'
               'enclosed. This form'      INTO #SCR-TBL-LST (1)
      COMPRESS 'is used when designating a named beneficiary(s)'
               'or your estate. Should'      INTO #SCR-TBL-LST (2)
      COMPRESS 'you need a more complex designation (i.e. Trust,'
               'Per Stirpes, Settlement'     INTO #SCR-TBL-LST (3)
      COMPRESS 'Options, etc.), please feel free to contact me and'
               'I will be glad to'           INTO #SCR-TBL-LST (4)
      MOVE     'prepare a special form tailored to meet your desires.'
                                               TO #SCR-TBL-LST (5)
      COMPRESS '  If you are unable to use our toll-free number, the'
               'enclosed form is a'        INTO #SCR-TBL-LST (7)
      COMPRESS 'worksheet that will provisionally be accepted pending'
               'the return of the'          INTO #SCR-TBL-LST (8)
      MOVE     'revised designation.' TO #SCR-TBL-LST (9)
      MOVE 9 TO #SC-X
    END-IF
*
    IF XX-R01.BEN-IND = '3'
      MOVE 'BNA' TO #CGA-SCR-SUFFIX
      RESET #SCR-TBL (*)
      COMPRESS '  A review of your file indicates that the beneficiary'
               'designation on'              INTO #SCR-TBL-LST (1)
      COMPRESS 'your recent application for additional coverage is not'
               'consistent with'             INTO #SCR-TBL-LST (2)
      COMPRESS 'your other certificate(s). If you desire a change in'
               'your designation'            INTO #SCR-TBL-LST (3)
      COMPRESS 'please complete the form enclosed and return it to this'
               'office. Other-'              INTO #SCR-TBL-LST (4)
      COMPRESS 'wise, the present designation(s) will stand until'
               'changed. Please note,'       INTO #SCR-TBL-LST (5)
      COMPRESS 'however, that the form is only for use when designating'
               'a named bene-'     INTO #SCR-TBL-LST (6)
      MOVE     'ficiary(ies) or your Estate.' TO #SCR-TBL-LST (7)
      MOVE 7 TO #SC-X
    END-IF
*
    IF XX-R01.BEN-IND = '4'
      MOVE 'BNC' TO #CGA-SCR-SUFFIX
      RESET #SCR-TBL (*)
      COMPRESS '  We are currently reviewing beneficiary designation(s)'
               'of our members.'             INTO #SCR-TBL-LST (1)
      COMPRESS 'Enclosed is a copy of your most recent designation(s)'
               'and a form to'               INTO #SCR-TBL-LST (2)
      COMPRESS 'change the designation(s) if you desire.'
                                             INTO #SCR-TBL-LST (3)
      MOVE 3 TO #SC-X
    END-IF
*
    IF XX-R01.BEN-IND = '3' OR = '4'
      ADD 2 TO #SC-X
      COMPRESS 'If you need a more complex designation, (i.e.'
               'trusts, trusts for minors,' INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS 'per stirpes, etc.) please call me on our toll-free'
               'number'                   INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS '(1-800-336-4538) with the information and I will be'
               'happy to prepare a'       INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      MOVE     'form for your signature.'   TO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS '  The AAFMAA can'
               'make an immediate'         INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS 'payment of benefits to a person of legal age. While'
               'it is entirely'            INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS 'proper to designate your estate or minor children,'
               'payment would'             INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS 'necessarily be delayed until a legal guardian could'
               'be appointed to'           INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      COMPRESS 'receive it. If payment is to be made to an Estate,'
               'we must await issuance'    INTO #SCR-TBL-LST (#SC-X)
      ADD 1 TO #SC-X
      MOVE     'of Letters Testamentary/Administration by the Court.'
                                             TO #SCR-TBL-LST (#SC-X)
    END-IF
*
    ADD 1 TO #SC-X
    COMPRESS '  If you have any questions, please feel free to call'
             'Lin Blakley on the'       INTO #SCR-TBL-LST (#SC-X)
*
    ADD 1 TO #SC-X
    MOVE     'toll-free number below.' TO #SCR-TBL-LST (#SC-X)
*
    ADD 1 TO #SC-X
*
    FOR #I1 11 TO 14   /* PM - 05/2000 free note recs 600000-700000
      IF MESSAGE (#I1) NE ' '
        ADD 1 TO #SC-X
        MOVE MESSAGE (#I1) TO #SCR-TBL-LST (#SC-X)
      END-IF
    END-FOR
*
*   COMPUTE #MR-MSG-NOTE-NUM = XX-R01.ID-NUMBER + 600000
*   FIND (1) NT-V WITH ID-NUMBER = #MR-MSG-NOTE-NUM
*     FOR #I1 11 TO 14
*       IF NT-V.NOTE-TEXT (#I1) NE ' '
*         ADD 1 TO #SC-X
*         MOVE NT-V.NOTE-TEXT (#I1) TO #SCR-TBL-LST (#SC-X)
*       END-IF
*     END-FOR
*   END-FIND
*
    IF #ZIP-CODE = 0
      NEWPAGE (6)
      WRITE (6) NOTITLE USING FORM 'AOAF005D'
    ELSE
      NEWPAGE (4)
      WRITE (4) NOTITLE USING FORM 'AOAF005D'
    END-IF
*
    FETCH RETURN 'E4700PBD'
  END-IF
END-SORT  /* (0142)
*
WRITE (2) / 12T 'TOTAL BEN LTR REQUESTS' #BEN-RQST-CNT
*
IF #BEN-RQST-CNT NE 0
  EJECT (8)
  WRITE (8) / 12T 'TOTAL BEN LTR REQUESTS' #BEN-RQST-CNT
END-IF
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-ADDR
* * * * * * * * * * * * * * * *
*
MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
COMPRESS 'CN' #ID-NUM INTO #FORMAT-CN LEAVING NO
MOVE #EDS-ZIP-BAR-CODE TO #SCR-ZIP-BAR-CODE
RESET #SPS-GEN-LINE-1 #SPS-GEN-LINE-2
IF #SPS-GEN-ID NE 0
  COMPRESS 'For' #SPS-GEN-NAME INTO #SPS-GEN-LINE-1
  COMPRESS 'With CN' #SPS-GEN-ID INTO #SPS-GEN-LINE-2
END-IF
*
END-SUBROUTINE
*
END
