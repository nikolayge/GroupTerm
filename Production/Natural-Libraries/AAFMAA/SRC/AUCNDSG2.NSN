* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram AUCNDSG2.
/**
/** :author nguentchev
/* TODO Enter your code here
DEFINE DATA
PARAMETER
1 #CN            (N6)
1 #POLICY-NUMBER (A16)
1 PrimaryBD      (A) DYNAMIC
1 ContingentBD   (A) DYNAMIC
1 #MSG           (A) DYNAMIC
LOCAL
1 NT-V
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 NOTE-DATE  (N8/60) /* P/A = PRIN/ALT DESIG
  2 REDEFINE NOTE-DATE /* L = linked pols
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7) /* (D, L)
      4 BENE-SPLIT-IND    (N1/7) /* (D, L)
      4 BENE-LST-UPD      (N8)   /* (D)
      4 BENE-STRT-PEND    (N8)   /* (D)
      4 BENE-FOL-UP-IND   (N1)   /* (D)
      4 BENE-OWN-TRNSF-DT (N8)   /* (D)
      4 BENE-SUC-OWN      (N1)   /* (d)
      4 BENE-NEW-LAYOUT   (N1)   /* (D)/* was -setl-opt
      4 BENE-COM-DIS      (N2)   /* (D)/* Common disaster not used
      4 BENE-PER-STR      (N1)   /* (D)/* Per Stirpes not user
      4 BENE-FIL1         (A1)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)  /* (D)
      4 BENE-RESTRICT    (A1)  /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1) /* (D, P/A)
        5 BENE-SHARE-TYP   (A1) /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 BENE-TEXTS-6-POLS (6)
      4 BENE-TEXT-PRIM-SEC (2)
        5 BENE-TEXT-OCC    (5)
          6 BENE-DESIG-2    (A60)
          6 REDEFINE BENE-DESIG-2
            7 BENE-BN-NAME        (A25)
            7 BENE-BN-SSN         (A9)
            7 BENE-BN-REL         (A1)
            7 BENE-BN-SHARE       (A10)
            7 BENE-BN-SET-OPT     (A1)
            7 BENE-BN-RESTRICTION (A1)
            7 BENE-BN-DOB-MM      (A2)
            7 BENE-BN-DOB-DD      (A2)
            7 BENE-BN-DOB-YYYY    (A4)
            7 BENE-BN-NEW-FILL    (A5)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 BENE-F-V VIEW OF A-BENE-FINAL
  2 ID-NUMBER           (N6)
  2 NOTE-DATE           (N8/60)
  2 NOTE-CLERK-ID       (A3/60)
  2 NOTE-TEXT           (A60/60)
  2 BENE-CONTACT-ID     (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
1 #WORK-DATE  (A6)
1 REDEFINE #WORK-DATE
  2 #WORK-YY   (A2)
  2 REDEFINE #WORK-YY
    3 #WORK-YY-N(N2)
  2 #WORK-REST (A4)
1 #WORK-8        (A8) 1 REDEFINE #WORK-8
  2 #WORK-8-N    (N8)
*
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9)
  2 SPLIT-IND     (9)
1 #PSTAT          (A1/9)
1 #PID            (A16/9)
1 #PID-NBR        (I2)
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 J       (I2)
1 #POLICY-MATCH   (L)
*
END-DEFINE
*
RESET PrimaryBD
  ContingentBD
  #POLICY-MATCH
  #MSG
FIND (1) POLICY WITH ID-NUMBER = #CN
END-FIND
FIND BENE-F-V WITH ID-NUMBER = #CN
  IF NO RECORDS FOUND
    PrimaryBD := ' '
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME BENE-F-V TO NT-V
/*
  FOR #I1 1 TO 6
    IF NT-V.BENE-ISS-DT (#I1, 1) NE 0
      IGNORE
    ELSE
      ESCAPE TOP
    END-IF
/* Collect policy pool for current beneficiary CN
    FOR #I3 1 TO 7  /* Same Designation
      IF NT-V.BENE-ISS-DT (#I1,#I3) = 0
        ESCAPE TOP
      END-IF
      MOVE EDITED NT-V.BENE-ISS-DT (#I1,#I3) (EM=999999) TO #WORK-DATE
      IF #WORK-YY-N LT 16
        #WORK-8 := '20'
      ELSE
        #WORK-8 := '19'
      END-IF
      COMPRESS #WORK-8 #WORK-YY #WORK-REST INTO #WORK-8 LEAVING NO
      FOR J 1 TO POLICY.C*ACTIVE-SEGMENTS
        IF POLICY.ISSUE-DATE (J) = #WORK-8-N
            AND POLICY.SPLIT-IND (J) = NT-V.BENE-SPLIT-IND (#I1,#I3)
          ADD 1 TO #PID-NBR
          #PID (#PID-NBR) := POLICY.POLICY-ID (J)
          #PSTAT (#PID-NBR) := POLICY.STATUS (J)
        END-IF
      END-FOR
      /*
      IF #PID-NBR > 0
          AND #POLICY-NUMBER = #PID (1:#PID-NBR)
        #POLICY-MATCH := TRUE
        ESCAPE BOTTOM  /* REQUESTED POLICY MATCH
      END-IF
    END-FOR
    IF #POLICY-MATCH
      FOR #I2 1 TO 2
        FOR #I3 1 TO 5
          IF NT-V.BENE-BN-NAME (#I1,#I2,#I3) = ' '
            ESCAPE TOP
          END-IF
          IF #I2 = 1
            COMPRESS PrimaryBD BENE-DESIG-2 (#I1,#I2,#I3)
              INTO PrimaryBD
          ELSE
            COMPRESS ContingentBD BENE-DESIG-2 (#I1,#I2,#I3)
              INTO ContingentBD
          END-IF
        END-FOR
      END-FOR
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
END
