* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : C2151NPO                                              *
* DESCRIPTION  : Show LTII re-new rates
************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG     REASON
* NGG    20121025         Program Written
* NGG    20130325    NG1  Change description for LTII+
************************************************************************
*
DEFINE DATA
PARAMETER
1 #DESIRED-FACE-AMT  (P7.2)
1 #AGEP              (N2)
1 #VT-TERM           (N2)
1 #GENDER            (A1)
1 #TITLE-LINE        (A79/2)
1 #FORMATED-LINES    (A79/18)
LOCAL
1 #EXTEND        (N2)
1 #CURRENT       (N5.2/99)
1 #GUARANTEED    (N5.2/99)
1 #LTIIRV        (A8/99)
1 #LTIIRVG       (A8/99)
1 #AGE           (A8/99)
1 #FACE-A        (A9)
1 J              (I4)
1 K              (I4)
1 L              (I4)
*
END-DEFINE
*
RESET #FORMATED-LINES (*)
IF #DESIRED-FACE-AMT > 0
  #EXTEND := 99
  CALLNAT 'C2150NPO'
    #DESIRED-FACE-AMT
    #AGEP
    #VT-TERM
    #EXTEND
    #CURRENT (*)
    #GUARANTEED (*)
  MOVE EDITED #DESIRED-FACE-AMT (EM=ZZ9,999) TO #FACE-A
  COMPRESS ' $' #FACE-A INTO #FACE-A LEAVING NO
  COMPRESS #FACE-A ' of Level Term II+ :'
    '(Current Age' #AGEP 'Gender' #GENDER ')'
    INTO #TITLE-LINE (1)
  COMPRESS ' Maximum monthly re-new rates after' #VT-TERM 'years:'   /* NG1
    INTO #TITLE-LINE (2)
ELSE
  ESCAPE ROUTINE
END-IF
K := #AGEP + #VT-TERM
FOR J 1 TO #EXTEND
  MOVE EDITED #CURRENT (J) (EM=ZZZZ9.99) TO #LTIIRV (J)
  MOVE EDITED #GUARANTEED (J) (EM=ZZZZ9.99) TO #LTIIRVG (J)
  MOVE EDITED K (EM=ZZZZZZZZ) TO #AGE (J)
  ADD 1 TO K
END-FOR
*
J := 1;  K := 1
COMPRESS #AGE     (J:J+7) INTO #FORMATED-LINES (K)
**COMPRESS #LTIIRV  (J:J+7) INTO #FORMATED-LINES (K + 1)  Current rates
COMPRESS #LTIIRVG (J:J+7) INTO #FORMATED-LINES (K + 1)
ADD 8 TO J
ADD 2 TO K
REPEAT UNTIL #CURRENT (J) = 0 OR K > 18
  COMPRESS #AGE     (J:J+7) INTO #FORMATED-LINES (K)
**COMPRESS #LTIIRV  (J:J+7) INTO #FORMATED-LINES (K + 1)  Current rates
  COMPRESS #LTIIRVG (J:J+7) INTO #FORMATED-LINES (K + 1)
  ADD 8 TO J
  ADD 2 TO K
END-REPEAT
END
