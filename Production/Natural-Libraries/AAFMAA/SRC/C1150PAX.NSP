* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1150PAX
* THIS PROGRAM PROCESSES THE 'AX' (APPL TRACK) CROSS-REFERENCE SCREEN
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX        /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
1 #SERVICES          (A1) INIT<'S'>  /* PM1
1 #INSURANCE         (A1) INIT<'I'>  /* PM1
1 #BOTH              (A1) INIT<'B'>  /* PM1
1 #GRANDFATHERED     (A1) INIT<'G'>  /* PM1
1 #BLANK             (A1) INIT<' '>  /* PM1
1 #SERVICES-AT-FLAG  (A1) INIT<'V'>  /* PM1
1 #SCR-SELECT-CMND (A2/18)
1 #S-NAME          (A25)
1 #SCR-SSN-TAB     (18)
2 #SCR-SSN      (A9)
2 REDEFINE #SCR-SSN
3 #SCR-SSN-N    (N9)
1 #SCR-ACTION-FLAG (A1/18)
1 #SCR-APPL-STATUS (A2/18)
1 #SCR-NAME        (A25/18)
1 #SCR-APPL-SOURCE (A7/18)
1 #SCR-REC-DATE    (A8/18)
1 #SCR-INQ-ID      (A6/18)
1 #SCR-PLAN        (A2/18)
1 #SCR-TYPE        (A1/18)
1 I (I1)
1 #HOLD-REC-DATE   (A8)
1 APPL-TRACK VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 SSN              (N9)
  2 NAME             (A25)
  2 DATE-RECEIVED    (N8)
  2 REDEFINE DATE-RECEIVED
  3 DR-CC            (A2)
  3 DR-YY            (A2)
  3 DR-MM            (A2)
  3 DR-DD            (A2)
* 2 ACTION-FLAG      (A1)
  2 AT-STATUS        (A2)
  2 APPL-SOURCE
  2 POLICY-TYPE      (A1)
  2 INTRO-FLAG       (A1)
  2 INS-ONLY         (A1)
  2 MEMBER-CN
*
1 #START-AT    (A15)
1 REDEFINE #START-AT
 2 #START-AT-N  (N9)
*
END-DEFINE
* * * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * * *
INPUT #START-AT
MOVE #START-AT-N TO #SCR-SSN-N (1)
*
RESET #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPEAT
  IF #CGA-SCR-COMMAND NE ' '
    FETCH 'G1000PXX'
  END-IF
*
  ASSIGN I = 1
  READ APPL-TRACK LOGICAL WITH SSN STARTING FROM #SCR-SSN-N(1)
*
    DECIDE FOR FIRST CONDITION      /* PM1
      WHEN #CGA-USER-COMPANY = #BOTH
        IGNORE
      WHEN #CGA-USER-COMPANY = #SERVICES
        IF APPL-TRACK.INTRO-FLAG NE #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
      WHEN NONE
        IF APPL-TRACK.INTRO-FLAG EQ #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
    END-DECIDE
*
    MOVE EDITED SSN (EM=999999999) TO #SCR-SSN(I)
    ASSIGN #SCR-NAME(I)   = NAME
*   ASSIGN #SCR-ACTION-FLAG(I)   = ACTION-FLAG
    ASSIGN #SCR-APPL-STATUS(I)   = AT-STATUS
    COMPRESS DR-MM '/' DR-DD '/' DR-YY INTO #SCR-REC-DATE(I)
       LEAVING NO SPACE
*
    DECIDE ON FIRST APPL-TRACK.POLICY-TYPE
      VALUE 'A' MOVE 'OL' TO #SCR-PLAN (I)
      VALUE 'B' MOVE '30' TO #SCR-PLAN (I)
      VALUE 'C' MOVE '20' TO #SCR-PLAN (I)
      VALUE 'D' MOVE 'SP' TO #SCR-PLAN (I)
      VALUE 'E' MOVE '7P' TO #SCR-PLAN (I)
      VALUE 'F' MOVE 'RT' TO #SCR-PLAN (I)
      VALUE 'G' MOVE 'LT' TO #SCR-PLAN (I)
      VALUE 'H' MOVE 'SO' TO #SCR-PLAN (I)
      NONE IGNORE
    END-DECIDE
*
    IF #CGA-USER-COMPANY = #SERVICES
      IF NOT INS-ONLY = #BLANK OR = #SERVICES
        MOVE INS-ONLY TO #SCR-TYPE (I)
      END-IF
    ELSE
      MOVE INTRO-FLAG TO #SCR-TYPE (I)
    END-IF
*
    MOVE APPL-SOURCE TO #SCR-APPL-SOURCE (I)
*
*   IF  ((ACTION-FLAG = 'I') AND (ID-NUMBER NE 0))
*    OR  (MEMBER-CN = ' ')
    IF ID-NUMBER NE 0
       MOVE EDITED ID-NUMBER(EM=ZZZZZZ) TO #SCR-INQ-ID(I)
    ELSE
       MOVE MEMBER-CN TO #SCR-INQ-ID(I)
    END-IF
*
    IF I GE 18      /* PM1
      ESCAPE BOTTOM
    ELSE
      ADD 1 TO I
    END-IF
*
  END-READ /* (0074)
*
  IF I LT 18
    ASSIGN #SCR-NAME(I) = '** END OF DATA **'
    ASSIGN #CGA-SCR-COMMAND = 'AR'
    ASSIGN #CGA-SCR-SUFFIX  = 'SSN'
  END-IF
*
  ASSIGN #CGA-SCR-ID = #SCR-SSN-N (18)
  INPUT MARK *#CGA-SCR-ID USING MAP 'C1100MAR'
*
  RESET #SCR-SSN         (1:18)
  RESET #SCR-NAME        (1:18)
* RESET #SCR-ACTION-FLAG (1:18)
  RESET #SCR-APPL-STATUS (1:18)
  RESET #SCR-REC-DATE    (1:18)
  RESET #SCR-APPL-SOURCE (1:18)
  RESET #SCR-TYPE        (1:18)
  MOVE #CGA-SCR-ID TO #SCR-SSN-N (1)
  RESET #SCR-INQ-ID      (1:18)
  RESET #SCR-PLAN        (1:18)
*
  IF #S-NAME NE ' '
    MOVE #S-NAME TO #SCR-SSN (1)
    RESET #S-NAME
  END-IF
*
END-REPEAT /* (0068)
END
