* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME      : ATHPCANC                                                 *
*                                                                      *
* FUNCTION  : Create an e-mail for cancelled CAP loan applications     *
*             and resets the values of the fields.                     *
* HISTORY   :                                                          *                                                          *
* vxt 05022011  Initial creation .                                     *
* bz-Name 07/07/2017     Fine Names                                   *
************************************************************************
DEFINE DATA
LOCAL USING GPRL0001       /* Property file
LOCAL USING GPRA0005       /* E-mail Information
*
LOCAL
1 status view of a-status
  2 id-number
  2 rank
  2 military-service
  2 member-contact-id
  2 intro-flag
  2 x-alt-lp-date (9)
  2 x-alt-lp-ind  (9)
  2 x-alt-lp-amt  (9)
  2 conv-duration (9)
  2 linked-cn     (1)
  2 issue-date    (9)
*
1 st view of a-status
  2 id-number
  2 member-contact-id
  2 x-alt-lp-date (9)
  2 x-alt-lp-ind  (9)
  2 x-alt-lp-amt  (9)
  2 conv-duration (9)
  2 name
*
1 #TR-FIELDS
  2 #TR-ID-NUMBER                     (N6)
  2 #TR-NAME                          (A) DYNAMIC
  2 #TR-DATE-LAST-UPDATE              (N8)
  2 #TR-CLERK-ID                      (A3)
  2 #LOG-DESCRIPTION                  (A) DYNAMIC
1 #SYNC-FIELD-TABLE                   (N3/1:32)
1 #OLD-DATA-TABLE               (1:32)
  2 #field-description                (a10)
  2 #old-data                         (a115)
  2 redefine #old-data
    3 #alt-ind-label1                 (a8) 
    3 #alt-ind-old                    (a2)
    3 #alt-amt-label1                 (A8)
    3 #alt-amt-old                    (a9)
    3 #alt-date-label1                (a10) 
    3 #alt-date-old                   (a10)
    3 #rest-1                         (a68) 
1 #NEW-DATA-TABLE               (1:32)
  2 #new-data-tag                     (a3)
  2 #new-data                         (a122)
  2 redefine #new-data
    3 #alt-ind-label2                 (a8) 
    3 #alt-ind-new                    (a2)
    3 #alt-amt-label2                 (A8)
    3 #alt-amt-new                    (a9)
    3 #alt-date-label2                (a10) 
    3 #alt-date-new                   (a10)
    3 #rest-1-new                     (a71) 
*
1 #curr-date                          (N8)
1 REDEFINE #curr-date
  2 #curr-date-YYYY                   (N4)
  2 #curr-date-MM                     (A2)
  2 #curr-date-DD                     (A2)
1 #curr-date-d                        (d)
1 #cap-date                           (n8)
1 #cap-date-d                         (d)
1 #work-date                          (n8)
1 redefine #work-date
  2 #work-date-yyyy                   (a4)
  2 #work-date-mm                     (a2)
  2 #work-date-dd                     (a2)
1 #work-date-a                        (a8)
1 #alt-lp-date                        (a10)
1 #diff                               (n8)
1 #pol-num                            (a16)
1 #owner-name                         (A)  DYNAMIC
1 #I                                  (I2)
1 #pol-seg                            (i2)
1 #policy-segment                     (a1)
1 #owner-email                        (a60)
1 #isn                                (n10)
1 #old-amt                            (n5.2) 
*
1 #mil-service                        (a3)
1 #EDIT-INVALID                       (L)
1 #RANK-LITERAL                       (A23)
1 #DISPL                              (P2)
1 #RANK-5                             (A5)
1 #CONTACT-ID                         (n8)
1 #cont-id                            (n8)
1 #cn                                 (a6)
1 redefine #cn
  2 #cn-n                             (n6)
1 #cnt-canc                           (p7)
1 #cnt-upd                            (p7)
1 #RANK-ABBR                          (A) DYNAMIC
1 #LETTER-ENV                         (A) DYNAMIC
1 #SALUTATION                         (A) DYNAMIC
1 #SALUTATION-WITH-NAME               (A) DYNAMIC
1 #name-in                            (a) dynamic
*
1 #non-ins-owner                      (l)
1 #multiple-owner                     (l)
1 #owner-occ-9                        (9)
  2 #owner-flag                       (a1)
  2 #owner-cn                         (n6/10)
  2 #OWNER-CN-BY-POL                  (N6)
  2 #OWNER-CN-SEG                     (I2/9)
  2 #ISSUE-DATE                       (N8/9)
  2 #ISSUE-DATE-DISP                  (A10/9)
  2 #BN-TYPE                          (A1/2)
  2 #BN-TEXT                          (A60/10)
1 #id-number                          (n6)
1 #description                        (a10)
1 #out-file-1                         (a250)
1 #alt-ind                            (a1)
1 #id-number1                         (n6)
*
1 #TAB                                (A1) INIT <H'09'>
1 #EMPTY-FILE-CMD                     (A)  DYNAMIC INIT <'>'>
1 #cancel-TEMPLATE                    (A3) INIT <'CAN'>
1 #pass-command                       (a2)
1 #issue-date-a                       (a10)
1 #name                               (a75)
1 #DAILY-EMAIL-CAP-CANCELL (A) DYNAMIC INIT
  <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/COM/EmailDailyFile.txt'>
1 #work-file (a) dynamic init
  <'/usr/SAG/AAFMAA/batch/workfiles/TEMP/isnlist.txt'>
1 #out-file-2 (a) dynamic init
  <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/ATH/reportcanc.txt'>
*
END-DEFINE
*
DEFINE WORK FILE 1 #DAILY-EMAIL-CAP-CANCELL TYPE 'ASCII-COMPRESSED'
                   ATTRIBUTES 'APPEND'  
DEFINE WORK FILE 2 #work-file TYPE 'ASCII-COMPRESSED'
define work file 3 #out-file-2 type 'ascii-compressed'
*
* Delete old email file
* CALLNAT 'GPRN0002' #EMPTY-FILE-CMD #DAILY-EMAIL-CAP-CANCELL
*
compress
  'CN'                    #tab
  'Name'                  #tab
  'Issue Date'            #tab
  'Policy Number'         #tab
  'AIL/EIL'               #tab
  'Date AIL/EIL created'  #tab
  into  #out-file-1 leaving no
write work 3 #out-file-1
write work 2 #isn #pol-seg #pol-num
*
#CURR-DATE := *datn
#curr-date-d := *datx
*
WRITE 'process started'
*
reset #cap-date #pol-seg #diff #alt-ind #id-number1 #id-number
read Status by ISN
  #cn := status.id-number
  F1. for #i 1 9
    if ( x-alt-lp-date (#i) ne 0 )
        and (x-alt-lp-ind (#i) = 'L' or = 'E')
*
      move x-alt-lp-date(#i) to #work-date-a
      move edited #work-date-a to #cap-date-d(em=yyyymmdd)
      move x-alt-lp-date(#i) to #cap-date
      move x-alt-lp-ind (#I) to #alt-ind
      #pol-seg := #i
      move *isn to #isn
      move status.id-number to #id-number1  #id-number
*                                                                        
      compute #diff = #curr-date-d - #cap-date-d
      if #diff gt 120 
        callnat 'gplnmsxx' #pol-num #id-number1 #pol-seg
        write work 2 #isn #pol-seg #pol-num
        perform create-email
        perform create-report
        escape bottom (f1.)
      end-if
    end-if
  end-for
*
*****************
define subroutine create-email
*****************
  add 1 to #cnt-canc
  reset #owner-email #policy-segment
  #POL-SEG                             := F_POLICY_SEGMENT(<#pol-num,#cn>)
  IF #POL-SEG NE 0
    #POLICY-SEGMENT := #POL-SEG
    #OWNER-EMAIL       := FUNCTION-OWNER-EMAIL(<#cn,#POLICY-SEGMENT>)
  END-IF
  IF #OWNER-EMAIL = ' '
    #OWNER-EMAIL       := FUNCTION-MEMBER-EMAIL(<#cn>)
  END-IF
  IF #OWNER-EMAIL = ' '
    #OWNER-EMAIL       := FUNCTION-CN-EMAIL(<#cn>)
  END-IF
  IF #OWNER-EMAIL = ' '
    #OWNER-EMAIL       := '999'
  END-IF
  move *isn to #isn
  reset   #mil-service
    #contact-id
    #salutation-with-name

  MOVE status.military-service TO #mil-service

  move status.member-contact-id to #contact-id

  #SALUTATION-WITH-NAME := 'S1'
  CALLNAT 'GPRS0CNT' #CONTACT-ID #SALUTATION-WITH-NAME
*  #SALUTATION-WITH-NAME := EDIT_NAME_LOWCASE (< #SALUTATION-WITH-NAME >)  /* bz-Names
  #owner-name := #SALUTATION-WITH-NAME
*
  callnat 'Gownrsxx' #CN-n
    #non-ins-owner
    #multiple-owner
    #owner-occ-9 (1:9)
*
  if #non-ins-owner and #pol-seg ne 0
    if #owner-cn(#pol-seg,1) ne 0
      #id-number := #owner-cn(#pol-seg,1)
      perform get-owner-name
    else
      if intro-flag = 'S' or = 'K' or = 'G'
        #id-number := linked-cn(1)
        perform get-owner-name
      end-if
    end-if
  end-if
*
  EMAIL-INFORMATION.DAL-CN             := status.id-number
  EMAIL-INFORMATION.OWNER-EMAIL        := #OWNER-EMAIL
  EMAIL-INFORMATION.POLICY-NUMBER      := #pol-num
  EMAil-INFORMAtion.OWNER-NAME         := #owner-name
  email-information.template-type      := #cancel-template
*
  if #owner-email = ' ' or = '999'
    escape top
  else
    WRITE WORK FILE 1 EMAIL-INFORMATION
  end-if
  end-subroutine
*
end-read
*
close work 2
*
read work 2 #isn #pol-seg #pol-num
    if #pol-seg ne 0
    perform reset-fields
  end-if
end-work
*
*****************
define subroutine get-owner-name
*****************
*
find st with st.id-number = #id-number
  if no
    escape bottom
  end-norec
  move st.member-contact-id to #cont-id
  CALLNAT 'RANKN001'
    #PASS-COMMAND
    status.RANK
    #MIL-SERVICE
    #EDIT-INVALID
    #DISPL
    #RANK-LITERAL
    #RANK-5
*
    #CONT-ID
    #RANK-ABBR
    #LETTER-ENV
    #SALUTATION
    #SALUTATION-WITH-NAME
*
  #name       := #letter-env
  #owner-name := #SALUTATION-WITH-NAME
*
end-find
*
end-subroutine
*
*****************
define subroutine reset-fields
*****************
*
get st #isn
*
perform prepare-tr-rec
*
*
write 'before' '=' st.id-number
  '='  st.x-alt-lp-date(#pol-seg)
  '='  st.x-alt-lp-ind(#pol-seg)
  '=' st.x-alt-lp-amt(#pol-seg)
  '=' st.conv-duration(#pol-seg)
*
reset st.x-alt-lp-date(#pol-seg)
  st.x-alt-lp-ind(#pol-seg)
  st.x-alt-lp-amt(#pol-seg)
  st.conv-duration(#pol-seg)
add 1 to #cnt-upd
*
write 'after' '=' st.id-number
  '=' st.x-alt-lp-date(#pol-seg)
  '='  st.x-alt-lp-ind(#pol-seg)
  '=' st.x-alt-lp-amt(#pol-seg)
  '=' st.conv-duration(#pol-seg)
*
update (0305)
end transaction
*
end-subroutine
*
*****************
define subroutine prepare-tr-rec
*****************
*
#alt-ind-label1 (1):= 'ALP IND' 
#alt-ind-old (1)   := st.x-alt-lp-ind(#pol-seg)
#alt-amt-label1 (1):= 'ALP AMT'
MOVE EDITED ST.X-ALT-LP-AMT(#pol-seg)(EM=Z,ZZZ.99-) TO #alt-amt-old(1)
#alt-date-label1(1):= 'ALP DATE'
#alt-date-old(1)   := st.x-alt-lp-date(#pol-seg) 
*
move st.x-alt-lp-amt(#pol-seg) to #old-amt 
reset #old-amt 
#alt-ind-label2(1) := 'ALP IND' 
#alt-ind-new (1):= ' '
#alt-amt-label2 (1) := 'ALP AMT'
MOVE EDITED #old-amt(EM=Z,ZZZ.99-) TO #alt-amt-new(1) 
#alt-date-label2 (1):= 'ALP DATE'
#alt-date-new (1):= '00000000' 
#tr-name                := st.name
#TR-ID-NUMBER           := st.id-number
#LOG-DESCRIPTION        := 'AM-CANCELLED' 
#TR-CLERK-ID            := 'EDS'
#TR-DATE-LAST-UPDATE    := *datn
*
perform write-tr-rec
*
end-subroutine
*
*****************
define subroutine write-tr-rec
*****************
*
CALLNAT 'GPRN0004'
  #TR-FIELDS #SYNC-FIELD-TABLE(*)
  #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
*
end-subroutine
*
*****************
define subroutine create-report
*****************
*
reset #issue-date-a #alt-lp-date #description
move status.issue-date(#pol-seg) to #work-date
compress #work-date-mm '/' #work-date-dd '/' #work-date-yyyy
  into #issue-date-a leaving no
*
move status.x-alt-lp-date(#pol-seg) to #work-date
compress #work-date-mm '/' #work-date-dd '/' #work-date-yyyy
  into #alt-lp-date leaving no
*
if status.x-alt-lp-ind(#pol-seg) = 'E'
  move 'EZ-Pay' to #description
else
  if status.x-alt-lp-ind(#pol-seg) = 'L'
    move 'Allotment' to #description
  end-if
end-if
*
#name-in              := '! *N'                     /* bz-Names
CALLNAT 'GPRS0CNT' #CONTACT-ID #name-in
* #name-in := EDIT_NAME_LOWCASE (< #NAME-in >)
#name := #name-in
*
compress status.id-number    #tab
  #name                      #tab
  #issue-date-a              #tab
  #pol-num                   #tab
  #description               #tab
  #alt-lp-date               #tab
  into #out-file-1 leaving no
write work 3 #out-file-1
*
end-subroutine
write 'dayly email cancellation e-mails' #cnt-canc  /
  'total record updated ' #cnt-upd
END
