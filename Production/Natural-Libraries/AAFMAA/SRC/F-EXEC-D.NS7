* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* F-EXEC-DATE
* Saves and retreives information about the last run or sets next
* run date for the process on GEN-TABLE record
* SEC-KEY points to the process type
* Action: W - save date
*         R - retrieve date
*******************************************************************
* MODIFICATION LOG
* USER   DATE      TAG     REASON
*******************************************************************                                                                      *
* YAK    06012010          Create program
*******************************************************************
DEFINE FUNCTION FUNCTION-EXEC-DATE RETURNS (N8)
DEFINE DATA
PARAMETER
1 #DATE         (N8)
1 #PROCESS-TYPE (A3)
1 #ACTION-TYPE  (A1)
LOCAL USING OBJLGENT
LOCAL
1 #MAIN-KEY      (A4) CONST <'DATE'>
1 #SEC-KEY       (A7)
1 #RUN-DATE (N8)
1 REDEFINE #RUN-DATE
  2 #RUN-DATE-A (A8)
END-DEFINE
*
#RUN-DATE := #DATE
COMPRESS #MAIN-KEY #PROCESS-TYPE INTO #SEC-KEY LEAVING NO
F.
FIND(1) GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  DECIDE ON FIRST VALUE #ACTION-TYPE
     VALUE 'W'                     /* save new date
        GEN-TABLE-V.ADDL-INFO := #RUN-DATE-A
        UPDATE (F.)
     VALUE 'R'                     /* retrieve saved date
        #RUN-DATE-A := GEN-TABLE-V.ADDL-INFO
        FUNCTION-EXEC-DATE := #RUN-DATE
     NONE VALUE
        IGNORE
  END-DECIDE
  END TRANSACTION
  ESCAPE ROUTINE
END-FIND
*
END-FUNCTION
END
