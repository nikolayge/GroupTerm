* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C2060NNC NEW CERTIFICATE SCREEN
* PURPOSE   : TO PROCESS NEW CERTIFICATE SCREEN FOR ANNUAL RENEWABLE
*
* TMT  06/16/2014  TT1  Contact Normalization - DOB and Sex
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-ISN         (P8)
1 #INS-INDEX       (I2)
LOCAL
1 #90                (N2) CONST <90>
1 #PLAN              (A3) 1 REDEFINE #PLAN
  2 #PLAN-1          (A1)
  2 #VT-TERM         (N2)
1 #ISSUE-AGE         (A2)
1 #TYPE-TEXT         (A12)
1 #AGE               (A3/18,5)
1 #CURRENT-QUOTE     (N5.2/1:#90)
1 #GUARANTEED-QUOTE  (N5.2/1:#90)
1 #LTIIRVG           (A8/18,5)
*
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID (N8)
  2 ID-NUMBER         (N6)
*   2 DATE-OF-BIRTH     (N8)             /* TT1
*   2 SEX      /* DW5                    /* TT1
  2 INTRO-FLAG
  2 C*ACTIVE-SEGMENTS
  2 DATE-ENTERED      (9)
  2 NUM-MBRSHIPS      (9)
  2 STATUS            (9)
  2 PLAN              (A3/9)
  2 ISSUE-AGE         (9)
  2 REDEFINE ISSUE-AGE
    3 ISSUE-AGE-OCC   (9)
      4 ISSUE-AGE-N    (N2)
  2 FACE-AMOUNT       (P7.2/9)
  2 ISSUE-DATE        (9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC (9)
      4 ISSUE-DATE-CENT   (N2)
      4 ISSUE-DATE-YYMMDD (N6)
      4 REDEFINE ISSUE-DATE-YYMMDD
        5 ISSUE-DATE-YY   (N2)
        5 ISSUE-DATE-MM   (N2)
        5 ISSUE-DATE-DD   (N2)
1 #EXTENT            (N2)
1 #TYPE-T  (A12/3) CONST <'Select','Super Select', 'Standard' >
1 K      (N3)
1 J      (I2)
1 L      (I2)
1 M      (I2)
1 M1     (I2)
1 #FACE-AMOUNT   (P7.2)
END-DEFINE
*
GET ST-V #CGA-ISN
*
#PLAN := PLAN ( #INS-INDEX )
DECIDE ON FIRST VALUE OF #PLAN-1
  VALUE 'W' #PLAN-1 := 'Q'
  VALUE 'V' #PLAN-1 := 'R'
  VALUE 'U' #PLAN-1 := 'S'
  NONE VALUE IGNORE
END-DECIDE
*
IF #PLAN-1 = 'S' OR= 'R' OR= 'Q'
  IF FACE-AMOUNT ( #INS-INDEX ) > 0
    DECIDE ON FIRST VALUE OF #PLAN-1
      VALUE 'Q' #TYPE-TEXT := #TYPE-T (2)
      VALUE 'R' #TYPE-TEXT := #TYPE-T (1)
      VALUE 'S' #TYPE-TEXT := #TYPE-T (3)
      NONE VALUE IGNORE
    END-DECIDE
     #EXTENT := #90
*    #VT-TERM := 5   only for testing
*    ISSUE-AGE-N ( #INS-INDEX ) := 18
    MOVE EDITED ISSUE-AGE-N ( #INS-INDEX )(EM=99) TO #ISSUE-AGE
    CALLNAT 'C2150NPO'
      FACE-AMOUNT ( #INS-INDEX )
      ISSUE-AGE-N ( #INS-INDEX )
      #VT-TERM
      #EXTENT
      #CURRENT-QUOTE (*)
      #GUARANTEED-QUOTE (*)
    L := 1 M := 0
    K := ISSUE-AGE-N ( #INS-INDEX ) + #VT-TERM
    FOR J 1 TO #90
      IF #GUARANTEED-QUOTE (J) <= 0
        ESCAPE BOTTOM
      END-IF
    END-FOR
    M1 := J / 5 + 1
    FOR J 1 TO #90
      IF #GUARANTEED-QUOTE (J) > 0
        ADD 1 TO M
        IF M > M1
          ADD 1 TO L
          M := 1
        END-IF
        MOVE EDITED #GUARANTEED-QUOTE (J) (EM=ZZZZ9.99) TO #LTIIRVG (M,L)
        MOVE EDITED K (EM=ZZZ) TO #AGE (M,L)
        ADD 1 TO K
      ELSE
        ESCAPE BOTTOM
      END-IF
    END-FOR
    #FACE-AMOUNT := FACE-AMOUNT ( #INS-INDEX )  /* Only for MAP
    INPUT USING MAP 'C2063MNC'
  END-IF
END-IF
END
