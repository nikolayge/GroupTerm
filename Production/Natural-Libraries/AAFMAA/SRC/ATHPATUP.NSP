* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PROGRAM-ID: ATHPATUP - "Daily AT processing (Cap & Fu )
*  CHECK THESE LINES TO REINSTATE CAP APP PROCESSING * FU 2/3/
*
* CAP APPS 3535/4570
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  RESET DISPLAY FIELDS FOR GRANKSXX                 *
* DRW  05162005 DW2  ADD SEX FOR LTII RATES                            *
* DRW  06042005 DW3  AFF F60/F90 PROCESSING                            *
* SAG  12012005 RH1  SS - REPLACED THE HEC CONSTANT '05' (TAB POSITION)*
*                    EBCDIC TO HEX CODE '09' ASCII                     *
* DRW  07062006 DW4  REMOVE BAR CODE FOR AFSC CUT SHEET                *
* YAK  05022007 YK1  CALL TO THE NEW SUBPROGRAM C4127SCO FOR           *
*                    'Application status by user' STATISTICS           *
* YK2  06042007 YK2  Create a new map to display Prospective CAP       *
*                    statistics. Add new counters to REDEFINE for      *
*                    NOTE record 800106                                *
* DRW 01/15/2008 DW5 FU30 chged to 45 days Also Remove OLD FU60 FU90   *
* DRW 01/16/2008 DW6 Add addl Follow-up Processing record updates      *
* PAM 04/10/2008 PM7 Changed FU30 to FU45 for display                  *
* PAM 02/26/2009 PM8 Added CAP types                                   *
* PAM 03/04/2009 PM9 Removed CAP distribution date for 'G' types       *
* JNT 08/29/2013 JT1 Address normalization                             *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING G3500AAD  /* PM ADDED 03/2000 FOR FU5 LETTER
LOCAL
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER
  2 NOTE-DATE  (N8/1)  /* ZFXNTPXX
  2 NOTE-TEXT  (A60/40)
  2 REDEFINE NOTE-TEXT /* IF THIS IS CHANGED ALSO CHG C1000PAT
*                     /* C2000PNM/C2100PAI/ZFXNTPXX
    3 PROSPECTIVE-OCC (85)   /* 85 POSSIBLE USERS OF PROSP SYSTEM
      4 PR-USER        (A3)   /* WHO INITIATED IT
      4 PR-POLS-INIT   (N5)   /* HOW MANY HE INITIATED
      4 PR-POLS-ISSUED (N5)   /* HOW MANY WERE ISSUED
      4 PR-POLS-CURR   (N5)   /* HOW MANY THERE AE CURRENTLY
      4 PR-AVAIL-1     (N5)   /* NOT YET USED
      4 PR-AVAIL-2     (N5)   /* NOT YET USED
*
  2 REDEFINE NOTE-TEXT /* IF THIS IS CHANGED ALSO CHG ATHPCAPN
*                     /* C2000PNM/C2100PAI/C1000PAT/C2DA0PIM/ZFXNTPXX
    3 CAPS-CONV-BY-YEAR  (10)    /* 10 PAST YEARS
      4 CAPS-CONV-BY-ORIGIN (13)  /* 13TH IS FOR UNKNOWN
        5 CAPS-CONV-ORIG   (N5)
*
    3 CAPS-CONV-BY-MON(12)   /* 12 LAST MONTHS (JAN THRU DEC)
      4 CAPS-CONV-MON    (N5)
*
    3 ATS-CONV-BY-FU  (5)    /* 5 FOLLOW-UP (IN CASE THERE WILL BE 5)
      4 ATS-CONV-FU      (N5)
*
    3 CAPS-AT-M-S (N5)
    3 CAPS-AT-P1  (N5)  /* PR/PM/PO/PX
    3 CAPS-AT-P2  (N5)  /* P1/P2/P3
    3 CAPS-AT-P3 (N5)   /* PI/PC/PP
    3 CAPS-AT-P4 (N5)   /* PY
    3 CAPS-AT-U  (N5)
    3 CAPS-AT-W  (N5)
    3 CAPS-AT-D  (N5)
    3 CAPS-AT-A  (N5)
    3 CAPS-AT-UNUSED-1 (N5)
    3 CAPS-AT-UNUSED-2 (N5)
    3 CAPS-AT-NCO (N5)
    3 CAPS-AT-OFF (N5)
*
    3 CAPS-IM-S  (N5)
    3 CAPS-IM-M  (N5)
    3 CAPS-IM-I  (N5)
    3 CAPS-IM-C  (N5)
    3 CAPS-IM-P  (N5)
    3 CAPS-IM-X  (N5)
    3 CAPS-IM-Y  (N5)
    3 CAPS-IM-Z  (N5)
*
    3 AT-M-S-0FU  (N5)
    3 AT-M-S-1FU  (N5)
    3 AT-M-S-2FU  (N5)
    3 AT-M-S-3FU  (N5)
    3 AT-M-S-4FU  (N5)
    3 AT-P      (N5)
    3 AT-U      (N5)
    3 AT-W      (N5)
    3 AT-A      (N5)
*
    3 AT-SPS    (N5)
    3 AT-CHILD  (N5)
    3 AT-GRANDCHILD (N5)
*
    3 CAPS-CONV-BY-MON-ADDL(12) /* 12 LAST MONTHS (JAN THRU DEC) /* YK2
      4 CAPS-CONV-MON-A  (N5)                                    /* YK2
      4 CAPS-CONV-MON-GR (N5)                                    /* YK2
*
* * * *   A P P L I C A T I O N   T R A C K I N G   F I L E   * *
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 NAME           (A25)
  2 RANK           (A3)
  2 SEX                          /* DW2
  2 DATE-OF-BIRTH  (N8)
  2 MEMBER-CN      (A6)
  2 MIL-STATUS     (A1)
  2 MIL-SERVICE    (A3)
  2 PLAN           (A3)
  2 CONTACT-CODE   (A1)
  2 MODE           (A1)
  2 PMT-PLAN       (A1)
  2 INS-ONLY       (A1)
  2 INTRO-FLAG     (A1)
*  2 ADDRESS-1      (A25)        /* JT1 start
*  2 ADDRESS-2      (A25)
*  2 ADDRESS-3      (A25)
*  2 ZIP-CODE       (N9)
*  2 REDEFINE ZIP-CODE
*    3 ZIP-CODE-5     (A5)
*    3 ZIP-CODE-4     (A4)       /* JT1 end
  2 SSN            (N9)
  2 DATE-RECEIVED  (N8)
  2 NEXT-ACTION-DATE (N8)
  2 ACTION-DATE    (N8)
  2 ACTION-FLAG    (A1)
  2 AT-STATUS      (A2)
  2 REDEFINE AT-STATUS
    3 AT-STATUS-1  (A1)
    3 AT-STATUS-2  (A1)
  2 ST-NOTE-USER   (A3/50)
  2 ST-NOTE-DATE   (N8/50)
  2 ST-NOTE-STATUS (A2/50)
  2 APPL-SOURCE    (A7)
  2 CAP-IND        (A1)
  2 USER-SOURCE    (A3)
  2 FOLLOW-UP-DATE (N8/5)
  2 REDEFINE FOLLOW-UP-DATE
    3 FUD-OCC       (5)
      4 FUD-YYYY     (N4)
      4 FUD-MM       (N2)
      4 FUD-DD       (N2)
  2 VA-CALC-TYPE   (A1)
  2 HOME-PHONE     (N10)
  2 REDEFINE HOME-PHONE
    3 HOME-PHONE-A  (A10)
  2 HOME-PHONE-INTR (A6)
  2 WORK-PHONE     (N10)
  2 REDEFINE WORK-PHONE
    3 WORK-PHONE-A  (A10)
  2 WORK-PHONE-INTR (A6)
  2 NOTES           (A75/40)  /* PM8
  2 CAP-TYPE                  /* PM8
*
* * * * * * * *
1 SA-V VIEW OF A-SOURCE-OF-APPL
  2 SOURCE-CODE-GROUP  (A1)
*
* * * *   I N T R O   M E M B E R   F I L E * * *
1 IM-V VIEW OF A-INTRO-MEMBER
  2 ID-NUMBER      (N6)
  2 NAME           (A25)
  2 RANK           (A3)
  2 INS-ONLY       (A1)
  2 MIL-SERVICE    (A3)
* 2 MIL-STATUS     (A1)      /* JT1 COMMENTED OUT
*  2 ADDRESS-1      (A25)
*  2 ADDRESS-2      (A25)
*  2 ADDRESS-3      (A25)
*  2 ZIP-CODE       (N9)
*   2 REDEFINE ZIP-CODE
*     3 ZIP-CODE-5     (A5)
*     3 ZIP-CODE-4     (A4)
*   2 SSN            (N9)
  2 ACTION-FLAG    (A1)
  2 CAP-LST-ACTION-CHG (N8)   /* ONLY USED FOR IM
  2 APPL-SOURCE    (A6)
  2 CAP-IND        (A1)
  2 VA-CALC-TYPE   (A1)
  2 HOME-PHONE     (N10)
  2 REDEFINE HOME-PHONE
    3 HOME-PHONE-A  (A10)
  2 HOME-PHONE-INTR (A6)
*
1 LABELS                 /* USED FROM YESTERDAY TO UPDATE AT FILE
  2 LAB-ID          (A9)
  2 REDEFINE LAB-ID
    3 LAB-AT         (A2)
    3 LAB-CN-ID      (N6)
  2 LAB-MBRSHIPS    (N1)
  2 LAB-NAME        (A25)
  2 LAB-RANK        (A4)
  2 REDEFINE LAB-RANK
    3 LAB-RANK-3     (A3)
  2 LAB-ACT-FLAG    (A1)    /* USUALLY MIL-STAT
  2 LAB-ADDR-1      (A25)
  2 LAB-ADDR-2      (A25)
  2 LAB-ADDR-3      (A25)
  2 LAB-ZIP-CODE    (A10)
  2 LAB-MIL-SERV    (A3)
  2 LAB-SSN         (N9)
  2 LAB-ZIP-CODE-DPB (A2)
  2 LAB-CHIEF-OF-STAFF-IND (A1)
  2 LAB-FILL-1       (A10)
  2 LAB-SPUSE        (A1)
  2 LAB-FILL-2       (A25)
  2 LAB-LBL-TYPE     (A3)
  2 LAB-FILL-3       (A2)
*
1 #COUNTERS
  2 #AT-RECS-READ     (P5)
  2 #IM-RECS-READ     (P5)
  2 #MISSED           (A15)
  2 #CNT-A-DELETED    (P5)
  2 #CNT-P-DELETED    (P5)
  2 #CNT-I-DELETED    (P5)
  2 #CNT-DELETED      (P5)
  2 #CNT-RD-DELETED    (P5)
  2 #AT-RECS-UPDT     (P5)
  2 #IM-RECS-UPDT     (P5)
  2 #AFB-CNT          (P5)
  2 #CNT              (P5)
  2 #CNT-4            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #I4               (I2)
  2 #I5               (I2)
*
1 #JCL-PARM       (A32)
1 REDEFINE #JCL-PARM
  2 #JCL-WEEKLY-MONTHLY (A7)     /* 'NON MONTHLY ' OR 'MONTHLY'
1 REDEFINE #JCL-PARM
  2 #JCL-MM    (N2)   /* DATE PARM 09/01/1999
  2 #JCL-X1    (A1)
  2 #JCL-DD    (N2)
  2 #JCL-X2    (A1)
  2 #JCL-YYYY  (N4)
*
1 #WEEKLY-MONTHLY (A7)
*
1 #MONTH-LITERALS (A48) INIT<'Jan Feb Mar Apr May Jun Jul Aug Sep '
  -'Oct Nov Dec '>
1 REDEFINE #MONTH-LITERALS   2 #MON-LIT (A4/12)
*
1 #CAPS-CONV-BY-MONTH-YYYY (N4/12)
1 #CONV-YYYY-CV (C/12)
*
1 #MMDDYYYY   (A8)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM      (N2)
  2 #DD      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A  (A4)
  2 #MM-A    (A2)
  2 #DD-A    (A2)
*
1 #TODAY-YYYYMMDD  (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY    (N4)
  2 REDEFINE #TODAY-YYYY
    3 #TODAY-CENT    (N2)
    3 #TODAY-YY      (N2)
  2 #TODAY-MM      (N2)
  2 #TODAY-DD      (N2)
*
1 #PROCESS-YYYYMMDD (N8)
1 REDEFINE #PROCESS-YYYYMMDD    /* PM8
 2 #PROCESS-YYYY    (A4)        /* PM8
 2 #PROCESS-MM      (A2)        /* PM8
 2 #PROCESS-DD      (A2)        /* PM8
1 #1-MON-AGO (N8)
1 #18-MONS-AGO (N8)
1 #FORM-IND   (A3)
1 #MSG-FORM-IND (A3)
*
1 #CNT-5    (P5)
1 #CNT-6    (P5)
1 #4-MONS-AGO  (N8)
1 #DAY-30    (N8)
1 REDEFINE #DAY-30
  2 #D30-YYYY (N4)
  2 #D30-MM  (N2)
  2 #D30-DD  (N2)
*
1 #DAY-40    (N8)
1 REDEFINE #DAY-40
  2 #D40-YYYY (N4)
  2 #D40-MM   (N2)
  2 #D40-DD   (N2)
*
1 #DAY-45    (N8)
1 REDEFINE #DAY-45
  2 #D45-YYYY (N4)
  2 #D45-MM   (N2)
  2 #D45-DD   (N2)
*
1 #DAY-60    (N8)
1 REDEFINE #DAY-60
  2 #D60-YYYY (N4)
  2 #D60-MM  (N2)
  2 #D60-DD  (N2)
*
1 #DAY-75    (N8)
1 REDEFINE #DAY-75
  2 #D75-YYYY (N4)
  2 #D75-MM   (N2)
  2 #D75-DD   (N2)
*
1 #DAY-90    (N8)
1 REDEFINE #DAY-90
  2 #D90-YYYY (N4)
  2 #D90-MM   (N2)
  2 #D90-DD   (N2)
*
1 #DAY-105   (N8)
1 REDEFINE #DAY-105
  2 #D105-YYYY (N4)
  2 #D105-MM   (N2)
  2 #D105-DD   (N2)
*
1 #DAY-120    (N8)
1 REDEFINE #DAY-120
  2 #D120-YYYY (N4)
  2 #D120-MM   (N2)
  2 #D120-DD   (N2)
*
1 #DAY-135    (N8)
1 REDEFINE #DAY-135
  2 #D135-YYYY (N4)
  2 #D135-MM   (N2)
  2 #D135-DD   (N2)
*
1 #DAY-150    (N8)
1 REDEFINE #DAY-150
  2 #D150-YYYY (N4)
  2 #D150-MM   (N2)
  2 #D150-DD   (N2)
*
1 #DAY-180    (N8)
1 REDEFINE #DAY-180
  2 #D180-YYYY (N4)
  2 #D180-MM   (N2)
  2 #D180-DD   (N2)
*
1 #DAY-240    (N8)
1 REDEFINE #DAY-240
  2 #D240-YYYY (N4)
  2 #D240-MM   (N2)
  2 #D240-DD   (N2)
*
1 #DAY-270    (N8)
1 REDEFINE #DAY-270
  2 #D270-YYYY (N4)
  2 #D270-MM   (N2)
  2 #D270-DD   (N2)
*
1 #MON-13    (N8)
1 REDEFINE #MON-13
  2 #M13-YYYY (N4)
  2 #M13-MM   (N2)
  2 #M13-DD   (N2)
*
1 #FORMAT-CN            (A7)  /* PM ADDED 03/2000 FOR FU5 LETTER
1 #SURVEY-CN            (A16) /* PM ADDED 03/2000 FOR FU5 LETTER
1 #DEAR-FORMATED-NAME   (A45) /* PM ADDED 03/2000 FOR FU5 LETTER
1 #SCR-ZIP-BAR-CODE     (A14) /* PM ADDED 03/2000 FOR FU5 LETTER
*
1 #UX        (I2)
1 #U-ATS     (N6/40)
1 #U-STATUS  (A2/40)
*
1 #PX        (I2)
1 #P-ATS     (N6/40)
*
1 #ACTION-FLAG     (A1)
1 #AT-NOTE-STATUS  (A2)
1 #AFB-BANK  (A40)
1 #TEXT   (A75)
1 #UPDATE    (L)
1 #IS-A-Q    (L)
1 #CNT-APP-TO-MAIL      (P5)
1 #CNT-ALT-STRT-TO-MAIL (P5)
1 #CNT-FU-30            (P5)
1 #CNT-FU-60            (P5)   /* DW3
1 #CNT-FU-90            (P5)   /* DW3
1 #CNT-REG-APP-TO-MAIL  (P5)
1 #CNT-FOLUP-150    (P5)
1 #CNT-FOLUP-90     (N5)
1 #CNT-FOLUP-60     (N5)
1 #CNT-FOLUP-30     (N5)
*
1 #CNT-CAP-AT-M-S   (N5)
1 #CNT-CAP-AT-P1    (N5)
1 #CNT-CAP-AT-P2    (N5)
1 #CNT-CAP-AT-P3    (N5)
1 #CNT-CAP-AT-P4 (N5)
1 #CNT-CAP-AT-U  (N5)
1 #CNT-CAP-AT-W  (N5)
1 #CNT-CAP-AT-D  (N5)
1 #CNT-CAP-AT-TOT (N5)   /* TOTAL OF ABOVE
1 #CNT-CAP-AT-A   (N5)
1 #CNT-CAP-AT-NCO (N5)
1 #CNT-CAP-AT-OFF (N5)
*
1 #CNT-AT-M-S-FU0  (N5)
1 #CNT-AT-M-S-FU1  (N5)
1 #CNT-AT-M-S-FU2  (N5)
1 #CNT-AT-M-S-FU3  (N5)
1 #CNT-AT-M-S-FU4  (N5)
1 #CNT-AT-I-TOT  (N5)
1 #CNT-AT-P      (N5)
1 #CNT-AT-U      (N5)
1 #CNT-AT-W      (N5)
1 #CNT-AT-TOT    (N5)
1 #CNT-AT-A      (N5)
*
1 #CNT-CAP-IM-S  (N5)
1 #CNT-CAP-IM-M  (N5)
1 #CNT-CAP-IM-I  (N5)
1 #CNT-CAP-IM-C  (N5)
1 #CNT-CAP-IM-P  (N5)
1 #CNT-CAP-IM-X  (N5)
1 #CNT-CAP-IM-Y  (N5)
1 #CNT-CAP-IM-Z  (N5)
1 #CNT-CAP-IM-TOT  (N5)  /* TOT OF ABOVE
*
1 #CNT-AT-SPS      (N5)
1 #CNT-AT-CHILD    (N5)
1 #CNT-AT-GRANDCHILD (N5)
*
1 #COMMAND    (A2) INIT <'BA'>
1 #INVALID    (L)
1 #RANK-DISPL (P2)
1 #RANK-LIT   (A23)
1 #RANK-DISPLAY (A5)
1 #MIL-SERV   (A3)
1 #INTRO      (L)
1 #FOUND-AT      (L)
1 #NT-NOT-FOUND  (L)
1 #ISN    (P8)
1 #YEARS    (N4/10)
1 #LAST-MON (N2)
1 #DATN   (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY (N4)
  2 #DATN-MM   (N2)
  2 #DATN-DD   (N2)
*
1 #ORIGIN-TOTS (P5/10)
1 #SPS-GEN-I   (P5)
1 #MBR-I       (P5)
1 #AFB-IND      (A1)
1 #HOME-PHONE     (A16)
1 #WORK-PHONE     (A16)
1 #DISP-DATE      (A10)    /* PM8
*
1 #PROSPECTIVE-OCC (85)    /* 85 POSSIBLE USERS OF PROSP SYSTEM
  2 #PR-USER        (A3)   /* WHO INITIATED IT
  2 #PR-POLS-INIT   (N5)   /* HOW MANY HE INITIATED
  2 #PR-POLS-ISSUED (N5)   /* HOW MANY WERE ISSUED
  2 #PR-POLS-CURR   (N5)   /* HOW MANY THERE AE CURRENTLY
  2 #PR-AVAIL-1     (N5)   /* NOT YET USED
  2 #PR-AVAIL-2     (N5)   /* NOT YET USED
*
  2 #PR-PCT         (N3.6)  /* PCT ISSUED
  2 #PR-PCT-DISP    (A6)
*
1 #PR-POLS-INIT-TOT   (N5)
1 #PR-POLS-ISSUED-TOT (N5)
1 #PR-POLS-CURR-TOT   (N5)
1 #PR-PCT-TOT         (N3.6)
1 #PR-PCT-DISP-TOT    (A6)
1 #CNT-7              (P5)
1 #INT        (I2)
1 #NOTE-TEXT  (A60/10)
1 #TO-INIT    (A3)
1 #AFSC-COMBO-LTRS (P5)
1 #SSN-DISP   (A11)
1 #AMT-DISP7  (A7)
1 #AMT-DISP6  (A6)
1 #COMP-CODE  (A7)
1 #EFF-DATE   (A8)
1 #FREQ       (A12)
1 #DUE-DATE   (A10)
1 #AGE        (N2)
1 #SMOKER     (A1)
1 #SCR-SUFFIX (A3)
1 #PLAN       (A3)
1 #MPREM      (N5.2)
1 #APREM      (N6.2)
1 #FACE-AMT   (N7.2)
1 #COM-INF-1  (A9)
1 #COM-INF-2  (A34)
1 #COM-INF-3  (A78)
*
1 #CNT-2    (N5)
1 #CAP-CONV-MON-TOT(N5/12)    /* YK2
1 #CAP-CONV-MONTHLY(N5/12)    /* YK2
1 #FUP-REC       (A125)       /* DW6
1 #FUP-AT    (A6)
1 REDEFINE #FUP-AT
 2 #FUP-AT-N (N6)
1 #SEP-1     (A60)    /* To store extra unneeded data
1 #SEP-2     (A60)
1 #SEP-3     (A60)
1 #SEP-4     (A60)
1 #SEP-5     (A60)
1 #SEP-6     (A60)
1 #SEP-7     (A60)
1 #NUM-READ   (N5)
*
END-DEFINE
************************************************************************
*
IF *DEVICE NE 'BATCH'   /* KEEP THIS FIRST
  IF #CGA-SCR-SUFFIX = 'CAP'
    FIND (1) NT-V WITH ID-NUMBER = 800106
*
      MOVE *DATN TO #DATN
      MOVE  #DATN-YYYY TO #YEARS (1)
      FOR #I1 1 TO 10
        IF (#I1 LT 10) AND (#YEARS(#I1) GT 1996) AND (#YEARS (#I1) NE 0)
          COMPUTE #YEARS (#I1 + 1) = #YEARS (#I1) - 1
        END-IF
        ADD CAPS-CONV-ORIG (#I1, *) TO #ORIGIN-TOTS (#I1)
      END-FOR
*
      COMPUTE #CNT-CAP-AT-TOT = CAPS-AT-M-S + CAPS-AT-P1
        + CAPS-AT-P2 + CAPS-AT-P3 + CAPS-AT-P4 + CAPS-AT-U + CAPS-AT-W
        + CAPS-AT-D
      COMPUTE #CNT-CAP-IM-TOT = CAPS-IM-S + CAPS-IM-M + CAPS-IM-I
        + CAPS-IM-C  + CAPS-IM-P + CAPS-IM-X + CAPS-IM-Y + CAPS-IM-Z
      COMPUTE #CNT-AT-I-TOT = AT-M-S-1FU + AT-M-S-2FU + AT-M-S-3FU
        +AT-M-S-4FU  + AT-M-S-0FU
      COMPUTE #CNT-AT-TOT = #CNT-AT-I-TOT + AT-P + AT-U + AT-W
*
      FOR #I1 1 TO 12
        IF #I1 GT #DATN-MM
          COMPUTE #CAPS-CONV-BY-MONTH-YYYY (#I1) = #DATN-YYYY - 1
        ELSE
          MOVE (AD=I) TO #CONV-YYYY-CV (#I1)
          MOVE #DATN-YYYY TO #CAPS-CONV-BY-MONTH-YYYY (#I1)
        END-IF
        #CAP-CONV-MON-TOT(#I1) := CAPS-CONV-MON(#I1) +      /* YK2
          CAPS-CONV-MON-A(#I1) + CAPS-CONV-MON-GR(#I1)      /* YK2
        IF #I1 NE 1
          #CAP-CONV-MONTHLY(#I1) := CAPS-CONV-MON(#I1) +    /* YK2
            CAPS-CONV-MON-A(#I1 - 1)                        /* YK2
        ELSE
          #CAP-CONV-MONTHLY(#I1) := CAPS-CONV-MON(#I1) +    /* YK2
            CAPS-CONV-MON-A(12)                             /* YK2
        END-IF
      END-FOR
*
      MOVE 'Press ENTER for additional APPL stats' TO #TEXT
      MOVE NOTE-DATE (1) TO #DATN
*     INPUT TEXT #TEXT USING MAP 'G4125MMK'
      INPUT TEXT #TEXT USING MAP 'G4128MMK'     /* YK2
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      INPUT USING MAP 'G4126MMK'
    END-FIND
*
    IF #CGA-SCR-COMMAND ='*'
      MOVE 'MK' TO #CGA-SCR-COMMAND
    END-IF
  ELSE
    CALLNAT 'C4127SMK' #CGA-ISN #CGA-SCR-COMMAND #CGA-SCR-ID
      #CGA-SCR-SUFFIX #CGA-CLERK            /* YK1
*
    IF #CGA-SCR-COMMAND ='*'
      MOVE 'MK' TO #CGA-SCR-COMMAND
    END-IF
  END-IF
*
  FETCH 'G1000PXX'
END-IF
*
FORMAT LS=133 PS=76
WRITE TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'PROGRAM MESSAGES'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE   (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'FOLLOW-UP"S MISSED'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE   (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'APPLICATIONS PAST 30 DAYS (NO ACTION-DATE)'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=60
WRITE   (4) TITLE LEFT 'PROGRAM:' *PROGRAM 2X
  'AUTO updates based on ATHPATLB 2:00 PM label extracts'
  2X 'DATE:' *DAT4U 2X 'TIME:' *TIMX
* (5) USED FOR NEW WAMA FILE
*
FORMAT (6) LS=133 PS=76
WRITE  (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'APPL TRACKING RECORDS DELETED AND TOTALS'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (7) LS=133 PS=76
WRITE  (7) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'APPLICATIONS PENDING LONGER THAN 6 MONTHS'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ WORK 1 ONCE RECORD #JCL-PARM
MOVE #JCL-WEEKLY-MONTHLY TO #WEEKLY-MONTHLY
*
IF *TIMN LT 1200000       /* MIDNITE TILL 12 NOON (NORMAL JOB TIME)
  MOVE *DATN TO #YYYYMMDD     /* *DATN - 1 IS CONSIDERED TODAY
  SUBTRACT 1 FROM #DD
*
  IF #DD LT 1
    SUBTRACT 1 FROM #MM
    IF #MM LT 1
      ADD 12 TO #MM
      SUBTRACT 1 FROM #YYYY
    END-IF
*
    IF #MM = 02
      MOVE 28 TO #DD
    ELSE
      IF #MM = 04 OR = 06 OR = 09 OR = 11
        MOVE 30 TO #DD
      ELSE
        MOVE 31 TO #DD
      END-IF
    END-IF
  END-IF
*
  MOVE #YYYYMMDD TO #PROCESS-YYYYMMDD
ELSE                  /* NOON TILL MIDNITE (IF JOB IS RUN EARLY)
  MOVE *DATN TO #YYYYMMDD #PROCESS-YYYYMMDD             /* PM8
END-IF
*
IF #WEEKLY-MONTHLY = 'DAILY'
  RESET #NUM-READ
  READ WORK 3 #FUP-REC  /* 15 Day Follow-up File       /* DW6
    PERFORM FU-SETUP-FOR-UPDT
*
    GET AT-V #ISN
    IF AT-V.FOLLOW-UP-DATE (1) = 0 /* These are MU not PE fields
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (1) /* To indicate not yet entered
    END-IF
*
    MOVE *DATN TO AT-V.FOLLOW-UP-DATE (2) /* 15 Day Follow-up date
    UPDATE (0657)
    END TRANSACTION
*
    WRITE AT-V.ID-NUMBER '15 Day Followup Date-rcvd' AT-V.DATE-RECEIVED
        'fu-dt-2=' AT-V.FOLLOW-UP-DATE (2)
  END-WORK
*
  RESET #NUM-READ
  READ WORK 4 #FUP-REC  /* 30 Day Follow-up File                /* DW6
    PERFORM FU-SETUP-FOR-UPDT
    GET AT-V #ISN
    IF AT-V.FOLLOW-UP-DATE (1) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (1)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (2) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (2)
    END-IF
*
    MOVE *DATN TO AT-V.FOLLOW-UP-DATE (3) /* 30 Day Follow-up date
    UPDATE (0673)
    END TRANSACTION
*
    WRITE AT-V.ID-NUMBER '30 Day Followup Date-rcvd' AT-V.DATE-RECEIVED
       'fu-dt-3=' AT-V.FOLLOW-UP-DATE (3)
  END-WORK
*
  RESET #NUM-READ
  READ WORK 5 #FUP-REC  /* 45 Day Follow-up File                /* DW6
    PERFORM FU-SETUP-FOR-UPDT
    GET AT-V #ISN
*
    IF AT-V.FOLLOW-UP-DATE (1) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (1)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (2) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (2)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (3) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (3)
    END-IF
*
    MOVE *DATN TO AT-V.FOLLOW-UP-DATE (4) /* 45 Day Follow-up date
    UPDATE (0693)
    END TRANSACTION
*
    WRITE AT-V.ID-NUMBER '45 Day Followup Date-rcvd' AT-V.DATE-RECEIVED
       'fu-dt-4=' AT-V.FOLLOW-UP-DATE (4)
  END-WORK
*
  RESET #NUM-READ
  READ WORK 6 #FUP-REC  /* 60 Day Follow-up File                /* DW6
    PERFORM FU-SETUP-FOR-UPDT
    GET AT-V #ISN
*
    IF AT-V.FOLLOW-UP-DATE (1) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (1)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (2) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (2)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (3) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (3)
    END-IF
*
    IF AT-V.FOLLOW-UP-DATE (4) = 0
      MOVE 99999999 TO AT-V.FOLLOW-UP-DATE (4)
    END-IF
*
    MOVE *DATN TO AT-V.FOLLOW-UP-DATE (5) /* 60 Day Follow-up date
    UPDATE (0718)
    END TRANSACTION
*
    WRITE AT-V.ID-NUMBER '60 Day Followup Date-rcvd' AT-V.DATE-RECEIVED
        'fu-dt-5=' AT-V.FOLLOW-UP-DATE (5)
  END-WORK                                                    /* END DW6
*
  READ WORK 2 LABELS
    RESET #ISN #UPDATE
    IF   (LAB-AT = 'AT')
        AND (LAB-LBL-TYPE = 'CAP' OR = 'F30' OR = 'REG' OR = 'F45' /* PM7
        OR = 'F60' OR = 'F90' OR = 'CPC')                      /* DW3 PM8
      MOVE TRUE TO #FOUND-AT
      IF LAB-CN-ID NE 0
        FIND (1) AT-V WITH ID-NUMBER = LAB-CN-ID
          IF NO RECORDS FOUND
            RESET #FOUND-AT
            WRITE LAB-CN-ID 'no AT record found using AT-number'
          END-NOREC
          MOVE *ISN TO #ISN
        END-FIND
      ELSE
        FIND AT-V WITH SSN = LAB-SSN
          IF NO RECORDS FOUND
            RESET #FOUND-AT
            WRITE LAB-SSN 'No AT record found using SSN'
          END-NOREC
          MOVE *ISN TO #ISN
*
          IF AT-V.INTRO-FLAG = 'V'   /* AFSC REC
            ESCAPE TOP
          END-IF
        END-FIND
      END-IF
    END-IF
*
    IF NOT #FOUND-AT
      ESCAPE TOP
    END-IF
*
    GET AT-V #ISN
*
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4 AT-V.ID-NUMBER AT-V.SSN LAB-LBL-TYPE
      AT-V.ACTION-FLAG AT-V.AT-STATUS
*
    MOVE 'EDS' TO #CGA-CLERK
*
    IF   (LAB-LBL-TYPE = 'CAP' OR = 'CPC')             /* PM8
        AND (AT-V.CAP-IND = 'Y')
      IF   (LAB-ACT-FLAG     = 'M') /* APP LABLS WERE PRODUCED YESTERDAY
          AND (AT-V.AT-STATUS-1 = 'M')
        MOVE TRUE TO #UPDATE
        ADD 1 TO #CNT-APP-TO-MAIL
        MOVE 'S' TO AT-V.ACTION-FLAG
        MOVE 'SM' TO AT-V.AT-STATUS
        MOVE AT-V.USER-SOURCE TO #CGA-CLERK  /* USE ORIG USER FOR THESE
*
        MOVE 'SM' TO #AT-NOTE-STATUS
        CALLNAT 'GADDNSXX' /* ADD A STATUS NOTE
          #CGA-CLERK
          #AT-NOTE-STATUS
          ST-NOTE-DATE   (1:50)
          ST-NOTE-USER   (1:50)
          ST-NOTE-STATUS (1:50)
          ACTION-DATE
      END-IF
*
      IF   (LAB-ACT-FLAG     = 'P') /* APP RCVD LBLS FOR ALLT STRT SENT
          AND (AT-V.AT-STATUS-1 = 'P') /*              YESTERDAY
        MOVE TRUE TO #UPDATE
        ADD 1 TO #CNT-ALT-STRT-TO-MAIL
        PERFORM CREATE-PROJ-DIST-NOTE                  /* PM8
        MOVE 'P' TO AT-V.ACTION-FLAG  /* WAS C
        MOVE 'PC' TO AT-V.AT-STATUS
*
        MOVE 'PC' TO #AT-NOTE-STATUS
        CALLNAT 'GADDNSXX' /* ADD A STATUS NOTE
          #CGA-CLERK
          #AT-NOTE-STATUS
          ST-NOTE-DATE   (1:50)
          ST-NOTE-USER   (1:50)
          ST-NOTE-STATUS (1:50)
          ACTION-DATE
      END-IF
    END-IF
*
    IF   (LAB-LBL-TYPE = 'REG')
        AND (LAB-ACT-FLAG    = 'M')   /* WAS S
      IF AT-V.AT-STATUS-1 = 'M'    /* WAS S
        MOVE TRUE TO #UPDATE
        ADD 1 TO #CNT-REG-APP-TO-MAIL
        MOVE 'S' TO AT-V.ACTION-FLAG  /* WAS I
        MOVE 'SM' TO AT-V.AT-STATUS
        MOVE AT-V.USER-SOURCE TO #CGA-CLERK  /* USE ORIG USER FOR THESE
*
        MOVE 'SM' TO #AT-NOTE-STATUS
        CALLNAT 'GADDNSXX' /* ADD A STATUS NOTE
          #CGA-CLERK
          #AT-NOTE-STATUS
          ST-NOTE-DATE   (1:50)
          ST-NOTE-USER   (1:50)
          ST-NOTE-STATUS (1:50)
          ACTION-DATE
      END-IF
    END-IF
*
    IF   (LAB-LBL-TYPE = 'F30' OR = 'F45')   /* DW3/DW5/PM7
        AND (AT-V.AT-STATUS-1 = 'S')   /* WAS I
      MOVE TRUE TO #UPDATE
      IF  LAB-LBL-TYPE = 'F30' OR = 'F45'    /* PM7
        ADD 1 TO #CNT-FU-30
        MOVE *DATN TO AT-V.FOLLOW-UP-DATE (1)
      END-IF
*
      MOVE 'S6' TO #AT-NOTE-STATUS
      CALLNAT 'GADDNSXX' /* ADD A STATUS NOTE
        #CGA-CLERK
        #AT-NOTE-STATUS
        ST-NOTE-DATE   (1:50)
        ST-NOTE-USER   (1:50)
        ST-NOTE-STATUS (1:50)
        ACTION-DATE
    END-IF
*
    IF #UPDATE
      UPDATE (0777)
      END TRANSACTION
    END-IF
  END-WORK
END-IF
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
*
END-IF
MOVE #YYYYMMDD TO #1-MON-AGO
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 1 FROM #YYYY
SUBTRACT 6 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #18-MONS-AGO
*
IF #WEEKLY-MONTHLY = 'MONTHLY'
  READ WORK 1 ONCE RECORD #JCL-PARM
  MOVE #JCL-MM TO #TODAY-MM
  MOVE #JCL-DD TO #TODAY-DD
  MOVE #JCL-YYYY TO #TODAY-YYYY
*
  SUBTRACT 1 FROM #TODAY-MM
  IF #TODAY-MM LT 1
    ADD 12 TO #TODAY-MM
    SUBTRACT 1 FROM #TODAY-YYYY
  END-IF
*
  IF #TODAY-MM = 2
    MOVE 28 TO #TODAY-DD
  ELSE
    IF #TODAY-MM = 4 OR = 6 OR = 9 OR = 11
      MOVE 30 TO #TODAY-DD
    ELSE
      MOVE 31 TO #TODAY-DD
    END-IF
  END-IF
*
  MOVE #TODAY-YYYYMMDD TO #DAY-30 #DAY-45 #DAY-75 #DAY-105 #DAY-135
    #MON-13  #DAY-120 #DAY-150 #DAY-180 #DAY-240 #DAY-90 #DAY-60 #DAY-270
    #DAY-40 /* PM 03/2000 TO CHG FU1 TO 40 DAYS
ELSE
  MOVE *DATN TO  #TODAY-YYYYMMDD
  MOVE *DATN TO #DAY-30 #DAY-45 #DAY-75 #DAY-105 #DAY-135 #DAY-150
    #MON-13      #DAY-120  #DAY-180 #DAY-240 #DAY-90 #DAY-60 #DAY-270
    #DAY-40 /* PM 03/2000 TO CHG FU1 TO 40 DAYS
END-IF
WRITE 'date = ' #TODAY-YYYYMMDD
*
SUBTRACT 1 FROM #D30-MM
IF #D30-MM < 1
  ADD 12 TO #D30-MM
  SUBTRACT 1 FROM #D30-YYYY
END-IF
*
MOVE #DAY-30 TO #DAY-40
SUBTRACT 10 FROM #D40-DD
IF #D40-DD < 1
  ADD 30 TO #D40-DD
  SUBTRACT 1 FROM #D40-MM
*
  IF #D40-MM < 1
    ADD 12 TO #D40-MM
    SUBTRACT 1 FROM #D40-YYYY
  END-IF
END-IF
*
MOVE #DAY-40 TO #DAY-45
SUBTRACT 5 FROM #D45-DD
IF #D45-DD < 1
  ADD 30 TO #D45-DD
  SUBTRACT 1 FROM #D45-MM
*
  IF #D45-MM < 1
    ADD 12 TO #D45-MM
    SUBTRACT 1 FROM #D45-YYYY
  END-IF
END-IF
*
SUBTRACT 2 FROM #D60-MM
IF #D60-MM < 1
  ADD 12 TO #D60-MM
  SUBTRACT 1 FROM #D60-YYYY
END-IF
*
SUBTRACT 2 FROM #D75-MM
IF #D75-MM < 1
  ADD 12 TO #D75-MM
  SUBTRACT 1 FROM #D75-YYYY
END-IF
*
SUBTRACT 15 FROM #D75-DD
IF #D75-DD < 1
  ADD 30 TO #D75-DD
  SUBTRACT 1 FROM #D75-MM
*
  IF #D75-MM < 1
    ADD 12 TO #D75-MM
    SUBTRACT 1 FROM #D75-YYYY
  END-IF
END-IF
*
SUBTRACT 3 FROM #D90-MM
IF #D90-MM < 1
  ADD 12 TO #D90-MM
  SUBTRACT 1 FROM #D90-YYYY
END-IF
*
SUBTRACT 3 FROM #D105-MM
IF #D105-MM < 1
  ADD 12 TO #D105-MM
  SUBTRACT 1 FROM #D105-YYYY
END-IF
*
SUBTRACT 15 FROM #D105-DD
IF #D105-DD < 1
  ADD 30 TO #D105-DD
  SUBTRACT 1 FROM #D105-MM
*
  IF #D105-MM < 1
    ADD 12 TO #D105-MM
    SUBTRACT 1 FROM #D105-YYYY
  END-IF
END-IF
*
SUBTRACT 4 FROM #D120-MM    /* 120
IF #D120-MM < 1
  ADD 12 TO #D120-MM
  SUBTRACT 1 FROM #D120-YYYY
END-IF
*
SUBTRACT 4 FROM #D135-MM    /* 135
IF #D135-MM < 1
  ADD 12 TO #D135-MM
  SUBTRACT 1 FROM #D135-YYYY
END-IF
*
SUBTRACT 15 FROM #D135-DD
IF #D135-DD < 1
  ADD 30 TO #D135-DD
  SUBTRACT 1 FROM #D135-MM
*
  IF #D135-MM < 1
    ADD 12 TO #D135-MM
    SUBTRACT 1 FROM #D135-YYYY
  END-IF
END-IF
*
SUBTRACT 5 FROM #D150-MM    /* 150
IF #D150-MM < 1
  ADD 12 TO #D150-MM
  SUBTRACT 1 FROM #D150-YYYY
END-IF
*
SUBTRACT 6 FROM #D180-MM    /* 180
IF #D180-MM < 1
  ADD 12 TO #D180-MM
  SUBTRACT 1 FROM #D180-YYYY
END-IF
*
SUBTRACT 8 FROM #D240-MM    /* 240
IF #D240-MM < 1
  ADD 12 TO #D240-MM
  SUBTRACT 1 FROM #D240-YYYY
END-IF
*
SUBTRACT 9 FROM #D270-MM    /* 270
IF #D270-MM < 1
  ADD 12 TO #D270-MM
  SUBTRACT 1 FROM #D270-YYYY
END-IF
*
SUBTRACT 1 FROM #M13-YYYY
SUBTRACT 1 FROM #M13-MM
IF #M13-MM < 1
  ADD 12 TO #M13-MM
  SUBTRACT 1 FROM #M13-YYYY
END-IF
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 4 FROM #MM
IF #MM LT 0
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #4-MONS-AGO
*
MOVE TRUE TO #INTRO
READ IM-V PHYSICAL
  ADD 1 TO #IM-RECS-READ
  MOVE IM-V.MIL-SERVICE TO #MIL-SERV
  RESET #UPDATE
*
  IF IM-V.CAP-IND = 'Y'
    ADD 1 TO #CNT-CAP-IM-TOT
    DECIDE ON EVERY VALUE IM-V.ACTION-FLAG
      VALUE 'S'  ADD 1 TO #CNT-CAP-IM-S
      VALUE 'M'  ADD 1 TO #CNT-CAP-IM-M
      VALUE 'N'  ADD 1 TO #CNT-CAP-IM-M
      VALUE 'I'  ADD 1 TO #CNT-CAP-IM-I
      VALUE 'C'  ADD 1 TO #CNT-CAP-IM-C
      VALUE 'P'  ADD 1 TO #CNT-CAP-IM-P
      VALUE 'U'  ADD 1 TO #CNT-CAP-IM-P
      VALUE 'X'  ADD 1 TO #CNT-CAP-IM-X
      VALUE 'Y'  ADD 1 TO #CNT-CAP-IM-Y
      VALUE 'Z'  ADD 1 TO #CNT-CAP-IM-Z
      NONE WRITE IM-V.ID-NUMBER 'invalid CAP action flag'
          IM-V.ACTION-FLAG
    END-DECIDE
  END-IF
*
  IF #UPDATE
    UPDATE (1059)
  END-IF
*
  END TRANSACTION
END-READ
*
RESET #INTRO
*
READ AT-V PHYSICAL
  ADD 1 TO #AT-RECS-READ
*
  IF AT-V.INTRO-FLAG = 'V'  /* Bypass AFSC Records
    ESCAPE TOP
  END-IF
*
  MOVE AT-V.MIL-SERVICE TO #MIL-SERV
  RESET #UPDATE
*
  IF USER-SOURCE NE ' '
    FOR #I1 1 TO 85
      IF (#PR-USER (#I1) = ' ') OR (USER-SOURCE = #PR-USER (#I1))
        MOVE USER-SOURCE TO #PR-USER (#I1)
*
        IF AT-V.AT-STATUS-1 NE 'A'
          ADD 1 TO #PR-POLS-CURR (#I1)
        END-IF
*
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-IF
*
  IF   (INTRO-FLAG = 'S' OR = 'K' OR = 'G')
      AND (AT-V.AT-STATUS-1 NE 'A')
    IF INTRO-FLAG = 'S'
      ADD 1 TO #CNT-AT-SPS
    END-IF
*
    IF INTRO-FLAG = 'K'
      ADD 1 TO #CNT-AT-CHILD
    END-IF
*
    IF INTRO-FLAG = 'G'
      ADD 1 TO #CNT-AT-GRANDCHILD
    END-IF
  END-IF
*
  IF AT-V.CAP-IND = 'Y'
    IF AT-V.AT-STATUS-1 = 'M' OR = 'S'
      ADD 1 TO #CNT-CAP-AT-M-S
    ELSE
      IF AT-V.AT-STATUS = 'PR' OR = 'PM' OR = 'PO' OR = 'PX'
        ADD 1 TO #CNT-CAP-AT-P1
      ELSE
        IF AT-V.AT-STATUS = 'P1' OR = 'P2' OR = 'P3'
          ADD 1 TO #CNT-CAP-AT-P2
        ELSE
          IF AT-V.AT-STATUS = 'PI' OR = 'PC' OR = 'PP'
            ADD 1 TO #CNT-CAP-AT-P3
          ELSE
            IF AT-V.AT-STATUS = 'PY'
              ADD 1 TO #CNT-CAP-AT-P4
            ELSE
              DECIDE ON FIRST VALUE AT-V.AT-STATUS-1
                VALUE 'U'  ADD 1 TO #CNT-CAP-AT-U
                VALUE 'W'  ADD 1 TO #CNT-CAP-AT-W
                VALUE 'D'  ADD 1 TO #CNT-CAP-AT-D
                VALUE 'A'  ADD 1 TO #CNT-CAP-AT-A
                NONE WRITE AT-V.ID-NUMBER AT-V.SSN
                    'invalid CAP AT-STATUS'
                    AT-V.ACTION-FLAG AT-V.AT-STATUS
              END-DECIDE
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
    IF AT-V.AT-STATUS-1 NE 'A'
      RESET #RANK-DISPL #RANK-LIT #RANK-DISPLAY                   /* PM1
      CALLNAT 'GRANKSXX' #COMMAND AT-V.RANK #MIL-SERV
        #INVALID #RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
      IF #INVALID OR (#RANK-DISPL = 99)
        RESET #RANK-DISPL
      END-IF
*
      IF (#RANK-DISPL GE 20) AND (#RANK-DISPL LE 30)
        ADD 1 TO #CNT-CAP-AT-NCO
      ELSE
        ADD 1 TO #CNT-CAP-AT-OFF
      END-IF
    END-IF
  ELSE
    IF AT-V.AT-STATUS-1 = 'M' OR = 'S'  /* WAS I AND S
      IF AT-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
        ADD 1 TO #SPS-GEN-I
      ELSE
        IF NOT AT-V.MEMBER-CN = ' ' OR = '000000'   /* A NEW MBR
          ADD 1 TO #MBR-I
        ELSE
          IF NOT AT-V.FOLLOW-UP-DATE (1) = 0 OR = 99999999   /* DW5
            ADD 1 TO #CNT-AT-M-S-FU1
          ELSE
            ADD 1 TO #CNT-AT-M-S-FU0
          END-IF
        END-IF
      END-IF
    ELSE
      IF AT-V.AT-STATUS-1 = 'P'
        ADD 1 TO #CNT-AT-P
      ELSE
        IF AT-V.AT-STATUS-1 = 'U'
          ADD 1 TO #CNT-AT-U
        ELSE
          IF AT-V.AT-STATUS-1 = 'W' OR = 'D'
            ADD 1 TO #CNT-AT-W
          ELSE
            IF AT-V.AT-STATUS-1 = 'A'
              ADD 1 TO #CNT-AT-A
            ELSE
              WRITE AT-V.ID-NUMBER AT-V.SSN 'invalid action flag'
                AT-V.ACTION-FLAG AT-V.AT-STATUS
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  RESET #IS-A-Q
  IF   (AT-V.AT-STATUS-1 = 'M' OR = 'S') /* WAS I & S
      AND (AT-V.CAP-IND NE 'Y')
    FIND (1) SA-V WITH SOURCE-CODE = AT-V.APPL-SOURCE
      IF NO RECORDS FOUND
        RESET SOURCE-CODE-GROUP
      END-NOREC
*
      IF SOURCE-CODE-GROUP = 'Q'
        MOVE TRUE TO #IS-A-Q
      END-IF
    END-FIND
  END-IF
*
  IF   (AT-V.AT-STATUS-1 = 'M' OR = 'S') /* WAS I & S
*  AND (AT-V.APPL-SOURCE NE 'MBRSRV')
      AND (AT-V.CAP-IND NE  'Y')
*  AND (NOT #IS-A-Q)
      AND (NOT INTRO-FLAG = 'S' OR = 'K' OR = 'G')
      AND (#WEEKLY-MONTHLY = 'MONTHLY') /* PM CHG TO DISP ONCE A MONTH
*  AND (AT-V.MEMBER-CN = ' ' OR = '000000') /* PM CHG - NO DISP MEMBER
    IF (AT-V.FOLLOW-UP-DATE (1) =  0)
        AND (AT-V.ACTION-DATE LT #DAY-40) /* PM CHG FROM 45 DAYS TO 40 DAYS
      MOVE 'MISSED "FU" 1' TO #MISSED
*     DISPLAY (2) AT-V.ID-NUMBER AT-V.SSN AT-V.NAME AT-V.ACTION-FLAG
*      AT-V.ACTION-DATE AT-V.FOLLOW-UP-DATE (1) AT-V.FOLLOW-UP-DATE (2)
*                        AT-V.FOLLOW-UP-DATE (3)    #MISSED
    END-IF
  END-IF
*
  IF   (AT-V.AT-STATUS-1 = 'P' OR = 'U')
      AND (AT-V.CAP-IND NE 'Y')
      AND (AT-V.DATE-RECEIVED LT #DAY-180)    /* 6 MONTHS OLD
      AND (#WEEKLY-MONTHLY = 'MONTHLY') /* PM CHG TO DISP ONCE A MONTH
    ADD 1 TO #CNT-7
    DISPLAY (7) #CNT-7 AT-V.ID-NUMBER AT-V.SSN
      AT-V.NAME AT-V.ACTION-FLAG AT-V.AT-STATUS AT-V.DATE-RECEIVED
  END-IF
*
  IF   (AT-V.AT-STATUS-1 = 'P' OR = 'U')
      AND (AT-V.CAP-IND NE 'Y')
      AND (AT-V.ACTION-DATE = 0)
      AND (AT-V.DATE-RECEIVED LT #DAY-30)     /* 30 DAYS PAST DATE-RECEICED
      AND (AT-V.DATE-RECEIVED GE 19900101)
      AND (#WEEKLY-MONTHLY = 'MONTHLY') /* PM CHG TO DISP ONCE A MONTH
    DISPLAY (3) AT-V.ID-NUMBER AT-V.SSN
      AT-V.NAME AT-V.ACTION-FLAG AT-V.AT-STATUS AT-V.DATE-RECEIVED
  END-IF
*
  IF AT-STATUS = MASK ('U')
    IF   (ACTION-DATE LE #1-MON-AGO) /* 1 MONTH SINCE LAST STATUS CHG
        AND (NEXT-ACTION-DATE = 0)   /* NOT ALREADY REPORTED
      MOVE *DATN TO NEXT-ACTION-DATE   /* TO IND IT HAS BEEN NX'd
      MOVE TRUE TO #UPDATE
      IF #UX GE 32   /* WAS 40 BUT CAN ONLY HANDLE 34
        ADD 1 TO #UX
        WRITE AT-V.ID-NUMBER 'exceeds 32 1-mon aged "U" status changes'
      ELSE
        WRITE AT-V.ID-NUMBER AT-V.SSN 'do 1 -mon aged LCC message'
        ADD 1 TO #UX
        MOVE AT-V.ID-NUMBER TO #U-ATS    (#UX)
        MOVE AT-V.AT-STATUS TO #U-STATUS (#UX)
      END-IF
    END-IF
  END-IF
*       BELOW WAS REMOVED IMMEDIATELY
* FOR #I1 50 TO 1 STEP -1    /* ERIC LANE's status PI nx
*   IF ST-NOTE-USER (#I1) NE ' '
*     ESCAPE BOTTOM
*   END-IF
* END-FOR
*
* IF #I1 = 0
*   MOVE 1 TO #I1
* END-IF
*
* IF   (ST-NOTE-STATUS (#I1) = 'PI')
*  AND (ST-NOTE-DATE (#I1) = #PROCESS-YYYYMMDD)
*   IF #PX GE 32
*     ADD 1 TO #PX
*     WRITE AT-V.ID-NUMBER #PX 'exceeds 32 PI status changes'
*   ELSE
*     WRITE AT-V.ID-NUMBER AT-V.SSN 'Changed to Status PI'
*     ADD 1 TO #PX
*     MOVE AT-V.ID-NUMBER TO #P-ATS (#PX)
*   END-IF
* END-IF
*
  IF #UPDATE
    UPDATE (1091)
  END-IF
*
  END TRANSACTION
END-READ
*
IF #UX NE 0   /* LCC's aged underwriting at'S
  RESET #NOTE-TEXT (*)
*
  IF #UX GT 32
    COMPRESS 'Only 32 are shown below. There are' #UX 'Call EDS'
      INTO #NOTE-TEXT (1)
  ELSE
    MOVE 'These AT"s are 1 month past the last status chg to "U"'
      TO #NOTE-TEXT (1)
  END-IF
*
  MOVE 2 TO #I1    /* 2 TO 5 NOTE LINE
  RESET #I2        /* 1 TO 8 ON EACH NOTE LINE
  FOR #UX 1 TO 32  /* 1 TO 32 AT's to nx
    IF #U-ATS (#UX) = 0
      ESCAPE BOTTOM
    END-IF
*
    ADD 1 TO #I2
    IF #I2 GT 8
      ADD 1 TO #I1
      MOVE 1 TO #I2
    END-IF
*
    COMPRESS #NOTE-TEXT (#I1) #U-ATS (#UX)                 /* ALLOW 32
      INTO #NOTE-TEXT (#I1)
  END-FOR
*
  MOVE 'VXT' TO #TO-INIT    /* vt was 'LCC'
  MOVE 'EDS' TO #CGA-CLERK
  MOVE '*NX' TO #CGA-SCR-SUFFIX
  FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
  END TRANSACTION
  DISPLAY #NOTE-TEXT (*)
END-IF
*
* IF #PX NE 0   /* ERIC LANES STATUS PI CHANGES
* RESET #NOTE-TEXT (*)
*
* IF #PX GT 32
*   COMPRESS 'Only 32 are shown below. There are' #PX 'Call EDS'
*     INTO #NOTE-TEXT (1)
* ELSE
*   MOVE 'These CAP "AT"s were changed to "PI" yesterday'
*     TO #NOTE-TEXT (1)
* END-IF
*
* MOVE 2 TO #I1    /* 2 TO 5 NOTE LINE
* RESET #I2        /* 1 TO 8 ON EACH NOTE LINE
* FOR #PX 1 TO 32  /* 1 TO 32 AT's to nx
*   IF #P-ATS (#PX) = 0
*     ESCAPE BOTTOM
*   END-IF
*
*   ADD 1 TO #I2
*   IF #I2 GT 8
*     ADD 1 TO #I1
*     MOVE 1 TO #I2
*   END-IF
*
*   COMPRESS #NOTE-TEXT (#I1) #P-ATS (#PX)                 /* ALLOW 32
*      INTO #NOTE-TEXT (#I1)
* END-FOR
*
* MOVE 'EL ' TO #TO-INIT
* MOVE 'EDS' TO #CGA-CLERK
* MOVE '*NX' TO #CGA-SCR-SUFFIX
* FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
* END TRANSACTION
* DISPLAY #NOTE-TEXT (*)
* END-IF
*
WRITE (6) 'TOT-AT-RECS-READ    = ' #AT-RECS-READ
WRITE (6) 'TOT-AT-RECS-UPDT    = ' #AT-RECS-UPDT
WRITE (6) 'TOT-IM-RECS-READ    = ' #IM-RECS-READ
WRITE (6) 'TOT-IM-RECS-UPDT    = ' #IM-RECS-UPDT
WRITE (6) 'TOT AT "A"s DELETED = ' #CNT-A-DELETED
WRITE (6) 'TOT AT "P"s DELETED = ' #CNT-P-DELETED
WRITE (6) 'TOT AT "I"s DELETED = ' #CNT-I-DELETED
WRITE (6) 'TOT AT "R/D"S DELETED=' #CNT-RD-DELETED
WRITE (6) 'TOT AT"s      DELETED=' #CNT-DELETED
*
WRITE (1) 'TOT CAP M  to S   (send app or LBL= ' #CNT-APP-TO-MAIL
WRITE (1) 'TOT CAP PI to PC  (send allt strt)= ' #CNT-ALT-STRT-TO-MAIL
WRITE (1) 'TOT REG S FU-45   (send follow-up)= ' #CNT-FU-30  /* PM7
WRITE (1) 'TOT REG S FU-60   (send follow-up)= ' #CNT-FU-60  /* DW3
WRITE (1) 'TOT REG S FU-90   (send follow-up)= ' #CNT-FU-90  /* DW3
WRITE (1) 'TOT REG M to S    (send app or LBL= ' #CNT-REG-APP-TO-MAIL
*
WRITE (6) 'TOT AT CAP action M/S       = ' #CNT-CAP-AT-M-S
WRITE (6) 'TOT AT CAP ACTION PR/PM/PO/PX = ' #CNT-CAP-AT-P1
WRITE (6) 'TOT AT CAP ACTION P1/P2/P3    = ' #CNT-CAP-AT-P2
WRITE (6) 'TOT AT CAP action PI/PC/PP    = ' #CNT-CAP-AT-P3
WRITE (6) 'TOT AT CAP action PY          = ' #CNT-CAP-AT-P4
WRITE (6) 'TOT AT CAP action u    = ' #CNT-CAP-AT-U
WRITE (6) 'TOT AT CAP action w    = ' #CNT-CAP-AT-W
WRITE (6) 'TOT AT CAP action d    = ' #CNT-CAP-AT-D
WRITE (6) 'TOT AT CAP action A    = ' #CNT-CAP-AT-A
*
WRITE (6) 'TOT REG INQ 0 fu"s  = ' #CNT-AT-M-S-FU0
WRITE (6) 'TOT REG INQ 1 fu"s  = ' #CNT-AT-M-S-FU1
WRITE (6) 'TOT REG INQ 2 fu"s  = ' #CNT-AT-M-S-FU2
WRITE (6) 'TOT REG INQ 3 fu"s  = ' #CNT-AT-M-S-FU3
WRITE (6) 'TOT REG INQ 4 fu"s  = ' #CNT-AT-M-S-FU4
WRITE (6) 'TOT sps-gen INQ     = ' #SPS-GEN-I
WRITE (6) 'TOT INQ for members = ' #MBR-I
WRITE (6) 'TOT REG P           = ' #CNT-AT-P
WRITE (6) 'TOT REG U           = ' #CNT-AT-U
WRITE (6) 'TOT REG W           = ' #CNT-AT-W
*
WRITE (6) 'TOT IM CAP action M    = ' #CNT-CAP-IM-M
WRITE (6) 'TOT IM CAP action C    = ' #CNT-CAP-IM-C
WRITE (6) 'TOT IM CAP action P    = ' #CNT-CAP-IM-P
WRITE (6) 'TOT IM CAP action X    = ' #CNT-CAP-IM-X
WRITE (6) 'TOT IM CAP action Y    = ' #CNT-CAP-IM-Y
WRITE (6) 'TOT IM CAP action Z    = ' #CNT-CAP-IM-Z
WRITE (6) 'TOT folup-45  lbls(FU1)= ' #CNT-FOLUP-30      /* PM7
WRITE (6) 'TOT folup-60  lbls(FU3)= ' #CNT-FOLUP-60
WRITE (6) 'TOT folup-90  lbls(FU4)= ' #CNT-FOLUP-90
WRITE (6) 'TOT FOLUP-150 LTRS(FU5)= ' #CNT-FOLUP-150
WRITE (6) /// 'AFSC combo MAILINGS= ' #AFSC-COMBO-LTRS
*
IF #AFSC-COMBO-LTRS NE 0
  WRITE '  afsc combo MAILINGS  = ' #AFSC-COMBO-LTRS
END-IF
*
RESET #NT-NOT-FOUND
FIND (1) NT-V WITH ID-NUMBER = 800106
  IF NO RECORDS FOUND
    MOVE TRUE TO #NT-NOT-FOUND
    ESCAPE BOTTOM
  END-NOREC
  MOVE *ISN TO #ISN
END-FIND
*
IF #NT-NOT-FOUND
  WRITE 'NX rec 800106 not found'
  MOVE 800106 TO NT-V.ID-NUMBER
  RESET CAPS-CONV-ORIG (*, *)
ELSE
  GET NT-V #ISN
END-IF
*
MOVE *DATN           TO NOTE-DATE (1)
MOVE #CNT-CAP-AT-M-S TO CAPS-AT-M-S
MOVE #CNT-CAP-AT-P1  TO CAPS-AT-P1
MOVE #CNT-CAP-AT-P2  TO CAPS-AT-P2
MOVE #CNT-CAP-AT-P3  TO CAPS-AT-P3
MOVE #CNT-CAP-AT-P4  TO CAPS-AT-P4
MOVE #CNT-CAP-AT-U   TO CAPS-AT-U
MOVE #CNT-CAP-AT-W   TO CAPS-AT-W
MOVE #CNT-CAP-AT-D   TO CAPS-AT-D
MOVE #CNT-CAP-AT-A   TO CAPS-AT-A
MOVE #CNT-CAP-AT-NCO TO CAPS-AT-NCO
MOVE #CNT-CAP-AT-OFF TO CAPS-AT-OFF
*
MOVE #CNT-AT-M-S-FU0   TO AT-M-S-0FU
MOVE #CNT-AT-M-S-FU1   TO AT-M-S-1FU
MOVE #CNT-AT-M-S-FU2   TO AT-M-S-2FU
MOVE #CNT-AT-M-S-FU3   TO AT-M-S-3FU
MOVE #CNT-AT-M-S-FU4   TO AT-M-S-4FU
MOVE #CNT-AT-P       TO AT-P
MOVE #CNT-AT-U       TO AT-U
MOVE #CNT-AT-W       TO AT-W
MOVE #CNT-AT-A       TO AT-A
*
MOVE #CNT-CAP-IM-S   TO CAPS-IM-S
MOVE #CNT-CAP-IM-M   TO CAPS-IM-M
MOVE #CNT-CAP-IM-I   TO CAPS-IM-I
MOVE #CNT-CAP-IM-C   TO CAPS-IM-C
MOVE #CNT-CAP-IM-P   TO CAPS-IM-P
MOVE #CNT-CAP-IM-X   TO CAPS-IM-X
MOVE #CNT-CAP-IM-Y   TO CAPS-IM-Y
MOVE #CNT-CAP-IM-Z   TO CAPS-IM-Z
*
MOVE #CNT-AT-SPS     TO AT-SPS
MOVE #CNT-AT-CHILD   TO AT-CHILD
MOVE #CNT-AT-GRANDCHILD   TO AT-GRANDCHILD
*
IF #WEEKLY-MONTHLY = 'MONTHLY'
  RESET CAPS-CONV-MON (#JCL-MM) CAPS-CONV-MON-A (#JCL-MM) CAPS-CONV-MON-GR (#JCL-MM)
  IF #JCL-MM = 01
    FOR #I1 10 TO 2 STEP -1   /* SHIFT YEARS OVER 1
      MOVE CAPS-CONV-ORIG (#I1 - 1, *) TO CAPS-CONV-ORIG (#I1 , *)
    END-FOR
    RESET CAPS-CONV-ORIG (1, *)  /* CLEAR NEXT YEAR
  END-IF
*
  MOVE #TODAY-YYYYMMDD TO #DAY-120 /* PRINT 90-DAY OLD CAP "C/M" LBLS
  SUBTRACT 3 FROM #D120-MM
  IF #D120-MM LT 1
    ADD 12 TO #D120-MM
    SUBTRACT 1 FROM #D120-YYYY
  END-IF
  RESET #INTRO
END-IF
*
IF #NT-NOT-FOUND
  STORE NT-V
ELSE
  UPDATE (1448)
END-IF
*
RESET #NT-NOT-FOUND #ISN
FIND (1) NT-V WITH ID-NUMBER = 800108  /* PROSPECTIVE POLS FOR MBRSERV
  IF NO RECORDS FOUND
    MOVE TRUE TO #NT-NOT-FOUND
    ESCAPE BOTTOM
  END-NOREC
  MOVE *ISN TO #ISN
END-FIND
*
IF #NT-NOT-FOUND
  WRITE 'NX rec 800108 not found'
  MOVE 800108 TO NT-V.ID-NUMBER
  RESET PROSPECTIVE-OCC (*)
ELSE
  GET NT-V #ISN
END-IF
*
MOVE *DATN TO NOTE-DATE (1)
*
FOR #I1 1 TO 85
  IF #PR-USER (#I1) = ' '
    ESCAPE BOTTOM
  END-IF
*
  FOR #I2 1 TO 85
    IF #PR-USER (#I1) = PR-USER (#I2)
      MOVE #PR-POLS-CURR (#I1) TO PR-POLS-CURR (#I2)
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FOR
DISPLAY #PR-USER (1:25) #PR-POLS-CURR (1:25) PR-USER (1:25)
  PR-POLS-INIT (1:25) PR-POLS-ISSUED (1:25) PR-POLS-CURR (1:25)
*
IF #NT-NOT-FOUND
  STORE NT-V
ELSE
  UPDATE (1525)
END-IF
*
END TRANSACTION
*
************************************************************************
DEFINE SUBROUTINE FU-SETUP-FOR-UPDT     /* DW6
************************************************************************
ADD 1 TO #NUM-READ
IF #NUM-READ = 1     /* Bypass Label record
  ESCAPE TOP
END-IF
*
SEPARATE #FUP-REC INTO #FUP-AT #SEP-1 #SEP-2 #SEP-3 #SEP-4 #SEP-5 #SEP-6 #SEP-7
  WITH DELIMITER #TAB /* Assumes AT number is in 1st 6 positions
MOVE TRUE TO #FOUND-AT
FIND (1) AT-V WITH ID-NUMBER = #FUP-AT-N
  IF NO RECORDS FOUND
    RESET #FOUND-AT
    WRITE #FUP-AT-N 'no AT record found using AT-number'
  END-NOREC
  MOVE *ISN TO #ISN
END-FIND
*
IF NOT #FOUND-AT
  ESCAPE TOP
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CREATE-PROJ-DIST-NOTE                          /* PM8
************************************************************************
*
IF AT-V.CAP-TYPE = 'C'                                           /* PM9
  #YYYYMMDD := #PROCESS-YYYYMMDD
*
  IF #DD LE 20
    ADD +1 TO #MM
  ELSE
    ADD +2 TO #MM
  END-IF
*
  IF #MM GT 12
    SUBTRACT 12 FROM #MM
    ADD +1 TO #YYYY
  END-IF
*
  #DD := 01
*
  FOR #I1 1 TO 40
    IF NOTES (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I1 GT 40 /* delete oldest note
    FOR #I2 2 TO 40
      MOVE NOTES (#I2) TO NOTES (#I2 - 1)
    END-FOR
*
    #I1 := 40
  END-IF
*
  COMPRESS #MM-A '/' #DD-A '/' #YYYY-A INTO #DISP-DATE LEAVING NO
  COMPRESS #PROCESS-MM #PROCESS-DD #PROCESS-YYYY INTO AT-V.NOTES (#I1) WITH '/'
  COMPRESS '*EDS-' AT-V.NOTES (#I1) 'Loan projected to distribute on'
           INTO AT-V.NOTES (#I1) LEAVING NO
  COMPRESS AT-V.NOTES (#I1) #DISP-DATE INTO AT-V.NOTES (#I1)
END-IF                                                           /* PM9
*
END-SUBROUTINE
*
END
