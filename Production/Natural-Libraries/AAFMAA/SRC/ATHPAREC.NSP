* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPAREC - ALLOTMENT RECONCILIATION
*
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
* * * * * * *
* M O D I F I C A T I O N   L O G
* * * * * * *
* DRW 03/02/2009 DW1
* RSE 09/21/2009 RE1  Change PROCESS-IND logic for Unification
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* * * * * * * *
*
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 SSN            (N9)
  2 REDEFINE SSN
    3 SSN-A        (A9)
  2 PROCESS-IND
  2 ALOT-AMT       (P5.2)
  2 NAME           (A25)
  2 MILITARY-SERVICE  (A3)
  2 REDEFINE MILITARY-SERVICE
    3 MIL-SERV-1       (A1)
    3 MIL-SERV-2       (A2)
  2 MILITARY-STATUS   (A1)
  2 C*NOTIFY-MSG
  2 NOTIFY-MSG    (A25/1:5)
* * *  * NOTE FILE * * * *   * * * *  *
1 A-NOTES-VIEW VIEW OF A-NOTES
  2 C*NOTE-TABLE
  2 NOTE-TEXT  (500)                    /* DW1
  2 REDEFINE NOTE-TEXT
   3 NOTE-TEXT-OCC  (60)
    4 NOTE-TEXT-CN   (A3)
    4 NOTE-TEXT-FILL (A6)
    4 NOTE-TEXT-3    (A3)
    4 NOTE-TEXT-REST (A28)
* * * *   A L L O T M E N T   F I L E * * * * * * * * * * * * *
1 #ALLOTMENT-FILE
  2 #AR-SSN         (N9)
  2 REDEFINE #AR-SSN
     3 #AR-SSN-A   (A9)
  2 #AR-ALLOT       (P5.2)
  2 #AR-NAME       (A22)
*
1 #HOLD-NOTE-MSG   (A25)
1 REDEFINE #HOLD-NOTE-MSG
  2 #HOLD-NOT4       (A4)
  2 #HOLD-REST       (A21)
1 REDEFINE #HOLD-NOTE-MSG
  2 #HOLD-NOT5       (A5)
  2 #HOLD-REST-2     (A20)
1 #COUNTERS
  2 #FOUND          (P5)
  2 #NOT-FOUND      (P5)
  2 #RECS-IN        (P5)
  2 #RECS-OUT       (P5)
*
1 #STAT-END      (L)
1 #ALLOT-END      (L)
1 #EOR-SW         (L)
1 #SERVICE-FLAG   (A1)
1 WS-ABEND-CODE    (I4)
1 #HOLD-ID-COMB     (A8)
1 REDEFINE #HOLD-ID-COMB
  2 #HOLD-CN      (A2)
  2 #HOLD-ID-NBR   (A6)
1 #HOLD-NAME       (A25)
1 REDEFINE #HOLD-NAME
 2 #HOLD-NAME-X (A1/25)
1 #HOLD-MIL-STAT   (A1)
1 #HOLD-MIL-ID     (A3)
1 #HOLD-SSN        (A9)
1 #HOLD-MST-ALLOT    (A7)
1 #HOLD-MST-ALLOT-N  (N5.2)
1 #HOLD-AR-ALLOT     (A7)
1 #HOLD-AR-ALLOT-N   (N5.2)
1 #HOLD-NOTIFY-MSG   (A25)
*
1 #HOLD-LAST-NAME    (A25)
1 REDEFINE #HOLD-LAST-NAME
 2 #HOLD-LAST-NAME-X (A1/25)
1 #PRIOR-LAST-NAME (A25)
*
1 #I1    (I2)
1 #I2    (I2)
1 #EXCESS-ALOT      (P5.2)
1 #TOT-AAFMAA-ALOT-NO-FIN-CTR (P5.2)
1 #TBL-SSN          (A09/10)
1 #TBL-NAME         (A25/10)
1 #TBL-ID-COMB      (A08/10)
1 #TBL-MIL-STAT     (A01/10)
1 #TBL-MST-ALLOT    (A07/10)
1 #TBL-AR-ALLOT     (A07/10)
1 #TBL-NOTIFY-MSG   (A25/10)
*
1 #SUB               (I2)
1 #WRITE-EXCP        (L)
1 #WRITE-MISMATCH    (L)
1 #LAST-AR-SSN       (N9)
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
FORMAT     PS=68
           LS=133
FORMAT(3)  PS=68
           LS=133
FORMAT(4) PS=68
          LS=133
FORMAT(5) PS=68
          LS=133
WRITE (3) TITLE LEFT
    40T 'AAFMAA'
    91T 'PAGE' 96T *PAGE-NUMBER (3) /
    40T 'ALLOTMENT RECONCILIATION - MISMATCH REPORT' 91T *DATN //
    16T 'SSN' 31T 'NAME' 57T 'ID' 63T 'STATUS'
    72T 'MIL' 79T 'AAFMAA' 88T 'FIN CTR' 104T 'NOTIFY MSG'/
    72T 'ID'  80T 'ALLOT'  89T 'ALLOT'//
WRITE (4) TITLE LEFT
    40T 'AAFMAA'
    91T 'PAGE' 96T *PAGE-NUMBER (4) /
    39T 'ALLOTMENT RECONCILIATION - EXCEPTION REPORT' 91T *DATN //
    16T 'SSN' 31T 'NAME' 57T 'ID' 63T 'STATUS'
    73T 'AAFMAA' 82T 'FIN CTR' 98T 'NOTIFY MSG'/
    74T 'ALLOT'  83T 'ALLOT'//
WRITE (5) TITLE LEFT
    40T 'AAFMAA'
    91T 'PAGE' 96T *PAGE-NUMBER (5) /
    39T 'ALLOTMENT RECONCILIATION - MATCHED ALLOTMENTS' 91T *DATN //
    16T 'SSN' 31T 'NAME' 57T 'ID' 63T 'STATUS'
    73T 'AAFMAA' 82T 'FIN CTR' 98T 'NOTIFY MSG'/
    74T 'ALLOT'  83T 'ALLOT'//
*
INPUT #SERVICE-FLAG
*
IF #SERVICE-FLAG = 'N'    /* INDICATES NOT (RUN)
  WRITE (3) 'ALLOTMENT RECONCILIATION NOT RUN'
  WRITE     'ALLOTMENT RECONCILIATION NOT RUN'
  STOP
END-IF
*
IF #SERVICE-FLAG = 'A' OR = 'F' OR = 'B'
  IGNORE
ELSE
  WRITE (3) 'ALLOTMENT RECONCILIATION NOT RUN INVALID PARM'
  WRITE     'ALLOTMENT RECONCILIATION NOT RUN INVALID PARM'
END-IF
*
PERFORM READ-ALLOT-REC
*
READ ST-V BY SSN
*  IF ST-V.PROCESS-IND = 'S' OR = 'C'              RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>) OR     /* RE1
     ST-V.PROCESS-IND = 'C'                     /* RE1
    ESCAPE TOP
  END-IF
*
  PERFORM PROCESS-RECD
END-ALL
*
SORT BY #HOLD-NAME
 USING #WRITE-EXCP #HOLD-SSN #HOLD-ID-COMB #HOLD-MIL-STAT #HOLD-MIL-ID
  #WRITE-MISMATCH  #HOLD-MST-ALLOT #HOLD-AR-ALLOT #HOLD-NOTIFY-MSG
                   #HOLD-AR-ALLOT-N #HOLD-MST-ALLOT-N
*
  IF #WRITE-MISMATCH
    PERFORM WRITE-LINE
  END-IF
*
  IF #WRITE-EXCP
    PERFORM CHECK-DUPL-REC
  END-IF
END-SORT
*
ASSIGN #STAT-END = TRUE
PERFORM PROCESS-RECD
*
IF #WRITE-EXCP
  PERFORM CHECK-DUPL-REC
END-IF
*
IF #WRITE-MISMATCH
  PERFORM WRITE-LINE
END-IF
*
WRITE 'TOTAL RECS READ     = ' #RECS-IN
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-ALLOT-REC
* * * * * * * * * * * * * * * * *
*
READ WORK 1 ONCE RECORD #ALLOTMENT-FILE
  AT END
    ASSIGN #ALLOT-END = TRUE
    ESCAPE ROUTINE
  END-ENDFILE
*
  IF #AR-SSN = #LAST-AR-SSN
    WRITE (3) '*** DUPLICATE FINANCE REC FOR SSN' #AR-SSN
  END-IF
  MOVE #AR-SSN TO #LAST-AR-SSN
ADD 1 TO #RECS-IN
*
END-SUBROUTINE /* (0199)
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-RECD
* * * * * * * * * * * * * * *
*
RESET #WRITE-EXCP #WRITE-MISMATCH
IF #STAT-END
  IF #ALLOT-END
    ESCAPE ROUTINE
  ELSE
    REPEAT UNTIL #ALLOT-END
      PERFORM WRITE-TAPE
    END-REPEAT
  END-IF
ELSE
  IF #ALLOT-END
    PERFORM WRITE-MASTER
  ELSE
    ASSIGN #EOR-SW = FALSE
    REPEAT UNTIL #EOR-SW
      PERFORM MATCH-ALOT
    END-REPEAT
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-TAPE
* * * * * * * * * * * * * *
*
MOVE '        ' TO #HOLD-ID-COMB
MOVE ' ' TO #HOLD-NOTIFY-MSG
MOVE #AR-NAME TO #HOLD-NAME
MOVE '?' TO #HOLD-MIL-STAT
MOVE '?' TO #HOLD-MIL-ID
MOVE #AR-SSN-A TO #HOLD-SSN
RESET            #HOLD-MST-ALLOT
RESET            #HOLD-MST-ALLOT-N
MOVE EDITED #AR-ALLOT (EM=ZZZ9.99) TO #HOLD-AR-ALLOT
MOVE #AR-ALLOT TO #HOLD-AR-ALLOT-N
PERFORM WRITE-LINE
PERFORM READ-ALLOT-REC
RESET #HOLD-CN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-MASTER
* * * * * * * * * * * * * * *
*
IF  ((#SERVICE-FLAG = 'F') AND (MIL-SERV-1 NE 'F'))
 OR ((#SERVICE-FLAG = 'A') AND (MIL-SERV-1 NE 'A'))
 OR (ST-V.ALOT-AMT  = 0)
  ESCAPE ROUTINE
END-IF
*
MOVE ID-NUMBER-A TO #HOLD-ID-NBR
MOVE 'CN' TO #HOLD-CN
MOVE ST-V.NAME TO #HOLD-NAME
MOVE ST-V.MILITARY-STATUS TO #HOLD-MIL-STAT
MOVE ST-V.MILITARY-SERVICE TO #HOLD-MIL-ID
MOVE SSN-A TO #HOLD-SSN
MOVE EDITED ST-V.ALOT-AMT (EM=ZZZ9.99) TO #HOLD-MST-ALLOT
MOVE ST-V.ALOT-AMT TO #HOLD-MST-ALLOT-N
RESET            #HOLD-AR-ALLOT
RESET            #HOLD-AR-ALLOT-N
PERFORM TEST-NOTIFY
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MATCH-ALOT
* * * * * * * * * * * * * * * *
*
IF ST-V.SSN = #AR-SSN
  IF ST-V.ALOT-AMT = #AR-ALLOT
    PERFORM READ-ALLOT-REC
    ASSIGN #EOR-SW = TRUE
  ELSE
    MOVE 'CN' TO #HOLD-CN
    MOVE ID-NUMBER-A TO #HOLD-ID-NBR
    MOVE ST-V.NAME TO #HOLD-NAME
    MOVE SSN-A TO #HOLD-SSN
    MOVE ST-V.MILITARY-STATUS TO #HOLD-MIL-STAT
    MOVE ST-V.MILITARY-SERVICE TO #HOLD-MIL-ID
    MOVE EDITED ST-V.ALOT-AMT (EM=ZZZ9.99) TO #HOLD-MST-ALLOT
    MOVE ST-V.ALOT-AMT TO #HOLD-MST-ALLOT-N
    MOVE EDITED #AR-ALLOT (EM=ZZZ9.99) TO #HOLD-AR-ALLOT
    MOVE #AR-ALLOT TO #HOLD-AR-ALLOT-N
    PERFORM TEST-NOTIFY
    PERFORM READ-ALLOT-REC
    ASSIGN #EOR-SW = TRUE
  END-IF
ELSE
  IF ST-V.SSN < #AR-SSN OR #ALLOT-END
    PERFORM WRITE-MASTER
    ASSIGN #EOR-SW = TRUE
  ELSE
    PERFORM WRITE-TAPE
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE TEST-NOTIFY
* * * * * * * * * * * * * * *
*
RESET #HOLD-NOTIFY-MSG
FIND (1) A-NOTES-VIEW WITH ID-NUMBER = ST-V.ID-NUMBER
  IF NO RECORDS FOUND
    PERFORM WRITE-LINE
    ESCAPE ROUTINE
  END-NOREC
*
  FOR #SUB 1 TO C*NOTE-TABLE
    IF  NOTE-TEXT(#SUB) = MASK ('*PAYS')
     OR NOTE-TEXT(#SUB) = MASK ('*PAID')
     OR NOTE-TEXT(#SUB) = MASK ('*PD BY')
     OR NOTE-TEXT(#SUB) = MASK ('*PREMIUMS PD BY')
     OR NOTE-TEXT(#SUB) = MASK ('*PREMS PD BY')
     OR ((NOTE-TEXT-CN (#SUB) = '*CN') AND (NOTE-TEXT-3 (#SUB) = 'PAY'))
      MOVE NOTE-TEXT(#SUB) TO #HOLD-NOTIFY-MSG
      MOVE TRUE TO #WRITE-EXCP
      ESCAPE ROUTINE
    END-IF
  END-FOR
*
  MOVE NOTE-TEXT(1) TO #HOLD-NOTIFY-MSG
  MOVE TRUE TO #WRITE-MISMATCH
END-FIND
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-LINE
* * * * * * * * * * * * * * *
*
WRITE (3) / 13T #HOLD-SSN 26T #HOLD-NAME 54T #HOLD-ID-COMB
        65T #HOLD-MIL-STAT 72T #HOLD-MIL-ID 77T #HOLD-MST-ALLOT
        87T #HOLD-AR-ALLOT 100T #HOLD-NOTIFY-MSG
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-DUPL-REC
* * * * * * * * * * * * * * * * *
*
RESET #HOLD-LAST-NAME
FOR #I1 1 TO 25
  IF #HOLD-NAME-X (#I1) = ','
    ESCAPE BOTTOM
  END-IF
*
  MOVE #HOLD-NAME-X (#I1) TO #HOLD-LAST-NAME-X (#I1)
END-FOR
*
* DISPLAY #HOLD-LAST-NAME #PRIOR-LAST-NAME 'AR-AMT' #HOLD-AR-ALLOT-N
*   'MST-AMT' #HOLD-MST-ALLOT-N #I2 #EXCESS-ALOT
*  #TOT-AAFMAA-ALOT-NO-FIN-CTR
*
 IF  (#PRIOR-LAST-NAME NE ' ')
 AND (#HOLD-LAST-NAME NE #PRIOR-LAST-NAME)
  IF #EXCESS-ALOT NE #TOT-AAFMAA-ALOT-NO-FIN-CTR
    FOR #I1 1 TO #I2
      WRITE (4) / 13T #TBL-SSN (#I1)
            26T #TBL-NAME (#I1) 54T #TBL-ID-COMB (#I1)
            65T #TBL-MIL-STAT (#I1) 71T #TBL-MST-ALLOT (#I1)
            81T #TBL-AR-ALLOT (#I1) 94T #TBL-NOTIFY-MSG (#I1)
    END-FOR
  ELSE
    FOR #I1 1 TO #I2
      WRITE (5) / 13T #TBL-SSN (#I1)
            26T #TBL-NAME (#I1) 54T #TBL-ID-COMB (#I1)
            65T #TBL-MIL-STAT (#I1) 71T #TBL-MST-ALLOT (#I1)
            81T #TBL-AR-ALLOT (#I1) 94T #TBL-NOTIFY-MSG (#I1)
    END-FOR
  END-IF
*
  RESET #EXCESS-ALOT #TOT-AAFMAA-ALOT-NO-FIN-CTR
  RESET #I2
  RESET #TBL-SSN (*)
  RESET #TBL-NAME (*)
  RESET #TBL-ID-COMB (*)
  RESET #TBL-MIL-STAT (*)
  RESET #TBL-MST-ALLOT (*)
  RESET #TBL-AR-ALLOT  (*)
  RESET #TBL-NOTIFY-MSG (*)
END-IF
*
MOVE #HOLD-LAST-NAME TO #PRIOR-LAST-NAME
ADD 1 TO #I2
MOVE #HOLD-SSN        TO #TBL-SSN (#I2)
MOVE #HOLD-NAME       TO #TBL-NAME (#I2)
MOVE #HOLD-ID-COMB    TO #TBL-ID-COMB (#I2)
MOVE #HOLD-MIL-STAT   TO #TBL-MIL-STAT (#I2)
MOVE #HOLD-MST-ALLOT  TO #TBL-MST-ALLOT (#I2)
MOVE #HOLD-AR-ALLOT   TO #TBL-AR-ALLOT  (#I2)
MOVE #HOLD-NOTIFY-MSG TO #TBL-NOTIFY-MSG (#I2)
*
IF #HOLD-AR-ALLOT-N NE 0
  COMPUTE #EXCESS-ALOT = #HOLD-AR-ALLOT-N - #HOLD-MST-ALLOT-N
ELSE
  ADD #HOLD-MST-ALLOT-N TO #TOT-AAFMAA-ALOT-NO-FIN-CTR
END-IF
*
END-SUBROUTINE
*
END
