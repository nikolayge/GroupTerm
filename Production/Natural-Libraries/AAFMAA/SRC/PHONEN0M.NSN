* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*********************
* PROGRAM  :  PHONEN0M
* FUNCTION : Returns all phone #s and their types
* 10/06/2010 vxt
* 11/22/2010 bz add phone-type-code and optional to parm.
* bz  12/09/2010 bz2 Change data type from N to A
**********************
DEFINE DATA PARAMETER
1 #CONTACT-ID   (N8)
1 #PHONE     (A26/10)
1 #PHONE-TYPE (A16/10)
1 #PHONE-TYPE-CODE (A1/10)   OPTIONAL      /* bz1
*
LOCAL
*
1 PHONE VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 COnTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3   (A3)   /* bz2
    3 DIAL-NUMBER-4   (A4)   /* bz2
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
  2 BEST-TIME-TO-CALL (A1)
  2 INVALID-IND (A1)
  2 PHONE-SOURCE (A1)
  2 START-DATE (N8)
  2 END-DATE (N8)
  2 REMARKS (A70/1:1)
  2 LAST-DATE-UPD (N8)
  2 LAST-TIME-UPD (N7)
  2 LAST-USER-UPD (A8)
*
1 #I          (N2)
1 #J          (N2)
*
END-DEFINE
*
If #contact-id = 0
escape routine
end-if
*
FIND PHONE WITH PHONE.CONTACT-ID = #CONTACT-ID
  IF NO RECORD FOUND
      #PHONE (1) := ' '        /*  bz1
      ESCAPE ROUTINE           /*  bz1
  END-NOREC
*
  ADD 1 TO #I
*
  DECIDE ON FIRST VALUE OF PHONE.PHONE-TYPE-CODE
    VALUE 'B'
      MOVE 'Business' TO #PHONE-TYPE(#I)
    VALUE 'A'
      MOVE 'Business Fax' TO #PHONE-TYPE (#I)
    VALUE 'C'
      MOVE 'Cell' TO #PHONE-TYPE(#I)
    VALUE 'H'
      MOVE 'Home' TO #PHONE-TYPE (#I)
    VALUE 'F'
      MOVE 'Home Fax' TO #PHONE-TYPE(#I)
    VALUE 'M'
      MOVE 'Mobile/Car Phone' TO #PHONE-TYPE (#I)
    VALUE 'O'
      MOVE 'Other' TO #PHONE-TYPE(#I)
    VALUE 'X'
      MOVE 'Other-fax' TO #PHONE-TYPE (#I)
    VALUE 'P'
      MOVE 'Pager' TO #PHONE-TYPE (#I)
    ANY  VALUE
       IF #PHONE-TYPE-CODE SPECIFIED
          #PHONE-TYPE-CODE (#I) := PHONE-TYPE-CODE      /*  bz1
       END-IF
    NONE IGNORE
  END-DECIDE
*
  IF PHONE.TYPE = 'D'      /* domestic phones
    COMPRESS '(' PHONE.AREA-CODE ')' PHONE.DIAL-NUMBER-3 '-' PHONE.DIAL-NUMBER-4 INTO #PHONE (#I)
      LEAVING NO
  ELSE
    IF PHONE.TYPE = 'I'   /* international
      COMPRESS '(' PHONE.INTL-ACCESS-CODE ')' PHONE.INTL-DIAL-NUMBER INTO #PHONE (#I)
    END-IF
  END-IF
*
END-FIND
END
