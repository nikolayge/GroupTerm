* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GPYSTSXX
*    DOES THE PAY STATUS EDIT AND CONVERTS BOTH WAYS
*    PAY-STATUS (X) ON ADABAS FILE
*
*     12 = UNPD - 1ST PLACEMENT FEE NOT PAID/PREM PMT METHOD NOT APPLIED
*     20 = FPWT - 1ST PLACEMENT FEE NOT PAID/PREM PMT METHOD APPLIED
*     21 = GAWT - 1ST PLACEMENT FEE PAID/PREM PMT METHOD NOT APPLIED
*     22 = PP   - 1ST PLACEMENT FEE PAID/PREM PMT METHOD APPLIED
*     41 = PUDB - PAID UP BUT STOP ALLOTMENT NOT YEY RECEIVED
*     42 = PDUP - PAID UP
*     43 = PULT - PAID UP USING LONG TERM CARE SETTLEMENT OPTION
*     44 = ETI  - EXTENDED TERM INSURANCE
*     44 = ETIM - EXT. TERM INS. (ONL ETI THIS MONTH- ONLY ON CI SCREEN)
*     45 = RPU  - REDUCED PAID UP
*     47 = PUSP - PAID UP WITH A SINGLE PREMIUM AT START OF POLICY
*     48 = PUAP - PAID UP WITH AN ADDITIONAL PREMIUM DURING POLICY LIFE
*     51 = DTH  - DEATH      ?
*     61 = CSV  - CASH VALUE ?
*     62 = CONV - CONVERTED  ?
*     71 = LAPE - LAPSED     ?
*     72 = EX96 - EXPIRED AT AGE 96 ?
*     73 = NH   - Nursing Home
********************************************************************************
*            MODIFICATION LOG                                                  *
* USER   DATE      TAG REASON                                                  *
* NGG    20130104  NG1 Add new pay status Nursing Home NH                      *
* vxt   02/15/2018 vt1 /Ticket HD-6299/ Add new pay status - PODB              *
********************************************************************************
*
DEFINE DATA
PARAMETER
1 #PAY-STATUS-XX   (A2)
1 #PAY-STATUS-XXXX (A4)
1 #INVALID         (L)
1 #ERR-MSG         (A60)
*
LOCAL
1 #PAY-STAT-TABLE
  2 #T-12    (A7) INIT <'12-UNPD'>
  2 #T-20    (A7) INIT <'20-FPWT'>
  2 #T-21    (A7) INIT <'21-GAWT'>
  2 #T-22    (A7) INIT <'22-PP  '>
  2 #T-41    (A7) INIT <'41-PUDB'>
  2 #T-42    (A7) INIT <'42-PDUP'>
  2 #T-43    (A7) INIT <'43-PULT'>
  2 #T-44    (A7) INIT <'44-ETI '>
  2 #T-44-M  (A7) INIT <'44-ETIM'>
  2 #T-45    (A7) INIT <'45-RPU '>
  2 #T-46    (a7) init <'46-PODB'>        /* vt1  
  2 #T-47    (A7) INIT <'47-PUSP'>
  2 #T-48    (A7) INIT <'48-PUAP'>
  2 #T-51    (A7) INIT <'51-DTH '>
  2 #T-61    (A7) INIT <'61-CSV '>
  2 #T-62    (A7) INIT <'62-CONV'>
  2 #T-71    (A7) INIT <'71-LAPE'>
  2 #T-72    (A7) INIT <'72-EX96'>
  2 #T-73    (A7) INIT <'73-NH'>         /* NG1
1 REDEFINE #PAY-STAT-TABLE
  2 #PAY-STAT-OCC            (19)        /* vt1  was 18
    3 #PAY-STAT-XX      (A2)
    3 #F1               (A1)
    3 #PAY-STAT-XXXX    (A4)
*
1 #I1       (I2)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FOR #I1 1 TO 19                             /* NG1    /* vt1  was 18
  IF #PAY-STATUS-XX = '  '
    IF #PAY-STATUS-XXXX = #PAY-STAT-XXXX (#I1)
      MOVE #PAY-STAT-XX (#I1) TO #PAY-STATUS-XX
      MOVE FALSE TO #INVALID
      ESCAPE ROUTINE
    END-IF
  ELSE
    IF #PAY-STATUS-XX = #PAY-STAT-XX (#I1)
      MOVE #PAY-STAT-XXXX (#I1) TO #PAY-STATUS-XXXX
      MOVE FALSE TO #INVALID
      ESCAPE ROUTINE
    END-IF
  END-IF
END-FOR /*(0073)
*
MOVE TRUE TO #INVALID
*
IF #PAY-STATUS-XX = '  '
  MOVE 'MUST BE UNPD/FPWT/GAWT/PP/PDUP/PULT/ETI/RPU/PODB/PUSP/PUAP'         /* vt1
    TO #ERR-MSG
ELSE
  MOVE 'PAY STATUS MUST BE 12/20/21/22/41/42/43/44/45/46/47/OR 48'          /* vt1
    TO #ERR-MSG
END-IF
*
END
