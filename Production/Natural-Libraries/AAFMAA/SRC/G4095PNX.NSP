* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G4095PNX
*     PRINT EDS TASKS REPORT FROM "NX" FILE
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING G4090LNX
LOCAL
*
1 #HOLD-DATN           (N8)
1 #HOLD-TIMN           (N7)
*
1 #NOTE-NUM       (N6)
1 #SCR-MSG        (A60)
1 #SCR-LINES      (A78/22)
1 REDEFINE #SCR-LINES
 2 #SCR-LINES-TAB   (22)
  3 #SCR-LINES-1ST-POS    (A1)
  3 #SCR-LINES-REST       (A77)
*
1 #SPLIT-LINE     (A78)
1 REDEFINE #SPLIT-LINE
 2 #FST-60        (A60)
 2 #LST-18        (A18)
*
1 #I1         (I2)
1 #I2         (I2)
1 #ISC        (I2)
1 #IAST       (I2)
*
1 #DATN         (N8)
1 REDEFINE #DATN
 2 #DATN-CC     (A2)
 2 #DATN-YY     (A2)
 2 #DATN-MM     (A2)
 2 #DATN-DD     (A2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
 2 #TIMN-HH     (A2)
 2 REDEFINE #TIMN-HH
  3 #TIMN-HH-N  (N2)
 2 #TIMN-MM     (A2)
 2 #TIMN-SS     (A2)
 2 #TIMN-T      (A1)
*
END-DEFINE
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
FORMAT (2) LS=80 PS=84
WRITE  (2) TITLE  / '**** STATUS OF EDS TASKS FOR AAFMAA ****'
                  /      'PGM:' *PROGRAM
                  60T *DATU *TIMX
*
READ NT-V BY ID-NUMBER FROM 990000       /* COMPLETED TASKS
  MOVE NT-V.ID-NUMBER TO #NOTE-NUM
  PERFORM BUILD-SCREEN
*
  FOR #ISC 1 TO 22
    IF  (#SCR-LINES (#ISC) = ' ')
     OR ((#ISC = 1 OR = 2) AND (#NOTE-NUM NE 990001)) /* SCN HEADER LINE
      ESCAPE TOP
    ELSE
      WRITE (2) #SCR-LINES (#ISC)
    END-IF
  END-FOR
*
END-READ
WRITE (2) ' '
*
READ NT-V BY ID-NUMBER FROM 980000     /* PENDING TASKS
  MOVE NT-V.ID-NUMBER TO #NOTE-NUM
  PERFORM BUILD-SCREEN
*
  IF ID-NUMBER GE 990000    /* BEGINNING OF COMPLETED TASKS
    ESCAPE BOTTOM
  END-IF
*
  FOR #ISC 1 TO 22
    IF  (#SCR-LINES (#ISC) = ' ')
     OR ((#ISC = 1 OR = 2) AND (#NOTE-NUM NE 980001)) /* SCN HEADER LINE
      ESCAPE TOP
    ELSE
      WRITE (2) #SCR-LINES (#ISC)
    END-IF
  END-FOR
*
END-READ
*
* * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-SCREEN
* * * * * * * * * * * * * * * * * * * * * * *
*
RESET #SCR-LINES (*)
MOVE 31 TO #I1               /* BUCKET OF 18 SECTION
MOVE 1  TO #I2               /* INDEX IN THAT BUCKET
*
FOR #ISC 1 TO 22
  IF #I2 GT 3
    ADD 1 TO #I1
    MOVE 1 TO #I2
  END-IF
*
  MOVE NT-V.NOTE-TEXT (#ISC)     TO #FST-60
  MOVE NT-V.NOTE-18   (#I1, #I2) TO #LST-18
  MOVE #SPLIT-LINE TO #SCR-LINES (#ISC)
*
  ADD 1 TO #I2
END-FOR
*
END-SUBROUTINE
*
*
END
