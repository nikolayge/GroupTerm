* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************
* PGM-ID: C230XPCI
**************************************************************
* YAK  03/15/2010  YK1 Add BIL-PAYER-FLAG to the process 
* YAK  09/24/2012  YK2 Add RENEW-FLAG to the process (Annual Renewal Term Plan project)  
**************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
LOCAL USING GONERLXX
LOCAL
1 #MEC-IND   (A9)
1 REDEFINE #MEC-IND
 2 #MEC-IND-X (A1/9)
*
1 #SCR-MEC-IND   (A9)
1 REDEFINE #SCR-MEC-IND
 2 #SCR-MEC-IND-X (A1/9)
*
1 #WYYYYMMDD
 2 #WYYYY    (N4)
 2 #WMM      (N2)
 2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
 2 #WYYYYMMDD-N (N8)
*
1 #WMMDDYYYY
 2 #WMM      (N2)
 2 #WDD      (N2)
 2 #WYYYY    (N4)
1 REDEFINE #WMMDDYYYY
 2 #WMMDDYYYY-A (A8)
*
1 #WMMYYYY
 2 #WMM      (N2)
 2 #WYYYY    (N4)
1 REDEFINE #WMMYYYY
 2 #WMMYYYY-A (A6)
*
1 #INVALID         (L)
1 #FORCED-PRORATION (L)
1 #ERR-MSG         (A60)
1 #WORK-FREQ         (P2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX
*
MOVE #CGA-MF-ORIG-PARM-1 TO #MEC-IND
*
RESET TR-REG-V
      #CI-CGA-STORAGE-AREA
      #SCR-CV
      #SCR-HEADER-CV
      #SCR-LST-ACCT-DATE
      #FILE-MATCHING-INDX (*)
      #SCR-TRAN (*)
      #SCR-PLN  (*)
      #SCR-AGE  (*)
      #SCR-EFF-DTE-CV (*)
      #SCR-EFF-DTE (*)
      #SCR-EFF-DTE-YYYYMMDD (*)
      #SCR-PAID-THRU (*)
      #SCR-PAID-THRU-YYYYMMDD (*)
      #SCR-BILL-TO-CV (*)
      #SCR-BILL-TO (*)
      #SCR-BILL-TO-YYYYMMDD (*)
      #SCR-PAID-UP (*)
      #SCR-PAID-UP-YYYYMMDD (*)
      #SCR-STATUS (*)
      #SCR-FACE-AMT-CV (*)
      #SCR-FACE-AMT (*)
      #SCR-ANN-PRM (*)
      #SCR-MODE (*)
      #SCR-MODAL-PRM (*)
      #SCR-RPU-ACV (*)
      #SCR-TRN-DT (*)
      #SCR-SMK (*)
      #SCR-SMK-CV (*)
      #SCR-ACV (*)
      #SCR-CSH-VL (*)
      #SCR-MIN-CV (*)
      #SCR-DB (*)
      #SCR-POL-OWNERSHIP (*)
      #SCR-BILL-PAYER (*)                         /* YK1
      #SCR-RENEW-FLAG (*)                         /* YK2
      #SCR-COST-BASIS (*)
      #SCR-ORIG-COST-BASIS (*)
      #SCR-MSG-LINE-1
      #SCR-MSG-LINE-2
MOVE *DATN TO #TODAY-YYYYMMDD-N
*
MOVE CI-V.DATE-ACCT-UPDATE TO #WYYYYMMDD-N
MOVE BY NAME #WYYYYMMDD TO #WMMDDYYYY
MOVE #WMMDDYYYY-A TO #SCR-LST-ACCT-DATE
*
RESET #ISC
FOR #IFL 1 TO CI-V.C*ACTIVE-SEGMENTS
*
  IF  (CI-V.STATUS (#IFL) = 'D')
   OR ((CI-V.STATUS (#IFL) = 'E' OR = 'F' OR = 'G')
         AND (#CGA-ORIG-SUFFIX = 'PUR' OR = 'PUU'))
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #ISC
  MOVE #IFL TO #FILE-MATCHING-INDX (#ISC)    /* SAVE MATCHING INDEX
  MOVE #MEC-IND-X     (#IFL)  TO #SCR-MEC-IND-X (#ISC)
  MOVE CI-V.PLAN      (#IFL)  TO #SCR-PLN (#ISC)
  MOVE CI-V.ISSUE-AGE (#IFL)  TO #SCR-AGE (#ISC)
*
  MOVE CI-V.ISSUE-DATE (#IFL) TO #WYYYYMMDD-N
  MOVE BY NAME #WYYYYMMDD TO #WMMDDYYYY
  MOVE #WMMDDYYYY-A TO #SCR-EFF-DTE (#ISC)
*
  IF  (CI-V.ONLINE-ACTION-GN     = '2')       /* NEW MEMBER THIS MONTH
   OR (CI-V.ONLINE-ACTION (#IFL) = '1')       /* NEW POLICY THIS MONTH
    IGNORE
  ELSE
    MOVE (AD=P) TO #SCR-EFF-DTE-CV (#ISC)
*   IF NOT #CGA-CLERK = 'DRW' OR = 'LCC'  /* partial csv's use face
*     MOVE (AD=P) TO #SCR-FACE-AMT-CV (#ISC)
*   END-IF
  END-IF
*
  MOVE CI-V.PAID-TO-DATE (#IFL) TO #WYYYYMMDD-N
  MOVE BY NAME #WYYYYMMDD TO #WMMYYYY
  MOVE #WMMYYYY-A TO #SCR-PAID-THRU (#ISC)
*
  IF CI-V.MODE (#IFL) NE 'G'
    IF CI-V.BILL-TO-DATE (#IFL) NE 0
      MOVE CI-V.BILL-TO-DATE (#IFL) TO #WYYYYMMDD-N
      MOVE BY NAME #WYYYYMMDD TO #WMMYYYY
      MOVE #WMMYYYY-A TO #SCR-BILL-TO (#ISC)
    END-IF
  ELSE
    MOVE (AD=P) TO #SCR-BILL-TO-CV (#ISC)
  END-IF
*
  MOVE CI-V.PAID-UP-DATE (#IFL) TO #WYYYYMMDD-N
  MOVE BY NAME #WYYYYMMDD TO #WMMYYYY
  MOVE #WMMYYYY-A TO #SCR-PAID-UP (#ISC)
*
  CALLNAT 'GPYSTSXX' CI-V.PAY-STATUS(#IFL) #SCR-STATUS(#ISC)
                                               #INVALID #ERR-MSG
*
  MOVE CI-V.FACE-AMOUNT   (#IFL) TO #SCR-FACE-AMT (#ISC)
  MOVE CI-V.ANNUAL-PREM   (#IFL) TO #SCR-ANN-PRM  (#ISC)
  MOVE CI-V.MODE          (#IFL) TO #SCR-MODE     (#ISC)
*
  PERFORM CALC-MODAL-PREM
*
  MOVE #TODAY-MM           TO #SCR-TRN-DT-MM (#ISC)
  MOVE #TODAY-YYYY         TO #SCR-TRN-DT-YYYY (#ISC)
  MOVE CI-V.SMOKER-FLAG   (#IFL) TO #SCR-SMK      (#ISC)
*
  IF CI-V.SMOKER-FLAG (#IFL) = 'C'
    MOVE (AD=P) TO #SCR-SMK-CV (#ISC)
  END-IF
*
  MOVE CI-V.ACC-CASH-VALUE(#IFL) TO #SCR-ACV      (#ISC)
  MOVE CI-V.CASH-VALUE    (#IFL) TO #SCR-CSH-VL   (#ISC)
  MOVE CI-V.MIN-CASH-VALUE(#IFL) TO #SCR-MIN-CV   (#ISC)
  MOVE CI-V.DEATH-BENEFIT (#IFL) TO #SCR-DB       (#ISC)
*
  MOVE CI-V.OWNERSHIP-FLAG (#IFL) TO #SCR-POL-OWNERSHIP   (#ISC)
  MOVE CI-V.BILL-PAYER-FLAG(#IFL) TO #SCR-BILL-PAYER      (#ISC)      /* YK1
  MOVE CI-V.RENEW-FLAG(#IFL)      TO #SCR-RENEW-FLAG      (#ISC)      /* YK2
  MOVE CI-V.COST-BASIS     (#IFL) TO #SCR-COST-BASIS      (#ISC)
  MOVE CI-V.ORIG-COST-BASIS(#IFL) TO #SCR-ORIG-COST-BASIS (#ISC)
END-FOR /*(0122)
*
MOVE #SCR-MEC-IND TO #CGA-MF-SCR-PARM-1
IF NOT #CGA-ORIG-SUFFIX = 'UPD' OR = 'UPO' OR = 'PUU'
  MOVE '*** WHAT IF CI CALC SCREEN ***' TO #SCR-HEADER
  MOVE (AD=I)                           TO #SCR-HEADER-CV
ELSE
  MOVE 'AAFMAA -- UPDATE INSURANCE DATA' TO #SCR-HEADER
END-IF
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-MODAL-PREM
* * * * * * * * * * * * * * * * *
*
IF #SCR-MODE (#ISC) = 'A'
  MOVE  1 TO #WORK-FREQ
ELSE
  IF #SCR-MODE (#ISC) = 'S'
    MOVE 2 TO #WORK-FREQ
  ELSE
    IF #SCR-MODE (#ISC) = 'Q'
      MOVE 4 TO #WORK-FREQ
    ELSE
      MOVE 12 TO #WORK-FREQ
    END-IF
  END-IF
END-IF
*
COMPUTE #SCR-MODAL-PRM (#ISC) = #SCR-ANN-PRM (#ISC)/ #WORK-FREQ
*
END-SUBROUTINE
END
