* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* ATHPEXSP
* Daily extract file for the Sales Portal import process
*
*******************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  06102008 n/a  Initial creation
*******************************************************
*
DEFINE DATA LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A     (A6)
  2 NAME
  2 DATE-OF-BIRTH  
  2 INTERNET-ADDR
  2 DATE-RECEIVED 
  2 USER-SOURCE
  2 ACTION-DATE
  2 LEAD-TYPE
  2 APPL-SOURCE
  2 AT-STATUS
*
1 #OUTPUT-RECORD   (A125)
1 #TAB             (A1) INIT <H'09'>
1 #READ-COUNTER    (N8)
1 #LEAD-TYPE       (A4)
1 #DOB             (N8)
1 REDEFINE #DOB 
  2 #DOB-A         (A8)
*
END-DEFINE
*
COMPRESS 'AT Number' #TAB 'Name' #TAB 'DOB' #TAB 'E-Mail ' #TAB 
         'Create Date' #TAB 'Create user' #TAB 'Source' #TAB 
         'Lead Type' #TAB 'Status Date' #TAB 'Current Status' 
                  INTO #OUTPUT-RECORD
WRITE WORK FILE 1 #OUTPUT-RECORD
READ AT-V BY ID-NUMBER
  ACCEPT IF DATE-RECEIVED GE 20070601  
  #READ-COUNTER := #READ-COUNTER + 1
  RESET #LEAD-TYPE #DOB
  #DOB := FUNCTION-DATE(<DATE-OF-BIRTH>)
  DECIDE ON FIRST VALUE LEAD-TYPE
     VALUE 'H'
        #LEAD-TYPE := 'HOT'
     VALUE 'W'
        #LEAD-TYPE := 'WARM'
     VALUE 'C'    
        #LEAD-TYPE := 'COLD'
     NONE
        IGNORE                 
  END-DECIDE 
  COMPRESS ID-NUMBER-A #TAB NAME #TAB #DOB-A #TAB
  INTERNET-ADDR #TAB DATE-RECEIVED #TAB USER-SOURCE #TAB
  APPL-SOURCE #TAB #LEAD-TYPE #TAB ACTION-DATE #TAB 
  AT-STATUS INTO #OUTPUT-RECORD
*
  WRITE WORK FILE 1 #OUTPUT-RECORD
END-READ
*
  WRITE 'Records extracted' #READ-COUNTER
END

