* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Subprogram PMDN0001 * Enter information for PortaMedic and EMSI
* bz     01/10/2013
* PM-STATUS is referenced only in PMBO0002 it is safe to use (!)
* Remove PMBP0002 (PMD Resp) and PMBP0001 (PMD Req)  from prod, Find and change Note "...PE" (!)
* 596473   
* 
DEFINE DATA 
PARAMETER
 1 PARM-APPL-TRACKING
   2 #AT-NUMBER (N6)
   2 #AT-NAME (A25)
   2 #ERROR-MESSAGE (A60)
   2 PM-CODE (A3/1:4)
   2 PM-REMARK (A130/1:4)
   2 PM-STATUS (A25)
   2 PM-VENDOR (A10) /*Name of the vendor USING PMDA0002
* 
LOCAL 
1 AT-V VIEW OF A-APPL-TRACKING
 2 NAME
 2 PM-VENDOR    (A10)
 2 PM-STATUS    (A25)
 2 C*PORTAMEDIC-REQUEST
 2 PORTAMEDIC-REQUEST(4)
   3 PM-CODE      (A3)
   3 PM-REMARK    (A130)
* 
1 #I  (N3) 
1 #I1 (I2) 
1 #POS (I4) 
* 
1 #TEXT (A78) 
1 #ERROR-CODE          (A1) INIT <'0'> 
* 
1 Screen_PMD 
  2 Vendor_EMSI (A1)  
  2 Vendor_PMDC (A1) INIT <'X'>   /*   Temporaraly
  2 Vendor_Name (A20)             /* BZ_OI-4268  
  2 Exems (10) 
    3 v_Scode (A1) 
    3 v_Sym   (A1) /* INIT <'A','B','C','D','E','F','G','H','I','J'> 
    3 v_Txt   (A30) 
    3 v_Remark  (A40) 
*
1 v_PFs  (A78) INIT <'  PF2 => Save  PF3 => Return  PF5 => Change vendor  PF8 => Verify Service'>
* 
1 GT-V VIEW OF A-GEN-TABLE 
  2  GEN-KEY             (A4) /*    D  
  2  GEN-SUB-KEY         (A3) 
  2  GEN-SECURITY-CODE   (N2) 
  2  GEN-ALPHA-VALUE     (A50) 
  2  GEN-NUMBER-VALUE    (N7.4) 
  2  GEN-DESCRIPTION     (A75) 

1 l_Test  (L)   
1 p_Resp  (A) DYNAMIC
1 p_RC    (N4)
*
END-DEFINE 
* 
* Main Processing 
* 
FIND GT-V WITH GEN-KEY = 'PMCD' 
     WHERE GEN-SUB-KEY = 'A' OR = 'B' OR = 'C' OR = 'D' OR = 'E' 
                    OR = 'F' OR = 'G' OR = 'H' OR = 'I' /* OR = 'J' 
END-ALL 
SORT BY GEN-SUB-KEY USING GEN-DESCRIPTION 
     #i := #i + 1          
     v_Sym(#i) := GEN-SUB-KEY               
     v_Txt(#i) := GEN-DESCRIPTION 
END-SORT   
* 
SET KEY PF3 NAMED 'Retrn' 
SET KEY PF1 = Help 
SET KEY PF2 NAMED 'Save'
SET KEY PF5 NAMED 'Vendor' 
SET KEY PF8 NAMED 'Verify'
SET KEY PF7 NAMED 'Load'
* 
DEFINE WINDOW CONFIRMATION-WINDOW 
      SIZE 10 * 50 
      BASE 12 / 12 
      TITLE 'Confirmation Window' 
      FRAMED ON 
* 
FORMAT IP=OFF /* KD=ON
IF  PARM-APPL-TRACKING.PM-VENDOR <> 'EMSI' PARM-APPL-TRACKING.PM-VENDOR := 'Portamedic' END-IF  /* For update
PERFORM sbr_Test_wsIAS
REPEAT
   IF PARM-APPL-TRACKING.PM-VENDOR = 'Portamedic'    /* BZ_OI-4268
        Vendor_Name := 'APPS Para Medical'           /* BZ_OI-4268
   ELSE                                              /* BZ_OI-4268
        Vendor_Name := PARM-APPL-TRACKING.PM-VENDOR  /* BZ_OI-4268
   END-IF                                            /* BZ_OI-4268
       
   INPUT (AD=O) WITH TEXT #TEXT MARK #POS
  2x 'MedExam services for' #AT-NUMBER(AD=UO) 4x #AT-NAME(AD=UO) 8x *DATX(EM=MM/DD/YYYY) //
  2x 'Vendor'  Vendor_Name(AD=IO)                                                     /* BZ_OI-4268
     36x                              F-IS-DEV(<>)(EM={Production}/'      {Test}') //
 2x v_Scode(01) (AD=UM) v_Sym(01)(AD=I) v_Txt(01) v_Remark(01) (AD=UMW)   /
 2x v_Scode(02) (AD=UM) v_Sym(02)(AD=I) v_Txt(02) v_Remark(02) (AD=UMW)   /
 2x v_Scode(03) (AD=UM) v_Sym(03)(AD=I) v_Txt(03) v_Remark(03) (AD=UMW)   / 
 2x v_Scode(04) (AD=UM) v_Sym(04)(AD=I) v_Txt(04) v_Remark(04) (AD=UMW)   /
 2x v_Scode(05) (AD=UM) v_Sym(05)(AD=I) v_Txt(05) v_Remark(05) (AD=UMW)   /
 2x v_Scode(06) (AD=UM) v_Sym(06)(AD=I) v_Txt(06) v_Remark(06) (AD=UMW)   /
 2x v_Scode(07) (AD=UM) v_Sym(07)(AD=I) v_Txt(07) v_Remark(07) (AD=UMW)   /
 2x v_Scode(08) (AD=UM) v_Sym(08)(AD=I) v_Txt(08) v_Remark(08) (AD=UMW)   /
 2x v_Scode(09) (AD=UM) v_Sym(09)(AD=I) v_Txt(09) v_Remark(09) (AD=UMW)   / 
 23/1 v_PFs (AD=I)

   IF Vendor_Name = 'APPS Para Medical'              /* BZ_OI-4268
       PARM-APPL-TRACKING.PM-VENDOR := 'Portamedic'  /* BZ_OI-4268
   ELSE                                              /* BZ_OI-4268
        PARM-APPL-TRACKING.PM-VENDOR := Vendor_Name  /* BZ_OI-4268
   END-IF                                            /* BZ_OI-4268



 #TEXT := " " 
 DECIDE ON FIRST VALUE *PF-KEY 
    VALUE 'PF2' 
      PERFORM sbr_Validate
      IF #TEXT  <> " " ESCAPE TOP END-IF    
      PERFORM sbr_Submit 
      ESCAPE BOTTOM
    VALUE 'PF3'
        #ERROR-MESSAGE := 'Canceled'
        RESET  PARM-APPL-TRACKING.PM-CODE(*) 
               PARM-APPL-TRACKING.PM-REMARK(*)
        PARM-APPL-TRACKING.PM-STATUS := 'Canceled'
        INPUT WINDOW = 'CONFIRMATION-WINDOW' 
           2x 'Request to' Vendor_Name(AD=O) / 
           2x 'has NOT been submitted'
        ESCAPE ROUTINE      
     VALUE 'PF5'
          IF PARM-APPL-TRACKING.PM-VENDOR = 'Portamedic' 
             PARM-APPL-TRACKING.PM-VENDOR := 'EMSI'
          ELSE    
             PARM-APPL-TRACKING.PM-VENDOR := 'Portamedic' 
          END-IF   
         COMPRESS 'Vendor changed to' PARM-APPL-TRACKING.PM-VENDOR  INTO #TEXT
     IF PARM-APPL-TRACKING.PM-VENDOR = 'Portamedic'                         /* BZ_OI-4268
         COMPRESS 'Vendor changed to' 'APPS Para Medical'  INTO #TEXT       /* BZ_OI-4268
     END-IF                                                                 /* BZ_OI-4268

    VALUE  'PF7'
      PERFORM sbr_Read_AT 
    VALUE  'PF8'
      PERFORM sbr_Test_wsIAS
    VALUE 'PF12' 
       SET KEY OFF 
       FETCH 'G1000PXX'
    VALUE 'ENTR'
      PERFORM sbr_Validate 
    NONE 
      IGNORE 
  END-DECIDE 
END-REPEAT 
* 
  IF p_RC = 0
      INPUT WINDOW = 'CONFIRMATION-WINDOW' 
      10x 'Request submitted.' 
      '-' (60) // 
      PARM-APPL-TRACKING.#ERROR-MESSAGE(AD=OI) //
      'Press "Enter" to continue'
  ELSE
      INPUT WINDOW = 'CONFIRMATION-WINDOW' 
       10x 'Request returns error' /
       '-' (60) //
       PARM-APPL-TRACKING.#ERROR-MESSAGE(AD=OI) //
       'Press "Enter" to continue'
  END-IF

SET WINDOW OFF 
SET KEY OFF
ESCAPE ROUTINE 
* ----------------------------------------------------------------------------
DEFINE SUBROUTINE sbr_Submit 
   RESET AT-V.PM-CODE(*) AT-V.PM-REMARK(*)
   MOVE BY NAME PARM-APPL-TRACKING TO AT-V 
   FIND(1) AT-V WITH ID-NUMBER = #AT-NUMBER 
        RESET AT-V.PM-CODE(*) AT-V.PM-REMARK(*)
        MOVE BY NAME PARM-APPL-TRACKING TO AT-V 
        UPDATE 
        CALLNAT 'PMBN00WS' #AT-NUMBER  PARM-APPL-TRACKING.PM-VENDOR p_Resp  p_RC
        IF p_RC <> 0
           RESET  PARM-APPL-TRACKING.PM-CODE(*) 
                  PARM-APPL-TRACKING.PM-REMARK(*)
                  PARM-APPL-TRACKING.#ERROR-MESSAGE := p_Resp      
           PARM-APPL-TRACKING.PM-STATUS := 'Error'      
*                PRINT 'Responce Error:' p_Resp           /* --db
        ELSE   
                 IF PARM-APPL-TRACKING.PM-VENDOR = 'EMSI'
                   PARM-APPL-TRACKING.PM-STATUS := 'Confirmed'
                   PARM-APPL-TRACKING.#ERROR-MESSAGE := 'EMSI- Order confirmed'
                 ELSE
                   PARM-APPL-TRACKING.#ERROR-MESSAGE := 'APPS- Order sent'                              
                   PARM-APPL-TRACKING.PM-STATUS := 'Sent'
                 END-IF
        END-IF
   END-FIND
* 
 IF #ERROR-CODE NE '0' 
   #TEXT := 'Error when processing request' 
 END-IF 
* 
END-SUBROUTINE 
* ================================================================= 
DEFINE SUBROUTINE sbr_Validate 
 
 RESET PARM-APPL-TRACKING.PM-CODE(*) PARM-APPL-TRACKING.PM-REMARK(*) 
 #i1 := 0 
 FOR #i = 1 TO 9  
   IF v_Scode(#i) <> ' '  
     #i1 := #i1 + 1  
     IF #i1 > 4  
         #TEXT := 'Too many service codes. Choose max 4 codes' 
         #POS := POS(v_Scode(01)) 
         ESCAPE ROUTINE 
      ELSE 
         IF v_Remark(#i) <> MASK (CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC)
            #TEXT := 'Non alphanumeric character in the Remark field'           
            #POS := POS(v_Remark(#i))
            ESCAPE ROUTINE 
         END-IF          
         PARM-APPL-TRACKING.PM-CODE(#i1) := v_Sym(#i)
         PARM-APPL-TRACKING.PM-REMARK(#i1) := v_Remark(#i)
      END-IF    
   END-IF
 END-FOR 
 
 IF #i1 =0  
   #TEXT := 'Please, select at least one Service Code' 
   #POS := POS(v_Scode(01)) 
   ESCAPE ROUTINE 
 END-IF    
END-SUBROUTINE 
* -------------------------------------------------------------------------------
DEFINE SUBROUTINE sbr_Read_AT 
  FIND(1) AT-V WITH ID-NUMBER = #AT-NUMBER
      MOVE BY NAME AT-V TO PARM-APPL-TRACKING 
      PARM-APPL-TRACKING.#AT-NAME := AT-V.NAME
      FOR #i = 1 TO C*PORTAMEDIC-REQUEST
          IF  AT-V.PM-CODE(#i) <> ' '
              EXAMINE v_Sym(*) FOR AT-V.PM-CODE(#i) INDEX #i1
              v_Scode (#i1) := 'X'
              v_Remark(#i1) :=  AT-V.PM-REMARK (#i)
          END-IF
      END-FOR     
  END-FIND
END-SUBROUTINE 
* -------------------------------------------------------------------------------
DEFINE sbr_Test_wsIAS
CALLNAT 'PMBN00WS' #AT-NUMBER  '?wsIAS    ' p_Resp  p_RC
IF p_RC = 0  
   #TEXT := 'Service is ready' 
ELSE 
   #TEXT := 'Service is not ready. Enter PF3 to exit and call MSS' 
END-IF
END-SUBROUTINE
END
