* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/* 1099 Maintenance file
*  - Travis Tinger
*
DEFINE DATA
PARAMETER
1 #REQUEST      (I4)
1 #MSG          (A) DYNAMIC
PARAMETER USING T1099PD1
LOCAL USING ERROR_L
LOCAL USING T1099L01
LOCAL
1 BLANK           (A1) CONST <' '>
1 #1099-EXISTS    (L)
1 #MESSAGE-LOG    (I4) CONST <2>
1 #ERROR-LOG      (I4) CONST <3>
1 #GET            (I4) CONST <0>
1 #ADD            (I4) CONST <1>
1 #UPDATE         (I4) CONST <2>
1 #DELETE         (I4) CONST <3>
1 #FIRST-UPDATE   (I4) CONST <4>
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  VALUE #GET    PERFORM GET-RECORD
  VALUE #FIRST-UPDATE PERFORM UPDATE-FIRST-RECORD
  NONE VALUE
    IGNORE
END-DECIDE
/*
DEFINE CHECK-EXIST
/*     -----------
RESET #1099-EXISTS
FIND T1099-V WITH T1099-V.CLAIMANT-ID = #PDA-1099.CLAIMANT-ID
    AND T1099-V.POLICY-NUMBER = #PDA-1099.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  #1099-EXISTS := TRUE
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
/*
DEFINE GET-RECORD
/*     -----------
#MSG := #NOT-EXIST      /* 'No record exist'
FIND T1099-V WITH T1099-V.CLAIMANT-ID = #PDA-1099.CLAIMANT-ID
    AND T1099-V.POLICY-NUMBER = #PDA-1099.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF T1099-V.INT-TYPE = 'A'
  MOVE BY NAME T1099-V TO #PDA-1099
  RESET #MSG
END-FIND
*
END-SUBROUTINE

DEFINE ADD-RECORD
/*     ----------
PERFORM CHECK-EXIST
IF #1099-EXISTS
/* 'No duplicate key allowed - record exist'
  #MSG := #DUPLICATE-KEY
ELSE
  MOVE BY NAME #PDA-1099 TO T1099-V
  DECIDE FOR EVERY CONDITION
    WHEN T1099-V.ADD-USER = BLANK
      T1099-V.ADD-USER      := *USER
    WHEN T1099-V.ADD-DATE = 0
      T1099-V.ADD-DATE      := *DATN
    WHEN T1099-V.ADD-TIME = 0
      T1099-V.ADD-TIME      := *TIMN
    WHEN T1099-V.LAST-UPD-USER = BLANK
      T1099-V.LAST-UPD-USER := *USER
    WHEN T1099-V.LAST-UPD-DATE = 0
      T1099-V.LAST-UPD-DATE := *DATN
    WHEN T1099-V.LAST-UPD-TIME = 0
      T1099-V.LAST-UPD-TIME := *TIMN
    WHEN NONE
      IGNORE
  END-DECIDE
  STORE T1099-V
  MOVE BY NAME T1099-V TO #PDA-1099
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
/*     -------------
#MSG := #NOTHING-UPDATE          /* 'No record exist'
FIND T1099-V WITH T1099-V.CLAIMANT-ID = #PDA-1099.CLAIMANT-ID
    AND T1099-V.POLICY-NUMBER = #PDA-1099.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #PDA-1099 TO T1099-V
  DECIDE FOR EVERY CONDITION
    WHEN T1099-V.LAST-UPD-USER = BLANK
      T1099-V.LAST-UPD-USER := *USER
    WHEN T1099-V.LAST-UPD-DATE = 0
      T1099-V.LAST-UPD-DATE := *DATN
    WHEN T1099-V.LAST-UPD-TIME = 0
      T1099-V.LAST-UPD-TIME := *TIMN
    WHEN NONE
      IGNORE
  END-DECIDE
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/*
DEFINE UPDATE-FIRST-RECORD
/*     -------------
#MSG := #NOTHING-UPDATE          /* 'No record exist'
FIND T1099-V WITH T1099-V.CLAIMANT-ID = #PDA-1099.CLAIMANT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF T1099-V.INT-TYPE = 'A'
  MOVE BY NAME #PDA-1099 TO T1099-V
  DECIDE FOR EVERY CONDITION
    WHEN T1099-V.LAST-UPD-USER = BLANK
      T1099-V.LAST-UPD-USER := *USER
    WHEN T1099-V.LAST-UPD-DATE = 0
      T1099-V.LAST-UPD-DATE := *DATN
    WHEN T1099-V.LAST-UPD-TIME = 0
      T1099-V.LAST-UPD-TIME := *TIMN
    WHEN NONE
      IGNORE
  END-DECIDE
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/*
DEFINE DELETE-RECORD
/*     -------------
#MSG := #NOTHING-DELETE      /* 'No record to delete'
FIND(1) T1099-V WITH T1099-V.CONTACT-ID = #PDA-1099.CONTACT-ID
    AND T1099-V.POLICY-NUMBER = #PDA-1099.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
*
* ON ERROR
*   COMPRESS #INTERNAL-ERROR
*     #POWN.REFERENCE-NUMBER
*     INTO #MSG
*   CALLNAT 'POSLOG' #ERROR-LOG #MSG
*   ESCAPE ROUTINE
* END-ERROR
END
