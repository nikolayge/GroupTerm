* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* bz1 12-01-2013 (OI-2395)
* bz-Name-C 07/07/2017     Fine Names. Keep all capital
DEFINE FUNCTION F-CN-INFO
  DEFINE DATA
  PARAMETER
  1 CN-INFORMATION
    2 CN-NUMBER         (N6)
    2 CONTACT-ID        (N8)    
    2 MEMBER-WIDOW-FLAG (A) DYNAMIC
    2 ACTIVE-STATUS     (A) DYNAMIC
    2 FULL-NAME         (A) DYNAMIC
    2 RANK              (A) DYNAMIC
    2 ADDRESS-LINE-1    (A) DYNAMIC
    2 ADDRESS-LINE-2    (A) DYNAMIC
    2 ADDRESS-LINE-3    (A) DYNAMIC
    2 EMAIL-ADDRESS     (A) DYNAMIC
    2 PREF-PHONE-NUMBER (A) DYNAMIC
    2 INS-MAIL-CODE     (A) DYNAMIC
*
  LOCAL USING OBJLSTAT
  LOCAL USING ADDA0001
  LOCAL
  1 AT-V VIEW OF A-APPL-TRACKING
    2 SSN
    2 INTERNET-ADDR
  1 #SSN        (N9)
  END-DEFINE
*
  #ADD-CONTACT-ID := CN-INFORMATION.CONTACT-ID
  #ADD-TYPE       := 'P'  /* Primary
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  CN-INFORMATION.ADDRESS-LINE-1    := #ADD-LINE-1
  CN-INFORMATION.ADDRESS-LINE-2    := #ADD-LINE-2
  CN-INFORMATION.ADDRESS-LINE-3    := #ADD-LINE-3
  CN-INFORMATION.PREF-PHONE-NUMBER := F-FORMATTED-PHONE(<CN-INFORMATION.CONTACT-ID,'H'>)
  CN-INFORMATION.EMAIL-ADDRESS     := F-PREF-EMAIL(<CN-INFORMATION.CONTACT-ID>)
  CN-INFORMATION.FULL-NAME := '!L'                        /* bz-Name-C
  CALLNAT 'GPRS0CNT' CN-INFORMATION.CONTACT-ID CN-INFORMATION.FULL-NAME
  CN-INFORMATION.RANK := '!A*R'                           /* bz-Name-C
  CALLNAT 'GPRS0CNT' CN-INFORMATION.CONTACT-ID CN-INFORMATION.RANK
*
FIND(1) ST-V WITH MEMBER-CONTACT-ID = CN-INFORMATION.CONTACT-ID
  CN-INFORMATION.CN-NUMBER := ST-V.ID-NUMBER
  DECIDE FOR EVERY CONDITION                             /* bz1
    WHEN PROCESS-IND = 'C' AND INTRO-FLAG NE 'A'         /* Contact
      CN-INFORMATION.MEMBER-WIDOW-FLAG := 'C'
    WHEN PROCESS-IND = 'C' AND INTRO-FLAG = 'A'          /* Pending CN
      CN-INFORMATION.MEMBER-WIDOW-FLAG := 'P'
      #SSN := ST-V.SSN
    WHEN ST-V.MEMBER-FLAG = 'Y'
      CN-INFORMATION.MEMBER-WIDOW-FLAG := 'M'
      IF NUMBER-MEMBERSHIPS = 0
        CN-INFORMATION.ACTIVE-STATUS := 'N'
      END-IF
    WHEN ST-V.WIDOW-FLAG = 'Y'
      CN-INFORMATION.MEMBER-WIDOW-FLAG := 'W'
      CN-INFORMATION.FULL-NAME      := NAME-SP
      CN-INFORMATION.RANK           := ST-V.TITLE-SP
    WHEN NONE
      CN-INFORMATION.MEMBER-WIDOW-FLAG := 'N'
  END-DECIDE
  CN-INFORMATION.INS-MAIL-CODE := ST-V.INS-MAIL-CODE
END-FIND
IF CN-INFORMATION.MEMBER-WIDOW-FLAG = 'P'  AND
    CN-INFORMATION.EMAIL-ADDRESS = ' '
  FIND AT-V WITH SSN = #SSN
    CN-INFORMATION.EMAIL-ADDRESS := AT-V.INTERNET-ADDR
  END-FIND
END-IF
END-FUNCTION
END
