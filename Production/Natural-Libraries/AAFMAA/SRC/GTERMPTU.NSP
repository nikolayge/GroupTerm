* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GTERMPTU
*  THIS PROGRAM IS THE MENU INTERFACE FOR
*     TERMINAL FILE UPDATES FOR AAFMAA (IE DAVE MEYER)
*
DEFINE DATA
* * * * * * * *
* M O D I F I C A T I O N   L O G 
*
* DRW 09/21/2009 DW1 Remove User Edit for Command - (Is already in G1000PXX)
* * * * * * *
*
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /*   WORK FIELDS FOR GONERCXX
LOCAL
1 TERM-VIEW VIEW OF A-TERMINALS
  2 TERMINAL-ID      (A8)
  2 SECURITY-CODE    (N2)
  2 CLERK-ID         (A3)
  2 COMMENTS         (A20)
  2 LST-CMND         (A2)
  2 LST-CMND-DT      (N8)
  2 INIT-USER        (A8)
  2 SSN              (N9)
  2 FUNCTIONS        (N2/10) /* YK1
  2 EMAIL            (A60)   /* YK1
  2 PHONE-EXTENSION  (A6)    /* YK2
  2 ACTIVE-DIRECTORY-ID (A50)   /* YK2
*
1 #W-SCREEN-FIELDS
  2 #W-TERM-ID         (A8)
  2 #W-DIRECTORY-ID    (A50)      /* YK2
  2 #W-OPTION          (A1)
  2 #W-SECUR           (N2)
  2 #W-COMMENTS        (A20)
  2 #W-CLERK           (A3)
  2 #W-INIT-USER       (A8)
  2 #W-SSN             (N9)
  2 #W-FUNCTIONS       (N2/10)     /* YK1
  2 #W-EMAIL           (A60)       /* YK1
  2 #W-PHONE-EXT       (A6)        /* YK2
*
1 MSG-TEXT           (A32)
1 #OPTION            (A1)
*
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
*
* IF  (#CGA-SECURITY-CODE = 99)       /* DW1
* OR (#CGA-CLERK = 'DRW')
*  IGNORE
* ELSE
*  INPUT TEXT 'USER Not Authorized to use "TU"'  MARK *#CGA-SCR-COMMAND
*      USING MAP 'G1000MXX'
*  FETCH 'G1000PXX'
* END-IF
**
REPEAT
*
  INPUT /// 27T 'TERMINAL FILE UPDATE' (I) /
    36T 'MENU' (I) ///
    26T '1. EDS NETWORK ID MAINTENANCE' (I) /
    26T '2. LIST EDS NETWORK ID"S' (I) /
    26T 'E. EXIT' (I) //
    26T 'CHOICE : ' (I) #OPTION
*
  IF #OPTION = 'E' OR = 'e' OR = '.'
    RESET #OPTION #CGA-SCR-COMMAND
    FETCH 'G1000PXX'
  ELSE
    DECIDE FOR FIRST CONDITION
      WHEN #OPTION = '1'
        PERFORM TERM-UPDATE
      WHEN #OPTION = '2'
        PERFORM TERM-LIST
      WHEN NONE
        REINPUT TEXT 'OPTION INVALID' MARK 1
    END-DECIDE
  END-IF
*
END-REPEAT /* (0062)
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE TERM-LIST
* * * * * * * * * * * * * *
*
EJECT
*
* READ TERM-VIEW LOGICAL BY TERMINAL-ID
READ TERM-VIEW LOGICAL BY CLERK-ID
  IF *LINE-COUNT LT 2
    WRITE 'NTWK-ID' (I) 3X 'SEC-CODE' (I)
      1X 'INITIALS' (I) 4X 'SSN' (I) 4X 'NAME/DESC' (I)
      9X 'Lst-Cmnd' (I) 4X 'Lst-Cmnd-Dt'
  END-IF
*
  WRITE   TERMINAL-ID 4X SECURITY-CODE 7X CLERK-ID 2X SSN (EM=999999999)
   2X COMMENTS 1X LST-CMND 8X LST-CMND-DT
*
  IF *LINE-COUNT GT 22
    EJECT
  END-IF
END-READ
*
END-SUBROUTINE   /* (0088)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE TERM-UPDATE
* * * * * * * * * * * * * * * *
*
REPEAT
*
  INPUT TEXT MSG-TEXT USING MAP 'GTERMMTU'
*
  IF #W-OPTION EQ 'Q' OR = '.'
    RESET #W-SECUR
    RESET #W-OPTION
    RESET #W-CLERK
    RESET #W-TERM-ID
    RESET #W-COMMENTS
    RESET #W-SSN
    RESET #W-FUNCTIONS(*)                             /* YK1
    RESET #W-EMAIL                                    /* YK1
    RESET #W-DIRECTORY-ID                             /* YK2
    RESET #W-PHONE-EXT                                /* YK2
    ESCAPE ROUTINE
  END-IF
*
 IF NOT #W-OPTION = 'A' OR = 'C' OR = 'D' OR = 'L'    /* YK1
   REINPUT 'OPTION MUST BE "A" "C" "D" "L" OR "Q"'    /* YK1
   MARK *#W-OPTION                                    /* YK1
 END-IF                                               /* YK1
*
 IF #W-TERM-ID = ' '                                  /* YK1
   REINPUT 'TERMINAL-ID IS REQUIRED'                  /* YK1
   MARK *#W-TERM-ID                                   /* YK1
 END-IF                                               /* YK1
*
*
  FIND (1) TERM-VIEW WITH TERMINAL-ID = #W-TERM-ID
    IF NO RECORDS FOUND
      IF #W-OPTION = 'A'
        PERFORM EDIT-FIELDS
        STORE TERM-VIEW
        END TRANSACTION
        ASSIGN MSG-TEXT =  'RECORD ADDED - THANK YOU'
        ESCAPE TOP
      ELSE
        RESET #W-SECUR
        RESET #W-CLERK
        RESET #W-COMMENTS
        REINPUT 'NETWORK-ID NOT FOUND' MARK *#W-TERM-ID
        ESCAPE TOP
      END-IF
    END-NOREC
*
    IF #W-OPTION = 'C'
      PERFORM EDIT-FIELDS
      UPDATE RECORD (0146)
      END TRANSACTION
      ASSIGN MSG-TEXT = 'RECORD UPDATED - THANK YOU'
      RESET #W-OPTION
      ESCAPE TOP
    ELSE
      END TRANSACTION
      IF #W-OPTION = 'L'
        PERFORM DISPLAY-FIELDS
        ASSIGN MSG-TEXT = 'OPTION "L" WAS SELECTED'
        ESCAPE TOP
      ELSE
        IF #W-OPTION = 'A'
          PERFORM DISPLAY-FIELDS
          REINPUT 'RECORD ALREADY EXISTS ON FILE' MARK *#W-TERM-ID
          ESCAPE TOP
        ELSE
          DELETE
          END TRANSACTION
          ASSIGN MSG-TEXT = 'RECORD DELETED - BYE BYE'
          RESET #W-OPTION
          ESCAPE TOP
        END-IF
      END-IF
    END-IF
  END-FIND   /* (0133)
END-REPEAT /* (0115)
END-SUBROUTINE    /* (0112)
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DISPLAY-FIELDS
* * * * * * * * * * * * * * * * *
*
MOVE TERM-VIEW.SECURITY-CODE   TO #W-SECUR
MOVE TERM-VIEW.CLERK-ID        TO #W-CLERK
MOVE TERM-VIEW.COMMENTS        TO #W-COMMENTS
MOVE TERM-VIEW.SSN             TO #W-SSN
MOVE TERM-VIEW.FUNCTIONS(*)    TO #W-FUNCTIONS(*)  /* YK1
MOVE TERM-VIEW.EMAIL           TO #W-EMAIL         /* YK1
MOVE TERM-VIEW.PHONE-EXTENSION TO #W-PHONE-EXT         /* YK2
MOVE TERM-VIEW.ACTIVE-DIRECTORY-ID TO #W-DIRECTORY-ID  /* YK2
*
END-SUBROUTINE /* (0192)
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE EDIT-FIELDS
* * * * * * * * * * * * * * *
*
IF #W-SECUR = 00
  REINPUT 'SECURITY-CODE Required' MARK *#W-SECUR
  ESCAPE TOP
END-IF
*
IF NOT #W-SECUR = 1 OR = 2 OR = 4 OR = 8 OR = 12   /* YK1
             OR = 16 OR = 20  OR = 97 OR = 98      /* YK1
             OR = 99 OR = 6                        /* YK1
  REINPUT 'INVALID SECURITY CODE'                  /* YK1
  MARK *#W-SECUR                                   /* YK1
END-IF                                             /* YK1
*
IF #W-CLERK = ' '
  REINPUT 'CLERK-ID Required' MARK *#W-CLERK
  ESCAPE TOP
END-IF
*
IF #W-SSN = 0
  IF #W-SECUR = 1 OR = 2 OR = 97
    IGNORE
  ELSE
    REINPUT 'Social Security Number Required' MARK *#W-SSN
    ESCAPE TOP
  END-IF
END-IF
*
MOVE #W-TERM-ID      TO TERM-VIEW.TERMINAL-ID
MOVE #W-SECUR        TO TERM-VIEW.SECURITY-CODE
MOVE #W-CLERK        TO TERM-VIEW.CLERK-ID
MOVE #W-SSN          TO TERM-VIEW.SSN
MOVE #W-COMMENTS     TO TERM-VIEW.COMMENTS
MOVE #W-FUNCTIONS(*) TO TERM-VIEW.FUNCTIONS(*)   /* YK1
MOVE #W-EMAIL        TO TERM-VIEW.EMAIL          /* YK1
MOVE #W-PHONE-EXT    TO TERM-VIEW.PHONE-EXTENSION     /* YK2
MOVE #W-DIRECTORY-ID TO TERM-VIEW.ACTIVE-DIRECTORY-ID /* YK2
*
END-SUBROUTINE /* (0206)
*
END
