* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHSWRAP - AUTOMATIC LINE WRAPPING FOR FORM LETTERS
*
* PARAMETER INFORMATION:
*    #SENTENCE - THE UNEDITED SENTENCE PASSED FROM CALLING PROGRAM
*    #NUM-SENTENCES - THE TOTAL NUMBER OF SENTENCES PASSED FROM THE
*                     CALLING PROGRAM
*    #SENTENCE-SIZE - THE SPECIFIED SENTENCE SIZE PASSED FROM THE
*                     CALLING PROGRAM
*    #PARAGRAPH-BREAK - THE SPECIFIED SENTENCE NUMBERS FOR A PARAGRAPH
*                       BREAK PASSED FROM THE CALLING PROGRAM
*    #FORMATTED-LINES - THE EDITED LINES, AT THE SPECIFIED LENGTH
*                       RETURNED TO THE CALLING PROGRAM
*    #NEW-LINES - THE NUMBER OF EDITED LINES THAT NOW EXIST RETURNED
*                 TO THE CALLING PROGRAM
*
*    NOTE:  PLEASE USE A '~' IN THE CALLING PROGRAM TO SIGNIFY THE
*           THE END OF A SENTENCE
*
*
DEFINE DATA
*
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
*
PARAMETER
*
1 #SENTENCE            (A250/100)
1 REDEFINE #SENTENCE
 2 #SENTENCE-OCCUR     (100)
  3 #LETTER-OCCUR      (250)
   4 #LETTER           (A1)
1 #NUM-SENTENCES       (I2)
1 #SENTENCE-SIZE       (I2)
1 #PARAGRAPH-BREAK     (P3/100)
1 #FORMATTED-LINES     (A250/100)
1 #NEW-LINES           (I2)
*
LOCAL
*
1 #COUNTERS
  2 #BREAK             (I2)    INIT <1>
  2 #UNLOAD            (I2)    INIT <1>
  2 #UNLOAD-2          (I2)    INIT <1>
  2 #START             (I2)    INIT <1>
  2 #FINISH            (I2)    INIT <1>
  2 #LTR               (I2)    INIT <1>
*
1 #ALL-DONE            (L)
1 #GOOD-LINE           (A100)
1 REDEFINE #GOOD-LINE
 2 #FORMATTED-LETTER    (A1/100)
1 #END-OF-SENTENCE     (A1)    INIT <'~'>
1 #BLANK               (A1)    INIT <' '>
*
END-DEFINE
************************************************************************
MOVE +1 TO #NEW-LINES
*
FOR #UNLOAD 1 TO #NUM-SENTENCES
*
  RESET #ALL-DONE
*
  MOVE +1 TO #START
  MOVE +1 TO #FINISH
*
  REPEAT UNTIL #ALL-DONE
*
    REPEAT UNTIL #LETTER (#UNLOAD,#FINISH) = #BLANK
      ADD +1 TO #FINISH
    END-REPEAT
*
    IF (#FINISH - #START) GT (#SENTENCE-SIZE - #LTR)
      MOVE #GOOD-LINE TO #FORMATTED-LINES (#NEW-LINES)
      ADD +1 TO #NEW-LINES
      MOVE +1 TO #LTR
      RESET #GOOD-LINE
    END-IF
*
    FOR #UNLOAD-2 #START TO #FINISH
*
      IF #LETTER (#UNLOAD,#UNLOAD-2) = #END-OF-SENTENCE
        IF #LTR < #SENTENCE-SIZE
          ADD +1 TO #LTR
        END-IF
        MOVE TRUE TO #ALL-DONE
        ESCAPE BOTTOM
      END-IF
*
      MOVE #LETTER (#UNLOAD,#UNLOAD-2) TO #FORMATTED-LETTER (#LTR)
      ADD +1 TO #LTR
*
    END-FOR
*
    ADD +1 TO #FINISH
    MOVE #FINISH TO #START
*
  END-REPEAT
*
  IF #UNLOAD = #PARAGRAPH-BREAK (#BREAK)
    MOVE #GOOD-LINE TO #FORMATTED-LINES (#NEW-LINES)
    ADD +2 TO #NEW-LINES
    ADD +1 TO #BREAK
    MOVE +1 TO #LTR
    RESET #GOOD-LINE
  END-IF
*
END-FOR
*
IF #GOOD-LINE NE ' '
  MOVE #GOOD-LINE TO #FORMATTED-LINES (#NEW-LINES)
END-IF
*
END
