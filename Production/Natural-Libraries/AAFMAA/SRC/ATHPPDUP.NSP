* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPPDUP    Check Paid-up Dates
*
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* DRW  08/28/2008 DW1  Gen Plus Processing
* RSE  09/30/2009 RE1  Change 600K logic for Unification
* TMT  01/13/2014 TT1  ADDED GP2 POLICY SUPPORT
* TMT  06/10/2104 TT2  CONTACT NORMALIZATION - SEX AND DOB
************************************************************************
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 MEMBER-CONTACT-ID               /* TT2
*  2 DATE-OF-BIRTH                 /* TT2
 2 X-LOAN-AMOUNT   (9)
 2 X-LOAN-AMOUNT-2 (9)
 2 STATUS      (9)
 2 PAY-STATUS  (9)
 2 SMOKER-FLAG (9)
 2 PLAN        (9)
 2 ISSUE-DATE  (9)
 2 REDEFINE ISSUE-DATE
  3 ISS-DT-OCC  (9)
   4 ISS-DT-YYYYMM  (N6)
   4 ISS-DT-DD      (N2)
 2 PAID-UP-DATE (9)
 2 REDEFINE PAID-UP-DATE
  3 PU-DT-OCC     (9)
   4 PU-DT-YYYYMM   (N6)
   4 PU-DT-DD       (N2)
 2 MONTH-ALLOC-AMT   (9)
*
1 #PLAN    (A3)
1 REDEFINE #PLAN
 2 #PLAN-A1   (A1)
 2 #PLAN-N2   (N2)
1 REDEFINE #PLAN
 2 #PLAN-FLEX-N2  (N2)
 2 REDEFINE #PLAN-FLEX-N2
  3 #PLAN-FLEX-N1 (N1)
  3 #PLAN-FLEX-P  (A1)
 2 #PLAN-FLEX-F   (A1)
*
1 #PD-UP-DT   (N8)
1 REDEFINE #PD-UP-DT
 2 #PD-UP-YYYYMM   (N6)
 2 #PD-UP-DD       (N2)
*
1 #PD-UP-DT-RT2  (N8)
1 REDEFINE #PD-UP-DT-RT2
 2 #PD-UP-YYYYMM-RT2   (N6)
 2 #PD-UP-DD-RT2       (N2)
*
1 #PD-UP-DT-RT3  (N8)
1 REDEFINE #PD-UP-DT-RT3
 2 #PD-UP-YYYYMM-RT3   (N6)
 2 #PD-UP-DD-RT3       (N2)
1 #I1    (I2)
1 #CNT-2   (N5)
1 #CNT-3   (N5)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY     (N4)
 2 #MM       (N2)
 2 #DD       (N2)
1 #UPDATE   (L)
1 DATE-OF-BIRTH           (N8)        /* TT2
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Bad Paid-Up Dates'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Paid-up Bad Paid-Up Dates'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ ST-V BY MEMBER-KEY FROM '001900'
*  IF ST-V.ID-NUMBER GE 600000               RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1  
    ESCAPE TOP                            /* RE1
*    ESCAPE BOTTOM                        /* RE1
  END-IF
*
  RESET #UPDATE
  FOR #I1 1 TO 9
    IF STATUS (#I1) NE 'D'
     OR (PLAN (#I1) = 'SO')
      ESCAPE TOP
    END-IF
*
    RESET #PD-UP-DT-RT2 #PD-UP-DT-RT3
    IF  PAY-STATUS (#I1) GT '42'
      RESET DATE-OF-BIRTH                                           /* TT2
      DATE-OF-BIRTH    := GET-DATE-OF-BIRTH(<MEMBER-CONTACT-ID>)    /* TT2
      IF ((PAY-STATUS (#I1) = '47')
        AND (PU-DT-YYYYMM (#I1) GT ISS-DT-YYYYMM (#I1)))
      OR ((PAY-STATUS (#I1) NE '47')
        AND (NOT PAY-STATUS (#I1) = '45')
        AND (PU-DT-YYYYMM (#I1) GT 200505))
         IF PAY-STATUS (#I1) = '47'
           MOVE TRUE TO #UPDATE
           MOVE ISSUE-DATE (#I1) TO #PD-UP-DT
           ADD 1 TO #CNT-2
           DISPLAY (2) #CNT-2
            'CN'        ST-V.ID-NUMBER
            'ISS-DT'    ISSUE-DATE (#I1)
            'DOB'       DATE-OF-BIRTH
            'PD-UP'     PAID-UP-DATE (#I1)
            'New/PD-UP' #PD-UP-DT
            'PAY/STAT'  PAY-STATUS (#I1)
            'PLAN'      PLAN (#I1)
            'SMOK/Flag' SMOKER-FLAG (#I1)
           MOVE ISSUE-DATE (#I1) TO PAID-UP-DATE (#I1)
         ELSE
           ADD 1 TO #CNT-3
           DISPLAY (3) #CNT-3 ST-V.ID-NUMBER ISSUE-DATE (#I1)
            'Pay/Stat' PAY-STATUS (#I1)
             PLAN (#I1) PAID-UP-DATE (#I1)
            'Smok/Flg'  SMOKER-FLAG (#I1)
            'DOB' DATE-OF-BIRTH
            'F/Loan' X-LOAN-AMOUNT (#I1)
            'V/Loan' X-LOAN-AMOUNT-2 (#I1)
         END-IF
      END-IF
    ELSE
      IF PLAN (#I1) = 'OL' or = 'GPL' OR = 'GP2'     /* TT1           /* DW1
        IF   (ISSUE-DATE (#I1) GE 19970101)
         AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
          COMPUTE #PD-UP-DT = DATE-OF-BIRTH + 1000000
        ELSE
          COMPUTE #PD-UP-DT = DATE-OF-BIRTH + 960000
        END-IF
      ELSE
        IF PLAN (#I1) = '20P'
          COMPUTE #PD-UP-DT = ISSUE-DATE (#I1) + 200000
        ELSE
          IF PLAN (#I1) = '30P'
            COMPUTE #PD-UP-DT = ISSUE-DATE (#I1) + 300000
          ELSE
            IF PLAN (#I1) = '7P'
              COMPUTE #PD-UP-DT = ISSUE-DATE (#I1) + 70000
            ELSE
              IF PLAN (#I1) = 'RT'
                COMPUTE #PD-UP-DT = ISSUE-DATE (#I1) + 50000
                COMPUTE #PD-UP-DT-RT2 = ISSUE-DATE (#I1) + 100000
                COMPUTE #PD-UP-DT-RT3 = ISSUE-DATE (#I1) + 150000
              ELSE
                IF PLAN (#I1) = 'LT'
                  IF SMOKER-FLAG  (#I1) = 'S'
                    COMPUTE #PD-UP-DT = DATE-OF-BIRTH + 500000
                  ELSE
                    COMPUTE #PD-UP-DT = DATE-OF-BIRTH + 600000
                  END-IF
                ELSE
                  MOVE PLAN (#I1) TO #PLAN
                  IF  (#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z') /* LTII
                    IF #PLAN = MASK (.NN)
                      COMPUTE #PD-UP-DT =
                         ISSUE-DATE(#I1) + (#PLAN-N2 * 10000)
                    END-IF
                  ELSE    /* MUST BE OL FLEX
                    IF #PLAN-FLEX-F = 'F'    /* ??F
                      IF #PLAN-FLEX-P = 'P'  /* 4PF etc
                        COMPUTE #PD-UP-DT =
                        ISSUE-DATE (#I1) + (#PLAN-FLEX-N1 * 10000)
                      ELSE                   /* MUST BE 59F ETC
                        COMPUTE #PD-UP-DT =
                           ISSUE-DATE (#I1) + (#PLAN-FLEX-N2 * 10000)
                      END-IF
                    END-IF
                  END-IF
                END-IF
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
*
      IF   (#PD-UP-YYYYMM     NE PU-DT-YYYYMM (#I1))
       AND (#PD-UP-YYYYMM-RT2 NE PU-DT-YYYYMM (#I1))
       AND (#PD-UP-YYYYMM-RT3 NE PU-DT-YYYYMM (#I1))
        MOVE 15 TO #PD-UP-DD
         ADD 1 TO #CNT-2
         DISPLAY (2) #CNT-2
                     ST-V.ID-NUMBER
                     ISSUE-DATE (#I1)
                     DATE-OF-BIRTH
                     PAID-UP-DATE (#I1)
                     #PD-UP-DT
                     PAY-STATUS (#I1)
                     PLAN (#I1)
                     SMOKER-FLAG (#I1)
*
        IF PLAN (#I1) = 'RT'
          WRITE ST-V.ID-NUMBER ISSUE-DATE (#I1)
             'RT policies must be updated manually'
        ELSE
          MOVE TRUE TO #UPDATE
          MOVE #PD-UP-DT TO PAID-UP-DATE (#I1)
        END-IF
      END-IF
    END-IF
  END-FOR
*
  IF #UPDATE
    UPDATE
  END-IF
*
  END TRANSACTION
END-READ
*
END
