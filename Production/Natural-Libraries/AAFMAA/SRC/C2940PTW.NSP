* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM-ID: C2940PTW
*         TW: TAX BENEFICIARY 1099-C   (CAP Loan (W)riteoff)
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* DRW 11/21/2007 DW1 Cloned from C2930PTC
* DRW 11/21/2007 DW2 Changes for Type W
* NGG 20121002   NG1 CN Expansion Project -                            *
*                    New beneficiary database file to expand Member IDs up to 500000
* bz-Name-C 07/07/2017     Fine Names. Keep all capital 
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2930LTC
LOCAL USING ADDA0001    /* NG1
LOCAL
1 #DATE-ENTERED-DISP    (A10)
1 #SCR-MSG         (A60)
1 REDEFINE #SCR-MSG
  2 #SCR-MSG-1     (A35)
  2 #SCR-MSG-2     (A25)
1 #PREM-TITLE      (A10)
1 #PREM-PAID       (N7.2)
1 #SCR-CV          (C)
1 #SCR-DEL-CV      (C)
1 #SCR-R-U-SURE-CV (C)
*
1 #SCR-DEL-MSG      (A29)
1 #SCR-R-U-SURE-MSG (A29)
1 #SCR-DEL-COD      (A1)
1 #SCR-R-U-SURE-COD (A1)
*
1 #SCN-INT-TYPE     (A1)
1 #SCR-ZIP-9             (N9)
1 REDEFINE #SCR-ZIP-9
  2 #SCR-ZIP-1             (N5)
  2 #SCR-ZIP-2             (N4)
*
1 #INT-1099-KEY     (A13)
1 REDEFINE #INT-1099-KEY
  2 #INT-1099-ID-NUMBER   (N9)
  2 #INT-1099-YYYY        (N4)
  2 REDEFINE #INT-1099-YYYY
    3 #INT-1099-CENT        (N2)
    3 #INT-1099-YY          (A2)
*
1 #YEAR-INT-TYPE-ID-KEY   (A14)
1 REDEFINE #YEAR-INT-TYPE-ID-KEY
  2 #YEAR-INT-YEAR            (N4)
  2 #YEAR-INT-TYPE            (A1)
  2 #YEAR-INT-ID-NUMBER       (N9)
*
1 I-V-1099 VIEW OF A-INT-1099
  2 YEAR-INT-TYPE-ID-KEY
*
1 #TODAY-YYYYMMDD        (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY           (N4)
  2 #TODAY-MM             (N2)
  2 #TODAY-DD             (N2)
*
1 #yyyymmdd     (n8)
1 redefine #yyyymmdd
  2 #yyyy-a        (a4)
  2 #mm-a          (a2)
  2 #dd-a          (a2)
*
1 #REC-NOT-FOUND          (L)
1 #ID-NOT-FOUND           (L)
1 #EDIT-FLAG              (A1)
1 #ISN                    (P8)
1 #HOLD-INT               (A1)
*
1 WIDOW-SSN-KEY     (A10)
1 #RELATED-ID-NUMBER (N6)
1 #NAME              (A25)
1 #ADDR-1            (A25)
1 #ADDR-2            (A25)
1 #ZIP-CODE          (N9)
1 #LETTER            (A7) CONST <'!LETTER'>   /* bz-Name
1 Letter             (A) DYNAMIC
*
END-DEFINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INCLUDE GONERCXX       /* NATURAL ERROR ROUTINE
*
INCLUDE C1099CXX
*
MOVE *DATN  TO #TODAY-YYYYMMDD
MOVE (AD=P) TO #SCR-CV
MOVE (AD=P) TO #SCR-DEL-CV
MOVE (AD=P) TO #SCR-R-U-SURE-CV
*
IF #CGA-SCR-ID NE MASK (NNNNNNNNN) OR #CGA-SCR-ID = 0
  INPUT TEXT 'INVALID RECIPIENT"S ID NUMBER'
    MARK *#CGA-SCR-ID USING MAP 'C2940MTW'
  FETCH 'G1000PXX'
END-IF
*
IF   (#CGA-SCR-SUFFIX NE ' ')
    AND (#CGA-SCR-SUFFIX NE MASK (NN) AND (#CGA-SCR-SUFFIX NE 'W'))
  INPUT TEXT 'INVALID YEAR - MUST BE YY'
    MARK *#CGA-SCR-SUFFIX USING MAP 'C2940MTW'
  FETCH 'G1000PXX'
END-IF
*
PERFORM SELECT-TW-RECORD
*
IF #ISN <= 0
  MOVE 'ENTER INFO FOR NEW 1099 RECORD' TO #SCR-MSG-1
  FIND (1) A-STAT WITH MEMBER-CONTACT-ID = #YEAR-INT-ID-NUMBER
    INT-1099-V.CONTACT-ID := A-STAT.MEMBER-CONTACT-ID
    INT-1099-V.RELATED-ID-NUMBER := A-STAT.ID-NUMBER
    INT-1099-V.SSN        := A-STAT.SSN
  END-FIND
ELSE
  GET INT-1099-V #ISN
  MOVE INT-1099-V.INT-TYPE       TO #HOLD-INT
  MOVE 'ENTER UPDATED 1099 INFO' TO #SCR-MSG
  MOVE DATE-ENTERED TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-ENTERED-DISP WITH '/'
  MOVE 'PRINCIPAL' TO #PREM-TITLE
  COMPUTE #PREM-PAID = AMOUNT - GROSS-AMT  /* This calcs Prin (P&I minus Int)  DW2
  MOVE INT-TYPE TO #SCN-INT-TYPE
  RESET #SCR-DEL-CV
  MOVE 'ENTER (D) TO DELETE RECORD  -' TO #SCR-DEL-MSG
END-IF
*
RESET #PDA-ADDRESS
MOVE CONTACT-ID TO #ADD-CONTACT-ID
MOVE 'P' TO #ADD-TYPE
CALLNAT 'ADDN0001' #PDA-ADDRESS
Letter := #LETTER
CALLNAT 'GPRS0CNT' #ADD-CONTACT-ID Letter
#NAME := Letter
*
RESET #SCR-CV
MOVE '*' TO #CGA-SCR-COMMAND
INPUT TEXT #SCR-MSG MARK *SSN-TAX-ID-IND  USING MAP 'C2940MTW'
*
IF #CGA-SCR-COMMAND NE '*'
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
IF #SCN-INT-TYPE = 'W'
  IGNORE
ELSE
  REINPUT 'DIST. TYPE MUST BE "W"'
    MARK *#SCN-INT-TYPE
END-IF
*
IF GROSS-AMT = 0
  REINPUT WITH 'GROSS AMOUNT MUST BE GREATER THAN 0'
    MARK *GROSS-AMT
END-IF
*
IF GROSS-AMT < AMOUNT
  REINPUT WITH 'GROSS DISTRIBUTION MUST BE GREATER THAN TAX AMOUNT'
    MARK *GROSS-AMT
END-IF
*
MOVE #INT-1099-ID-NUMBER TO INT-1099-V.SSN
MOVE #INT-1099-YYYY      TO INT-1099-V.YEAR
MOVE 'S'                 TO INT-1099-V.SSN-TAX-ID-IND
*
MOVE #SCN-INT-TYPE TO INT-1099-V.INT-TYPE
*
PERFORM CHECK-FOR-DUPLICATES
*
IF #REC-NOT-FOUND
  STORE INT-1099-V
  MOVE 'NEW 1099 RECORD ADDED' TO #SCR-MSG
ELSE
  IF #SCR-DEL-COD = 'D'
    MOVE (AD=P) TO #SCR-CV
    MOVE (AD=P) TO #SCR-DEL-CV
    RESET #SCR-R-U-SURE-CV
    MOVE 'ARE YOU SURE? (Y)ES OR (N)O -' TO #SCR-R-U-SURE-MSG
    INPUT MARK *#SCR-R-U-SURE-COD USING MAP 'C2940MTW'
*
    IF #CGA-SCR-COMMAND NE '*'
      END TRANSACTION
      FETCH 'G1000PXX'
    END-IF
*
    IF #SCR-R-U-SURE-COD = 'Y'
      MOVE '1099 RECORD DELETED' TO #SCR-MSG
      DELETE (0127)
    ELSE
      MOVE '1099 RECORD NOT DELETED' TO #SCR-MSG
    END-IF
*
  ELSE
    UPDATE (0127)
    MOVE '1099 RECORD UPDATED'   TO #SCR-MSG
  END-IF
END-IF
*
END TRANSACTION
*
MOVE (AD=P) TO #SCR-CV
MOVE (AD=P) TO #SCR-DEL-CV
MOVE (AD=P) TO #SCR-R-U-SURE-CV
MOVE 'TC' TO #CGA-SCR-COMMAND
MOVE 'PRINCIPAL' TO #PREM-TITLE
COMPUTE #PREM-PAID = GROSS-AMT - AMOUNT  /* This calcs Prin (P&I minue Int)  DW2
*
INPUT TEXT #SCR-MSG USING MAP 'C2940MTW'
FETCH 'G1000PXX'
*
DEFINE SELECT-TW-RECORD
/*     ================
IF #CGA-SCR-SUFFIX = ' '
  MOVE #TODAY-YYYY TO #INT-1099-YYYY
ELSE
  IF #CGA-SCR-SUFFIX = 'W'
    ASSIGN #INT-1099-YY = '92'
  ELSE
    MOVE #CGA-SCR-SUFFIX TO #INT-1099-YY
  END-IF
  IF #INT-1099-YY GT '50'
    MOVE 19 TO #INT-1099-CENT
  ELSE
    MOVE 20 TO #INT-1099-CENT
  END-IF
END-IF
IF #CGA-SCR-ID > 500000
  #INT-1099-ID-NUMBER := #CGA-SCR-ID
END-IF
*
CALLNAT 'C1099SXX'
  #CGA-SCR-COMMAND
  #CGA-SCR-ID
  #CGA-SCR-SUFFIX
  #INT-1099-KEY
  #YEAR-INT-TYPE-ID-KEY
  #ISN
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
END-SUBROUTINE
*
END
