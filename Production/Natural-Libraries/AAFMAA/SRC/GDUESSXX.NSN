* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GDUESSXX - CALC dues
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  05062005 DW1  No dues if 1st pol is paid-up
* DRW  03292006 DW2  Bypass dues if process-ind not = 'G'
* DRW  08282008 DW3 Gen Plus Processing
* YAK  09262012 YK1 Use function to identify Level Term II plans
*******************************************
*
* Since Gen Plus os always Gen it never has Dues     /* DW3\
*
DEFINE DATA
PARAMETER
1 #CN        (N6)
1 REDEFINE #CN
 2 #CN-A     (A6)
1 #SEG-X      (I2)
1 #SVC-FEE    (N3.2)
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PROCESS-IND
 2 WIDOW-FLAG
 2 INTRO-FLAG
 2 DUES-BALANCE
 2 DUES-ALL-PAID-IND
 2 ISSUE-DATE          (9)
 2 STATUS              (9)
 2 PLAN                (9)
 2 REDEFINE PLAN
  3 PLAN-OCC      (9)
   4 PLAN-A3
    5 PLAN-A1   (A1)
    5 PLAN-N2   (N2)
   4 REDEFINE PLAN-A3
    5 PLAN-A2   (A2)
    5 PLAN-FILL (A1)
  2 MONTH-ALLOC-AMT     (9)
  2 PAY-STATUS          (9)
*
1 #SVC-FEE-SP (N3.2) INIT <450.00>
1 #SVC-FEE-2P (N2.2) INIT <18.50>
1 #SVC-FEE-3P (N2.2) INIT <12.50>
1 #SVC-FEE-4P (N1.2) INIT <9.50>
1 #SVC-FEE-5P (N1.2) INIT <7.50>
1 #SVC-FEE-6P (N1.2) INIT <6.50>
1 #SVC-FEE-7P (N1.2) INIT <6.00>
1 #SVC-FEE-10 (N1.2) INIT <4.50>
1 #SVC-FEE-20 (N1.2) INIT <3.00>
1 #SVC-FEE-30 (N1.2) INIT <2.50>
1 #SVC-FEE-OL (N1.2) INIT <2.00>
1 #SVC-FEE-TM (N1.2) INIT <2.00>
*
1 #I1               (I2)
1 #I2               (I2)
1 #FST-X            (I2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
* WRITE 'top of GDUESSXX' #CN #SEG-X
FIND ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORDS FOUND
    WRITE 'no rec found in Gduessxx for' #CN
  END-NOREC
END-FIND
*
FOR #FST-X 1 TO 9   /* get 1st active pol
  IF ST-V.STATUS (#FST-X) = 'D'
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #FST-X GT 9
  MOVE 1 TO #FST-X
END-IF
*
MOVE #SEG-X TO #I1
*
RESET #SVC-FEE
IF  (ST-V.ISSUE-DATE (#I1) LT 19970101)
 OR (ST-V.MONTH-ALLOC-AMT (#I1) = 999.99)
 OR (ST-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
                   OR = 'O' OR = 'P' OR = 'Q')
 OR (ST-V.DUES-ALL-PAID-IND = 'Y')
 OR (ST-V.PAY-STATUS (#I1) GT '40')   /* DW1
 OR (#I1 NE #FST-X)
 OR (ST-V.STATUS (#I1) NE 'D')
 OR (ST-V.PROCESS-IND NE 'G')   /* DW2
   ESCAPE ROUTINE
END-IF
*
DECIDE ON FIRST VALUE ST-V.PLAN-A2 (#I1)
  VALUE 'OL' MOVE #SVC-FEE-OL TO #SVC-FEE
* VALUE 'RT' MOVE #SVC-FEE-TM TO #SVC-FEE              /* YK1
* VALUE 'LT' MOVE #SVC-FEE-TM TO #SVC-FEE              /* YK1
  VALUE 'SP' MOVE #SVC-FEE-SP TO #SVC-FEE
  VALUE '2P' MOVE #SVC-FEE-2P TO #SVC-FEE
  VALUE '3P' MOVE #SVC-FEE-3P TO #SVC-FEE
  VALUE '4P' MOVE #SVC-FEE-4P TO #SVC-FEE
  VALUE '5P' MOVE #SVC-FEE-5P TO #SVC-FEE
  VALUE '6P' MOVE #SVC-FEE-6P TO #SVC-FEE
  VALUE '7P' MOVE #SVC-FEE-7P TO #SVC-FEE
  VALUE '8P' MOVE #SVC-FEE-7P TO #SVC-FEE
  VALUE '9P' MOVE #SVC-FEE-7P TO #SVC-FEE
  VALUE '20' MOVE #SVC-FEE-20 TO #SVC-FEE
  VALUE '30' MOVE #SVC-FEE-30 TO #SVC-FEE
  NONE        IGNORE
END-DECIDE
*
* IF (ST-V.PLAN-A1 (#I1) GE 'U') AND (ST-V.PLAN-A1 (#I1) LE 'Z')    /* YK1
IF FUNCTION-TERM-POLICY(<ST-V.PLAN(#I1)>)                           /* YK1
  MOVE #SVC-FEE-TM TO #SVC-FEE
END-IF
*
IF (#SVC-FEE = 0) AND (ST-V.PLAN-A2 (#I1) = MASK (NN))
    IF ST-V.PLAN (#I1) LE '19F' MOVE #SVC-FEE-10 TO #SVC-FEE ELSE
    IF ST-V.PLAN (#I1) LE '29F' MOVE #SVC-FEE-20 TO #SVC-FEE ELSE
    IF ST-V.PLAN (#I1) LE '34F' MOVE #SVC-FEE-30 TO #SVC-FEE ELSE
                                 MOVE #SVC-FEE-OL TO #SVC-FEE
  END-IF END-IF END-IF
END-IF
* WRITE 'end of GDUESSXX svc-fee=' #SVC-FEE
*
END
