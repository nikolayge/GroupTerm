* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID DNXGF005   Defaul Work 
************************************************************************ 
*                       MODIFICATION LOG                               * 
* USER   DATE      TAG     REASON                                      * 
*                                                                      * 
* NGG   20110404  None    Program written.                             * 
*                                                                      * 
************************************************************************ 
* 
DEFINE FUNCTION FUNCTION-DEFAULT-EMAIL RETURNS (A) DYNAMIC 
  DEFINE DATA 
  PARAMETER 
  1 #CONTACT-ID         (N8) 
  LOCAL 
  1 BLANK         (A1)   CONST <' '> 
  1 #BUSINESS     (A1/2) CONST <'B','M'> 
  1 #PERSONAL     (A1)   CONST <'P'> 
  1 #OTHER        (A1)   CONST <'O'> 
  1 EMAIL VIEW OF A-CONTACT-EMAIL 
    2 EMAIL-ID 
    2 CONTACT-ID 
    2 EMAIL-TYPE 
    2 EMAIL-ADDRESS 
    2 EMAIL-PROTECTION-IND 
    2 EMAIL-UNDELIV 
    2 EMAIL-PREFERRED-IND 
    2 STATUS 
  END-DEFINE 
* 
FUNCTION-DEFAULT-EMAIL := BLANK 
* 
FIND EMAIL WITH CONTACT-ID = #CONTACT-ID 
  IF NO RECORDS FOUND 
    ESCAPE BOTTOM 
  END-NOREC 
  IF EMAIL-TYPE = #BUSINESS (*) 
      AND FUNCTION-DEFAULT-EMAIL = BLANK 
    IF STATUS = BLANK 
      FUNCTION-DEFAULT-EMAIL := EMAIL-ADDRESS 
    END-IF 
    ESCAPE TOP 
  END-IF 
  IF EMAIL-TYPE = #OTHER 
      AND FUNCTION-DEFAULT-EMAIL = BLANK 
    IF STATUS = BLANK 
      FUNCTION-DEFAULT-EMAIL := EMAIL-ADDRESS 
    END-IF 
    ESCAPE TOP 
  END-IF 
  IF EMAIL-TYPE = #PERSONAL 
    IF STATUS = BLANK 
      FUNCTION-DEFAULT-EMAIL := EMAIL-ADDRESS 
      ESCAPE BOTTOM 
    END-IF 
  END-IF 
  IF EMAIL-PREFERRED-IND = 'Y' 
    ESCAPE BOTTOM 
  END-IF 
END-FIND 
END-FUNCTION 
* 
END 
