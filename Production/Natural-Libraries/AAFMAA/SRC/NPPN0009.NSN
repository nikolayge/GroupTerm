* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***********************************************************
* SUBPROGRAM-ID: NPPN0009 policy print Military Information
***********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    10202011       Initial Creation
********************************************************
DEFINE DATA
PARAMETER USING NPPA0001
LOCAL USING OBJLMINF
LOCAL
1 #I                (I2)
1 #BASD             (N8)
1 #DOP              (N8)
1 #AIR-FORCE        (A1) CONST <'F'> 
1 #SHORT-FORM       (A1) CONST <'S'>
1 #TAFMSD-LABEL     (A) DYNAMIC CONST <'Total Active Federal Military Service Date    '>
1 #BASD-LABEL       (A) DYNAMIC CONST <'Army Basic Active Service Date                    '>
1 #RANK-LABEL       (A) DYNAMIC CONST <'Date of Promotion to '>
1 #GUARD-RESERVE    (L)
1 #GUARD-RESERVE-ARRAY (A3/14) CONST
  <"FR ","FRA","FGA","FG ",       /* Air Force */
  "AGA", "AG ", "AR ", "ARA",    /* Army */
  "CGR", "CGA",                /* Cost Guard */
  "MR ", "MRA",                 /* Marine Corps */
  "NR ", "NRA" >                /* Navy  */
* Labels:
* Gross Military Retirement Pay
1 GuardReserveSBPAnnuity    (A) DYNAMIC CONST <'Guard Reserve SBP Annuity '>
1 GuardReserveSBPBaseAmount (A) DYNAMIC CONST <'SBP Base Amount '>
1 SBPOption                 (A) DYNAMIC CONST <'SBP Option '>
1 SBPAnnuityAmount          (A) DYNAMIC CONST <'SBP Annuity Amount '>
1 #WORK-FIELD (N3)
1 #LINE       (A) dynamic
1 #WORK-LINE  (A50)
END-DEFINE
*
*  MEMBER-RANK 
*  MEMBER-PREV-RANK 
*  MEMBER-PEBD 
*  MEMBER-BASD-TAFMSD-LABEL 
*  MEMBER-BASD-TAFMSD 
*  MEMBER-DOP 
*  MEMBER-PREV-DOP 
*  MEMBER-RETIREMENT-DATE 
*  MEMBER-GROSS-RETIRED-PAY-AMOUNT 
*  MEMBER-SBP-OPTION 
*  MEMBER-SBP-AMOUNT 
*  MEMBER-VA-CLAIM-NUMBER 
*  MEMBER-VA-DISABILITY-DATE
*  MEMBER-VA-DISABILITY-PCT 
*
FIND(1) MIL-INFO-V WITH ID-NUMBER = POLICY-INFORMATION.MEMBER-CN
END-FIND
*
EXAMINE FULL #GUARD-RESERVE-ARRAY(*) FOR FULL MILITARY-SERVICE INDEX #I
IF #I <> 0 #GUARD-RESERVE := TRUE END-IF

IF BASE-PAY-EF-DATE <> 0
POLICY-INFORMATION.MEMBER-PEBD := FUNCTION-CALENDAR(<BASE-PAY-EF-DATE,#SHORT-FORM>)
END-IF

IF ACTIVE-DUTY-BASE-DATE <> 0
POLICY-INFORMATION.MEMBER-BASD-TAFMSD := FUNCTION-CALENDAR(<ACTIVE-DUTY-BASE-DATE,#SHORT-FORM>)
END-IF

IF SUBSTR(MILITARY-SERVICE,1,1) = #AIR-FORCE
  POLICY-INFORMATION.MEMBER-BASD-TAFMSD-LABEL := #TAFMSD-LABEL   
ELSE 
  POLICY-INFORMATION.MEMBER-BASD-TAFMSD-LABEL := #BASD-LABEL
END-IF

COMPRESS #RANK-LABEL POLICY-INFORMATION.MEMBER-RANK INTO POLICY-INFORMATION.MEMBER-RANK-LABEL 
IF DATE-OF-RANK <> 0
POLICY-INFORMATION.MEMBER-DOP := FUNCTION-CALENDAR(<DATE-OF-RANK,#SHORT-FORM>)
END-IF

IF PRIOR-DATE-OF-RANK <> 0
POLICY-INFORMATION.MEMBER-PREV-DOP := FUNCTION-CALENDAR(<PRIOR-DATE-OF-RANK,#SHORT-FORM>)
END-IF

IF DATE-RETIRED <> 0
POLICY-INFORMATION.MEMBER-RETIREMENT-DATE := FUNCTION-CALENDAR(<DATE-RETIRED,#SHORT-FORM>)
END-IF

* GuardReserveSBPBaseAmount/SBPAnnuityAmount
IF MIL-SBP-AMT <> 0
MOVE EDITED MIL-SBP-AMT (EM=ZZZZ9) TO POLICY-INFORMATION.MEMBER-SBP-AMOUNT
COMPRESS '$' *TRIM(POLICY-INFORMATION.MEMBER-SBP-AMOUNT) INTO 
    POLICY-INFORMATION.MEMBER-SBP-AMOUNT LEAVING NO
END-IF           
IF #GUARD-RESERVE = TRUE
POLICY-INFORMATION.MEMBER-SBP-OPTION := RSV-NG-TYPE-ANNUITY   /* GuardReserveSBPAnnuity
POLICY-INFORMATION.MEMBER-SBP-OPTION-LABEL := GuardReserveSBPAnnuity
POLICY-INFORMATION.MEMBER-SBP-AMOUNT-LABEL := GuardReserveSBPBaseAmount
ELSE
POLICY-INFORMATION.MEMBER-SBP-OPTION := MIL-SBP-OPT           /* SBPOption
POLICY-INFORMATION.MEMBER-SBP-OPTION-LABEL := SBPOption
POLICY-INFORMATION.MEMBER-SBP-AMOUNT-LABEL := SBPAnnuityAmount
END-IF

IF RETIRED-PAY-AMT <> 0
MOVE EDITED RETIRED-PAY-AMT (EM=ZZZZ9) TO POLICY-INFORMATION.MEMBER-GROSS-RETIRED-PAY-AMOUNT
COMPRESS '$' *TRIM(POLICY-INFORMATION.MEMBER-GROSS-RETIRED-PAY-AMOUNT) INTO 
    POLICY-INFORMATION.MEMBER-GROSS-RETIRED-PAY-AMOUNT LEAVING NO
END-IF

POLICY-INFORMATION.MEMBER-VA-CLAIM-NUMBER := VA-CLAIM-NUMBER 

IF VA-DISABLE-DATE <> 0
POLICY-INFORMATION.MEMBER-VA-DISABILITY-DATE := FUNCTION-CALENDAR(<VA-DISABLE-DATE,#SHORT-FORM>)
END-IF

IF VA-DISABILITY-PCT > 0
#WORK-FIELD := VA-DISABILITY-PCT * 100 
MOVE EDITED #WORK-FIELD (EM=ZZ9) TO POLICY-INFORMATION.MEMBER-VA-DISABILITY-PCT 
COMPRESS POLICY-INFORMATION.MEMBER-VA-DISABILITY-PCT '%' INTO POLICY-INFORMATION.MEMBER-VA-DISABILITY-PCT LEAVING NO
END-IF

*     
END
