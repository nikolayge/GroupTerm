* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: DNXUN10A
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20130812  None    Program written.                             *
* TMT   20151103  TT1     1099 PROJECT
* bz    2016-05-03 bz-4404
* vt    12/06/2016 vt1    Ticket HD-2056 - 1316 error on map           *
************************************************************************
DEFINE DATA
PARAMETER
1 #MEMBER-CN             (N6)
1 #CLAIMANT-CONTACT-ID   (N8/1:V)
1 #NUMBER_OF_CLAIMANTS   (I4)
1 #CGA-USER-COMPANY      (A1)
1 #CLERK-ID              (A3)
1 #MSG                   (A72)
LOCAL
1 BLANK     (A1) CONST <' '>
1 #DEAD     (A4) CONST <'Dcsd'>
1 #ORIG-COMMAND (A2)  INIT <'CB'>
1 #MAX-CLAIMANTS (I4) CONST <15>
1 #OREL         (A25) CONST <'--- Other Relations -----'>
*
1 #CONTACT-ID    (N8)
1 #PARENT-COID   (N8)
1 #PAGE          (N2)
1 #TOT-PAGES     (N2)
1 #TOT-REL       (N3)
1 #L             (N7)
1 #CV-SELECT     (C/1:150)       /* vt1   was 15
1 #RELATIONS
  2 #RID            (N8/150)
  2 #ROLE-DESC      (A12/150)
  2 #RNAME          (A25/150)
  2 #RDOB           (A10/150)
  2 #RDOD           (A4/150)
  2 #CN             (A6/150)
  2 #SELECT         (A1/150)       /* POPULATE IT !!!
*
1 #CONTACT-NAME  (A25)
1 #D             (D)
1 #SSN           (N9)
1 #DOB           (A10)
1 #DOD           (A4)
1 #ID-NUMBER     (A14)
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 CONTACT-TYPE                                /* bz-4404
  2 FIRM-ORGANIZATION-NAME                      /* bz-4404
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-A (A8)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A (A8)
  2 SSN
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
*
1 #MODE              (A1)
1 P_PARMS
  2 P_ERROR_NUM (I4) /*ERROR NUMBER
  2 P_ERROR_LINE (I4) /*ERROR LINE
  2 P_ERROR_PROGRAM (A32)
  2 P_ERROR_LIB (A32)
  2 P_ERROR_DBID (I4)
  2 P_ERROR_FNR (I4)
  2 P_ERROR_TIME (T)
  2 P_ERROR_PGM_TYPE (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG (I4)
  2 P_ERROR_LEVEL (I4)
  2 P_ERROR_CLASS (I1)
  2 P_ERROR_TYPE (I1)
1 J              (I4)
1 K              (I4)
1 #Text          (A78)
1 #vld           (N2)
*
END-DEFINE
*
SET KEY PF3  NAMED 'Exit'
SET KEY PF4  NAMED 'Cnt'
SET KEY PF7  NAMED 'Prev'
SET KEY PF8  NAMED 'Next'
SET KEY PF12 NAMED 'Reln'
*
IF #MEMBER-CN <= 0
  ESCAPE ROUTINE
END-IF
*
FIND CONTACT WITH ID-NUMBER = #MEMBER-CN
  #CONTACT-ID := #PARENT-COID := CONTACT.CONTACT-ID
  #CONTACT-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  #SSN := CONTACT.SSN
  IF DATE-OF-BIRTH > 0
    MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
    MOVE EDITED #D (EM=MM/DD/YYYY) TO #DOB
  ELSE
    RESET #DOB
  END-IF
  IF DATE-OF-DEATH > 0
    #DOD := #DEAD
  ELSE
    RESET #DOD
  END-IF
  COMPRESS '( CN' CONTACT.ID-NUMBER ')' INTO #ID-NUMBER
END-FIND
*
RESET J #RELATIONS
FIND CONTACT-REL WITH CONTACT-1-ID = #PARENT-COID
  ADD 1 TO J
  ASSIGN #RID (J) = CONTACT-2-ID
  PERFORM POP-RID   /* POPULATE
  #ROLE-DESC (J) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
  FIND CONTACT WITH CONTACT-ID = CONTACT-2-ID
    IF CONTACT-TYPE = 'O'                                                                /* bz-4404
      #RNAME (J) := FIRM-ORGANIZATION-NAME                                              /* bz-4404
    ELSE                                                                                 /* bz-4404
      #RNAME (J) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    END-IF

    IF DATE-OF-BIRTH > 0
      MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
      MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB (J)
    ELSE
      RESET #RDOB (J)
    END-IF
    IF DATE-OF-DEATH > 0
      #RDOD (J) := #DEAD
    ELSE
      RESET #RDOD (J)
    END-IF
    IF ID-NUMBER NE 0
      MOVE EDITED ID-NUMBER (EM=999999) TO #CN ( J )
    ELSE
      RESET #CN ( J )
    END-IF
    COMPRESS FUNCTION-ROLE-DECORATION (<#RID (J)>)
      #ROLE-DESC (J) INTO #ROLE-DESC (J) LEAVING NO
    IF DATE-OF-DEATH <> 0                                           /* bz-4404
      COMPRESS '!' #ROLE-DESC(J) INTO #ROLE-DESC(J) LEAVING NO  /* bz-4404
    END-IF                                                          /* bz-4404
  END-FIND
END-FIND
*
ADD 1 TO J
ASSIGN #RNAME (J) := #OREL
FIND CONTACT-REL WITH CONTACT-2-ID = #PARENT-COID
  ADD 1 TO J
  ASSIGN #RID (J) = CONTACT-1-ID
  PERFORM POP-RID   /* POPULATE
  IF ROLE-1-CD = 'M'
    ASSIGN ROLE-1-CD = ROLE-2-CD
  END-IF
  IF ROLE-2-CD = 'G'
    #ROLE-DESC  (J) := 'GRNDPARENT'
  ELSE
    #ROLE-DESC  (J) := FUNCTION-DISP-RL-DESC(<ROLE-1-CD>)
  END-IF
  FIND CONTACT WITH CONTACT-ID = CONTACT-1-ID
    #RNAME (J) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    IF DATE-OF-BIRTH > 0
      MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
      MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB (J)
    ELSE
      RESET #RDOB (J)
    END-IF
    IF DATE-OF-DEATH > 0
      #RDOD (J) := #DEAD
    ELSE
      RESET #RDOD (J)
    END-IF
    IF ID-NUMBER NE 0
      MOVE EDITED ID-NUMBER (EM=999999) TO #CN ( J )
    ELSE
      RESET #CN ( J )
    END-IF
    COMPRESS FUNCTION-ROLE-DECORATION (<#RID (J)>)
      #ROLE-DESC (J) INTO #ROLE-DESC (J) LEAVING NO
    IF DATE-OF-DEATH <> 0                                           /* bz-4404
      COMPRESS '!' #ROLE-DESC(J) INTO #ROLE-DESC(J) LEAVING NO  /* bz-4404
    END-IF                                                          /* bz-4404

  END-FIND
END-FIND
*                   User Interface
#TOT-REL := J - 1
IF #TOT-REL > 15
  SET KEY PF5  NAMED 'PgUp'
  SET KEY PF6  NAMED 'PgDn'
END-IF
DIVIDE 15 INTO #TOT-REL GIVING #TOT-PAGES REMAINDER #PAGE
IF #PAGE > 0
  ADD 1 TO #TOT-PAGES
END-IF
*
#PAGE := 1
#L := 1
REPEAT
  #Text := ' ' #Vld := 0
  MOVE (AD=PN) TO #CV-SELECT (*)   /* vt 12/7/2016
  FOR J 0 14
    IF #RNAME ( #L + J ) NE BLANK
        AND #RNAME ( #L + J ) NE #OREL
*       IF #ROLE-DESC( J + 1) <> SCAN '!'                         /* bz-4404   /* vt1
*        MOVE (AD=DU) TO #CV-SELECT( J + 1)                       /* bz-4404   /* vt1
      if #role-desc (#l + j) ne scan '!'                                       /* vt1
        move (AD=DU) to #cv-select(#l + j)                                     /* vt1
      ELSE
*        IF #SELECT( J + 1) <> ' '                                /* bz-4404   /* vt1
        if #select (#l + j) ne ' '  /* vt
          #Text := 'Remove highlighted claimant selection - it has DOD'
*          #vld := J + 1
          #vld := #l + J
*          MOVE (AD=DV) TO #CV-SELECT(J + 1)                     /* bz-4404    /* vt1
          move (ad=dv) to #cv-select (#l + j)                                  /* vt1
        END-IF
      END-IF
    END-IF
  END-FOR
*
  INPUT WITh TEXT #Text USING MAP 'DNXUM010'
  IF #vld <> 0
    IF #SELECT(#vld) <> ' '
        and *pf-key ne  'PF12'    /* vt 05/25/2016
      REINPUT #Text
    END-IF
  END-IF
  #Text := ' ' #Vld := 0
*
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF7'
      IF #PAGE > 1
        SUBTRACT 1 FROM #PAGE
        SUBTRACT 15 FROM #L
      END-IF
    VALUE 'PF8'
      IF #TOT-REL >= #L + 15
        ADD 1 TO #PAGE
        ADD 15 TO #L
      END-IF
    VALUE 'PF12','PF4'
      PERFORM CHECK-CURS-LINE
    NONE VALUE IGNORE
  END-DECIDE
  UNTIL *PF-KEY = 'PF3'
*    OR #CLAIMANT-CONTACT-ID (*) > 0
END-REPEAT
*           Populate #CLAIMANT-CONTACT-ID
J := 1  K := 2 RESET #L
REPEAT UNTIL K > #NUMBER_OF_CLAIMANTS
  IF #CLAIMANT-CONTACT-ID (J) = 0
    IF  #CLAIMANT-CONTACT-ID (K) > 0
      #CLAIMANT-CONTACT-ID (J)  := #CLAIMANT-CONTACT-ID (K)
      RESET #CLAIMANT-CONTACT-ID (K)
      ADD 1 TO #L
    END-IF
  END-IF
  ADD 1 TO J
  ADD 1 TO K
END-REPEAT
*
ADD 1 TO #TOT-REL   /* For comment line
FOR J 1 TO #TOT-REL
  IF #SELECT (J) > BLANK
      AND #RID (J) > 0
    ADD 1 TO #L
    #CLAIMANT-CONTACT-ID (#L) := #RID (J)
  END-IF
END-FOR
#NUMBER_OF_CLAIMANTS := #L
SUBTRACT 1 FROM #TOT-REL
*
DEFINE CHECK-CURS-LINE
/*     ===============
IF *CURS-LINE > 4 AND *CURS-LINE < 20
  J := *CURS-LINE - 4
END-IF
IF #RID (J) > 0
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #RID (J)
  ELSE
    CALLNAT 'GDIF4XXX' #CONTACT-ID #RID (J)
  END-IF
END-IF
END-SUBROUTINE
/*
DEFINE POP-RID
/*     =======
K := 1
REPEAT
  UNTIL K > #NUMBER_OF_CLAIMANTS
    OR #SELECT (J) = 'X'
  IF #RID (J) = #CLAIMANT-CONTACT-ID (K)
    #SELECT (J) := 'X'
*     RESET #CLAIMANT-CONTACT-ID (K) /* TT1
  END-IF
  ADD 1 TO K
END-REPEAT
END-SUBROUTINE
/*
ON ERROR
  CALLNAT 'USR2001N' P_PARMS  /* Get the actual error line details
  COMPRESS 'Application error' P_ERROR_NUM 'In program' P_ERROR_PROGRAM
    'Error line' P_ERROR_LINE
    INTO #MSG
  INPUT //// #MSG (AD=IO)
    / 'Please call production support'
  #MSG := 'PF3'
  ESCAPE ROUTINE
END-ERROR
*
END
