* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNDSDA.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER USING LWSADS
PARAMETER
1 ErrorMsg (A) DYNAMIC
LOCAL USING POSADSGN  /* Designation data
LOCAL
1 BLANK         (A1) CONST <' '>
1 #GET          (I4) INIT <0>
1 GET-DS-GUID   (I4) CONST <5>
1 #DSGN-ISN     (N8)
1 CUR-LINE      (A70)
1 #D       (N8) 1 REDEFINE #D
  2 #YYYY  (A4)
  2 #MM    (A2)
  2 #DD    (A2)
1 L        (I4)
1 J        (I4)

/*        ONLY FOR LWSNGUID 
1 #POLICY-ROLE-GUID  (A50/99)
1 #POLICY-ROLE       (N2/99)
1 #POLICY-ID         (A16)
1 PolicyGUID         (A50)
END-DEFINE
/* 
RESET #DSGN-ISN 
/* PolicyDesignation
#DSGN.DESIGNATION-ID := PolicyDesignation.DESIGNATION-ID
CALLNAT 'POSN0011'
  #GET
  ErrorMsg
  #DSGN-ISN
  #DSGN
DECIDE ON FIRST VALUE OF #DSGN.STATUS-CODE
  VALUE 'F'
    DsStatus := 'Final'
  VALUE 'V'
    DsStatus := 'Validated'
  NONE VALUE
    ESCAPE TOP
END-DECIDE

IF #DSGN.ALL-CHILDREN = 'Y'
  AllChildren := 'true'
ELSE
  AllChildren := 'false'
END-IF

IF #DSGN.RESTRICTED-IRREVO = 'Y'
  RestrictedIrrvcble := 'true'
ELSE
  RestrictedIrrvcble := 'false'
END-IF

IF SUBSTR (#DSGN.PER-STIRPES,1,1) = 'Y'
  PSprimary := 'true'
ELSE
  PSprimary := 'false'
END-IF
IF SUBSTR (PER-STIRPES,2,1) = 'Y'
  PScontingent := 'true'
ELSE
  PScontingent := 'false'
END-IF

COMPRESS INSTRUCTIONS (*) INTO DesignationText
PERFORM REMOVE-LINE-FEED
InstructionsPrimary := DesignationText

COMPRESS CONTINGENT-INSTR (*) INTO DesignationText
PERFORM REMOVE-LINE-FEED
InstructionsContingent := DesignationText

COMPRESS BENE-SHOW (*) INTO DesignationText
PERFORM REMOVE-LINE-FEED


#D := DATE-SIGNED
COMPRESS #YYYY #MM #DD INTO DateUpdated
  WITH DELIMITER '-'

/* Designation URI
CALLNAT 'LWSNGUID' GET-DS-GUID
  #POLICY-ID
  PolicyDesignation.DESIGNATION-ID
  PolicyGUID
  #POLICY-ROLE-GUID (*)
  #POLICY-ROLE (*)
  PolicyDesignation.DesignationGUID
  ErrorMsg

DEFINE REMOVE-LINE-FEED
/*
* \b  Backspace (ascii code 08)
* \f  Form feed (ascii code 0C)
* \n  New line
* \r  Carriage return
* \t  Tab
* \"  Double quote
* \\  Backslash character

FOR J 1 TO 500
  DECIDE ON FIRST VALUE OF  DesignationText-A1 (J)
    VALUE H'08', H'09', H'0A', H'0C', H'0D'
      RESET DesignationText-A1 (J)
    VALUE '"', '\', ':', '&'
      RESET DesignationText-A1 (J)
    NONE VALUE
      IGNORE
  END-DECIDE
END-FOR
END-SUBROUTINE

END
