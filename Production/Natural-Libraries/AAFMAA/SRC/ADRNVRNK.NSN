* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRNVRNK
* DESCRIPTION: RANK MAINTENANCE
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  02/11/2011  None Creation
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #RETURN-FLAG    (L)
1 #ID-NUMBER      (N6)
1 #BRANCH         (A3)
1 #RANK-ABBR      (A10)
1 #BRANCH-POS     (L)
1 #RANK-POS       (L)
1 #ERROR-MSG      (A60)
*
LOCAL
1 GEN-V VIEW OF A-GEN-TABLE
  2 GEN-ALPHA-VALUE
*
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 MILITARY-ID
  2 RANK-ABBR
*
1 #VALID          (L)
1 #RANK-DESC      (A45)
1 #PAY-GRADE      (A3)
1 #RANK-ID        (N6)
1 #PROCESS        (L) INIT<FALSE>
1 #SECURED        (A1)
END-DEFINE
*
RESET #BRANCH-POS #RANK-POS #ERROR-MSG
*
IF #PROCESS  /* Never executed. Just included So that REINPUT HELP could refer back to this INPUT.
  INPUT 'PASS' #RANK-ABBR (HE='HLPHCODR',#BRANCH,#RANK-DESC,#RANK-ABBR,#PAY-GRADE,#RANK-ID,#RETURN-FLAG,#SECURED)
END-IF
*
**WRITE '3' '='#RETURN-FLAG
IF #RETURN-FLAG
  RESET #VALID
  FIND GEN-V WITH SEC-KEY = 'CODE024'
    IF GEN-ALPHA-VALUE = #BRANCH OR
      (#ID-NUMBER = 0 AND #BRANCH = MASK(.'  ') AND SUBSTR(GEN-ALPHA-VALUE,1,1) = #BRANCH)
      MOVE TRUE TO #VALID
      ESCAPE BOTTOM
    END-IF
  END-FIND
  IF NOT #VALID
    SET WINDOW OFF
    RESET #RETURN-FLAG
    MOVE 'Invalid/Missing Branch. Please use look-up.' TO #ERROR-MSG
    MOVE TRUE TO #BRANCH-POS
    ESCAPE ROUTINE
  END-IF
**  WRITE '4' '='#RETURN-FLAG
  REINPUT USING HELP
    MARK *#RANK-ABBR
ELSE
  RESET #VALID
  FIND GEN-V WITH SEC-KEY = 'CODE024'
    IF GEN-ALPHA-VALUE = #BRANCH OR
      (#ID-NUMBER = 0 AND #BRANCH = MASK(.'  ') AND SUBSTR(GEN-ALPHA-VALUE,1,1) = #BRANCH)
      MOVE TRUE TO #VALID
      ESCAPE BOTTOM
    END-IF
  END-FIND
  IF NOT #VALID
    MOVE 'Invalid/Missing Branch. Please use look-up.' TO #ERROR-MSG
    MOVE TRUE TO #BRANCH-POS
    ESCAPE ROUTINE
  END-IF
  IF #RANK-ABBR = ' '
    MOVE 'Invalid/Missing Rank. Please use look-up.' TO #ERROR-MSG
    MOVE TRUE TO #RANK-POS
    ESCAPE ROUTINE
  END-IF
  RESET #VALID
  READ RANK-V
    IF SUBSTR(MILITARY-ID,1,1) = SUBSTR(#BRANCH,1,1) AND
      *TRANSLATE(#RANK-ABBR,UPPER) = *TRANSLATE(RANK-ABBR,UPPER)
      MOVE TRUE TO #VALID
      ESCAPE BOTTOM
    END-IF
  END-READ
  IF NOT #VALID
    MOVE 'Invalid Rank for this Branch. Please use look-up.' TO #ERROR-MSG
    MOVE TRUE TO #RANK-POS
    ESCAPE ROUTINE
  END-IF
END-IF
*
END

