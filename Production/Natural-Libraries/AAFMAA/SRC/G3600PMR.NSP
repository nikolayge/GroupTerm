* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: G3600PMR
* THIS PROGRAM PROCESSES THE 'MR' MAILING REQUEST UPDATE SCREEN
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    09282004  DW1     add CREDIT CARD processing                  *
* PAM    03112005  PM2     Reset display fields for GRANKSXX           *
* DRW    04292005  DW3  Add DOB processing to GMAXSXX
* DRW  10/01/2007 DW4 Delete AFSC Suffix's (BA/DI/EDU/FAS/FIN/FIR/IFR/MS/MS2/SSA/DOP)
*                                          (BFA/BPD)
* VXT   08/23/2012 vt1     Replaced PAM with VXT                       *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING C2150APO
LOCAL USING CONTPDA1
LOCAL
* below is used in linkage to C2151SPO
1 #SCN-AOD-YYYYMMDD (N8)
1 #SCN-AS-OF-DT     (A8)
1 REDEFINE #SCN-AS-OF-DT
  2 #SCN-AOD-N       (N8)
1 REDEFINE #SCN-AS-OF-DT
  2 #SCN-AOD-MMDD    (A4)
  2 #SCN-AOD-YYYY    (A4)
*
1 #SCN-DOB          (A8)
1 REDEFINE #SCN-DOB
  2 #SCN-DOB-N       (N8)
1 REDEFINE #SCN-DOB
  2 #SCN-DOB-MMDD    (A4)
  2 #SCN-DOB-YYYY    (A4)
*
1 #VP-SEL    (L)
1 #VP-CURR   (A1) INIT <'N'>
1 #VP-FACE   (N3/4)
1 #VP-PLAN   (N2/4)
1 #VP-SMOK   (A1/4)
1 #VP-W-APP  (A1/4)
1 #VP-APP-DUES (A1/4)
1 #DIV         (P3)
1 #REM         (P3)
*
1 #SCN-INS-ONLY     (A1)
1 #SCN-SPS-GEN      (A1)
1 #SCN-SPS-GEN-CV   (C)
1 #DESIRED-FACE   (N6)
1 REDEFINE #DESIRED-FACE
  2 #DES-FACE-N3     (N3)
  2 #DES-FACE-REST   (N3)
1 #DESIRED-FACE-AMT (N6/3)
1 #DESIRED-PLAN     (A1/10)
1 #FIRST-TIME       (L) INIT <TRUE>
1 #FROM-AT          (L)
1 #FROM-IM          (L)
1 #SO-MBR           (L)
1 #TERMED-MBR       (L)
1 #SSN              (N9)
1 #VT-TYPE    (A2)
1 #VT-TERM    (N2)
1 #VT-RIDER   (A1)
* above is linkage to C2151SPO
*
1 #FOUND-LPX     (L)
1 #APP-OVERRIDE    (L)
1 #BYPASS          (L)
1 #CLERK-ABB-PASS  (A3)
1 #INVALID   (L)
1 #CLERK-ABB-NAME-PASS (A20)
*
1 #YN                (A1)
1 #MR-CNT            (P3)
1 #ACS-IND           (A1) INIT <'N'>
1 #NUM-PAYING-POLS   (P2)
1 #MR-MSG-NOTE-NUM (N6)
1 #MR-MSG          (A60/8)
1 #HOLD-MSG        (A60/8)
1 #MR-MSG-CV       (C/8)
1 #MR-NT-REC-FND     (L)
1 #MR-MSG-FND        (L)
1 #MR-MSG-CHGED      (L)
1 #NT-ISN            (P8)
1 #NOTE-NUM          (P8)
1 #NOTE-REC-NOT-FOUND (L)
1 #NOTE-ISN           (P8)
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER         (N6)
  2 NOTE-LINE-NUM      (P1/40)
  2 NOTE-DATE          (N8/40)
  2 NOTE-CLERK-ID      (A3/40)
  2 NOTE-TEXT          (A60/40)
*
1 #SCR-CV-X           (C)
1 #SCR-CV             (C/20)
1 #SCR-MR-TYPE        (A1/20)
1 REDEFINE #SCR-MR-TYPE
  2 #SCR-MR-TYPE-2     (A1/20)
*
1 #SCR-CV-ACCT        (C/10)
1 #SCR-MR-TYPE-ACCT   (A1/10)
*
1 #SCR-CV-APPL        (C/9)
1 #SCR-MR-TYPE-APPL   (A1/9)
*
1 #MR-X            (I2)
1 #BASD            (P8)
1 #ASQ             (L)
1 #MODE-G          (L)
1 #MODE-E          (L)
1 #FND-999         (L)
1 #NUM-POLS        (P2)
1 #CAP-FACE-AMOUNT (P6.2)
1 #CAP-PAY-STATUS  (A2)
1 #CAP-MODE        (A1)
1 #CAP-PLAN        (A3)
1 #NO-STORE        (L)
1 #UNDER-AGE-22    (L)
1 #NO-PENDING      (L)
1 #NO-FINAL        (L)
1 #NO-EXISTING-RET  (L)
1 #DO-NOT-UPDATE    (L)
1 #NO-OWNER       (L)
1 #SCR-ID-N         (N6)
1 REDEFINE #SCR-ID-N
  2 #SCR-ID-A        (A6)
1 #I             (I1)
1 #I1            (I1)
1 #I2            (I1)
1 #PROCESS-FLAG  (L) INIT<FALSE>
1 #TEXT          (A75)
1 #DOB           (N8)
1 #NAME          (A25)
1 #NAME-X        (A45)
1 #FIRST/MID     (A25)
1 #SUFFIX        (A25)
1 #EXTRA1        (A25)
1 #EXTRA2        (A25)
1 #RANK          (A3)
1 #RANK-DISPLAY  (A5)
1 #XXX           (A3) INIT <'XXX'>
1 #PASS-COMMAND  (A2) INIT <'AD'>
1 #RANK-LITERAL  (A23)
1 #EDIT-INVALID  (L)
1 #DISPL         (P2)
1 #TYPE          (A3)
1 REDEFINE #TYPE
  2 #TYPE-2  (A2)
  2 #TYPE-3  (A1)
*
1 #YYYYMMDD      (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY        (N4)
  2 REDEFINE #YYYY
    3 #CENT       (N2)
    3 #YY         (N2)
  2 #MMDD        (N4)
  2 REDEFINE #MMDD
    3 #MM          (N2)
    3 #DD          (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A      (A4)
  2 #MM-A        (A2)
  2 #DD-A        (A2)
*
1 #MMDDYYYY-A    (A8)
1 REDEFINE #MMDDYYYY-A
  2 #MM-Z        (A2)
  2 #DD-Z        (A2)
  2 #YYYY-Z      (A4)
*
1 MR-R01 VIEW OF REQUEST
  2 ID-NUMBER        (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A    (A6)
  2 MEMBER-NAME      (A20)
  2 CLERK-ID         (A3)
  2 ZIP-CODE         (N5)
  2 REDEFINE ZIP-CODE
    3 FIR-IND         (N1)
    3 MSG-IND         (N1)
    3 RET-IND         (N1)
    3 FIN-IND         (N1)
    3 FAS-IND         (N1)
  2 RQST-TABLE       (A10)   /* add to g3601pmr also
  2 REDEFINE RQST-TABLE
    3 BEN-IND         (A1)
    3 MAL-IND         (A1)   /* mbr accts letter
    3 BEN-SHO-IND     (A1)
    3 ACC-IND         (A1)
    3 CAP-IND         (A1)
    3 APP-IND         (A1)
    3 PIL-IND         (A1)
    3 CO-IND          (A1)  /* created by "co"
    3 VP-IND          (A1)
    3 MS-IND          (A1)
  2 MESSAGE          (A60/20)
  2 REQUEST-DATE     (N4)
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM-N   (N2)
    3 REQUEST-DD     (A2)
  2 ASSUM-RET-DATE   (N8)
  2 REDEFINE ASSUM-RET-DATE
    3 ASSUM-RET-DATE-X   (N2/4)
*
  2 ASSUM-RET-DATE-2 (N8)
  2 REDEFINE ASSUM-RET-DATE-2
    3 ASSUM-RET-DATE-2-X (N1/8)
  2 REDEFINE ASSUM-RET-DATE-2
    3 ASSUM-RET-DATE-2-2X (N2/4)
  2 ASSUM-RET-DATE-3 (N8)
  2 REDEFINE ASSUM-RET-DATE-3
    3 ASSUM-RET-DATE-3-X (N1/8)
  2 REDEFINE ASSUM-RET-DATE-3
    3 ASSUM-RET-DATE-3-2X (N2/4)  /* (1) USED FOR VAR TERM TERM
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 CONTACT-ID                   /* TT1
  2 INTRO-FLAG
*  2 ADDRESS-1                  /* TT1
  2 NAME
  2 SEX
  2 SSN
  2 DATE-OF-BIRTH
  2 RANK
  2 MEMBER-CN
  2 FACE-AMT-DOLS
  2 FACE-AMT-UNITS
  2 PLAN
*
1 ST-V VIEW OF A-STATUS
  2 INTRO-FLAG
  2 MEMBER-CONTACT-ID           /* TT1
*   2 DATE-OF-BIRTH             /* TT1
  2 PROCESS-IND
*
1 MR-S01 VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID            /* TT1
  2 WIDOW-FLAG
  2 INTRO-FLAG
*  2 SEX
  2 PROCESS-IND
  2 SUSPEND-CODE
*  2 DATE-OF-BIRTH
  2 NUMBER-MEMBERSHIPS
  2 RESTRICT-MBR-SW
  2 NAME
  2 NAME-SP
*  2 DATE-OF-BIRTH-SP
  2 DEBIT-CREDIT-AMT
  2 SSN
  2 SSN-SP
  2 STATUS-CODE-SP
  2 X-LPO-PMT-RCVD    (9)
  2 X-LPO-PMT-RCVD-2  (9)
  2 RANK
  2 HANDLE-FLAG
*  2 ADDRESS-CHG-CODE
*  2 ADDRESS-1
  2 S-ADDRESS-CODE
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
  2 CHIEF-OF-STAFF-IND
  2 DATE-RETIRED
  2 STATUS       (9)
  2 MODE         (9)
  2 SMOKER-FLAG  (9)
  2 PLAN         (9)
  2 PAY-STATUS   (9)
  2 EXPIRY-DAY   (9)
  2 FACE-AMOUNT  (9)
  2 CONV-DURATION (9)
*
1 IM-V VIEW OF A-INTRO-MEMBER
  2 ID-NUMBER        (N6)
  2 NAME             (A25)
  2 SSN
  2 DATE-OF-BIRTH
  2 RANK             (A3)
  2 ADDRESS-1        (A25)
*
1 #REC-NOT-FND (L)
1 #MBR-FND (L)
1 #AT-FND (L)
1 #IM-FND (L)
*
1 #DATE-OF-BIRTH    (N8.0)            /* TT1
1 #DATE-OF-BIRTH-SP (N8.0)            /* TT1
1 #ST-DATE-OF-BIRTH (N8.0)            /* TT1
1 #SPOUSE-SEX       (A1)              /* TT1
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX         /* NATURAL ERROR ROUTINE
*
RESET #CGA-ISN
*
IF #CGA-SCR-ID LT 999999
  MOVE #CGA-SCR-ID TO #CGA-ID
END-IF
*
IF (#CGA-SCR-ID LT 500000)   /* MEMBER
    OR ((#CGA-SCR-ID GE 600000) AND (#CGA-SCR-ID LT 700000)) /* AFSC MBR
  FIND (1) MR-S01 WITH MEMBER-KEY = #CGA-ID-G
    IF NO RECORDS FOUND
      FIND (1) MR-S01 WITH WIDOW-KEY = #CGA-ID-G
        IF NO RECORDS FOUND
          MOVE TRUE TO #REC-NOT-FND
        END-NOREC
      END-FIND
    END-NOREC
*
    IF NOT #REC-NOT-FND
      MOVE TRUE TO #MBR-FND
*
      RESET PDA-CONTACT #DATE-OF-BIRTH #SPOUSE-SEX #DATE-OF-BIRTH-SP
      P-CONTACT-ID := MR-S01.MEMBER-CONTACT-ID 
      CALLNAT 'CONTS001' PDA-CONTACT #DATE-OF-BIRTH-SP #SPOUSE-SEX
      #DATE-OF-BIRTH   := P-DATE-OF-BIRTH
*
      IF #CGA-SCR-SUFFIX = MASK ('AS')
        MOVE MR-S01.NAME-SP TO #NAME
        MOVE MR-S01.SSN-SP  TO #SSN
        RESET #RANK
        MOVE 'S' TO #INTRO-FLAG
        MOVE #DATE-OF-BIRTH-SP TO #DOB
      ELSE
        MOVE MR-S01.NAME TO #NAME
        MOVE MR-S01.SSN  TO #SSN
        MOVE MR-S01.RANK TO #RANK
        MOVE MR-S01.INTRO-FLAG TO #INTRO-FLAG
        MOVE #DATE-OF-BIRTH TO #DOB
      END-IF
*
      MOVE *ISN TO #CGA-ISN
    END-IF
  END-FIND
ELSE
  IF #CGA-SCR-ID LT 600000   /* AT RECORD (5XXXXX)
    FIND (1) AT-V WITH ID-NUMBER = #CGA-ID
      IF NO RECORDS FOUND
        MOVE TRUE TO #REC-NOT-FND
      END-NOREC
*
      MOVE TRUE TO #AT-FND
      MOVE AT-V.NAME TO #NAME
      MOVE AT-V.SSN  TO #SSN
      MOVE AT-V.RANK TO #RANK
      MOVE AT-V.INTRO-FLAG TO #INTRO-FLAG
      MOVE AT-V.DATE-OF-BIRTH TO #DOB
      MOVE *ISN TO #CGA-ISN
    END-FIND
  ELSE
    IF #CGA-SCR-ID LT 800000   /* IM record (7XXXXX)
      FIND (1) IM-V WITH ID-NUMBER = #CGA-ID
        IF NO RECORDS FOUND
          MOVE TRUE TO #REC-NOT-FND
        END-NOREC
        MOVE TRUE TO #IM-FND
        MOVE IM-V.NAME TO #NAME
        MOVE IM-V.SSN  TO #SSN
        MOVE IM-V.RANK TO #RANK
        MOVE IM-V.DATE-OF-BIRTH TO #DOB
        RESET #INTRO-FLAG
        MOVE *ISN TO #CGA-ISN
      END-FIND
    ELSE
      MOVE TRUE TO #REC-NOT-FND
      FIND MR-S01 WITH SSN = #CGA-SCR-ID
        IF NOT MR-S01.PROCESS-IND = 'G' OR = #CGA-USER-COMPANY
          ESCAPE TOP
        END-IF
*       
      RESET PDA-CONTACT #SPOUSE-SEX #DATE-OF-BIRTH
      P-CONTACT-ID := MR-S01.MEMBER-CONTACT-ID 
      CALLNAT 'CONTS001' PDA-CONTACT #DATE-OF-BIRTH-SP #SPOUSE-SEX
      #DATE-OF-BIRTH   := P-DATE-OF-BIRTH
*      
        MOVE TRUE TO #MBR-FND
        MOVE MR-S01.INTRO-FLAG TO #INTRO-FLAG
        MOVE MR-S01.NAME TO #NAME
        MOVE MR-S01.SSN  TO #SSN
        MOVE MR-S01.RANK TO #RANK
        MOVE MR-S01.ID-NUMBER TO #CGA-SCR-ID
        MOVE #DATE-OF-BIRTH TO #DOB                           /* TT1
        MOVE *ISN TO #CGA-ISN   /* FOR MEMBER RECORD
        MOVE FALSE TO #REC-NOT-FND
        ESCAPE BOTTOM
      END-FIND
*
      IF #REC-NOT-FND
        FIND AT-V WITH SSN = #CGA-SCR-ID
          IF #CGA-USER-COMPANY = 'S'
            IF AT-V.INTRO-FLAG NE 'V'
              ESCAPE TOP
            END-IF
          ELSE
            IF AT-V.INTRO-FLAG EQ 'V'
              ESCAPE TOP
            END-IF
          END-IF
          MOVE TRUE TO #AT-FND #FROM-AT
          MOVE AT-V.NAME TO #NAME
          MOVE AT-V.SSN  TO #SSN
          MOVE AT-V.RANK TO #RANK
          MOVE AT-V.INTRO-FLAG TO #INTRO-FLAG
          MOVE AT-V.ID-NUMBER TO #CGA-SCR-ID
          MOVE AT-V.DATE-OF-BIRTH TO #DOB
          MOVE *ISN TO #CGA-ISN
          MOVE FALSE TO #REC-NOT-FND
          ESCAPE BOTTOM
        END-FIND
      END-IF
*
      IF #REC-NOT-FND
        FIND (1) IM-V WITH SSN = #CGA-SCR-ID
          MOVE TRUE TO #IM-FND #FROM-IM
          MOVE IM-V.NAME TO #NAME
          MOVE IM-V.SSN  TO #SSN
          MOVE IM-V.RANK TO #RANK
          MOVE IM-V.DATE-OF-BIRTH TO #DOB
          RESET #INTRO-FLAG
          MOVE IM-V.ID-NUMBER TO #CGA-SCR-ID
          MOVE *ISN TO #CGA-ISN
          MOVE FALSE TO #REC-NOT-FND
        END-FIND
      END-IF
    END-IF
  END-IF
END-IF
*
IF #CGA-SCR-ID LT 999999
  MOVE #CGA-SCR-ID TO #CGA-ID
  MOVE #CGA-SCR-ID TO #CGA-ORIG-ID
END-IF
*
IF #MBR-FND AND (MR-S01.WIDOW-FLAG = 'Y')
  MOVE MR-S01.NAME-SP TO #NAME
  RESET #RANK
END-IF
*
IF #REC-NOT-FND
  INPUT TEXT 'CN/AT/IM NUMBER or SSN not found' MARK *#CGA-SCR-COMMAND
    USING MAP 'G1000MXX'
  FETCH 'G1000PXX'
END-IF
*
IF (#CGA-SCR-ID GE 100) AND (#CGA-SCR-ID LE 149)
  INPUT TEXT 'Employee "MR"s are not allowed' MARK *#CGA-SCR-COMMAND
    USING MAP 'G1000MXX'
  FETCH 'G1000PXX'
END-IF
*
IF #CGA-SCR-SUFFIX = 'BEN'
    OR (#CGA-SCR-SUFFIX = MASK ('BN'))
  FETCH 'G3601PMR'
END-IF
*
RESET #HOLD-MSG (*)
**                       PM CHG 05/2000 TO FREE NOTE REC 600000-700000
FIND (1) MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  FOR #I1 1 TO 8
    MOVE MR-R01.MESSAGE (#I1) TO #MR-MSG (#I1)
    MOVE MR-R01.MESSAGE (#I1) TO #HOLD-MSG (#I1)
  END-FOR
END-FIND
*
RESET #DISPL #RANK-LITERAL #RANK-DISPLAY                          /* PM2
CALLNAT 'GRANKSXX' #PASS-COMMAND #RANK #XXX #EDIT-INVALID
  #DISPL #RANK-LITERAL #RANK-DISPLAY
MOVE #RANK-LITERAL TO #FULL-RANK
SEPARATE #NAME INTO #LAST-NAME #FIRST/MID
  #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER ','
COMPRESS #FULL-RANK #LAST-NAME INTO #NAME-X
COMPRESS #NAME-X ',' INTO #NAME-X LEAVING NO
*
MOVE '*' TO #CGA-SCR-COMMAND
MOVE #CGA-SCR-ID TO #SCR-ID-N
*
* MSG/DOP/VLT/ should not be in list below
IF #CGA-SCR-SUFFIX = 'DEL' OR = 'PUP' OR = 'ACB' OR = 'ACS' OR = 'PIL'
    OR = 'APL' OR = 'FAL'   OR = 'RFS' OR = 'RFC' OR = 'RFZ'
    OR = 'LPF' OR = 'LPV' /* no longer in batch 1/1/2002 -added 5/27/2002
    OR = 'LPB'            /* both LPF & LPV
    OR = 'VP'                          /* only DRW/PAM
    OR = 'MAM'   /* mbr-acct-menu
    OR = 'CAP'
  MOVE #CGA-SCR-SUFFIX TO #TYPE
*
  IF #CGA-SCR-SUFFIX = 'MAM'
    PERFORM MBR-ACCTS-LTR-MENU
  END-IF
*
  PERFORM PROCESS-LINE
*
  IF #TEXT = ' '
    DECIDE ON FIRST #TYPE
      VALUE 'PUP' MOVE 'Paid-up Info Requested with APP'    TO #TEXT
      VALUE 'ACB' MOVE 'Access-to-file Form Requested'      TO #TEXT
      VALUE 'ACS' MOVE 'Access-to-file -SPS Form Requested' TO #TEXT
      VALUE 'PIL' MOVE 'Policy Information Letter Requested' TO #TEXT
      VALUE 'APL' MOVE 'Auto Prem Loan Reminder Requested'  TO #TEXT
      VALUE 'FAL' MOVE 'File Allotment Form Requested'      TO #TEXT
      VALUE 'RFS' MOVE 'Refund Allotment due to Start Rqst' TO #TEXT
      VALUE 'RFC' MOVE 'Refund Allotment due to Change Rqst' TO #TEXT
      VALUE 'RFZ' MOVE 'Refund EZ-pay due to Start'         TO #TEXT
      VALUE 'LPF' MOVE 'Loan Pmt for Fixed Loan'            TO #TEXT
      VALUE 'LPV' MOVE 'Loan Pmt for Variable Loan'         TO #TEXT
      VALUE 'LPB' MOVE 'Loan Pmt for Fixed & Var Loan'      TO #TEXT
      VALUE 'VP ' MOVE 'Value-added Projection'             TO #TEXT
      VALUE 'CAP' MOVE 'CAP Options letter Requested'  TO #TEXT
      NONE IGNORE
    END-DECIDE
  END-IF
*
  IF #TYPE = 'VP'
    COMPRESS #TEXT '- Press ENTER to see VP"s and Apps' INTO #TEXT
    MOVE '*' TO #CGA-SCR-COMMAND
  ELSE
    MOVE 'MR' TO #CGA-SCR-COMMAND
  END-IF
*
  MOVE (AD=P) TO #SCR-CV (*) #SCR-CV-X
  MOVE (AD=P) TO #MR-MSG-CV (*)
*
  INPUT TEXT #TEXT MARK *#CGA-SCR-COMMAND USING MAP 'G3600MMR'
*
  IF (#TYPE = 'VP') AND (#CGA-SCR-COMMAND = '*')
      AND (#CGA-SCR-ID = #CGA-ORIG-ID)
    PERFORM SHOW-VP-APP
  END-IF
*
  FETCH 'G1000PXX'
END-IF
*
MOVE (AD=P) TO #SCR-CV (9:9) #SCR-CV (19:20) /* <<<<<<<<<<<
MOVE (AD=P) TO #SCR-CV (3) #SCR-CV (18) /* a/o 9/2003 bmg
MOVE (AD=P) TO #SCR-CV(1:2) #SCR-CV(6) #scr-cv(8) #scr-cv(11) #scr-cv(13:17) /* DW4

RESET #CGA-SCR-SUFFIX
IF #TEXT = ' '
  MOVE 'Select type of "MR"' TO #TEXT
END-IF
INPUT TEXT #TEXT MARK *#SCR-MR-TYPE (1) USING MAP 'G3600MMR'
*
IF  (#CGA-SCR-COMMAND NE '*')
    OR (#CGA-SCR-ID NE #CGA-ORIG-ID)
  FETCH 'G1000PXX'
END-IF
*
RESET #MR-X #MR-MSG-CHGED #MR-MSG-FND
FOR #I1 1 TO 8
  IF #MR-MSG (#I1) NE ' '
    MOVE TRUE TO #MR-MSG-FND
  END-IF
*
  IF #MR-MSG (#I1) NE #HOLD-MSG (#I1)
    MOVE TRUE TO #MR-MSG-CHGED
  END-IF
END-FOR
*
RESET #MR-X
FOR #I1 1 TO 20
  IF #SCR-MR-TYPE (#I1) NE ' '
    IF #SCR-MR-TYPE (#I1) = '*'
      REINPUT '* invalid here'
        MARK *#SCR-MR-TYPE (#I1)
    END-IF
    MOVE #I1 TO #MR-X
  END-IF
END-FOR
*
IF   (#MR-X = 0)
    AND (NOT #MR-MSG-CHGED)
  REINPUT 'Select an option or send a message'
    MARK *#SCR-MR-TYPE (1)
END-IF
*
RESET #TEXT
*
IF #MR-MSG-CHGED
  MOVE 'MSG' TO #TYPE
  PERFORM PROCESS-LINE
END-IF
*
FOR #MR-X 1 TO 20
  IF   (#SCR-MR-TYPE (#MR-X) NE ' ')
      AND (#SCR-MR-TYPE (#MR-X) NE '*')
    DECIDE ON FIRST #MR-X
*     VALUE 3  PERFORM APPLICATION-MENU
      VALUE 4  MOVE 'PUP'  TO #TYPE
      VALUE 5  PERFORM MBR-ACCTS-LTR-MENU
      VALUE 7  MOVE 'CAP'  TO #TYPE
*
      VALUE 10 IF #ACS-IND = 'Y'
          MOVE 'ACS' TO #TYPE
        ELSE
          MOVE 'ACB' TO #TYPE
        END-IF
      VALUE 12 MOVE 'DEL'  TO #TYPE
*
      VALUE 18 MOVE 'VP'   TO #TYPE
      NONE IGNORE
    END-DECIDE
*
    IF #MR-X = 10
        AND NOT #ACS-IND = 'Y' OR = 'N'
      REINPUT 'Spouse (Y/N) must be Y or N'
        MARK *#ACS-IND
    END-IF
*
    RESET #TEXT
    PERFORM PROCESS-LINE
*
    IF #TEXT = ' '
      MOVE '*' TO #SCR-MR-TYPE (#MR-X)
    ELSE
      ESCAPE BOTTOM
    END-IF
  END-IF
END-FOR
END TRANSACTION
*
IF #TEXT = ' '
  MOVE 'Action(s) requested have been processed' TO #TEXT
END-IF
*
MOVE (AD=P) TO #SCR-CV (*) #SCR-CV-X
MOVE (AD=P) TO #MR-MSG-CV (*)
IF (#TYPE = 'VP') AND (#CGA-SCR-COMMAND = '*')
  COMPRESS #TEXT '- Press ENTER to see VP"s and Apps' INTO #TEXT
  MOVE '*' TO #CGA-SCR-COMMAND
ELSE
  MOVE 'MR' TO #CGA-SCR-COMMAND
END-IF
*
INPUT TEXT #TEXT MARK *#CGA-SCR-COMMAND USING MAP 'G3600MMR'
*
IF (#TYPE = 'VP') AND (#CGA-SCR-COMMAND = '*')
    AND (#CGA-SCR-ID = #CGA-ORIG-ID)
  PERFORM SHOW-VP-APP
  MOVE 'MR' TO #CGA-SCR-COMMAND
END-IF
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-LINE
* * * * * * * * * * * * * * * *
*
IF   (#TYPE = 'VP') AND (NOT #CGA-CLERK = 'DRW' OR = 'VXT')   /* vt1
  COMPRESS 'VP no longer valid' INTO #TEXT
  ESCAPE ROUTINE
END-IF
*
IF #TYPE = 'DEL'
  FIND NUMBER MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
*
  IF *NUMBER = 0
    MOVE 'DELETE REJECTED - RECORD NOT REQUESTED' TO #TEXT
  ELSE
    FIND (1) MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
      RESET #MR-CNT
      DECIDE FOR EVERY CONDITION
        WHEN MSG-IND NE 0   ADD 1 TO #MR-CNT
        WHEN BEN-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN MAL-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN BEN-SHO-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN ACC-IND NE ' ' ADD 1 TO #MR-CNT
*       WHEN APP-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN PIL-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN CO-IND NE ' '  ADD 1 TO #MR-CNT
*       WHEN VP-IND NE ' '  ADD 1 TO #MR-CNT
        WHEN CAP-IND NE ' ' ADD 1 TO #MR-CNT
        WHEN NONE IGNORE
      END-DECIDE
*
      IF #MR-CNT GT 1
        MOVE 'N' TO #YN
        FORMAT IP=OFF
        INPUT // 'Multiple Requests exist - You may want to check "ML"'
          // 'Enter "Y" to continue the delete'
          // 'or "N" to bypass delete' #YN
        IF NOT (#YN = 'Y' OR = 'y')
          INPUT TEXT 'Delete process bypassed' MARK *#CGA-SCR-COMMAND
            USING MAP 'G1000MXX'
          FETCH 'G1000PXX'
        END-IF
      END-IF
*
      IF   (REQUEST-MM-N GT 50)
          AND (#TYPE NE 'DEL')
        MOVE 'EDS "MR" request run is in progress - try later' TO #TEXT
      ELSE
        DELETE (0674)
      END-IF
*
      END TRANSACTION
    END-FIND /* (0674)
*
*   IF REQUEST-DATE NE 9999
    IF REQUEST-MM-N LT 50
      COMPRESS #MR-CNT 'Request(s) DELETED' INTO #TEXT
    END-IF
  END-IF
*
  ESCAPE ROUTINE
END-IF
*
RESET #NO-STORE
*
RESET #FOUND-LPX
IF #TYPE = 'LPF' OR = 'LPB'
  FOR #I1 1 TO 9
    IF X-LPO-PMT-RCVD (#I1) NE 0
      MOVE TRUE TO #FOUND-LPX
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF NOT #FOUND-LPX
    MOVE 'No LPF loan payments found' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
RESET #FOUND-LPX
IF #TYPE = 'LPV' OR = 'LPB'
  FOR #I1 1 TO 9
    IF X-LPO-PMT-RCVD-2 (#I1) NE 0
      MOVE TRUE TO #FOUND-LPX
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF NOT #FOUND-LPX
    MOVE 'No LPV loan payments found' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
* below never used a/o 9/2003 bmg
IF #TYPE = 'AS ' OR = 'ASN' OR = 'AST' OR = 'ASG' OR = 'AMS'
  RESET #BYPASS
  FIND ST-V WITH SSN = MR-S01.SSN-SP
    IF NOT ST-V.PROCESS-IND EQ 'G' OR = MR-S01.PROCESS-IND
      ESCAPE TOP
    END-IF
*
  RESET #ST-DATE-OF-BIRTH
  #ST-DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)
    IF #ST-DATE-OF-BIRTH = #DATE-OF-BIRTH                           /* TT1 Comparing ST and MR-S01
      MOVE TRUE TO #BYPASS /* sps already has ins or is a member
      ESCAPE BOTTOM
    END-IF
  END-FIND
*
  FIND AT-V WITH SSN = MR-S01.SSN-SP
    IF #CGA-USER-COMPANY = 'S'
      IF AT-V.INTRO-FLAG NE 'V'
        ESCAPE TOP
      END-IF
    ELSE
      IF AT-V.INTRO-FLAG EQ 'V'
        ESCAPE TOP
      END-IF
    END-IF
*
    IF AT-V.DATE-OF-BIRTH = #DATE-OF-BIRTH-SP
      MOVE TRUE TO #BYPASS /* sps is applying for ins
      ESCAPE BOTTOM
    END-IF
  END-FIND
*
  IF #BYPASS
    MOVE 'REJECTED - Sps already has insurance or "AT" rec' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF MR-S01.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    MOVE 'REJECTED - Sps App on a SPS/GEN invalid' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF MR-S01.SUSPEND-CODE = '7' OR = '8' OR = '9'
    MOVE 'REJECTED - for insurance/medical' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF (#DATE-OF-BIRTH-SP LE *DATN - 710000)                 /* TT1
      OR (#DATE-OF-BIRTH-SP = 0)                           /* TT1
      OR (MR-S01.STATUS-CODE-SP = 'D')
    MOVE 'REJECTED - Sps is over age 71 or deceased' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF ((#TYPE = 'PUP' OR = 'APL' OR = 'RFS' OR = 'RFC' OR = 'RFZ')
    OR (#TYPE = 'APP' OR = 'APN' OR = 'APT' OR = 'AMS' OR = 'CAP'))
    AND (#MBR-FND)
  RESET #NUM-PAYING-POLS
  RESET #TOT-FACE
  RESET #ASQ
  RESET #MODE-G
  RESET #MODE-E
  RESET #FND-999 #NUM-POLS #CAP-FACE-AMOUNT #CAP-PAY-STATUS #CAP-PLAN
  RESET #CAP-MODE
*
  FOR #I1 1 TO 9
    IF STATUS (#I1) = 'D'
      ADD 1 TO #NUM-POLS
    END-IF
*
    IF CONV-DURATION (#I1) = 999
      MOVE TRUE TO #FND-999
      MOVE FACE-AMOUNT (#I1) TO #CAP-FACE-AMOUNT
      MOVE PAY-STATUS  (#I1) TO #CAP-PAY-STATUS
      MOVE MR-S01.PLAN (#I1) TO #CAP-PLAN
      MOVE MODE        (#I1) TO #CAP-MODE
    END-IF
*
    IF MODE (#I1) = 'G'
      MOVE TRUE TO #MODE-G
    END-IF
*
    IF MODE (#I1) = 'E'
      MOVE TRUE TO #MODE-E
    END-IF
*
    IF MODE (#I1) = 'A' OR = 'S' OR = 'Q' OR = 'M'
        OR = '0' OR = '1' OR = '2' OR = '4'  /* DW1
      MOVE TRUE TO #ASQ
    END-IF
*
    IF STATUS (#I1) = 'D'
      IF PAY-STATUS (#I1) = '45'
        COMPUTE #TOT-FACE = #TOT-FACE +  (EXPIRY-DAY (#I1) * 5000)
      ELSE
        ADD FACE-AMOUNT (#I1) TO #TOT-FACE
      END-IF
    END-IF
*
    IF  (STATUS (#I1) NE 'D')
        OR (PAY-STATUS (#I1) GE '42')
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-PAYING-POLS
  END-FOR
*
  CALLNAT 'GMAXSXX'     /* obtain maximum amounts per insurance line
    #CGA-ORIG-COMMAND
    #MAX-FACE
    #MAX-FACE-TERM
    #OVERRIDE-MAX-FACE-TOTAL
    #OVERRIDE-MAX-FACE-TERM
    #INTRO-FLAG
    #DOB    /* DW3
*
  IF #TYPE = 'PUP'
    IF   ((#TOT-FACE GT (#MAX-FACE - 5000))
        OR (#DOB LE *DATN - 710000))
        AND (#NUM-PAYING-POLS = 0)
      MOVE 'REJECTED - No pols to pay up & has max units' TO #TEXT
      ESCAPE ROUTINE
    END-IF
  END-IF
*
  IF #TYPE = 'APP' OR = 'APN' OR = 'APT' OR = 'AMS'
    IF #TOT-FACE GT (#MAX-FACE - 5000)
      MOVE 'REJECTED - Has maximum coverage' TO #TEXT
      ESCAPE ROUTINE
    END-IF
*
    IF ((#DOB LE *DATN - 710000)
        AND (NOT #INTRO-FLAG = 'K' OR = 'G'))
        OR ((#DOB LE *DATN - 240000)
        AND (#INTRO-FLAG = 'K' OR = 'G'))
      MOVE 'REJECTED - due to age' TO #TEXT
      ESCAPE ROUTINE
    END-IF
  END-IF
*
  IF (#TYPE = 'APT' OR = 'AST') AND (#DOB GE *DATN - 160000)
    MOVE 'REJECTED - No Smoker rates for UNDER age 16' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'APL')
      AND (NOT #ASQ)
    MOVE 'REJECTED - Mode must be A/S/Q' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'RFZ')
      AND (NOT #MODE-E)
    MOVE 'REJECTED - Mode must be E' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'RFS' OR = 'RFC')
      AND (NOT #MODE-G)
    MOVE 'REJECTED - Mode must be G' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'RFS' OR = 'RFC' OR = 'RFZ')
      AND (DEBIT-CREDIT-AMT = 0)
    MOVE 'REJECTED - DEBIT-CREDIT cannot be zero' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'CAP')
    IF (NOT MR-S01.INTRO-FLAG = 'B' OR = 'C' OR = 'P' OR = 'Q')
      MOVE 'REJECTED - Must be a CAP Mbr' TO #TEXT
      ESCAPE ROUTINE
    END-IF
  END-IF
END-IF
*
IF #TYPE = 'ACS'
  IF MR-S01.NAME-SP = ' '
    MOVE 'REJECTED - No spouse exists' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF MR-S01.STATUS-CODE-SP = 'D'
    MOVE 'REJECTED - Spouse is deceased' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF MR-S01.STATUS-CODE-SP = 'F'
    MOVE 'REJECTED - Spouse is former' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF   (#TYPE = 'FAL')
    AND (MILITARY-STATUS NE 'A')
  MOVE 'Mbr must be active for "FAL"' TO #TEXT
  ESCAPE ROUTINE
END-IF
*
* below never used a/o 9/2003 bmg
IF #TYPE = 'VP '
  IF #DOB = 0
    MOVE 'Date of birth needed for projections' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF #DOB LE *DATN - 850000   /* WAS 71
    MOVE 'VP Rejected due to age' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  MOVE 'N' TO #VP-W-APP (*) #VP-APP-DUES (*)
*
  MOVE '*' TO #CGA-SCR-COMMAND
  INPUT MARK *#VP-FACE (1) USING MAP 'G3605MMR'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET #VP-SEL
  FOR #I1 1 TO 4
    IF #VP-FACE (#I1) = 0
      IF  (#VP-PLAN (#I1) NE 0)
          OR (#VP-SMOK (#I1) NE ' ')
          OR (#VP-W-APP (#I1)  = 'Y')
          OR (#VP-APP-DUES (#I1) = 'Y')
        REINPUT 'Face amt must be entered'
          MARK *#VP-FACE (#I1)
      END-IF
*
      ESCAPE TOP
    END-IF
    MOVE TRUE TO #VP-SEL
*
    IF  (#VP-FACE (#I1) LT 10)
        OR (#VP-FACE (#I1) GT 300)
      REINPUT 'Invalid Face Amount'
        MARK *#VP-FACE (#I1)
    END-IF
*
    DIVIDE 5 INTO #VP-FACE (#I1) GIVING #DIV REMAINDER #REM
    IF #REM NE 0
      REINPUT 'Face amt must be a multiple of 5'
        MARK *#VP-FACE (#I1)
    END-IF
*
    MOVE *DATN TO #YYYYMMDD
    SUBTRACT #DOB FROM #YYYYMMDD
*
    IF   (NOT #VP-PLAN (#I1) = 99 OR = 00)
        AND (#VP-PLAN (#I1) GT (100 - #YYYY))
      REINPUT 'Term exceeds age 100'
        MARK *#VP-PLAN (#I1)
    END-IF
*
    IF NOT #VP-SMOK (#I1) = 'S' OR = 'N'
      REINPUT 'Invalid Smoker Flag'
        MARK *#VP-SMOK (#I1)
    END-IF
*
    IF NOT #VP-W-APP (#I1) = 'Y' OR = 'N'
      REINPUT 'Must be "Y" or "N"'
        MARK *#VP-W-APP (#I1)
    END-IF
*
    IF   (#VP-W-APP (#I1) = 'Y')
        AND (NOT #VP-PLAN (#I1) = 0 OR = 7 OR = 20 OR = 30 OR = 99)
      REINPUT 'Plan cannot be FLEX to get an APP'
        MARK *#VP-W-APP (#I1)
    END-IF
*
    IF (#VP-APP-DUES (#I1) = 'Y') AND (#VP-W-APP (#I1) NE 'Y')
      REINPUT 'App must be selected if "No dues" is selected'
        MARK *#VP-W-APP (#I1)
    END-IF
*
    IF NOT #VP-SEL AND #VP-CURR = ' '
      REINPUT 'Make a selection'
        MARK *#VP-CURR
    END-IF
  END-FOR
*
  IF #VP-CURR = 'Y'
    IF #AT-FND
      IF ((AT-V.FACE-AMT-UNITS = 0) AND (AT-V.FACE-AMT-DOLS = 0))
          OR (AT-V.PLAN = ' ')
        REINPUT 'AT-Rec must have Face Amount and Plan'
          MARK *#VP-CURR
      END-IF
    ELSE
      IF NUMBER-MEMBERSHIPS = 0
        REINPUT 'Must have current insurance'
          MARK *#VP-CURR
      END-IF
    END-IF
  END-IF
END-IF
*
 IF  (#IM-FND AND (IM-V.ADDRESS-1 = ' '))
     OR (#AT-FND AND (F-UNDEL-ADDRESS(<AT-V.CONTACT-ID>)))          /* TT1
     OR (#MBR-FND
     AND ((F-UNDEL-ADDRESS(<MR-S01.MEMBER-CONTACT-ID>))))           /* TT1
   MOVE 'REJECTED - no deliverable address' TO #TEXT
   ESCAPE ROUTINE
 END-IF
*
IF  (#TYPE = 'APP' OR = 'APN' OR ='APT' OR = 'AMS')
  IF #AT-FND AND (#DOB = 0)
    MOVE 'Appl Tracking DOB missing' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF RESTRICT-MBR-SW = 'Y'
    MOVE 'REJECTED - MEMBER IS SPECIAL-CATAGORY' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF SUSPEND-CODE = '5' OR = '6' OR = '9'
    MOVE 'REJECTED - for insurance/medical' TO #TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*
* below never used a/o 9/2003 bmg
IF ((#TYPE = MASK ('AP')) OR (#TYPE = MASK ('AS')) OR (#TYPE = 'AMS'))
    AND (#TYPE NE 'APL')
*
  IF #TYPE = MASK ('AS')
    MOVE MR-S01.NAME-SP TO #NAME
    MOVE MR-S01.SSN-SP  TO #SSN
    MOVE #DATE-OF-BIRTH-SP TO #DOB                            /* TT1
  END-IF
*
  MOVE #CGA-ORIG-COMMAND TO #PDA-SCR-COMMAND
  MOVE #CGA-SCR-ID       TO #PDA-SCR-ID
  MOVE #TYPE             TO #PDA-SCR-SUFFIX
  MOVE *DATN TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SCN-AS-OF-DT LEAVING NO
*
  IF #TYPE = 'APT' OR = 'AST'
    MOVE 'S' TO #SMOKER-FLAG
  ELSE
    IF #TYPE = 'APN' OR = 'ASN'
      MOVE 'N' TO #SMOKER-FLAG
    ELSE
      MOVE 'N' TO #SMOKER-FLAG /* assume non-smoker unless "S" exists
      FOR #I1 1 TO 9
        IF MR-S01.SMOKER-FLAG (#I1) = 'S'
          MOVE 'S' TO #SMOKER-FLAG
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-IF
  END-IF
*
  MOVE #DOB TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SCN-DOB LEAVING NO
  MOVE #NAME TO #FORMATED-NAME
*
  IF #INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    MOVE #INTRO-FLAG TO #SCN-SPS-GEN
  END-IF
*
  IF #MBR-FND AND MR-S01.PLAN (1) = 'SO' OR = 'FSO'
    MOVE TRUE TO #SO-MBR
  END-IF
*
  IF #MBR-FND AND (NUMBER-MEMBERSHIPS = 0)
    MOVE TRUE TO #TERMED-MBR
  END-IF
*
  CALLNAT 'C2151SPO'   /* input override screen
    #PDA-AREA-C2150APO
    #SCN-AOD-YYYYMMDD
    #SCN-AS-OF-DT
    #DOB
    #SCN-DOB
    #SCN-SPS-GEN
    #SCN-SPS-GEN-CV
    #SSN
    #DESIRED-FACE-AMT (1:3)
    #DESIRED-PLAN     (1:10)
    #FIRST-TIME
    #FROM-AT
    #FROM-IM
    AT-V.MEMBER-CN
    #SO-MBR
    #TERMED-MBR
    MR-S01.RESTRICT-MBR-SW
    MR-S01.SUSPEND-CODE
    #VT-TYPE
    #VT-TERM
    #VT-RIDER
*  MR-S01.SEX
*
  MOVE #PDA-SCR-COMMAND TO #CGA-SCR-COMMAND
  MOVE #PDA-SCR-ID      TO #CGA-SCR-ID
  MOVE #PDA-SCR-SUFFIX  TO #CGA-SCR-SUFFIX
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  FOR #I1 1 TO 3
    IF #DESIRED-FACE-AMT (#I1) NE 0
      MOVE TRUE TO #APP-OVERRIDE
    END-IF
  END-FOR
*
  FOR #I1 1 TO 8   /* ONLY 8 USED
    IF #DESIRED-PLAN (#I1) NE ' '
      MOVE TRUE TO #APP-OVERRIDE
    END-IF
  END-FOR
END-IF
*
FIND (1) MR-R01 WITH ID-NUMBER   = #CGA-SCR-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
* IF REQUEST-DATE = 9999
  IF REQUEST-MM-N GT 50
    MOVE 'EDS "MR" request run is in progress - try later' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF   (#TYPE = 'VP')
      AND ((MR-R01.BEN-IND = '5' OR = '6' OR = '7' OR = '8')
      OR  (MR-R01.APP-IND NE ' ')
      OR  (MR-R01.RET-IND NE 0))
    MOVE 'VP not allowed same day as "BNP/BNN/BNT/BNL/RET or APP"'
      TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF   #APP-OVERRIDE
      AND ((MR-R01.BEN-IND = '5' OR = '6' OR = '7' OR = '8')
      OR  (MR-R01.RET-IND NE 0)
      OR  (MR-R01.VP-IND NE ' '))
    MOVE 'APP not allowed same day as "BNP/BNN/BNT/BNL/VP"'
      TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF #TYPE = 'APL' OR = 'FAL'
      OR = 'RFS' OR = 'RFC' OR = 'RFZ' OR = 'LPF' OR = 'LPV' OR = 'LPB'
    IF MR-R01.MAL-IND NE ' '
      MOVE '2 MBR-ACCTS letters not allowed same day' TO #TEXT
      ESCAPE ROUTINE
    END-IF
*
    IF NUMBER-MEMBERSHIPS = 0
      MOVE 'REJECTED - No Policies' TO #TEXT
      ESCAPE ROUTINE
    END-IF
  END-IF
*
  IF ((#TYPE = 'APP' OR = 'APN' OR = 'APT' OR = 'AS' OR = 'ASN'
      OR = 'AST' OR = 'ASG' OR = 'AMS'
      OR = 'AG1' OR = 'AG2' OR = 'AG3' OR = 'AG4' OR = 'AG5' OR = 'AG6'))
      AND (MR-R01.APP-IND NE ' ')
    MOVE 'Only 1 APP rqst per day' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  IF  (#TYPE = 'APP' OR = 'APN' OR = 'APT' OR = 'AMS')
      AND (MR-R01.APP-IND = 'P')
    MOVE '"PUP" which includes appl is already requested' TO #TEXT
    ESCAPE ROUTINE
  END-IF
*
  MOVE TRUE TO #NO-STORE
  ESCAPE BOTTOM
END-FIND /* (1173)
*
IF #NO-STORE
  FIND (1) MR-R01 WITH ID-NUMBER = #CGA-SCR-ID
    PERFORM BUILD-MR-REC
    UPDATE (1234)
  END-FIND
ELSE
  PERFORM BUILD-MR-REC
  STORE MR-R01
END-IF
*
END TRANSACTION
*
END-SUBROUTINE /* (0660)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-MR-REC
* * * * * * * * * * * * * * * *
*
MOVE #CGA-SCR-ID TO MR-R01.ID-NUMBER
MOVE #NAME       TO MR-R01.MEMBER-NAME
MOVE #CGA-CLERK  TO MR-R01.CLERK-ID
MOVE *DATN  TO #YYYYMMDD
MOVE #MMDD  TO MR-R01.REQUEST-DATE
*
IF #APP-OVERRIDE
  IF #SMOKER-FLAG = 'S'
    MOVE 2 TO ASSUM-RET-DATE-3-2X (1)
  ELSE
    MOVE 1 TO ASSUM-RET-DATE-3-2X (1)
  END-IF
*
  FOR #I1 1 TO 3
    IF #DESIRED-FACE-AMT (#I1) NE 0
      COMPUTE ASSUM-RET-DATE-X (#I1) = #DESIRED-FACE-AMT (#I1) / 5000
    END-IF
  END-FOR
*
  FOR #I1 1 TO 8    /* ONLY 8 USED
    IF #DESIRED-PLAN (#I1) NE ' '
      MOVE 1 TO ASSUM-RET-DATE-2-X (#I1)
*
      IF #I1 = 8  /* var term
        MOVE #VT-TERM TO ASSUM-RET-DATE-3-2X (2)
        DECIDE ON FIRST VALUE #VT-TYPE
          VALUE 'ST'
            IF #VT-RIDER = 'Y'
              MOVE 4 TO ASSUM-RET-DATE-2-X (#I1)
            ELSE
              MOVE 1 TO ASSUM-RET-DATE-2-X (#I1)
            END-IF
          VALUE 'SE'
            IF #VT-RIDER = 'Y'
              MOVE 5 TO ASSUM-RET-DATE-2-X (#I1)
            ELSE
              MOVE 2 TO ASSUM-RET-DATE-2-X (#I1)
            END-IF
          VALUE 'SS'
            IF #VT-RIDER = 'Y'
              MOVE 6 TO ASSUM-RET-DATE-2-X (#I1)
            ELSE
              MOVE 3 TO ASSUM-RET-DATE-2-X (#I1)
            END-IF
          VALUE '**'
            IF #VT-RIDER = 'Y'
              MOVE 8 TO ASSUM-RET-DATE-2-X (#I1)
            ELSE
              MOVE 7 TO ASSUM-RET-DATE-2-X (#I1)
            END-IF
          NONE IGNORE
        END-DECIDE
      END-IF
    END-IF
  END-FOR
END-IF
*
* below never used a/o 9/2003 bmg
IF #TYPE = 'VP'
  IF #VP-CURR = 'Y'
    MOVE '2' TO VP-IND
  ELSE
    MOVE '1' TO VP-IND
  END-IF
*
  RESET ASSUM-RET-DATE ASSUM-RET-DATE-2 ASSUM-RET-DATE-3
  FOR #I1 1 TO 3
    IF #VP-FACE (#I1) NE 0
      COMPUTE ASSUM-RET-DATE-X (#I1) = #VP-FACE (#I1) / 5
      MOVE #VP-PLAN (#I1) TO ASSUM-RET-DATE-2-2X (#I1)
*
      IF #VP-SMOK (#I1) = 'S'
        MOVE 2 TO ASSUM-RET-DATE-3-X (#I1)
      ELSE
        MOVE 1 TO ASSUM-RET-DATE-3-X (#I1)
      END-IF
*
      IF #VP-W-APP (#I1) = 'Y'
        IF #VP-CURR = 'Y'
          MOVE '4' TO VP-IND
        ELSE
          MOVE '3' TO VP-IND
        END-IF
*
        MOVE 1 TO ASSUM-RET-DATE-3-X (#I1 + 4)
        IF #VP-APP-DUES (#I1) = 'Y'
          MOVE 2 TO ASSUM-RET-DATE-3-X (#I1 + 4)
        END-IF
      END-IF
    END-IF
  END-FOR
END-IF
*
DECIDE ON FIRST VALUE #TYPE
  VALUE 'ACB' MOVE 1 TO MR-R01.ACC-IND
  VALUE 'ACS' MOVE 2 TO MR-R01.ACC-IND
  VALUE 'PUP' MOVE 'P' TO MR-R01.APP-IND
  VALUE 'PIL' MOVE 1 TO MR-R01.PIL-IND
  VALUE 'APL' MOVE 2 TO MR-R01.MAL-IND
  VALUE 'FAL' MOVE 3 TO MR-R01.MAL-IND
  VALUE 'RFS' MOVE 4 TO MR-R01.MAL-IND
  VALUE 'RFC' MOVE 5 TO MR-R01.MAL-IND
  VALUE 'RFZ' MOVE 6 TO MR-R01.MAL-IND
  VALUE 'LPF' MOVE 7 TO MR-R01.MAL-IND
  VALUE 'LPV' MOVE 8 TO MR-R01.MAL-IND
  VALUE 'LPB' MOVE 9 TO MR-R01.MAL-IND
  VALUE 'CAP' MOVE '1' TO MR-R01.CAP-IND
  NONE IGNORE
END-DECIDE
*
IF #TYPE = 'MSG'
  IF #MR-MSG-FND OR #MR-MSG-CHGED
    FOR #I1 1 TO 8
      MOVE #MR-MSG (#I1) TO MR-R01.MESSAGE (#I1)
    END-FOR
  END-IF
*
  IF #MR-MSG-FND
    IF MR-R01.MSG-IND = 2 OR = 3
      MOVE 3 TO MR-R01.MSG-IND
    ELSE
      MOVE 1 TO MR-R01.MSG-IND
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE /* (1248)
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CORR-MSG-FORMAT
* * * * * * * * * * * * * * * * * *
*
MOVE #CGA-CLERK TO #CLERK-ABB-PASS
MOVE ' ' TO #CLERK-ABB-NAME-PASS
CALLNAT 'GCKNMSXX' #CLERK-ABB-PASS
  #INVALID
  #CLERK-ABB-NAME-PASS
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MBR-ACCTS-LTR-MENU
* * * * * * * * * * * * * * * * * * * *
*
MOVE (AD=P) TO #SCR-CV-ACCT (10)
RESET #CGA-SCR-SUFFIX
IF #TEXT = ' '
  MOVE 'Select type of "MR"' TO #TEXT
END-IF
*
INPUT TEXT #TEXT MARK *#SCR-MR-TYPE-ACCT (1) USING MAP 'G3603MMR'
*
IF  (#CGA-SCR-COMMAND NE '*')
*  OR (#CGA-SCR-ID NE #CGA-ORIG-ID)
  FETCH 'G1000PXX'
END-IF
*
RESET #MR-X #MR-MSG-CHGED #MR-MSG-FND
FOR #I1 1 TO 8
  IF #MR-MSG (#I1) NE ' '
    MOVE TRUE TO #MR-MSG-FND
  END-IF
*
  IF #MR-MSG (#I1) NE #HOLD-MSG (#I1)
    MOVE TRUE TO #MR-MSG-CHGED
  END-IF
END-FOR
*
RESET #MR-X
FOR #I1 1 TO 10
  IF #SCR-MR-TYPE-ACCT (#I1) NE ' '
    IF #SCR-MR-TYPE-ACCT (#I1) = '*'
      REINPUT '* invalid here'
        MARK *#SCR-MR-TYPE-ACCT (#I1)
    END-IF
    MOVE #I1 TO #MR-X
  END-IF
END-FOR
*
IF   (#MR-X = 0)
    AND (NOT #MR-MSG-CHGED)
  REINPUT 'Select an option or send a message'
    MARK *#SCR-MR-TYPE-ACCT (1)
END-IF
*
RESET #TEXT
*
IF #MR-MSG-CHGED
  MOVE 'MSG' TO #TYPE
  PERFORM PROCESS-LINE
END-IF
*
FOR #MR-X 1 TO 10
  IF   (#SCR-MR-TYPE-ACCT (#MR-X) NE ' ')
      AND (#SCR-MR-TYPE-ACCT (#MR-X) NE '*')
    DECIDE ON FIRST #MR-X
      VALUE 1  MOVE 'PIL'  TO #TYPE
      VALUE 2  MOVE 'APL'  TO #TYPE
      VALUE 3  MOVE 'FAL'  TO #TYPE
      VALUE 4  MOVE 'RFS'  TO #TYPE
      VALUE 5  MOVE 'RFC'  TO #TYPE
      VALUE 6  MOVE 'RFZ'  TO #TYPE
      VALUE 7  MOVE 'LPF'  TO #TYPE
      VALUE 8  MOVE 'LPV'  TO #TYPE
      VALUE 9  MOVE 'LPB'  TO #TYPE
      NONE IGNORE
    END-DECIDE
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
* below never used a/o 9/2003 bmg
DEFINE SUBROUTINE APPLICATION-MENU
* * * * * * * * * * * * * * * * * * * *
*
RESET #CGA-SCR-SUFFIX
IF #TEXT = ' '
  MOVE 'Select type of "MR"' TO #TEXT
END-IF
*
INPUT TEXT #TEXT MARK *#SCR-MR-TYPE-APPL (1) USING MAP 'G3604MMR'
*
IF  (#CGA-SCR-COMMAND NE '*')
*  OR (#CGA-SCR-ID NE #CGA-ORIG-ID)
  FETCH 'G1000PXX'
END-IF
*
RESET #MR-X #MR-MSG-CHGED #MR-MSG-FND
FOR #I1 1 TO 8
  IF #MR-MSG (#I1) NE ' '
    MOVE TRUE TO #MR-MSG-FND
  END-IF
*
  IF #MR-MSG (#I1) NE #HOLD-MSG (#I1)
    MOVE TRUE TO #MR-MSG-CHGED
  END-IF
END-FOR
*
RESET #MR-X
FOR #I1 1 TO 9
  IF #SCR-MR-TYPE-APPL (#I1) NE ' '
    IF #SCR-MR-TYPE-APPL (#I1) = '*'
      REINPUT '* invalid here'
        MARK *#SCR-MR-TYPE-APPL (#I1)
    END-IF
    MOVE #I1 TO #MR-X
  END-IF
END-FOR
*
IF   (#MR-X = 0)
    AND (NOT #MR-MSG-CHGED)
  REINPUT 'Select an option or send a message'
    MARK *#SCR-MR-TYPE-APPL (1)
END-IF
*
RESET #TEXT
*
IF #MR-MSG-CHGED
  MOVE 'MSG' TO #TYPE
  PERFORM PROCESS-LINE
END-IF
*
FOR #MR-X 1 TO 9
  IF   (#SCR-MR-TYPE-APPL (#MR-X) NE ' ')
      AND (#SCR-MR-TYPE-APPL (#MR-X) NE '*')
    DECIDE ON FIRST #MR-X
      VALUE 1  MOVE 'APP'  TO #TYPE
      VALUE 2  MOVE 'APN'  TO #TYPE
      VALUE 3  MOVE 'APT'  TO #TYPE
      VALUE 4  MOVE 'AS '  TO #TYPE
      VALUE 5  MOVE 'ASN'  TO #TYPE
      VALUE 6  MOVE 'AST'  TO #TYPE
      VALUE 7  MOVE 'AMS'  TO #TYPE
      VALUE 8  MOVE 'ASG'  TO #TYPE
      NONE IGNORE
    END-DECIDE
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
* below never used a/o 9/2003 bmg except for DRW/PAM
DEFINE SUBROUTINE SHOW-VP-APP
* * * * * * * * * * * * * * *
*
RESET #CGA-ISN
*
MOVE 'VP' TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
MOVE 'MR' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
FETCH RETURN 'C2V10PVP'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF VP-IND = '3' OR = '4'   /* ALSO GIVE APP
  MOVE 'MR' TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
  MOVE 'VP' TO #CGA-SCR-SUFFIX  #CGA-ORIG-COMMAND
  FETCH RETURN 'C2150PPO'
END-IF
*
IF #CGA-SCR-COMMAND = '*'
  MOVE 'MR' TO #CGA-SCR-COMMAND
  RESET #CGA-SCR-SUFFIX
END-IF
*
END-SUBROUTINE
*
END
