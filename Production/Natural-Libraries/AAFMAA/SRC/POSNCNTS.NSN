* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : POSNCNTS                                              *
* AUTHOR       : Travis Tinger                                         *
* DESCRIPTION  : Input parameter is #POLICY-NUMBER. This Subprogram    *
*    will return all policy relationship information. If the optional  *
*    parameter #RELATIONSHIP-TYPE is specified then this subprogram    *
*    will only return policy relationship information pertinent to     *
*    the specified relationship type                                   *
*                                                                      *
************************************************************************
* Possible values for optional parameter #RELATIONSHIP-TYPE and the    *
* meaning behind them:  See POSLROLE                                   *            *
*                                                                      *
************************************************************************
*                       MODIFICATION LOG                               *
* USER  DATE       TAG   REASON                                        *
* TMT  01/28/2015  NONE  Initial Creation                              *
* YAK  04/30/2017  YK1   Comment out local error processing            *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #POLICY-NUMBER        (A16)  /* Input
PARAMETER USING POSAGET
LOCAL USING ERROR_L
LOCAL USING POSLROLE
LOCAL USING POSL0002
LOCAL
1 #INSURED-CONTACT  (N8)
1 ST-V VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N2)
  2 POLICY-ID (A16/9)
  2 STATUS    (A1/9)
1 I              (I2)
1 #ERROR-CODE    (I4)
1 #ERROR-LOG     (I4) CONST <3>
END-DEFINE
*
RESET #NUMBER-OF-RELATIONS
  #INSURED-CONTACT
XX.
FIND PC-V WITH POLICY-NUMBER = #POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  IF PC-V.RELATION = InsuredRole
    #INSURED-CONTACT := PC-V.CONTACT-ID
  END-IF
  IF #RELATIONSHIP-TYPE > 0
      AND #RELATIONSHIP-TYPE NE PC-V.RELATION
    ESCAPE TOP
  END-IF

  ADD 1 TO #NUMBER-OF-RELATIONS
  EXPAND ARRAY #POLICY-RELATIONS TO (1:#NUMBER-OF-RELATIONS)
  EXPAND ARRAY #ISNS TO (1:#NUMBER-OF-RELATIONS)
*   #POLICY-RELATIONS.ISN(#NUMBER-OF-RELATIONS) := *ISN(XX.)
  #ISNS(#NUMBER-OF-RELATIONS) := *ISN(XX.)
  MOVE BY NAME PC-V TO #POLICY-RELATIONS(#NUMBER-OF-RELATIONS)
END-FIND
*
IF #NUMBER-OF-RELATIONS = 0
  #MSG := #NOT-EXIST      /* 'No record exist'
ELSE
  RESET #MSG
  IF #INSURED-CONTACT > 0
    FIND (1) ST-V WITH MEMBER-CONTACT-ID = #INSURED-CONTACT
      FOR I 1 TO C*ACTIVE-SEGMENTS
        IF POLICY-ID (I) = #POLICY-NUMBER
          POLICY-STATUS (*) := ST-V.STATUS (I)
          IF ST-V.STATUS (I) = 'D'
            ESCAPE BOTTOM
          END-IF
        END-IF
      END-FOR
    END-FIND
  END-IF
END-IF
* YK1 >
* ON ERROR
*   #ERROR-CODE := *ERROR-NR
*   #MSG := 'INTERNAL ERROR'
*   CALLNAT 'AUCLOG' #ERROR-CODE #MSG #ERROR-LOG
* END-ERROR
* < YK1
END
