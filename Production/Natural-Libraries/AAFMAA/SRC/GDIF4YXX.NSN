* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA
PARAMETER
1 #MEMBER-CONTACT-ID    (N8)
1 #LINE-WITH-DPND       (A78)
LOCAL
1 #SSN-A        (A9) 1 REDEFINE #SSN-A
  2 #SSN-N      (N9)
1 #SSN          (A11) 1 REDEFINE #SSN
  2 #NNN        (A3)
  2 FILLER 1X
  2 #NN         (A2)
  2 FILLER 1X
  2 #NNNN       (A4)
1 #DATE         (A09) 1 redefine #DATE
  2 #DATE-1     (A1)
1 #NAME         (A15)
1 #LAST-NAME    (A15)
1 #FIRST-NAME   (A15)
1 #D            (D)
1 #DY           (A8) 1 REDEFINE #DY
  2 #DY-N       (N8)
1 #DEPENDENT    (A78)
*
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-ID       (N8)         /* D
  2 ID-NUMBER        (N6)         /* D
  2 FIRST-NAME       (A25)
  2 MIDDLE-NAME      (A25)
  2 LAST-NAME        (A25)
  2 SUFFIX           (A10)
  2 GENDER-CD        (A1)         /* --NA
  2 DATE-OF-BIRTH    (N8) (HD='DOB')
  2 SSN              (N9)         /* D
  2 DATE-OF-DEATH    (N8)
  2 BIRTH-CERT-CODE  (A1)
  2 CONTACT-REMARKS  (A75/1:1)    /* MU-FIELD
  2 LAST-USER-UPD    (A8)
  2 LAST-DATE-UPD    (N8)
  2 LAST-TIME-UPD    (N7)
* ---------------------------------------------- Contact Relationship
1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID                      (N8.0) /* D
  2 ROLE-1-CD                         (A1)
  2 CONTACT-2-ID                      (N8.0) (HD='CONTACT') /* D
  2 ROLE-2-CD                         (A1)
  2 LAST-USER-UPD                     (A8)
  2 ACCESS-AUTH-SVC                   (A1)   /*  IAS access
  2 ACCESS-AUTH-INS                   (A1)   /*  SAS access
  2 LAST-DATE-UPD                     (N8.0)
  2 LAST-TIME-UPD                     (N7.0)

1 #CGA-USER-COMPANY  (A1)
1 #CGA-CLERK         (A3)
1 #MODE              (A1)  INIT <'V'>
1 #ORIG-COMMAND      (A2)  INIT <'DC'>
1 #MSG               (A72)

END-DEFINE
*
MOVE SUBSTR (#LINE-WITH-DPND,53,11) TO #SSN
MOVE SUBSTR (#LINE-WITH-DPND,26,9)  TO #DATE
MOVE SUBSTR (#LINE-WITH-DPND,9,15) TO #NAME
IF #DATE-1 = ' '
  ASSIGN #DATE-1 = '0'
END-IF
IF #DATE = MASK (DDAAAYYYY)
  move edited #DATE TO #D (EM=DDLLLYYYY)
  move edited #D (EM=YYYYMMDD) TO #DY
END-IF
IF #SSN = MASK (NNN'-'NN'-'NNNN)
  COMPRESS #NNN #NN #NNNN INTO #SSN-A LEAVING NO
END-IF
SEPARATE #NAME INTO #LAST-NAME #FIRST-NAME IGNORE WITH DELIMITERS ','
* write notitle 'After user input...' *CURS-LINE *CURS-COL
*  / #SSN #DATE #NAME #DY  #SSN-N #LAST-NAME #FIRST-NAME

FIND CNT-REL WITH CONTACT-1-ID = #MEMBER-CONTACT-ID
  ACCEPT IF ROLE-2-CD  = 'K' OR= 'G'
  FIND CNT WITH CONTACT-ID = CONTACT-2-ID
    IF CNT.SSN = #SSN-N
        OR CNT.DATE-OF-BIRTH = #DY-N
        OR CNT.FIRST-NAME = #FIRST-NAME
*      COMPRESS
*        CONTACT-2-ID DATE-OF-BIRTH SSN FIRST-NAME LAST-NAME
*        INTO #DEPENDENT
*      WRITE #DEPENDENT
      CALLNAT 'GUPCISXX'
        #MEMBER-CONTACT-ID   /* Members CONTACT-ID
        CONTACT-2-ID         /* Spouse/Dependent/Other CONTACT-ID
        #ORIG-COMMAND
        CNT.ID-NUMBER        /* CN or ID-NUMBER
        #CGA-USER-COMPANY
        #CGA-CLERK
        #MODE
        #MSG
    END-IF
  END-FIND
END-FIND
*
END
