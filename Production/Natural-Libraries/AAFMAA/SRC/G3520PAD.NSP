* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: G3520PAD     ** FETCHED FROM G3500PAD
* >>>>>>>>>>>>>>>> IS ONLY USED FOR PUL AS OF 4/23/2002
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    05272004  PM1     Adjusted PUP and PUM processes to allow up  *
*                          to 6 non-paid up whole life policies.       *
* DRW    12282004  DW2     Increase Var Loan to handle 999,999.99      *
*                          to 6 non-paid up whole life policies.       *
* PAM    02032005  PM3     Do not display paid-up line for over-loaned *
*                          policies on 'PUP' and 'PUM' letters.        *
* PAM    02102005  PM4     Increased #LUMP-SUM and #LUMP-PMT from 5.2  *
*                          to 6.2.                                     *
* DRW    03052005  DW5     ADD AL (Annuity Life) processing            *
* PAM    03112005  PM6     Reset display fields for GRANKSXX           *
* DRW    05152005  DW7     Add face Amt & TPP linkage to CVALUSXX      *
* DRW    04292005  DW8  Add DOB processing to GMAXSXX                  *
* DRW    07082005  DW9  Make max age 71 for AD PUP & add 'CN XXXXX'    *
* DRW    08222006  DW10 Increase size of Paid-up amt                   *
* DRW  08/30/2008  DW11 Gen Plus Processing                            *
* PAM  08/23/2010  PM12 Added new address storage location             *
* YAK    10162012  YK1  Use function to identify Level Term II plans   *
* YAK    11082012  YK2  Add No Medical Term plan (EZ)                  *
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* TMT  10/07/2013  TT1  ADDRESS NORMALIZATION
* vxt  12/04/2013  vt1  expand the size of #nu-ann-prem to fix 0936 error
* TMT  12/06/2013  TT1  1 MILLION DOLLAR STABILITY PROJECT
* TMT  06/23/2014  TT3  Contact Normalization - DOB and Sex
* TMT  10/13/2014  TT4  AnnuityLife Stabilization
* NGG  10/19/2015  NG1  Policy Owneship
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING GC2V6LXX
LOCAL USING CONTPDA1
LOCAL
*
1 #LEFT   (A) DYNAMIC    /* PM12
1 #RIGHT  (A) DYNAMIC    /* PM12
1 #I      (N2)           /* PM12
1 #RESULT (A70)          /* PM12
*
1 #TRAN-DATE   (N8)
1 #SUFFIX      (A3)
1 #PAY-STATUS-48 (A2)   INIT <'48'>
1 #MAX-7-PAY   (P6.2)
1 #MEC-PREMS-PAID-TO-DATE (P7.2)      /*TT1
1 #EXCEEDS-MEC (P7.2)                 /*TT1
1 #1035-NX     (A60)
1 #1035-EXCH-AMT  (P6.2)
1 #LUMP-PMT  (P6.2)                                               /* PM4
1 #AP-PREMS-PAID (P7.2)
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)  /* NG99
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID         (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
*
* ABOVE for CALC-PREMS-PAID
*
1 ST-V VIEW OF A-STATUS
  2 X-FIRST-PLACE-FEE  (9)
  2 X-BILL-ADJUST      (9)
  2 PREM-RATING   (9)
*
1 IM-V VIEW OF A-INTRO-MEMBER
 2 ID-NUMBER
 2 NAME
 2 RANK
*  2 ADDRESS-1                        /*TT1 COMMENTED OUT ADDRESS LINES AND ZIP-CODE
*  2 ADDRESS-2
*  2 ADDRESS-3
*  2 ZIP-CODE
*
1 #PLAN-3 (A3)
1 REDEFINE #PLAN-3
 2 #PLAN-A1  (A1)
 2 #PLAN-A2  (A2)
1 #INTRO-FLAG          (A1)
1 #FORM-IND            (A3)
1 #FAC-AMT             (N7.2)
1 #ISS-DATE            (N8)
1 #ISS-AGE             (N2)
1 #SMOK-FLG            (A1)
1 #TO-PLAN             (A3)
1 #OUTPUT-CALC-AMT     (P7.2)     /* passed to c1601sco for results
1 #OUTPUT-OLD-PDUP-AMT (P7.2) /* passed to c1601sco for results
1 #PRORATE-LOAN        (L)
1 #RPU-ACV             (P7.2)
1 #OUTPUT-PREM-DIFF    (N6.2)
1 #NU-ANN-PREM        (N7.2)          /* vt1 was n6.2
1 #LUMP-SUM           (N6.2)                                      /* PM4
1 #DUES-DIFF           (N5.2)
1 #TOT-LOAN-AMOUNT     (P6.2)   /* DW2
1 #TOT-CURR-ACCUM-INT  (P5.2)
*
1 #MULTIPLIER         (P1.3)
1 #WORK-LAST-DURATION (P3)
1 #PREMS-YET-TO-PAY  (P7.2)
1 #NUM-MONTHS        (P5)
1 #AMT-TO-ADD-TO-ACV (P5.2)
1 #AMT-TO-CHG-MBR    (P5.2)
1 #WHOLE-DOLLARS       (P7)
1 #DOLLARS-DISP        (A8)   /* DW10
1 #NUM-POLS            (P2)
1 #REM-FACE            (P6)
1 #REM-FACE-DISP       (A7)
1 #TOT-FACE            (P6)
1 #MAX-FACE-TOTAL           (P7.2)
1 #MAX-FACE-TERM            (P7.2)
1 #OVERRIDE-MAX-FACE-TOTAL  (P7.2)
1 #OVERRIDE-MAX-FACE-TERM   (P7.2)
1 #PAY-STAT            (A4)
1 #EFF-DATE            (N8)
*
1 #TAMRA           (L)
1 #PAY-UP          (L)
1 #BOTH            (L)
1 #FIXED           (L)
1 #VARIABLE        (L)
1 #LOAN-AMT        (P5.2)
1 #LOAN-AMT-2      (P5.2)
1 #RESTRICT-MBR-SW (A1)
1 #SUSPEND-CODE    (A1)
*
1 #DAT-CNT         (P3)
1 #SCR-DAT-NUM-LBL (A2)
1 #BEG-ID          (N6)
1 REDEFINE #BEG-ID
 2 #BEG-ID-A       (A6)
1 #END-ID          (N6)
1 #S-X     (I2)
1 #TEXT    (A40/22)
1 #SCR-ASS-RET-DT (A11)
1 #DATX     (A10)
1 #DATX-ESTATE (A10)
1 #AD-I     (I2)
1 #I2       (I2)
1 #SEG-I    (I2)
1 #AD-FF-3  (A78)       1 #AD-FF-4  (A78)
1 #AD-FF-5  (A78)       1 #AD-FF-6  (A78)
1 #AD-ADDR-MSG    (A70/3)                                     /* vt 10/19/2015 was a30/3
1 #AD-TABLE (A78/14)    /* was 30      /*TT1 EXPANDED FROM 13 TO 14
1 REDEFINE #AD-TABLE
 2 #AD-FF-12 (A78)       2 #AD-FF-13 (A78)
 2 #AD-FF-14 (A78)       2 #AD-FF-15 (A78)
 2 #AD-FF-16 (A78)       2 #AD-FF-17 (A78)
 2 #AD-FF-18 (A78)       2 #AD-FF-19 (A78)
 2 #AD-FF-20 (A78)       2 #AD-FF-21 (A78)
 2 #AD-FF-22 (A78)       2 #AD-FF-23 (A78)
 2 #AD-FF-24 (A78)
*
1 #NO-MAILOUT           (L)
1 #FOREIGN-ADDR         (L)
*
1 #MODE  (A1)
1 #MODE-DESC (A10)  INIT <'??????????'>
1 #PAID-TO-DATE (N8)
1 REDEFINE #PAID-TO-DATE
 2 #PT-CENT (N2)
 2 #PT-YY   (N2)
 2 #PT-MM   (N2)
 2 REDEFINE #PT-MM
  3 #PT-MM-A (A2)
 2 #PT-DD   (N2)
1 #DT-TO-MM/DD/YY   (A8) INIT <'XX/XX/XX'>
1 #DT-FROM-MM/DD/YY (A8) INIT <'XX/XX/XX'>
1 #ERROR-MESSAGE  (A65)
1 #CLERK-ABB-PASS  (A3)
1 #INVALID   (L)
1 #CLERK-ABB-NAME-PASS (A20)
*
1 #CORR-MSG (A50) CONST<'This correspondence has been prepared for '
  -'you by:'>
*
1 #DATE-OF-BIRTH    (N8.0)         /* TT3
1 #SEX              (A1)           /* TT3
1 #DATU       (A8)  /* 'MM/DD/YY'
1 REDEFINE #DATU
  2 #CURR-MM  (N2)
  2 #CURR-F1  (A1)
  2 #CURR-DD  (N2)
  2 #CURR-F2  (A1)
  2 #CURR-YY  (N2)
1 REDEFINE #DATU
  2 #CURR-MM-A  (A2)
  2 #CURR-F1-A  (A1)
  2 #CURR-DD-A  (A2)
  2 #CURR-F2-A  (A1)
  2 #CURR-YY-A  (A2)
1 #PREV-YY    (N2)
1 REDEFINE #PREV-YY
 2 #PREV-YY-A  (A2)
1 #DAY-LITERALS (A24) INIT <'312831303130313130313031'>
1 REDEFINE #DAY-LITERALS 2 #DAY-LIT (A2/12)
*
1 #MONTH-LITERALS (A48) INIT<'Jan Feb Mar Apr May Jun Jul Aug Sep '
  -'Oct Nov Dec '>
1 REDEFINE #MONTH-LITERALS   2 #MON-LIT (A4/12)
*
1 #F-M-L (A108) INIT<'January  February March    April    May      '
  -'June     July     August   SeptemberOctober  November December '>
1 REDEFINE #F-M-L            2 #F-MON-LIT (A9/12)
*
1 #SCR-MIL-ID              (A6)
* 1 #SCR-NAME-RET            (A31)                                 /* vt1
*
1 #SCR-AS-OF-LINE          (A11)
1 REDEFINE #SCR-AS-OF-LINE
 2 #SCR-AS-OF-DAY        (A2)
 2 #SCR-AS-OF-FILL       (A1)
 2 #SCR-AS-OF-MON        (A4)
 2 #SCR-AS-OF-YEAR       (N4)
*
1 #AGE-59-1-2 (N8)
1 #YYYYMMDD  (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY    (N4)
 2 #MM      (N2)
 2 #DD      (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A (A4)
 2 #MM-A   (A2)
 2 #DD-A   (A2)
*
1 #MON-X      (I2)
1 #W-YYYYMM   (N7)
1 REDEFINE #W-YYYYMM
  2 #W-0CENT  (N3)
  2 #W-YY     (N2)
  2 #W-MM     (N2)
*
1 #LAST-MON   (A4)
1 #LAST-MON-CURR-YY   (N2)
*
1 #RET       (A3)
1 #DEL       (A1) CONST<','>
1 #DELL      (A1)
*
1 #HOLD-DATE (N8)
1 REDEFINE #HOLD-DATE
 2 #HD-CCYY    (N4)
 2 #HD-MM      (N2)
 2 #HD-DD      (N2)
*
1 #A-X             (I2)
1 #D-X             (I2)
*
1 #W-AMT          (A10)
* 1 #FULL-RANK       (A23)                                                            /* vt1
1 #XXX            (A3) INIT <'XXX'>
1 #EDIT-INVALID (L)
* 1 #RANK-LITERAL (A23)                                                               /* vt1
* 1 #RANK-DISPLAY (A5)                                                                /* vt1
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #ID-NUM                (N6)
* 1 #HOLD-RANK-5           (A5)                                /* vt1 \/
*  1 REDEFINE #HOLD-RANK-5
*   2 #HOLD-RANK           (A3)                                /* vt1 /\
1 #HOLD-NAME             (A70)                                /* vt1 was 25
1 #HOLD-LAST-NAME        (A25)
1 #HOLD-SSN              (N9)
1 REDEFINE #HOLD-SSN
 2 #SSN-1                (A3)
 2 #SSN-2                (A2)
 2 #SSN-3                (A4)
1 #HOLD-DEBIT-CREDIT-AMT (P7.2)
1 #HOLD-ALOT-AMT         (P5.2)
*
1 #FN                    (A45)
1 #EXT-DEAR-FORMATED-NAME    (A70)                                /* vt1 was #dear-formated-name(a45)
1 #FORMATED-NAME-LEFT    (A70)                                /* vt1 was 45
1 #EXT-FORMATED-NAME         (A70)                                /* vt1 was #FORMATED-NAME (A45)
* 1 REDEFINE #FORMATED-NAME
* 2 #FN-1ST-2               (A2)
* 2 #FN-LST-43              (A68)                               /* vt1 was 43
* 1 REDEFINE #FORMATED-NAME
* 2 #FN-1ST-5               (A5)
* 2 #FN-LST-40              (A65)                                /* vt1 was 40
* 1 REDEFINE #FORMATED-NAME
* 2 #FN-1ST-7               (A7)
* 2 #FN-LST-38              (A63)                               /* vt1 was 38
* 1 REDEFINE #FORMATED-NAME
* 2 #FN-1ST-10              (A10)
* 2 #FN-LST-35              (A60)                               /* vt1 was 35
* 1 REDEFINE #FORMATED-NAME
*  2 #FN-1ST-12              (A12)
* 2 #FN-LST-33              (A58)                               /* vt1 was 33
* 1 REDEFINE #FORMATED-NAME
* 2 #FN-1ST-15              (A15)
* 2 #FN-LST-30              (A55)                               /* vt1 was 30
*
1 #FIRST-ADDRESS         (A70)    /* PM12
1 #SECOND-ADDRESS        (A70)    /* PM12
1 #THIRD-ADDRESS         (A70)    /* PM12
1 #FORMAT-CN-CN          (A9)
1 #MIL-STATUS            (A1)
1 #MIL-SERVICE           (A3)
1 #EDS-ZIP-BAR-CODE      (A14)
1 #NUM-PDUP              (P2)
1 #MODE-G                (L)
1 #PDUP-X                (I2)
1 #ISS-DT                (N8/7)
1 #DATE-DISP             (A10)
1 #PASSED-AMT            (P5.2) /* passed from g3500pad
1 #AP-PLAN   (A3)
1 #ID-9   (N9)
1 #SUFF-3 (A3)
*
1 #AS-OF-DT   (N8)                                                /* PM3
1 #LOAN-BAL   (N7.2/9)                                            /* PM3
1 #LOAN-BAL-2 (N7.2/9)                                            /* PM3
1 #ACC-INT    (N7.2/9)                                            /* PM3
1 #ACC-INT-2  (N7.2/9)                                            /* PM3
1 #CSV        (N7.2/9)                                            /* PM3
1 #CVALUE     (N7.2/9)                                            /* PM3
1 #LVALUE     (N7.2/9)                                            /* PM3
1 #MCSV       (N7.2/9)                                            /* PM3
1 #DB         (N7.2/9)                                            /* PM3
1 #ANN-PREMIUM (N7.2/9)                                           /* PM3
1 #FACE-AMT    (N7.2/9)  /* DW7
1 #TPP         (N7.2/9)  /* DW7
*
1 #memo-to-name   (a70)                             /* vt1
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
* INPUT #ID-NUM #HOLD-NAME #HOLD-SSN #HOLD-LAST-NAME #HOLD-RANK-5                      /* vt1
INPUT #ID-NUM #HOLD-NAME #HOLD-SSN #HOLD-LAST-NAME  #memo-to-name                          /* vt1
      #HOLD-DEBIT-CREDIT-AMT #HOLD-ALOT-AMT
*      #FORMATED-NAME #FIRST-ADDRESS #SECOND-ADDRESS #THIRD-ADDRESS                 /* vt1
       #EXT-FORMATED-NAME #FIRST-ADDRESS #SECOND-ADDRESS #THIRD-ADDRESS             /* vt1
      #MIL-STATUS #MIL-SERVICE
      #MODE #PAID-TO-DATE #LOAN-AMT #LOAN-AMT-2
      #RESTRICT-MBR-SW #SUSPEND-CODE
      #EDS-ZIP-BAR-CODE #AD-FF-3 #PASSED-AMT #EXT-DEAR-FORMATED-NAME                  /* vt1
      #AD-ADDR-MSG (1:3) #INTRO-FLAG
*
MOVE #EXT-FORMATED-NAME TO #FORMATED-NAME-LEFT
MOVE #AD-ADDR-MSG (3) TO #ERROR-MESSAGE         /* NG1
RESET #AD-ADDR-MSG (3)                          /* NG1 
*
MOVE *DATU TO #DATU
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #DATX WITH '/'
MOVE #DATX TO #DATX-ESTATE
*
* RESET #DISPL #RANK-LITERAL #RANK-DISPLAY                          /* PM6            /* vt1  \/
* CALLNAT 'GRANKSXX' #PASS-COMMAND #HOLD-RANK #MIL-SERVICE
*      #EDIT-INVALID #DISPL #RANK-LITERAL #RANK-DISPLAY
* MOVE #RANK-LITERAL TO #FULL-RANK                                                    /* vt1 /\
*
DECIDE FOR EVERY CONDITION
  WHEN #CGA-SCR-SUFFIX = 'PUP' OR = 'PUM' OR = 'OLD'
    PERFORM PUP-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'PUL'   /* paid-up thank you letter
    PERFORM PUL-PROCESS
  WHEN NONE IGNORE
END-DECIDE
*
IF #ID-NUM = 38501 /* wants to not see info on envelope
  RESET #ID-NUM #MIL-STATUS #MIL-SERVICE
END-IF
*
IF *DEVICE = 'BATCH'
  EJECT
  WRITE NOTITLE USING FORM 'G3503FAD'
  MOVE 1 TO #CGA-SECURITY-CODE
ELSE
*
  #LEFT           := #FIRST-ADDRESS           /* PM12
  #RIGHT          := *TRIM(#AD-ADDR-MSG (1))  /* PM12
  #RESULT         := #LEFT                    /* PM12
  #I              := 70 - *LENGTH(#RIGHT)     /* PM12
  MOVE #RIGHT TO SUBSTRING(#RESULT,#I)        /* PM12
  #FIRST-ADDRESS  := #RESULT                  /* PM12
*
  #LEFT           := #SECOND-ADDRESS          /* PM12
  #RIGHT          := *TRIM (#AD-ADDR-MSG (2)) /* PM12
  #RESULT         := #LEFT                    /* PM12
  #I              := 70 - *LENGTH(#RIGHT)     /* PM12
  MOVE #RIGHT TO SUBSTRING(#RESULT,#I)        /* PM12
  #SECOND-ADDRESS := #RESULT                  /* PM12
*
  #LEFT           := #THIRD-ADDRESS           /* PM12
  #RIGHT          := *TRIM (#AD-ADDR-MSG (3)) /* PM12
  #RESULT         := #LEFT                    /* PM12
  #I              := 70 - *LENGTH(#RIGHT)     /* PM12
  MOVE #RIGHT TO SUBSTRING(#RESULT,#I)        /* PM12
  #THIRD-ADDRESS := #RESULT                   /* PM12
*
  INPUT WITH TEXT #ERROR-MESSAGE USING MAP 'G3500MAD'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  IF #TAMRA        /* ******** TAMRA TEXT IS ALSO IN AOAP005A *****
    RESET #AD-TABLE (*)
    MOVE 3 TO #I2
    COMPRESS 'PROVISIONS OF THE TECHNICAL AND MISCELLANEOUS REVENUE'
           'ACT OF 1988 (TAMRA)'   INTO #AD-TABLE(#I2)
    ADD 2 TO #I2
    COMPRESS '  This act imposes tax implications on loans taken'
             'from or cash surrenders'  INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'made of policies issued on or after 21 June 1988'
             'and paid up within less'     INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'than seven years from date of issue.'
             'The IRS priority sequence for with-' INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'drawals is taxable gains first and then cost basis'
             '(premiums paid in).'  INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'Taxable gains withdrawn before age 59 1/2, unless'
             'the owner is disabled,'  INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'are subject to a 10% penalty tax, as well as ordinary'
              'income tax. Cost'     INTO #AD-TABLE(#I2)
    ADD 1 TO #I2
    COMPRESS 'Basis (premiums paid in) is withdrawn without taxation'
             'and penalty.'         INTO #AD-TABLE(#I2)
*
    MOVE 'AD' TO #CGA-SCR-COMMAND
    RESET #ERROR-MESSAGE
    INPUT WITH TEXT #ERROR-MESSAGE USING MAP 'G3500MAD'
*
    IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
    END-IF
  END-IF
END-IF
*
IF *DEVICE = 'BATCH'    /* TO MAKE POSSIBLE FETCH RETURN FROM AOAP006E
  ESCAPE ROUTINE
END-IF
*
FETCH 'G1000PXX'
*
*********************************
DEFINE SUBROUTINE CORR-MSG-FORMAT
*********************************
*
MOVE #CGA-CLERK TO #CLERK-ABB-PASS
MOVE ' ' TO #CLERK-ABB-NAME-PASS
CALLNAT 'GCKNMSXX' #CLERK-ABB-PASS
                #INVALID
                #CLERK-ABB-NAME-PASS
*
IF *DEVICE = 'BATCH'
  ADD 2 TO #I2
ELSE
  MOVE 13 TO #I2
END-IF
COMPRESS #CORR-MSG #CLERK-ABB-NAME-PASS INTO #AD-TABLE (#I2)
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PUP-PROCESS
* * * * * * * * * * * * * * * * *
*
FIND (1) ST-V WITH MEMBER-KEY = #CGA-ID-G
END-FIND
COMPRESS 'CN' #ID-NUM INTO #FORMAT-CN-CN               /* DW9
*
MOVE *DATN TO #TODAY-YYYYMMDD-N
FIND (1) CI-V WITH MEMBER-KEY = #CGA-ID-G
  FOR #IFL 1 TO 9
    MOVE CI-V.PLAN (#IFL) TO #PLAN-3
    IF  (CI-V.STATUS (#IFL) NE 'D')
     OR (CI-V.PAY-STATUS (#IFL) GE '42')
     OR (CI-V.PLAN (#IFL) = 'GPL')                   /* DW11
*    OR (CI-V.PLAN       (#IFL) = 'RT' OR = 'LT' OR = 'SO' OR = 'FSO')  /* YK1
*    OR ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))                       /* YK1  
     OR (CI-V.PLAN(#IFL) = 'SO' OR = 'FSO')                             /* YK1     
     OR FUNCTION-TERM-POLICY(<CI-V.PLAN(#IFL)>)                         /* YK1
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-POLS
  END-FOR
END-FIND
*
MOVE *DATN TO #AS-OF-DT                                           /* PM3
RESET #SUFFIX                                                     /* PM3
MOVE CI-V.FACE-AMOUNT (*) TO #FACE-AMT (*)   /* DW7
CALLNAT 'CVALUSXX'                                                /* PM3
  CI-V.ID-NUMBER
  #SUFFIX
  #AS-OF-DT
  #LOAN-BAL   (1:9)
  #LOAN-BAL-2 (1:9)
  #ACC-INT    (1:9)
  #ACC-INT-2  (1:9)
  #CSV        (1:9)
  #CVALUE     (1:9)
  #LVALUE     (1:9)
  #MCSV       (1:9)
  #DB         (1:9)
  #ANN-PREMIUM  (1:9)
  #FACE-AMT   (1:9)   /* DW7
  #TPP        (1:9)   /* DW7
*
MOVE *DATN TO #YYYYMMDD   /* age 59 1/2
SUBTRACT 59 FROM #YYYY
SUBTRACT 6 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #AGE-59-1-2
*
RESET #I2
ADD 1 TO #I2
COMPRESS '  The following is furnished in response to your recent'
         'inquiry:' TO #AD-TABLE (#I2)
*
RESET #TOT-FACE PDA-CONTACT #SEX #DATE-OF-BIRTH               /* TT3
P-CONTACT-ID := CI-V.MEMBER-CONTACT-ID                        /* TT3
CALLNAT 'CONTS001' PDA-CONTACT                                /* TT3
  #SEX           := P-GENDER                                  /* TT3
  #DATE-OF-BIRTH := P-DATE-OF-BIRTH                           /* TT3


FOR #IFL 1 TO 9
  IF CI-V.STATUS (#IFL) = 'D'
    MOVE *DATN TO #EFF-DATE
    MOVE CI-V.PLAN (#IFL) TO #PLAN-3
*
    IF ((CI-V.PLAN (#IFL) = 'LT')
     AND (#DATE-OF-BIRTH LT (#EFF-DATE - 400000)))                /* TT3
     OR  ((CI-V.PLAN(#IFL) = 'RT')
     AND (CI-V.ISSUE-AGE(#IFL) GT '65'))
     OR  ((CI-V.PLAN(#IFL) = 'EZ')                                /* YK2
     AND (CI-V.PAID-UP-DATE(#IFL) LE #EFF-DATE))                  /* YK2
*    OR  (((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))               /* YK1
     OR (F-LT2-TRUE(<CI-V.PLAN(#IFL)>)                            /* YK1
     AND ((CI-V.ISSUE-DATE(#IFL) + 40000) LE #EFF-DATE))
     OR ( IS-ANNUITY-LIFE(< CI-V.PLAN(#IFL) >))   /* DW5          /* TT4
*
      CALLNAT 'GDROPSTM' USING
        CI-V.PLAN          (#IFL)
        CI-V.SMOKER-FLAG   (#IFL)
        #DATE-OF-BIRTH                                            /* TT3
        CI-V.ISSUE-DATE    (#IFL)
        CI-V.PAID-UP-DATE  (#IFL)
        #EFF-DATE
        CI-V.ISSUE-AGE     (#IFL)
        #PAY-STAT
        CI-V.FACE-AMOUNT   (#IFL)
        CI-V.DEATH-BENEFIT (#IFL)
        CI-V.CASH-VALUE    (#IFL)
        CI-V.BEG-CASH-VALUE(#IFL)
    END-IF
*
    IF CI-V.ISSUE-DATE (#IFL) LT 19941201
      COMPUTE #TOT-FACE = #TOT-FACE + (CI-V.NUM-MBRSHIPS (#IFL) * 5000)
    ELSE
      ADD CI-V.FACE-AMOUNT (#IFL) TO #TOT-FACE
    END-IF
*
    IF CI-V.MTD-ACV-PREM-ADJ (#IFL) NE 9999999.99
      ADD CI-V.MTD-ACV-PREM-ADJ (#IFL) TO CI-V.ACC-CASH-VALUE (#IFL)
      ADD CI-V.MTD-ACV-PREM-ADJ (#IFL) TO CI-V.CASH-VALUE (#IFL)
      ADD CI-V.MTD-ACV-PREM-ADJ(#IFL) TO CI-V.NXT-MON-CASH-VALUE (#IFL)
    END-IF
*
    IF CI-V.MTD-ACV-INT-ADJ (#IFL) NE 99999.99
      ADD CI-V.MTD-ACV-INT-ADJ (#IFL) TO CI-V.ACC-CASH-VALUE (#IFL)
      ADD CI-V.MTD-ACV-INT-ADJ (#IFL) TO CI-V.CASH-VALUE (#IFL)
      ADD CI-V.MTD-ACV-INT-ADJ (#IFL) TO CI-V.NXT-MON-CASH-VALUE (#IFL)
    END-IF
*
    IF CI-V.MTD-ACV-COI-ADJ (#IFL) NE 99999.99
      SUBTRACT CI-V.MTD-ACV-COI-ADJ (#IFL)
       FROM CI-V.ACC-CASH-VALUE (#IFL)
      SUBTRACT CI-V.MTD-ACV-COI-ADJ (#IFL) FROM CI-V.CASH-VALUE (#IFL)
      SUBTRACT CI-V.MTD-ACV-COI-ADJ (#IFL)
             FROM CI-V.NXT-MON-CASH-VALUE (#IFL)
    END-IF
  END-IF
*
  MOVE CI-V.PLAN (#IFL) TO #PLAN-3
  IF  (CI-V.STATUS (#IFL) NE 'D')
   OR (CI-V.PAY-STATUS (#IFL) GE '42')
*  OR (CI-V.PLAN (#IFL) = 'RT' OR = 'LT' OR = 'SO' OR = 'FSO')    /* YK1
*  OR ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))                   /* YK1
   OR (CI-V.PLAN(#IFL) = 'SO' OR = 'FSO')                         /* YK1 
   OR FUNCTION-TERM-POLICY(<CI-V.PLAN(#IFL)>)                     /* YK1   
   OR (CI-V.PLAN (#IFL) = 'GPL')                   /* DW11
    ESCAPE TOP
  END-IF
*
  RESET #SUFF-3
  MOVE CI-V.FACE-AMOUNT (#IFL) TO #FAC-AMT
  MOVE 'SI' TO #AP-PLAN
*
  IF PREM-RATING (#IFL) NE ' '
    COMPRESS 'SB' PREM-RATING (#I1) INTO #SUFF-3 LEAVING NO
  END-IF
*
  CALLNAT 'GPREMSXX' #CGA-ORIG-COMMAND #ID-9 #SUFF-3
           #AP-PLAN CI-V.ISSUE-AGE-N (#IFL)
           #NU-ANN-PREM CI-V.ISSUE-DATE (#IFL)
           CI-V.SMOKER-FLAG (#IFL) #FAC-AMT #SEX                  /* TT3
*
  IF #LOAN-BAL (#IFL) + #LOAN-BAL-2 (#IFL) + #ACC-INT (#IFL)      /* PM3
                      + #ACC-INT-2 (#IFL) GT #LVALUE (#IFL)       /* PM3
    ADD 1 TO #I2                                                  /* PM3
    MOVE CI-V.ISSUE-DATE (#IFL) TO #YYYYMMDD                      /* PM3
    COMPRESS 'Policy issued' #MM-A INTO #AD-TABLE (#I2)           /* PM3
    COMPRESS #AD-TABLE (#I2) '/' #DD-A '/' #YYYY-A                /* PM3
                                        INTO #AD-TABLE (#I2) LEAVING NO
    COMPRESS #AD-TABLE (#I2) 'has loan greater than 75%. Pay-up'  /* PM3
                             'not recommended.'                   /* PM3
                               INTO #AD-TABLE (#I2)               /* PM3
  ELSE                                                            /* PM3
    MOVE TRUE TO #PAY-UP
    MOVE 'AP' TO #TO-PLAN
    MOVE PAID-TO-DATE (#IFL) TO #TRAN-DATE
    PERFORM CALL-AOCX2V6A
*
    IF   (CI-V.ISSUE-DATE (#IFL) GE 19970101)
     AND (CI-V.MONTH-ALLOC-AMT (#IFL) NE 999.99)
      ADD #NU-ANN-PREM      TO #OUTPUT-CALC-AMT
      ADD #DUES-DIFF        TO #OUTPUT-CALC-AMT
    END-IF
*
    MOVE ROUNDED #OUTPUT-CALC-AMT TO #WHOLE-DOLLARS
*
    IF #WHOLE-DOLLARS LT 0
      RESET #WHOLE-DOLLARS
    END-IF
    MOVE EDITED #WHOLE-DOLLARS (EM=-ZZZ,ZZ9) TO #DOLLARS-DISP /* DW10
*
    IF   (CI-V.ISSUE-DATE (#IFL) GE 19880621)
     AND (*DATN LT CI-V.ISSUE-DATE (#IFL) + 70000) /* within 7 years
*    AND (DATE-OF-BIRTH GT #AGE-59-1-2)  /* less than 59 1/2
*               /* above removed 1/15/2003 as per KHB
      MOVE #DATE-OF-BIRTH               TO #PP-DATE-OF-BIRTH               /* TT3
      MOVE CI-V.ANNUAL-PREM (#IFL)      TO #PP-ANNUAL-PREM
      MOVE CI-V.PAY-STATUS (#IFL)       TO #PP-PAY-STATUS
      MOVE CI-V.TOT-PREMIUMS-PAID (#IFL) TO #PP-TOT-PREMIUMS-PAID
      MOVE CI-V.PAID-TO-DATE (#IFL)      TO #PP-PAID-TO-DATE
      MOVE CI-V.ISSUE-DATE (#IFL)        TO #PP-ISSUE-DATE
      MOVE CI-V.MODE (#IFL)              TO #PP-MODE
      MOVE CI-V.PLAN (#IFL)              TO #PP-PLAN
      MOVE CI-V.COST-BASIS (#IFL)        TO #PP-COST-BASIS
      MOVE CI-V.MONTH-ALLOC-AMT (#IFL)   TO #PP-MONTH-ALLOC-AMT
      MOVE #INTRO-FLAG                  TO #PP-INTRO-FLAG
      MOVE ST-V.X-FIRST-PLACE-FEE (#IFL) TO #PP-FIRST-PLACE-FEE
      MOVE ST-V.X-BILL-ADJUST     (#IFL) TO #PP-BADJ
*
      CALLNAT 'GPRPDSCO'
        #PP-DATE-OF-BIRTH
        #PP-ANNUAL-PREM
        #PP-PAY-STATUS
        #PP-TOT-PREMIUMS-PAID
        #PP-PAID-TO-DATE
        #PP-ISSUE-DATE
        #PP-MODE
        #PP-PLAN
        #PP-COST-BASIS
        #PP-MONTH-ALLOC-AMT
        #PP-INTRO-FLAG
        #PP-FIRST-PLACE-FEE
        #PREMS-PAID
        #SUB-PREMS-PAID
        #PP-BADJ
*
      COMPUTE #AP-PREMS-PAID = #PREMS-PAID + #WHOLE-DOLLARS
      MOVE *DATN TO #YYYYMMDD
*
      RESET #SUFFIX
      CALLNAT 'GMECCSXX' CI-V.ID-NUMBER
        #SUFFIX
        CI-V.PLAN        (#IFL)
        CI-V.ISSUE-DATE  (#IFL)
        CI-V.PAID-TO-DATE(#IFL)
        #YYYYMMDD          /* assumed CI-V.PAID-UP-DATE(#IFL)
        CI-V.ISSUE-AGE   (#IFL)
        CI-V.ANNUAL-PREM (#IFL)
        CI-V.SMOKER-FLAG (#IFL)
        CI-V.FACE-AMOUNT (#IFL)
        CI-V.DURATION    (#IFL)
        #PAY-STATUS-48     /* assumed ci-V.PAY-STATUS  (#IFL)
        CI-V.TOT-PREMIUMS-PAID (#IFL)
        #AP-PREMS-PAID  /* calc tpp
*
        #MAX-7-PAY
        #MEC-PREMS-PAID-TO-DATE
        #EXCEEDS-MEC
        #1035-NX
        #1035-EXCH-AMT
        #LUMP-PMT
*
      IF #EXCEEDS-MEC NE 0
        MOVE 'press ENTER to see TAMRA info/This will be a Mec policy'
                                             TO #ERROR-MESSAGE
        MOVE '*' TO #CGA-SCR-COMMAND
        MOVE TRUE TO #TAMRA
      END-IF
    END-IF
*
    ADD 1 TO #I2
    MOVE CI-V.ISSUE-DATE (#IFL) TO #YYYYMMDD
*
    COMPRESS 'The cost to pay up the policy issued' #MM-A
                                         INTO #AD-TABLE (#I2)
    COMPRESS #AD-TABLE (#I2) '/' #DD-A '/' #YYYY-A
                                        INTO #AD-TABLE (#I2) LEAVING NO
    COMPRESS #AD-TABLE (#I2) ' is $' #DOLLARS-DISP '.'
                               INTO #AD-TABLE (#I2) LEAVING NO
  END-IF                                                          /* PM3
END-FOR
*
IF #PAY-UP
  IF #CGA-SCR-SUFFIX = 'PUM'
    IF #I2 LT 7                                                   /* PM1
      ADD 2 TO #I2
    ELSE                                                          /* PM1
      ADD 1 TO #I2                                                /* PM1
    END-IF                                                        /* PM1
    COMPRESS '  Enclosed are value projections of your AAFMAA'
             'insurance showing value'       INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'growth under your current plan and after being paid-up.'
             'Simply remit the'   INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'amount needed if you wish to pursue this action, or give'
             'us a call if you'  INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    MOVE  'have any questions.' TO #AD-TABLE (#I2)
*
    IF #I2 LT 11                                                  /* PM1
      ADD 2 TO #I2
    ELSE                                                          /* PM1
      ADD 1 TO #I2                                                /* PM1
    END-IF                                                        /* PM1
    COMPRESS 'Please write' #FORMAT-CN-CN 'on the check and make'
             'payable to AAFMAA.'           TO #AD-TABLE (#I2)
*
    PERFORM CORR-MSG-FORMAT
    ESCAPE ROUTINE
  ELSE
    ADD 1 TO #I2
    COMPRESS '  The above payoff information is valid through the end'
         'of' #F-MON-LIT (#TODAY-MM) #TODAY-YYYY
                INTO #AD-TABLE (#I2)
    COMPRESS #AD-TABLE (#I2) '.' INTO #AD-TABLE (#I2) LEAVING NO
    ADD 1 TO #I2
    COMPRESS 'For any payoff action after the above date, send'
             'the amount listed and we'  INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'will bill or refund any difference. You may also call'
             'our Policy'  INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'Services Department for the current cost prior to'
              'mailing your check.' TO #AD-TABLE (#I2)
  END-IF
END-IF
*
CALLNAT 'GMAXSXX'
  #CGA-ORIG-COMMAND
  #MAX-FACE-TOTAL
  #MAX-FACE-TERM
  #OVERRIDE-MAX-FACE-TOTAL
  #OVERRIDE-MAX-FACE-TERM
  #INTRO-FLAG
  #DATE-OF-BIRTH   /* DW8                                        /* TT3
*
COMPUTE #REM-FACE = #MAX-FACE-TOTAL - #TOT-FACE
*
IF  (#TOT-FACE GT (#MAX-FACE-TOTAL - 5000))
 OR (#DATE-OF-BIRTH LE (*DATN - 790000))     /* DW9              /* TT3
 OR (#RESTRICT-MBR-SW = 'Y')
 OR (#SUSPEND-CODE = '5' OR = '6')
  IF #I2 LT 11                                                    /* PM1
    ADD 2 TO #I2
  ELSE                                                            /* PM1
    ADD 1 TO #I2                                                  /* PM1
  END-IF
  MOVE 'You have the maximum available coverage.' TO #AD-TABLE (#I2)
*
  IF #SUSPEND-CODE = '5' OR = '6'
    COMPRESS 'Additional Insurance may be available if your medical'
         'condition has changed.' INTO #AD-TABLE (#I2)
  END-IF
*
  IF #NUM-POLS = 0
    COMPRESS #AD-TABLE (#I2) 'All policies are paid up.'
                           INTO #AD-TABLE (#I2)
    ESCAPE ROUTINE
  END-IF
ELSE
  IF *DEVICE = 'BATCH'
    ADD 1 TO #I2
  END-IF
*
  ADD 1 TO #I2
  MOVE EDITED #REM-FACE (EM=ZZZ,ZZ9) TO #REM-FACE-DISP
  COMPRESS '  You may apply for $' #REM-FACE-DISP
           ' added coverage.' INTO #AD-TABLE (#I2) LEAVING NO
END-IF
*
ADD 1 TO #I2
COMPRESS 'Please write' #FORMAT-CN-CN 'on the check and make'
         'payable to AAFMAA.'           TO #AD-TABLE (#I2)
*
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PUL-PROCESS
* * * * * * * * * * * * * * * * *
*
MOVE *DATN TO #TODAY-YYYYMMDD-N
FIND (1) CI-V WITH MEMBER-KEY = #CGA-ID-G
  FOR #IFL 1 TO 9
    IF CI-V.PAY-STATUS (#IFL) = '48'
      ADD 1 TO #NUM-PDUP
      MOVE #IFL TO #PDUP-X
      MOVE CI-V.ISSUE-DATE (#IFL) TO #ISS-DT (#NUM-PDUP)
      IF CI-V.MODE (#IFL) = 'G'
        MOVE TRUE TO #MODE-G
      END-IF
    END-IF
  END-FOR
END-FIND
*
IF #PDUP-X = 0
  MOVE 'No PUAP Policies' TO #ERROR-MESSAGE
  ESCAPE ROUTINE
END-IF
*
RESET #I2
ADD 1 TO #I2
* COMPRESS 'DEAR' #FULL-RANK #HOLD-LAST-NAME INTO #AD-TABLE (#I2)                     /* vt1
move #ext-dear-formated-name to #AD-TABLE (#I2)                                           /* vt1
COMPRESS #AD-TABLE (#I2) ':'     INTO #AD-TABLE (#I2) LEAVING NO
ADD 1 TO #I2
MOVE EDITED #HOLD-DEBIT-CREDIT-AMT (EM=ZZZ,ZZ9.99) TO #W-AMT
COMPRESS '  Thank you for your $' #W-AMT ' check. Your insurance with'
         ' the AAFMAA,'      TO #AD-TABLE (#I2) LEAVING NO
ADD 1 TO #I2
COMPRESS 'under Certificate No.'  #CGA-ID 'issued on'  TO #AD-TABLE (#I2)
*
MOVE #ISS-DT (1) TO #YYYYMMDD
COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
COMPRESS #AD-TABLE (#I2) #DATE-DISP TO #AD-TABLE (#I2)
ADD 1 TO #I2
*
IF #NUM-PDUP GE 2
  MOVE #ISS-DT (2) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 2
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 3
  MOVE #ISS-DT (3) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 3
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 4
  MOVE #ISS-DT (4) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 4
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 5
  MOVE #ISS-DT (5) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 5
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 6
  MOVE #ISS-DT (6) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 6
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 7
  MOVE #ISS-DT (7) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  IF #NUM-PDUP = 7
    COMPRESS #AD-TABLE (#I2) 'and' INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
  END-IF
  COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
*
IF #NUM-PDUP GE 8
  MOVE #ISS-DT (4) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DATE-DISP WITH '/'
  COMPRESS #AD-TABLE (#I2) 'and' #DATE-DISP INTO #AD-TABLE (#I2)
END-IF
COMPRESS #AD-TABLE (#I2) ',' INTO #AD-TABLE (#I2) LEAVING NO
*
IF #NUM-PDUP GE 4
  ADD 1 TO #I2
END-IF
*
COMPRESS #AD-TABLE (#I2) 'is now fully paid up as of'
                         INTO #AD-TABLE (#I2)
COMPRESS #TODAY-MM #TODAY-YYYY INTO #DATE-DISP WITH '/'
COMPRESS #AD-TABLE (#I2) #DATE-DISP INTO #AD-TABLE (#I2)
COMPRESS #AD-TABLE (#I2) '.' INTO #AD-TABLE (#I2) LEAVING NO
*
IF CI-V.FIRS-LOCATION-CODE (#PDUP-X) = '*'
  ADD 1 TO #I2
  IF #NUM-PDUP LE 4
    ADD 1 TO #I2
  END-IF
*
  IF #NUM-PDUP GT 1
    COMPRESS '  Enclosed are copies of the original data sheets, the'
             'originals are attached' INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'to your certificates of membership and insurance on'
             'file.'            INTO #AD-TABLE (#I2)
  ELSE
    COMPRESS '  Enclosed is a copy of the original data sheet, the'
             'original is attached' INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    COMPRESS 'to your certificate of membership and insurance on'
             'file.'            INTO #AD-TABLE (#I2)
  END-IF
ELSE
  ADD 1 TO #I2
  IF #NUM-PDUP GT 1
    COMPRESS '  Enclosed are amended data sheets to be attached'
             'to your original' INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    MOVE 'certificates of membership and insurance.' TO #AD-TABLE (#I2)
  ELSE
    COMPRESS '  Enclosed is an amended data sheet to be attached'
             'to your original' INTO #AD-TABLE (#I2)
    ADD 1 TO #I2
    MOVE 'certificate of membership and insurance.' TO #AD-TABLE (#I2)
  END-IF
END-IF
*
IF #MODE-G
  ADD 1 TO #I2
  COMPRESS '  A form to assist you in stopping your allotment is'
           'also enclosed. When' INTO #AD-TABLE (#I2)
  ADD 1 TO #I2
  COMPRESS 'we receive advice from the Finance Center of the'
           'discontinuance, we will'     INTO #AD-TABLE (#I2)
  ADD 1 TO #I2
  MOVE  'refund any premiums overpaid.' TO #AD-TABLE (#I2)
END-IF
*
ADD 1 TO #I2
COMPRESS 'Please let us know if you have any questions concerning'
         'this matter.'        INTO #AD-TABLE (#I2)
*
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALL-AOCX2V6A
* * * * * * * * * * * * * * * * *
*
CALLNAT 'C1601SCO'
  #CGA-ORIG-COMMAND
  #CGA-ORIG-ID
  #CGA-ID
  #CGA-SCR-SUFFIX
  #TO-PLAN
  #SEX                                   /* TT3
  #IFL
* #TODAY-YYYYMMDD-N
  #TRAN-DATE
  STATUS          (1:9)
  PLAN            (1:9)
  ONLINE-ACTION   (1:9)
  MODE            (1:9)
  CONVERSION-FLAG (1:9)
  SMOKER-FLAG     (1:9)
  ISSUE-AGE       (1:9)
  PAY-STATUS      (1:9)
  ISSUE-DATE      (1:9)
  PAID-TO-DATE    (1:9)
  FACE-AMOUNT     (1:9)
  ANNUAL-PREM     (1:9)
  LAST-DURATION   (1:9)
  CASH-VALUE      (1:9)
  DEATH-BENEFIT   (1:9)
  NUM-MBRSHIPS    (1:9)
  DURATION        (1:9)
  MONTH-IND       (1:9)
  MIN-CASH-VALUE  (1:9)
  NXT-YR-MIN-CASH-VALUE (1:9)
  NXT-MON-CASH-VALUE    (1:9)
  NXT-MON-DEATH-BENEFIT (1:9)
  ACC-CASH-VALUE        (1:9)
  CONV-DURATION         (1:9)
  MONTH-ALLOC-AMT       (1:9)
  PREM-DEFICIENCY       (1:9)
  BEG-CASH-VALUE        (1:9)
  NET-PR-RATIO          (1:9)
  #OUTPUT-CALC-AMT
  #OUTPUT-OLD-PDUP-AMT
  #PRORATE-LOAN
  #RPU-ACV
  #TOT-LOAN-AMOUNT
  #TOT-CURR-ACCUM-INT
  #NU-ANN-PREM
  #OUTPUT-PREM-DIFF
  #DUES-DIFF
  #LUMP-SUM
*
END-SUBROUTINE /* (1016)
*
END
