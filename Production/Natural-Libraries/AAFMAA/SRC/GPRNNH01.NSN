* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID GPRNNH01
DEFINE DATA
PARAMETER
1 #CN        (N6)
1 #PARM-DATE (N8) 1 REDEFINE #PARM-DATE
  2 #YEAR    (N4)
  2 #MONTH   (N2)
1 #MSG       (A) DYNAMIC
LOCAL
1 #CGA-CLERK (A3)
1 ST-V-UPD VIEW OF A-STATUS
  2 PAID-TO-DATE                      (N8.0/9)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME
  2 INTRO-FLAG
  2 PROCESS-IND
  2 STATUS                            (A1/9)
  2 PLAN                              (A3/9)
  2 PAY-STATUS                        (A2/9)
  2 PRIOR-STATUS                      (A2/9)
  2 MODE                              (A1/9)
  2 SMOKER-FLAG                       (A1/9)
  2 ISSUE-DATE                        (N8.0/9)
  2 SPLIT-IND                         (P1.0/9)
  2 PAID-TO-DATE                      (N8.0/9)
  2 BILL-TO-DATE                      (N8.0/9)
  2 FACE-AMOUNT                       (P7.2/9)
  2 ANNUAL-PREM                       (P5.2/9)
  2 DEBIT-CREDIT-AMT                  (P7.2)
  2 DATE-ACCT-UPDATE                  (N8)
1 BLANK            (A1) CONST <' '>
1 NURSING-HOME     (A2) CONST <'73'>
1 #1-YR-AGO        (N8) 1 REDEFINE #1-YR-AGO
  2 #1-YR-AGO-YYYY (N4)
  2 #MM            (N2)
  2 #DD            (N2)
1 #MMDDYYYY        (A8) 1 REDEFINE #MMDDYYYY
  2 #MMA           (A2)
  2 #DDA           (A2)
  2 #YYYY          (A4)
1 #SEG-X           (I2)   /* index of pol to build pol #
1 J                (I2)
1 #HOLD-CUR-ACCT-UPDT    (N8)     /* Not used
1 #EXTRA-STOP            (L)      /* Not used
1 #CMBX                  (A4)     /* Not used
1 #TIME-OF-UPDATE        (N7)     /* Not used
1 #SG-TBL                (15)
  2 #SG-CAT1             (A2)
  2 #SG-CAT2             (A2)
  2 #SG-AMT              (N6.2)
1 #SCR-LINES
  2 #SCR-TRAN-APPLIED (L/15)      /* Not used
  2 #SCR-ID-SSN (N9/15)           /* Not used
  2 #SCR-ID-SSN-CV (C/15)
  2 #SCR-EFF-DT (A8/15)           /* What value
  2 #SCR-EFF-DT-CV (C/15)
  2 #SCR-ISS-DT (A8/15)           /* Part of description
  2 #SCR-ISS-DT-CV (C/15)
  2 #SCR-ISS-DT-X (I2/15)         /* Not used
  2 #SCR-TRAN-CODE (A4/15)        /* set
  2 #SCR-TRAN-CODE-CV (C/15)
  2 #SCR-TRAN-AMT (N5.2/15)       /* set
  2 #SCR-TRAN-AMT-CV (C/15)
  2 #SCR-NAME (A25/15)            /* NOT USED
  2 #SCR-NAME-CV (C/15)
  2 #SCR-ADJ (A1/15)              /* set
  2 #SCR-ADJ-CV (C/15)
  2 #SCR-ALP-CODE (A1/15)         /* What value
  2 #SCR-ALP-CODE-CV (C/15)
  2 #SCR-AMT-FLAG (A1/15)         /* Not used
  2 #SCR-AMT-FLAG-CV (C/15)
  2 #SCR-CASH-CODE (A1/15)        /* set
  2 #SCR-CASH-CODE-CV (C/15)
END-DEFINE
*
ON ERROR
  CALLNAT 'GETERRD' #MSG
  ESCAPE ROUTINE
END-ERROR
*
RESET #MSG
#CGA-CLERK := 'EDS'
IF #CN <= 0
  #MSG := 'There is no Member Status Record'
END-IF
FIND (1) ST-V WITH ID-NUMBER = #CN
  IF NO RECORDS FOUND
    #MSG := 'There is no Member Status Record'
    ESCAPE ROUTINE
  END-NOREC
  FOR J 1 TO 9
    IF PAY-STATUS (J) EQ NURSING-HOME
      IF PAID-TO-DATE (J) < #PARM-DATE
        #1-YR-AGO := PAID-TO-DATE (J)
        IF #MM = 12
          #MM := 1
          ADD 1 TO #1-YR-AGO-YYYY
        ELSE
          #MM := #MONTH + 1
        END-IF
        GET ST-V-UPD *ISN (0093)
        ST-V-UPD.PAID-TO-DATE (J) := #1-YR-AGO
        UPDATE (0108)
        PERFORM ACCT-REGISTER
        #MSG := 'UPDATED'
      END-IF
    END-IF
  END-FOR
END-FIND
*
DEFINE ACCT-REGISTER
/*     -------------
#SCR-CASH-CODE (J)  := 'A'
#SCR-TRAN-CODE (J)  := 'PREP'
#SCR-ADJ       (J)  := '-'
#SCR-TRAN-AMT  (J)  := ANNUAL-PREM (J) / 12
#1-YR-AGO           := #PARM-DATE
MOVE EDITED #MM (EM=99) TO #MMA
MOVE EDITED #DD (EM=99) TO #DDA
MOVE EDITED #1-YR-AGO-YYYY (EM=9999) TO #YYYY
#SCR-EFF-DT    (J)  := #MMDDYYYY
#CMBX               := 'NHPW'
#1-YR-AGO := #PARM-DATE
SUBTRACT 1 FROM #1-YR-AGO-YYYY
#SG-AMT (1) := #SCR-TRAN-AMT  (J)
IF ISSUE-DATE (J) GT #1-YR-AGO
  MOVE '1Y' TO #SG-CAT1 (1)
ELSE
  MOVE 'RN' TO #SG-CAT1 (1)
END-IF
*
CALLNAT 'C2520SAM'
  #SCR-LINES
  ST-V.ID-NUMBER
  ST-V.PROCESS-IND
  ST-V.INTRO-FLAG
  ST-V.NAME
  ST-V.PLAN   (1:9)
  ST-V.DEBIT-CREDIT-AMT
  ST-V.DATE-ACCT-UPDATE
  #PARM-DATE
  #TIME-OF-UPDATE
  J
  #HOLD-CUR-ACCT-UPDT     /* Optional
  #CGA-CLERK
  #EXTRA-STOP             /* False
  #SG-TBL (1:15)
  #CMBX    /* Add #CMBX to pass Combo code Add #CMBX logic for ADCR/RDCR transactions
*
#SCR-TRAN-CODE (J)  := 'NHPW'
RESET #SCR-ADJ (J) #SG-CAT1 (1) #SG-AMT (1)
CALLNAT 'C2520SAM'
  #SCR-LINES
  ST-V.ID-NUMBER
  ST-V.PROCESS-IND
  ST-V.INTRO-FLAG
  ST-V.NAME
  ST-V.PLAN   (1:9)
  ST-V.DEBIT-CREDIT-AMT
  ST-V.DATE-ACCT-UPDATE
  #PARM-DATE
  #TIME-OF-UPDATE
  J
  #HOLD-CUR-ACCT-UPDT     /* Optional
  #CGA-CLERK
  #EXTRA-STOP             /* False
  #SG-TBL (1:15)
  #CMBX    /* Add #CMBX to pass Combo code Add #CMBX logic for ADCR/RDCR transactions
END-SUBROUTINE
END
