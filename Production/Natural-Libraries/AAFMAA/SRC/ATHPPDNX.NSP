* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPPDNX - DElete Other Companys records
*
DEFINE DATA
* * * * * * * *
* M O D I F I C A T I O N   L O G 
*
* DRW 03/09/2009 DW1 Increase Note Occurrance from 60 to 500
* NGG  20121010  NG1 CN Expansion Project -beneficiary data are
*                      moved to new database files
*
LOCAL
*
1 PD-V VIEW OF A-MBRSHIP-MTD-DETAIL
 2 ID-NUMBER
 2 ENTRY-DATE
 2 CODE
 2 ISSUE-DATE
 2 USER
*
1 NT-V VIEW OF A-NOTES
 2 C*NOTE-TABLE                     /* DW1
 2 ID-NUMBER       (N6)
 2 NOTE-DATE       (N8/500)         /* DW1
 2 NOTE-LINE-NUM   (500)            /* DW1
 2 NOTE-CLERK-ID   (A3/500)         /* DW1
 2 NOTE-TEXT       (A60/500)        /* DW1
*
1 #COUNTERS
 2 #CNT-1            (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
1 #TYPE   (A2)
1 #UPDATE   (L)
*
END-DEFINE
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'PD RECS DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'NS RECS DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'NX RECS DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE 'PD' TO #TYPE
READ PD-V BY ENTRY-DATE
  IF USER = ' ' OR = 'EDS' OR = 'DRW' OR = 'TDM'   /* AFSC EDS Users
    END TRANSACTION
    ESCAPE TOP
  END-IF
*    BELOW IS ALL OF THE  AFSC USERS
  IF USER = 'BJS' OR = 'DKD' OR = 'DLM' OR = 'DSW' OR = 'EMD' OR = 'GJD'
       OR = 'JAB' OR = 'JTK' OR = 'KRM' OR = 'MAS' OR = 'MMP' OR = 'MPR'
       OR = 'MS ' OR = 'PJC' OR = 'RAS' OR = 'SCC' OR = 'SLB' OR = 'SRS'
       OR = 'SWP' OR = 'WDS' OR = 'CGU' OR = 'RDB' OR = 'JMJ'
       OR = 'JAK'
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 #TYPE PD-V.ID-NUMBER USER CODE ENTRY-DATE
    DELETE
  END-IF
  END TRANSACTION
END-READ
*
MOVE 'NS' TO #TYPE
READ NT-V BY ID-NUMBER FROM 000000
* IF NT-V.ID-NUMBER GE 200000        NG1
  IF NT-V.ID-NUMBER GE 500000    /*  NG1
    END TRANSACTION
    ESCAPE BOTTOM
  END-IF
*
  RESET #UPDATE
  FOR #I1 1 TO C*NOTE-TABLE               /* DW1
    IF  (NOTE-CLERK-ID (#I1) = ' ' OR = 'EDS'
                              OR = 'DRW' OR = 'TDM')   /* AFSC EDS Users
     OR (NT-V.NOTE-TEXT (#I1) NE  MASK ('*@'))    /* Pending note
      ESCAPE TOP
    END-IF
*
*    BELOW IS ALL OF THE  AFSC USERS
    IF NOTE-CLERK-ID (#I1)= 'BJS' OR = 'DKD' OR = 'DLM' OR = 'DSW'
       OR = 'JAB' OR = 'JTK' OR = 'KRM' OR = 'MAS' OR = 'MMP' OR = 'MPR'
       OR = 'MS ' OR = 'PJC' OR = 'RAS' OR = 'SCC' OR = 'SLB' OR = 'SRS'
       OR = 'SWP' OR = 'WDS' OR = 'CGU' OR = 'RDB' OR = 'JMJ' OR = 'GJD'
       OR = 'JAK' OR = 'EMD'
      MOVE TRUE TO #UPDATE
      ADD 1 TO #CNT-3
      DISPLAY (3) #CNT-3 #TYPE  NT-V.ID-NUMBER NOTE-CLERK-ID (#I1)
         NOTE-CLERK-ID (#I1) NOTE-TEXT (#I1)
      PERFORM CLEAR-AND-COMPRESS
*
      FOR #I3 1 TO 4
*        WRITE NT-V.ID-NUMBER 'I1=' #I1
        IF   (NOTE-LINE-NUM  (#I1) NE 1)
         AND (NOTE-TEXT (#I1) NE ' ')
          WRITE (3) '  ***' NOTE-LINE-NUM (#I1) NOTE-TEXT (#I1)
          PERFORM CLEAR-AND-COMPRESS
        ELSE
          ESCAPE BOTTOM
        END-IF
      END-FOR
      SUBTRACT 1 FROM #I1
    END-IF
  END-FOR
*
  IF #UPDATE
    UPDATE (0081)
  END-IF
  END TRANSACTION
END-READ
*
MOVE 'NX' TO #TYPE
READ NT-V BY ID-NUMBER FROM 900000
  IF NT-V.ID-NUMBER GE 980000
    END TRANSACTION
    ESCAPE BOTTOM
  END-IF
*
  IF NOTE-CLERK-ID (2) = ' ' OR = 'EDS'
                             OR = 'DRW' OR = 'TDM'   /* AFSC EDS Users
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
*    BELOW IS ALL OF THE  AFSC USERS
  IF NOTE-CLERK-ID(2)= 'BJS' OR = 'DKD' OR = 'DLM' OR = 'DSW' OR = 'EMD'
       OR = 'JAB' OR = 'JTK' OR = 'KRM' OR = 'MAS' OR = 'MMP' OR = 'MPR'
       OR = 'MS ' OR = 'PJC' OR = 'RAS' OR = 'SCC' OR = 'SLB' OR = 'SRS'
       OR = 'SWP' OR = 'WDS' OR = 'CGU' OR = 'RDB' OR = 'JMJ' OR = 'GJD'
       OR = 'JAK'
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4 #TYPE  NT-V.ID-NUMBER NOTE-CLERK-ID (2)
       NOTE-CLERK-ID (2) NOTE-TEXT (1)
    FOR #I1 1 TO 60
      IF NOTE-TEXT (#I1) = ' '
        ESCAPE BOTTOM
      END-IF
*
      WRITE (4) NOTE-TEXT (#I1)
    END-FOR
*
    DELETE
END-IF
  END TRANSACTION
*
END-READ
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE CLEAR-AND-COMPRESS
* * * * * * * * * * * * * *
*
FOR #I2 #I1 TO 499    /* WAS 59                            DW1
  MOVE NOTE-TEXT     (#I2 + 1) TO NOTE-TEXT     (#I2)
  MOVE NOTE-DATE     (#I2 + 1) TO NOTE-DATE     (#I2)
  MOVE NOTE-LINE-NUM (#I2 + 1) TO NOTE-LINE-NUM (#I2)
  MOVE NOTE-CLERK-ID (#I2 + 1) TO NOTE-CLERK-ID (#I2)
END-FOR
*
RESET NOTE-TEXT     (500)                       /* DW1
RESET NOTE-DATE     (500)                       /* DW1
RESET NOTE-LINE-NUM (500)                       /* DW1
RESET NOTE-CLERK-ID (500)                       /* DW1
*
END-SUBROUTINE
*
END
