* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ADRN0006
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    08/17/2010  None    Program written                           *
* RSE    10/28/2010  RE1     Use subprogram to add new address pool id *
* NGG    07/07/2011  NG1     Limit to 5 contacts sharing address       *
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID        (N8)
1 #ADDR-ISN          (P8)
1 #CN-SYNC-ISNS      (P8/1:V)
LOCAL
1 J                  (I4)
1 X                  (I4)
1 #PAGE              (N2)
1 #TOT-PAGES         (N2)
1 #TOT-REL           (N3)
1 #L                 (N7)
1 #LINE              (N3/15)
1 #RELATIONS
  2 #REQUEST        (A1/5)
  2 #ADDR-TYPE      (A10/5)
  2 #CN             (N6/5)
  2 #RNAME          (A25/5)
  2 #ROLE-DESC      (A10/5)
  2 #RDOB           (A10/5)
  2 #RID            (N8/5)
  2 #ADDR-PROTECT   (A1/5)

1 #CONTACT-NAME     (A25)
1 #D                (D)
1 #DOB              (A10)
1 #ID-NUMBER        (A6)
1 #ISN              (P8)
1 #SHARED           (L)
1 #SECURED          (L)
1 #ADDR-ID          (N8)
1 #K                (I4)
1 A-ADD-REL VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID        (N8)
  2 ADDRESS-POOL-ID   (N8)
  2 ATTENTION-LINE    (A50)
  2 ADDR-LOCATION     (A1)
  2 ADDR-TYPE-CD      (A1)
  2 PREVENT-OVER-IND  (A1)
  2 ADDRESS-SOURCE    (A1)
  2 STATUS            (A1)
  2 STATUS-DATE       (N8) 2 REDEFINE STATUS-DATE
    3 STATUS-DATE-A   (A8)
  2 START-DATE        (N8) 2 REDEFINE START-DATE
    3 START-DATE-A    (A8)
  2 END-DATE          (N8) 2 REDEFINE END-DATE
    3 END-DATE-A      (A8)
  2 LAST-DATE-UPD     (N8) 2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A     (A8)
  2 LAST-TIME-UPD     (N7) 2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A     (A7)
  2 LAST-USER-UPD     (A8)

1 A-ADD-POOL VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID (N8)
  2 STREET          (A75)
  2 UNIT            (A25)
  2 CITY            (A50)
  2 STATE-CODE      (A2)
  2 ZIP-CODE        (N9) 2 REDEFINE ZIP-CODE
    3 ZIP-5         (N5)
    3 ZIP-4         (N4)
  2 COUNTRY-CODE    (A2)
  2 INTL-REGION     (A50)
  2 POSTAL-CODE     (A25)
  2 URBANIZATION    (A50)
  2 ADDR-FORMAT-TYPE-CD (A1)
  2 LAST-DATE-UPD   (N8)2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A   (A8)
  2 LAST-TIME-UPD   (N7) 2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A   (A7)
  2 LAST-USER-UPD   (A8)

1 NEW-ADD-POOL VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID (N8)
  2 STREET          (A75)
  2 UNIT            (A25)
  2 CITY            (A50)
  2 STATE-CODE      (A2)
  2 ZIP-CODE        (N9) 2 REDEFINE ZIP-CODE
    3 ZIP-5         (N5)
    3 ZIP-4         (N4)
  2 COUNTRY-CODE    (A2)
  2 INTL-REGION     (A50)
  2 POSTAL-CODE     (A25)
  2 URBANIZATION    (A50)
  2 ADDR-FORMAT-TYPE-CD (A1)
  2 LAST-DATE-UPD   (N8)2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A   (A8)
  2 LAST-TIME-UPD   (N7) 2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A   (A7)
  2 LAST-USER-UPD   (A8)
1 #NEW-ADDRESS-POOL-ID  (N8)

1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER (N6)
  2 CONTACT-ID
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-A (A8)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A (A8)
  2 SSN

1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD

1 STATUS VIEW OF A-STATUS
  2 C*LINKED-CN
  2 LINKED-CN (N6/30)

1 #CODE-NAME        (A) DYNAMIC
1 #POS              (I4)
1 BLANK             (A1) CONST <' '>
1 SECURED           (A1) CONST <'S'>
1 #MSG              (A65/4) INIT <
  'The following CNs share the current address.',
  'Type ''X'' next to CNs that should share the new address.',
  'CNs without ''X'' will no longer be shared.',
  'Contacts with no CN who share will be updated automatically.'
  >

END-DEFINE

DEFINE WINDOW CONFIRM
  SIZE 20*80
  BASE 8/4

SET WINDOW 'CONFIRM'

GET A-ADD-POOL #ADDR-ISN
#ADDR-ID := A-ADD-POOL.ADDRESS-POOL-ID

RESET #NEW-ADDRESS-POOL-ID
  J #RELATIONS
FIND A-ADD-REL WITH ADDRESS-POOL-ID = #ADDR-ID
  IF A-ADD-REL.CONTACT-ID = #CONTACT-ID
    ESCAPE TOP
  END-IF
  FIND (1) CONTACT WITH CONTACT-ID = A-ADD-REL.CONTACT-ID
  END-FIND
*  PRINT A-ADD-REL
*    / CONTACT
  IF CONTACT.ID-NUMBER > 0
      OR A-ADD-REL.PREVENT-OVER-IND = SECURED
    ADD 1 TO J
    IF J > 5               /* NG1
      ESCAPE BOTTOM
    END-IF
    #ADDR-TYPE    ( J ) := A-ADD-REL.ADDR-TYPE-CD
    #CODE-NAME     := 'ADDR-TYPE'
    #ADDR-TYPE    ( J ) := FUNCTION-ADDR-CODES(<#CODE-NAME,A-ADD-REL.ADDR-TYPE-CD>)
    #ADDR-PROTECT ( J ) := A-ADD-REL.PREVENT-OVER-IND
    #CN           ( J ) := CONTACT.ID-NUMBER
    #RID          ( J ) := A-ADD-REL.CONTACT-ID
    #RNAME (J) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    IF DATE-OF-BIRTH > 0
      MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
      MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB (J)
    ELSE
      RESET #RDOB (J)
    END-IF
    IF A-ADD-REL.PREVENT-OVER-IND = SECURED
      #REQUEST ( J ) := SECURED
    END-IF
  ELSE
    ESCAPE TOP
  END-IF

  FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID  /* Parent
      AND CONTACT-2-ID = A-ADD-REL.CONTACT-ID
    #ROLE-DESC (J) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
  END-FIND
  IF #ROLE-DESC (J) = BLANK
    FIND CONTACT-REL WITH CONTACT-2-ID = #CONTACT-ID /* Child
        AND CONTACT-1-ID = A-ADD-REL.CONTACT-ID
      #ROLE-DESC (J) := FUNCTION-DISP-RL-DESC(<ROLE-1-CD>)
    END-FIND
  END-IF
END-FIND

IF J <= 0
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF

#POS := POS ( #REQUEST (1) )
INPUT USING MAP 'ADRM0005'
DECIDE ON FIRST VALUE OF *PF-KEY
  VALUE 'ENTR'
    RESET X
    FOR J 1 TO 5
      IF #REQUEST (J) = BLANK OR= SECURED
        FIND (1) A-ADD-REL WITH ADDRESS-POOL-ID = #ADDR-ID
            AND CONTACT-ID = #RID (J)
          PERFORM NEW-ADDR-LINK
          A-ADD-REL.ADDRESS-POOL-ID := #NEW-ADDRESS-POOL-ID
          UPDATE
        END-FIND
      ELSE
        FIND (1) STATUS WITH ID-NUMBER = #CN ( J )
          ADD 1 TO X
          #CN-SYNC-ISNS ( X ) := *ISN (0221)
        END-FIND
      END-IF
    END-FOR
  NONE VALUE IGNORE
END-DECIDE

SET WINDOW OFF

DEFINE NEW-ADDR-LINK
/*     =============
IF #NEW-ADDRESS-POOL-ID = 0
*  READ (1) NEW-ADD-POOL DESCENDING BY ADDRESS-POOL-ID              /* RE1
*  END-READ
*  #NEW-ADDRESS-POOL-ID := NEW-ADD-POOL.ADDRESS-POOL-ID + 1
  CALLNAT 'ADRNT001' #NEW-ADDRESS-POOL-ID                                  /* RE1
  MOVE BY NAME A-ADD-POOL TO NEW-ADD-POOL
  NEW-ADD-POOL.ADDRESS-POOL-ID := #NEW-ADDRESS-POOL-ID
  STORE NEW-ADD-POOL
END-IF
END-SUBROUTINE

END
