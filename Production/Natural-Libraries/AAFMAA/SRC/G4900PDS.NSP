* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G4900PDS - DOCUMENT SIGNOUT FROM VUALT
*
* MODIFICATIONS:
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING GONERLXX      /* WORK FIELDS FOR GONERCXX
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER          (N6)
 2 NAME               (A25)
 2 RANK               (A3)
 2 PROCESS-IND        (A1)  /* PM - 06/2000 - SPLIT COMPANIES
*
1 FR-V VIEW OF A-FIRS
 2 ID-NUMBER          (N6)
 2 DOCU-LAST-FM-DATE  (N8)
*
1 TR-V VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER             (N6)
 2 NAME                  (A25)
 2 DATE-LAST-UPDATE      (N8)
 2 TERMINAL-ID           (A8)
 2 CLERK-ID              (A3)
 2 TRANSACTION-DATN      (P9)
 2 TRANSACTION-TIMN      (P7)
 2 OLD-DATA              (A125/1)
 2 PROCESS-IND           (A1)  /* PM - 06/2000 - SPLIT COMPANIES
*
1 TM-V VIEW OF A-TERMINALS
 2 CLERK-ID   (A3)
*
*
1 #SCR-CV       (C)
1 #I1           (I2)
1 #CLK          (A3)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INCLUDE GONERCXX     /* NATURAL ERROR ROUTINE
*
GET ST-V #CGA-ISN
*
IF (#CGA-CLERK NE #CGA-SCR-SUFFIX)
 AND (#CGA-SCR-SUFFIX NE ' ')
  MOVE #CGA-SCR-SUFFIX TO #CLK
ELSE
  MOVE #CGA-CLERK TO #CLK
END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
INPUT MARK *#CLK USING MAP 'G4900MDS'
*
IF  (#CGA-SCR-COMMAND NE '*')
 OR (#CGA-SCR-ID NE #CGA-ORIG-ID)
  FETCH 'G1000PXX'
END-IF
*
FIND (1) TM-V WITH CLERK-ID = #CLK
  IF NO RECORD FOUND
    REINPUT 'User-id invalid'
    MARK *#CLK
  END-NOREC
*
  IF #CLK = ' ' OR = 'VLT' OR = 'BOD' OR = 'POD' OR = 'EDS' OR = 'WYO'
                OR = 'X'
    REINPUT 'User-id must be entered'
    MARK *#CLK
  END-IF
END-FIND
*
MOVE #CGA-ID     TO TR-V.ID-NUMBER
MOVE ST-V.PROCESS-IND TO TR-V.PROCESS-IND /* PM - 06/2000 - SPLIT COMP
MOVE ST-V.NAME   TO TR-V.NAME
MOVE FR-V.DOCU-LAST-FM-DATE TO TR-V.DATE-LAST-UPDATE
MOVE *INIT-ID           TO TR-V.TERMINAL-ID
MOVE #CLK               TO TR-V.CLERK-ID
MOVE *DATN              TO TR-V.TRANSACTION-DATN
MOVE *TIMN              TO TR-V.TRANSACTION-TIMN
MOVE 'DS-DOCUMENT SIGNOUT' TO TR-V.OLD-DATA (1)
*
STORE TR-V
*
FIND (1) FR-V WITH MEMBER-KEY = #CGA-ID-G
  MOVE *DATN TO FR-V.DOCU-LAST-FM-DATE
  UPDATE (0091)
  END TRANSACTION
END-FIND
*
MOVE (AD=P) TO #SCR-CV
*
MOVE 'DS' TO #CGA-SCR-COMMAND
INPUT TEXT 'TRANSACTION COMPLETED' USING MAP 'G4900MDS'
*
FETCH 'G1000PXX'
*
END
