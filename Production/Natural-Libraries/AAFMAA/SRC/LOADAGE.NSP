* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
LOCAL
1 #IN-RECORD    (A1000)
1 #VALUES       (A10/10)
1 #NBT          (I4)
1 #TAB          (A1) INIT <H'09'>
1 PR-V VIEW OF A-PRODUCTS-RULES
  2 LAST-LOAD-USER                    (A8)
  2 LAST-LOAD-DATE                    (N8.0)
  2 LAST-LOAD-TIME                    (N7.0)
  2 PLANCODE                          (A3)
  2 TERM                              (N3.0)
  2 NICOTINE                          (A1)
  2 APPLICANT                         (A1)
  2 AGEMIN                            (N3.2)
  2 AGEMAX                            (N3.2)
  2 EFFECTIVE-DATE                    (N8.0)
  2 END-DATE                          (N8.0)
*
1 J                (I4)
1 K                (I4)
1 #RECS            (I4)
1 #RATE            (A6)
1 #RATES           (A) DYNAMIC
END-DEFINE
*
DEFINE WORK FILE 2 '/usr/SAG/tmp/productages_export.txt' TYPE 'ASCII'
*
READ PR-V
  COMPRESS FULL
    LAST-LOAD-USER
    LAST-LOAD-DATE
    LAST-LOAD-TIME
    PLANCODE
    TERM
    NICOTINE
    APPLICANT
    AGEMIN
    AGEMAX
    EFFECTIVE-DATE
    END-DATE
    INTO #IN-RECORD WITH DELIMITERS #TAB
  WRITE WORK 2 #IN-RECORD
  DELETE
  ADD 1 TO #RECS
  IF #RECS >= 100
    RESET #RECS
    END TRANSACTION
  END-IF
END-READ
END TRANSACTION
*
DEFINE WORK FILE 1 '/usr/SAG/tmp/productages.txt' TYPE 'ASCII'
*
NEWPAGE
RESET #RECS
PRINT 'LOADING NEW Product ages rules'
READ WORK FILE 1 #IN-RECORD
  ADD 1 TO #RECS
  IF #RECS = 1
    ESCAPE TOP
  END-IF
  RESET PR-V
  SEPARATE #IN-RECORD INTO #VALUES (*) WITH DELIMITER #TAB GIVING NUMBER #NBT
  PR-V.PLANCODE := #VALUES (1)
  IF #VALUES (2) NE ' '
    PR-V.TERM     := VAL (#VALUES (2))
  END-IF
  PR-V.NICOTINE := #VALUES (3)
  PR-V.APPLICANT := #VALUES (4)
  PR-V.AGEMIN    := VAL (#VALUES (5))
  PR-V.AGEMAX    := VAL (#VALUES (6))
  STORE PR-V
  END TRANSACTION
  PRINT
    LAST-LOAD-USER
    LAST-LOAD-DATE
    LAST-LOAD-TIME
    PLANCODE
    TERM
    NICOTINE
    APPLICANT
    AGEMIN
    AGEMAX
    EFFECTIVE-DATE
    END-DATE
*
END-WORK
END
