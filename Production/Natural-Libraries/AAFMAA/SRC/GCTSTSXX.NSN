* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GCTSTSXX   CONVERT ADDRESSES TO CITY STATES
*
DEFINE DATA
PARAMETER
*
1 #SPLIT      (A1)   /* S=split city/state into #city and #state
1 #ADDR-1     (A25)
1 #ADDR-2     (A25)
1 #ADDR-3     (A25)
*
1 #STREET     (A25)
1 #STREET-2   (A25)
1 #CITY       (A22)
1 #STATE      (A2)
1 #ERR-MSG    (A60)
*
LOCAL
*
1 #I1      (I2)
1 #CITY-1  (A25)
1 #ST-OCC         (10)
 2 #ST-X    (A25)
 2 REDEFINE #ST-X
  3 #ST-A2   (A2)
  3 #ST-REST (A23)
*
END-DEFINE
* * * * * * * * *
*
* WRITE 'top of GCTSTSXX'
* DISPLAY #SPLIT
*        #ADDR-2  (EM=XXXXXXXXXX)
*        #ADDR-3  (EM=XXXXXXXXXX)
*        #STREET  (EM=XXXXXXXXXX)
*        #STREET-2(EM=XXXXXXXXXX)
*        #CITY    (EM=XXXXXXXXXX)
*        #STATE   (EM=XXXXXXXXXX)
* IF #STREET = ' '   /* convert 3 addresses to city state
IF #SPLIT = 'S' /* split city/state into #city and #state
*   WRITE 'build street street2 fron 3 addrs'
  IF #ADDR-3 NE ' '
    MOVE #ADDR-1 TO #STREET
    MOVE #ADDR-2 TO #STREET-2
    SEPARATE #ADDR-3 INTO #CITY-1
        #ST-X (1) #ST-X (2) #ST-X (3) #ST-X (4) #ST-X (5)
        #ST-X (6) #ST-X (7) #ST-X (8) #ST-X (9) #ST-X (10)
  ELSE
    IF #ADDR-2 NE ' '
      MOVE #ADDR-1 TO #STREET
      RESET #STREET-2
      SEPARATE #ADDR-2 INTO #CITY-1
          #ST-X (1) #ST-X (2) #ST-X (3) #ST-X (4) #ST-X (5)
          #ST-X (6) #ST-X (7) #ST-X (8) #ST-X (9) #ST-X (10)
    ELSE
      RESET #STREET
      RESET #STREET-2
      SEPARATE #ADDR-1 INTO #CITY-1
           #ST-X (1) #ST-X (2) #ST-X (3) #ST-X (4) #ST-X (5)
           #ST-X (6) #ST-X (7) #ST-X (8) #ST-X (9) #ST-X (10)
    END-IF
  END-IF
*
  FOR #I1 10 TO 1 STEP -1
    IF #ST-X (#I1) NE ' '
      IF #ST-REST (#I1) = ' '
        MOVE #ST-X (#I1) TO #STATE
        RESET #ST-X (#I1)
      END-IF
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  COMPRESS #CITY-1 #ST-X (1) #ST-X (2) #ST-X (3) #ST-X (4) #ST-X (5)
     INTO #CITY
ELSE    /* conpress #city and #STATE into city/state
*  WRITE 'build 3 addres from street street2'
  MOVE #STREET TO #ADDR-1
*
  IF #STREET-2 = ' '
    COMPRESS #CITY #STATE INTO #ADDR-2
    RESET #ADDR-3
  ELSE
    MOVE #STREET-2 TO #ADDR-2
    COMPRESS #CITY #STATE INTO #ADDR-3
  END-IF
END-IF
*
END
