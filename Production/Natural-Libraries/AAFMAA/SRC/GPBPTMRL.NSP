* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GPBPTMRL
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* PAM    03/18/2011  None    Program written                           *
************************************************************************
*
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 OUT-TERM-REF-AMOUNT     (99)
 2 OUT-TERM-REF-YEAR       (99)
 2 OUT-TERM-REF-ENTRY-DATE (99)
*
1 #I1                      (I2)
1 #IN-REC                  (A100)
1 #TAB                     (A1)   INIT <H'09'>
*
1 #INPUT-TABLE
 2 #INP-CN                 (A)  DYNAMIC
 2 #INP-YEAR               (A)  DYNAMIC
 2 #INP-AMOUNT             (A)  DYNAMIC
*
1 #CN                      (N6)
1 #YEAR                    (N4)
1 #AMOUNT                  (N7.2)
*
END-DEFINE
************************************************************************
*
DEFINE WORK FILE 1 TYPE 'ASCII-COMPRESSED'
*
READ WORK FILE 1 #IN-REC
*
  SEPARATE #IN-REC INTO #INP-CN #INP-YEAR #INP-AMOUNT IGNORE
    WITH DELIMITER #TAB
*
  IF #INP-CN NE MASK (9)
    ESCAPE TOP
  END-IF
*
  #CN          := VAL (#INP-CN)
  #YEAR        := VAL (#INP-YEAR)
  #AMOUNT      := VAL (#INP-AMOUNT)
*
  ST01. FIND (1) ST-V WITH ST-V.ID-NUMBER = #CN
    IF NO RECORDS FOUND
      DISPLAY (1) #CN
      ESCAPE BOTTOM
    END-NOREC
*
    FOR #I1 1 TO 99
      IF ST-V.OUT-TERM-REF-AMOUNT (#I1) = 0
        ST-V.OUT-TERM-REF-AMOUNT (#I1)      := #AMOUNT
        ST-V.OUT-TERM-REF-YEAR (#I1)        := #YEAR
        ST-V.OUT-TERM-REF-ENTRY-DATE (#I1)  := *DATN
        DISPLAY (2) ST-V.ID-NUMBER #YEAR #AMOUNT
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    UPDATE (ST01.)
  END-FIND
*
  END TRANSACTION
END-WORK
*
END
