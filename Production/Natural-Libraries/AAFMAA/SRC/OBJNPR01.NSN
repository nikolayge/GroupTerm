* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*  PROGRAM ID : OBJNPR01
*
DEFINE DATA
PARAMETER USING OBJAPR01
PARAMETER
1 #REQUEST (A) DYNAMIC
1 #MSG     (A) DYNAMIC
LOCAL USING OBJLPR01
LOCAL
1 PR-UV VIEW OF A-PROCESS-REQUEST
  2 REQUEST-STATUS       (A1/1:10)
  2 REQUEST-STATUS-DATE  (N8/1:10)
  2 REQUEST-FINAL-STATUS (A1)
  2 REQUEST-FS-DATE      (N8)
1 #ALL-COMPLETED    (L)
*
1 #NOT-EXIST      (A) DYNAMIC CONST <'No record exist'>
1 #NOTHING-UPDATE (A) DYNAMIC CONST <'No record to delete'>
1 #NOTHING-DELETE (A) DYNAMIC CONST <'No record to update'>
1 Completed       (A1) CONST <'C'>
1 BLANK           (A1) CONST <' '>
1 #J              (I4)
1 #TEMP           (A) DYNAMIC
1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
*
1 #ERROR-TIME             (A19)
*
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE 'ADD'          PERFORM ADD-RECORD
  VALUE 'UPDATE'       PERFORM UPDATE-RECORD
  VALUE 'DELETE'       PERFORM DELETE-RECORD
  VALUE 'GET'          PERFORM GET-RECORD
  VALUE 'GET_BY_CN'    PERFORM GET-BY-CN
  VALUE 'GET_BY_CI'    PERFORM GET-BY-CI
  VALUE 'EXIST'        PERFORM CHECK-EXIST
  VALUE 'GET_BY_DATE'  PERFORM GET-BY-DATE
  VALUE 'UPDATE_RFS'   PERFORM UPDATE-RFS   /* Request Final Status
  NONE VALUE
    IGNORE
END-DECIDE
*
DEFINE CHECK-EXIST
/*     -----------
F-NUMBER.
FIND NUMBER PR-V WITH REQUEST-TYPE = #PROCESS-REQ.REQUEST-TYPE
END-SUBROUTINE
/*
DEFINE GET-RECORD
/*     ----------
FIND PR-V WITH REQUEST-TYPE = #PROCESS-REQ.REQUEST-TYPE
    AND REQUEST-KEY = #PROCESS-REQ.REQUEST-KEY
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME PR-V TO #PROCESS-REQ
END-FIND
END-SUBROUTINE
/**
DEFINE GET-BY-CN
/*     ----------
FIND PR-V WITH REQUEST-TYPE = #PROCESS-REQ.REQUEST-TYPE
    AND ID-NUMBER = #PROCESS-REQ.ID-NUMBER
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME PR-V TO #PROCESS-REQ
END-FIND
END-SUBROUTINE
/**
DEFINE GET-BY-CI
/*     ----------
FIND PR-V WITH REQUEST-TYPE = #PROCESS-REQ.REQUEST-TYPE
    AND CONTACT-ID = #PROCESS-REQ.CONTACT-ID
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME PR-V TO #PROCESS-REQ
END-FIND
END-SUBROUTINE
/**
DEFINE UPDATE-RFS
/*     ----------
FIND PR-V WITH REQUEST-TYPE = #PROCESS-REQ.REQUEST-TYPE
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'
    ESCAPE BOTTOM
  END-NOREC
  IF PR-V.REQUEST-FINAL-STATUS NE BLANK
      AND PR-V.REQUEST-FINAL-STATUS NE Completed
    #ALL-COMPLETED := TRUE
    COMPRESS PR-V.PROCESS-STATUS (*) INTO #TEMP
    IF #TEMP NE BLANK
      FOR #J 1 TO 10
        IF PR-V.PROCESS-STATUS (#J) NE BLANK
            AND PR-V.PROCESS-STATUS (#J) NE Completed
          RESET #ALL-COMPLETED
        END-IF
      END-FOR
      IF #ALL-COMPLETED
        GET PR-UV *ISN (0108)
        FOR #J 1 10
          IF PR-UV.REQUEST-STATUS (#J) = BLANK
            PR-UV.REQUEST-STATUS (#J) := PR-V.REQUEST-FINAL-STATUS
            PR-UV.REQUEST-STATUS-DATE (#J) := PR-V.REQUEST-FS-DATE
            ESCAPE BOTTOM
          END-IF
        END-FOR
        PR-UV.REQUEST-FINAL-STATUS := Completed
        PR-UV.REQUEST-FS-DATE := *DATN
        UPDATE (0125)
        END TRANSACTION
      END-IF
    END-IF
  END-IF
END-FIND
END-SUBROUTINE
/**
ON ERROR
/*
  CALLNAT 'USR2001N' P_PARMS
/*
  MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
    TO #ERROR-TIME
/*
  COMPRESS
    'Error number:'  P_ERROR_NUM
    'Error class:'   P_ERROR_CLASS
    'Error line:'    P_ERROR_LINE
    'Error program:' P_ERROR_PROGRAM
    'Error program type:'      P_ERROR_PGM_TYPE
    'Error program type long:' P_ERROR_PGM_TYPE_LONG
    'Error program level:'     P_ERROR_LEVEL
    'Language at error time:'  P_ERROR_LANG
    'Error library:' P_ERROR_LIB
    'DBID:'          P_ERROR_DBID
    'FNR:'           P_ERROR_FNR
    'Error type:'    P_ERROR_TYPE
    'Error time:'    #ERROR-TIME
    INTO #MSG
  ESCAPE ROUTINE
END-ERROR
*
END
