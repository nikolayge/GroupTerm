* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
** PROGRAM-ID     ATHPWHIS
** ***** update threshold info on d3180pba ba val at year-end
** ***** DON'T FORGET TO RUN ATHPSCYE ALSO TO BUMP DATES BY 1 YEAR ***
** ***** also ATHJNMBL & ATHJDETH <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<< ***
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* SAG  11282005 RH1  SS - Modified the custom coded ON ERROR stmt &
*                    replaced it with copycode GABNDCX1         
* DRW  01229006 DW1  Add Entry for FICA Wage Wage Base
*******************************************
**
**
**   REPORT-FILE        ASSIGN REPORT 1
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 ABEND-CODE                 (I4)
1 #NEW-YEAR-INFO
  2 #NY-YEAR    (N4.0) INIT <2007>    /* DW1
  2 #NY-MAX     (P7.0) INIT <97500>
1 #PREVIOUS-YEAR-INFO
  2 #PY-YEAR    (N4.0) INIT <2006>
  2 #PY-WAGE    (P7.0) INIT <94200>
1 #PREVIOUS-PREVIOUS-YEAR-INFO
  2 #PPY-YEAR   (N4.0) INIT <2005>
  2 #PPY-WAGE   (P7.0) INIT <90000>
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER          (N6)
  2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A        (A6)
  2 WIDOW-FLAG         (A1)
  2 PROCESS-IND
  2 INTRO-FLAG
  2 NAME
  2 STATUS-CODE
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
   3 DOD-YYYY   (N4)
   3 DOD-MM     (N2)
   3 DOD-DD     (N2)
  2 ACTIVE-DUTY-PAY-YTD
  2 LAST-FM-DATE-WH
  2 LOW-YEAR-IN-TABLE
  2 C*WAGE-HISTORY-TABLE
  2 WHT-YEAR        (N4.0/60)
  2 WHT-WAGE        (P7.0/60)
  2 WHT-FLAG        (A1/60)
1 #N        (I2)
1 #T        (I2)
1 #NO-RECD-ACC   (P4.0)
1 #NO-RECD       (P5.0)
1 #ACC-WAGE      (P5.0)
1 #ACC-Z         (P5.0)
1 #ACC-M         (P5.0)
1 #ACC-NO-WAGE   (P5.0)
1 #PREV-WAGE     (P5.0)
1 #UPDATE-RECD   (P5.0)
1 #DOD-ZAP       (P5.0)
1 #ACC-WAGE-LT-MAX-N (P5)
1 #ACC-WAGE-GE-MAX-N (P5)
1 #ACC-WAGE-LT-MAX   (P5)
1 #ACC-WAGE-GE-MAX   (P5)
1 #PREV-WAGE-MAX     (P5)
END-DEFINE
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR       /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'C U R R E N T  R C D' ST-V.ID-NUMBER-A
*     MOVE +4095 TO ABEND-CODE
*     CALL 'ABEND' USING ABEND-CODE
#BATCH-LITERAL-1 := ST-V.ID-NUMBER-A
INCLUDE GABNDCX1                                       /* SAG RH1 11282005 SS - END
END-ERROR
*
FORMAT     PS=76   LS=133
FORMAT (2) PS=76   LS=133
FORMAT (3) PS=76   LS=133
*
WRITE TITLE LEFT 'PROGRAM: ' *PROGRAM  /* PAPER COPY
      53T 'AAFMAA'
      84T 'PAGE' 89T *PAGE-NUMBER  5X 'DATE:' *DATN 2X 'TIME:' *TIMX
    / 35T 'YEAR-END WAGE HISTORY UPDATE - ADDED WAGE HISTORY'   /
*
WRITE (2) TITLE LEFT 'PROGRAM: ' *PROGRAM  /* FICHE COPY
      53T 'AAFMAA'
      84T 'PAGE' 89T *PAGE-NUMBER  5X 'DATE:' *DATN 2X 'TIME:' *TIMX
    / 35T 'YEAR-END WAGE HISTORY UPDATE - ADDED WAGE HISTORY'   /
*
WRITE (3) TITLE LEFT 'PROGRAM: ' *PROGRAM   /* ACTIVE PAY USED REPORT
      53T 'AAFMAA'
      84T 'PAGE' 89T *PAGE-NUMBER  5X 'DATE:' *DATN 2X 'TIME:' *TIMX
    / 38T 'YEAR-END WAGE HISTORY UPDATE - ACTIVE PAY USED'   /
*
READ ST-V BY MEMBER-KEY FROM '001900'
* READ ST-V BY MEMBER-KEY FROM '086000'        /* <<><<
* IF ST-V.ID-NUMBER GE 86000
*   ESCAPE BOTTOM
* END-IF
*
  IF  (WIDOW-FLAG = 'Y')                    /* bypass widows
   OR (PROCESS-IND = 'I' OR = 'O' OR = 'D' OR = 'C')
   OR (INTRO-FLAG = 'S' OR = 'K' OR = 'G')  /* bypass sps/gen
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF C*WAGE-HISTORY-TABLE GT 0
    PERFORM WAGE-UPDATE
    UPDATE
  ELSE
    IF ACTIVE-DUTY-PAY-YTD GT 0
      IF ST-V.ACTIVE-DUTY-PAY-YTD LT #PY-WAGE
        ADD 1 TO #ACC-WAGE-LT-MAX-N
        MOVE #NY-YEAR                 TO ST-V.WHT-YEAR (1)
        RESET                            ST-V.WHT-WAGE (1)
        RESET                            ST-V.WHT-FLAG (1)
        MOVE #PY-YEAR                 TO ST-V.WHT-YEAR (2)
        MOVE ST-V.ACTIVE-DUTY-PAY-YTD TO ST-V.WHT-WAGE (2)
        MOVE 'E'                      TO ST-V.WHT-FLAG (2)
        MOVE #PY-YEAR                 TO ST-V.LOW-YEAR-IN-TABLE
        UPDATE
      ELSE
        ADD 1 TO #ACC-WAGE-GE-MAX-N
      END-IF
*
      ADD +1 TO #NO-RECD-ACC
      WRITE   20T 'ID-' ST-V.ID-NUMBER    /* PAPER COPY
              '  NO WAGE-HISTORY RECORD - ACCUM YTD WAGES = $'
              ACTIVE-DUTY-PAY-YTD
      WRITE (2)   20T 'ID-' ST-V.ID-NUMBER   /* FICHE COPY
              '  NO WAGE-HISTORY RECORD - ACCUM YTD WAGES = $'
              ACTIVE-DUTY-PAY-YTD
    ELSE
      ADD +1 TO #NO-RECD
    END-IF
  END-IF
*
  END TRANSACTION
END-READ
*
READ ST-V BY WIDOW-KEY FROM '001900'
* ESCAPE BOTTOM        /* <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
  IF  (PROCESS-IND = 'I' OR = 'O' OR = 'D') /* bypass ins only
   OR (INTRO-FLAG = 'S' OR = 'K' OR = 'G')  /* bypass sps/gen
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF C*WAGE-HISTORY-TABLE GT 0
    PERFORM WAGE-UPDATE
    UPDATE
  ELSE
    IF ACTIVE-DUTY-PAY-YTD GT 0
      IF ST-V.ACTIVE-DUTY-PAY-YTD LT #PY-WAGE
        ADD 1 TO #ACC-WAGE-LT-MAX-N
        MOVE #NY-YEAR                 TO ST-V.WHT-YEAR (1)
        RESET                            ST-V.WHT-WAGE (1)
        RESET                            ST-V.WHT-FLAG (1)
        MOVE #PY-YEAR                 TO ST-V.WHT-YEAR (2)
        MOVE ST-V.ACTIVE-DUTY-PAY-YTD TO ST-V.WHT-WAGE (2)
        MOVE 'E'                      TO ST-V.WHT-FLAG (2)
        MOVE #PY-YEAR                 TO ST-V.LOW-YEAR-IN-TABLE
        UPDATE
      ELSE
        ADD 1 TO #ACC-WAGE-GE-MAX-N
      END-IF
*
      ADD +1 TO #NO-RECD-ACC
      WRITE   20T 'ID-' ST-V.ID-NUMBER    /* PAPER COPY
              '  NO WAGE-HISTORY RECORD - ACCUM YTD WAGES = $'
              ACTIVE-DUTY-PAY-YTD
      WRITE (2)   20T 'ID-' ST-V.ID-NUMBER   /* FICHE COPY
              '  NO WAGE-HISTORY RECORD - ACCUM YTD WAGES = $'
              ACTIVE-DUTY-PAY-YTD
    ELSE
      ADD +1 TO #NO-RECD
    END-IF
  END-IF
*
  END TRANSACTION
END-READ
*
WRITE ///  ' '
WRITE / 30T 'NUMBER OF NO W/H RECS OR YTD ACCUM     ' #NO-RECD
WRITE / 30T 'NUMBER OF ADDED W/H RECS WITH YTD ACCUM' #NO-RECD-ACC
WRITE / 30T 'NUMBER OF ADDED W/H RECS YTD LESS TN MX' #ACC-WAGE-LT-MAX-N
WRITE / 30T 'NUMBER OF ADDED W/H RECS YTD =GTR TN MX' #ACC-WAGE-GE-MAX-N
WRITE / 30T 'NUMBER OF UPDATED W/H WITH YTD ACCUM   ' #ACC-WAGE
WRITE / 30T 'NUMBER OF UPDTD W/H RECS YTD LESS TN MX' #ACC-WAGE-LT-MAX
WRITE / 30T 'NUMBER OF UPDTD W/H RECS YTD =GTR TN MX' #ACC-WAGE-GE-MAX
WRITE / 30T 'NUMBER OF UPDATED W/H USING PREV YRS WG' #PREV-WAGE
WRITE / 30T 'NBR OF UPDTD W/H USING PRIOR MAX WAGE  ' #PREV-WAGE-MAX
WRITE / 30T 'NUMBER OF WAGES ALREADY ENTERED        ' #ACC-NO-WAGE
WRITE / 30T 'NUMBER OF STATUS Z                     ' #ACC-Z
WRITE / 30T 'NUMBER OF wages zapped due to after dod' #DOD-ZAP
WRITE / 30T 'NUMBER OF WAGE HISTORY UPDATES         ' #UPDATE-RECD
WRITE (2) ///  ' '
WRITE (2) / 30T 'NUMBER OF NO W/H RECS OR YTD ACCUM     ' #NO-RECD
WRITE (2) / 30T 'NUMBER OF ADDED W/H RECS WITH YTD ACCUM' #NO-RECD-ACC
WRITE (2) / 30T 'NBR OF ADDED W/H RCS YTD LESS TN MX' #ACC-WAGE-LT-MAX-N
WRITE (2) / 30T 'NBR OF ADDED W/H RCS YTD =GTR TN MX' #ACC-WAGE-GE-MAX-N
WRITE (2) / 30T 'NUMBER OF UPDATED W/H WITH YTD ACCUM   ' #ACC-WAGE
WRITE (2) / 30T 'NBR OF UPDTD W/H RECS YTD LESS TN MX' #ACC-WAGE-LT-MAX
WRITE (2) / 30T 'NBR OF UPDTD W/H RECS YTD =GTR TN MX' #ACC-WAGE-GE-MAX
WRITE (2) / 30T 'NUMBER OF UPDATED W/H USING PREV YRS WG' #PREV-WAGE
WRITE (2) / 30T 'NBR OF UPDTD W/H USING PRIOR MAX WAGE  ' #PREV-WAGE-MAX
WRITE (2) / 30T 'NUMBER OF WAGES ALREADY ENTERED        ' #ACC-NO-WAGE
WRITE (2) / 30T 'NUMBER OF STATUS Z                     ' #ACC-Z
WRITE (2) / 30T 'NUMBER OF wages zapped due to after dod' #DOD-ZAP
WRITE (2) / 30T 'NUMBER OF WAGE HISTORY UPDATES         ' #UPDATE-RECD
WRITE (3) ///  ' '
WRITE (3) / 30T 'NUMBER OF NO W/H RECS OR YTD ACCUM     ' #NO-RECD
WRITE (3) / 30T 'NUMBER OF ADDED W/H RECS WITH YTD ACCUM' #NO-RECD-ACC
WRITE (3) / 30T 'NBR OF ADDED W/H RCS YTD LESS TN MX' #ACC-WAGE-LT-MAX-N
WRITE (3) / 30T 'NBR OF ADDED W/H RCS YTD =GTR TN MX' #ACC-WAGE-GE-MAX-N
WRITE (3) / 30T 'NUMBER OF UPDATED W/H WITH YTD ACCUM   ' #ACC-WAGE
WRITE (3) / 30T 'NBR OF UPDTD W/H RECS YTD LESS TN MX' #ACC-WAGE-LT-MAX
WRITE (3) / 30T 'NBR OF UPDTD W/H RECS YTD =GTR TN MX' #ACC-WAGE-GE-MAX
WRITE (3) / 30T 'NUMBER OF UPDATED W/H USING PREV YRS WG' #PREV-WAGE
WRITE (3) / 30T 'NBR OF UPDTD W/H USING PRIOR MAX WAGE  ' #PREV-WAGE-MAX
WRITE (3) / 30T 'NUMBER OF WAGES ALREADY ENTERED        ' #ACC-NO-WAGE
WRITE (3) / 30T 'NUMBER OF STATUS Z                     ' #ACC-Z
WRITE (3) / 30T 'NUMBER OF wages zapped due to after dod' #DOD-ZAP
WRITE (3) / 30T 'NUMBER OF WAGE HISTORY UPDATES         ' #UPDATE-RECD
*
* * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE WAGE-UPDATE
* * * * * * * * * * * * * * * * * * * * * * *
*
IF ST-V.WHT-YEAR (1) = #NY-YEAR
  WRITE / 20T ST-V.ID-NUMBER 30T 'ERROR IN YEAR' 50T ST-V.WHT-YEAR (1)
            60T #PY-YEAR
  ESCAPE ROUTINE
END-IF
*
MOVE ST-V.C*WAGE-HISTORY-TABLE TO #T    /* SAVE ORIGINAL NUM ENTRIES
FOR #N ST-V.C*WAGE-HISTORY-TABLE TO 1 STEP -1
  MOVE ST-V.WHT-YEAR (#N) TO ST-V.WHT-YEAR (#N + 1)
  MOVE ST-V.WHT-WAGE (#N) TO ST-V.WHT-WAGE (#N + 1)
  MOVE ST-V.WHT-FLAG (#N) TO ST-V.WHT-FLAG (#N + 1)
END-FOR
ADD 1 TO #T                                 /* INCREASE BY 1 FOR CURR YR
*
MOVE #NY-YEAR TO ST-V.WHT-YEAR (1)
RESET            ST-V.WHT-WAGE (1)
RESET            ST-V.WHT-FLAG (1)
*
IF ST-V.STATUS-CODE = 'Z'
  ADD +1 TO #ACC-Z
ELSE
  IF (ST-V.STATUS-CODE = 'M') AND (ST-V.WHT-WAGE (2) = 0)
    IF ST-V.WHT-YEAR (2) = #PY-YEAR
      ADD +1 TO #ACC-M
      MOVE #PY-WAGE TO ST-V.WHT-WAGE (2)   /* USE PRIOR-MAX
      MOVE 'E'      TO ST-V.WHT-FLAG (2)
      WRITE / 20T ST-V.ID-NUMBER 30T 'USE-MAX' 50T #PY-WAGE
    ELSE
      WRITE / 20T ST-V.ID-NUMBER 30T '"M" UPDT BYPASSED - MISMATCHED YR'
    END-IF
  ELSE
    IF ST-V.ACTIVE-DUTY-PAY-YTD NE 0
      ADD +1 TO #ACC-WAGE
      WRITE (3) / 20T 'ID-' ST-V.ID-NUMBER
              '  ACCUM YTD WAGES = $'
              ST-V.ACTIVE-DUTY-PAY-YTD
*
      IF ST-V.ACTIVE-DUTY-PAY-YTD LT #PY-WAGE
        ADD 1 TO #ACC-WAGE-LT-MAX
*
        IF ST-V.WHT-YEAR (2) NE #PY-YEAR
          FOR #N #T TO 2 STEP -1      /* INSERT A BUCKET FOR PRIOR YEAR
            MOVE ST-V.WHT-YEAR (#N) TO ST-V.WHT-YEAR (#N + 1)
            MOVE ST-V.WHT-WAGE (#N) TO ST-V.WHT-WAGE (#N + 1)
            MOVE ST-V.WHT-FLAG (#N) TO ST-V.WHT-FLAG (#N + 1)
          END-FOR
          ADD 1 TO #T                /* INCREASE FOR ADDED BUCKET
        END-IF
*
        MOVE #PY-YEAR                 TO ST-V.WHT-YEAR (2)
        MOVE ST-V.ACTIVE-DUTY-PAY-YTD TO ST-V.WHT-WAGE (2)
        MOVE 'E'                      TO ST-V.WHT-FLAG (2)
      ELSE
        ADD 1 TO #ACC-WAGE-GE-MAX
*
        IF ST-V.WHT-YEAR (2) = #PY-YEAR
          FOR #N 2 TO #T          /* REMOVE THIS YEAR SINCE MAXED
            MOVE ST-V.WHT-YEAR (#N + 1) TO ST-V.WHT-YEAR (#N)
            MOVE ST-V.WHT-WAGE (#N + 1) TO ST-V.WHT-WAGE (#N)
            MOVE ST-V.WHT-FLAG (#N + 1) TO ST-V.WHT-FLAG (#N)
          END-FOR
          SUBTRACT 1 FROM #T      /* DECREASE FOR DELETED BUCKET
        END-IF
      END-IF
    ELSE
      IF ST-V.WHT-WAGE (2) GT 0
        ADD +1 TO #ACC-NO-WAGE
      ELSE
        IF ST-V.WHT-YEAR (3) = #PPY-YEAR    /* PRIOR-PRIOR YEAR EXISTS
          MOVE ST-V.WHT-WAGE (3) TO ST-V.WHT-WAGE (2)
          MOVE 'E'               TO ST-V.WHT-FLAG (2)
          ADD +1 TO #PREV-WAGE
        ELSE
          IF ST-V.WHT-YEAR (2) = #PY-YEAR
            MOVE #PPY-WAGE TO ST-V.WHT-WAGE (2)   /* USE PRIOR-PRIOR-MAX
            MOVE 'E'       TO ST-V.WHT-FLAG (2)
            ADD +1 TO #PREV-WAGE-MAX
            WRITE / 20T ST-V.ID-NUMBER 30T 'PRIOR-PRIORMX' 50T #PPY-WAGE
          ELSE
            WRITE / 20T ST-V.ID-NUMBER
                 30T 'PR/PR/MX UPDT BYPASSED - MISMATCHED YR'
          END-IF
        END-IF
*
        IF   (DOD-YYYY NE 0)
         AND (ST-V.WHT-YEAR (2) GE DOD-YYYY)
          WRITE 25T ST-V.ID-NUMBER WHT-YEAR (2) WHT-WAGE (2) DOD-YYYY
                                             'Wage Zapped due to DOD'
          RESET ST-V.WHT-WAGE (2)
          ADD 1 TO #DOD-ZAP
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
* MOVE *DATN TO ST-V.LAST-FM-DATE-WH   /* REMOVED 01/02/90 AS PER JUD
ADD +1 TO #UPDATE-RECD
*
END-SUBROUTINE  /* (7520)
*
END
