* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C2H10PVM
*           'VM' - VALUE ADDED MONTHLY HISTORY
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    09282004  DW1     add CREDIT CARD processing                  *
* PAM    03072005  PM2     Changed #INS-INDEX from (I1) to (I2)        *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2H30LVW
LOCAL
1 #MSG-1    (A59)
1 #MSG-2    (A76)
*
1  MONTHS
  2  #SCR-JAN      (A3) INIT <'JAN'>
  2  #SCR-FEB      (A3) INIT <'FEB'>
  2  #SCR-MAR      (A3) INIT <'MAR'>
  2  #SCR-APR      (A3) INIT <'APR'>
  2  #SCR-MAY      (A3) INIT <'MAY'>
  2  #SCR-JUN      (A3) INIT <'JUN'>
  2  #SCR-JUL      (A3) INIT <'JUL'>
  2  #SCR-AUG      (A3) INIT <'AUG'>
  2  #SCR-SEP      (A3) INIT <'SEP'>
  2  #SCR-OCT      (A3) INIT <'OCT'>
  2  #SCR-NOV      (A3) INIT <'NOV'>
  2  #SCR-DEC      (A3) INIT <'DEC'>
1 REDEFINE MONTHS
  2 #SCR-MONTHS-TABLE    (12)
    3 #SCR-MONTH          (A3)
1 #SCR-AS-OF-DATE         (A11)
1 REDEFINE #SCR-AS-OF-DATE
  2  #SCR-AS-OF-DAY       (A2)
  2  #SCR-FILL-1          (A1)
  2  #SCR-AS-OF-MONTH     (A3)
  2  #SCR-FILL-2          (A1)
  2  #SCR-AS-OF-YEAR      (A4)
*
1 #SCR-HEADER-LINE         (A44)
1 REDEFINE #SCR-HEADER-LINE
  2 #SCR-FILLER            (A1)
  2 #SCR-NUM-MEMBER        (N1)
  2 #SCR-MEMBERSHIP        (A11)
  2 #SCR-PLURAL-MEMBER     (A2)
  2 #SCR-ISSUED            (A8)
  2 #SCR-DATE              (A11)
  2 #SCR-AT-AGE            (A8)
  2 #SCN-AGE               (A2)
*
1 #SCR-CERT-NUMBER        (A14)
1 REDEFINE  #SCR-CERT-NUMBER
  2  #SCR-CERT-PREFIX     (A4)
  2  #SCR-CERT-CN         (N5)
  2  #SCR-CERT-SUFFIX     (A5)
*
1  #TEMP-NAME             (A24)
1  #SCR-RANK-NAME         (A28)
1  REDEFINE #SCR-RANK-NAME
  2 #SCR-RANK             (A3)
  2 #SCR-NAME             (A25)
1  #SCR-PREM-ANN          (A7)
1  #TEMP-PREM             (N3.2)
1  #TEMP-PREM-MON         (A8)
1  #TEMP-PREM-ANN         (A8)
1  #SCR-POLICY-NUMBER     (A16)
1  #SCR-MODE              (A28)
1  REDEFINE #SCR-MODE
  2 #SCR-MODE-PLAN        (A6)
  2 #SCR-PREM-MON         (A9)
  2 #SCR-MODE-NAME        (A13)
1  #SCN-TYPE              (A8)
1  #SCR-WORDS             (A27)
1  #SCR-CONV-L            (A20)
1  #SCR-CONV              (A10)
1  #SCR-COMMENT           (A4/13)
1  #SCR-YR-MN-YY          (A4/13)
1  #SCR-YR-MN-MM          (A3/13)
1  #SCR-GROWTH-RATE       (N5/13)
1  REDEFINE #SCR-GROWTH-RATE
  2 #SCR-GROWTH-RATE-A    (A5/13)
1  #SCR-LOAN-VALUE        (N8/13)
1 REDEFINE #SCR-LOAN-VALUE
  2 #SCR-LOAN-VALUE-A     (A8/13)
1  #SCR-CASH-VALUE        (N8/13)
1  REDEFINE #SCR-CASH-VALUE
  2 #SCR-CASH-VALUE-A     (A8/13)
1  #SCR-DEATH-BENEFIT     (N8/13)
1  REDEFINE #SCR-DEATH-BENEFIT
  2 #SCR-DEATH-BENEFIT-A  (A8/13)
1  #LOAN-VALUE   (P7.2)
1 #HOLD-DB                (N8)
1 #HOLD-LOAN-INT(N6)
1 #HOLD-LOAN-BAL          (N8)
1  #SCR-LOAN-BAL          (N8/13)
1  REDEFINE #SCR-LOAN-BAL
  2 #SCR-LOAN-BAL-A       (A8/13)
1  #SCR-LOAN-INT          (N8/13)
1  REDEFINE #SCR-LOAN-INT
2   #SCR-LOAN-INT-A       (A8/13)
1  #SCR-NET-DEATH-BENEFIT (N8/13)
1 REDEFINE #SCR-NET-DEATH-BENEFIT
2 #SCR-NET-DEATH-BENEFIT-A (A8/13)
1 #HOLD-TEMP-NET         (N8)
1  #SCR-XTR-MN            (A3)
1  #SCR-XTR-YR            (A2)
1  #SCR-XTR-COMMENT       (A10)
*
1  #INDEX                 (I2)
1  #INDEXV                (I2)
1  #CV-INDEX              (I2)
1  #INDEX-MONTH           (I2)
1  #INDEX-YEAR            (I2)
1 #LAST-NAME              (A25)
1 #FIRST/MIDDLE           (A25)
1 #SUFFIX                 (A25)
1 #DEL                    (A1)  INIT <','>
1 #INS-INDEX              (I2)                                    /* PM2
*
1 #START-KEY              (N15) INIT <000000000000000>
1 REDEFINE #START-KEY
  2 #START-KEY-ID         (N6)
  2 #START-KEY-ISSUE-DT   (N8)
  2 #START-KEY-SPLIT-ID   (N1)
*
1 #END-KEY                (N15) INIT <999999999999999>
1 REDEFINE #END-KEY
  2 #END-KEY-ID           (N6)
  2 #END-KEY-ISSUE-DT     (N8)
  2 #END-KEY-SPLIT-ID     (N1)
1 #FOUND-HIST             (L)
*
1  #HOLD-CONVERT-DATE-N  (N8)
1  REDEFINE #HOLD-CONVERT-DATE-N
  2  #HOLD-CONVERT-DATE    (A8)
  2  REDEFINE #HOLD-CONVERT-DATE
    3 #HOLD-CC            (A2)
    3 #HOLD-YY            (A2)
    3 #HOLD-MM            (A2)
    3 REDEFINE #HOLD-MM
      4 #HOLD-MM-N        (N2)
    3 #HOLD-DD            (A2)
*
1  #HOLD-YR-MN-DATE       (A6)
1  REDEFINE #HOLD-YR-MN-DATE
  2 #HOLD-YR-MN-CC        (A2)
  2 #HOLD-YR-MN-YY        (A2)
  2 REDEFINE #HOLD-YR-MN-YY
    3 #HOLD-YR-MN-YY-N    (N2)
  2 #HOLD-YR-MN-MM        (A2)
  2 REDEFINE #HOLD-YR-MN-MM
    3 #HOLD-YR-MN-MM-N    (N2)
1 #Y2K-HOLD-YR            (N4)
*
1  #HOLD-MONTH           (A3)
1  #INDEXT               (I2)
1  #I1                   (I2)
1  #INVALID              (L)
1  #RECORD-CNTR          (I2)
1  #ERR-MSG              (A60)
1  #SUP-MESSAGE          (A35)
1  #FIELD-CHANGED        (L) INIT <FALSE>
1  #HOLD-HIST-ISN        (P8)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
GET STATUS-V #CGA-ISN
*
*
***********AS OF DATE
MOVE *DATN TO #HOLD-CONVERT-DATE
MOVE #HOLD-DD TO #SCR-AS-OF-DAY
COMPRESS #HOLD-CC #HOLD-YY INTO #SCR-AS-OF-YEAR LEAVING NO SPACE
MOVE #HOLD-MM-N TO #I1
  MOVE #SCR-MONTH (#I1) TO #SCR-AS-OF-MONTH
***********NAME AND RANK
MOVE RANK TO #SCR-RANK
*
SEPARATE NAME INTO #LAST-NAME #FIRST/MIDDLE #SUFFIX
  WITH DELIMITER #DEL
IF #SUFFIX = '    '
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #SCR-NAME
ELSE
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #TEMP-NAME
  COMPRESS #TEMP-NAME ',' #SUFFIX INTO #SCR-NAME LEAVING NO SPACE
END-IF
*
MOVE ID-NUMBER TO #START-KEY-ID
MOVE ID-NUMBER TO #END-KEY-ID
MOVE 00000000 TO #START-KEY-ISSUE-DT
MOVE 0 TO #START-KEY-SPLIT-ID
MOVE 99999999 TO #END-KEY-ISSUE-DT
MOVE 9 TO #END-KEY-SPLIT-ID
RESET #FOUND-HIST
*
MOVE '* ' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-ID
MOVE 0 TO #INS-INDEX
*
****************** READ HISTORY LOOP ***********************************
READ VA-HIST WITH RECORD-KEY FROM #START-KEY
    THRU #END-KEY
  ADD 1 TO #RECORD-CNTR
  IF #RECORD-CNTR GT 1
    INPUT TEXT #SUP-MESSAGE MARK *#CGA-SCR-COMMAND USING MAP 'C2H10MVM'
  END-IF
  RESET #SUP-MESSAGE
  RESET #SCR-XTR-MN
  RESET #SCR-XTR-YR
  RESET #SCR-XTR-COMMENT
  MOVE '(' TO #SCR-FILLER
  MOVE ' MEMBERSHIP ' TO #SCR-MEMBERSHIP
  MOVE ' ISSUED ' TO #SCR-ISSUED
  MOVE ' AT AGE ' TO #SCR-AT-AGE
  RESET #FOUND-HIST
  FOR #CV-INDEX 1 TO 9
    IF STATUS (#CV-INDEX) = 'D'
      IF STATUS-V.ISSUE-DATE(#CV-INDEX) = VA-HIST.ISSUE-DATE
        MOVE TRUE TO #FOUND-HIST
        ESCAPE BOTTOM
      END-IF
    END-IF
  END-FOR
  IF #FOUND-HIST
      ASSIGN #INS-INDEX = #CV-INDEX
      PERFORM HEADER-INFO
   ELSE
    MOVE 'NO INSUR MASTER' TO #SUP-MESSAGE
    MOVE '                           ' TO #SCR-WORDS
    COMPRESS 'CN' ID-NUMBER INTO #SCR-POLICY-NUMBER LEAVING NO SPACE
    MOVE '* ' TO #CGA-SCR-COMMAND
    MOVE ' ' TO #SCR-FILLER
    MOVE '            ' TO #SCR-MEMBERSHIP
    MOVE '        ' TO #SCR-AT-AGE
    MOVE VA-HIST.IS-DT-MM TO #INDEX-MONTH
    MOVE #SCR-MONTH(#INDEX-MONTH) TO #HOLD-MONTH
    COMPRESS IS-DT-DD #HOLD-MONTH IS-DT-YYYY INTO #SCR-DATE
    PERFORM DETAIL-HISTORY
    IF #INDEX = 13
     IGNORE
    ELSE
     COMPUTE #INDEX = #INDEX + 1
     FOR #INDEXV #INDEX TO 13
      PERFORM RESET-SCRN
     END-FOR
   END-IF
  END-IF
END-READ
*
IF #RECORD-CNTR = 0
  MOVE 'NO HISTORY RECORDS' TO #SUP-MESSAGE
END-IF
*
IF INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q'
  MOVE 'C.A.P. policies show unreduced Cash value' TO #MSG-2
END-IF
*
MOVE 'VM' TO #CGA-SCR-COMMAND
MOVE ID-NUMBER TO #CGA-SCR-ID
INPUT TEXT #SUP-MESSAGE MARK *#CGA-SCR-COMMAND USING MAP 'C2H10MVM'
FETCH  'G1000PXX'
*
************** HEADER-INFO
DEFINE SUBROUTINE HEADER-INFO
IF #CGA-SCR-COMMAND NE '*'
   OR #CGA-SCR-ID NE 0 OR
   #CGA-SCR-SUFFIX NE ' '
   FETCH 'G1000PXX'
END-IF
*
CALLNAT 'GPLNMSXX' #SCR-POLICY-NUMBER ID-NUMBER #INS-INDEX
****************ISSUE DATE
MOVE ISS-MM(#INS-INDEX) TO #INDEX-MONTH
MOVE #SCR-MONTH(#INDEX-MONTH) TO #HOLD-MONTH
COMPRESS ISS-DD(#INS-INDEX) #HOLD-MONTH
  ISS-YYYY(#INS-INDEX) INTO #SCR-DATE
*
IF NUM-MBRSHIPS(#INS-INDEX) = 1
  MOVE 1 TO #SCR-NUM-MEMBER
  MOVE ') ' TO #SCR-PLURAL-MEMBER
ELSE
  MOVE NUM-MBRSHIPS(#INS-INDEX) TO #SCR-NUM-MEMBER
  MOVE 'S)' TO #SCR-PLURAL-MEMBER
END-IF
*
IF PAY-STATUS (#INS-INDEX) = '44'
  MOVE '(EXTENDED TERM INSURANCE)    ' TO #SCR-MODE
ELSE
  IF PAY-STATUS (#INS-INDEX) GT '40'
    MOVE '(PAID-UP)                    ' TO #SCR-MODE
  ELSE
    MOVE '(PAID ' TO #SCR-MODE-PLAN
    IF MODE(#INS-INDEX) = 'A' OR = '1'    /* DW1
      MOVE ' ANNUALLY)   ' TO #SCR-MODE-NAME
***************ANNUAL PREMIUM
      MOVE EDITED ANNUAL-PREM (#INS-INDEX)(EM=Z,ZZ9.99)
              TO #TEMP-PREM-ANN
      COMPRESS '$' #TEMP-PREM-ANN INTO #SCR-PREM-MON LEAVING NO SPACE
*
    ELSE
      IF MODE(#INS-INDEX) = 'S' OR = '2'     /* DW1
        MOVE ' SEMI-ANNUAL)' TO #SCR-MODE-NAME
************** SEMI-ANNUAL PREMIUM
        COMPUTE #TEMP-PREM = ANNUAL-PREM(#INS-INDEX) / 2
        MOVE EDITED #TEMP-PREM (EM=Z,ZZ9.99) TO #TEMP-PREM-MON
        COMPRESS '$' #TEMP-PREM-MON INTO #SCR-PREM-MON
          LEAVING NO SPACE
*
      ELSE
        IF MODE(#INS-INDEX) = 'Q' OR = '4'     /* DW1
          MOVE '  QUARTERLY) ' TO #SCR-MODE-NAME
*************** QUARTERLY PREMIUM
          COMPUTE #TEMP-PREM = ANNUAL-PREM(#INS-INDEX) / 4
          MOVE EDITED #TEMP-PREM (EM=Z,ZZ9.99) TO #TEMP-PREM-MON
          COMPRESS '$' #TEMP-PREM-MON INTO #SCR-PREM-MON
            LEAVING NO SPACE
*
        ELSE
          MOVE ' MONTHLY)      ' TO #SCR-MODE-NAME
***************MONTHLY PREMIUM
          COMPUTE #TEMP-PREM = ANNUAL-PREM(#INS-INDEX) / 12
          MOVE EDITED #TEMP-PREM (EM=Z,ZZ9.99) TO #TEMP-PREM-MON
          COMPRESS '$' #TEMP-PREM-MON INTO #SCR-PREM-MON
            LEAVING NO SPACE
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF CONVERSION-FLAG(#INS-INDEX) = 'N'
  MOVE 'INSURANCE CATEGORY -' TO #SCR-CONV-L
  IF  SMOKER-FLAG(#INS-INDEX) = 'S'
    MOVE 'SMOKER' TO #SCR-CONV
  ELSE
    MOVE 'NON-SMOKER' TO #SCR-CONV
  END-IF
END-IF
*
***************PLAN NAME
IF PLAN-2(#INS-INDEX) = 'OL'
  MOVE 'ORDINARY' TO #SCN-TYPE
ELSE
  IF PLAN-2(#INS-INDEX) = '20'
    MOVE '  20-PAY' TO #SCN-TYPE
  ELSE
    MOVE '  30-PAY' TO #SCN-TYPE
  END-IF
END-IF
MOVE 'LIFE WITH ANNUAL PREMIUM OF' TO #SCR-WORDS
*
***************ANNUAL PREMIUM
MOVE EDITED ANNUAL-PREM(#INS-INDEX)(EM=Z,ZZ9.99) TO #TEMP-PREM-ANN
COMPRESS '$' #TEMP-PREM-ANN INTO #SCR-PREM-ANN LEAVING NO SPACE
*****************AGE
MOVE ISSUE-AGE(#INS-INDEX) TO #SCN-AGE
*
PERFORM DETAIL-HISTORY
IF #INDEX = 13
 IGNORE
ELSE
  COMPUTE #INDEX = #INDEX + 1
  FOR #INDEXV #INDEX TO 13
   PERFORM RESET-SCRN
  END-FOR
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE RESET-SCRN
* * * * * * * * * * * * * * * *
*
RESET #SCR-GROWTH-RATE-A(#INDEXV)
RESET #SCR-LOAN-VALUE-A(#INDEXV)
RESET #SCR-CASH-VALUE-A(#INDEXV)
RESET #SCR-DEATH-BENEFIT-A(#INDEXV)
RESET #SCR-LOAN-BAL-A(#INDEXV)
RESET #SCR-LOAN-INT-A(#INDEXV)
RESET #SCR-NET-DEATH-BENEFIT-A(#INDEXV)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DETAIL-HISTORY
* * * * * * * * * * * * * * * * *
*
ASSIGN #INDEX = 0
FOR #INDEXV 1 TO 13
  IF VA-HIST.YEAR-MONTH-M(#INDEXV) = 0
    ESCAPE TOP
  ELSE
    COMPUTE #INDEX = #INDEX + 1
  END-IF
  MOVE VA-HIST.YEAR-MONTH-M(#INDEXV) TO #HOLD-YR-MN-DATE
  MOVE #SCR-MONTH (#HOLD-YR-MN-MM-N) TO #SCR-YR-MN-MM (#INDEX)
  MOVE #HOLD-YR-MN-YY-N TO #Y2K-HOLD-YR
  IF #HOLD-YR-MN-YY-N GE 50
     ADD +1900 TO #Y2K-HOLD-YR
  ELSE
    ADD +2000 TO #Y2K-HOLD-YR
  END-IF
  MOVE #Y2K-HOLD-YR TO #SCR-YR-MN-YY (#INDEX)
*
  IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE 199501
    MOVE '8.25%' TO #SCR-GROWTH-RATE-A (#INDEX)
  ELSE
    IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE 199401
      MOVE ' 8.0%' TO #SCR-GROWTH-RATE-A (#INDEX)
    ELSE
      IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE 199304
        MOVE ' 8.5%' TO #SCR-GROWTH-RATE-A (#INDEX)
      ELSE
        IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE 199204
          MOVE ' 8.9%' TO #SCR-GROWTH-RATE-A (#INDEX)
        ELSE
          IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE 199104
            MOVE ' 9.3%' TO #SCR-GROWTH-RATE-A (#INDEX)
          ELSE
            MOVE '10.1%' TO #SCR-GROWTH-RATE-A (#INDEX)
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF VA-HIST.YEAR-MONTH-M (#INDEXV) =
      VA-HIST.PREV-ANN-DATE OR =
      VA-HIST.TWO-AGO-ANN-DATE
    MOVE 'ANN.' TO #SCR-COMMENT(#INDEX)
  ELSE
    MOVE '    ' TO #SCR-COMMENT(#INDEX)
  END-IF
*
  COMPUTE ROUNDED #LOAN-VALUE = VA-HIST.CASH-VALUE-M (#INDEXV) * .75
  MOVE EDITED #LOAN-VALUE (EM=ZZZZ,ZZ9) TO #SCR-LOAN-VALUE-A (#INDEX)
* IF VA-HIST.TWO-AGO-ANN-DATE = 0
*   MOVE EDITED VA-HIST.PREV-CASH-VALUE (EM=ZZZZ,ZZ9)
*   TO #SCR-LOAN-VALUE-A (#INDEX)
* ELSE
*   IF VA-HIST.YEAR-MONTH-M (#INDEXV) GE
*        VA-HIST.PREV-ANN-DATE
*     MOVE EDITED VA-HIST.PREV-CASH-VALUE(EM=ZZZZ,ZZ9)
*        TO #SCR-LOAN-VALUE-A (#INDEX)
*   ELSE
*     MOVE EDITED VA-HIST.TWO-AGO-CASH-VALUE(EM=ZZZZ,ZZ9)
*        TO #SCR-LOAN-VALUE-A (#INDEX)
*   END-IF
* END-IF
*
  MOVE EDITED VA-HIST.CASH-VALUE-M(#INDEXV)(EM=ZZZZ,ZZ9) TO
      #SCR-CASH-VALUE-A (#INDEX)
  MOVE EDITED VA-HIST.DEATH-BENEFIT-M(#INDEXV)(EM=ZZZZ,Z99)
     TO #SCR-DEATH-BENEFIT-A (#INDEX)
*
  IF   (ISSUE-DATE (#INS-INDEX) GE 19970101) /* in nxt pols these
   AND (MONTHLY-ALLOC-AMT NE 999.99)  /* flds are not loan & int
    COMPRESS 'Loan Info not given if issued on or after'
        '1/1/1997' INTO #MSG-1
    RESET #SCR-LOAN-BAL-A (#INDEX)
    RESET #SCR-LOAN-INT-A (#INDEX)
    RESET #SCR-NET-DEATH-BENEFIT-A (#INDEX)
  ELSE
    MOVE EDITED VA-HIST.PRORAT-LOAN-BAL-M(#INDEXV) (EM=ZZZZ,ZZ9)
       TO #SCR-LOAN-BAL-A (#INDEX)
    MOVE EDITED VA-HIST.PRORAT-LN-ACC-INT-M(#INDEXV) (EM=ZZZZ,ZZ9)
       TO #SCR-LOAN-INT-A (#INDEX)
    MOVE VA-HIST.DEATH-BENEFIT-M(#INDEXV) TO #HOLD-DB
    MOVE VA-HIST.PRORAT-LOAN-BAL-M(#INDEXV) TO #HOLD-LOAN-BAL
    MOVE VA-HIST.PRORAT-LN-ACC-INT-M(#INDEXV) TO #HOLD-LOAN-INT
    COMPUTE #HOLD-TEMP-NET = #HOLD-DB - #HOLD-LOAN-BAL - #HOLD-LOAN-INT
    MOVE EDITED #HOLD-TEMP-NET(EM=ZZZZ,ZZ9) TO
        #SCR-NET-DEATH-BENEFIT-A(#INDEX)
  END-IF
*
  IF VA-HIST.TERMINATION-DATE = 0
    IF VA-HIST.ETI-DATE = 0
      ESCAPE TOP
    ELSE
      MOVE VA-HIST.ETI-DATE TO #HOLD-YR-MN-DATE
      MOVE #SCR-MONTH (#HOLD-YR-MN-MM-N) TO #SCR-YR-MN-MM(#INDEX)
      MOVE #HOLD-YR-MN-YY-N TO #Y2K-HOLD-YR
      IF #HOLD-YR-MN-YY-N GE 50
         ADD +1900 TO #Y2K-HOLD-YR
      ELSE
         ADD +2000 TO #Y2K-HOLD-YR
      END-IF
      MOVE #Y2K-HOLD-YR TO #SCR-YR-MN-YY (#INDEX)
      MOVE 'ETI' TO #SCR-COMMENT (#INDEX)
      MOVE VA-HIST.ETI-LOAN-VALUE TO #SCR-LOAN-VALUE(#INDEX)
      MOVE VA-HIST.ETI-CASH-VALUE TO #SCR-CASH-VALUE(#INDEX)
     MOVE VA-HIST.ETI-DEATH-BENE TO #SCR-DEATH-BENEFIT(#INDEX)
    END-IF
  ELSE
    MOVE VA-HIST.TERMINATION-DATE TO #HOLD-YR-MN-DATE
    MOVE #SCR-MONTH(#HOLD-YR-MN-MM-N) TO #SCR-XTR-MN
    MOVE #HOLD-YR-MN-YY-N TO #SCR-XTR-YR
    IF  VA-HIST.TERMINATION-CODE = 'DTH'
      MOVE 'DEATH' TO #SCR-XTR-COMMENT
    ELSE
      IF VA-HIST.TERMINATION-CODE = 'CSV'
        MOVE 'CASH VALUE' TO #SCR-XTR-COMMENT
      ELSE
        IF VA-HIST.TERMINATION-CODE = 'LAP'
          MOVE 'LAPSE' TO #SCR-XTR-COMMENT
        ELSE
        IF VA-HIST.TERMINATION-CODE = 'EXP'
          MOVE 'EXPIRATION' TO #SCR-XTR-COMMENT
          ELSE
            MOVE 'ORDINARY' TO #SCR-XTR-COMMENT
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-FOR                  /* <<<<<< HISTORY DETAIL LOOP 13 TIMES ?
*
END-SUBROUTINE
*
********************************** ORIGINAL COMMAND? *******************
IF #CGA-SCR-COMMAND NE #CGA-ORIG-COMMAND OR
    #CGA-SCR-ID NE #CGA-ORIG-ID
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
*
END
