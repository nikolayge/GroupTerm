* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GOPTOSXX  calc option-out of sharing info calculater
*     callnat from athp05p0 and c2v12cvp (c2v10pvp)
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  10282005 DW1  Reinstate NXT Dues calculation and CHG Formula
* DRW  11102005 DW2  Add G/F SVC fee Calculation to OPT-OUT CALC
* DRW  11142005 DW3  ADD Process Dt to Linkage & calc no adj aftr 10/05
* DRW  12122005 DW4  ADD Initial DE-GF date processing
* DRW 05/28/2009 DW5 Add OO FEE entries for 2006 through 2009 
* VXt 01/11/2012 vt1 Add new fee amounts.
* VXT 01/21/2013 vt2 Add new fee amounts.
* vxt 01/23/2014 vt3 Add new fee amounts /Ticket 2520/
* vxt 01/14/2015 vt4 Add new fee amounts /Ticket 1691 - effective rates for year 2015/
* vxt 12/23/2016 vt5 New fee amounts for 2017
* vxt 12/28/2017 vt6 Ticket 6975 - new rates for 2018
*******************************************
*
DEFINE DATA PARAMETER
1 #CN              (N6)
1 #PROCESS-DATE    (N6)    /* DW3
1 #STATUS          (A1/9)
1 #PAY-STATUS      (A2/9)
1 #ISSUE-DATE      (N8/9)
1 #MONTH-ALLOC-AMT (P7.2/9)           /*TT1
1 #FACE-AMOUNT     (P7.2/9)
1 #NUM-MBRSHIPS    (P1.0/9)
1 #EXPIRY-DAY      (P3.0/9)
*
1 #COI-ADJ         (N3.2/9)
1 #NXT-X           (I2)    /* index of nxt pol to adj
1 #NXT-PRM-ADJ     (P1.2)
1 #OO-EFF-DATE     (N8)
1 #DEGF-EFF-DATE   (N8)   /* DW4
*
LOCAL
*
1 #NXT-FEE    (N1.2) INIT <2.00>  /* Not Used as of 10/20/2005 /* DW1
*
1 #AFSC-FEE-TBL
 2 #FEE-INFO-1  (A12)  INIT <'20000700-255'>
 2 #FEE-INFO-2  (A12)  INIT <'20010701-264'>
 2 #FEE-INFO-3  (A12)  INIT <'20020701-270'>
 2 #FEE-INFO-4  (A12)  INIT <'20030701-274'>
 2 #FEE-INFO-5  (A12)  INIT <'20040701-274'>  /* Missed on 7/1/2004
 2 #FEE-INFO-6  (A12)  INIT <'20050701-288'>
 2 #FEE-INFO-7  (A12)  INIT <'20060701-300'>  /* Not entered till 5/28/2009 DW5
 2 #FEE-INFO-8  (A12)  INIT <'20070701-310'>  /* Not entered till 5/28/2009 DW5
 2 #FEE-INFO-9  (A12)  INIT <'20080701-317'>  /* Not entered till 5/28/2009 DW5
 2 #FEE-INFO-10 (A12)  INIT <'20090701-335'>  /* Not entered till 5/28/2009 DW5 
 2 #fee-info-11 (a12)  INIT <'20120101-347'>  /* vt1 
 2 #fee-info-12 (a12)  INIT <'20130101-353'>  /* vt2
 2 #fee-info-13 (a12)  INIT <'20140101-358'>  /* vt3 
 2 #fee-info-14 (a12)  INIT <'20150101-364'>  /* vt4
 2 #fee-info-15 (a12)  INIT <'20160101-364'>  /* vt4
 2 #fee-info-16 (a12)  INIT <'20170101-366'>  /* vt5
 2 #fee-info-17 (a12)  INIT <'20180101-374'>  /* vt6
1 REDEFINE #AFSC-FEE-TBL
 2 #AFSC-OCC   (17)  /* keep same as below                 /* DW5  /* vt1 was 10  /* vt2 was 11 /* vt3 was 12 /* vt4 was 13 /* vt5 was 15 /* vt6 was 16
  3 #FEE-DATE (N8)
  3 #FEE-FILL (A1)
  3 #FEE      (N1.2)
1 #NUM-ENTRIES  (N3) INIT <17>  /* keep same as above      /* DW5    /* vt1 was 10  /* vt2 was 11 /* vt3 was 12  /* vt4 was 13  /* vt5 was 15 /* vt6 was 16
*
1 #G-F-FEE-TBL                                      /* DW2
 2 #G-F-FEE-INFO-1  (A12)  INIT <'20030701-320'>
 2 #G-G-FEE-INFO-2  (A12)  INIT <'20040701-325'>
 2 #G-F-FEE-INFO-3  (A12)  INIT <'20050701-334'>
 2 #G-F-FEE-INFO-4  (A12)  INIT <'20060701-348'> /*Not entered till 5/28/2009 DW5
 2 #G-F-FEE-INFO-5  (A12)  INIT <'20070701-360'> /*Not entered till 5/28/2009 DW5
 2 #G-F-FEE-INFO-6  (A12)  INIT <'20080701-368'> /*Not entered till 5/28/2009 DW5
 2 #G-F-FEE-INFO-7  (A12)  INIT <'20090701-389'> /*Not entered till 5/28/2009 DW5  
 2 #g-f-fee-info-8  (a12)  INIT <'20120101-403'>           /* vt1 
 2 #g-f-fee-info-9  (a12)  INIT <'20130101-410'>           /* vt2 
 2 #g-f-fee-info-10 (a12)  INIT <'20140101-416'>           /* vt3  
 2 #g-f-fee-info-11 (a12)  INIT <'20150101-423'>           /* vt4
 2 #g-f-fee-info-12 (a12)  INIT <'20160101-423'>           /* vt4
 2 #g-f-fee-info-13 (a12)  INIT <'20170101-426'>           /* vt5
 2 #g-f-fee-info-14 (a12)  INIT <'20180101-435'>           /* vt6
1 REDEFINE #G-F-FEE-TBL
 2 #G-F-OCC   (14)  /* keep same as below        /* DW7    /* vt1 was 7   /*vt2 was 8   /* vt3 was 9 /* vt4 was 10  /* vt5 was 12 /* vt6 was 13
  3 #G-F-FEE-DATE (N8)
  3 #G-F-FEE-FILL (A1)
  3 #G-F-FEE      (N1.2)
1 #G-F-NUM-ENTRIES  (N3) INIT <14>  /* keep same as above   /* DW7  /* vt1 was 7  /* vt2 was 8 /* vt3 was 9  /* vt4 was 10  /* vt5 was 12 /* vt6 was 13
*
1 #AFSC-FEE    (N1.2)
1 #I1          (I2)
1 #UNITS       (N3/9)
1 #TOT-UNITS   (N3)
1 #FST-POL-X   (I2)
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT   (A60/60)
 2 REDEFINE NOTE-TEXT   /* Opt-Out Message
  3 OO-NOTE-TEXT-OCC (60)
   4 OO-TEXT-FST  (A34) /*'*#OPTED-OUT OF LIVING SERVICES ON 05/23/2005'
   4 OO-DATE-MM   (A2)
   4 OO-DATE-F1   (A1)
   4 OO-DATE-DD   (A2)
   4 OO-DATE-F2   (A1)
   4 OO-DATE-YYYY (A4)
   4 OO-TEXT-LST  (A16)
 2 REDEFINE NOTE-TEXT        /* DE/GF Message
  3 DEGF-NOTE-TEXT-OCC (60)
   4 DEGF-TEXT-FST    (A11)  /* '*#DE-GF ON 03/18/2004'
   4 DEGF-DATE-MM   (A2)
   4 DEGF-DATE-F1   (A1)
   4 DEGF-DATE-DD   (A2)
   4 DEGF-DATE-F2   (A1)
   4 DEGF-DATE-YYYY (A4)
   4 DEGF-TEXT-LST  (A39)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY     (N4)
 2 #MM       (N2)
 2 #DD       (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A (A8)
*
END-DEFINE
*
* WRITE 'OO-PROCESS' #CN 'iss-dt"s' #ISSUE-DATE (1:9)
RESET #COI-ADJ (*) #NXT-X #NXT-PRM-ADJ #OO-EFF-DATE  #DEGF-EFF-DATE
*
FIND (1) NT-V WITH ID-NUMBER = #CN
  FOR #I1 1 TO 60
    IF NOTE-TEXT (#I1) = MASK ('*#OPTED-OUT OF LIVING SERVICES ON')
      COMPRESS OO-DATE-YYYY (#I1) OO-DATE-MM (#I1) OO-DATE-DD (#I1)
             INTO #YYYYMMDD-A LEAVING NO
      MOVE #YYYYMMDD TO #OO-EFF-DATE
    END-IF
*
    IF NOTE-TEXT (#I1) = MASK ('*#DE-GF ON ')
      COMPRESS DEGF-DATE-YYYY(#I1) DEGF-DATE-MM(#I1) DEGF-DATE-DD(#I1)
             INTO #YYYYMMDD-A LEAVING NO
      MOVE #YYYYMMDD TO #DEGF-EFF-DATE
    END-IF
  END-FOR
END-FIND
*
FOR #I1 1 TO 9
  IF #STATUS (#I1) = 'D'
    MOVE #I1 TO #FST-POL-X
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #FST-POL-X GT 9
  MOVE 1 TO #FST-POL-X
END-IF
*
FOR #I1 1 TO 9
  IF   (#ISSUE-DATE      (#I1) GE 19970101)
   AND (#MONTH-ALLOC-AMT (#I1) NE 999.99)
*         /* below removed 4/2003 since the $2.00 dues
*   MOVE #FST-POL-X TO #NXT-X   /* is no longer part of his monthly
*   MOVE #NXT-FEE TO #NXT-PRM-ADJ /* amt due (no acv adj is neccessary)
*   reinstated 10/28/2005 to give adj to OPT OUT NXT policies as per TEH
*             started using the ($2.88 - $2.00) amt instead of $2.00
*    THIS ADJUSTMENT WILL ONLY BE USED IF THE 1ST POLICY IS A W/L POLICY
*
   AND (#FST-POL-X = #I1)   /* Adjust only to 1st W/L policy
    PERFORM GET-CURRENT-AFSC-FEE
*
    MOVE #FST-POL-X TO #NXT-X                         /* DW1
    RESET #NXT-PRM-ADJ      /* No longer Done
    COMPUTE #COI-ADJ (#FST-POL-X) = #AFSC-FEE - #NXT-FEE       /* DW1
*
    IF #PROCESS-DATE GE 200511  /* starting on 11/2005 M/E no OO adj DW3
      RESET #COI-ADJ (#FST-POL-X)
    END-IF
*
    ESCAPE BOTTOM
  END-IF
*
  IF  (#STATUS (#I1) NE 'D')
  OR  ((#ISSUE-DATE      (#I1) GE 19970101)
   AND (#MONTH-ALLOC-AMT (#I1) NE 999.99))
   ESCAPE TOP
  END-IF
*
  IF #PAY-STATUS (#I1) = '44' OR = '45'  /* rpu /eti
    IF #ISSUE-DATE (#I1) LT 19941201
      MOVE #NUM-MBRSHIPS (#I1) TO #UNITS (#I1)
    ELSE
     MOVE #EXPIRY-DAY (#I1) TO #UNITS (#I1)
    END-IF
  ELSE
    COMPUTE #UNITS (#I1) = #FACE-AMOUNT (#I1) / 5000
  END-IF
*
  IF #UNITS (#I1) LE 0
    MOVE 1 TO #UNITS (#I1)
  END-IF
*
 ADD #UNITS (#I1) TO #TOT-UNITS
END-FOR
*
IF #NXT-X = 0   /* must have old pols
  PERFORM GET-CURRENT-AFSC-FEE                  /* DW1
*
  FOR #I1 1 TO 9
    IF #UNITS (#I1) GT 0
      COMPUTE ROUNDED #COI-ADJ (#I1) = #AFSC-FEE *
                               (#UNITS (#I1) / #TOT-UNITS)
    END-IF
  END-FOR
END-IF
* WRITE 'OO-PROCESS' #CN 'old-new fee' #AFSC-FEE
*
* WRITE #CN 'old/new coi adj' #COI-ADJ (*) 'nxt=' #NXT-X #NXT-PRM-ADJ
* WRITE 'month-alloc-amt =' #MONTH-ALLOC-AMT (*)
*
IF #PROCESS-DATE GE 200511  /* starting on 11/2005 M/E no OO adj DW3
  RESET #COI-ADJ (*)
END-IF
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GET-CURRENT-AFSC-FEE    /* DW1
* * * * * * * * * * * * * * * * * * * *
*
MOVE #OO-EFF-DATE TO #YYYYMMDD    /* DW1
IF #YYYYMMDD = 0
  MOVE *DATN TO #YYYYMMDD
END-IF
*
FOR #I1 #NUM-ENTRIES TO 1 STEP -1    /* DW1
 IF  (#YYYYMMDD GE #FEE-DATE (#I1))
  OR (#I1 = 1)
  MOVE #FEE (#I1) TO #AFSC-FEE
   ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
