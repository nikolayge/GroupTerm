* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************
* GPBPCPEM    Boris Zaslavsky  31/05/2015 (Rewriten to use GPRS0EML) 
* Send email to users if CAP loans not distributed      
********************************************************
*
DEFINE DATA
 LOCAL USING GPRL0EML
LOCAL
1 v_PARM          (A78)
1 v_A             (A)  DYNAMIC 
1 v_In            (A6) 
1 #i              (I2)
1 #j              (I2)
1 #k              (I2)
*
END-DEFINE
************************************************************************
*
ON ERROR
  PRINT *PROGRAM 'Line:' *ERROR-LINE 'Err:' *ERROR-NR  /
  'Input:' #i 
END-ERROR

IF *DEVICE <> 'BATCH'
   DEFINE WORK FILE 1 "/usr/SAG/AAFMAA/batch/workfiles/PERMDA/ATH/LOANS_SKIPPED.TXT"
   p_To  := "bzaslavskiy@aafmaa.com"
ELSE   
 REPEAT WHILE *DATA <> 0
     INPUT v_PARM
     DECIDE FOR FIRST CONDITION
       WHEN v_PARM = ' ' OR v_PARM = MASK('#')
             IGNORE
       WHEN v_PARM = SCAN('@')
           v_A := *TRIM(v_PARM)
           IF #i = 0
             p_To   := v_A
           ELSE  
             COMPRESS p_To ',' v_A INTO p_To
           END-IF
           #i := #i + 1  
       WHEN NONE IGNORE
     END-DECIDE  
 END-REPEAT
END-IF
PRINT *DATX 'CAP loans skipped' 'Email To:' p_To
p_Subj := 'CAP loans not sent to AFB - Invalid Bridger'
p_Msg(1) := 'The following members had CAP loans skipped in IAS due to invalid Bridger verifications.'

#i := 0 #j := 0 #k := 3 
READ WORK 1 v_In
  IF v_In = 'HEADER'  ESCAPE TOP END-IF
  #i := #i + 1
  #j := #j + 1
  IF #j = 9 #j := 1  #k := #k + 1 END-IF
  COMPRESS  p_Msg(#k)  v_In INTO p_Msg(#k)
END-WORK
*
IF #i >  0
  CALLNAT 'GPRS0EML' p_EMAIL 
  IF p_rc > 0 
     PRINT 'Send Email returns Error Code:' p_rc 
  END-IF
ELSE
  PRINT 'No skipped CAP loans.  Email not sent'
END-IF
*
END
