* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: GPF12SXX
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* NGG   20100225  None    Program written.                             *
* NGG   20101214  NG1     Role decoration function                     *
* RSE   20111031  RE1     Change function for role description         *
* bz    2016-05-03 bz-4404
* 
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID   (N8)
LOCAL
1 BLANK     (A1) CONST <' '>
1 #DEAD     (A4) CONST <'Dcsd'>
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 CONTACT-TYPE                             /* bz-4404
  2 FIRM-ORGANIZATION-NAME                   /* bz-4404
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-A (A8)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A (A8)
  2 SSN
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
1 CountNotes VIEW OF A-CONTACT-NOTES

1 #PARENT-COID   (N8)
1 J              (I4)
1 #PAGE          (N2)
1 #TOT-PAGES     (N2)
1 #TOT-REL       (N3)
1 #L             (N7)
1 #LINE          (N3/15)
1 #RELATIONS
  2 #RID            (N8/150)
  2 #ROLE-DESC      (A12/150)
  2 #RNAME          (A25/150)
  2 #RDOB           (A10/150)
  2 #RDOD           (A4/150)
  2 #RSSN           (A9/150)

1 #CONTACT-NAME  (A25)
1 #D             (D)
1 #SSN           (N9)
1 #DOB           (A10)
1 #DOD           (A4)
1 #ID-NUMBER     (A14)

1 FIRS VIEW OF A-FIRS
  2 C*DEPENDENT-TABLE
  2 DEPN-CONTACT-ID (99)
  2 DEPN-STATUS-CD  (99)

END-DEFINE

SET KEY ALL
SET KEY PF3  NAMED 'Exit'
SET KEY PF4  NAMED 'Cnt'
SET KEY PF7  NAMED 'PgUp'
SET KEY PF8  NAMED 'PgDn'
SET KEY PF12 NAMED 'Reln'

IF #CONTACT-ID <= 0
  ESCAPE ROUTINE
END-IF

FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
  ASSIGN #PARENT-COID = CONTACT.CONTACT-ID
  #CONTACT-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  #SSN := CONTACT.SSN
  IF DATE-OF-BIRTH > 0
    MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
    MOVE EDITED #D (EM=MM/DD/YYYY) TO #DOB
  ELSE
    RESET #DOB
  END-IF
  IF DATE-OF-DEATH > 0
    #DOD := #DEAD
  ELSE
    RESET #DOD
  END-IF
  IF CONTACT.ID-NUMBER > 0
    COMPRESS '( CN' CONTACT.ID-NUMBER ')' INTO #ID-NUMBER
  ELSE
    RESET #ID-NUMBER
    IF #DOD = BLANK
      FIND FIRS WITH DEPN-CONTACT-ID = #CONTACT-ID
        FOR #L 1 TO C*DEPENDENT-TABLE
          IF DEPN-CONTACT-ID (#L) = #CONTACT-ID
              AND FIRS.DEPN-STATUS-CD (#L) = 'D'
            #DOD := #DEAD
          END-IF
        END-FOR
      END-FIND
    END-IF
  END-IF
END-FIND

RESET J #RELATIONS
FIND CONTACT-REL WITH CONTACT-1-ID = #PARENT-COID
  ADD 1 TO J
  ASSIGN #RID (J) = CONTACT-2-ID
*  #ROLE-DESC (J) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
  IF ROLE-2-CD = 'Y'
    MOVE 'Fmr Wf/Husband' TO #ROLE-DESC (J)
  ELSE
    #ROLE-DESC (J) := FUNCTION-CODE-1-DESC(<'041',ROLE-2-CD>)
  END-IF
  FIND CONTACT WITH CONTACT-ID = CONTACT-2-ID
    IF CONTACT-TYPE = 'O'                                                                /* bz-4404
       #RNAME (J) := FIRM-ORGANIZATION-NAME                                              /* bz-4404
    ELSE                                                                                 /* bz-4404
       #RNAME (J) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)  
    END-IF                                                                               /* bz-4404   
    IF DATE-OF-BIRTH > 0
      MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
      MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB (J)
    ELSE
      RESET #RDOB (J)
    END-IF
    IF DATE-OF-DEATH > 0
      #RDOD (J) := #DEAD
    ELSE
      IF CONTACT.ID-NUMBER > 0
        RESET #RDOD (J)
      ELSE
        FIND FIRS WITH DEPN-CONTACT-ID = CONTACT-2-ID
          FOR #L 1 TO C*DEPENDENT-TABLE
            IF FIRS.DEPN-CONTACT-ID (#L) = CONTACT-2-ID
                AND FIRS.DEPN-STATUS-CD (#L) = 'D'
              #DOD := #DEAD
            END-IF
          END-FOR
        END-FIND
      END-IF
    END-IF
    IF SSN > 0
      MOVE EDITED SSN (EM=9(9)) TO #RSSN (J)
    END-IF
    COMPRESS FUNCTION-ROLE-DECORATION (<#RID (J)>)           /* NG1
      #ROLE-DESC (J) INTO #ROLE-DESC (J) LEAVING NO
      IF DATE-OF-DEATH <> 0                                           /* bz-4404
            COMPRESS '!' #ROLE-DESC(J) INTO #ROLE-DESC(J) LEAVING NO  /* bz-4404
      END-IF                                                          /* bz-4404
  END-FIND
END-FIND

ADD 1 TO J
ASSIGN #RNAME (J) := '--- Other Relations -----'
FIND CONTACT-REL WITH CONTACT-2-ID = #PARENT-COID
  ADD 1 TO J
  ASSIGN #RID (J) = CONTACT-1-ID
  IF ROLE-1-CD = 'M'
    ASSIGN ROLE-1-CD = ROLE-2-CD
  END-IF
  IF ROLE-2-CD = 'G'
    #ROLE-DESC  (J) := 'GRNDPARENT'
  ELSE
*    #ROLE-DESC  (J) := FUNCTION-DISP-RL-DESC(<ROLE-1-CD>)
    #ROLE-DESC  (J) := FUNCTION-CODE-1-DESC(<'041',ROLE-1-CD>)           /* RE1
  END-IF
  FIND CONTACT WITH CONTACT-ID = CONTACT-1-ID
    IF CONTACT-TYPE = 'O'                                                                /* bz-4404
       #RNAME (J) := FIRM-ORGANIZATION-NAME                                              /* bz-4404
    ELSE                                                                                 /* bz-4404
       #RNAME (J) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    END-IF  

    IF DATE-OF-BIRTH > 0
      MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
      MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB (J)
    ELSE
      RESET #RDOB (J)
    END-IF
    IF DATE-OF-DEATH > 0
      #RDOD (J) := #DEAD
    ELSE
      IF CONTACT.ID-NUMBER > 0
        RESET #RDOD (J)
      ELSE
        FIND FIRS WITH DEPN-CONTACT-ID = CONTACT-1-ID
          FOR #L 1 TO C*DEPENDENT-TABLE
            IF FIRS.DEPN-CONTACT-ID (#L) = CONTACT-1-ID
                AND FIRS.DEPN-STATUS-CD (#L) = 'D'
              #DOD := #DEAD
            END-IF
          END-FOR
        END-FIND
      END-IF
    END-IF
    IF SSN > 0
      MOVE EDITED SSN (EM=9(9)) TO #RSSN (J)
    END-IF
    COMPRESS FUNCTION-ROLE-DECORATION (<#RID (J)>)       /* NG1
      #ROLE-DESC (J) INTO #ROLE-DESC (J) LEAVING NO
      IF DATE-OF-DEATH <> 0                                           /* bz-4404
            COMPRESS '!' #ROLE-DESC(J) INTO #ROLE-DESC(J) LEAVING NO  /* bz-4404
      END-IF                                                          /* bz-4404

      
  END-FIND
END-FIND

#TOT-REL := J - 1
DIVIDE 15 INTO #TOT-REL GIVING #TOT-PAGES REMAINDER #PAGE
IF #PAGE > 0
  ADD 1 TO #TOT-PAGES
END-IF
#PAGE := 1
#L := 1
REPEAT
  RESET #LINE (*)
  if #RID (1) > 0
    #LINE (1) := #L
  end-if
  FOR J 1 TO 14
    if #RID (#L + J) > 0
      #LINE ( J + 1) := #LINE (J) + 1
    end-if
  END-FOR
  INPUT USING MAP 'GPF12MXX'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF7'
      IF #PAGE > 1
        SUBTRACT 1 FROM #PAGE
        SUBTRACT 15 FROM #L
      END-IF
    VALUE 'PF8'
      IF #TOT-REL >= #L + 15
        ADD 1 TO #PAGE
        ADD 15 TO #L
      END-IF
    VALUE 'PF12','PF4'
      PERFORM CHECK-CURS-LINE
    NONE VALUE IGNORE
  END-DECIDE
  UNTIL *PF-KEY = 'PF3'
*   OR *PF-KEY = 'ENTR'
END-REPEAT

DEFINE CHECK-CURS-LINE
/*     ===============
IF *CURS-LINE > 4 AND *CURS-LINE < 20
  J := *CURS-LINE - 4
END-IF
IF #RID (J) > 0
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #RID (J)
  ELSE
    CALLNAT 'GDIF4XXX' #CONTACT-ID #RID (J)
  END-IF
END-IF
END-SUBROUTINE

END
