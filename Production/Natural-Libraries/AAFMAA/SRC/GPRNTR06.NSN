* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************
* SUBPROGRAM-ID: GPRNTR06 - TRANSACTION LOG GENERIC MODULE
* Includes Credit Cards. ECheck and Payment Request transactions
***************************************************************
*        MODIFICATION LOG
***************************************************************
* USER   DATE      TAG  REASON
* YAK    01152011       Initial Creation
*                       (Enhanced Billing and Payment project)
* YAK    09102016  YK1  EB&P Phase III
***************************************************************
*
DEFINE DATA
PARAMETER
1 #FUNCTION         (A) DYNAMIC
PARAMETER USING GPRATR06
LOCAL USING GPRA0004               /* PDA to pass data to TR log subprogram
LOCAL USING GPRL0004
LOCAL
1 #LOG-CC-INFO-TRAN        (A) DYNAMIC CONST <'CreditCardInfo'>
1 #LOG-ECHECK-INFO-TRAN    (A) DYNAMIC CONST <'ECheckInfo'>
1 #LOG-EZPAY-INFO-TRAN     (A) DYNAMIC CONST <'EZPayInfo'>
1 #LOG-PAYMENT-REQUEST-TRAN(A) DYNAMIC CONST <'PaymentRequest'>
1 #DISCLAIMER-TRAN         (A) DYNAMIC CONST <'Disclaimer'>
1 #CA                      (A) DYNAMIC
1 #LOOK-UP-KEY      (A7)
1 #LOOK-UP-VALUE    (A1)
1 #LOOK-UP-REQ-STATUS (A7) CONST <'CODE067'>
1 TR_DESCRIPTION    (A) DYNAMIC
1 #I  (I2)
1 #II (I2)
END-DEFINE
*
DECIDE ON FIRST VALUE #FUNCTION
  VALUE #LOG-CC-INFO-TRAN
    TR_DESCRIPTION := #CA
    PERFORM LOG-CC-INFO-TRAN
  VALUE #LOG-ECHECK-INFO-TRAN
    TR_DESCRIPTION := #CA
    PERFORM LOG-ECHECK-INFO-TRAN
  VALUE #LOG-EZPAY-INFO-TRAN
    TR_DESCRIPTION := #CA
    PERFORM LOG-EZPAY-INFO-TRAN
  VALUE #LOG-PAYMENT-REQUEST-TRAN
    PERFORM LOG-PAYMENT-REQUEST-TRAN
  VALUE #DISCLAIMER-TRAN
    RESET TR_DESCRIPTION
    PERFORM LOG-DISCLAIMER-TRAN    
  NONE VALUE
    IGNORE
END-DECIDE
*
PERFORM WRITE-TRAN-LOG
*
********************************
DEFINE SUBROUTINE WRITE-TRAN-LOG
********************************
IF #I NE 0
  #TR-NAME               := TR_NAME
  #TR-ID-NUMBER          := TR_ID_NUMBER
  #LOG-DESCRIPTION       := TR_DESCRIPTION
  #TR-CLERK-ID           := TR_CLERK
  #TR-DATE-LAST-UPDATE   := BEFORE_IMAGE.DATE-LAST-UPDATE
  #NEW-DATA-TAG(1)       := TR_DESCRIPTION
  CALLNAT 'GPRN0004'
    #TR-FIELDS #SYNC-FIELD-TABLE(*)
    #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
END-IF
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE LOG-CC-INFO-TRAN
**********************************
*
* CC-NUMBER: EXP-DATE: 00 2000
* CC-NUMBER: 426690******7096 EXP-DATE: 02 2014
* ADDRESS CONFIRMATION: N
* ADDRESS CONFIRMATION: Y
* NAME: ALDERMAN, JENNIFER
* NAME: ALDERMAN, KENNETH D.
* ADDRESS: 535 MANSFIELD LN COLUMBIA SC 29203 9098
* ADDRESS: 535 MANSFIELD LN COLUMBIA SC 29203 9098
*
IF AFTER_IMAGE.CC-NUMBER <> BEFORE_IMAGE.CC-NUMBER OR AFTER_IMAGE.CC-GUID <> BEFORE_IMAGE.CC-GUID OR
    AFTER_IMAGE.CC-EXPIRATION-DATE <> BEFORE_IMAGE.CC-EXPIRATION-DATE OR
    AFTER_IMAGE.CC-DEFAULT-IND <> BEFORE_IMAGE.CC-DEFAULT-IND                 /* YK1
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'CC-NUMBER:' BEFORE_IMAGE.CC-NUMBER 'EXP-DATE:' BEFORE_IMAGE.CC-EXPIRATION-DATE
    INTO #OLD-DATA(#I)
  COMPRESS 'CC-NUMBER:' AFTER_IMAGE.CC-NUMBER 'EXP-DATE:' AFTER_IMAGE.CC-EXPIRATION-DATE
    INTO #NEW-DATA(#I)
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'CC-GUID:' BEFORE_IMAGE.CC-GUID 'DEF-IND:' BEFORE_IMAGE.CC-DEFAULT-IND        /* YK1
    INTO #OLD-DATA(#I)
  COMPRESS 'CC-GUID:' AFTER_IMAGE.CC-GUID 'DEF-IND:' AFTER_IMAGE.CC-DEFAULT-IND          /* YK1
    INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.CCHOLDER-NAME <> BEFORE_IMAGE.CCHOLDER-NAME
* YK1 >
  IF #I = 0
    #I := #I + 1
    #FIELD-DESCRIPTION(#I) := #CC-INFO
    COMPRESS 'CC-NUMBER:' BEFORE_IMAGE.CC-NUMBER 'EXP-DATE:' BEFORE_IMAGE.CC-EXPIRATION-DATE
      INTO #OLD-DATA(#I)
    COMPRESS 'CC-NUMBER:' AFTER_IMAGE.CC-NUMBER 'EXP-DATE:' AFTER_IMAGE.CC-EXPIRATION-DATE
      INTO #NEW-DATA(#I)
  END-IF
* < YK1
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'NAME:' BEFORE_IMAGE.CCHOLDER-NAME INTO #OLD-DATA(#I)
  COMPRESS 'NAME:' AFTER_IMAGE.CCHOLDER-NAME  INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.CCHOLDER-ADDRESS-1 <> BEFORE_IMAGE.CCHOLDER-ADDRESS-1 OR
    AFTER_IMAGE.CCHOLDER-ADDRESS-2 <> BEFORE_IMAGE.CCHOLDER-ADDRESS-2 OR
    AFTER_IMAGE.CCHOLDER-ADDRESS-3 <> BEFORE_IMAGE.CCHOLDER-ADDRESS-3
* YK1 >
  IF #I = 0
    #I := #I + 1
    #FIELD-DESCRIPTION(#I) := #CC-INFO
    COMPRESS 'CC-NUMBER:' BEFORE_IMAGE.CC-NUMBER 'EXP-DATE:' BEFORE_IMAGE.CC-EXPIRATION-DATE
      INTO #OLD-DATA(#I)
    COMPRESS 'CC-NUMBER:' AFTER_IMAGE.CC-NUMBER 'EXP-DATE:' AFTER_IMAGE.CC-EXPIRATION-DATE
      INTO #NEW-DATA(#I)
  END-IF
* < YK1
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'ADDRESS:' BEFORE_IMAGE.CCHOLDER-ADDRESS-1 BEFORE_IMAGE.CCHOLDER-ADDRESS-2
    BEFORE_IMAGE.CCHOLDER-ADDRESS-3 INTO #OLD-DATA(#I)
  COMPRESS 'ADDRESS:' AFTER_IMAGE.CCHOLDER-ADDRESS-1 AFTER_IMAGE.CCHOLDER-ADDRESS-2
    AFTER_IMAGE.CCHOLDER-ADDRESS-3 INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.CC-ADDR-VALIDATION-CODE <> BEFORE_IMAGE.CC-ADDR-VALIDATION-CODE
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'ADDRESS CONFIRMATION:' BEFORE_IMAGE.CC-ADDR-VALIDATION-CODE INTO #OLD-DATA(#I)
  COMPRESS 'ADDRESS CONFIRMATION:' AFTER_IMAGE.CC-ADDR-VALIDATION-CODE  INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.CC-STATUS <> BEFORE_IMAGE.CC-STATUS OR
    AFTER_IMAGE.CC-STATUS-DATE <> BEFORE_IMAGE.CC-STATUS-DATE
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CC-INFO
  COMPRESS 'CC-STATUS:' BEFORE_IMAGE.CC-STATUS 'AS OF:' BEFORE_IMAGE.CC-STATUS-DATE
    INTO #OLD-DATA(#I)
  COMPRESS 'CC-STATUS:' AFTER_IMAGE.CC-STATUS 'AS OF:' AFTER_IMAGE.CC-STATUS-DATE
    INTO #NEW-DATA(#I)
END-IF
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE LOG-ECHECK-INFO-TRAN            /* YK2
**************************************
*
* ROUTING-NBR: 111111111 ACCT-NBR: 121212******1212 TYPE: CHECKING
* ROUTING-NBR: 999999999 ACCT-NBR: 426690******7096 TYPE: SAVING
* ACCOUNT NAME: ALDERMAN, JENNIFER
* ACCOUNT NAME: ALDERMAN, KENNETH D.
* ADDRESS: 539 MANSFIELD LN COLUMBIA SC 29203 9097
* ADDRESS: 535 MANSFIELD LN COLUMBIA SC 29203 9098
*
IF AFTER_IMAGE.ROUTING-NUMBER <> BEFORE_IMAGE.ROUTING-NUMBER OR
    AFTER_IMAGE.ACCOUNT-NUMBER <> BEFORE_IMAGE.ACCOUNT-NUMBER OR
    AFTER_IMAGE.ACCOUNT-TYPE <> BEFORE_IMAGE.ACCOUNT-TYPE
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #ECHECK-INFO
  COMPRESS 'ROUTING-NBR:' BEFORE_IMAGE.ROUTING-NUMBER 'ACCT-NBR:' BEFORE_IMAGE.ACCOUNT-NUMBER
    'TYPE' BEFORE_IMAGE.ACCOUNT-TYPE INTO #OLD-DATA(#I)
  COMPRESS 'ROUTING-NBR:' AFTER_IMAGE.ROUTING-NUMBER 'ACCT-NBR:' AFTER_IMAGE.ACCOUNT-NUMBER
    'TYPE' AFTER_IMAGE.ACCOUNT-TYPE INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.ACCOUNT-HOLDER-NAME <> BEFORE_IMAGE.ACCOUNT-HOLDER-NAME
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #ECHECK-INFO
  COMPRESS 'NAME:' BEFORE_IMAGE.ACCOUNT-HOLDER-NAME INTO #OLD-DATA(#I)
  COMPRESS 'NAME:' AFTER_IMAGE.ACCOUNT-HOLDER-NAME  INTO #NEW-DATA(#I)
END-IF
IF AFTER_IMAGE.ACCOUNT-ADDRESS-1 <> BEFORE_IMAGE.ACCOUNT-ADDRESS-1 OR
    AFTER_IMAGE.ACCOUNT-ADDRESS-2 <> BEFORE_IMAGE.ACCOUNT-ADDRESS-2 OR
    AFTER_IMAGE.ACCOUNT-ADDRESS-3 <> BEFORE_IMAGE.ACCOUNT-ADDRESS-3
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #ECHECK-INFO
  COMPRESS 'ADDRESS:' BEFORE_IMAGE.ACCOUNT-ADDRESS-1 BEFORE_IMAGE.ACCOUNT-ADDRESS-2
    BEFORE_IMAGE.ACCOUNT-ADDRESS-3 INTO #OLD-DATA(#I)
  COMPRESS 'ADDRESS:' AFTER_IMAGE.ACCOUNT-ADDRESS-1 AFTER_IMAGE.ACCOUNT-ADDRESS-2
    AFTER_IMAGE.ACCOUNT-ADDRESS-3 INTO #NEW-DATA(#I)
END-IF
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE LOG-EZPAY-INFO-TRAN            /* YK2
**************************************
*
* ROUTING-NBR: 111111111 ACCT-NUMBER: 121212******1212 TYPE: CHECKING
* ROUTING-NBR: 999999999 ACCT-NUMBER: 426690******7096 TYPE: SAVING
*
IF AFTER_IMAGE.ROUTING-NUMBER <> BEFORE_IMAGE.ROUTING-NUMBER OR
    AFTER_IMAGE.ACCOUNT-NUMBER <> BEFORE_IMAGE.ACCOUNT-NUMBER OR
    AFTER_IMAGE.ACCOUNT-TYPE <> BEFORE_IMAGE.ACCOUNT-TYPE
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #EZPAY-INFO
  COMPRESS 'ROUTING-NBR:' BEFORE_IMAGE.ROUTING-NUMBER 'ACCT-NBR:' BEFORE_IMAGE.ACCOUNT-NUMBER
    'TYPE' BEFORE_IMAGE.ACCOUNT-TYPE INTO #OLD-DATA(#I)
  COMPRESS 'ROUTING-NBR:' AFTER_IMAGE.ROUTING-NUMBER 'ACCT-NBR:' AFTER_IMAGE.ACCOUNT-NUMBER
    'TYPE' AFTER_IMAGE.ACCOUNT-TYPE INTO #NEW-DATA(#I)
END-IF
END-SUBROUTINE
*
******************************************
DEFINE SUBROUTINE LOG-PAYMENT-REQUEST-TRAN           /* YK2
******************************************
*
* Echeck Request Payment: Scheduled - 20140910 25.50 eCheck
* Echeck Request Payment: Completed - 20140910 25.50 eCheck
*
* Echeck Request Payment: Scheduled - 20140910 25.50 eCheck
* Echeck Request Payment: Cancelled - 20140910 25.50 eCheck
*
IF AFTER_IMAGE.REQUEST-STATUS <> BEFORE_IMAGE.REQUEST-STATUS
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #ECHECK-INFO
  #LOOK-UP-KEY := #LOOK-UP-REQ-STATUS
  BEFORE_IMAGE.REQUEST-STATUS := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,BEFORE_IMAGE.REQUEST-STATUS>)
  AFTER_IMAGE.REQUEST-STATUS := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,AFTER_IMAGE.REQUEST-STATUS>)
  COMPRESS BEFORE_IMAGE.REQ-POL-NUMBER BEFORE_IMAGE.REQUEST-STATUS BEFORE_IMAGE.PAYMENT-DATE
    BEFORE_IMAGE.PAYMENT-AMOUNT BEFORE_IMAGE.REQUEST-TYPE INTO #OLD-DATA(#I)
  COMPRESS AFTER_IMAGE.REQ-POL-NUMBER AFTER_IMAGE.REQUEST-STATUS AFTER_IMAGE.PAYMENT-DATE
    AFTER_IMAGE.PAYMENT-AMOUNT AFTER_IMAGE.REQUEST-TYPE INTO #NEW-DATA(#I)
*
END-IF
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE LOG-DISCLAIMER-TRAN
*************************************
*
IF AFTER_IMAGE.CC-DISCLAIMER = 'true' 
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DISCLAIMER
  #NEW-DATA(#I) := 'Credit Card disclaimer confirmed'
END-IF  
IF AFTER_IMAGE.BA-DISCLAIMER = 'true'  
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DISCLAIMER
  #NEW-DATA(#I) := 'Bank Account disclaimer confirmed'
END-IF  
END-SUBROUTINE
*
END
