* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDOWT   ** DOWNLOAD TR ACCOUNTING REG FILE
*     see ATHPDOWA for 1000 byte outpur processing
*
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* RSE  10052009 RE1  Change PROCESS-IND logic for Unification
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACTS       (P6)
*
1 AR-V VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER        (N6)
  2 PROCESS-IND      (A1) /* PM - 06/2000 - SPLIT COMPANIES
  2 NAME             (A25)
  2 TERMINAL-ID      (A8)
  2 DATE-LAST-ACCT-UPDATE (N8)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (P9)
  2 TRANSACTION-TIMN (P7)
  2 TXN-CODE         (A4)
  2 SEQUENCE-NUM     (N1)
  2 TXN-ACCT-NUMBER  (N3)
  2 ACCT-DATE        (N8)
  2 DEBIT-AMOUNT     (P7.2)
  2 CREDIT-AMOUNT    (P7.2)
  2 DESCRIPTION      (A20)
  2 MESSAGE-CODE     (A1)
  2 AALP-FLAG        (A1)
  2 CASH-CODE        (A1)
  2 USER-COMPANY     (A1)         /* RE1
*
1 #NUM-RECS          (P5)
1 #REC-OVERFLOW      (A250)
1 REDEFINE #REC-OVERFLOW
  2 #REC-OVF-1ST-125   (A125)
  2 #REC-OVF-2ND-125   (A125)
*
1 #EXTRACT-150
  2 #EXT-125-1   (A125)
  2 #EXT-25-2    (A25)
*
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
*
1 #LETTER-TABLE
  2 #1T (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>
  2 #2T (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>
1 REDEFINE #LETTER-TABLE
  2 #LETTER-OCC  (26)
    3 #UPPER     (A1)
    3 #LOWER     (A1)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=62
WRITE      TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
* FORMAT (2) LS=133 PS=62
* WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM
*                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
COMPRESS
  'ID'
  #TAB 'PRC-IND'
  #TAB 'NAME'
  #TAB 'TERM-ID'
  #TAB 'LST-ACCT-UPDT'
  #TAB 'USER'
  #TAB 'TRNS-DATN'
  #TAB 'TRNS-TIMN'
  #TAB 'TXN-CODE'
  #TAB 'SEQ-NUM'
  #TAB 'TXN-ACCT-#'
  #TAB 'ACCT-DT'
  #TAB 'DEB-AMT'
  #TAB 'CRED-AMT'
  #TAB 'DESC'
  #TAB 'MES-CODE'
  #TAB 'AALP-FLG'
  #TAB 'CASH-COD'
  INTO #REC-OVERFLOW LEAVING NO
PERFORM CHECK-OVER-125
PERFORM BUILD-FIXED-FILE   /* to get last chunk
WRITE WORK FILE 1 #EXTRACT-150
RESET #EXT-125-1
RESET #EXT-25-2
RESET #REC-OVERFLOW
*
READ AR-V BY TRANSACTION-DATN
* IF PROCESS-IND = 'S' /* Bypass AFFL ACCOUNTING Trans only use AAFM's    RE1
  IF AR-V.USER-COMPANY = 'S' /* Bypass AFFL ACCOUNTING Trans only use AAFM's  RE1
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #EXTRACTS
  DISPLAY ID-NUMBER NAME TXN-CODE TRANSACTION-DATN
  COMPRESS
    ID-NUMBER
    #TAB PROCESS-IND
    #TAB NAME
    #TAB TERMINAL-ID
    #TAB DATE-LAST-ACCT-UPDATE
    #TAB CLERK-ID
    #TAB TRANSACTION-DATN
    #TAB TRANSACTION-TIMN
    #TAB TXN-CODE
    #TAB SEQUENCE-NUM
    #TAB TXN-ACCT-NUMBER
    #TAB ACCT-DATE
    #TAB DEBIT-AMOUNT
    #TAB CREDIT-AMOUNT
    INTO #REC-OVERFLOW LEAVING NO
  PERFORM CHECK-OVER-125
  COMPRESS #REC-OVERFLOW
    #TAB DESCRIPTION
    #TAB MESSAGE-CODE
    #TAB AALP-FLAG
    #TAB CASH-CODE
    INTO #REC-OVERFLOW LEAVING NO
  PERFORM CHECK-OVER-125
  PERFORM BUILD-FIXED-FILE   /* to get last chunk
* WRITE 'write rec' #EXTRACT-150 (eM=XXXXXXXXXXXXXXXXXXXXX)
* WRITE 'write rec' #EXTRACT-150
  ADD 1 TO #NUM-RECS
  WRITE WORK FILE 1 #EXTRACT-150
  RESET #EXT-125-1
  RESET #EXT-25-2
  RESET #REC-OVERFLOW
END-READ /* (0050)
*
WRITE (2) 'NUMBER MEMBERS  = ' #EXTRACTS
WRITE (2) 'NUMBER RECORDS  = ' #NUM-RECS
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-OVER-125
* * * * * * * * * * * * * * * * *
*
IF #REC-OVF-2ND-125 NE ' '
  PERFORM BUILD-FIXED-FILE
  MOVE #REC-OVF-2ND-125 TO #REC-OVF-1ST-125
  RESET #REC-OVF-2ND-125
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * *
DEFINE BUILD-FIXED-FILE
* * * * * * * * * * * *
*
IF #EXT-125-1 = ' '
  MOVE #REC-OVF-1ST-125 TO #EXT-125-1
ELSE
  IF #EXT-25-2 = ' '
    MOVE #REC-OVF-1ST-125 TO #EXT-25-2
  END-IF
END-IF
*
END-SUBROUTINE
*
END
