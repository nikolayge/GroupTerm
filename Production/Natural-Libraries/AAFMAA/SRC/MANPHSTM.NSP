* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Program MANPHSTM.
/**
/** :author yfayngersh
/*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 ANH-V VIEW OF A-ANNU-HISTORY
  2 POLICY-NUMBER     (A16)
  2 CONTACT-ID        (N8.0)
  2 ANNUITANT-STATUS  (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION   (N7.2)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8) 2 REDEFINE ONE-MP-DATE
    3 #ONET-VEK (A2)
    3 #ONET-YY  (A2)
    3 #ONET-MM  (A2)
    3 #ONET-DD  (A2)    
  2 PRINCIPAL       (N7.2)
  2 ANNUAL-PAYMENT  (N7.2)
  2 RESERVE-AMOUNT  (N7.2)
  2 WITHDRAWALS     (N7.2)
  2 REFERENCE-CN    (N6.0)
  2 START-DATE      (N8.0) 
  2 ADD-USER        (A8)
  2 ADD-DATE        (N8.0)
  2 REDEFINE ADD-DATE
    3 #VEK  (A2)
    3 #YY   (A2)
    3 #MM   (A2)
    3 #DD   (A2)
  2 ADD-TIME (N7.0) 2 REDEFINE ADD-TIME
    3 HH   (A2)
    3 II   (A2)
    3 SS   (A2)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0) 2 REDEFINE LAST-UPD-DATE
    3 LUD-VEK    (A2)
    3 LUD-YY     (N2)
    3 LUD-MM     (N2)
    3 LUD-DD     (N2)   
  2 LAST-UPD-TIME (N7.0)
  2 GENERATION    (N7)
*
1 AU-V VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER     (A16)
  2 CONTACT-ID        (N8.0)
  2 WITHDRAWALS       (N7.2)
  2 MONTHLY-PENSION   (N7.2)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8)  2 REDEFINE ONE-MP-DATE
    3 ONET-VEK (A2)
    3 ONET-YY  (A2)
    3 ONET-MM  (A2)
    3 ONET-DD  (A2)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 REDEFINE LAST-UPD-DATE
    3 VEK  (A2)
    3 YY   (A2)
    3 MM   (A2)
    3 DD   (A2)
*
1 CONTACT-V VIEW OF A-CONTACTS
  2 ID-NUMBER  (N6)
  2 CONTACT-TYPE  (A1)
  2 CONTACT-ID (N8)
  2 GENDER-CD  (A1)
  2 SSN        (N9)
  2 DATE-OF-BIRTH (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 YYYY (N4)
    3 MM   (N2)
    3 DD   (N2)
*
1 #MAX-HIST   (I4) CONST <10>
1 #SCR-DATA
  2 POLICY-NBR        (A16)
  2 ISSUE-DATE        (N8)
  2 REDEFINE ISSUE-DATE
    3 #YYYY  (N4)
    3 #MM    (N2)
    3 #DD    (N2)
  2 #FIELD-VALUE   (N5.2/#MAX-HIST)
  2 #ORIG-DATE     (A8/#MAX-HIST)
  2 #ORIG-USER     (A8/#MAX-HIST)
  2 #HIST-DATE     (A8/#MAX-HIST)
  2 #HIST-TIME     (A8/#MAX-HIST)
  2 #HIST-USER     (A8/#MAX-HIST)
  2 #ONET-MP       (N5.2/#MAX-HIST)
  2 #ONET-DATE     (A8/#MAX-HIST)
1 #Field-titles    (A8/8) INIT <
  'MonPaymt',
  'HistDate',
  'HistTime',
  'HistUser',
  'OrigDate',
  'OrigUser',
  'OneTPay',
  'OneTDate'
  >
1 #ADDRESS            (A43/4)
1 #AGE                (N03.0)
1 #TODAY-A            (A10)
1 #SCR-TERMID         (A12)
*
1 #TODAY              (N8)
1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
*
1 #VALID-TYPES (A2/7) CONST <'AL','C ','A ','L ', 'L1','L2','L3'>
1 #VALID-STATUS (A1/3) CONST <'A','D','P'>
*
1 BLANK (A1) CONST    <' '>
1 #MSG                (A60)
1 #13TH-CHECK         (N7.2)
1 #DEPOSIT            (N7.2)
1 #DOB                (A10)
1 #AU-V-LUD           (A10)
1 #ANN-LUD            (A10)
1 #SD                 (A10)
1 #ID                 (A10)
1 #JDate              (A5) 1 REDEFINE #JDATE
  2 YY       (A2)
  2 #DDD     (N3)
1 #PR                 (N0.5)
1 #PAID               (N7.2)
1 #CODE-DESCRIPTION   (A24)
1 J    (I4)
*
1 #HIST-ISNS     (P8/99)
1 #HIST-ISNS-N   (I4)
1 #CURRENT-ISN   (P8)
*
END-DEFINE
*
SET KEY PF3 NAMED "Exit"
SET KEY PF7 NAMED 'Up'
SET KEY PF8 NAMED 'Down'
*
MOVE *DAT4U TO #TODAY-A
COMPRESS *INIT-USER '-' #CGA-CLERK INTO #SCR-TERMID LEAVING NO
MOVE *DATN TO #TODAY
MOVE '*' TO #CGA-SCR-COMMAND
*
CONTACT-V.CONTACT-ID := #CGA-SCR-ID
#CGA-SCR-ID := #CGA-ORIG-ID     /* PASSED CN
#SCR-DATA.ISSUE-DATE := #CGA-MF-SCR-PARM-1-N
MOVE LEFT JUSTIFIED #CGA-SCR-COMMAND TO #CGA-SCR-COMMAND
CALLNAT 'ADRN0016' CONTACT-V.CONTACT-ID
  #ADDRESS (*)
*
FIND CONTACT-V WITH CONTACT-ID = CONTACT-V.CONTACT-ID
  IF NO RECORDS FOUND
    #MSG := 'No contact record found'
    ESCAPE BOTTOM
  END-NOREC
  IF CONTACT-V.CONTACT-TYPE NE 'O'
    #AGE := #TODAY.#YYYY - CONTACT-V.YYYY
    IF #TODAY.#MM < CONTACT-V.MM
      #AGE := #AGE - 1
    ELSE IF #TODAY.#MM = CONTACT-V.MM AND #TODAY.#DD < CONTACT-V.DD
        #AGE := #AGE - 1
      END-IF
    END-IF
  END-IF
END-FIND
*
COMPRESS CONTACT-V.MM CONTACT-V.DD CONTACT-V.YYYY INTO #DOB WITH DELIMITER '/'
*
GET AU-V #CGA-ISN
MOVE AU-V.POLICY-NUMBER TO #SCR-DATA.POLICY-NBR

COMPRESS AU-V.MM AU-V.DD AU-V.YY INTO #AU-V-LUD WITH DELIMITER '/'

DECIDE ON FIRST VALUE OF #CGA-MF-SCR-PARM-2
  VALUE 'MONTH'
    PERFORM GET-MONTHLY-HIST
  NONE VALUE
    #CGA-SCR-ID := CONTACT-V.CONTACT-ID
    FETCH 'MANPADTL'
END-DECIDE
*
RPT.
REPEAT
  INPUT TEXT #MSG USING MAP 'MANMHST3'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPT.)
*     VALUE 'PF7'
*       PERFORM HISTORY-UP
*     VALUE 'PF8'
*       PERFORM HISTORY-DOWN
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      ESCAPE TOP
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
**#CGA-SCR-ID := CONTACT-V.CONTACT-ID
**FETCH 'MANPADTL'
/*
DEFINE GET-MONTHLY-HIST
/*     ------------------
RESET J
*
**IF AU-V.MONTHLY-PENSION > 0
MOVE 1 TO J
#FIELD-VALUE (J) := AU-V.MONTHLY-PENSION

#ORIG-DATE (J) := #AU-V-LUD

#ORIG-USER (J) := AU-V.LAST-UPD-USER
IF AU-V.ONE-MONTH-PENSION > 0
  #ONET-MP (J) := AU-V.ONE-MONTH-PENSION
  IF AU-V.ONE-MP-DATE > 0
    COMPRESS AU-V.ONET-MM
      AU-V.ONET-DD
      AU-V.ONET-YY INTO #ONET-DATE (J) WITH DELIMITER '/'
  END-IF
END-IF
**END-IF
FIND ANH-V WITH POLICY-NUMBER = AU-V.POLICY-NUMBER
    AND CONTACT-ID = CONTACT-V.CONTACT-ID
    SORTED BY GENERATION DESCENDING

  IF ANH-V.MONTHLY-PENSION > 0
*     IF ANH-V.MONTHLY-PENSION NE #FIELD-VALUE (J)
*       ADD 1 TO J
*     END-IF
    ADD 1 TO J
    IF J LE #MAX-HIST
      COMPRESS ANH-V.#MM ANH-V.#DD ANH-V.#YY
        INTO #HIST-DATE (J) WITH DELIMITER '/'
      COMPRESS ANH-V.HH ANH-V.II ANH-V.SS
        INTO #HIST-TIME (J) WITH DELIMITER ':'
      #FIELD-VALUE (J) := ANH-V.MONTHLY-PENSION
      #HIST-USER    (J) := ANH-V.ADD-USER

      COMPRESS ANH-V.LUD-MM ANH-V.LUD-DD ANH-V.LUD-YY
        INTO #ORIG-DATE (J) WITH DELIMITER '/'
      #ORIG-USER (J) := AU-V.LAST-UPD-USER

      IF ANH-V.ONE-MONTH-PENSION > 0
        #ONET-MP (J) := ANH-V.ONE-MONTH-PENSION
        IF ANH-V.ONE-MP-DATE > 0
          COMPRESS ANH-V.#ONET-MM
            ANH-V.#ONET-DD
            ANH-V.#ONET-YY INTO #ONET-DATE (J) WITH DELIMITER '/'
        END-IF
      END-IF

    ELSE
      ESCAPE BOTTOM
    END-IF
  END-IF
END-FIND
END-SUBROUTINE
END
