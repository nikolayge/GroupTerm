* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MANF0003.
/**
/** :author nguentchev
*
/* Total Paid: Initial Payment + all Monthly Payments + all 13th Checks
/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Taxable Amount: All Taxable Amount in the calendar year + Initial Payment + 13th Check
/*  Net Amount: Gross Amount - Taxable Amount
/* Long Term Care Settlement
/*  Taxable Amount: Should be blank; it is determined by the IRS
/*  Net Amount: Can be left blank
/*
DEFINE FUNCTION GET-AZ-1099-PAY RETURNS (N7.2)
  DEFINE DATA
  PARAMETER
  1 #ISN      (P8)
  1 #DATE     (N8) BY VALUE
  1 REDEFINE #DATE
    2 PARM-YYYY (N4)
    2 PARM-MM   (N2)
    2 PARM-DD   (N2)
  1 #TODAY (N8) 1 REDEFINE #TODAY
    2 #YYYY  (N4)
    2 #MM    (N2)
    2 #DD    (N2)
  1 REDEFINE #TODAY
    2 #TODAY-YYYYMM  (N6)
  LOCAL
  1 ANNUITANTS VIEW OF A-ANNUITANTS
    2 LAST-UPD-DATE     (N8) 2 REDEFINE LAST-UPD-DATE
      3 LUD-YYYYMM  (N6)
    2 POLICY-NUMBER     (A16)
    2 CONTACT-ID        (N8)
    2 SETTLEMENT-OPTION (A2)
    2 MONTHLY-PENSION   (N7.2)
    2 WITHDRAWALS       (N7.2)
    2 INITIAL-PAYMENT   (N7.2)
    2 DEPOSIT           (N7.2)
    2 T13TH-CHECK       (N7.2)
    2 TAXABLE           (N7.2)
    2 START-DATE (N8.0) 2 REDEFINE START-DATE
      3 START-YYYY (N4)
      3 START-MM   (N2)
    2 WITHDRAWAL-DATE   (N8)
    2 REDEFINE WITHDRAWAL-DATE
      3 WD-YYYY (N4)
      3 WD-MM   (N2)
      3 WD-DD   (N2)
    2 PAID-UP-DATE (N8) 2 REDEFINE PAID-UP-DATE
      3 PAID-UP-YYYY (N4)
  1 #INTEREST-ONLY (A2) CONST <'IO'>
  1 #PRO-RATE-X    (N7.2)
  1 A-ANNU-HISTORY VIEW OF A-ANNU-HISTORY
    2 ADD-DATE          (N8)
    2 MONTHLY-PENSION   (N7.2)
  1 ANH-V VIEW OF A-ANNU-HISTORY
    2 ADD-USER (A8)
    2 ADD-DATE (N8.0)
    2 ADD-TIME (N7.0)
    2 LAST-UPD-USER (A8)
    2 LAST-UPD-DATE (N8.0)
    2 LAST-UPD-TIME (N7.0)
    2 POLICY-NUMBER (A16)
    2 CONTACT-ID (N8.0) /*
    2 ANNUITANT-STATUS (A1)
    2 SETTLEMENT-OPTION (A2)
    2 MONTHLY-PENSION (N7.2)
    2 PRINCIPAL (N7.2)
    2 ANNUAL-PAYMENT (N7.2)
    2 RESERVE-AMOUNT (N7.2)
    2 WITHDRAWALS (N7.2)
    2 REFERENCE-CN (N6.0)
    2 START-DATE (N8.0)
    2 GENERATION (N7.0)
    2 PAID-UP-DATE (N8.0)
    2 INITIAL-PAYMENT (N7.2)
    2 DEPOSIT (N7.2)
    2 T13TH-CHECK (N7.2)
    2 TOTAL-WD (N7.2)
    2 NUMBER-WD (N4.0)
    2 RESIDUAL (N7.2)
    2 TOTAL-PAID (N7.2)
    2 TAXABLE (N7.2)
    2 CHECK-NUMBER (A10)
    2 ONE-MONTH-PENSION (N7.2)
    2 ONE-MP-DATE (N8.0)
    2 WITHDRAWAL-DATE (N8.0)
    2 YEAR-END (N4.0)
  END-DEFINE
/*
  RESET GET-AZ-1099-PAY
  GET ANNUITANTS #ISN

IF ANNUITANTS.LUD-YYYYMM > #TODAY-YYYYMM  
  FIND ANH-V WITH POLICY-NUMBER = ANNUITANTS.POLICY-NUMBER
      AND CONTACT-ID = ANNUITANTS.CONTACT-ID
      AND YEAR-END = #TODAY.#YYYY
    IF NO RECORDS FOUND
      FIND ANH-V WITH POLICY-NUMBER = ANNUITANTS.POLICY-NUMBER
          AND CONTACT-ID = ANNUITANTS.CONTACT-ID
          SORTED BY GENERATION DESCENDING
        IF ANH-V.LAST-UPD-DATE <= #TODAY
          MOVE BY NAME ANH-V TO ANNUITANTS
          ESCAPE BOTTOM
        END-IF
      END-FIND
      ESCAPE BOTTOM
    END-NOREC
    MOVE BY NAME ANH-V TO ANNUITANTS
  END-FIND
END-IF

IF PAID-UP-DATE > 0
  IF PAID-UP-YYYY < #TODAY.#YYYY
    ESCAPE ROUTINE
  ELSE
    #TODAY := PAID-UP-DATE
    SUBTRACT 1 FROM #TODAY.#MM
  END-IF
END-IF
*
IF ANNUITANTS.START-DATE > 0
    AND ANNUITANTS.START-YYYY = PARM-YYYY
  #DATE := ANNUITANTS.START-DATE
  GET-AZ-1099-PAY := ANNUITANTS.INITIAL-PAYMENT
  /* Initial payment is taxable amount for establish month
  ADD 1 TO PARM-MM
END-IF
*
GET-AZ-1099-PAY := GET-AZ-1099-PAY + ANNUITANTS.T13TH-CHECK
*
REPEAT UNTIL #DATE.PARM-MM > 12
    OR #DATE.PARM-MM > #TODAY.#MM
  GET-AZ-1099-PAY := GET-AZ-1099-PAY
    + GET-AZ-MONTHLY-TAX (< #ISN, #DATE >)
  ADD 1 TO #DATE.PARM-MM
END-REPEAT

END-FUNCTION
END
