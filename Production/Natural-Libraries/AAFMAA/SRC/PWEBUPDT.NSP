* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: PWEBUPDT - member information update
* update IAS wiht the extract from Proxy Ballot website
* runs by request when the extract is available
****************************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  05142008 n/a  Initial creation
* TMT  05142014 TT1  Contact Normalization - Emails - This Program No longer
*                    will update the status file's email address
*****************************************************************************
*
DEFINE DATA
LOCAL USING OBJLSTAT
LOCAL USING OBJLTRRG
*
LOCAL
1 #INPUT-RECORD      (A1000)
1 #CN-NUMBER         (A) Dynamic
1 #RANK              (A) Dynamic
1 #E-MAIL            (A) Dynamic
1 #HOME-PHONE        (A) Dynamic
1 #WORK-PHONE        (A) Dynamic
1 #FAX-NUMBER        (A) Dynamic
1 #DATE-UPDATE-W     (A) Dynamic
1 #ANN-RPT-FLAG      (A) Dynamic
*
1 #I         (N2)
1 #I1        (N2)
1 #ERROR     (L)
1 #DOMAIN    (A60)
1 #DATE-UPDATE    (N8)                /* FROM WEBSITE
1 #ID-NUMBER            (A6)
1 REDEFINE #ID-NUMBER
  2 #ID-NUMBER-N        (N6)
1 #TRX  (I2)
1 #SFX  (I2)
1 #W-OLD-NEW-DATA  (A125)
1 REDEFINE #W-OLD-NEW-DATA
  2 #W-DESCRIPTION (A10)
  2 #W-DATA        (A115)
*
1 #COMMAND       (A2)
1 #RANK-PASS     (A3)
1 #INVALID       (L)
1 #RANK-PAY      (P2)
1 #RANK-LITERAL  (A23)
1 #RANK-DISPLAY  (A5)
1 #UPDATE-DATE   (L)
*
1 #TAB           (A1) INIT <H'09'>
1 #ERROR-MESSAGE (A78)
1 #PHONE-A       (A20)
1 #PHONE         (N10)
1 REDEFINE #PHONE
  2 #PHONE-10     (A10)
1 #PHONE-INTR-EXT (A6)
*
1 #REC-READ           (N4)
1 #CNT-RANK          (N4)
1 #CNT-ANN-RPT-FLAG  (N4)
1 #CNT-FAX-NUMBER    (N4)
1 #CNT-HOME-NUMBER   (N4)
1 #CNT-WORK-NUMBER   (N4)
1 #CNT-E-MAIL        (N4)
1 #CNT-ALL           (N4)
1 #CNT-RECENT-CHANGE (N4)
1 #CNT-RANK-INVALID  (N4)
1 #CNT-HOME-PHONE-INVALID (N4)
1 #CNT-WORK-PHONE-INVALID (N4)
1 #CNT-FAX-NUMBER-INVALID (N4)
1 #CNT-ANN-RPT-FLAG-INVALID (N4)
1 #CNT-E-MAIL-INVALID     (N4)
1 #CNT-HOME-PHONE-SAME    (N4)
1 #CNT-WORK-PHONE-SAME    (N4)
1 #CNT-FAX-NUMBER-SAME    (N4)
1 #CNT-RANK-SAME          (N4)
1 #CNT-ANN-RPT-FLAG-SAME  (N4)
1 #CNT-E-MAIL-SAME        (N4)
1 EMAIL-ADDRESS           (A) DYNAMIC
*
END-DEFINE
*
FORMAT (1)  LS=133 PS=76
WRITE  (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  '(1) FIELDS NOT UPDATED - ERROR LOG DETAIL'
  5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
FORMAT (2)  LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  '(2) TRANSACTION LOG '
  5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
FORMAT (3)  LS=133 PS=76
WRITE (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  '(3) NOT UPDATED - RECENT CHANGES'
  5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
FORMAT (4)  LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  '(4) UPDATE STATISTICS'
  5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
*
READ WORK FILE 1 #INPUT-RECORD
  IF #INPUT-RECORD = MASK ('ID-NUMBER')
    ESCAPE TOP
  END-IF
  EXAMINE #INPUT-RECORD TRANSLATE INTO UPPER CASE
  SEPARATE #INPUT-RECORD INTO #CN-NUMBER #RANK #E-MAIL
    #HOME-PHONE #WORK-PHONE #FAX-NUMBER #DATE-UPDATE-W
    #ANN-RPT-FLAG
    WITH DELIMITER #TAB
  RESET #UPDATE-DATE #ERROR #PHONE-A #DATE-UPDATE #RANK
  #REC-READ := #REC-READ + 1
  #CN-NUMBER := *TRIM(#CN-NUMBER)
  #ID-NUMBER-N := VAL(#CN-NUMBER)
*
  IF #ANN-RPT-FLAG NE ' '
    IF NOT (#ANN-RPT-FLAG = 'N' OR = 'F' OR = 'S' )
      COMPRESS #ID-NUMBER 'ANNUAL REPORT FLAG WILL NOT CHANGE. FLAG '
        #ANN-RPT-FLAG ' INVALID' INTO #ERROR-MESSAGE
      WRITE (1) #ERROR-MESSAGE
      #CNT-ANN-RPT-FLAG-INVALID := #CNT-ANN-RPT-FLAG-INVALID + 1
      RESET #ANN-RPT-FLAG
    END-IF
  END-IF
*
  IF   #E-MAIL NE ' '
    PERFORM EMAIL-EDIT
    IF #ERROR
      COMPRESS #ID-NUMBER 'E-MAIL WILL NOT CHANGE. '
        #E-MAIL 'INVALID' #ERROR-MESSAGE INTO #ERROR-MESSAGE
      WRITE (1) #ERROR-MESSAGE
      #CNT-E-MAIL-INVALID := #CNT-E-MAIL-INVALID + 1
      RESET #E-MAIL
    END-IF
  END-IF
*
  IF #HOME-PHONE NE ' '
    #PHONE-A := #HOME-PHONE
    PERFORM PHONE-EDIT
    IF #ERROR
      COMPRESS #ID-NUMBER 'HOME PHONE NUMBER WILL NOT CHANGE. '
        #HOME-PHONE 'INVALID' INTO #ERROR-MESSAGE
      WRITE (1) #ERROR-MESSAGE
      #CNT-HOME-PHONE-INVALID := #CNT-HOME-PHONE-INVALID + 1
      RESET #HOME-PHONE
    ELSE
      #HOME-PHONE := #PHONE-A
    END-IF
  END-IF
  IF #WORK-PHONE NE ' '
    #PHONE-A := #WORK-PHONE
    PERFORM PHONE-EDIT
    IF #ERROR
      COMPRESS #ID-NUMBER 'WORK PHONE NUMBER WILL NOT CHANGE. '
        #WORK-PHONE 'INVALID' INTO #ERROR-MESSAGE
      WRITE (1) #ERROR-MESSAGE
      #CNT-WORK-PHONE-INVALID := #CNT-WORK-PHONE-INVALID + 1
      RESET #WORK-PHONE
    ELSE
      #WORK-PHONE := #PHONE-A
    END-IF
  END-IF
  IF #FAX-NUMBER NE ' '
    #PHONE-A := #FAX-NUMBER
    PERFORM PHONE-EDIT
    IF #ERROR
      COMPRESS #ID-NUMBER 'FAX NUMBER WILL NOT CHANGE. '
        #FAX-NUMBER 'INVALID' INTO #ERROR-MESSAGE
      WRITE (1) #ERROR-MESSAGE
      RESET #FAX-NUMBER
      #CNT-FAX-NUMBER-INVALID := #CNT-FAX-NUMBER-INVALID + 1
    ELSE
      #FAX-NUMBER := #PHONE-A
    END-IF
  END-IF
*
  FIND(1) ST-V WITH MEMBER-KEY = #ID-NUMBER
    IF #DATE-UPDATE-W NE ' '
      #DATE-UPDATE-W := *TRIM(#DATE-UPDATE-W)
      #DATE-UPDATE := VAL(#DATE-UPDATE-W)
      WRITE #DATE-UPDATE
      IF #DATE-UPDATE < 20090413
        #DATE-UPDATE := 20090413
      END-IF
      IF ST-V.DATE-OTHER-UPDATE GT #DATE-UPDATE
        WRITE (3) #ID-NUMBER ' - RECORD RECENTLY UPDATED. NO CHANGES APPLIED '
          ST-V.DATE-OTHER-UPDATE ' ' #DATE-UPDATE
        #CNT-RECENT-CHANGE := #CNT-RECENT-CHANGE + 1
        ESCAPE BOTTOM
      END-IF
    END-IF
    IF #RANK NE ' '
      #RANK-PASS := #RANK
      EXAMINE #RANK-PASS TRANSLATE INTO UPPER CASE
      CALLNAT 'GRANKSXX' #COMMAND #RANK-PASS ST-V.MILITARY-SERVICE
        #INVALID #RANK-PAY #RANK-LITERAL #RANK-DISPLAY
      IF #INVALID
        COMPRESS #ID-NUMBER ' RANK WILL NOT CHANGE. RANK ' #RANK ' INVALID'
          INTO #ERROR-MESSAGE
        WRITE (1) #ERROR-MESSAGE
        #CNT-RANK-INVALID := #CNT-RANK-INVALID + 1
        RESET #RANK
      END-IF
    END-IF
    PERFORM UPDATE-WRITE-TR-LOG
    UPDATE
  END-FIND
  END TRANSACTION
END-WORK
*
WRITE (4)
  'RECORDS READ                     ' #REC-READ           /
  'RANK UPDATED                     ' #CNT-RANK           /
  'ANNUAL REPORT FLAG UPDATED       ' #CNT-ANN-RPT-FLAG   /
  'HOME NUMBER UPDATED              ' #CNT-HOME-NUMBER    /
  'WORK NUMBER UPDATED              ' #CNT-WORK-NUMBER    /
  'FAX NUMBER UPDATED               ' #CNT-FAX-NUMBER     /
  'E-MAIL UPDATED                   ' #CNT-E-MAIL         /
  'RECORDS UPDATED                  ' #CNT-ALL            /
  'RECENT-CHANGES                   ' #CNT-RECENT-CHANGE  /
  'RANK INVALID                     ' #CNT-RANK-INVALID   /
  'ANNUAL REPORT FLAG INVALID       ' #CNT-ANN-RPT-FLAG-INVALID   /
  'HOME-PHONE-INVALID               ' #CNT-HOME-PHONE-INVALID     /
  'WORK-PHONE INVALID               ' #CNT-WORK-PHONE-INVALID     /
  'FAX NUMBER INVALID               ' #CNT-FAX-NUMBER-INVALID     /
  'E-MAIL INVALID                   ' #CNT-E-MAIL-INVALID         /
  'RANK NOT CHANGED                 ' #CNT-RANK-SAME              /
  'ANNUAL REPORT FLAG NOT CHANGED   ' #CNT-ANN-RPT-FLAG-SAME   /
  'HOME PHONE NOT CHANGED           ' #CNT-HOME-PHONE-SAME    /
  'WORK PHONE NOT CHANGED           ' #CNT-WORK-PHONE-SAME    /
  'FAX NUMBER NOT CHANGED           ' #CNT-FAX-NUMBER-SAME    /
  'E-MAIL NOT CHANGED               ' #CNT-E-MAIL-SAME
*
****************************
DEFINE SUBROUTINE EMAIL-EDIT
****************************
*
RESET #ERROR
EXAMINE #E-MAIL TRANSLATE INTO UPPER CASE
* MOVE LEFT #E-MAIL TO #E-MAIL
EXAMINE #E-MAIL FOR ' ' POSITION #I
IF #I NE 0
  #ERROR := TRUE
  #ERROR-MESSAGE := '- IMBEDDED SPACES ARE NOT ALLOWED'
  ESCAPE ROUTINE
END-IF
EXAMINE #E-MAIL FOR '@' NUMBER #I GIVING POSITION #I1
IF #I NE 1
  #ERROR := TRUE
  #ERROR-MESSAGE := '- 1 "@" IS REQUIRED'
  ESCAPE ROUTINE
END-IF
#I1 := #I1 + 1
#DOMAIN := SUBSTR(#E-MAIL,#I1)
EXAMINE #DOMAIN FOR '.' NUMBER #I1 POSITION #I
IF #I1 = 0
  #ERROR := TRUE
  #ERROR-MESSAGE := '- "." IS REQUIRED IN THE DOMAIN'
  ESCAPE ROUTINE
ELSE
  IF #I1 > 1
    REPEAT WHILE (#I1 NE 1)
      #I := #I + 1
      #DOMAIN := SUBSTR(#DOMAIN,#I)
      EXAMINE #DOMAIN FOR '.' NUMBER #I1 POSITION #I
    END-REPEAT
  END-IF
END-IF
#I := #I + 1
#DOMAIN := SUBSTR(#DOMAIN,#I)
IF NOT #DOMAIN = 'COM' OR = 'NET' OR = 'ORG' OR = 'EDU'
    OR = 'MIL' OR = 'GOV' OR = 'KR' OR = 'DE' OR = 'FR' OR = 'US'
    OR = 'UK' OR = 'IT' OR = 'CA' OR = 'MX' OR = 'VA' OR = 'JP'
    OR = 'SE' OR = 'LT' OR = 'CR' OR = 'GR' OR = 'BE' OR = 'RU'
    OR = 'CH' OR = 'BIZ' OR = 'DK' OR = 'CC'
  #ERROR := TRUE
  #ERROR-MESSAGE := '- NOT A STANDARD DOMAIN'
  ESCAPE ROUTINE
END-IF
*
END-SUBROUTINE
*
****************************
DEFINE SUBROUTINE PHONE-EDIT
****************************
*
RESET #ERROR
EXAMINE #PHONE-A FOR '-' DELETE
EXAMINE #PHONE-A FOR '/' DELETE
EXAMINE #PHONE-A FOR '\' DELETE
EXAMINE #PHONE-A FOR '(' DELETE
EXAMINE #PHONE-A FOR ')' DELETE
EXAMINE #PHONE-A FOR ' ' DELETE
EXAMINE #PHONE-A FOR '.' DELETE
EXAMINE #PHONE-A FOR ',' DELETE
EXAMINE #PHONE-A FOR '+' DELETE
EXAMINE #PHONE-A TRANSLATE INTO UPPER CASE
EXAMINE #PHONE-A FOR 'EXT' REPLACE 'X'
#PHONE-10 := SUBSTR(#PHONE-A,1,10)
IF #PHONE-10 NE MASK(NNNNNNNNNN)
  #ERROR := TRUE
  ESCAPE ROUTINE
END-IF
* IF #PHONE < 1000000000
*  #ERROR := TRUE
*  ESCAPE ROUTINE
* END-IF
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE UPDATE-WRITE-TR-LOG
*************************************
*
RESET #TRX #SFX TR-V
IF #RANK NE ' '
  IF ST-V.RANK NE #RANK
    #SFX := #SFX + 1
    #TRX := #TRX + 1
    #UPDATE-DATE := TRUE
    #W-DESCRIPTION := 'RANK'
    SYNC-FLD-NUM(#SFX) := 10
    #W-DATA := SYNC-FLD-OLD-DATA(#SFX) := ST-V.RANK
    OLD-DATA (#TRX) := #W-OLD-NEW-DATA
    #W-DATA := SYNC-FLD-NEW-DATA(#SFX) := #RANK
    NEW-DATA (#TRX) := #W-OLD-NEW-DATA
    ST-V.RANK := #RANK
    #CNT-RANK := #CNT-RANK + 1
  ELSE
    #CNT-RANK-SAME := #CNT-RANK-SAME + 1
  END-IF
END-IF
*
IF #ANN-RPT-FLAG NE ' '
  IF ST-V.ANNUAL-RPT-FLAG NE #ANN-RPT-FLAG
    #TRX := #TRX + 1
    #W-DESCRIPTION := 'ANN RP CD'
    #W-DATA := ST-V.ANNUAL-RPT-FLAG
    OLD-DATA (#TRX) := #W-OLD-NEW-DATA
    #W-DATA := #ANN-RPT-FLAG
    NEW-DATA (#TRX) := #W-OLD-NEW-DATA
    ST-V.ANNUAL-RPT-FLAG := #ANN-RPT-FLAG
    #CNT-ANN-RPT-FLAG := #CNT-ANN-RPT-FLAG + 1
  ELSE
    #CNT-ANN-RPT-FLAG-SAME := #CNT-ANN-RPT-FLAG-SAME + 1
  END-IF
END-IF
*
IF #E-MAIL NE ' '
*
  RESET EMAIL-ADDRESS                                               /* TT1
  EMAIL-ADDRESS := F-PREF-EMAIL(<ST-V.MEMBER-CONTACT-ID>)           /* TT1
*            
  IF EMAIL-ADDRESS NE #E-MAIL                                       /* TT1
    #SFX := #SFX + 1
    #TRX := #TRX + 1
    #W-DESCRIPTION := 'INTERNET'
    SYNC-FLD-NUM(#SFX) := 15
    #W-DATA := SYNC-FLD-OLD-DATA(#SFX) := EMAIL-ADDRESS             /* TT1
    OLD-DATA (#TRX) := #W-OLD-NEW-DATA
    #W-DATA := SYNC-FLD-NEW-DATA(#SFX) := #E-MAIL
    NEW-DATA (#TRX) := #W-OLD-NEW-DATA
    EMAIL-ADDRESS := #E-MAIL
    #CNT-E-MAIL := #CNT-E-MAIL + 1
  ELSE
    #CNT-E-MAIL-SAME := #CNT-E-MAIL-SAME + 1
  END-IF
END-IF
*
* Phone change processing
* IF  #HOME-PHONE NE ' '
*   #PHONE-10 := SUBSTR(#HOME-PHONE,1,10)
*   #PHONE-INTR-EXT := SUBSTR(#HOME-PHONE,11,6)
*   IF ST-V.TELE-NUMBER NE VAL(#PHONE-10)
*       OR ST-V.HOME-TELE-INTR NE #PHONE-INTR-EXT
*     #SFX := #SFX + 1
*     #TRX := #TRX + 1
*     #W-DESCRIPTION := 'HOME-TEL #'
*     IF ST-V.WIDOW-FLAG = 'Y'
*       #W-DESCRIPTION := 'WID-TELE'
*     END-IF
*     SYNC-FLD-NUM(#SFX) := 12
*     #PHONE := ST-V.TELE-NUMBER
*     COMPRESS #PHONE-10 ST-V.HOME-TELE-INTR TO #W-DATA
*     OLD-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-OLD-DATA(#SFX) := #W-DATA
*     #PHONE-10 := SUBSTR(#HOME-PHONE,1,10)
*     COMPRESS #PHONE-10 #PHONE-INTR-EXT INTO #W-DATA
*     NEW-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-NEW-DATA(#SFX) := #W-DATA
*     ST-V.TELE-NUMBER := VAL(#PHONE-10)
*     ST-V.HOME-TELE-INTR := #PHONE-INTR-EXT
*     #CNT-HOME-NUMBER := #CNT-HOME-NUMBER + 1
*   ELSE
*     #CNT-HOME-PHONE-SAME := #CNT-HOME-PHONE-SAME + 1
*   END-IF
* END-IF
* *
* IF #WORK-PHONE NE ' '
*   #PHONE-10 := SUBSTR(#WORK-PHONE,1,10)
*   #PHONE-INTR-EXT := SUBSTR(#WORK-PHONE,11,6)
*   IF  ST-V.NXT-OF-KIN-TELE-NUMBER NE VAL(#PHONE-10)
*       OR ST-V.WORK-TELE-INTR NE #PHONE-INTR-EXT
*     #SFX := #SFX + 1
*     #TRX := #TRX + 1
*     #W-DESCRIPTION := 'WORK-TEL #'
*     IF ST-V.WIDOW-FLAG = 'Y'
*       #W-DESCRIPTION := 'NOK/WK TEL'
*     END-IF
*     SYNC-FLD-NUM(#SFX) := 13
*     #PHONE := ST-V.NXT-OF-KIN-TELE-NUMBER
*     COMPRESS #PHONE-10 ST-V.WORK-TELE-INTR INTO #W-DATA
*     OLD-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-OLD-DATA(#SFX) := #W-DATA
*     #PHONE-10 := SUBSTR(#WORK-PHONE,1,10)
*     COMPRESS #PHONE-10 #PHONE-INTR-EXT INTO #W-DATA
*     NEW-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-NEW-DATA(#SFX) := #W-DATA
*     ST-V.NXT-OF-KIN-TELE-NUMBER := VAL(#PHONE-10)
*     ST-V.WORK-TELE-INTR := #PHONE-INTR-EXT
*     #CNT-WORK-NUMBER := #CNT-WORK-NUMBER + 1
*   ELSE
*     #CNT-WORK-PHONE-SAME := #CNT-WORK-PHONE-SAME + 1
*   END-IF
* END-IF
* *
* IF #FAX-NUMBER NE ' '
*   #PHONE-10 := SUBSTR(#FAX-NUMBER,1,10)
*   #PHONE-INTR-EXT := SUBSTR(#FAX-NUMBER,11,6)
*   IF  ST-V.FAX-NUMBER NE VAL(#PHONE-10)
*       OR ST-V.FAX-NUMBER-INTR NE #PHONE-INTR-EXT
*     #SFX := #SFX + 1
*     #TRX := #TRX + 1
*     #W-DESCRIPTION := 'FAX # '
*     SYNC-FLD-NUM(#SFX) := 14
*     #PHONE := ST-V.FAX-NUMBER
*     COMPRESS #PHONE-10 ST-V.FAX-NUMBER-INTR INTO #W-DATA
*     OLD-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-OLD-DATA(#SFX) := #W-DATA
*     #PHONE-10 := SUBSTR(#FAX-NUMBER,1,10)
*     COMPRESS #PHONE-10 #PHONE-INTR-EXT INTO #W-DATA
*     NEW-DATA (#TRX) := #W-OLD-NEW-DATA
*     SYNC-FLD-NEW-DATA(#SFX) := #W-DATA
*     ST-V.FAX-NUMBER := VAL(#PHONE-10)
*     ST-V.FAX-NUMBER-INTR := #PHONE-INTR-EXT
*     #CNT-FAX-NUMBER := #CNT-FAX-NUMBER + 1
*   ELSE
*     #CNT-FAX-NUMBER-SAME := #CNT-FAX-NUMBER-SAME + 1
*   END-IF
* END-IF
*
IF #TRX > 0
  IF #UPDATE-DATE
    MOVE *DATN TO ST-V.DATE-OTHER-UPDATE
  END-IF
  PERFORM WRITE-TRLOG
  PERFORM WRITE-REPORT
END-IF
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE WRITE-TRLOG
*****************************
*
TR-V.ID-NUMBER   := ST-V.ID-NUMBER
TR-V.NAME        := ST-V.NAME
TR-V.PROCESS-IND := ST-V.PROCESS-IND
TR-V.DATE-LAST-UPDATE := ST-V.DATE-OTHER-UPDATE
TR-V.CLERK-ID    := 'EDS'
TR-V.TERMINAL-ID := *INIT-ID
TR-V.TRANSACTION-DATN := *DATN
TR-V.TRANSACTION-TIMN := *TIMN
COMPRESS 'WEB-UPDATE' NEW-DATA(1) INTO NEW-DATA(1)
STORE TR-V
#CNT-ALL := #CNT-ALL + 1
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE WRITE-REPORT
******************************
*
WRITE (2) 'TR' TR-V.ID-NUMBER   '      PRIOR                     NEW'
FOR #I1 1 TO 32
  IF OLD-DATA (#I1) NE ' '
    WRITE (2) OLD-DATA (#I1) (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
      NEW-DATA (#I1) (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
  ELSE
    ESCAPE BOTTOM
  END-IF
END-FOR
END-SUBROUTINE
END
