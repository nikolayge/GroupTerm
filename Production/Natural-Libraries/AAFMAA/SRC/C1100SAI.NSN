* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1100SAI   ** DETERMINE AUTO ISSUE AMOUNTS
*
*******************************************************************************
*   DATE      INITIALS    TAG      REASON                                     *
*   20070328    PAM       None     Program Written                            *
*   200700601   PAM       PM1      Added Military Service to Criteria         *
*******************************************************************************
*
DEFINE DATA
PARAMETER USING C1100LAI
*
LOCAL USING C1101LAI
LOCAL USING GENFLLXX
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER                   (N6)
 2 ISSUE-DATE                  (N8/9)
 2 STATUS                      (A1/9)
 2 FACE-AMOUNT                 (P7.2/9)
 2 AUTO-ISSUE                  (A1/9)
*
1 #I1                          (I2)
1 #ACTIVE                      (A1)  INIT <'D'>
1 #YES                         (A1)  INIT <'Y'>
1 #YES-2                       (A1)  INIT <'y'>
*
END-DEFINE
********************************************************************************
*
PERFORM FIND-AUTO-ISSUE-AMOUNT
*
PERFORM FIND-EXISTING-AUTO-ISSUE-AMOUNT
*
********************************************************************************
DEFINE SUBROUTINE FIND-AUTO-ISSUE-AMOUNT
********************************************************************************
*
#AUTO-ISSUE-INTRO-FLAG := #INTRO-FLAG
#AUTO-ISSUE-AGE        := #AGE-1100
#AUTO-ISSUE-STATUS     := #MEMBER-MILITARY-STATUS
#AUTO-ISSUE-SERVICE    := #MEMBER-MILITARY-SERVICE                     /* PM1
*
CALLNAT 'C1101SAI'
  #PDA-AUTO-ISSUE-AMOUNT
  #PDA-GENERAL-FILE
*
#AUTO-ISSUE-KEY-FOUND-1100 := #AUTO-ISSUE-KEY-FOUND
*
END-SUBROUTINE
*
********************************************************************************
DEFINE SUBROUTINE FIND-EXISTING-AUTO-ISSUE-AMOUNT
********************************************************************************
*
RESET #CURRENT-AUTO-ISSUE #REMAINING-AUTO-ISSUE
*
FIND ST-V WITH MEMBER-KEY = #MEMBER-CN-A
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  FOR #I1 1 TO 9        /* ONLY ACTIVE POLICIES COUNT TOWARDS AUTO ISSUE TOTALS
    IF ST-V.AUTO-ISSUE (#I1) = #YES OR = #YES-2
     AND ST-V.STATUS (#I1) = #ACTIVE
      ADD ST-V.FACE-AMOUNT (#I1) TO #CURRENT-AUTO-ISSUE
    END-IF
  END-FOR
END-FIND
*
COMPUTE #REMAINING-AUTO-ISSUE = #GEN-VALUE (1) - #CURRENT-AUTO-ISSUE
*
IF #REMAINING-AUTO-ISSUE LT 0
  RESET #REMAINING-AUTO-ISSUE
END-IF
*
END-SUBROUTINE
*
END
