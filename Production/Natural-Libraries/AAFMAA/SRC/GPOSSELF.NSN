* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPOSSELF                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Load selection screen and choose file                 *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE DATA
PARAMETER USING GPOASELF
*
LOCAL
1 #CNT                                     (P5)
1 #SCREEN                                  (P5)
1 #SCREEN-COUNT                            (P5)
1 #LINE-COUNT                              (P5)
1 #MAIN-SCREEN                             (P5)
1 #MAIN-SCREEN-COUNT                       (P5)
1 #MAIN-MAX-ENTRY                          (P5)
1 #MAIN-DET-IND                            (I4)
1 #SCR-IND                                 (I4)
1 #I                                       (I4)
*
1 #DISPLAY-FOLDER-FILE-NAME                (A70)  INIT <'/usr/SAG/AAFMAA/batch/workfiles/TEMP/ACH_FILE_LIST.TXT'>
1 #SHCMD                                   (A250)
1 #INPUT-RECORD                            (A50)
1 #TEXT                                    (A50)
*
1 #FILE-TABLE                              (500)
 2 #FILENAME                               (A50)
 2 #FILEDATE                               (A10)
 2 #FILETIME                               (A10)
 2 #TRUE-FILENAME                          (A50)
*
1 #DISPLAY-FILE-TABLE                      (10)
 2 #DISPLAY-SELECT                         (A1)
 2 #DISPLAY-FILENAME                       (A50)
 2 #DISPLAY-FILEDATE                       (A10)
 2 #DISPLAY-FILETIME                       (A8)
 2 #DISPLAY-TRUE-FILENAME                  (A50)
 2 #SCR-CV                                 (C)
*
1 #WORK-FIELDS
 2 #DATE                                   (A8)
 2 REDEFINE #DATE
  3 #DATE-YYYY                             (A4)
  3 #DATE-MM                               (A2)
  3 #DATE-DD                               (A2)
 2 #TIME                                   (A6)
 2 REDEFINE #TIME
  3 #TIME-HH                               (A2)
  3 #TIME-MM                               (A2)
  3 #TIME-SS                               (A2)
*
END-DEFINE
************************************************************************
*
DEFINE WINDOW FILE-BOX
  SIZE 30*65
  BASE TOP LEFT
*
PERFORM CREATE-FILE-LIST
*
READ WORK 1 #INPUT-RECORD
  ADD +1 TO #CNT
  SEPARATE #INPUT-RECORD INTO #FILE-TABLE (#CNT) WITH DELIMITER ':'
  #TRUE-FILENAME (#CNT) := #INPUT-RECORD
END-WORK
*
COMPUTE #SCREEN-COUNT = #CNT / 10 + 1
#MAIN-MAX-ENTRY      := #CNT
#SCREEN              := 1
*
PERFORM NEXT-SCREEN
*
SET WINDOW 'FILE-BOX'
  REPEAT
    PERFORM SET-KEYS
*
    IF #CNT = 0
      #TEXT := 'No records to process'
    END-IF
*
    #MAIN-DET-IND := ((#SCREEN - 1) * 10) + 1
    #LINE-COUNT   := #MAIN-DET-IND + 9
    IF #LINE-COUNT > #MAIN-MAX-ENTRY
      #LINE-COUNT := #MAIN-MAX-ENTRY
    END-IF
*
    RESET #SCR-CV (*)
    RESET #SCR-IND #DISPLAY-FILE-TABLE (*)
    FOR #MAIN-DET-IND #MAIN-DET-IND TO #LINE-COUNT
      ADD +1 TO #SCR-IND
      #DISPLAY-TRUE-FILENAME (#SCR-IND) := #TRUE-FILENAME (#MAIN-DET-IND)
      #DISPLAY-FILENAME (#SCR-IND)      := #FILENAME (#MAIN-DET-IND)
      #DATE                             := #FILEDATE (#MAIN-DET-IND)
      COMPRESS #DATE-MM #DATE-DD #DATE-YYYY INTO #DISPLAY-FILEDATE(#SCR-IND) WITH '/'
      #TIME                             := #FILETIME (#MAIN-DET-IND)
      COMPRESS #TIME-HH #TIME-MM #TIME-SS INTO #DISPLAY-FILETIME (#SCR-IND) WITH ':'
      RESET #SCR-CV (#SCR-IND)
    END-FOR
*
    #SCR-IND := #SCR-IND + 1
    FOR #SCR-IND #SCR-IND TO 10
      MOVE (AD=P) TO #SCR-CV (#SCR-IND)
    END-FOR
*
    INPUT TEXT #TEXT USING MAP 'GPOMSELF'
*
    DECIDE ON FIRST VALUE *PF-KEY
      VALUE 'PF3'
        SET WINDOW OFF
        ESCAPE ROUTINE
      VALUE 'PF8', 'PF7'
        PERFORM NEXT-SCREEN
        ESCAPE TOP
      NONE
        IGNORE
    END-DECIDE
*
    EXAMINE #DISPLAY-SELECT (*) FOR 'X' GIVING INDEX #I
    IF #I = 0
      #I := *CURS-LINE
      #I := #I - 6
    END-IF
*
    IF #I LT 1 OR #I GT #SCR-IND
      REINPUT 'Please select a file to process'
    END-IF
*
    #SELECT-FILE-NAME      := #DISPLAY-TRUE-FILENAME (#I)
    #SELECT-FILE-NAME-ONLY := #DISPLAY-FILENAME (#I)
    ESCAPE BOTTOM
  END-REPEAT
SET WINDOW OFF
*
***********************************************************************
DEFINE SUBROUTINE SET-KEYS
***********************************************************************
*
SET KEY PF3   NAMED 'Retrn'
SET KEY PF7   NAMED 'Up'
SET KEY PF8   NAMED 'Down'
*
END-SUBROUTINE
*
*
***********************************************************************
 DEFINE SUBROUTINE NEXT-SCREEN
***********************************************************************
*
IF *PF-KEY = 'PF8'
  IF #SCREEN LT #SCREEN-COUNT
    #SCREEN := #SCREEN + 1
  END-IF
*
  IF #SCREEN = #SCREEN-COUNT
    #TEXT   := 'Last Screen'
  END-IF
ELSE
  IF #SCREEN GT 1
    #SCREEN := #SCREEN - 1
  END-IF
*
  IF #SCREEN = 1
    #TEXT   := 'First Screen'
  END-IF
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CREATE-FILE-LIST
************************************************************************
*
RESET #SHCMD
COMPRESS 'rm' #DISPLAY-FOLDER-FILE-NAME '>/dev/null' INTO #SHCMD
CALL 'SHCMD' #SHCMD
*
RESET #SHCMD
COMPRESS '>' #DISPLAY-FOLDER-FILE-NAME INTO #SHCMD LEAVING NO
COMPRESS 'ls' #SELECT-FILE-FOLDER #SHCMD INTO #SHCMD
CALL 'SHCMD' #SHCMD
*
DEFINE WORK FILE 1 #DISPLAY-FOLDER-FILE-NAME
*
END-SUBROUTINE
*
END
