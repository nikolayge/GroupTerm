* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GPRNNMT2    *** No Medical Terms Rates ***
/**
/** :author nguentchev
/*
* Your initial premium is based on your age at issue; premiums increase as you enter each new
* five-year age band.  Premiums are not guaranteed.  However, your rates will only change if
* they are changed for all others in the same class of insureds under this group policy.
* A group of insureds with the same issue age and gender is an example of a class.
* The issue age is in interval 45-74. Face amounts are 10000,20000,30000,40000,50000.
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* NGG   20130325   NG1   Change EZ Term rates                          *
*                                                                      *
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
LOCAL USING GRTL0001
LOCAL
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
1 LTII-V VIEW OF A-PRODUCT-RATES
  2 VAR-TERM-RATE (N3.2/10)
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 LTII-TYPE (A2)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 RIDER-OPTION (A1)
  2 LTII-KEY     (A14)
1 J      (I2)
1 K      (I2)
END-DEFINE
*
RESET J
MOVE ALL '-' TO F-A-DASH (*)
MOVE ALL '-' TO M-A-DASH (*)
##GENDER    := 'F'
##LTII-TYPE := 'EZ'   /* CURRENT KEY=XCRX99
##NICOTINE  := 'X'
##AGE       := 45
##EFFECTIVE-DATE := 20121101
READ LTII-V BY LTII-KEY STARTING FROM #LTII-KEY
  IF LTII-TYPE NE ##LTII-TYPE
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
    ESCAPE TOP
  END-IF
  ADD 1 TO J
  FOR K 1 7                                                         /* NG1
    MOVE EDITED VAR-TERM-RATE (K) (EM=ZZ9.99) TO FEMALE-NMT-A (J,K)
  END-FOR
END-READ
RESET J
##GENDER    := 'M'
READ LTII-V BY LTII-KEY STARTING FROM #LTII-KEY
  IF LTII-TYPE NE ##LTII-TYPE
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
    ESCAPE TOP
  END-IF
  ADD 1 TO J
  FOR K 1 7                                                        /* NG1
    MOVE EDITED VAR-TERM-RATE (K) (EM=ZZ9.99) TO MALE-NMT-A (J,K)
  END-FOR
END-READ
INPUT USING MAP 'GPRMNMT1'
*
END
