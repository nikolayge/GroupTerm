* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2232SCB       PROCESSES ST/FIRS TRANS REGISTER
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* RSE  03/26/10      Creation for CR222
* RSE  08/12/10 RE1  Add Role Code 'O' for CAO - Casualty Assistance Officer
* RSE  10/25/10 RE2  Add Role Codes 'H' (Grandparent) and 'Z' (Business Partner)
* RSE  11/24/10 RE3  Add SSN Protection
* RSE  12/08/10 RE4  Add DEATH-CERT-CODE
* RSE  12/21/10 RE5  Add Role Codes 'U' (Unknown) and 'W' (Funeral)
* TMT  07/15/14 TT1  Contact Normalization - DOB and Sex
*******************************************
DEFINE DATA
PARAMETER                       /* CANNOT EXCEED 40 ADDRESSES
*
1 #CONT-ISN              (P8)
1 #ST-ISN                (P8)
1 #FIRS-ISN              (P8)
1 #ITR1                  (I2)
1 #ITR2                  (I2)
*
1 #ID-NUMBER             (N6)
1 #PROCESS-IND           (A1)   /* PM - 06/2000 - SPLIT COMPANIES
1 #ST-NAME               (A25)
1 #LAST-FM-DATE          (N8)
1 #CGA-CLERK             (A3)
1 #HOLD-TIME             (P7)
*
1 #ORIG-ROLE             (A1)
1 #ROLE                  (A1)
1 #FIRST-NAME            (A25)
1 #MIDDLE-NAME           (A25)
1 #LAST-NAME             (A25)
1 #SUFFIX                (A10)
1 #DEPN-CONTACT-ID       (N8)
1 #DATE-OF-BIRTH         (N8)
1 REDEFINE #DATE-OF-BIRTH
  2 #DATE-OF-BIRTH-A      (A8)
1 #DATE-OF-MARRIAGE      (N8)
1 REDEFINE #DATE-OF-MARRIAGE
  2 #DATE-OF-MARRIAGE-A   (A8)
1 #DATE-OF-DIVORCE       (N8)
1 REDEFINE #DATE-OF-DIVORCE
  2 #DATE-OF-DIVORCE-A    (A8)
1 #SSN                   (N9)
1 REDEFINE #SSN
  2 #SSN-A                (A9)
1 #SEX                   (A1)
1 #BCC                   (A1)
1 #DCC                   (A1)  /* RE4
1 #DOD                   (N8)
1 #ORG-NAME              (A75)
1 #SSN-PROTECT           (A1)  /* RE3
*
LOCAL USING GONERLXX
LOCAL USING C2231LCB
LOCAL USING CONTPDA1      /* TT1
LOCAL
1 FR-V VIEW OF A-FIRS
  2 DEPN-CONTACT-ID    (99)
  2 DEPN-NAME          (99)
  2 DEPN-DATE-OF-BIRTH (99)
  2 REDEFINE DEPN-DATE-OF-BIRTH
    3 DEPN-DATE-OF-BIRTH-OCC  (99)
      4 DEPN-DATE-OF-BIRTH-A   (A8)
  2 DEPN-SSN           (99)
*
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID                    /* TT1
  2 NAME-SP
*  2 DATE-OF-BIRTH-SP
*  2 REDEFINE DATE-OF-BIRTH-SP
*   3 DATE-OF-BIRTH-SP-A (A8)
  2 DATE-OF-MARRIAGE
  2 REDEFINE DATE-OF-MARRIAGE
    3 DATE-OF-MARRIAGE-A   (A8)
  2 DATE-OF-DIVORCE
  2 REDEFINE DATE-OF-DIVORCE
    3 DATE-OF-DIVORCE-A   (A8)
  2 SSN-SP
  2 REDEFINE SSN-SP
    3 SSN-SP-A   (A9)
  2 FORMER-SPOUSE-NAME
  2 REDEFINE FORMER-SPOUSE-NAME
    3 FORMER-SPOUSE-NAME-A   (A8)
*  2 FORMER-SPOUSE-DT-OF-BIRTH
*  2 REDEFINE FORMER-SPOUSE-DT-OF-BIRTH
*   3 FORMER-SPOUSE-DT-OF-BIRTH-A (A8)
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
  2 REDEFINE FORMER-SPOUSE-DATE-OF-MARRIAGE
    3 FORMER-SPOUSE-DATE-OF-MARRIAGE-A (A8)
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 REDEFINE FORMER-SPOUSE-DATE-OF-DIVORCE
    3 FORMER-SPOUSE-DATE-OF-DIVORCE-A (A8)
  2 FORMER-SPOUSE-SSN
  2 REDEFINE FORMER-SPOUSE-SSN
    3 FORMER-SPOUSE-SSN-A   (A9)
*
1 CONT-V VIEW OF A-CONTACTS
  2 CONTACT-ID       (N8)           /* D
  2 ID-NUMBER        (N6)           /* D
  2 FIRST-NAME       (A25)
  2 MIDDLE-NAME      (A25)
  2 LAST-NAME        (A25)
  2 SUFFIX           (A10)
  2 DATE-OF-BIRTH    (N8)
  2 DATE-OF-DEATH    (N8)
  2 BIRTH-CERT-CODE  (A1)
  2 DEATH-CERT-CODE  (A1)           /* RE4
  2 GENDER-CD        (A1)
  2 SSN              (N9)           /* D
  2 SSN-PROTECT      (A1)
  2 FIRM-ORGANIZATION-NAME (A75)
  2 CONTACT-REMARKS  (A75/1)
  2 REDEFINE CONTACT-REMARKS
    3 #OLD-DESC      (A20/1)
*
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #SFX        (I2)   /* DW3
1 #DEP-CHG    (L)
1 #FLD-X      (I2)   /* DW3
1 #DEP-SSN  (N9)
1 REDEFINE #DEP-SSN
  2 #DEP-SSN-A   (A9)
1 #NAME      (A25)
1 #NAME-2    (A25)
1 #FIRST     (A20)
1 #MI        (A1)
1 #LAST      (A25)
1 #ERR-MSG   (A60)
*
1 #ST-DATE-OF-BIRTH                (N8.0)            /* TT1 BEGIN
1 #ST-DATE-OF-BIRTH-SP             (N8.0)
1 REDEFINE #ST-DATE-OF-BIRTH-SP
  2 #ST-DATE-OF-BIRTH-SP-A         (A8)
1 #ST-DATE-OF-BIRTH-FORMER         (N8.0)
1 REDEFINE #ST-DATE-OF-BIRTH-FORMER
  2 #ST-DATE-OF-BIRTH-FORMER-A     (A8)
1 #ST-GENDER-SPOUSE                (A1)              /* TT1 END

END-DEFINE
* * * * * * * * * *
* INCLUDE GONERCXX
*
* * * * * * * GET VIEW TO UPDATE AND CHECK CONCURRENT UPDATING * * *
*
IF #CONT-ISN NE 0
  GET CONT-V #CONT-ISN
END-IF
*
IF #ST-ISN NE 0
  GET ST-V #ST-ISN
END-IF
*
IF #FIRS-ISN NE 0
  GET FR-V #FIRS-ISN
END-IF
*
* * * * * * * * * * TRANS REG PROCESSING * * * * * * * *
*
PERFORM INIT-TRANS-REG
RESET #SFX                     /* DW3
MOVE 29 TO #FLD-X   /* DEP FLD NUM's are 30 thru 43 (14 deps)
*
**write '='#FIRST-NAME 'fn'CONT-V.FIRST-NAME '='#MIDDLE-NAME 'mn'CONT-V.MIDDLE-NAME
**   '='#LAST-NAME 'ln'CONT-V.LAST-NAME '='#SUFFIX 'sf'CONT-V.SUFFIX '='#ROLE
#NAME-2 := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
ADD 1 TO #ITR2
DECIDE ON FIRST VALUE OF #ROLE
  VALUE 'S'
    MOVE 'SPS-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
  VALUE 'X'
    MOVE 'FSP-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
  VALUE 'K', 'G', 'D'
    MOVE 'DEP-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
  VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'             /* RE1    RE2  RE5
    MOVE 'OTH-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
  VALUE 'M'
    MOVE 'MEM-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
  NONE
    IGNORE
END-DECIDE
PERFORM CHECK-FULL-TRAILER
MOVE #NAME-2      TO OLD-DATA-DATA (#ITR1, #ITR2) NEW-DATA-DATA  (#ITR1, #ITR2)
*
IF #ORIG-ROLE NE #ROLE
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'SPS-ROL' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE #ORIG-ROLE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #ROLE        TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'FSP-ROL' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE #ORIG-ROLE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #ROLE        TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D','A','B','E','F','I','P','R','T','O','H','Z','U','W'   /* RE1 RE2 RE5
      FOR #I1 1 14
*    display 'dob' '='#I1 '='#DEPN-CONTACT-ID '='DEPN-CONTACT-ID(#I1) '='#SFX '='#FLD-X
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          IF #ORIG-ROLE = 'K' OR = 'G' OR = 'D'
            MOVE 'DEP-ROL' TO OLD-DATA-LABEL (#ITR1, #ITR2)
              NEW-DATA-LABEL (#ITR1, #ITR2)
          ELSE
            MOVE 'OTH-ROL' TO OLD-DATA-LABEL (#ITR1, #ITR2)
              NEW-DATA-LABEL (#ITR1, #ITR2)
          END-IF
*       display #I1 #DEPN-CONTACT-ID DEPN-CONTACT-ID(#I1) #SFX #FLD-X
          MOVE #ORIG-ROLE TO OLD-DATA-DATA (#ITR1, #ITR2)
          MOVE #ROLE      TO NEW-DATA-DATA(#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF #FIRST-NAME NE CONT-V.FIRST-NAME OR #MIDDLE-NAME NE CONT-V.MIDDLE-NAME OR
    #LAST-NAME NE CONT-V.LAST-NAME OR #SUFFIX NE CONT-V.SUFFIX
*   OR #OLD-DESC(1) = 'OLD FIRST-NAME' AND
  ADD 1 TO #ITR2
  IF #FIRST-NAME = ' ' AND #MIDDLE-NAME = ' ' AND #LAST-NAME = ' ' AND #SUFFIX = ' '
    RESET #NAME
  ELSE
    MOVE #FIRST-NAME TO #FIRST
    MOVE #MIDDLE-NAME TO #MI
    MOVE #LAST-NAME TO #LAST
    CALLNAT 'GFMLNSXX' #NAME #FIRST #MI #LAST #ERR-MSG
  END-IF
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'SPS-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 22 TO SYNC-FLD-NUM(#SFX)
      MOVE NAME-SP   TO OLD-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-OLD-DATA(#SFX)
      MOVE #NAME     TO NEW-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'X'
      MOVE 'FSP-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 53 TO SYNC-FLD-NUM(#SFX)
      MOVE FORMER-SPOUSE-NAME   TO OLD-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-OLD-DATA(#SFX)
      MOVE #NAME  TO NEW-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-NAME (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          ADD 1 TO #SFX
          COMPUTE SYNC-FLD-NUM(#SFX) = #FLD-X + #I1           /* DW3
          MOVE DEPN-NAME (#I1) TO OLD-DATA-DATA  (#ITR1, #ITR2)
            SYNC-FLD-OLD-DATA (#SFX)   /* DW3
          MOVE #NAME     TO NEW-DATA-DATA  (#ITR1, #ITR2)
            SYNC-FLD-NEW-DATA (#SFX)   /* DW3
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1  RE2  RE5
      MOVE CONT-V.FIRST-NAME TO #FIRST
      MOVE CONT-V.MIDDLE-NAME TO #MI
      MOVE CONT-V.LAST-NAME TO #LAST
      CALLNAT 'GFMLNSXX' #NAME-2 #FIRST #MI #LAST #ERR-MSG
      MOVE 'OTH-NAME' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE #NAME-2    TO OLD-DATA-DATA (#ITR1, #ITR2)
      MOVE #NAME      TO NEW-DATA-DATA(#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF #DATE-OF-BIRTH NE CONT-V.DATE-OF-BIRTH
  ADD 1 TO #ITR2
*
  RESET PDA-CONTACT #ST-DATE-OF-BIRTH-SP #ST-GENDER-SPOUSE #ST-DATE-OF-BIRTH-FORMER #ST-DATE-OF-BIRTH   /* TT1 BEGIN
  P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'CONTS001' PDA-CONTACT #ST-DATE-OF-BIRTH-SP #ST-GENDER-SPOUSE #ST-DATE-OF-BIRTH-FORMER
     #ST-DATE-OF-BIRTH := P-DATE-OF-BIRTH                                                               /* TT1 END

  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'DOB-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 23 TO SYNC-FLD-NUM(#SFX)
       MOVE #ST-DATE-OF-BIRTH-SP-A   TO OLD-DATA-DATA  (#ITR1, #ITR2)              /* TT1
*                                    SYNC-FLD-OLD-DATA(#SFX)
      MOVE #DATE-OF-BIRTH-A  TO NEW-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'X'
      MOVE 'DOB-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 54 TO SYNC-FLD-NUM(#SFX)
       MOVE #ST-DATE-OF-BIRTH-FORMER-A TO OLD-DATA-DATA  (#ITR1, #ITR2)            /* TT1
*                                           SYNC-FLD-OLD-DATA(#SFX)
      MOVE #DATE-OF-BIRTH-A  TO NEW-DATA-DATA  (#ITR1, #ITR2)
        SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*    display 'dob' '='#I1 '='#DEPN-CONTACT-ID '='DEPN-CONTACT-ID(#I1) '='#SFX '='#FLD-X
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-DOB' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
*       display #I1 #DEPN-CONTACT-ID DEPN-CONTACT-ID(#I1) #SFX #FLD-X
          IF NOT #DEP-CHG
            MOVE TRUE TO #DEP-CHG
            ADD 1 TO #SFX
          END-IF
          COMPUTE SYNC-FLD-NUM(#SFX) = #FLD-X + #I1           /* DW3
          COMPRESS SYNC-FLD-OLD-DATA(#SFX) 'DOB=' DEPN-DATE-OF-BIRTH-A (#I1)
            INTO SYNC-FLD-OLD-DATA (#SFX)
          COMPRESS SYNC-FLD-NEW-DATA (#SFX) 'DOB=' #DATE-OF-BIRTH-A
            INTO SYNC-FLD-NEW-DATA (#SFX)
          MOVE DEPN-DATE-OF-BIRTH (#I1) TO OLD-DATA-DATA (#ITR1, #ITR2)
          MOVE #DATE-OF-BIRTH TO NEW-DATA-DATA(#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1        RE2 RE5
      MOVE 'DOB-OTH' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE CONT-V.DATE-OF-BIRTH TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DATE-OF-BIRTH       TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF (#ROLE = 'S' AND DATE-OF-MARRIAGE NE #DATE-OF-MARRIAGE)
    OR (#ROLE = 'X' AND FORMER-SPOUSE-DATE-OF-MARRIAGE NE #DATE-OF-MARRIAGE)
  ADD 1 TO #ITR2
  ADD 1 TO #SFX    /* DW3
  IF #ROLE = 'S'
    MOVE 'DOM' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
    MOVE 26 TO SYNC-FLD-NUM(#SFX)
    MOVE DATE-OF-MARRIAGE-A TO OLD-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-OLD-DATA(#SFX)
    MOVE #DATE-OF-MARRIAGE-A TO NEW-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-NEW-DATA(#SFX)
  ELSE
    MOVE 'FSP-DOM' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
    MOVE 57 TO SYNC-FLD-NUM(#SFX)
    MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO OLD-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-OLD-DATA(#SFX)
    MOVE #DATE-OF-MARRIAGE-A TO NEW-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-NEW-DATA(#SFX)
  END-IF
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF (#ROLE = 'S' AND DATE-OF-DIVORCE NE #DATE-OF-DIVORCE) OR
    (#ROLE = 'X' AND FORMER-SPOUSE-DATE-OF-DIVORCE NE #DATE-OF-DIVORCE)
  ADD 1 TO #ITR2
  ADD 1 TO #SFX    /* DW3
  IF #ROLE = 'S'
    MOVE 'DIV/DT' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
    MOVE 27 TO SYNC-FLD-NUM(#SFX)
    MOVE DATE-OF-DIVORCE-A TO OLD-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-OLD-DATA(#SFX)
    MOVE #DATE-OF-DIVORCE-A TO NEW-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-NEW-DATA(#SFX)
  ELSE
    MOVE 'FSP-DIV' TO OLD-DATA-LABEL (#ITR1, #ITR2)
      NEW-DATA-LABEL (#ITR1, #ITR2)
    MOVE 58 TO SYNC-FLD-NUM(#SFX)
    MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO OLD-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-OLD-DATA(#SFX)
    MOVE #DATE-OF-DIVORCE-A TO NEW-DATA-DATA  (#ITR1, #ITR2)
      SYNC-FLD-NEW-DATA(#SFX)
  END-IF
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF CONT-V.SSN NE #SSN
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'SSN-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 24 TO SYNC-FLD-NUM(#SFX)
      MOVE SSN-SP   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE SSN-SP-A TO SYNC-FLD-OLD-DATA(#SFX)
      MOVE #SSN    TO NEW-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN-A  TO SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'X'
      MOVE 'SSN-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      ADD 1 TO #SFX    /* DW3
      MOVE 55 TO SYNC-FLD-NUM(#SFX)
      MOVE FORMER-SPOUSE-SSN   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE FORMER-SPOUSE-SSN-A  TO SYNC-FLD-OLD-DATA(#SFX)
      MOVE #SSN  TO NEW-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN-A  TO SYNC-FLD-NEW-DATA(#SFX)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-SSN' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          IF NOT #DEP-CHG
            MOVE TRUE TO #DEP-CHG
            ADD 1 TO #SFX
          END-IF
          COMPUTE SYNC-FLD-NUM(#SFX) = #FLD-X + #I1           /* DW3
          MOVE DEPN-SSN (#I1) TO #DEP-SSN
          COMPRESS SYNC-FLD-OLD-DATA(#SFX) 'SSN=' #DEP-SSN-A
            INTO SYNC-FLD-OLD-DATA (#SFX)
          MOVE #SSN TO #DEP-SSN
          COMPRESS SYNC-FLD-NEW-DATA (#SFX) 'SSN=' #DEP-SSN-A
            INTO SYNC-FLD-NEW-DATA (#SFX)
          MOVE DEPN-SSN (#I1) TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #SSN TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','M','P','R','T','O','H','Z','U','W'     /* RE1  RE2  RE5
      MOVE 'SSN-OTH' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE CONT-V.SSN   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN         TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF CONT-V.GENDER-CD NE #SEX
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'SEX-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE GENDER-CD   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SEX    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'SEX-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE GENDER-CD   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SEX  TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-SEX' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          MOVE GENDER-CD  TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #SEX TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1  RE2 RE5
      MOVE 'SEX-OTH' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE GENDER-CD   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SEX        TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF CONT-V.BIRTH-CERT-CODE NE #BCC
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'BCC-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE BIRTH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #BCC    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'BCC-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE BIRTH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #BCC  TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-BCC' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          MOVE BIRTH-CERT-CODE  TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #BCC TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1   RE2 RE5
      MOVE 'OTH-BCC' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE BIRTH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #BCC              TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF CONT-V.DEATH-CERT-CODE NE #DCC                         /* RE4 START
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'DCC-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DEATH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DCC    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'DCC-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DEATH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DCC  TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-DCC' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          MOVE DEATH-CERT-CODE  TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #DCC TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1   RE2 RE5
      MOVE 'OTH-DCC' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DEATH-CERT-CODE   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DCC              TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF                                                    /* RE4 END
*
IF CONT-V.DATE-OF-DEATH NE #DOD
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'DOD-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DATE-OF-DEATH   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DOD    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'DOD-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DATE-OF-DEATH   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DOD  TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-DOD' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          MOVE DATE-OF-DEATH  TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #DOD TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1   RE2 RE5
      MOVE 'DOD-OTH' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE DATE-OF-DEATH     TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #DOD              TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF
*
IF CONT-V.SSN-PROTECT NE #SSN-PROTECT                       /* RE3
  ADD 1 TO #ITR2
  DECIDE ON FIRST VALUE OF #ROLE
    VALUE 'S'
      MOVE 'SSPR-SP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE SSN-PROTECT   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN-PROTECT    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'X'
      MOVE 'SSPR-FSP' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE SSN-PROTECT   TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN-PROTECT  TO NEW-DATA-DATA  (#ITR1, #ITR2)
    VALUE 'K', 'G', 'D'
      FOR #I1 1 14
*      WRITE 'ssn' '='#I1 '='DEPN-CONTACT-ID(#I1) '='#DEPN-CONTACT-ID '='DEPN-SSN (#I1)
        IF DEPN-CONTACT-ID(#I1) = 0 OR            /* Add a new dependent
            #DEPN-CONTACT-ID = DEPN-CONTACT-ID(#I1) /* Update dependent
          MOVE 'DEP-SSPR' TO OLD-DATA-LABEL (#ITR1, #ITR2)
            NEW-DATA-LABEL (#ITR1, #ITR2)
          MOVE SSN-PROTECT  TO OLD-DATA-DATA  (#ITR1, #ITR2)
          MOVE #SSN-PROTECT TO NEW-DATA-DATA  (#ITR1, #ITR2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W'     /* RE1   RE2 RE5
      MOVE 'SSPR-OTH' TO OLD-DATA-LABEL (#ITR1, #ITR2)
        NEW-DATA-LABEL (#ITR1, #ITR2)
      MOVE SSN-PROTECT     TO OLD-DATA-DATA  (#ITR1, #ITR2)
      MOVE #SSN-PROTECT    TO NEW-DATA-DATA  (#ITR1, #ITR2)
    NONE
      IGNORE
  END-DECIDE
  PERFORM CHECK-FULL-TRAILER
END-IF                                                     /* RE3
*
**display #ROLE FIRM-ORGANIZATION-NAME(AL=20) #ORG-NAME(AL=20)
IF (#ROLE = 'A' OR = 'B' OR = 'E' OR = 'F' OR = 'I' OR = 'P' OR = 'R' OR = 'T' OR = 'O'   /* RE1
    OR = 'H' OR = 'Z' OR = 'U' OR = 'W') AND                                              /* RE2 RE5
    FIRM-ORGANIZATION-NAME NE #ORG-NAME
  ADD 1 TO #ITR2
  MOVE 'OTH-ORG' TO OLD-DATA-LABEL (#ITR1, #ITR2)
    NEW-DATA-LABEL (#ITR1, #ITR2)
  IF #CONT-ISN = 0
    MOVE FIRM-ORGANIZATION-NAME   TO OLD-DATA-DATA  (#ITR1, #ITR2)
    MOVE #ORG-NAME                TO NEW-DATA-DATA  (#ITR1, #ITR2)
  ELSE
    MOVE #ORG-NAME                TO OLD-DATA-DATA  (#ITR1, #ITR2)
    MOVE FIRM-ORGANIZATION-NAME   TO NEW-DATA-DATA  (#ITR1, #ITR2)
  END-IF
  PERFORM CHECK-FULL-TRAILER
END-IF
*
* * * * * * * * WRITE OUT TRANS REG * * * * * * * *
*
MOVE 'CB-' TO TR-REG-V.NEW-DATA-DC (1,1)
STORE TR-REG-V
**END TRANSACTION
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-FULL-TRAILER
* * * * * * * * * * * * * * * * * * *
*
IF #ITR2 = 5
  IF #ITR1 = 10
    MOVE 'CB' TO TR-REG-V.NEW-DATA-DC (1,1)
    STORE TR-REG-V    /* 10 ALREADY - WRITE IT OUT
    PERFORM INIT-TRANS-REG           /* INITIALIZE TRANS REGISTER
  ELSE
    ADD  1 TO #ITR1
    MOVE 0 TO #ITR2
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE INIT-TRANS-REG
* * * * * * * * * * * * * * * * *
*
RESET TR-REG-V
MOVE #ID-NUMBER    TO TR-REG-V.ID-NUMBER
MOVE #PROCESS-IND  TO TR-REG-V.PROCESS-IND /* PM - 06/2000 - SPLIT COMP
MOVE #ST-NAME         TO TR-REG-V.NAME
MOVE #LAST-FM-DATE TO TR-REG-V.DATE-LAST-UPDATE
MOVE *INIT-ID          TO TR-REG-V.TERMINAL-ID
MOVE #CGA-CLERK        TO TR-REG-V.CLERK-ID
MOVE *DATN             TO TR-REG-V.TRANSACTION-DATN
MOVE #HOLD-TIME        TO TR-REG-V.TRANSACTION-TIMN
MOVE 1 TO #ITR1
MOVE 0 TO #ITR2
*
END-SUBROUTINE
*
END
