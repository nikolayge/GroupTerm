* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPM82A - PUT ID NUMBER ON AUTO ALLOTMENT RECORDS
*
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* RSE  09/29/2009 RE1  Change PROCESS-IND logic for Unification
************************************************************************
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 PROCESS-IND
  2 SSN            (N9)
  2 NAME           (A25)
* * * *   A L L O T M E N T   F I L E * * * * * * * * * * * * *
1 #ALLOTMENT-FILE
  2 #AR-ID-NUMBER   (N6)
  2 #AR-SERV        (A1)
  2 #AR-SSN         (N9)
  2 #AR-ST-SP       (A1)
  2 #AR-REAS        (A2)
  2 #AR-ACT-RET     (A1)
  2 #AR-BLNK-CODE   (A3)
  2 #AR-EFF-DT      (A4)
  2 #AR-STP-DT      (A4)
  2 #AR-AMT         (P8.2)
  2 #AR-NAME        (A25)
  2 #AR-FILLER      (A38)
*
1 #COUNTERS
  2 #FOUND          (P5)
  2 #NOT-FOUND      (P5)
  2 #RECS-IN        (P5)
  2 #RECS-OUT       (P5)
*
1 #END-OF-FILE    (L)
1 #WIDOW-SSN-KEY  (A10)
1 #FND-ST         (L)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
PERFORM READ-REC
*
REPEAT UNTIL #END-OF-FILE
*
  RESET #FND-ST
  FIND ST-V WITH SSN = #AR-SSN
*    IF ST-V.PROCESS-IND = 'S'                   /* RE1
    IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)        /* RE1   
      ESCAPE TOP
    END-IF
    MOVE TRUE TO #FND-ST
    ESCAPE BOTTOM
  END-FIND
*
  IF NOT #FND-ST
    MOVE #AR-SSN TO #WIDOW-SSN-KEY
    FIND ST-V WITH SSN-KEY-SP = #WIDOW-SSN-KEY
*      IF ST-V.PROCESS-IND = 'S'                    RE1
      IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)      /* RE1   
        ESCAPE TOP
      END-IF
      MOVE TRUE TO #FND-ST
      ESCAPE BOTTOM
    END-FIND
  END-IF
*
  IF NOT #FND-ST
    RESET ST-V
    ADD 1 TO #NOT-FOUND
  ELSE
    MOVE ID-NUMBER TO #AR-ID-NUMBER
    MOVE NAME      TO #AR-NAME
    ADD 1 TO #FOUND
  END-IF
*
  WRITE WORK 2 #ALLOTMENT-FILE
  ADD 1 TO #RECS-OUT
  PERFORM READ-REC
*
END-REPEAT
*
WRITE 'TOTAL SSN FOUND     = ' #FOUND
WRITE 'TOTAL SSN NOT FOUND = ' #NOT-FOUND
WRITE 'TOTAL RECS READ     = ' #RECS-IN
WRITE 'TOTAL RECS WRITTEN  = ' #RECS-OUT
*
DEFINE SUBROUTINE READ-REC
*
READ WORK 1 ONCE RECORD #ALLOTMENT-FILE
AT END
  MOVE TRUE TO #END-OF-FILE
  ESCAPE ROUTINE
END-ENDFILE
ADD 1 TO #RECS-IN
*
END-SUBROUTINE /* (0100)
*
END
