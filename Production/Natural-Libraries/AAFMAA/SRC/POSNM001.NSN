* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Find linked Member
DEFINE DATA
PARAMETER
1 #ORIGINAL-CN         (N6)
1 #REFERENCE-NUMBER    (N6)
1 #FOUND-LINKED-MEM    (L)
1 #MAX-CALLS           (I2)
LOCAL
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 PROCESS-IND
  2 INTRO-FLAG
  2 LINKED-CN    (15)
*
1 #MEMBERSHIP-EXCEPTIONS (A)  DYNAMIC CONSTANT <'/usr/SAG/tmp/Membership_Exceptions.txt'>
1 #TAB                   (A1) CONSTANT         <H'09'>
1 #ERROR-1               (A)  DYNAMIC CONSTANT <'No Linked members stored in status file'>
1 #ERROR-2               (A)  DYNAMIC CONSTANT <'Linked person is not a member.  Endless loop'>
1 #OUTPUT-A              (A250)
END-DEFINE
*
DEFINE WORK FILE 3 #MEMBERSHIP-EXCEPTIONS TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
FIND(1) ST-V WITH ID-NUMBER = #REFERENCE-NUMBER
  IF NO RECORDS FOUND
    RESET #OUTPUT-A
    COMPRESS #ORIGINAL-CN #ERROR-1 INTO #OUTPUT-A WITH DELIMITER #TAB
    WRITE WORK FILE 3 #OUTPUT-A
    ESCAPE ROUTINE
  END-NOREC  
  IF (PROCESS-IND ='C')
      OR (INTRO-FLAG = 'A' OR = 'S' OR ='K' OR = 'G')  
    ADD 1 TO #MAX-CALLS
    IF #MAX-CALLS >= 8 /* ENDLESS LOOP.  Usually means 2 non-members are linked to each other
      RESET #MAX-CALLS #OUTPUT-A
      COMPRESS #REFERENCE-NUMBER #ERROR-2 INTO #OUTPUT-A WITH DELIMITER #TAB
      WRITE WORK FILE 3 #OUTPUT-A
      ESCAPE ROUTINE
    END-IF   
    #REFERENCE-NUMBER := LINKED-CN(1)
    CALLNAT 'POSNM001' #ORIGINAL-CN #REFERENCE-NUMBER #FOUND-LINKED-MEM #MAX-CALLS
  ELSE
    #FOUND-LINKED-MEM := TRUE
    ESCAPE ROUTINE
  END-IF
END-FIND
*
END
