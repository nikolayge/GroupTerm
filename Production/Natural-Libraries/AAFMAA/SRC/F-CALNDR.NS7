* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Function FUNCTION-CALENDAR
* Function returns Date in the requested format
* Available formats
* A - Determine bi-monthly run date
* B - Last day of the current month DD-Mon-YYYY
* C - Month YYYY
* D - MM/DD/YYYY format
* E - MM/YYYY format
* F - DD Month YYYY
* G - Next Day YYYYMMDD (same as *DATN format)
* H - Previous Day YYYYMMDD (same as *DATN format)
* I - 60 days ahead YYYYMMDD (same as *DATN format)
* J - 3 Month ahead YYYYMMDD
* K - 6 Month ahead YYYYMMDD
* L - One day back (used for loan capitalization note) DD-Mon-YYYY
* M - Month
* N - First day of the next month - YYYYMMDD
* O - One month forward - YYYYMMDD
* P - One month back - YYYYMMDD
* Q - Last Day of the previous month
* R - One month forward - Month
* S - DD-Mon-YYYY
* T - Two month forward - YYYYMMDD
* U - First day of the current month - YYYYMMDD
* V - Month DD, YYYY
* W - Date - DD-Mon-YYYY
* X - First business day of the month - DD-Mon-YYYY
* Y - Last business day of the month  - DD-Mon-YYYY
* Z - 45 days back - YYYYMMDD
* 1 - 60 days ahead DD-Mon-YYYY
* 2 - First business day of the next month - DD-Mon-YYYY
* 3 - Last day of the current month Month DD, YYYY
* 4 - Last day of the current month YYYYMMDD
* 5 - Last day of the next month - YYYYMMDD
* 6 - Date in format DDMMMYY
* 7 - Date in format MM/DD/YY
* 8 - Last business day of the month  - YYYYMMDD
* 9 - First day of the month - DD-Mon-YYYY
* 0 - Last day of the month  - DD-Mon-YYYY
*************************************************************
*        MODIFICATION LOG
*************************************************************
* USER   DATE      TAG  REASON
* YAK    06232008  Initial Creation
* YK1    07012009  Add value 'B' for ACH process
* YK2    08122009  Add value 'D' for Online Application
* YK3    04012015  Add new functions for MC2
* YK4    08012016  Add new functions for DV upload
* YK5    09012017  Add new option for Bill Cycle run
* YK5    11202017  Add new options for conversion letter
* YF1    02022018  Fixed date compression for month end
*************************************************************
DEFINE FUNCTION FUNCTION-CALENDAR RETURNS (A20)
  DEFINE DATA
  PARAMETER
  1 #DATE (N8)
  1 #FORMAT (A1)
  LOCAL
  1 #MONTH (A10/12) INIT
    <'January','February','March','April','May','June','July','August',
    'September','October','November','December'>
  1 #MONTH-3 (A3)
  1 #PMT-DAYS (N2/12) INIT <31,28,31,30,31,30,31,31,30,31,30,31>
  1 #DATEN (N8)
  1 REDEFINE #DATEN
    2 #YYYY (N4)
    2 REDEFINE #YYYY
      3 #CC (A2)
      3 #YY (A2)
    2 #MM   (N2)
    2 #DD   (N2)
  1 REDEFINE #DATEN
    2 #DATE-A (A8)
  1 #DATX   (D)
  1 #DAY    (A) DYNAMIC
  1 #SATURDAY    (A) DYNAMIC INIT <'Saturday'>
  1 #SUNDAY      (A) DYNAMIC INIT <'Sunday'>
  END-DEFINE
*
#DATEN := #DATE
DECIDE ON FIRST VALUE #FORMAT
  VALUE 'A'
    IF #DD = 1 THEN #DD := 15
    ELSE #DD := 1
      #MM := #MM + 1
      IF #MM > 12
        #MM := 1
        #YYYY := #YYYY + 1
      END-IF
    END-IF
    FUNCTION-CALENDAR := #DATE-A
  VALUE 'B'
    #DD := #PMT-DAYS(#MM)
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'C'
    COMPRESS #MONTH(#MM) #YYYY INTO FUNCTION-CALENDAR
  VALUE 'D'
    COMPRESS FULL #MM #DD #YYYY INTO FUNCTION-CALENDAR WITH '/'
  VALUE 'E'
    COMPRESS FULL #MM #YYYY INTO FUNCTION-CALENDAR WITH '/'
  VALUE 'F'
    COMPRESS #DD #MONTH(#MM) #YYYY INTO FUNCTION-CALENDAR
  VALUE 'G'
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    #DATX := #DATX + 1
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    FUNCTION-CALENDAR := #DATE-A
  VALUE 'H'
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    #DATX := #DATX - 1
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    FUNCTION-CALENDAR := #DATE-A
  VALUE 'I','1'
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    #DATX := #DATX + 60
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    IF #FORMAT = 'I'
      FUNCTION-CALENDAR := #DATE-A
    ELSE
      #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
      COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
    END-IF
  VALUE 'J'
    #MM := #MM + 6
    IF #MM > 12
      #MM := #MM - 12
      #YYYY := #YYYY + 1
    END-IF
    COMPRESS FULL #YYYY #MM INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'K'
    #MM := #MM + 3
    IF #MM > 12
      #MM := #MM - 12
      #YYYY := #YYYY + 1
    END-IF
    COMPRESS FULL #YYYY #MM INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'L'
    #DD := #DD - 1
    IF #DD = 0
      #MM := #MM - 1
      IF #MM = 0
        #MM := 12
        #YYYY := #YYYY - 1
      END-IF
      #DD := #PMT-DAYS(#MM)
    END-IF
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'M'
    FUNCTION-CALENDAR := #MONTH(#MM)
  VALUE 'N'
    #DD := 1
    #MM := #MM + 1
    IF #MM > 12
      #MM := 1
      #YYYY := #YYYY + 1
    END-IF
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'O'
    #MM := #MM + 1
    IF #MM > 12
      #MM := #MM - 12
      #YYYY := #YYYY + 1
    END-IF
    IF #DD > #PMT-DAYS(#MM)
      #DD := #PMT-DAYS(#MM)
    END-IF
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'P'
    #MM := #MM - 1
    IF #MM = 0
      #MM := 12
      #YYYY := #YYYY - 1
    END-IF
    IF #DD > #PMT-DAYS(#MM)
      #DD := #PMT-DAYS(#MM)
    END-IF
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'Q'
    #MM := #MM - 1
    IF #MM = 0
      #MM := 12
      #YYYY := #YYYY - 1
    END-IF
    #DD := #PMT-DAYS(#MM)
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'R'
    #MM := #MM + 1
    IF #MM > 12
      #MM := 1
    END-IF
    FUNCTION-CALENDAR := #MONTH(#MM)
  VALUE 'S'
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'T'
    #MM := #MM + 2
    IF #MM > 12
      #MM := #MM - 12
      #YYYY := #YYYY + 1
    END-IF
    IF #DD > #PMT-DAYS(#MM)
      #DD := #PMT-DAYS(#MM)
    END-IF
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'U'
    #DD := 1
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
  VALUE 'V'
    COMPRESS #MONTH(#MM) #DD INTO FUNCTION-CALENDAR
    COMPRESS FUNCTION-CALENDAR ',' INTO FUNCTION-CALENDAR LEAVING NO
    COMPRESS FUNCTION-CALENDAR #YYYY INTO FUNCTION-CALENDAR
  VALUE 'W'
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'X'
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    MOVE EDITED #DATX (EM=N(10)) TO #DAY
    DECIDE ON FIRST VALUE #DAY
      VALUE #SATURDAY
        #DATX := #DATX + 2
      VALUE #SUNDAY
        #DATX := #DATX + 1
      NONE VALUE
        IGNORE
    END-DECIDE
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'Y'
    #DD := #PMT-DAYS(#MM)
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    MOVE EDITED #DATX (EM=N(10)) TO #DAY
    DECIDE ON FIRST VALUE #DAY
      VALUE #SATURDAY
        #DATX := #DATX - 1
      VALUE #SUNDAY
        #DATX := #DATX - 2
      NONE VALUE
        IGNORE
    END-DECIDE
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE 'Z'
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    #DATX := #DATX - 50
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    FUNCTION-CALENDAR := #DATE-A
  VALUE '2'
    #DD := 1
    #MM := #MM + 1
    IF #MM > 12
      #MM := 1
      #YYYY := #YYYY + 1
    END-IF
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    MOVE EDITED #DATX (EM=N(10)) TO #DAY
    DECIDE ON FIRST VALUE #DAY
      VALUE #SATURDAY
        #DATX := #DATX + 2
      VALUE #SUNDAY
        #DATX := #DATX + 1
      NONE VALUE
        IGNORE
    END-DECIDE
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE '3', '4'
    #DD := #PMT-DAYS(#MM)
    IF #FORMAT = '3'
      COMPRESS #MONTH(#MM) #DD INTO FUNCTION-CALENDAR
      COMPRESS FUNCTION-CALENDAR ',' INTO FUNCTION-CALENDAR LEAVING NO
      COMPRESS FUNCTION-CALENDAR #YYYY INTO FUNCTION-CALENDAR
    ELSE
      COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
    END-IF
  VALUE '5'
    #MM := #MM + 1
    IF #MM > 12
      #MM := 1
      #YYYY := #YYYY + 1
    END-IF
    #DD := #PMT-DAYS(#MM)
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO
* YK4 >
  VALUE '6'
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YY INTO FUNCTION-CALENDAR LEAVING NO
  VALUE '7'
    COMPRESS #DD #MM #YY INTO FUNCTION-CALENDAR WITH '/'
* < YK4
* YK5 >
  VALUE '8'
    #DD := #PMT-DAYS(#MM)
    MOVE EDITED #DATE-A TO #DATX (EM=YYYYMMDD)
    MOVE EDITED #DATX (EM=N(10)) TO #DAY
    DECIDE ON FIRST VALUE #DAY
      VALUE #SATURDAY
        #DATX := #DATX - 1
      VALUE #SUNDAY
        #DATX := #DATX - 2
      NONE VALUE
        IGNORE
    END-DECIDE
    MOVE EDITED #DATX (EM=YYYYMMDD) TO #DATE-A
    COMPRESS FULL #YYYY #MM #DD INTO FUNCTION-CALENDAR LEAVING NO      /* YF1
* < YK5
* YK6 >
  VALUE '9'
    #DD := 1
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
  VALUE '0'
    IF #MM > 12
      #YYYY := #YYYY + 1
      #MM := #MM - 12
    END-IF  
    #MONTH-3 := SUBSTR(#MONTH(#MM),1,3)
    #DD := #PMT-DAYS(#MM)
    COMPRESS #DD #MONTH-3 #YYYY INTO FUNCTION-CALENDAR WITH '-'
* < YK6
  NONE VALUE
    IGNORE
END-DECIDE
*
END-FUNCTION
END
