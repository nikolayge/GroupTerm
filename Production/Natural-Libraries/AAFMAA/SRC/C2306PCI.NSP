* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2306PCI
*
*    CI SCREEN 'RPU' PROCESSING   OLD TRAN 606
*
***********************************************************************
**           MODIFICATION LOG
** WHO   DATE   TAG       REASON
** SAG 11012005 RH1  AB - Replace CALL to Cobol with CALLNAT to Natural
** TMT 12062013 TT1  1 MILLION DOLLAR STABILITY PROJECT
***********************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
LOCAL USING GC2V6LXX
LOCAL
*
1 #TR-REG-RPU-GR
 2 #TR-REG-RPU-LBL-1         (A08) INIT <'CI-RPU  '>
 2 #TR-REG-RPU-LBL-2         (A11) INIT <'ISSUE-DATE '>
 2 #TR-REG-RPU-ISSUE-DATE    (N08)
 2 #TR-REG-RPU-LBL-3         (A07) INIT <' PLAN '>
 2 #TR-REG-RPU-PLAN          (A02)
 2 #TR-REG-RPU-LBL-4         (A11) INIT <' FACE-AMT '>
 2 #TR-REG-RPU-FAC-AMT       (A10)
 2 #TR-REG-RPU-LBL-5         (A09) INIT <' NEW-DB '>
 2 #TR-REG-RPU-DB            (A10)
1 REDEFINE #TR-REG-RPU-GR
 2 #TR-REG-RPU               (A76)    /*TT1 INCREASED TO 76
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY  (N4)
 2 #MM    (N2)
 2 #DD    (N2)
*
1 #WORK-FREQ         (P2)
1 #DAILY-INT-RATE    (P0.7)
1 #TOT-LST-ANN-CV    (P5.2)
1 #WORK-MONTHS       (P3)
1 #WORK-AMT          (P5.2)
1 #CURR-DAYS         (P2)
*
1 #RPU-MSG-LINE-G
 2 #RPU-MSG-LABEL   (A19)
 2 #RPU-MSG-AMT     (A10)
1 REDEFINE #RPU-MSG-LINE-G
 2 #RPU-MSG-LINE    (A29)
*
1 #PASSED-INFO-G1
 2 #PRORATED-LOAN-INT-TABLE   (9)
  3 #PASSED-LOAN-PRIN   (P7.2)
1 REDEFINE #PASSED-INFO-G1
 2 #PASSED-INFO-1       (A45)    /* MUST BE KEPT UPDATED WITH ABOVE
*
1 #PASSED-INFO-G2
 2 #PRORATED-LOAN-INT-TABLE   (9)
  3 #PASSED-LOAN-INT    (P5.2)
 2 #TOT-RPU-LOAN-AMT    (P7.2)
 2 #TOT-RPU-INT-AMT     (P5.2)
1 REDEFINE #PASSED-INFO-G2
 2 #PASSED-INFO-2       (A45)    /* MUST BE KEPT UPDATED WITH ABOVE
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INPUT #PASSED-INFO-1     /* RECEIVE PASSED INFO FROM C2300PCI
      #PASSED-INFO-2     /* RECEIVE PASSED INFO FROM C2300PCI
*
MOVE TRUE TO #REC-CHGD              /* INDICATE RECORD CHANGED
*
MOVE 'A'               TO CI-V.ONLINE-ACTION (#IFL)   /* ONLINE ACTION
MOVE #TODAY-YYYYMMDD-N TO CI-V.PAID-UP-DATE  (#IFL)   /* PAID-UP DATE
*
IF CI-V.ONLINE-ACTION-GN = '1' OR = '2' OR = '3'     /* ALREADY FLAGGED
  IGNORE
ELSE
  MOVE  '4' TO CI-V.ONLINE-ACTION-GN     /* INDICATE ACTION THIS MONTH
END-IF
*
MOVE '45' TO CI-V.PAY-STATUS (#IFL)  /* PAY STATUS CHG MUST FOLLOW ABOVE
*
* ANY CHANGES TO AOCX2V6A LOGIC HERE MUST ALSO GO IN C2300PCI  <<<<<<<<
MOVE 606                      TO #L-CI-TRAN
MOVE #SCR-TRN-DT-YYYY (#ISC) TO #YYYY
MOVE #SCR-TRN-DT-MM (#ISC)    TO #MM
MOVE #TODAY-DD                TO #DD
MOVE #YYYYMMDD                TO #L-606-TRN-DATE
MOVE CI-V.ISSUE-DATE        (#IFL) TO #L-606-ISS-DT
MOVE CI-V.ISSUE-AGE-N       (#IFL) TO #L-606-ISS-AGE
MOVE CI-V.PLAN              (#IFL) TO #L-606-TYPE
MOVE #SCR-RPU-ACV           (#ISC) TO #L-606-ACV
MOVE CI-V.MIN-CASH-VALUE    (#IFL) TO #L-606-LST-MN-CV
MOVE CI-V.NXT-YR-MIN-CASH-VALUE (#IFL) TO #L-606-NXT-MN-CV
*
MOVE CI-V.ACC-CASH-VALUE (#IFL) TO #L-606-LN-INT
*
* IF  (CI-V.ISSUE-DATE-DD (#IFL) = 01)    /* LOGIC BELOW REMOVED 05/89
*  OR (CI-V.ISSUE-DATE-DD (#IFL) GT #TODAY-DD)    /* AS PER CROOKS EDDY
*   IF CI-V.CASH-VALUE (#IFL) GT CI-V.ACC-CASH-VALUE (#IFL)
*     MOVE CI-V.CASH-VALUE (#IFL) TO #L-606-LN-INT
*   ELSE
*     MOVE CI-V.ACC-CASH-VALUE (#IFL) TO #L-606-LN-INT
*   END-IF
* ELSE
*   IF CI-V.NXT-MON-CASH-VALUE (#IFL) GT CI-V.ACC-CASH-VALUE (#IFL)
*     MOVE CI-V.NXT-MON-CASH-VALUE (#IFL) TO #L-606-LN-INT
*   ELSE
*     MOVE CI-V.ACC-CASH-VALUE (#IFL) TO #L-606-LN-INT
*   END-IF
* END-IF
*
SUBTRACT #L-606-ACV FROM #L-606-LN-INT
* DISPLAY
*  'TRAN' #L-CI-TRAN
*  'TRN/DATE' #L-606-TRN-DATE
*  'ISS/DT'   #L-606-ISS-DT
*  'ISS/AGE'  #L-606-ISS-AGE
*  'LST/MCV'  #L-606-LST-MN-CV
*  'NXT/MCV'  #L-606-NXT-MN-CV
*  'ACV'      #L-606-ACV
*  'LN/INT'   #L-606-LN-INT
*  'RPU/ACV'  #RPU-ACV
*
RESET #L-606-NEW-DB
      #L-606-NEW-FAC-AMT
      #L-606-NEW-LST-MCV
      #L-606-NEW-NXT-MCV
      #L-606-NEW-CUR-MCV
**CALL 'AOCX2V6A' #L-CI-TRAN        /* SAG 11012005 RH1
  CALLNAT 'AOCX2V6A' #L-PARMS        /* SAG 11012005 RH1
*
FOR #I1 1 TO 9
  IF #SCR-TRAN (#I1) = 'RPU'
    ADD #PASSED-LOAN-PRIN (#I1) TO #TOT-RPU-LOAN-AMT
    ADD #PASSED-LOAN-INT  (#I1) TO #TOT-RPU-INT-AMT
  END-IF
END-FOR
*
* DISPLAY #ISC #PASSED-LOAN-PRIN (#ISC) #TOT-RPU-LOAN-AMT
MOVE 'Loan to be removed '   TO #RPU-MSG-LABEL
MOVE EDITED #TOT-RPU-LOAN-AMT (EM=ZZZZZZZ.99) TO #RPU-MSG-AMT    /*TT1 INCREASED SIZE
MOVE #RPU-MSG-LINE TO #SCR-MSG-LINE-1
*
MOVE 'Int to be removed  '   TO #RPU-MSG-LABEL
MOVE EDITED #TOT-RPU-INT-AMT (EM=ZZZZZZZ.99) TO #RPU-MSG-AMT    /*TT1 INCREASED SIZE
COMPRESS #SCR-MSG-LINE-1 '*' #RPU-MSG-LINE INTO #SCR-MSG-LINE-1
*
* below must be before reset of face-amount to retain original units
IF CI-V.ISSUE-DATE (#IFL) GE 19941201
  COMPUTE CI-V.EXPIRY-DAY (#IFL) = CI-V.FACE-AMOUNT (#IFL) / 5000
END-IF
*
MOVE #L-606-NEW-FAC-AMT TO CI-V.FACE-AMOUNT           (#IFL)
MOVE #L-606-NEW-DB      TO CI-V.DEATH-BENEFIT         (#IFL)
                           CI-V.NXT-MON-DEATH-BENEFIT (#IFL)
MOVE #L-606-NEW-LST-MCV TO CI-V.MIN-CASH-VALUE        (#IFL)
MOVE #L-606-NEW-NXT-MCV TO CI-V.NXT-YR-MIN-CASH-VALUE (#IFL)
MOVE #SCR-RPU-ACV (#ISC) TO CI-V.ACC-CASH-VALUE     (#IFL)
                            CI-V.CASH-VALUE         (#IFL)
                            CI-V.NXT-MON-CASH-VALUE (#IFL)
*
IF   (CI-V.CONVERSION-FLAG (#IFL) = 'C')
 AND (CI-V.CONV-DURATION (#IFL) LE 10)
  COMPUTE ROUNDED CI-V.CASH-VALUE (#IFL) = CI-V.ACC-CASH-VALUE (#IFL)
   - (CI-V.MONTH-ALLOC-AMT (#IFL) * (120
   - (((CI-V.CONV-DURATION (#IFL) - 1) * 12) + CI-V.MONTH-IND (#IFL))))
  MOVE CI-V.CASH-VALUE (#IFL) TO CI-V.NXT-MON-CASH-VALUE (#IFL)
END-IF
*
IF #L-606-NEW-CUR-MCV GT CI-V.CASH-VALUE (#IFL)
  MOVE #L-606-NEW-CUR-MCV TO CI-V.CASH-VALUE (#IFL)
                             CI-V.NXT-MON-CASH-VALUE (#IFL)
END-IF
*
* COMPUTE ROUNDED CI-V.LAST-ANN-CASH-VALUE (#IFL) =
*   (CI-V.CASH-VALUE (#IFL) * .90)
*                  RESET SCREEN <<<<<<<<<<<<<<<<<<<<<<<<<<<
MOVE 'RPU'                      TO #SCR-STATUS   (#ISC)
MOVE CI-V.FACE-AMOUNT (#IFL) TO #SCR-FACE-AMT (#ISC)
MOVE CI-V.DEATH-BENEFIT (#IFL)  TO #SCR-DB       (#ISC)
MOVE CI-V.MIN-CASH-VALUE (#IFL) TO #SCR-MIN-CV   (#ISC)
MOVE CI-V.ACC-CASH-VALUE (#IFL) TO #SCR-ACV      (#ISC)
MOVE CI-V.CASH-VALUE (#IFL)     TO #SCR-CSH-VL   (#ISC)
*                  WRITE TRANS REGISTER <<<<<<<<<<<<<<<<<<<<<<
MOVE CI-V.ISSUE-DATE   (#IFL) TO #TR-REG-RPU-ISSUE-DATE  /* TRANS REG
MOVE CI-V.PLAN         (#IFL) TO #TR-REG-RPU-PLAN
MOVE EDITED CI-V.FACE-AMOUNT(#IFL) (EM=ZZZZZZZ.99) TO #TR-REG-RPU-FAC-AMT
MOVE EDITED CI-V.DEATH-BENEFIT(#IFL) (EM=ZZZZZZZ.99) TO #TR-REG-RPU-DB
MOVE #TR-REG-RPU         TO TR-REG-V.OLD-DATA (#ITR1)
ADD 1 TO #ITR1
*
END
