* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPUPCB      ** AAFMAA CORE BENEFITS MBR SINCE FIELD UPDATE
*
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* RSE  09/30/2009 RE1  Change 600K logic for Unification
************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 ISSUE-DATE (1)
 2 REDEFINE ISSUE-DATE
  3 ISSUE-YYYYMM  (N6)
  3 ISSUE-DD      (N2)
 2 REDEFINE ISSUE-DATE
  3 ISSUE-YYYY    (N4)
  3 ISSUE-MMDD    (N4)
 2 PROCESS-IND
 2 INTRO-FLAG
 2 MBR-SINCE
 2 CONV-DURATION  (9)
*
1 #W-DATE-EDITED (A11)
1 #MONTHS (A36)  INIT
     <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MONTHS
 2 #MON       (A3/12)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (N4)
 2 #MM     (N2)
 2 #DD     (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM (N6)
 2 #DD-A   (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A (A8)
*
1 #MMDDYYYY   (A8)
1 REDEFINE #MMDDYYYY
 2 #MM-X   (A2)
 2 #DD-X   (A2)
 2 #YYYY-X (A4)
*
1 #INVALID         (L)
1 #ERR-MSG         (A60)
1 #EDIT-FLAG       (A1)    INIT<'N'>
1 #HOLD-DT             (N6)
1 #HOLD-DT1            (N6)
1 #HOLD-DT-1           (N6)
1 #HOLD-DT2            (N6)
1 #HOLD-DT-2           (N6)
1 #HOLD-ID             (N6)
1 #ST-REC-CHGD         (L)
1 #PREV-RANGE-OK       (L) INIT <FALSE>
*
1 #STATUS-REC          (N14)
1 REDEFINE #STATUS-REC
 2 #ID-NUMBER          (N6)
 2 #ISSUE-DATE         (N8)
*
1 #LAST-GOOD-DATE      (N8)
1 REDEFINE #LAST-GOOD-DATE
 2 #LAST-GOOD-YYYYMM   (N6)
 2 #LAST-GOOD-DD       (N2)
1 #MBR-SINCE-DATE      (N6)
1 #UPDATE-FLAG         (L) INIT <FALSE>
1 #I1         (I2)
1 #I2         (I2)
1 #CNT        (N6)
1 #CNT-1      (N6)
1 #CNT-2      (N6)
1 #CNT-3      (N6)
1 #CNT-4      (N6)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX           /* NATURAL ERROR ROUTINE
*
FORMAT     LS=80 PS=60
WRITE      TITLE LEFT 'PROGRAM:' *PROGRAM  'MBRS WITH ISSUE DATES '
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (2) LS=80 PS=60
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM  '   ISSUE DATES EQUAL 0 '
                 5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (3) LS=80 PS=60
WRITE  (3) TITLE LEFT 'PROGRAM:' *PROGRAM  ' ISSUE DATES OUT OF SEQ'
                 5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (4) LS=80 PS=60
WRITE  (4) TITLE LEFT 'PROGRAM:' *PROGRAM  ' EXCEPTIONS            '
                 5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
MOVE 19250601 TO #LAST-GOOD-DATE
*
READ ST-V BY MEMBER-KEY STARTING FROM '001900'
*
  IF ID-NUMBER LT 001900
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF PROCESS-IND = 'C'
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF ISSUE-YYYY = 1945
    MOVE 19451001 TO #LAST-GOOD-DATE
  END-IF
*
*  IF ID-NUMBER > 600000 AND ID-NUMBER < 700000        RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)             /* RE1
    ESCAPE TOP                           /* Bypass these    RE1  DRW         
*    MOVE ISSUE-DATE (1) TO #LAST-GOOD-DATE               /* RE1  DRW
  END-IF
*
  IF ID-NUMBER LE 11000 AND ISSUE-DATE (1) > 0
    MOVE ISSUE-DATE (1) TO #LAST-GOOD-DATE
    MOVE ISSUE-YYYYMM   TO #MBR-SINCE-DATE
    MOVE ISSUE-YYYYMM   TO MBR-SINCE
  ELSE
    IF ISSUE-DATE (1) = 0
      MOVE #LAST-GOOD-YYYYMM TO #MBR-SINCE-DATE
      MOVE #LAST-GOOD-YYYYMM TO MBR-SINCE
    ELSE
      MOVE #LAST-GOOD-DATE TO #YYYYMMDD
      ADD 4 TO #MM
      IF #MM > 12
        COMPUTE #MM = (#MM - 12)
        ADD 1 TO #YYYY
      END-IF
      MOVE #YYYYMM   TO #HOLD-DT2
*
      MOVE #LAST-GOOD-DATE TO #YYYYMMDD
      SUBTRACT 4 FROM #MM
      IF #MM <= 0
        COMPUTE #MM = (#MM + 12)
        SUBTRACT 1 FROM #YYYY
      END-IF
      MOVE #YYYYMM   TO #HOLD-DT-2
      IF ISSUE-YYYYMM GE #HOLD-DT-2 AND ISSUE-YYYYMM LE #HOLD-DT2
        MOVE ISSUE-YYYYMM TO #MBR-SINCE-DATE
        MOVE ISSUE-YYYYMM TO MBR-SINCE
        MOVE ISSUE-DATE (1) TO #LAST-GOOD-DATE
      ELSE
        MOVE #LAST-GOOD-YYYYMM TO #MBR-SINCE-DATE
        MOVE #LAST-GOOD-YYYYMM TO MBR-SINCE
        IF ISSUE-YYYY = 1952
          MOVE ISSUE-DATE (1) TO #LAST-GOOD-DATE
        END-IF
      END-IF
    END-IF
  END-IF
*
  UPDATE (0104)
  END TRANSACTION
*
  ADD 1 TO #CNT
  DISPLAY #CNT  (EM=ZZ,ZZ9)
   'CN'               ID-NUMBER
   'Issue/YYYYMM'     ISSUE-YYYYMM
   'Mbr/Since/Date'   #MBR-SINCE-DATE
   'Last/Good/YYYYMM' #LAST-GOOD-YYYYMM
    #HOLD-DT-2 #HOLD-DT2
END-READ
END
