* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram RTEN0005.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 CONTACT-ID  (N8)
1 NICOTINE    (A1)
1 #GENDER-P   (A1)
1 AGE         (P3) BY VALUE
1 DURATION    (P3)
1 CLASS-CODE  (A1)
1 VAWL-FACTOR   (N11.7)
1 VAWL-FACTOR-1 (N11.7)
LOCAL
1 BLANK  (A1) CONST <' '>
1 CNT-V VIEW OF A-CONTACTS
  2 GENDER-CD (A1)
1 PRTES-V VIEW OF A-PRODUCT-RATES
  2 EFFECTIVE-DATE (A8)
  2 LTII-TYPE (A2)
  2 WL-PLAN-CODE (A3)
  2 WL-RATE      (N7.2)
  2 MORTALITY    (N5.4)
  2 MORTALITY-AGE (A3)
1 FKEY
  2 #GENDER     (A1)
  2 #NICOTINE   (A1)
  2 #AGE        (A3)
  2 #CLASS-CODE (A1)
1 REDEFINE FKEY
  2 #FACTOR-KEY (A6)
END-DEFINE
/*
RESET VAWL-FACTOR
  VAWL-FACTOR-1
IF #GENDER-P = ' '
  IF CONTACT-ID > 0
    FIND (1) CNT-V WITH CONTACT-ID = CONTACT-ID
      #GENDER := CNT-V.GENDER-CD
    END-FIND
  ELSE
    #GENDER := 'M'
  END-IF
ELSE
  #GENDER := #GENDER-P
END-IF
/*
IF NICOTINE = 'S'
  #NICOTINE := 'Y'
ELSE
  #NICOTINE := NICOTINE
END-IF
IF CLASS-CODE = BLANK
  #CLASS-CODE := 'B'
ELSE
  #CLASS-CODE := CLASS-CODE
END-IF
AGE := AGE + DURATION /* - 1
MOVE EDITED AGE (EM=999) TO #AGE
/*
FIND PRTES-V WITH FACTOR-KEY = #FACTOR-KEY
  VAWL-FACTOR := PRTES-V.WL-RATE
END-FIND
SUBTRACT 1 FROM AGE
MOVE EDITED AGE (EM=999) TO #AGE
FIND PRTES-V WITH FACTOR-KEY = #FACTOR-KEY
  VAWL-FACTOR-1 := PRTES-V.WL-RATE
END-FIND
VAWL-FACTOR := VAWL-FACTOR / 10000000
VAWL-FACTOR-1 := VAWL-FACTOR-1 / 10000000
END
