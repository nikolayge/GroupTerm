* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP005D - MR'S USING "AD" PIL/APL/FAL/MS LTRS FROM "MR"
*                                 & MR MS2     also LPF/LPV
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW    04292005  DW1     Add DOB processing to GMAXSXX
* TMT    07152014  TT1     Contact Normalization - DOB and Sex
*******************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 #PARM-INPUT    (A36)
1 #PARM-CHOICE   (A12)
1 #PARM-CLERK    (A3)
*
1 TERM-V VIEW OF A-TERMINALS
 2 CLERK-ID
 2 SECURITY-CODE
*
* * * *   M A N U A L   R E Q U E S T   F I L E   * * * * * * *
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND       (N1)
   3 MSG-IND       (N1)
   3 RET-IND       (N1)
   3 FIN-IND       (N1)
   3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND       (A1)
   3 MAL-IND       (A1)
   3 BEN-SHO-IND   (A1)
   3 ACC-IND       (A1)
   3 CAP-IND       (A1)
   3 APP-IND       (A1)
   3 PIL-IND       (A1)
   3 CO-IND        (A1)
   3 VP-IND        (A1)
   3 MS-IND        (A1)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
     4 REQUEST-MM-N (N2)
    3 REQUEST-DD   (A2)
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-CONTACT-ID     /* TT1
  2 INTRO-FLAG
  2 PROCESS-IND       (A1)
  2 RESTRICT-MBR-SW   (A1)
  2 SUSPEND-CODE      (A1)
*   2 DATE-OF-BIRTH    (N8)
  2 NUMBER-MEMBERSHIPS
  2 STATUS           (9)
  2 ISSUE-DATE       (9)
  2 PAY-STATUS       (9)
  2 FACE-AMOUNT      (9)
  2 NUM-MBRSHIPS     (9)
  2 EXPIRY-DAY       (9)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 REDEFINE #MM
     4 #MM-N  (N2)
    3 #DD     (A2)
*
1 #COUNTERS
 2 #MS-RQST-CNT    (P5)
 2 #PIL-RQST-CNT   (P5)
 2 #MAL-RQST-CNT   (P5)
*
1 #MAX-FACE-TOTAL           (P7.2)
1 #MAX-FACE-TERM            (P7.2)
1 #OVERRIDE-MAX-FACE-TOTAL  (P7.2)
1 #OVERRIDE-MAX-FACE-TERM   (P7.2)
1 #TOT-FACE         (P7)
1 #I1               (I2)
1 #I2               (I2)
1 #IFL              (I2)
1 #ISN              (P8)
1 #DATE-OF-BIRTH    (N8.0)         /* TT1
*
END-DEFINE
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
         5X 'AAFMAA PIL/APL/FAL/RFS/RFC/RFZ/LPF/LPV/SCx  LETTER DETAIL'
         5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
INPUT #PARM-INPUT                    /* TYPE OF MAIL-OUTS
MOVE #PARM-INPUT TO #PARM-CHOICE
*
IF #PARM-CHOICE = 'RQST'
  INPUT #PARM-CLERK                    /* CLERK
END-IF
*
INPUT #PARM-INPUT                    /* MEMBER DATE RANGE
INPUT #PARM-INPUT                    /* MEMBER AGE 62 DATE RANGE
INPUT #PARM-INPUT                    /* WIDOW DATE RANGE
*
IF #PARM-CHOICE = 'MBR-RQST-WID' OR = 'MBR-RQST' OR = 'RQST'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
* LETTER REQUEST PROCESSING
* * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
  IF (#PARM-CLERK = '***') AND (REQUEST-MM-N LT 50) /* BYPASS IF
    ESCAPE TOP                              /* entered after job started
  END-IF
*
  IF  (XX-R01.PIL-IND NE ' ')
   OR ((XX-R01.MAL-IND NE ' ') AND (ST-V.PROCESS-IND NE 'S'))
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
END-ALL  /* (0135)
*
SORT BY ID-NUMBER
  USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE RQST-TABLE
*
  FIND (1) TERM-V WITH CLERK-ID = XX-R01.CLERK-ID
    IF NO RECORDS FOUND
      WRITE XX-R01.CLERK-ID 'user not found in AOAP005F'
      MOVE 'B' TO #CGA-USER-COMPANY
      ESCAPE BOTTOM
    END-NOREC
*
    IF TERM-V.SECURITY-CODE = 1 OR = 2 OR = 97
      MOVE 'S' TO #CGA-USER-COMPANY
    ELSE
      MOVE 'I' TO #CGA-USER-COMPANY
    END-IF
  END-FIND
*
  RESET *ISN #ISN
  FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
        IF NO RECORDS FOUND
          WRITE (2) 'MBR NOT FOUND' ST-V.ID-NUMBER
          ESCAPE BOTTOM
        END-NOREC
*
        MOVE *ISN TO #ISN
      END-FIND
    END-NOREC
*
    IF #ISN = 0
      MOVE *ISN TO #ISN
    END-IF
  END-FIND
*
  IF #ISN = 0
    ESCAPE TOP
  END-IF
*
  RESET #TOT-FACE
  FOR #IFL 1 TO 9
    IF ST-V.STATUS (#IFL) = 'D'
      IF ST-V.PAY-STATUS (#IFL) = '44' OR = '45'
        IF ST-V.ISSUE-DATE (#IFL) LT 19941201
        COMPUTE #TOT-FACE = #TOT-FACE + (ST-V.NUM-MBRSHIPS(#IFL) * 5000)
        ELSE
         COMPUTE #TOT-FACE = #TOT-FACE + (ST-V.EXPIRY-DAY (#IFL) * 5000)
        END-IF
      ELSE
        ADD ST-V.FACE-AMOUNT (#IFL) TO #TOT-FACE
      END-IF
    END-IF
  END-FOR
*
  MOVE #ISN TO #CGA-ISN
  MOVE XX-R01.ID-NUMBER TO #CGA-ID
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
*
  IF XX-R01.PIL-IND NE ' '
    ADD 1 TO #PIL-RQST-CNT
    MOVE 'PIL' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
*
    MOVE XX-R01.CLERK-ID TO #CGA-CLERK
    MOVE 'AD'  TO #CGA-ORIG-COMMAND #CGA-SCR-COMMAND
    FETCH RETURN 'G3500PAD'
*
*   PERFORM PRINT-APPL   /* rem'd 03/2004 DRW - apps created at AAFMAA
  END-IF
*
  IF (XX-R01.MAL-IND NE ' ') AND (ST-V.PROCESS-IND NE 'S')
    ADD 1 TO #MAL-RQST-CNT
    IF XX-R01.MAL-IND = '2'
      MOVE 'APL' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '3'
      MOVE 'FAL' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '4'
      MOVE 'RFS' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '5'
      MOVE 'RFC' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '6'
      MOVE 'RFZ' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '7'
      MOVE 'LPF' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '8'
      MOVE 'LPV' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    IF XX-R01.MAL-IND = '9'
      MOVE 'LPB' TO #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
    END-IF
*
    MOVE XX-R01.CLERK-ID TO #CGA-CLERK
    MOVE 'AD'  TO #CGA-ORIG-COMMAND #CGA-SCR-COMMAND
    FETCH RETURN 'G3500PAD'
  END-IF
END-SORT  /* (0148)
*
WRITE (2) /  12T 'TOTAL PIL     REQUESTS' #PIL-RQST-CNT
WRITE (2) /  12T 'TOTAL MAL     REQUESTS' #MAL-RQST-CNT
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-APPL
* * * * * * * * * * * * * * *
*
RESET #DATE-OF-BIRTH                                                /* TT1
#DATE-OF-BIRTH   := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)     /* TT1
*
CALLNAT 'GMAXSXX'
  #CGA-ORIG-COMMAND
  #MAX-FACE-TOTAL
  #MAX-FACE-TERM
  #OVERRIDE-MAX-FACE-TOTAL
  #OVERRIDE-MAX-FACE-TERM
  ST-V.INTRO-FLAG
  #DATE-OF-BIRTH   /* DW1                                           /* TT1
*
IF  (ST-V.NUMBER-MEMBERSHIPS = 0)       /* IS A TERMINATED MEMBER
 OR (#TOT-FACE GT (#MAX-FACE-TOTAL - 5000)) /* HAS MAXIMUM coverage
 OR (ST-V.SUSPEND-CODE = '5' OR = '6' OR = '9') /* INS REJ/DEF INS
 OR (ST-V.RESTRICT-MBR-SW = 'Y')        /* RESTRICTED (SPC CATEGORY)
 OR ((*DATN - 710000) GT #DATE-OF-BIRTH)  /* PAST AGE 71            /* TT1
  IGNORE
ELSE
  MOVE ST-V.ID-NUMBER TO #CGA-SCR-ID
  FETCH RETURN 'C2150PPO'    /*<<<<<<<<<<<<<<<<<<<<<<<<
END-IF
*
END-SUBROUTINE
*
END
