* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-TMRFEQ                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Term Refund Equalization                              *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM  04/10/2009  None    Program Written                             *
************************************************************************
*
DEFINE FUNCTION FUNCTION-TERM-REFUND-EQUAL RETURNS (N8)
DEFINE DATA
PARAMETER
1 #SEC-KEY            (A7)
1 #DATE               (N8)
1 #FACE-AMOUNT        (N7.2)
*
LOCAL USING OBJLGENT
LOCAL
*
1 #PROGRAM            (A8)
1 #ERROR-MESSAGE      (A70)
1 #SEV-LEVEL          (A1) INIT <'S'>
1 #FACE-AMOUNT-VALUES (A50)
1 REDEFINE #FACE-AMOUNT-VALUES
 2 #BEGIN-FACE-AMOUNT (N7)
 2 #BEGIN-SPACE       (A1)
 2 #END-FACE-AMOUNT   (N7)
*
END-DEFINE
************************************************************************
*
FUNCTION-TERM-REFUND-EQUAL := #DATE
*
FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY SORTED BY EFFECTIVE-DATE DESCENDING
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  PERFORM SET-FACE-LIMITS
*
  IF #DATE LE EFFECTIVE-DATE AND #DATE LE END-DATE
   AND #FACE-AMOUNT LE #END-FACE-AMOUNT
    IF APPROVED-FLAG NE 'A'
      ESCAPE BOTTOM
    END-IF
*
    FUNCTION-TERM-REFUND-EQUAL := EFFECTIVE-DATE
    ESCAPE ROUTINE
  END-IF
END-FIND
*
************************************************************************
DEFINE SUBROUTINE SET-FACE-LIMITS
************************************************************************
*
#BEGIN-FACE-AMOUNT   := 0
#END-FACE-AMOUNT     := 9999999
*
IF GEN-TABLE-V.GEN-ALPHA-VALUE NE ' '
  #FACE-AMOUNT-VALUES := GEN-TABLE-V.GEN-ALPHA-VALUE
END-IF
*
END-SUBROUTINE
*
END-FUNCTION
*
END
