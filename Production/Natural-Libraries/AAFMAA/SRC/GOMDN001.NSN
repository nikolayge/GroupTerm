* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
**************************************************************************
* SUBPROGRAM-ID: GOMDN001
* DESCRIPTION: Add/Update   rank data
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
*
* PAM   09/10/2011 PM1  Store military service
*
**************************************************************************
*
DEFINE DATA
PARAMETER USING GOMDA001
PARAMETER
1 #CLERK-ID  (A3)
1 #MSG       (A) DYNAMIC
1 #UPDATED   (L)
*
LOCAL
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID          (N8)
  2 RANK-ID             (N6)
  2 RANK-PROTECT        (A1)
  2 PREV-RANK-ID        (N6)
  2 MILITARY-STATUS     (A1)
  2 RANK-PROTECT-DATE   (N8)
  2 LAST-RANK-DATE-UPD  (N8)
  2 LAST-RANK-USER-UPD  (A8)
1 RR-V VIEW OF A-RANK-REFERENCE
  2 RANK-ABBR           (A25)
  2 PAY-GRADE           (A3)
  2 MILITARY-ID         (A3)
  2 RANK-IN-SAS         (A3)
1 ST-V VIEW OF A-STATUS             /* PM1
 2 MEMBER-CONTACT-ID                /* PM1
 2 MILITARY-SERVICE                 /* PM1
 2 RANK                             /* PM1
*
END-DEFINE
*
IF #MIL.CONTACT-ID <= 0
  COMPRESS *PROGRAM 'No contact ID' INTO #MSG
ELSE
  FIND (1) CONTACT WITH CONTACT-ID = #MIL.CONTACT-ID
    MOVE BY NAME #MIL TO CONTACT
    CONTACT.LAST-RANK-DATE-UPD := *DATN
    CONTACT.LAST-RANK-USER-UPD := *USER
    UPDATE (0051)
  END-FIND
*
  ST01. FIND (1) ST-V WITH ST-V.MEMBER-CONTACT-ID = #MIL.CONTACT-ID  /* PM1
    ST-V.MILITARY-SERVICE := #MIL.MILITARY-ID                        /* PM1
    FIND (1) RR-V WITH RR-V.RANK-ID = #MIL.RANK-ID                   /* PM1
      ST-V.RANK := RR-V.RANK-IN-SAS                                  /* PM1
    END-FIND                                                         /* PM1             
    UPDATE (ST01.)                                                   /* PM1
  END-FIND                                                           /* PM1
*
  #UPDATED := TRUE
END-IF
END
