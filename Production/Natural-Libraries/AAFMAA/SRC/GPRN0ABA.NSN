* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* GPRN0ABA Get/Confirm Bank Name by ABA Routing Number
* BZ 07/18/2014
*  Usage:
*  p_RN  - ABA Routing Number
*  p_Name - Bank Name (blank if not found)
*  p_RC   : 1- Access error; 2 - Wrong ABA;  9 - Natural error; 0: Returns Bank name
*  

DEFINE DATA 
PARAMETER
1 Parms
  2 p_RN   (A9)
  2 p_Name (A)  DYNAMIC
  2 p_RC   (I4) 

LOCAL
1 v_ABA        (A) DYNAMIC
1 #i (I4)
1 #j (I4)
1 f_ABA        (A) DYNAMIC INIT <'/usr/SAG/APPS/ABA/ABA.TXT'>

END-DEFINE
ON ERROR                  
   COMPRESS 'Pgm:' *PROGRAM 'ln:' *ERROR-LINE 'Err:' *ERROR-NR INTO p_Name
   p_RC := 9
   ESCAPE ROUTINE 
END-ERROR
*
 IF p_RN <> MASK (NNNNNNNNN)
    p_RC := 4
    ESCAPE ROUTINE
  END-IF   
 
 IF NOT FN_Exist(<f_ABA>)
    p_RC := 1
    ESCAPE ROUTINE
  END-IF   

  DEFINE WORK FILE 17 f_ABA TYPE 'UNFORMATTED'
  READ WORK FILE 17 ONCE v_ABA
  CLOSE WORK FILE 17
  EXAMINE v_ABA FOR p_RN GIVING POSITION #i
  IF #i = 0  p_RC := 2  ESCAPE ROUTINE  END-IF   

  EXAMINE SUBSTRING (v_ABA,#i) FOR H'0A' GIVING POSITION #j
  #j := #j - 10 
  #i := #i + 9
  p_Name := SUBSTRING( v_ABA , #i , #j)
  p_RC := 0

END
