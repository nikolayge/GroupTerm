* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
** PROGRAM-ID     ATHPYAIR
** REMARKS WIDOW REPORTING SYSTEM
**          USER ABENDS-
**                 10  - BAD DATE PARM
**
**   REPORT-FILE        ASSIGN REPORT 1
************************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* SAG  11282005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX1                *
* bz  06/09/2013  Rebranding (Search for AAFMAA)
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 ABEND-CODE                 (I4)
1 #ID-NUMBER-G               (A6)
1 REDEFINE #ID-NUMBER-G
  2 #ID-NUMBER               (N6)
*
1 FIRS-RECORD VIEW OF A-FIRS
  2 ID-NUMBER          (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A        (A6)
  2 C*POLICY-TABLE
  2 POL-NAME(32)
  2 POL-REMARKS(32)
  2 BENEFIT-PAID
  2 DATE-PAID
1 STATUS-RECORD VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME-SP
/* END OF VIEW STATUS-RECORD
1 #BEN-PAID      (N4)
1 #ACC-INT       (N4)
1 #INDEX         (I2)
1 #HOLD-POL-REMARK  (A21)
1 REDEFINE #HOLD-POL-REMARK
  2 #HPR-7    (A7)
  2 #HPR-F    (A14)
1 #HOLD-ID  (A8)
END-DEFINE
*****************************************************************
*****************************************************************
*          P R O C E D U R E   D I V I S I O N                  *
*****************************************************************
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR        /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'C U R R E N T  R C D' STATUS-RECORD.ID-NUMBER
*     MOVE +4095 TO ABEND-CODE
*     CALL 'ABEND' USING ABEND-CODE
  #BATCH-LITERAL-1 := STATUS-RECORD.ID-NUMBER
  INCLUDE GABNDCX1                                      /* SAG RH1 11282005 SS - END
END-ERROR
*
FORMAT PS=55
  LS=133
WRITE TITLE LEFT
  40T 'AAFMAA'
  91T 'PAGE' 96T *PAGE-NUMBER
  //    32T 'ID' 55T 'DATE BENEFIT' 80T 'REMARKS'
  /     30T 'NUMBER' 59T 'PAID' //
READ FIRS-RECORD LOGICAL BY WIDOW-KEY
  PERFORM ACCR-INT
END-READ
WRITE /// 50T 'NUMBER OF ACC-INT' #ACC-INT
*
DEFINE SUBROUTINE ACCR-INT
IF FIRS-RECORD.DATE-PAID > 19890000
    AND FIRS-RECORD.DATE-PAID < 19900000
  ADD 1 TO #BEN-PAID
  IF FIRS-RECORD.C*POLICY-TABLE = 0
    IGNORE
  ELSE
    FOR #INDEX 1 TO FIRS-RECORD.C*POLICY-TABLE
      IF FIRS-RECORD.POL-NAME(#INDEX) = 'AMAA' OR = 'AAFMAA'
        ASSIGN #HOLD-POL-REMARK = FIRS-RECORD.POL-REMARKS(#INDEX)
        IF #HPR-7 = 'ACC INT'
          ADD 1 TO #ACC-INT
          COMPRESS 'WN' FIRS-RECORD.ID-NUMBER-A INTO #HOLD-ID
            LEAVING NO SPACE
          WRITE / 29T #HOLD-ID
            56T FIRS-RECORD.DATE-PAID
            80T FIRS-RECORD.POL-REMARKS(#INDEX)
          ESCAPE ROUTINE
        END-IF
      END-IF
    END-FOR
  END-IF
END-IF
*
END-SUBROUTINE  /* (7520)
*
************************************************************************
END
