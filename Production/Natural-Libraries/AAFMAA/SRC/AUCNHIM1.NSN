* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNHIM1.
/**
/** :author nguentchev   ONLY PARENT RELATIONS
/*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* YF   11/09/2017  YF1     Added logic for services                    *
************************************************************************
*
DEFINE DATA
PARAMETER USING AUCAHI01
LOCAL USING OBJADBTS
LOCAL  USING OBJLMB01
LOCAL
1 #GET    (I4) CONST <0>
1 #ADD    (I4) CONST <1>
1 #UPDATE (I4) CONST <2>
1 #DELETE (I4) CONST <3>
1 BLANK   (A1) CONST <' '>
*
1 #UNSUPPORTED (A) DYNAMIC CONST <'UNSUPPORTED REQUEST'>
1 #REQUIRED    (A) DYNAMIC CONST <'CONTACT ID REQUIRED'>
1 #UPDATECONFLICT (A) DYNAMIC CONST <'UPDATE CONFLICT'>
*
1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 CONTACT-TYPE
  2 WEBSITE-URL
1 STA-V VIEW OF A-STATUS
  2 DATE-OF-DIVORCE
  2 DATE-OF-MARRIAGE
  2 MBR-SINCE              (N6)
  2 ACCESS-AUTH-CODE

1 #D   (N8) 1 REDEFINE #D
  2 #YYYYMM   (N6)
  2 #DD       (N1)
END-DEFINE
*
RESET ErrorCode
  ErrorDescription
*
#DB.#FILE-NAME := 'A-CONTACTS'
*
FIND CNT-V WITH CONTACT-ID = HeaderInfo.ContactID
  IF NO RECORDS FOUND
    ErrorCode := 1
    COMPRESS *PROGRAM #REQUIRED INTO ErrorDescription
    ESCAPE ROUTINE
  END-NOREC
  MOVE *ISN TO #DB.#FILE-REC-ISN
  #DB.#REQUEST   := #GET
  CALLNAT 'OBJNDBTS' #DB  /* Get last update date/time
END-FIND
*
DECIDE ON FIRST VALUE OF ActionType
  VALUE #GET    PERFORM GET-RECORD
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  ANY VALUE
    PERFORM SYNC-OPERATIONS
  NONE VALUE
    ErrorCode := 2
    ErrorDescription := #UNSUPPORTED
END-DECIDE
*
DEFINE GET-RECORD
/*     ==========
GET CNT-V #DB.#FILE-REC-ISN
LastUpdatedDate := #DB.LAST-DATE-UPD
LastUpdatedTime := #DB.LAST-TIME-UPD
LastUpdatedUser := #DB.LUU-NAME
*
HeaderInfo.CN := CNT-V.ID-NUMBER
Title := 'LETTER'
CALLNAT 'GPRS0CNT' ContactID Title
HeaderInfo.SubTitle := f-subtitle (< HeaderInfo.CN >)
IF HeaderInfo.CN > 0
  FIND (1) STA-V WITH ID-NUMBER = HeaderInfo.CN
    #YYYYMM := MBR-SINCE
    #DD     := 1
    HeaderInfo.MemberSince  := #D
    HeaderInfo.AAFMAAAccess := ACCESS-AUTH-CODE
  END-FIND
  HeaderInfo.WEALTH := FALSE
  HeaderInfo.MORTGAGE := FALSE
  HeaderInfo.PREMIER := FALSE
  HeaderInfo.SURVIVOR := FALSE
  HeaderInfo.COMPLIMENTARY := FALSE
  FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = HeaderInfo.CN /* YF1 START
    DECIDE FOR EVERY CONDITION
      WHEN SVC-PLAN = 'WMT' AND SVC-STATUS = 'A'
        HeaderInfo.WEALTH := TRUE
      WHEN SVC-PLAN = 'MTG' AND SVC-STATUS = 'A'
        HeaderInfo.MORTGAGE := TRUE
      WHEN SVC-PLAN = 'PSM' AND SVC-STATUS = 'A'
        HeaderInfo.PREMIER := TRUE
      WHEN SVC-PLAN = 'SAS' AND SVC-STATUS = 'A'
        HeaderInfo.SURVIVOR := TRUE
      WHEN SVC-PLAN = 'CMS' AND SVC-STATUS = 'A'
        HeaderInfo.COMPLIMENTARY := TRUE
      WHEN NONE
        IGNORE
    END-DECIDE
  END-FIND                                              /* YF1 END
END-IF
*
END-SUBROUTINE
*
DEFINE ADD-RECORD
/*     ==========
ErrorCode := 3
COMPRESS *PROGRAM 'ADD-RECORD is NOT IMPLEMENTED'
  INTO ErrorDescription
END-SUBROUTINE
*
DEFINE UPDATE-RECORD
/*     =============
ErrorCode := 3
COMPRESS *PROGRAM 'UPDATE-RECORD is NOT IMPLEMENTED'
  INTO ErrorDescription
*
END-SUBROUTINE
*
DEFINE DELETE-RECORD
/*     =============
ErrorCode := 3
COMPRESS *PROGRAM 'DELETE-RECORD is NOT IMPLEMENTED'
  INTO ErrorDescription
END-SUBROUTINE
*
DEFINE SYNC-OPERATIONS
/*     ===============
IF ActionType > 0
  ErrorCode := 3
  COMPRESS *PROGRAM 'SYNC-OPERATIONS are NOT IMPLEMENTED'
    INTO ErrorDescription
END-IF
END-SUBROUTINE
*
END
