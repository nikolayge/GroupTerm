* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP338A - weekly bene reminder production
*                        1. 30 day bene chg follow-up
*                        2. 60 day bene chg follow-up
*
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*********** MODIFICATION LOG *************************************
** Who   When      Tag  Description
** SAG 01/23/2006  RH1  AB - Replaced READ statement with FIND for same result
** RSE 10/10/2012  RE1  CN Expansion Project - Get beneficiary data from new bene files
* bz  06/09/2013        Rebranding (Search for AAFMAA)
* JNT 09/13/2013   JT1  Address normalization
*****************************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL
**1 NT-V VIEW OF A-NOTES    /* note file used for pending beneficiaries                  /* RE1
1 BENE-D-V VIEW OF A-BENE-DRAFT   /* Beneficiary file used for pending beneficiaries     /* RE1
 2 ID-NUMBER          (N6)    /* let D = bene designation (1-4)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A      (A6)
 2 NOTE-DATE          (N8/60) /* LET P/A = PRINCIPAL/ALTERNATE DESING
 2 REDEFINE NOTE-DATE         /* let L = linked policies
  3 BENE-DATES-6-POLS (6)
   4 BENE-ISS-DT       (N6/7)   /* (D, L)
   4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
   4 BENE-LST-UPD      (N8)     /* (D)
   4 BENE-STRT-PEND    (N8)     /* (D)
   4 BENE-FOL-UP-IND   (N1)     /* (D)
   4 BENE-OWN-TRNSF-DT (N8)     /* (D)
   4 BENE-SUC-OWN      (N1)     /* (D)
   4 BENE-NEW-LAYOUT   (N1)     /* (D)
   4 BENE-FIL1         (A4)
 2 NOTE-CLERK-ID       (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-6-POLS  (6)
   4 BENE-STATUS      (A1)     /* (D)
   4 BENE-RESTRICT    (A1)     /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)    /* (D, P/A)
    5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
    5 BENE-MISC-FIL-2  (A12)
*  2 NOTE-TEXT          (A60/60)
*  2 REDEFINE NOTE-TEXT
*   3 BENE-TEXTS-6-POLS (6)
*    4 BENE-TEXT-PRIM-SEC (2)
*     5 BENE-TEXT        (A60/5)    /* (D, P/A, text-line)
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL
 2 ENTRY-DATE       (N8)
 2 REC-ISN          (N8)
 2 CODE             (A1)
 2 NUM-MBRSHIPS     (N3)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER            (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A         (A6)
* 2 ZIP-CODE             (N9)                                      /* JT1
 2 NUMBER-MEMBERSHIPS   (N1)
*
1 #SCR-DATE-DISP          (A10)
1 #NT-NUM                 (N6)
1 REDEFINE #NT-NUM
 2 #NT-NUM-A              (A6)
*
1 #WORK-DATE      (N8)
1 REDEFINE #WORK-DATE
 2 #WRK-YYYY      (N4)
 2 #WRK-MM        (N2)
 2 #WRK-DD        (N2)
*
1 #I1          (I2)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 #DD     (A2)
*
1 #COUNTERS
  2 #MEMBER-CNT       (P5)
  2 #NUM-BENE-30-DAY  (P5)
  2 #NUM-BENE-60-DAY  (P5)
  2 #NUM-BENE-DELETED (P5)
*
1 #PEND-NT-ISN     (P8)
1 #DELETED         (L)
1 #UPDATE-BENE     (L)
1 #AGO-30-DAYS     (N8)
1 REDEFINE #AGO-30-DAYS
 2 #AGO-30-DAYS-YYYY (N4)
 2 #AGO-30-DAYS-MM   (N2)
 2 #AGO-30-DAYS-DD   (N2)
1 #AGO-60-DAYS     (N8)
1 REDEFINE #AGO-60-DAYS
 2 #AGO-60-DAYS-YYYY (N4)
 2 #AGO-60-DAYS-MM   (N2)
 2 #AGO-60-DAYS-DD   (N2)
1 #REM-30  (L)
1 #REM-60  (L)
1 #INT     (I2)
1 #NOTE-TEXT (A60/10)
1 #SPS-GEN-LINE-1  (A30)
1 #SPS-GEN-LINE-2  (A15)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE  'PROG:' *PROGRAM
             8X 'AAFMAA' /
                'Bene Reminders'
*
FORMAT     LS=133 PS=66
FORMAT (4) LS=133 PS=66
*
* * * * * * * * * * * * * * * * *
*
MOVE *DATN TO #AGO-30-DAYS
SUBTRACT 1 FROM #AGO-30-DAYS-MM
IF #AGO-30-DAYS-MM LT 1
    MOVE 12 TO #AGO-30-DAYS-MM
    SUBTRACT 1 FROM #AGO-30-DAYS-YYYY
END-IF
*
MOVE #AGO-30-DAYS TO #AGO-60-DAYS
SUBTRACT 1 FROM #AGO-60-DAYS-MM
IF #AGO-60-DAYS-MM LT 1
    MOVE 12 TO #AGO-60-DAYS-MM
    SUBTRACT 1 FROM #AGO-60-DAYS-YYYY
END-IF
*
MOVE *DATN TO #DATN
*
**READ NT-V WITH ID-NUMBER = 200000             /* RE1 START
READ BENE-D-V BY ID-NUMBER 
*  IF ID-NUMBER GE 400000
*    ESCAPE BOTTOM
*  END-IF                                       /* RE1 END
  MOVE *ISN TO #PEND-NT-ISN
*
  RESET #DELETED
  RESET #REM-30 #REM-60
  FOR #I1 1 TO 6
    IF  (BENE-STATUS (#I1) = 'P')
     AND (BENE-FOL-UP-IND (#I1) = 2) /* got 60-day ltr last run
      ADD 1 TO #NUM-BENE-DELETED
*
**    READ MBRSHIP-MTD-DET-V BY ENTRY-DATE      /* SAG 01/23/2006  RH1 
**               FROM NT-V.BENE-LST-UPD (#I1)   /* SAG 01/23/2006  RH1
**               THRU NT-V.BENE-LST-UPD (#I1)   /* SAG 01/23/2006  RH1
*      FIND MBRSHIP-MTD-DET-V WITH ENTRY-DATE EQ NT-V.BENE-LST-UPD (#I1) /* SAG 01/23/2006  RH1           /* RE1
      FIND MBRSHIP-MTD-DET-V WITH ENTRY-DATE EQ BENE-D-V.BENE-LST-UPD (#I1) /* SAG 01/23/2006  RH1        /* RE1
        IF   (REC-ISN = #PEND-NT-ISN)
         AND (CODE = 'B')
         AND (MBRSHIP-MTD-DET-V.NUM-MBRSHIPS = #I1)
          DELETE (0165)
          ESCAPE BOTTOM
        END-IF
      END-FIND                /* SAG 01/23/2006  RH1
**    END-READ                /* SAG 01/23/2006  RH1
      MOVE TRUE TO #DELETED
      DELETE (0148)
      END TRANSACTION
*
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #DELETED
    ESCAPE TOP
  END-IF
*
  RESET #UPDATE-BENE
  FOR #I1 1 TO 6
    IF  (BENE-STATUS (#I1) = 'P')
     AND (BENE-FOL-UP-IND (#I1) = 0)
     AND (BENE-LST-UPD (#I1) LE #AGO-30-DAYS)
      MOVE BENE-LST-UPD (#I1) TO #WORK-DATE
      COMPRESS #WRK-MM #WRK-DD #WRK-YYYY INTO #SCR-DATE-DISP WITH '/'
*
*      COMPUTE #NT-NUM = NT-V.ID-NUMBER - 200000                /* RE1
*      FIND (1) ST-V WITH MEMBER-KEY = #NT-NUM-A                /* RE1
      #NT-NUM := BENE-D-V.ID-NUMBER                             /* RE1
      FIND (1) ST-V WITH MEMBER-KEY = #NT-NUM-A                 /* RE1
        PERFORM FORMAT-LABEL
      END-FIND
*
      IF NUMBER-MEMBERSHIPS = 0  /* no letter if terminated/ dead
        ESCAPE BOTTOM
      END-IF
*
      MOVE TRUE TO #UPDATE-BENE
      MOVE 1 TO BENE-FOL-UP-IND (#I1)
      ADD 1 TO #NUM-BENE-30-DAY
*
      MOVE TRUE TO #REM-30
*     NEWPAGE (4)
*     WRITE   (4) NOTITLE USING FORM 'ATHF338A'
    END-IF
*
    IF  (BENE-STATUS (#I1) = 'P')
     AND (BENE-FOL-UP-IND (#I1) = 1)
     AND (BENE-LST-UPD (#I1) LE #AGO-60-DAYS)
      MOVE TRUE TO #UPDATE-BENE
      MOVE 2 TO BENE-FOL-UP-IND (#I1)
*
      MOVE BENE-LST-UPD (#I1) TO #WORK-DATE
      COMPRESS #WRK-MM #WRK-DD #WRK-YYYY INTO #SCR-DATE-DISP WITH '/'
*
*      COMPUTE #NT-NUM = NT-V.ID-NUMBER - 200000                 /* RE1
*      FIND (1) ST-V WITH MEMBER-KEY = #NT-NUM-A                 /* RE1
      #NT-NUM := BENE-D-V.ID-NUMBER                              /* RE1
      FIND (1) ST-V WITH MEMBER-KEY = #NT-NUM-A                  /* RE1
        MOVE *ISN TO #CGA-ISN
        MOVE ST-V.ID-NUMBER TO #CGA-ID
                               #CGA-SCR-ID
        PERFORM FORMAT-LABEL
      END-FIND
*
      IF NUMBER-MEMBERSHIPS = 0  /* no letter if terminated/ dead
        ESCAPE BOTTOM
      END-IF
*
      ADD 1 TO #NUM-BENE-60-DAY
      MOVE TRUE TO #REM-60
*
*     IF ST-V.ZIP-CODE = 0
*       NEWPAGE (6)
*       WRITE   (6) NOTITLE USING FORM 'ATHF339A'
*     ELSE
*       NEWPAGE (4)
*       WRITE   (4) NOTITLE USING FORM 'ATHF339A'
*     END-IF
*
*     MOVE 'BND' TO #CGA-SCR-SUFFIX
*     FETCH RETURN 'E4700PBD'
    END-IF
  END-FOR
*
  IF #UPDATE-BENE
    UPDATE (0148)
*
*   IF #REM-30 OR #REM-60
*     IF #REM-30
*       MOVE '30 Day Beneficiary Reminder sent' TO #NOTE-TEXT (1)
*     ELSE
*       MOVE '60 Day Beneficiary Reminder sent' TO #NOTE-TEXT (1)
*     END-IF
*
*     MOVE 1 TO #INT
*     MOVE 'EDS' TO #CGA-CLERK
*     CALLNAT 'E4205SDM' ST-V.ID-NUMBER
*                        #INT
*                        #CGA-CLERK
*                        #NOTE-TEXT (1:10)
*   END-IF
  END-IF
*
  END TRANSACTION
END-READ
*
* NEWPAGE (4)
WRITE (2) #MEMBER-CNT       ' = RECORDS READ'
WRITE (2) #NUM-BENE-30-DAY  ' = bene 30 day reminders (not sent)'
WRITE (2) #NUM-BENE-60-DAY  ' = bene 60 day reminders (not sent)'
WRITE (2) #NUM-BENE-DELETED ' = benes deleted'
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-LABEL
* * * * * * * * * * * * * * *
*
MOVE ST-V.ID-NUMBER TO #CGA-SCR-ID
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
IF #SPS-GEN-ID NE 0
  COMPRESS 'For' #SPS-GEN-NAME INTO #SPS-GEN-LINE-1
  COMPRESS 'With CN' #SPS-GEN-ID INTO #SPS-GEN-LINE-2
ELSE
  RESET #SPS-GEN-LINE-1 #SPS-GEN-LINE-2
END-IF
*
END-SUBROUTINE /* (0282)
*
END
