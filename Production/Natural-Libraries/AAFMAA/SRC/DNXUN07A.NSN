* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: DNXUN07A
* DESCRIPTION:   Funeral Contact
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NGG  12/09/2010  None Creation
* RSE  04/14/2011  RE1  Add Contact Type logic
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID                   (N8)
1 #FUNERAL-CONTACT-ID           (N8)
1 #USER-COMPANY                 (A1)
1 #CLERK-ID                     (A3)
1 #MSG                          (A72)
LOCAL
1 #MAP-CV                    (C)
1 #POS                       (I4)
1 #H1                        (A78)
1 #H2                        (A78)
1 #SECURED                   (A1)
1 #ERRORS                    (L)
1 #UPDATED                   (L)
1 #CLERK-NAME                (A20)
1 #LAST-UPDATED              (A10)
1 #SCR-ROLE                  (A1)  INIT <'W'>
1 #SCREEN
  2 #SCR-PREFIX-TITLE          (A25)
  2 #SCR-FIRST-NAME            (A25)
  2 #SCR-MIDDLE-NAME           (A25)
  2 #SCR-LAST-NAME             (A25)
  2 #SCR-SUFFIX                (A10)
  2 #SCR-WEBSITE-URL           (A70)
  2 #SCR-GENDER-CD             (A1)
  2 #SCR-FIRM-ORGANIZATION-NAME (A75)
  2 #SCR-CONTACT-TYPE          (A1)     /* RE1
  2 #CONTACT-REMARKS            (A75/3)
  2 #FUNERAL-ID                (A12)

1 P_PARMS
  2 P_ERROR_NUM (I4) /*ERROR NUMBER
  2 P_ERROR_LINE (I4) /*ERROR LINE
  2 P_ERROR_PROGRAM (A32)
  2 P_ERROR_LIB (A32)
  2 P_ERROR_DBID (I4)
  2 P_ERROR_FNR (I4)
  2 P_ERROR_TIME (T)
  2 P_ERROR_PGM_TYPE (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG (I4)
  2 P_ERROR_LEVEL (I4)
  2 P_ERROR_CLASS (I1)
  2 P_ERROR_TYPE (I1)
*
1 BLANK   (A1) CONST <' '>
1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER                    (N6)
1 #CNT-ISN                       (P8)
1 FUNERAL VIEW OF A-CONTACTS
  2 CONTACT-ID                   (N8)
  2 CONTACT-TYPE                 (A1)
  2 PREFIX-TITLE                 (A25)
  2 FIRST-NAME                   (A25)
  2 MIDDLE-NAME                  (A25)
  2 LAST-NAME                    (A25)
  2 SUFFIX                       (A10)
  2 GENDER-CD                    (A1)
  2 WEBSITE-URL                  (A70)
  2 FIRM-ORGANIZATION-NAME       (A75)
  2 DELETE-FLAG                  (A1)
  2 CONTACT-REMARKS              (A75/3)
  2 LAST-USER-UPD                (A8)
  2 LAST-DATE-UPD                (N8) 2 REDEFINE LAST-DATE-UPD
    3 #YYYY     (A4)
    3 #MM       (A2)
    3 #DD       (A2)
  2 LAST-TIME-UPD                (N7)
*
1 #ST-ISN                        (N8)
1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER                    (N6)
1 #TEST   (A8) CONST <'STAGEDN'>
*
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
  2 REDEFINE COMMENTS
    3 #NEW-CONTACT-ID (N8)
1 CNT-REL-S VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID     (N8)           /* D
  2 ROLE-1-CD        (A1)
  2 CONTACT-2-ID     (N8)           /* D
  2 ROLE-2-CD        (A1)
  2 ACCESS-AUTH-SVC  (A1)
  2 ACCESS-AUTH-INS  (A1)
  2 LAST-USER-UPD    (A8)
  2 LAST-DATE-UPD    (N8)
  2 LAST-TIME-UPD    (N7)
*
1 #SEARCH            (A1)
1 #CGA-SCR-ID        (N6)
1 #ROLE-DESC         (A15)
*
END-DEFINE
*
SET KEY ENTR NAMED 'Save'
SET KEY PF1=HELP
SET KEY PF3 NAMED 'Exit'
SET KEY PF4 NAMED 'CG'
SET KEY PF7 NAMED 'Prev'
SET KEY PF8 NAMED 'Next'
SET KEY PF12 NAMED 'Reln'
*
ON ERROR
  CALLNAT 'USR2001N' P_PARMS  /* Get the actual error line details
  COMPRESS 'Application error' P_ERROR_NUM 'In program' P_ERROR_PROGRAM
    'Error line' P_ERROR_LINE
    INTO #MSG
  INPUT //// #MSG (AD=IO)
    / 'Please call production support'
  #MSG := 'PF3'
  ESCAPE ROUTINE
END-ERROR

PERFORM INITIAL-SETTINGS
*
REPEAT
  PERFORM LOAD-DATA
  REPEAT
    INPUT WITH TEXT #MSG MARK #POS USING MAP 'DNXUM07A'
    DECIDE ON FIRST VALUE OF *PF-KEY
      VALUE 'PF3','PF7','PF8'
        ESCAPE BOTTOM
      VALUE 'PF4'
        IF #FUNERAL-CONTACT-ID > 0
          FETCH RETURN 'ADRP0001' #CGA-SCR-ID #CGA-SCR-ID #FUNERAL-CONTACT-ID #CLERK-ID
        END-IF
      VALUE 'ENTR'
        PERFORM INPUT-VALIDATION
      NONE VALUE IGNORE
    END-DECIDE
    UNTIL NOT #ERRORS
  END-REPEAT
  IF *PF-KEY NE 'PF3'
    PERFORM DB-UPDATE
    IF #UPDATED
      #MSG := 'Record successfully updated'
      IF #CLERK-ID = 'NG'
        WRITE NOTITLE #MSG
      END-IF
      END TRANSACTION
    ELSE
      IF #CLERK-ID = 'NG'
        WRITE NOTITLE 'No Record is updated'
      END-IF
      BACKOUT TRANSACTION
    END-IF
  END-IF
  UNTIL *PF-KEY = 'PF3' OR= 'PF7' OR= 'PF8' OR= 'ENTR'
END-REPEAT
*
DEFINE LOAD-DATA
/*     =========
IF #CNT-ISN > 0
  GET FUNERAL #CNT-ISN
  #SCR-PREFIX-TITLE          := FUNERAL.PREFIX-TITLE
  #SCR-FIRST-NAME            := FUNERAL.FIRST-NAME
  #SCR-MIDDLE-NAME           := FUNERAL.MIDDLE-NAME
  #SCR-LAST-NAME             := FUNERAL.LAST-NAME
  #SCR-SUFFIX                := FUNERAL.SUFFIX
  #SCR-WEBSITE-URL           := FUNERAL.WEBSITE-URL
  #SCR-GENDER-CD             := FUNERAL.GENDER-CD
  #SCR-FIRM-ORGANIZATION-NAME := FUNERAL.FIRM-ORGANIZATION-NAME
  #SCR-CONTACT-TYPE          := FUNERAL.CONTACT-TYPE             /* RE1
  #CONTACT-REMARKS (*)       := CONTACT-REMARKS (*)
* COMPRESS '(' FUNERAL.CONTACT-ID ')' INTO #FUNERAL-ID
ELSE
  RESET #SCREEN
  #SCR-CONTACT-TYPE          := 'O'          /* RE1
END-IF
#ROLE-DESC                   := FUNCTION-DISP-RL-DESC(<#SCR-ROLE>)
#CLERK-NAME := FUNCTION-CLERK-NAME (< #CLERK-ID >)
COMPRESS #MM #DD #YYYY INTO #LAST-UPDATED WITH DELIMITER '/'
END-SUBROUTINE
*
DEFINE INPUT-VALIDATION
/*     ================
RESET #MSG #ERRORS

END-SUBROUTINE
*
DEFINE DB-UPDATE
/*     =========
RESET #UPDATED
IF #FUNERAL-CONTACT-ID > 0
  GET FUNERAL #CNT-ISN
  PERFORM DB-FIELDS
  UPDATE (0204)
ELSE
  RESET FUNERAL
  PERFORM DB-FIELDS
  FIND (1) TERM WITH TERMINAL-ID = 'MASTERCO'
    ADD 1 TO #NEW-CONTACT-ID
    UPDATE
  END-FIND
  MOVE #NEW-CONTACT-ID     TO FUNERAL.CONTACT-ID
  MOVE 'N'                 TO FUNERAL.DELETE-FLAG
  STORE FUNERAL
  IF #CLERK-ID = 'NG'
    PRINT 'New contact:' FUNERAL
  END-IF
  MOVE *ISN (0216) TO #CNT-ISN
  MOVE #CONTACT-ID         TO CNT-REL-S.CONTACT-1-ID
  MOVE #NEW-CONTACT-ID     TO #FUNERAL-CONTACT-ID
    CNT-REL-S.CONTACT-2-ID
  MOVE #SCR-ROLE           TO CNT-REL-S.ROLE-2-CD
  MOVE 'M'                 TO CNT-REL-S.ROLE-1-CD
  MOVE #CLERK-ID           TO CNT-REL-S.LAST-USER-UPD
  MOVE *DATN               TO CNT-REL-S.LAST-DATE-UPD
  MOVE *TIMN               TO CNT-REL-S.LAST-TIME-UPD
  STORE CNT-REL-S
  IF #CLERK-ID = 'NG'
    PRINT 'Relation record:' CNT-REL-S
  END-IF
END-IF
MOVE TRUE TO #UPDATED
END-SUBROUTINE
*
DEFINE DB-FIELDS
/*     =========
FUNERAL.CONTACT-TYPE := #SCR-CONTACT-TYPE    /* RE1
FUNERAL.PREFIX-TITLE := #SCR-PREFIX-TITLE
FUNERAL.FIRST-NAME := #SCR-FIRST-NAME
FUNERAL.MIDDLE-NAME := #SCR-MIDDLE-NAME
FUNERAL.LAST-NAME := #SCR-LAST-NAME
FUNERAL.SUFFIX := #SCR-SUFFIX
FUNERAL.WEBSITE-URL := #SCR-WEBSITE-URL
FUNERAL.GENDER-CD := #SCR-GENDER-CD
FUNERAL.FIRM-ORGANIZATION-NAME := #SCR-FIRM-ORGANIZATION-NAME
FUNERAL.CONTACT-REMARKS (*) := #CONTACT-REMARKS (*)
MOVE #CLERK-ID           TO FUNERAL.LAST-USER-UPD
MOVE *DATN               TO FUNERAL.LAST-DATE-UPD
MOVE *TIMN               TO FUNERAL.LAST-TIME-UPD
END-SUBROUTINE

DEFINE INITIAL-SETTINGS
/*     ================
MOVE (AD=U) TO #MAP-CV
*
FIND (1) CONTACT WITH CONTACT-ID = #CONTACT-ID
  CALLNAT 'ADRN0004' #CONTACT-ID #H1 #H2
END-FIND
IF #FUNERAL-CONTACT-ID > 0
  FIND FUNERAL WITH CONTACT-ID = #FUNERAL-CONTACT-ID
    MOVE *ISN (0262) TO #CNT-ISN
  END-FIND
ELSE
  RESET #CNT-ISN
END-IF
*
END-SUBROUTINE
END
