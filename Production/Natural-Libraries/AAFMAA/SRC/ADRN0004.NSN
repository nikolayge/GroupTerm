* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRN0004
* DESCRIPTION: Common routine to display contact data and headings on menu  
*              screens.
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NGG              None Creation
* RSE  05-04-2011  RE1  Check Contact Type
**************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID (N8)
1 #HEADER1    (A78)
1 #HEADER2    (A78)
LOCAL
1 #DASH     (A1) CONST <'-'>
1 #H1       (A79) 1 REDEFINE #H1
  2 FILLER 42X
  2 #T-GEN  (A3)
  2 FILLER  4X
  2 #T-SSN  (A7)         /* RE1 was 3
  2 FILLER 4X            /* RE1 was 8
  2 #T-DOB  (A3)
  2 FILLER 8X
  2 #T-DOD  (A3)
1 REDEFINE #H1
  2 FILLER 60X
  2 #T-EST  (A15)        /* RE1
1 #H2       (A79)
1 #SSN           (A11) 1 REDEFINE #SSN
  2 #SSN-N       (A9)
1 REDEFINE #SSN
  2 #SSN-3        (A3)
  2 #SSN-2        (A2)
  2 #SSN-4        (A4)
1 #DOB            (A10)
1 #DOD            (A10)
1 #D              (D)
1 #CONTACT-NAME   (A42)

1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 GENDER-CD
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-A (A8)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A (A8)
  2 SSN
  2 CONTACT-TYPE
  2 FIRM-ORGANIZATION-NAME
  2 FIRM-ESTABLISHED-DATE 2 REDEFINE FIRM-ESTABLISHED-DATE  /* RE1
    3 FIRM-ESTABLISHED-DATE-A (A8)                          /* RE1

END-DEFINE

#T-GEN := 'SEX'
#T-SSN := 'SSN'
#T-DOB := 'DOB'
#T-DOD := 'DOD'

FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
  IF CONTACT-TYPE = 'O'                     /* RE1 START
    RESET #T-GEN #T-DOB #T-DOD
    #T-SSN := 'TIN/EIN'   
    #T-EST := ' EST. DATE'                  
    IF FIRM-ORGANIZATION-NAME NE ' '
      #CONTACT-NAME := FIRM-ORGANIZATION-NAME
    ELSE
      #CONTACT-NAME := '***MISSING ORGANIZATION NAME***'
    END-IF
  ELSE
    #CONTACT-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)   
  END-IF                                    /* RE1 END
END-FIND

IF CONTACT.SSN > 0 
  MOVE EDITED CONTACT.SSN (EM=9(9)) TO #SSN-N
  COMPRESS #SSN-3 #SSN-2 #SSN-4 INTO #SSN WITH DELIMITER #DASH
ELSE
  RESET #SSN
END-IF

IF CONTACT.DATE-OF-BIRTH > 0 AND CONTACT.CONTACT-TYPE NE 'O' /* RE1
  MOVE EDITED CONTACT.DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
  MOVE EDITED #D (EM=MM/DD/YYYY) TO #DOB
END-IF

IF CONTACT.DATE-OF-DEATH > 0 AND CONTACT.CONTACT-TYPE NE 'O' /* RE1
  MOVE EDITED CONTACT.DATE-OF-DEATH-A TO #D (EM=YYYYMMDD)
  MOVE EDITED #D (EM=MM/DD/YYYY) TO #DOD
END-IF

IF CONTACT.FIRM-ESTABLISHED-DATE > 0 AND CONTACT.CONTACT-TYPE = 'O' /* RE1
  MOVE EDITED CONTACT.FIRM-ESTABLISHED-DATE-A TO #D (EM=YYYYMMDD)
  MOVE EDITED #D (EM=MM/DD/YYYY) TO #DOB
END-IF

IF CONTACT.CONTACT-TYPE = 'O'
  COMPRESS FULL
    #CONTACT-NAME
    '   '
    #SSN
    ' '
    #DOB
    #DOD
    INTO #H2
ELSE
  COMPRESS FULL
    #CONTACT-NAME
    CONTACT.GENDER-CD
    #SSN
    #DOB
    #DOD
    INTO #H2
END-IF

#HEADER1 := #H1
#HEADER2 := #H2

END
