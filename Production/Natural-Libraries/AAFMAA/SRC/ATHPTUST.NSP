* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPTUST  - Accumulate Trade-up Statistics
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    04192004  PM1     Changed to accomodate decimal places for    *
*                          1035 exchange amount.                       *
* PAM    01032005  PM2     Modify process for trade-up issue errors.   *
* PAM    03092005  PM3     Modify to read all accounts.                *
* PAM    04212005  PM5     Create report of member's/policies that     *
*                          traded up during the month. Create report   *
*                          for termed policies (possible errors).      *
* PAM    12092005  PM6     Added process to fix partial 1035 exchange  *
* SAG  12012005 RH1  SS - Replaced HEX constant '05' with HEX '09' ASCII
*                          amounts.                                    *
* PAM    01122006  PM7     Added date parameters for special runs      *
* PAM    05122006  PM8     Added yaer-to-date report file              *
* DRW    01012007  DW9     Added Header Label Record To YTF File       *
* RSE    09302009  RE1     Change PROCESS-IND/600K logic for Unification
* YAK    10102012  YK1  Use function to identify Level Term II plans   *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
*
1 NN-V VIEW OF A-NOTES    /* note file used for pending beneficiaries
 2 ID-NUMBER                           (N6)
 2 NOTE-TABLE                          (60)
  3 NOTE-DATE                          (N8)
  3 REDEFINE NOTE-DATE
   4 NT-DATE-YYYY                      (N4)
   4 REDEFINE NT-DATE-YYYY
    5 NT-DATE-CC                       (N2)
    5 NT-DATE-YY                       (N2)
   4 NT-DATE-MM                        (N2)
   4 NT-DATE-DD                        (N2)
  3 NOTE-CLERK-ID                      (A3)
  3 NOTE-TEXT                          (A60)
  3 REDEFINE NOTE-TEXT
   4 NT-POL-ISS                        (A9)
   4 NT-POL-SPACE                      (A1)
   4 NT-POL-ISS-DATE                   (A10)
   4 NT-POL-SPACE-2                    (A1)
   4 NT-POL-CSV                        (A12)
   4 NT-POL-SPACE-3                    (A1)
   4 NT-POL-CSV-DATE                   (A10)
  3 REDEFINE NOTE-TEXT
   4 NT-TU-FILL-1                      (A10)
   4 NT-TU-ISSUE-DATE                  (A10)
   4 REDEFINE NT-TU-ISSUE-DATE
    5 NT-TU-ISSUE-MM                   (N2)
    5 NT-TU-DASH-1                     (A1)
    5 NT-TU-ISSUE-DD                   (N2)
    5 NT-TU-DASH-2                     (A1)
    5 NT-TU-ISSUE-YYYY                 (N4)
   4 NT-TU-FILL-2                      (A1)
   4 NT-TRADE-UP                       (A13)
   4 NT-TU-FILL-3                      (A26)
  3 REDEFINE NOTE-TEXT
   4 NT-1035-AMOUNT-CHAR-OCC           (60)
    5 NT-1035-AMOUNT-CHAR              (A1)
  3 REDEFINE NOTE-TEXT   /* to handle zzz,zzz as 1035 amt         /* PM1
   4 NT-TXT-LBL-3                      (A12)                      /* PM1
   4 NT-TXT-1035-THOUS-3               (A3)                       /* PM1
   4 NT-TXT-1035-COMMA-3               (A1)                       /* PM1
   4 NT-TXT-1035-HUNDR-3               (A3)                       /* PM1
   4 NT-TEXT-FILL-1-3                  (A8)                       /* PM1
   4 NT-TXT-1035-MM-3                  (A2)                       /* PM1
   4 NT-TXT-1035-FILL1-3               (A1)                       /* PM1
   4 NT-TXT-1035-DD-3                  (A2)                       /* PM1
   4 NT-TXT-1035-FILL2-3               (A1)                       /* PM1
   4 NT-TXT-1035-YYYY-3                (A4)                       /* PM1
   4 NT-TXT-FILL-2-3                   (A1)                       /* PM1
   4 NT-TXT-PLUS-3                     (A6)                       /* PM1
   4 NT-TXT-LUMP-THOUS-3               (A2)                       /* PM1
   4 NT-TXT-LUMP-COMMA-3               (A1)                       /* PM1
   4 NT-TXT-LUMP-HUNDR-3               (A3)                       /* PM1
   4 NT-TXT-FILL-3-3                   (A10)                      /* PM1
  3 REDEFINE NOTE-TEXT   /* to handle zzz,zzz.99 as 1035 amt      /* PM1
   4 NT-TXT-LBL-4                      (A12)                      /* PM1
   4 NT-TXT-1035-THOUS-4               (A3)                       /* PM1
   4 NT-TXT-1035-COMMA-4               (A1)                       /* PM1
   4 NT-TXT-1035-HUNDR-4               (A3)                       /* PM1
   4 NT-TXT-1035-POINT-4               (A1)                       /* PM1
   4 NT-TXT-1035-CENTS-4               (A2)                       /* PM1
   4 NT-TEXT-FILL-1-4                  (A8)                       /* PM1
   4 NT-TXT-1035-MM-4                  (A2)                       /* PM1
   4 NT-TXT-1035-FILL1-4               (A1)                       /* PM1
   4 NT-TXT-1035-DD-4                  (A2)                       /* PM1
   4 NT-TXT-1035-FILL2-4               (A1)                       /* PM1
   4 NT-TXT-1035-YYYY-4                (A4)                       /* PM1
   4 NT-TXT-FILL-2-4                   (A1)                       /* PM1
   4 NT-TXT-PLUS-4                     (A6)                       /* PM1
   4 NT-TXT-LUMP-THOUS-4               (A2)                       /* PM1
   4 NT-TXT-LUMP-COMMA-4               (A1)                       /* PM1
   4 NT-TXT-LUMP-HUNDR-4               (A3)                       /* PM1
   4 NT-TXT-LUMP-POINT-4               (A1)                       /* PM1
   4 NT-TXT-LUMP-CENTS-4               (A2)                       /* PM1
   4 NT-TXT-FILL-3-4                   (A4)                       /* PM1
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 TRAD-UP-SENT-DT
 2 REDEFINE TRAD-UP-SENT-DT
  3 TRAD-UP-SENT-CC                    (N2)
  3 TRAD-UP-SENT-YY                    (N2)
  3 TRAD-UP-SENT-MM                    (N2)
  3 TRAD-UP-SENT-DD                    (N2)
 2 PROCESS-IND
 2 TERM-PREM-PAID
 2 COST-BASIS                          (9)
 2 ORIG-COST-BASIS                     (9)
 2 STATUS                              (9)
 2 ISSUE-DATE                          (9)
 2 X-FIRST-PLACE-FEE                   (9)
 2 X-BILL-ADJUST                       (9)
 2 MODE                                (9)
 2 PLAN                                (9)                        /* PM5
 2 REDEFINE PLAN
  3 PLAN-OCC                           (9)
   4 PLAN-A1                           (A1)
   4 PLAN-A2                           (A2)
*
1 #I1                                  (I2)
1 #I2                                  (I2)
1 #I3                                  (I2)
1 #I4                                  (I2)
1 #I5                                  (I2)
1 #I6                                  (I2)
*
1 #CNT-1                               (P7)
1 #CNT-2                               (P7)
1 #CNT-3                               (P7)
1 #CNT-4                               (P7)
1 #CNT-5                               (P7)
1 #CNT-6                               (P7)
1 #CNT-7                               (P7)
1 #CNT-8                               (P7)
*
1 #JCL-PARM                            (A32)                      /* PM5
1 REDEFINE #JCL-PARM
 2 #JCL-MM                             (N2)   /* DATE PARM MM/DD/YYYY
 2 #JCL-X1                             (A1)
 2 #JCL-DD                             (N2)
 2 #JCL-X2                             (A1)
 2 #JCL-YYYY                           (N4)
*
1 #PARM-INPUT         (A42)                                       /* PM7
1 REDEFINE #PARM-INPUT                                            /* PM7
 2 #PARM-FILLER-1     (A13)                                       /* PM7
 2 #PARM-START-DATE   (N8)                                        /* PM7
 2 #PARM-FILLER-2     (A13)                                       /* PM7
 2 #PARM-END-DATE     (N8)                                        /* PM7
*
1 #START-DATE                          (N8)
1 REDEFINE #START-DATE
 2 #START-DATE-YYYY                    (N4)
 2 #START-DATE-MM                      (N2)
 2 #START-DATE-DD                      (N2)
1 #END-DATE                            (N8)
1 #YEAR-START-DATE                     (N8)
1 REDEFINE #YEAR-START-DATE                                       /* PM8
 2 #YEAR-DATE-YYYY                     (N4)                       /* PM8
 2 #YEAR-DATE-MM                       (N2)                       /* PM8
 2 #YEAR-DATE-DD                       (N2)                       /* PM8
1 #ACTION-FLAG                         (A1) INIT <'B'>
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-RECORD                      (A125)
*
1 #TERMED                              (L)                        /* PM5
1 #CSV-FOUND                           (L)
1 #1035-FOUND                          (L)
1 #IS-A-TRADE-UP                       (L)
1 #TRADE-UP-ISSUE-DATE
 2 #TRADE-UP-ISSUE-YYYY                (A4)
 2 #TRADE-UP-ISSUE-MM                  (A2)
 2 #TRADE-UP-ISSUE-DD                  (A2)
1 REDEFINE #TRADE-UP-ISSUE-DATE
 2 #ISSUE-DATE                         (N8)
1 #TRADE-UP-DATE-MM                    (A2)
1 #TRADE-UP-DATE-DD                    (A2)
1 #TRADE-UP-DATE-YYYY                  (A4)
1 #TRADE-UP-DATE                       (A10)
1 #TRADE-UP-ISSUE-DATE-DISP            (A10)
1 #HOLD-CN                             (N6)
1 #HOLD-MM                             (N2)
1 #HOLD-YY                             (N2)
*
1 #TTL-DEGRANDFATHERED                 (P7)
1 #PCT-DEGRANDFATHERED                 (P5.2)
1 #TTL-LETTERS-SENT                    (P7)
1 #TTL-MEMBERS-TU                      (P7)
1 #PCT-MEMBERS-TU                      (P5.2)
1 #TTL-POLICIES-TRADED                 (P7)
1 #TTL-EXCHANGE-AMOUNT                 (N11.2)                    /* PM1
1 #WORK-EXCHANGE-AMOUNT                (A6)
1 REDEFINE #WORK-EXCHANGE-AMOUNT
 2 #WORK-DIGIT                         (A1/6)
1 REDEFINE #WORK-EXCHANGE-AMOUNT
 2 #WORK-AMOUNT-NUMERIC                (N6)
1 #1035-EXCHANGE-AMOUNT                (N6.2)                     /* PM1
1 #WORK-CENTS-AMOUNT                   (A2)                       /* PM1
1 REDEFINE #WORK-CENTS-AMOUNT                                     /* PM1
 2 #WORK-CENTS                         (A1/2)                     /* PM1
1 REDEFINE #WORK-CENTS-AMOUNT                                     /* PM1
 2 #WORK-CENTS-NUMERIC                 (N0.2)                     /* PM1
1 #AVG-POLICIES-TRADED                 (P5.2)
1 #AVG-EXCHANGE-AMT                    (N15.2)                    /* PM1
*
1 #DISPLAY-EXCHANGE-AMT                (A18)                      /* PM1
1 #DISPLAY-AVG-EXCHANGE-AMT            (A18)                      /* PM1
1 #DISPLAY-PCT-DEG                     (A7)
1 #DISPLAY-PCT-TU                      (A7)
1 #DISPLAY-DEGRAND                     (A16)
1 #DISPLAY-LETTERS-SENT                (A16)
1 #DISPLAY-MEMBERS-TU                  (A16)
1 #DISPLAY-POLICIES-TU                 (A16)
*
**** Individual Month Accumulation Table ****
*
1 #MONTH-TOTAL-TABLE                   (50)
 2 #MONTH-TOTAL                        (12)
  3 #TTL-DEGRANDFATHERED-MNTH          (P7)
  3 #TTL-LETTERS-SENT-MNTH             (P7)
  3 #TTL-MEMBERS-TU-MNTH               (P7)
  3 #TTL-POLICIES-TRADED-MNTH          (P7)
  3 #TTL-EXCHANGE-AMOUNT-MNTH          (N11.2)                    /* PM1
*
1 #YY-INDEX                            (I2)
1 #MM-INDEX                            (I2)
*
1 #HEADER                              (A50)
1 #HEADER-YY                           (N2)
1 MONTH-LETTER                         (A9/12)
    INIT (1) <'JANUARY  '>
         (2) <'FEBRUARY '>
         (3) <'MARCH    '>
         (4) <'APRIL    '>
         (5) <'MAY      '>
         (6) <'JUNE     '>
         (7) <'JULY     '>
         (8) <'AUGUST   '>
         (9) <'SEPTEMBER'>
        (10) <'OCTOBER  '>
        (11) <'NOVEMBER '>
        (12) <'DECEMBER '>
*
1 #YYYYMMDD                            (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY                               (N4)
 2 #MM                                 (N2)
 2 #DD                                 (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A                         (A8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM                             (N6)
*
1 #DAYS-CSV                            (P3)                       /* PM2
1 #ACTION-CODE                         (A1) INIT <'A'>            /* PM2
1 #DAYS-DIFF                           (N8)                       /* PM2
*
1 #START                               (L)                        /* PM7
1 #END                                 (L)                        /* PM7
*
END-DEFINE
***********************************************************************
INCLUDE GONERCXX
*
FORMAT  (1) LS=133 PS=76
WRITE   (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Trade-Up Program Totals'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (7) LS=133 PS=76
WRITE   (7) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Trade-Up Program Totals'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (2) LS=133 PS=76
WRITE   (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Members that Traded-Up policies'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (3) LS=133 PS=76
WRITE   (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Members that Received Trade-Up Letters'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (4) LS=133 PS=76
WRITE   (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Members that De-grandfathered through Trade-Up program'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (5) LS=133 PS=76
WRITE   (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Total Policies Traded-Up'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT  (6) LS=133 PS=76
WRITE   (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Total Amount Exchanged through Trade-Up program'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
FORMAT  (8) LS=133 PS=76
WRITE   (8) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Trade-up Errors - Information Missing'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
INPUT #PARM-INPUT                                                 /* PM7
*
READ WORK 1 ONCE RECORD #JCL-PARM                                 /* PM5
PERFORM FORMAT-DATES
*
COMPRESS 'ID Number' #TAB 'Issue Date' #TAB 'Trade-Up Date' #TAB  /* PM5
         '1035 Exchange Amount' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 2 #EXTRACT-RECORD
WRITE WORK 3 #EXTRACT-RECORD
WRITE WORK 4 #EXTRACT-RECORD    /* DW9
RESET #EXTRACT-RECORD
*
READ ST-V                                                         /* PM3
*  IF ID-NUMBER GE 600000                                            RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)                           /* RE1
   OR ID-NUMBER LT 1900                                           /* PM3
    ESCAPE TOP                                                    /* PM3
  END-IF
*
*  IF PROCESS-IND = 'S' OR = 'C'                                     RE1
  IF PROCESS-IND = 'C'                                            /* RE1
    ESCAPE TOP
  END-IF
*
  IF TRAD-UP-SENT-DT NE 0
   AND (NOT #START OR #START AND TRAD-UP-SENT-DT GE #START-DATE)  /* PM7
   AND (NOT #END OR #END AND TRAD-UP-SENT-DT LE #END-DATE)        /* PM7
    ADD +1 TO #TTL-LETTERS-SENT
    ADD +1 TO #CNT-3
    DISPLAY (3) 'Cnt-3' #CNT-3 'Member' ST-V.ID-NUMBER
                'Date Sent' TRAD-UP-SENT-DT
*
    MOVE TRAD-UP-SENT-YY TO #YY-INDEX
    MOVE TRAD-UP-SENT-MM TO #MM-INDEX
    ADD +1 TO #TTL-LETTERS-SENT-MNTH (#YY-INDEX,#MM-INDEX)
  END-IF
*
  RESET #HOLD-CN #HOLD-MM #HOLD-YY
  FOR #I6 1 TO 9
    IF ST-V.ISSUE-DATE (#I6) = 0
      ESCAPE BOTTOM
    END-IF
*
*   IF  (ST-V.PLAN (#I6) = 'RT' OR = 'LT')                        /* PM5     /* YK1
*    OR ((PLAN-A1 (#I6) GE 'U') AND (PLAN-A1 (#I6) LE 'Z'))                  /* YK1
    IF FUNCTION-TERM-POLICY(<ST-V.PLAN(#I6)>)                                /* YK1
       ESCAPE TOP
    END-IF
*
**** This next section is to remove known tu issue errors *********  PM2
    IF  (ST-V.ID-NUMBER = 023147 AND ST-V.ISSUE-DATE (#I6) = 20040318)
     OR (ST-V.ID-NUMBER = 023664 AND ST-V.ISSUE-DATE (#I6) = 20041012)
     OR (ST-V.ID-NUMBER = 023871 AND ST-V.ISSUE-DATE (#I6) = 20041229)
     OR (ST-V.ID-NUMBER = 034247 AND ST-V.ISSUE-DATE (#I6) = 20041028)
     OR (ST-V.ID-NUMBER = 035487 AND ST-V.ISSUE-DATE (#I6) = 20040723)
     OR (ST-V.ID-NUMBER = 040338 AND ST-V.ISSUE-DATE (#I6) = 20040413)
     OR (ST-V.ID-NUMBER = 042065 AND ST-V.ISSUE-DATE (#I6) = 20040407)
     OR (ST-V.ID-NUMBER = 042091 AND ST-V.ISSUE-DATE (#I6) = 20041116)
     OR (ST-V.ID-NUMBER = 046333 AND ST-V.ISSUE-DATE (#I6) = 20040624)
     OR (ST-V.ID-NUMBER = 046715 AND ST-V.ISSUE-DATE (#I6) = 20040720)
     OR (ST-V.ID-NUMBER = 050942 AND ST-V.ISSUE-DATE (#I6) = 20040330)
     OR (ST-V.ID-NUMBER = 051443 AND ST-V.ISSUE-DATE (#I6) = 20041208)
     OR (ST-V.ID-NUMBER = 053506 AND ST-V.ISSUE-DATE (#I6) = 20040514)
     OR (ST-V.ID-NUMBER = 053702 AND ST-V.ISSUE-DATE (#I6) = 20040723)
     OR (ST-V.ID-NUMBER = 055528 AND ST-V.ISSUE-DATE (#I6) = 20040303)
     OR (ST-V.ID-NUMBER = 056964 AND ST-V.ISSUE-DATE (#I6) = 20041005)
     OR (ST-V.ID-NUMBER = 057494 AND ST-V.ISSUE-DATE (#I6) = 20041116)
     OR (ST-V.ID-NUMBER = 057579 AND ST-V.ISSUE-DATE (#I6) = 20040609)
     OR (ST-V.ID-NUMBER = 066118 AND ST-V.ISSUE-DATE (#I6) = 20040625)
     OR (ST-V.ID-NUMBER = 067079 AND ST-V.ISSUE-DATE (#I6) = 20040802)
     OR (ST-V.ID-NUMBER = 069318 AND ST-V.ISSUE-DATE (#I6) = 20040818)
     OR (ST-V.ID-NUMBER = 073467 AND ST-V.ISSUE-DATE (#I6) = 20041022)
     OR (ST-V.ID-NUMBER = 077333 AND ST-V.ISSUE-DATE (#I6) = 20040927)
     OR (ST-V.ID-NUMBER = 077333 AND ST-V.ISSUE-DATE (#I6) = 20041021)
     OR (ST-V.ID-NUMBER = 082481 AND ST-V.ISSUE-DATE (#I6) = 20040519)
     OR (ST-V.ID-NUMBER = 083760 AND ST-V.ISSUE-DATE (#I6) = 20040914)
     OR (ST-V.ID-NUMBER = 084525 AND ST-V.ISSUE-DATE (#I6) = 20040127)
     OR (ST-V.ID-NUMBER = 105917 AND ST-V.ISSUE-DATE (#I6) = 20041118)
     OR (ST-V.ID-NUMBER = 039085 AND ST-V.ISSUE-DATE (#I6) = 20050125)
     OR (ST-V.ID-NUMBER = 034303 AND ST-V.ISSUE-DATE (#I6) = 20050126)
     OR (ST-V.ID-NUMBER = 073286 AND ST-V.ISSUE-DATE (#I6) = 20050126)
     OR (ST-V.ID-NUMBER = 079092 AND ST-V.ISSUE-DATE (#I6) = 20050127)
     OR (ST-V.ID-NUMBER = 100260 AND ST-V.ISSUE-DATE (#I6) = 20050104)
     OR (ST-V.ID-NUMBER = 057739 AND ST-V.ISSUE-DATE (#I6) = 20050726)
     OR (ST-V.ID-NUMBER = 043414 AND ST-V.ISSUE-DATE (#I6) = 20050926)
     OR (ST-V.ID-NUMBER = 053658 AND ST-V.ISSUE-DATE (#I6) = 20050517)
     OR (ST-V.ID-NUMBER = 066118 AND ST-V.ISSUE-DATE (#I6) = 20050608)
     OR (ST-V.ID-NUMBER = 046304 AND ST-V.ISSUE-DATE (#I6) = 20051207)
     OR (ST-V.ID-NUMBER = 061111 AND ST-V.ISSUE-DATE (#I6) = 20060313)
     OR (ST-V.ID-NUMBER = 064661 AND ST-V.ISSUE-DATE (#I6) = 20060505)
     OR (ST-V.ID-NUMBER = 081185 AND ST-V.ISSUE-DATE (#I6) = 20060727)
     OR (ST-V.ID-NUMBER = 079737 AND ST-V.ISSUE-DATE (#I6) = 20060816)
     OR (ST-V.ID-NUMBER = 039125 AND ST-V.ISSUE-DATE (#I6) = 20061031)
      ESCAPE TOP
    END-IF
**** end error section ********************************************  PM2
*
    FIND NN-V WITH ID-NUMBER = ST-V.ID-NUMBER
      PERFORM DETERMINE-TRADE-UP
*
      IF #IS-A-TRADE-UP
        RESET #CSV-FOUND #1035-FOUND
*
        IF PROCESS-IND NE 'G'
          ADD +1 TO #TTL-DEGRANDFATHERED
          ADD +1 TO #CNT-4
          DISPLAY (4) 'Cnt-4' #CNT-4
                    'De-grandfathered Member'
                     ST-V.ID-NUMBER 'Process Ind' PROCESS-IND
          ADD +1 TO #TTL-DEGRANDFATHERED-MNTH (#YY-INDEX,#MM-INDEX)
        END-IF
*
        FOR #I2 1 TO 60
          IF NN-V.NOTE-TEXT (#I2) = ' '
            ESCAPE BOTTOM
          END-IF
*
          IF ST-V.ID-NUMBER = 44896 OR = 54865 OR = 58863 OR = 79791
                         OR = 56964 OR = 66103 OR = 71696
            MOVE 45 TO #DAYS-CSV
          ELSE
            MOVE 6 TO #DAYS-CSV
          END-IF
*
          IF NN-V.NT-POL-ISS (#I2) = '* POL ISS' /* PM2
           AND NN-V.NT-POL-CSV (#I2) = "CSV EXC'D ON"
            CALLNAT 'GDATESXX'
              NN-V.NOTE-DATE (#I2)
              NN-V.NOTE-DATE (#I1)
              #DAYS-DIFF
              #ACTION-CODE
*
            IF #DAYS-DIFF LE #DAYS-CSV
              ADD +1 TO #TTL-POLICIES-TRADED
              ADD +1 TO #TTL-POLICIES-TRADED-MNTH (#YY-INDEX,#MM-INDEX)
              ADD +1 TO #CNT-5
              DISPLAY (5) 'Cnt-5' #CNT-5
                          'Member' ST-V.ID-NUMBER
                          'Policy Traded' NOTE-TEXT (#I2)
              MOVE TRUE TO #CSV-FOUND
            END-IF
          END-IF
*
          IF NN-V.NT-POL-ISS (#I2) = '*1035 EXC'
            IF NT-TXT-1035-POINT-4 (#I2) = '.' /* new .99 format  PM1
              COMPRESS NT-TXT-1035-YYYY-4 (#I2) NT-TXT-1035-MM-4 (#I2)
                    NT-TXT-1035-DD-4   (#I2) INTO #YYYYMMDD-A LEAVING NO
            ELSE
              COMPRESS NT-TXT-1035-YYYY-3 (#I2) NT-TXT-1035-MM-3 (#I2)
                    NT-TXT-1035-DD-3   (#I2) INTO #YYYYMMDD-A LEAVING NO
            END-IF
*
            IF  (#YYYYMMDD-A NE MASK (YYYYMMDD))
             OR (#YYYYMMDD NE #ISSUE-DATE)
              ESCAPE TOP
            END-IF
*
            MOVE TRUE TO #1035-FOUND
            RESET #WORK-EXCHANGE-AMOUNT #I4
                  #1035-EXCHANGE-AMOUNT #WORK-CENTS-AMOUNT
            FOR #I3 13 TO 60
              IF NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3) = ' ' OR = ','
                ESCAPE TOP
              END-IF
*
              IF NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3) = 'O'
                ESCAPE BOTTOM
              END-IF
*
              IF NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3) = '.'
                IF NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3+1) = MASK (N)
                 AND NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3+2) = MASK (N)
                  MOVE NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3+1)
                       TO #WORK-CENTS (1)
                  MOVE NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3+2)
                       TO #WORK-CENTS (2)
                  ESCAPE BOTTOM
                END-IF
              END-IF
*
              ADD +1 TO #I4
              MOVE NN-V.NT-1035-AMOUNT-CHAR (#I2,#I3)
                 TO #WORK-DIGIT (#I4)
            END-FOR
*
            IF #WORK-DIGIT (6) = ' ' /* only shift right if < $100,000
            MOVE 6 TO #I5       /* shift number to right for calculation
              FOR #I4 6 TO 1 STEP -1
                IF #WORK-DIGIT (#I4) = ' '
                  ESCAPE TOP
                ELSE
                  MOVE #WORK-DIGIT (#I4) TO #WORK-DIGIT (#I5)
                  SUBTRACT 1 FROM #I5
                  MOVE ' ' TO #WORK-DIGIT (#I4)
                END-IF
              END-FOR
            END-IF
*
            MOVE #WORK-AMOUNT-NUMERIC TO #1035-EXCHANGE-AMOUNT
            ADD #WORK-CENTS-NUMERIC TO #1035-EXCHANGE-AMOUNT
*
            PERFORM ADJUST-1035-AMOUNTS                           /* PM6
            MOVE EDITED #1035-EXCHANGE-AMOUNT (EM=ZZZ,ZZ9.99)
                  TO #DISPLAY-EXCHANGE-AMT
            COMPRESS '$' #DISPLAY-EXCHANGE-AMT INTO
                  #DISPLAY-EXCHANGE-AMT LEAVING NO
            ADD +1 TO #CNT-6
            DISPLAY (6) 'Cnt-6' #CNT-6 'Member' ST-V.ID-NUMBER
                        'Exchange Amt' #DISPLAY-EXCHANGE-AMT
            ADD #1035-EXCHANGE-AMOUNT TO #TTL-EXCHANGE-AMOUNT
            ADD #1035-EXCHANGE-AMOUNT TO
                #TTL-EXCHANGE-AMOUNT-MNTH (#YY-INDEX,#MM-INDEX)
          END-IF
        END-FOR
*
        IF NOT #CSV-FOUND
          WRITE (8) ST-V.ID-NUMBER 'CSV not found'
        END-IF
        IF NOT #1035-FOUND
          WRITE (8) ST-V.ID-NUMBER '1035 not found'
        END-IF
*
        IF NOTE-DATE (#I1) GE #START-DATE                         /* PM5
         AND NOTE-DATE (#I1) LE #END-DATE
          COMPRESS ST-V.ID-NUMBER #TAB
                   #TRADE-UP-ISSUE-DATE-DISP #TAB
                   #TRADE-UP-DATE #TAB
                   #DISPLAY-EXCHANGE-AMT INTO #EXTRACT-RECORD LEAVING NO
          WRITE WORK 2 #EXTRACT-RECORD
          RESET #EXTRACT-RECORD
        END-IF
*
        IF #TERMED                                                /* PM5
          COMPRESS ST-V.ID-NUMBER #TAB
                   #TRADE-UP-ISSUE-DATE-DISP #TAB
                   #TRADE-UP-DATE #TAB
                   #DISPLAY-EXCHANGE-AMT INTO #EXTRACT-RECORD LEAVING NO
          WRITE WORK 3 #EXTRACT-RECORD
          RESET #EXTRACT-RECORD
        END-IF
*
        IF NOTE-DATE (#I1) GE #YEAR-START-DATE                    /* PM8
         AND NOTE-DATE (#I1) LE #END-DATE                         /* PM8
          COMPRESS ST-V.ID-NUMBER #TAB                            /* PM8
                   #TRADE-UP-ISSUE-DATE-DISP #TAB                 /* PM8
                   #TRADE-UP-DATE #TAB                            /* PM8
                   #DISPLAY-EXCHANGE-AMT INTO #EXTRACT-RECORD LEAVING NO
          WRITE WORK 4 #EXTRACT-RECORD                            /* PM8
          RESET #EXTRACT-RECORD                                   /* PM8
        END-IF                                                    /* PM8
      END-IF
    END-FIND
  END-FOR
END-READ
*
MOVE EDITED #TTL-LETTERS-SENT (EM=Z,ZZZ,ZZ9) TO #DISPLAY-LETTERS-SENT
MOVE EDITED #TTL-DEGRANDFATHERED (EM=Z,ZZZ,ZZ9) TO #DISPLAY-DEGRAND
MOVE EDITED #TTL-MEMBERS-TU (EM=Z,ZZZ,ZZ9) TO #DISPLAY-MEMBERS-TU
MOVE EDITED #TTL-POLICIES-TRADED (EM=Z,ZZZ,ZZ9) TO #DISPLAY-POLICIES-TU
*
MOVE EDITED #TTL-EXCHANGE-AMOUNT (EM=ZZ,ZZZ,ZZZ,ZZ9.99)           /* PM1
     TO #DISPLAY-EXCHANGE-AMT
COMPRESS '$' #DISPLAY-EXCHANGE-AMT INTO
     #DISPLAY-EXCHANGE-AMT LEAVING NO
*
COMPUTE ROUNDED #AVG-POLICIES-TRADED = #TTL-POLICIES-TRADED /
                                       #TTL-MEMBERS-TU
*
COMPUTE #AVG-EXCHANGE-AMT = #TTL-EXCHANGE-AMOUNT / #TTL-MEMBERS-TU
MOVE EDITED #AVG-EXCHANGE-AMT (EM=ZZ,ZZZ,ZZZ,ZZ9.99)              /* PM1
     TO #DISPLAY-AVG-EXCHANGE-AMT
COMPRESS '$' #DISPLAY-AVG-EXCHANGE-AMT INTO
     #DISPLAY-AVG-EXCHANGE-AMT LEAVING NO
*
COMPUTE ROUNDED #PCT-DEGRANDFATHERED = #TTL-DEGRANDFATHERED
                             / #TTL-MEMBERS-TU * 100
MOVE EDITED #PCT-DEGRANDFATHERED (EM=ZZZ9.9) TO #DISPLAY-PCT-DEG
COMPRESS #DISPLAY-PCT-DEG '%' INTO #DISPLAY-PCT-DEG
     LEAVING NO
*
COMPUTE ROUNDED #PCT-MEMBERS-TU = #TTL-MEMBERS-TU / #TTL-LETTERS-SENT
                                * 100
MOVE EDITED #PCT-MEMBERS-TU (EM=ZZ9.9) TO #DISPLAY-PCT-TU
COMPRESS #DISPLAY-PCT-TU '%' INTO #DISPLAY-PCT-TU LEAVING NO
*
WRITE (1) ' '
WRITE (1) 'Total Members Traded-Up       =  ' #DISPLAY-MEMBERS-TU
WRITE (1) 'Pct Members Traded-Up         =      ' #DISPLAY-PCT-TU
WRITE (1) 'Total Letters Sent            =  ' #DISPLAY-LETTERS-SENT
WRITE (1) 'Total Members Degrandfathered =  ' #DISPLAY-DEGRAND
WRITE (1) 'Pct Members Degrandfathered   =         ' #DISPLAY-PCT-DEG
WRITE (1) 'Total # Policies Traded-Up    =  ' #DISPLAY-POLICIES-TU
WRITE (1) 'Average # Policies Traded-Up  =  ' #AVG-POLICIES-TRADED
WRITE (1) ' '
WRITE (1) 'Total Amount Exchanged        = ' #DISPLAY-EXCHANGE-AMT
WRITE (1) 'Average Amount Exchanged      = ' #DISPLAY-AVG-EXCHANGE-AMT
*
WRITE (7) ' '
WRITE (7) 'Total Members Traded-Up       =  ' #DISPLAY-MEMBERS-TU
WRITE (7) 'Pct Members Traded-Up         =      ' #DISPLAY-PCT-TU
WRITE (7) 'Total Letters Sent            =  ' #DISPLAY-LETTERS-SENT
WRITE (7) 'Total Members Degrandfathered =  ' #DISPLAY-DEGRAND
WRITE (7) 'Pct Members Degrandfathered   =         ' #DISPLAY-PCT-DEG
WRITE (7) 'Total # Policies Traded-Up    =  ' #DISPLAY-POLICIES-TU
WRITE (7) 'Average # Policies Traded-Up  =  ' #AVG-POLICIES-TRADED
WRITE (7) ' '
WRITE (7) 'Total Amount Exchanged        = ' #DISPLAY-EXCHANGE-AMT
WRITE (7) 'Average Amount Exchanged      = ' #DISPLAY-AVG-EXCHANGE-AMT
*
WRITE (2) 'Total Members Traded-Up       =  ' #DISPLAY-MEMBERS-TU
WRITE (2) 'Pct Members Traded-Up         =      ' #DISPLAY-PCT-TU
WRITE (3) 'Total Letters Sent            =  ' #DISPLAY-LETTERS-SENT
WRITE (4) 'Total Members Degrandfathered =  ' #DISPLAY-DEGRAND
WRITE (4) 'Pct Members Degrandfathered   =         ' #DISPLAY-PCT-DEG
WRITE (5) 'Total # Policies Traded-Up    =  ' #DISPLAY-POLICIES-TU
WRITE (5) 'Average # Policies Traded-Up  =  ' #AVG-POLICIES-TRADED
WRITE (6) 'Total Amount Exchanged        = ' #DISPLAY-EXCHANGE-AMT
WRITE (6) 'Average Amount Exchanged      = ' #DISPLAY-AVG-EXCHANGE-AMT
*
***** Unload totals by month *******************************************
*
FOR #YY-INDEX 1 TO 50
  FOR #MM-INDEX 1 TO 12
    IF #TTL-LETTERS-SENT-MNTH (#YY-INDEX,#MM-INDEX) = 0
     AND #TTL-DEGRANDFATHERED-MNTH (#YY-INDEX,#MM-INDEX) = 0
     AND #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX) = 0
     AND #TTL-POLICIES-TRADED-MNTH (#YY-INDEX,#MM-INDEX) = 0
     AND #TTL-EXCHANGE-AMOUNT-MNTH (#YY-INDEX,#MM-INDEX) = 0
      ESCAPE TOP
    END-IF
*
    NEWPAGE (1)
    NEWPAGE (7)
    MOVE EDITED #YY-INDEX (EM=99) TO #HEADER
    COMPRESS MONTH-LETTER (#MM-INDEX) '-20' #HEADER INTO
                #HEADER LEAVING NO
    COMPRESS 'Trade-Up Statistics for' #HEADER INTO #HEADER
    WRITE (1) #HEADER
    WRITE (1)'---------------------------------------------'
    WRITE (7) #HEADER
    WRITE (7)'---------------------------------------------'
*
    MOVE EDITED #TTL-LETTERS-SENT-MNTH (#YY-INDEX,#MM-INDEX)
                (EM=Z,ZZZ,ZZ9) TO #DISPLAY-LETTERS-SENT
    MOVE EDITED #TTL-DEGRANDFATHERED-MNTH (#YY-INDEX,#MM-INDEX)
                (EM=Z,ZZZ,ZZ9) TO #DISPLAY-DEGRAND
    MOVE EDITED #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX)
                (EM=Z,ZZZ,ZZ9) TO #DISPLAY-MEMBERS-TU
    MOVE EDITED #TTL-POLICIES-TRADED-MNTH (#YY-INDEX,#MM-INDEX)
                (EM=Z,ZZZ,ZZ9) TO #DISPLAY-POLICIES-TU
*
    MOVE EDITED #TTL-EXCHANGE-AMOUNT-MNTH (#YY-INDEX,#MM-INDEX)
                (EM=ZZ,ZZZ,ZZZ,ZZ9.99) TO #DISPLAY-EXCHANGE-AMT   /* PM1
    COMPRESS '$' #DISPLAY-EXCHANGE-AMT INTO
         #DISPLAY-EXCHANGE-AMT LEAVING NO
*
    COMPUTE ROUNDED #AVG-POLICIES-TRADED =
         #TTL-POLICIES-TRADED-MNTH (#YY-INDEX,#MM-INDEX) /
         #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX)
*
    COMPUTE #AVG-EXCHANGE-AMT =
         #TTL-EXCHANGE-AMOUNT-MNTH (#YY-INDEX,#MM-INDEX) /
         #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX)
    MOVE EDITED #AVG-EXCHANGE-AMT (EM=ZZ,ZZZ,ZZZ,ZZ9.99)          /* PM1
         TO #DISPLAY-AVG-EXCHANGE-AMT
    COMPRESS '$' #DISPLAY-AVG-EXCHANGE-AMT INTO
         #DISPLAY-AVG-EXCHANGE-AMT LEAVING NO
*
    COMPUTE ROUNDED #PCT-DEGRANDFATHERED =
         #TTL-DEGRANDFATHERED-MNTH (#YY-INDEX,#MM-INDEX) /
         #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX) * 100
    MOVE EDITED #PCT-DEGRANDFATHERED (EM=ZZZ9.9) TO #DISPLAY-PCT-DEG
    COMPRESS #DISPLAY-PCT-DEG '%' INTO #DISPLAY-PCT-DEG
         LEAVING NO
*
*   COMPUTE ROUNDED #PCT-MEMBERS-TU =
*        #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX) /
*        #TTL-LETTERS-SENT-MNTH (#YY-INDEX,#MM-INDEX) * 100
*   MOVE EDITED #PCT-MEMBERS-TU (EM=ZZ9.9) TO #DISPLAY-PCT-TU
*   COMPRESS #DISPLAY-PCT-TU '%' INTO #DISPLAY-PCT-TU LEAVING NO
*
    WRITE (1) ' '
    WRITE (1) 'Total Members Traded-Up       =  ' #DISPLAY-MEMBERS-TU
*   WRITE (1) 'Pct Members Traded-Up         =      ' #DISPLAY-PCT-TU
    WRITE (1) 'Total Letters Sent            =  ' #DISPLAY-LETTERS-SENT
    WRITE (1) 'Total Members Degrandfathered =  ' #DISPLAY-DEGRAND
    WRITE (1) 'Pct Members Degrandfathered   =         '
                                                  #DISPLAY-PCT-DEG
    WRITE (1) 'Total # Policies Traded-Up    =  ' #DISPLAY-POLICIES-TU
    WRITE (1) 'Average # Policies Traded-Up  =  ' #AVG-POLICIES-TRADED
    WRITE (1) ' '
    WRITE (1) 'Total Amount Exchanged        = ' #DISPLAY-EXCHANGE-AMT
    WRITE (1) 'Average Amount Exchanged      = '
                                               #DISPLAY-AVG-EXCHANGE-AMT
*
    WRITE (7) ' '
    WRITE (7) 'Total Members Traded-Up       =  ' #DISPLAY-MEMBERS-TU
*   WRITE (7) 'Pct Members Traded-Up         =      ' #DISPLAY-PCT-TU
    WRITE (7) 'Total Letters Sent            =  ' #DISPLAY-LETTERS-SENT
    WRITE (7) 'Total Members Degrandfathered =  ' #DISPLAY-DEGRAND
    WRITE (7) 'Pct Members Degrandfathered   =         '
                                                  #DISPLAY-PCT-DEG
    WRITE (7) 'Total # Policies Traded-Up    =  ' #DISPLAY-POLICIES-TU
    WRITE (7) 'Average # Policies Traded-Up  =  ' #AVG-POLICIES-TRADED
    WRITE (7) ' '
    WRITE (7) 'Total Amount Exchanged        = ' #DISPLAY-EXCHANGE-AMT
    WRITE (7) 'Average Amount Exchanged      = '
                                               #DISPLAY-AVG-EXCHANGE-AMT
  END-FOR
END-FOR
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-TRADE-UP
************************************************************************
*
RESET #IS-A-TRADE-UP #TRADE-UP-ISSUE-DATE #TRADE-UP-DATE
      #TRADE-UP-ISSUE-DATE-DISP
*
FOR #I1 1 TO 60
  IF NN-V.NT-TRADE-UP (#I1) = 'IS A TRADE-UP'
   AND (NOT #START OR #START AND NN-V.NOTE-DATE (#I1) GE #START-DATE)
   AND (NOT #END OR #END AND NN-V.NOTE-DATE (#I1) LE #END-DATE)/* PM7
    MOVE EDITED NT-TU-ISSUE-MM (#I1) (EM=99)     TO #TRADE-UP-ISSUE-MM
    MOVE EDITED NT-TU-ISSUE-DD (#I1) (EM=99)     TO #TRADE-UP-ISSUE-DD
    MOVE EDITED NT-TU-ISSUE-YYYY (#I1) (EM=9999) TO #TRADE-UP-ISSUE-YYYY
*
    IF ST-V.ISSUE-DATE (#I6) EQ #ISSUE-DATE
      MOVE TRUE TO #IS-A-TRADE-UP
      COMPRESS #TRADE-UP-ISSUE-MM '/' #TRADE-UP-ISSUE-DD '/'
          #TRADE-UP-ISSUE-YYYY INTO #TRADE-UP-ISSUE-DATE-DISP LEAVING NO
*
      MOVE EDITED NT-DATE-MM (#I1) (EM=99) TO #TRADE-UP-DATE-MM
      MOVE EDITED NT-DATE-DD (#I1) (EM=99) TO #TRADE-UP-DATE-DD
      MOVE EDITED NT-DATE-YYYY (#I1) (EM=9999) TO #TRADE-UP-DATE-YYYY
      COMPRESS #TRADE-UP-DATE-MM '/' #TRADE-UP-DATE-DD '/'
               #TRADE-UP-DATE-YYYY INTO #TRADE-UP-DATE LEAVING NO
      ADD +1 TO #CNT-2
      DISPLAY (2) 'Cnt-2' #CNT-2 'Member #' ST-V.ID-NUMBER
                  'Trade-Up Policy' NT-TU-ISSUE-DATE (#I1)
                  'Trade-Up Date' #TRADE-UP-DATE
*
      RESET #TERMED                                               /* PM5
      IF ST-V.STATUS (#I6) NE 'D'
**** the following have been identified and are legitimate *******  PM2
        IF (ST-V.ID-NUMBER = 33816 AND ST-V.ISSUE-DATE (#I6) = 20040625)
        OR (ST-V.ID-NUMBER = 44568 AND ST-V.ISSUE-DATE (#I6) = 20040609)
        OR (ST-V.ID-NUMBER = 57219 AND ST-V.ISSUE-DATE (#I6) = 20040809)
        OR (ST-V.ID-NUMBER = 74875 AND ST-V.ISSUE-DATE (#I6) = 20040616)
        OR (ST-V.ID-NUMBER = 93700 AND ST-V.ISSUE-DATE (#I6) = 20040723)
          IGNORE
        ELSE
          MOVE TRUE TO #TERMED                                    /* PM5
          WRITE (8) ST-V.ID-NUMBER ' tu policy not active'
        END-IF
      END-IF
      MOVE NT-DATE-YY (#I1) TO #YY-INDEX
      MOVE NT-DATE-MM (#I1) TO #MM-INDEX
      IF ST-V.ID-NUMBER NE #HOLD-CN
       OR (NT-DATE-YY (#I1) NE #HOLD-YY
       AND NT-DATE-MM (#I1) NE #HOLD-MM)
        ADD +1 TO #TTL-MEMBERS-TU
        ADD +1 TO #TTL-MEMBERS-TU-MNTH (#YY-INDEX,#MM-INDEX)
        MOVE ST-V.ID-NUMBER TO #HOLD-CN
        MOVE NT-DATE-YY (#I1) TO #HOLD-YY
        MOVE NT-DATE-MM (#I1) TO #HOLD-MM
      END-IF
*
      ESCAPE BOTTOM
    END-IF
  END-IF
*
  IF NN-V.NOTE-TEXT (#I1) = ' '
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE FORMAT-DATES
************************************************************************
*
RESET #START #END                                                 /* PM7
IF #PARM-START-DATE = 00000000                                    /* PM7
  MOVE #JCL-YYYY TO #START-DATE-YYYY
  MOVE #JCL-MM   TO #START-DATE-MM
  MOVE #JCL-DD   TO #START-DATE-DD
*
  SUBTRACT 1 FROM #START-DATE-MM
  IF #START-DATE-MM = 0
    MOVE 12 TO #START-DATE-MM
    SUBTRACT 1 FROM #START-DATE-YYYY
  END-IF
ELSE                                                              /* PM7
  MOVE #PARM-START-DATE TO #START-DATE                            /* PM7
  MOVE TRUE TO #START                                             /* PM7
END-IF                                                            /* PM7
*
IF #PARM-END-DATE = 00000000                                      /* PM7
  CALLNAT 'GDATESXX'
    #END-DATE
    #START-DATE
    #YYYYMMDD
    #ACTION-FLAG
ELSE                                                              /* PM7
  MOVE #PARM-END-DATE TO #END-DATE                                /* PM7
  MOVE TRUE TO #END                                               /* PM7
END-IF                                                            /* PM7
*
IF #PARM-START-DATE = 00000000                                    /* PM8
 AND #PARM-END-DATE = 00000000                                    /* PM8
  MOVE #START-DATE TO  #YEAR-START-DATE                           /* PM8
  MOVE 01 TO #YEAR-DATE-MM                                        /* PM8
  MOVE 01 TO #YEAR-DATE-DD                                        /* PM8
END-IF                                                            /* PM8
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE ADJUST-1035-AMOUNTS                             /* PM6
************************************************************************
*
IF NN-V.ID-NUMBER = 89507 AND #1035-EXCHANGE-AMOUNT = 62991.37
  MOVE 21749.72 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 44376 AND #1035-EXCHANGE-AMOUNT = 85615.54
  MOVE 24824.99 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 71085 AND #1035-EXCHANGE-AMOUNT = 46728.58
  MOVE 12613.35 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 64931 AND #1035-EXCHANGE-AMOUNT = 42855.82
  MOVE 14819.46 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 71696 AND #1035-EXCHANGE-AMOUNT = 9900.26
  MOVE 5609.83 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 66103 AND #1035-EXCHANGE-AMOUNT = 41322.30
  MOVE 10760.12 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 71056 AND #1035-EXCHANGE-AMOUNT = 137149.03
  MOVE 19149.16 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 61899 AND #1035-EXCHANGE-AMOUNT = 29380.06
  MOVE 11372.23 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 67661 AND #1035-EXCHANGE-AMOUNT = 29094.81
  MOVE 9268.93 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 51243 AND #1035-EXCHANGE-AMOUNT = 33396.93
  MOVE 21422.74 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 39594 AND #1035-EXCHANGE-AMOUNT = 19342.60
  MOVE 19042.30 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 34388 AND #1035-EXCHANGE-AMOUNT = 29878.24
  MOVE 20270.52 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 36189 AND #1035-EXCHANGE-AMOUNT = 46764.96
  MOVE 27749.96 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 58437 AND #1035-EXCHANGE-AMOUNT = 51150.24
  MOVE 8359.14 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 42536 AND #1035-EXCHANGE-AMOUNT = 164974.28
  MOVE 51005.41 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 43490 AND #1035-EXCHANGE-AMOUNT = 65007.65
  MOVE 20072.15 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 62775 AND #1035-EXCHANGE-AMOUNT = 36865.71
  MOVE 13912.31 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 54714 AND #1035-EXCHANGE-AMOUNT = 22184.00
  MOVE 16085.39 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 66334 AND #1035-EXCHANGE-AMOUNT = 52267.36
  MOVE 16366.00 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 61387 AND #1035-EXCHANGE-AMOUNT = 29642.31
  MOVE 14443.14 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 55826 AND #1035-EXCHANGE-AMOUNT = 55485.70
  MOVE 18056.14 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 43831 AND #1035-EXCHANGE-AMOUNT = 27598.22
  MOVE 27605.94 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 70895 AND #1035-EXCHANGE-AMOUNT = 122887.48
  MOVE 4869.33 TO #1035-EXCHANGE-AMOUNT
END-IF
*
IF NN-V.ID-NUMBER = 54940 AND #1035-EXCHANGE-AMOUNT = 68683.22
  MOVE 24515.49 TO #1035-EXCHANGE-AMOUNT
END-IF
*
END-SUBROUTINE
*
END
