* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram POSN0015.
/**
/** :author nguentchev
/*
/* Converts new policy ownership data to the old BD format
* bz-Name-C 07/07/2017     Fine Names. Keep all capital
/*
DEFINE DATA
PARAMETER
1 #CN            (N6)
1 #ISN           (P8)
1 #SEGMENT       (N1)
1 #TEMPDATA      (L)
LOCAL USING AUCADA01
LOCAL USING POSLROLE
LOCAL
1 #POLICY-NUMBER (A16)
1 #LEGACY        (L)
1 NT-F-V VIEW OF A-BENE-FINAL
  2 ADD-USER           (A8)
  2 ID-NUMBER          (N6)
  2 NOTE-DATE          (N8/60)
  2 NOTE-CLERK-ID      (A3/60)
  2 NOTE-TEXT          (A60/60)
  2 BENE-CONTACT-ID    (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
/*
1 NT-V
  2 ADD-USER            (A8)
  2 ID-NUMBER           (N6)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)  /* (D)
      4 BENE-RESTRICT    (A1)  /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1) /* (D, P/A)
        5 BENE-SHARE-TYP   (A1) /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 BENE-TEXTS-6-POLS (6)
      4 BENE-TEXT-PRIM-SEC (2)
        5 BENE-TEXT-OCC    (5)
          6 BENE-DESIG-2    (A60)
          6 REDEFINE BENE-DESIG-2
            7 BENE-BN-NAME        (A25)
            7 BENE-BN-SSN         (A9)
            7 BENE-BN-REL         (A1)
            7 BENE-BN-SHARE       (A10)
            7 BENE-BN-SET-OPT     (A1)
            7 BENE-BN-RESTRICTION (A1)
            7 BENE-BN-DOB-MM      (A2)
            7 BENE-BN-DOB-DD      (A2)
            7 BENE-BN-DOB-YYYY    (A4)
            7 BENE-BN-NEW-FILL    (A5)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
  2 NOTE-DATE  (N8/60) /* P/A = PRIN/ALT DESIG
  2 REDEFINE NOTE-DATE /* L = linked pols
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7) /* (D, L)
      4 BENE-SPLIT-IND    (N1/7) /* (D, L)
      4 BENE-LST-UPD      (N8)   /* (D)
      4 BENE-STRT-PEND    (N8)   /* (D)
      4 BENE-FOL-UP-IND   (N1)   /* (D)
      4 BENE-OWN-TRNSF-DT (N8)   /* (D)
      4 BENE-SUC-OWN      (N1)   /* (d)
      4 BENE-NEW-LAYOUT   (N1)   /* (D)/* was -setl-opt
      4 BENE-COM-DIS      (N2)   /* (D)/* Common disaster not used
      4 BENE-PER-STR      (N1)   /* (D)/* Per Stirpes not user
      4 BENE-FIL1         (A1)
  2 LST-BD-FINALIZATION-USER (A3)
/*
1 PC-V VIEW OF A-POLICY-CONTACTS
  2 ADD-USER      (A8)
  2 ADD-DATE      (N8)
  2 LAST-UPD-DATE (N8)
  2 RELATION      (N2)
1 DS-V VIEW OF A-DESIGNATION
  2 PRIMARY-BD (5)
  2 SECONDARY-BD (5)
  2 INSTRUCTIONS (A70/1:8)
  2 CONTINGENT-INSTR (A70/1:5)
1 CT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 SSN
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 DATE-OF-BIRTH (N8) 2 REDEFINE DATE-OF-BIRTH
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
/*
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID  (N8)
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9)
  2 SPLIT-IND     (9)
/*
1 CREL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
/*
1 BLANK   (A1) CONST <' '>

1 J   (I2)
1 K   (I2)
1 L   (I2)
1 J1  (I2)
1 J2  (I2)
1 J3  (I2)
1 #DATE-TRUN  (N8) 1 REDEFINE #DATE-TRUN
  2 #CENTRY  (A2)
  2 #YYMMDD  (N6)
END-DEFINE
/*
IF #CN <= 0
  ESCAPE ROUTINE
END-IF
RESET J1 J2 J3
  NT-V
/*
#LEGACY := TRUE
FIND PC-V WITH REFERENCE-NUMBER = #CN
  ACCEPT IF RELATION = PrimaryBene OR= ContingentBene
    OR= UnformatedBene OR= UnformContBene
  IF PC-V.ADD-USER = 'LOD'
      AND PC-V.ADD-DATE = PC-V.LAST-UPD-DATE
    IGNORE
  ELSE
    RESET #LEGACY
    ESCAPE BOTTOM
  END-IF
END-FIND
IF #LEGACY
  RESET #TEMPDATA
  ESCAPE ROUTINE
END-IF
IF #SEGMENT <= 0
  #SEGMENT := 1
END-IF
FIND (1) ST-V WITH ID-NUMBER = #CN
  FIND (1) CT-V WITH CONTACT-ID = ST-V.MEMBER-CONTACT-ID
    DesignationPolicies.InsuredContactID := CT-V.CONTACT-ID
    CALLNAT 'AUCNDAM1' DesignationPolicies
    IF C@Designation <= 0 /* No final designation
      RESET #ISN #TEMPDATA
      ESCAPE ROUTINE
    END-IF
    IF #SEGMENT > C@Designation
      #SEGMENT := C@Designation
    END-IF
    FOR K 1 TO C@Policy_002 (#SEGMENT)
      IF DesignationPolicies.InSameDesignation (#SEGMENT,K)
        #POLICY-NUMBER := DesignationPolicies.PolicyNumber (#SEGMENT,K)
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
*   FOR J 1 TO C*ACTIVE-SEGMENTS
*     IF STATUS (J) = 'D'
*       ADD 1 TO J1
*       IF J1 = #SEGMENT
*         #POLICY-NUMBER := POLICY-ID (J)
*         ESCAPE BOTTOM
*       END-IF
*     END-IF
*   END-FOR
END-FIND
/*
NT-V.ID-NUMBER := #CN
NT-V.ADD-USER  := 'BDBENE'
/*
*  BENE-STRT-PEND    :=
*  BENE-FOL-UP-IND   :=
*  BENE-OWN-TRNSF-DT :=
*  BENE-SUC-OWN      :=
FOR J 1 TO C@Designation
  NT-V.BENE-NEW-LAYOUT (J) := 1
  NT-V.BENE-STATUS  (J) := 'F'
  NT-V.BENE-LST-UPD (J) := DateSigned (J)
  IF RestrictIrrevocable (J)
    NT-V.BENE-RESTRICT (J) := 'Y' /* Y or something else
  END-IF
  NT-V.BENE-COM-DIS      (J) := CommonDisaster (J)
*   IF DesignationPolicies.PerStirpesPrimary (J)
*     NT-V.BENE-PER-STR    (J) := 1
*   END-IF
  IF PerStirpesContingent (J)
    NT-V.BENE-PER-STR     (J) := 1
  END-IF
  RESET J2
  FOR K 1 TO C@Policy_002 (J)
    IF DesignationPolicies.InSameDesignation (J,K)
      ADD 1 TO J2
      #DATE-TRUN := IssueDate (J,K)
      NT-V.BENE-ISS-DT (J,J2) := #YYMMDD
      FOR J1 1 TO C*ACTIVE-SEGMENTS
        IF ST-V.POLICY-ID (J1) = PolicyNumber (J,K)
          NT-V.BENE-SPLIT-IND (J,J2) := ST-V.SPLIT-IND (J1)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-IF
  END-FOR
/*
*   FOR J2 1 TO C@Policy_002 (J)
*     IF InSameDesignation (J, J2)
*         AND PolicyNumber (J,J2) = #POLICY-NUMBER
*       #SEGMENT := J
*     END-IF
*   END-FOR
*
*   NT-V.BENE-TYPE (J,1) := 'B'
*   NT-V.BENE-TYPE (J,2) := 'B'
*   IF DesignationPolicies.AllChildrenContingent (J)
*     NT-V.BENE-TYPE (J,2) := 'A'
*   END-IF
  RESET J2 J3
  FOR L 1 TO C@DesignationContact (J)
    IF BeneTypeCode (J,L) = UnformatedBene
      IF DesignationPolicies.PerStirpesPrimary (J)
*         AND DesignationPolicies.BDbeneType (J,L) EQ BLANK
        NT-V.BENE-TYPE  (J,1) := 'S'
      ELSE
        NT-V.BENE-TYPE  (J,1)  := DesignationPolicies.BDbeneType (J,L)
      END-IF
      NT-V.BENE-SHARE-TYP  (J,1)  :=
        DesignationPolicies.BDbeneShareType (J,L)
      FIND DS-V WITH DESIGNATION-ID =
          DesignationPolicies.DesignationID (J)
        IF DesignationPolicies.PrimaryInstruction (J) NE BLANK
          NT-V.BENE-DESIG-2 (J,1,1:5) := DS-V.INSTRUCTIONS (1:5)
        ELSE
          NT-V.BENE-DESIG-2 (J,1,1:5) := DS-V.PRIMARY-BD (1:5)
        END-IF
      END-FIND
    END-IF
    IF BeneTypeCode (J,L) = UnformContBene
      IF PerStirpesContingent (J)
          AND DesignationPolicies.BDbeneType (J,L) EQ BLANK
        NT-V.BENE-TYPE  (J,2) := 'S'
      ELSE
        NT-V.BENE-TYPE  (J,2)  := DesignationPolicies.BDbeneType (J,L)
      END-IF
      NT-V.BENE-SHARE-TYP  (J,2)  :=
        DesignationPolicies.BDbeneShareType (J,L)
      IF DesignationPolicies.BDbeneType (J,L) NE 'C' /* Cntnuation
        FIND DS-V WITH DESIGNATION-ID =
            DesignationPolicies.DesignationID (J)
          IF DesignationPolicies.ContingentInstruction (J) NE BLANK
            NT-V.BENE-DESIG-2 (J,2,1:5) := DS-V.CONTINGENT-INSTR (1:5)
          ELSE
            NT-V.BENE-DESIG-2 (J,2,1:5) := DS-V.SECONDARY-BD (1:5)
          END-IF
        END-FIND
      ELSE
        NT-V.BENE-DESIG-2 (J,2,1:5) := DS-V.CONTINGENT-INSTR (1:5)
      END-IF
    END-IF
    IF BeneficiaryType (J,L) = 'Primary'
      ADD 1 TO J2
      IF J2 > 5  /* LEGACY BD RESTRICTION
        ESCAPE TOP
      END-IF
      IF DesignationPolicies.PerStirpesPrimary (J)
*          AND DesignationPolicies.BDbeneType (J,L) EQ BLANK
        NT-V.BENE-TYPE  (J,1) := 'S'
      ELSE
        IF DesignationPolicies.BDbeneType (J,L) NE BLANK
          NT-V.BENE-TYPE  (J,1) := DesignationPolicies.BDbeneType (J,L)
        END-IF
      END-IF
      IF DesignationPolicies.BDbeneShareType (J,L) NE BLANK
        NT-V.BENE-SHARE-TYP  (J,1) :=
          DesignationPolicies.BDbeneShareType (J,L)
      END-IF
      NT-V.BENE-BN-NAME    (J,1,J2) := BeneficiaryName (J,L)
      NT-V.BENE-BN-SSN     (J,1,J2) := BeneficiarySSN  (J,L)
      IF Share (J,L) NE '0'
        NT-V.BENE-BN-SHARE   (J,1,J2) := Share (J,L)
        IF NT-V.BENE-BN-SHARE   (J,1,J2) IS (N3.2)
          COMPRESS NT-V.BENE-BN-SHARE   (J,1,J2) '%' INTO
            NT-V.BENE-BN-SHARE   (J,1,J2)
        END-IF
      ELSE
        RESET NT-V.BENE-BN-SHARE   (J,1,J2)
      END-IF
      IF SettlementCode (J,L) = '0'
        RESET SettlementCode (J,L)
      END-IF
      NT-V.BENE-BN-SET-OPT (J,1,J2) := SettlementCode (J,L)
      IF RestrictedCode (J,L)
        NT-V.BENE-BN-RESTRICTION (J,1,J2) := 'Y'
      END-IF
      BENE-CONTACT-ID-R (J,1,J2) := BeneficiaryContactID (J,L)
      FIND CT-V WITH CONTACT-ID = BeneficiaryContactID (J,L)
        BENE-BN-DOB-MM  (J,1,J2)   := CT-V.MM
        BENE-BN-DOB-DD  (J,1,J2)   := CT-V.DD
        BENE-BN-DOB-YYYY  (J,1,J2) := CT-V.YYYY
        NT-V.BENE-BN-NAME (J,1,J2) :=
          FUNCTION-CONTACT-NAME(< CT-V.CONTACT-ID, VERSION(J,L) >)
          NT-V.BENE-BN-NAME (J,1,J2) := *TRANSLATE(NT-V.BENE-BN-NAME (J,1,J2) , UPPER) /* bz-Name
*        NT-V.BENE-BN-SSN     (J,1,J2) := CT-V.SSN
        MOVE EDITED CT-V.SSN (EM=9(9)) TO NT-V.BENE-BN-SSN (J,1,J2)
        FIND CREL-V WITH CONTACT-1-ID = InsuredContactID
            AND CONTACT-2-ID = CT-V.CONTACT-ID
*           IF ROLE-2-CD = 'K'
*             ROLE-2-CD := 'C'
*           END-IF
          BENE-BN-REL (J,1,J2) := ROLE-2-CD
        END-FIND
      END-FIND
    END-IF
    IF BeneficiaryType (J,L) = 'Contingent'
      ADD 1 TO J3
      IF J3 > 4  /* LEGACY BD RESTRICTION
        ESCAPE TOP
      END-IF
      IF PerStirpesContingent (J)
          AND DesignationPolicies.BDbeneType (J,L) EQ BLANK
        NT-V.BENE-TYPE  (J,2) := 'S'
      ELSE
        IF DesignationPolicies.BDbeneType (J,L) NE BLANK
          NT-V.BENE-TYPE  (J,2)  := DesignationPolicies.BDbeneType (J,L)
        END-IF
      END-IF
      IF DesignationPolicies.BDbeneShareType (J,L) NE BLANK
        NT-V.BENE-SHARE-TYP  (J,2)  :=
          DesignationPolicies.BDbeneShareType (J,L)
      END-IF
      BENE-BN-NAME    (J,2,J3) := BeneficiaryName (J,L)
      BENE-BN-SSN     (J,2,J3) := BeneficiarySSN  (J,L)
      IF Share (J,L) NE '0'
        BENE-BN-SHARE   (J,2,J3) := Share (J,L)
        IF NT-V.BENE-BN-SHARE   (J,2,J3) IS (N3.2)
          COMPRESS NT-V.BENE-BN-SHARE   (J,2,J3) '%' INTO
            NT-V.BENE-BN-SHARE   (J,2,J3)
        END-IF
      ELSE
        RESET BENE-BN-SHARE   (J,2,J3)
      END-IF
      IF SettlementCode (J,L) = '0'
        RESET SettlementCode (J,L)
      END-IF
      BENE-BN-SET-OPT (J,2,J3) := SettlementCode (J,L)
      IF RestrictedCode (J,L)
        BENE-BN-RESTRICTION (J,2,J3) := 'Y'
      END-IF
      BENE-CONTACT-ID-R (J,2,J3) := BeneficiaryContactID (J,L)
      FIND CT-V WITH CONTACT-ID = BeneficiaryContactID (J,L)
        BENE-BN-DOB-MM  (J,2,J3)   := CT-V.MM
        BENE-BN-DOB-DD  (J,2,J3)   := CT-V.DD
        BENE-BN-DOB-YYYY  (J,2,J3) := CT-V.YYYY
        NT-V.BENE-BN-NAME (J,2,J3) :=
          FUNCTION-CONTACT-NAME(< CT-V.CONTACT-ID,VERSION(J,L) >)
          NT-V.BENE-BN-NAME (J,2,J3) := *TRANSLATE(NT-V.BENE-BN-NAME (J,2,J3), UPPER) /* bz-Name
*        NT-V.BENE-BN-SSN     (J,2,J3) := CT-V.SSN
        MOVE EDITED CT-V.SSN (EM=9(9)) TO NT-V.BENE-BN-SSN (J,2,J3)
        FIND CREL-V WITH CONTACT-1-ID = InsuredContactID
            AND CONTACT-2-ID = CT-V.CONTACT-ID
*           IF ROLE-2-CD = 'K'
*             ROLE-2-CD := 'C'
*           END-IF
          BENE-BN-REL (J,2,J3) := ROLE-2-CD
        END-FIND
      END-FIND
    END-IF
  END-FOR
END-FOR
/*
IF #ISN = 0
  MOVE BY NAME NT-V TO NT-F-V
  STL.
  STORE NT-F-V
  MOVE *ISN (STL.) TO #ISN
ELSE
  GET NT-F-V #ISN
  MOVE BY NAME NT-V TO NT-F-V
  UPDATE
END-IF
#TEMPDATA := TRUE
/*
END
