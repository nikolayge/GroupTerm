* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP06P0    - SORT AND PRINT ATHP05P0 NEW BUS/TTR EXTRACTS
*
*  THIS PROGRAM RUNS IN BATCH MODE ONLY
************************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
* bz  06/09/2013  Rebranding (Search for AAFMAA)
************************************************************************
DEFINE DATA
LOCAL  USING GONERLXX                       /* SAG RH1 11292005 SSS
LOCAL
*
1 #SCR-LOSS-ROWS (18)
 2 #SCR-LOSS-COLS     (P5/12)
 2 #SCR-LOSS-TOTS     (P5/2)
*
1 #REPT-EXTRACT          (A74)
1 REDEFINE #REPT-EXTRACT
 2 #RE-SORT-KEY             (A34)
 2 REDEFINE #RE-SORT-KEY
  3 #RE-SECTION           (A1)
  3 #RE-SUBSECTION        (A1)
  3 #RE-NAME-ID-NUMBER    (A32)
 2 #RE-REMAINING-FIELDS    (A40)
 2 REDEFINE #RE-REMAINING-FIELDS
  3 #RE-UNITS-GAIN-LOSS   (N2)
  3 #RE-CURR-UNITS        (N2)
  3 #RE-ISS-AGE           (A2)
  3 #RE-ISS-DATE          (A8)
  3 #RE-TYPE              (A4)
  3 #RE-TERM-AGE          (N3)
  3 #RE-TERM-DATE         (A8)
  3 #RE-PLAN              (A3)
  3 #RE-RANK              (A3)
  3 #RE-STAT              (A1)
  3 #RE-SERV              (A3)
  3 #RE-MODE              (A1)
1 REDEFINE #REPT-EXTRACT
 2 #RE-FILL1              (A2)
 2 #RE-ACT-RET-SEP    (3)
  3 #RE-LOSS-BY-GRADE     (P5/6)
 2 #RE-FILL2              (A18)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #I4              (I2)
1 #ISTART          (I2)
1 #CNT             (P3)
1 #HEADER          (A33) INIT <'NEW BUSINESS REPORT'>
1 #SUB-HEADER      (A24) INIT <'NEW MEMBERS'>
1 #NUM-RECS        (P5)
1 #LAST-SECTION    (A1)
*
1 #ABEND-CODE       (I4)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*    WRITE   / '*E R R O R - N R    ' *ERROR-NR      /* SAG RH1 11292005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     MOVE +4095 TO #ABEND-CODE
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
INCLUDE GABNDCX3
STOP                                                 /* SAG RH1 11292005 SS - END
END-ERROR
*
FORMAT (2) PS=62 LS=133
WRITE (2) TITLE LEFT 'PROGRAM: ATHP06P0'10X
                'AAFMAA'
                5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
          / #SUB-HEADER 8X #HEADER
*
REPEAT
  READ WORK 1 ONCE RECORD #REPT-EXTRACT
    AT END
      ESCAPE BOTTOM
    END-ENDFILE
*
    IF #RE-SECTION = '7' OR = '8'     /* DETAIL-LOSS-WORK SHEET
      IF #RE-SECTION NE #LAST-SECTION
        RESET #I1
      END-IF
*
      MOVE #RE-SECTION TO #LAST-SECTION
*
      IF #RE-SECTION = '7'
        MOVE 0 TO #ISTART     /* ARMY
      ELSE
        MOVE 6 TO #ISTART     /* AIRF
      END-IF
*
      FOR #I3 1 TO 3          /* ACT-RET-SEP
        MOVE #ISTART TO #I2
        ADD 1 TO #I1          /* LINES ON PAGE (18 OF THEM)
*
        FOR #I4 1 TO 6
          ADD 1 TO #I2
          MOVE #RE-LOSS-BY-GRADE (#I3, #I4) TO #SCR-LOSS-COLS(#I1, #I2)
*
          IF #I4 = 2 OR = 3     /* FIELD OR COMPANY MBRS
            ADD #RE-LOSS-BY-GRADE (#I3, #I4) TO #SCR-LOSS-TOTS (#I1, 1)
          END-IF
*
          IF #I4 = 5 OR = 6     /* FIELD OR COMPANY UNITS
            ADD #RE-LOSS-BY-GRADE (#I3, #I4) TO #SCR-LOSS-TOTS (#I1, 2)
          END-IF
        END-FOR
      END-FOR
*
      ESCAPE TOP
    END-IF
*
    IF (#NUM-RECS GT 0) AND (#RE-SECTION = '9')
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-RECS
END-ALL
*
* * * * * * SORT INPUT RECORDS * * * * * *
*
SORT BY #RE-SECTION
        #RE-SUBSECTION
        #RE-NAME-ID-NUMBER
  USING #RE-REMAINING-FIELDS
    ADD 1 TO #CNT
*
  AT BREAK #RE-SECTION
    IF #RE-SECTION = '1'
      MOVE 'NEW BUSINESS REPORT'               TO #HEADER
    ELSE
      MOVE 'TERMINATION AND TRANSFER REGISTER' TO #HEADER
    END-IF
*
    WRITE (2) 'TOTAL-CNT = ' #CNT
    RESET #CNT
*
    IF #RE-SECTION = '1'
      DECIDE ON FIRST VALUE OF #RE-SUBSECTION
        VALUE '1' MOVE 'NEW MEMBERS'        TO #SUB-HEADER
        VALUE '2' MOVE 'ADDITIONAL MEMBERS' TO #SUB-HEADER
        VALUE '3' MOVE 'REINSTATEMENTS'     TO #SUB-HEADER
        NONE      IGNORE
      END-DECIDE
    ELSE
      DECIDE ON FIRST VALUE OF #RE-SUBSECTION
        VALUE '1' MOVE 'DEATHS'         TO #SUB-HEADER
        VALUE '2' MOVE 'CASH SURRENDER/EXCHANGES' TO #SUB-HEADER
        VALUE '3' MOVE 'LAPSES/RESIGNATIONS'      TO #SUB-HEADER
        VALUE '4' MOVE 'EXPIRATIONS'              TO #SUB-HEADER
        VALUE '5' MOVE 'CONVERSION"S'             TO #SUB-HEADER
        VALUE '6' MOVE 'PAID-UP"S'                TO #SUB-HEADER
        VALUE '7' MOVE 'RPU"S-ETI"S'              TO #SUB-HEADER
        NONE      IGNORE
      END-DECIDE
    END-IF
*
    NEWPAGE (2)
  END-BREAK
*
  AT BREAK #RE-SUBSECTION
    IF #CNT NE 0
      WRITE (2) 'TOTAL-CNT = ' #CNT
      RESET #CNT
*
      IF #RE-SECTION = '1'
        DECIDE ON FIRST VALUE OF #RE-SUBSECTION
          VALUE '1' MOVE 'NEW MEMBERS'        TO #SUB-HEADER
          VALUE '2' MOVE 'ADDITIONAL MEMBERS' TO #SUB-HEADER
          VALUE '3' MOVE 'REINSTATEMENTS'     TO #SUB-HEADER
          NONE      IGNORE
        END-DECIDE
      ELSE
        DECIDE ON FIRST VALUE OF #RE-SUBSECTION
          VALUE '1' MOVE 'DEATHS'         TO #SUB-HEADER
          VALUE '2' MOVE 'CASH SURRENDER/EXCHANGES' TO #SUB-HEADER
          VALUE '3' MOVE 'LAPSES/RESIGNATIONS'      TO #SUB-HEADER
          VALUE '4' MOVE 'EXPIRATIONS'              TO #SUB-HEADER
          VALUE '5' MOVE 'CONVERSION"S'             TO #SUB-HEADER
          VALUE '6' MOVE 'PAID-UP"S'                TO #SUB-HEADER
          VALUE '7' MOVE 'RPU"S-ETI"S'              TO #SUB-HEADER
          NONE      IGNORE
        END-DECIDE
      END-IF
*
      NEWPAGE (2)
    END-IF
  END-BREAK
*
  DISPLAY (2) 'COUNT'      #CNT
              'RANK'       #RE-RANK
              'NAME-ID-NUMBER' #RE-NAME-ID-NUMBER
              'MIL/STAT'   #RE-STAT
              'MIL/SERV'   #RE-SERV
              'ISS/AGE'    #RE-ISS-AGE
              'ISS-DATE'   #RE-ISS-DATE
              'TERM/AGE'   #RE-TERM-AGE
              'TERM/DATE'  #RE-TERM-DATE
              'TYPE'       #RE-TYPE
              'PLAN'       #RE-PLAN
              'MODE'       #RE-MODE
              'UNITS'      #RE-UNITS-GAIN-LOSS
              'TOT/UNITS'  #RE-CURR-UNITS
END-SORT
*
WRITE (2) NOTITLE USING FORM 'ATHP06F1'
*
END
