* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ADRN0001 *** Get Next Available Address Pool ID  **********
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG    REASON                                      *
*                                                                      *
* PAM    09/24/2010 None   Program written.                            *
* PAM    10/28/2010 PM1    Fixed AID reservation value                 *
*                                                                      *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #POOL-ID                (N8)
*
LOCAL
*
1 TR-V VIEW OF A-TERMINALS
 2 TERMINAL-ID
 2 CLERK-ID
 2 SECURITY-CODE
 2 COMMENTS
 2 SSN
*
1 ADDR-CHECK-POOL-V VIEW OF A-ADDRESS-POOL
 2 ADDRESS-POOL-ID
*
1 #COMMENTS               (A20)
1 REDEFINE #COMMENTS
 2 #ID                    (N8)
 2 #DESCRIPTION           (A12)
*
1 #AID                    (N8)
1 #ID-FOUND               (L)
*
END-DEFINE
*
************************************************************************
*
TR01. FIND (1) TR-V WITH TR-V.TERMINAL-ID = 'ADDRPOOL'
  #COMMENTS        := TR-V.COMMENTS
  REPEAT
    #AID           := #ID
*
    RESET #ID-FOUND
    FIND (1) ADDR-CHECK-POOL-V WITH ADDR-CHECK-POOL-V.ADDRESS-POOL-ID = #AID
      #ID-FOUND    := TRUE
    END-FIND
*
    IF #ID-FOUND
      ADD +1 TO #ID                /* PM1
      ESCAPE TOP
    END-IF
*
    ESCAPE BOTTOM
  END-REPEAT
*
  ADD +1 TO #ID                    /* PM1
  TR-V.COMMENTS    := #COMMENTS
  UPDATE (TR01.)
*
  #POOL-ID         := #AID
END-FIND
*
END
