* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: C2153SPO - New Application layout
*    Used for DAILY AT APPLICATIONS APP file & also online a/o 10/2002
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    07232004  PM1     Do not display term insurance amounts under *
*                          the minimum of $50,000 per Lisa Milman.     *
*                                                                      *
* PAM    02142005  PM2     Do not display 'After Refund' lines for RT  *
*                          and LT during BATCH processing.             *
* DRW    05122005  DW3     Chg Checking for RT & LT info               *
* DRW    05232005  DW4     Add VLT VGLI APP lETTER Processing          *
* PAM    06272005  PM5     Add PNT (Neal Telletier) to quote           *
*                          Processing - removed MW                     *
* PAM    07252005  PM6     Add JTK (Joy T Khalil) to quote processing  *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* SAG  12062005  RH1  SS - Replaced the HEX constant '7F'(Double Quote)*
*                     in EBCDIC to HEX code '22' in ASCII.             *
* TDM    02202006  TM1     ADD SRG (STEPHEN GARNETT) TO TABLE          *
* PAM    09142006  PM7     Aded KMT (Keith Tederick) To table          *
* PAM    03212007  PM8     Always use current date for quote letters   *
* DRW    08132007  DW9     Add Quot-LT2-Class Processing               *
* DRW   06/09/2008 DW10 Remove ANB/RLB/BMG/KEC as per Lisa Milman      *
* DRW   08/13/2008 DW11 GENERATIONS PLUS Processing                    *
* RSE   03/07/2013 RE1  Change LT2 (Select) to LT2+ on map; add EZ     *
*                       Term rates on map                              *
* NGG   20130617   NG1  PO Changes                                     *
* TMT   11/11/2013 TT1  1 MILLION STABILITY PROJECT                    *
* TMT   12/31/2013 TT2  ADDED GP2 POLICY                               *
************************************************************************
*
DEFINE DATA
PARAMETER USING C2150APO
PARAMETER
1 #CLASS (N1) OPTIONAL       /* NG1
LOCAL USING GONERLXX
LOCAL USING GRTL0003         /* RE1
LOCAL
1 #AP-LINES (P2) INIT <33>   /* fixed # of lines
*
* Below table is also in ATHPATLB  - keep them in sync
1 #USER-EXT  /* ANB/KEC/BMG/RLB deleted 6/9/2008 as per LGM   DW10
  2 C-1  (A7) INIT <'PNP-104'>                                     /* PM5
  2 C-2  (A7) INIT <'LKW-107'>
  2 C-3  (A7) INIT <'AZS-112'>
  2 C-4  (A7) INIT <'KMT-114'>                                     /* PM7
  2 C-5  (A7) INIT <'KFZ-106'>
  2 C-6  (A7) INIT <'DMH-113'>
  2 C-7  (A7) INIT <'REM-108'>
  2 C-8  (A7) INIT <'SRG-109'>                                     /* PM6
1 REDEFINE #USER-EXT
  2 #EXT-OCC   (8)                                                /* PM6
    3 #EXT-USER   (A3)
    3 #EXT-FIL1   (A1)
    3 #EXT-EXT    (A3)
1 #NUM-EXT   (P3) INIT <8> /* MUST BE SAME AS ABOVE              /* PM6
*
1 #FILL-BLNKS (A78/24)
1 #USER-DATE (A14)
1 #EXT  (A3)
1 #Q         (A1) INIT <H'22'>     /* SAG RH1 12062005 SS
1 #PAGE-TOP  (A1) INIT <H'0C'>
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #FULL-APP-RECORD
  2 #FULL-APP-REC  (A125/25)
  2 REDEFINE #FULL-APP-REC
    3 #FULL-APP-REC-75 (A75/40) /* to write temp
*
1 #REC-250    (A250)
1 REDEFINE #REC-250
  2 #REC-125-A (A125)
  2 REDEFINE #REC-125-A
    3 #REC-62-A (A62) /* to write temp
    3 #REC-63-A (A63) /* to write temp
  2 #REC-125-B (A125)
  2 REDEFINE #REC-125-B
    3 #REC-62-B (A62) /* to write temp
    3 #REC-63-B (A63) /* to write temp
1 REDEFINE #REC-250
  2 #REC-250-X   (A1/250)
1 #AP-X1  (I2)   /* bump thru #rec-250-x
1 #AP-X2  (I2)   /* bump thru #full-app-rec-75
*
1 #APP-REC   (A100)  /* daily AT new app file
1 REDEFINE #APP-REC
  2 #APP-REC-X (A1/100)
1 REDEFINE #APP-REC
  2 #APP-LABL-1 (A18)
  2 #APP-DATA-1 (A82)
1 REDEFINE #APP-REC
  2 #APP-REC-3  (A3)
  2 #APP-REC-PLUS (A1)   /* used for '+' (has lnked-at #)
  2 #APP-REC-AST  (A1)   /* used for '*' (has w/ note)
  2 #APP-REC-95 (A95)
1 REDEFINE #APP-REC
  2 #APP-28   (A28)
  2 REDEFINE #APP-28
    3 #APP-28-2 (A2)
    3 #APP-26   (A26)
  2 #APP-60   (A60)
  2 REDEFINE #APP-60
    3 #APP-AMTS-OCC  (5)
      4 #APP-AMTS-12  (A12)
      4 REDEFINE #APP-AMTS-12
        5 #APP-AMTS-FIL-1 (A2)
        5 #APP-AMTS-MID-5 (A5)
        5 #APP-AMTS-LST-4 (A4)
        5 #APP-AMTS-FIL-2 (A1)
      4 REDEFINE #APP-AMTS-12
        5 #APP-AMTS-FIL-3 (A1)
        5 #APP-AMTS-MID-7 (A7)
        5 #APP-AMTS-LST-3 (A3)
        5 #APP-AMTS-FIL-4 (A1)
      4 REDEFINE #APP-AMTS-12
        5 #APP-AMTS-FIL-5 (A2)
        5 #APP-AMTS-LF-BR (A1)
        5 #APP-AMTS-THOU  (A1)
        5 #APP-AMTS-7     (A7)
        5 REDEFINE #APP-AMTS-7
          6 #APP-AMTS-7-X  (A1/7)
        5 #APP-AMTS-RT-BR (A1)
*
1 #SCN-LINES    (A78/22)
1 REDEFINE #SCN-LINES
  2 #SCN-LINES-OCC   (22)
    3 #SCN-18        (A18)
    3 #SCN-60        (A60)
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 USER-SOURCE
  2 ST-NOTE-DATE (2)
  2 MEMBER-CN
  2 REFERING-ID-NUMBER   (1)
  2 NOTES            (A75/40)
  2 REDEFINE NOTES
    3 NOTES-TBL  (40)
      4 NOTES-75   (A75)
      4 REDEFINE NOTES-75
        5 NOTES-AST  (A1)
        5 NOTES-USER (A3)
        5 NOTES-DASH (A1)
        5 NOTES-DATE (A10)
        5 REDEFINE NOTES-DATE
          6 NOTES-MM   (A2)
          6 NOTES--1   (A1)
          6 NOTES-DD   (A2)
          6 NOTES--2   (A1)
          6 NOTES-YYYY (A4)
        5 NOTES-REST (A60)
*
1 #INVALID    (L)
1 #CLERK-NAME-PASS  (A20)
1 #ZIP    (A10)
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 #I4     (I2)
1 #SC-X   (I2)
1 #LABEL  (L)
*
1 #TOT-FACE-DISP  (A14)         /*TT1 increased from A11 to A14
1 #NUM-QUOTES   (P3)
1 #LINES        (N3)
1 #SVC-FEE-CV   (C)
1 #FOUND-LT2  (L)
1 #FOUND-LT   (L)  /* DW3
1 #FOUND-RT   (L)  /* DW3
1 #SMOK   (A10)
1 #TERM    (A2)
1 REDEFINE #TERM
  2 #TERM-N (N2)
1 #STREET   (A25)
1 #STREET-2 (A25)
1 #CITY     (A22)
1 #STATE    (A2)
1 #ERR-MSG  (A60)
1 #WHO      (A10)
1 #TOB      (A3)
1 #DEAR     (A45)
1 #SPLIT    (A1)
*
1 #ZIP-CODE-9  (N9)
1 REDEFINE #ZIP-CODE-9
  2 #ZIP-5   (A5)
  2 #ZIP-4   (A4)
1 #ZIP-10   (A10)
*
1 #RQST-DATE (A10)
1 #2-DAYS-AGO  (N8)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
*
1 #RANK-CONV    (A23)
1 REDEFINE #RANK-CONV
  2 #RANK-CONV-X  (A1/23)
*
1 #LETTER-TABLE
  2 #1T (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>
  2 #2T (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>
1 REDEFINE #LETTER-TABLE
  2 #LETTER-OCC  (26)
    3 #UPPER     (A1)
    3 #LOWER     (A1)
*
1 #MINIMUM-TERM-FACE (P6) INIT <50>                               /* PM1
1 #GPL-db              (n10.2)               /* DW11
1 #rebuild-gpl      (l)
1 #num-gpl          (n5)
1 #MONTHLY-PREM-EZ  (N3.2)    /* RE1
1 #VTR              (A7)      /* RE1
* below is linkage to GPREMSXX -- also uses #smoker-flag   RE1 START
1 #SCR-COMMAND (A2)
1 #SCR-ID      (N9)
1 #SCR-SUFFIX  (A3)
1 #PLAN        (A3)
1 REDEFINE #PLAN
  2 #PLAN-A1   (A1)
  2 #PLAN-N2   (N2)
1 #AGE         (N2)
1 #APRM        (N7.2)        /*TT1 Increased from 6.2 to 7.2
1 #ISSUE-DATE  (N8)
1 #FACE-AMT    (N7.2)
1 #a           (A) DYNAMIC   /*TT1 Used for SUBSTRING
* above is linkage to GPREMSXX                             RE1 END
*
1 #RATING-CLASS (A) DYNAMIC CONST <'* Premiums are for rating class'>   /* NG1
*
END-DEFINE
* * * * * * * * *
*
* WRITE 'top C2153spo' 'SCR-OPT-GPL=' #scr-opt-gpl-dues(*)
*  for #i1 1 to 13
*    write 'plan-to-use=' #i1 #plan-to-use (#i1)
*  end-for
*   write 'GPL-dues=' #scr-opt-GPL-dues (*)
*   write 'fac-to-use' #fac-to-use (*)
MOVE *DATN TO #YYYYMMDD
SUBTRACT 2 FROM #DD
IF #DD LT 1
  SUBTRACT 1 FROM #MM
  IF #MM LT 1
    ADD 12 TO #MM
    SUBTRACT 1 FROM #YYYY
  END-IF
*
  IF #MM = 2
    ADD 28 TO #DD
  ELSE
    ADD 30 TO #DD
  END-IF
END-IF
MOVE #YYYYMMDD TO #2-DAYS-AGO
*
MOVE #ZIP-CODE TO #ZIP-CODE-9
*
IF #ZIP-4 = '0000'
  MOVE #ZIP-5 TO #ZIP-10
ELSE
  COMPRESS #ZIP-5 #ZIP-4 INTO #ZIP-10 WITH '-'
END-IF
*
IF *DEVICE = 'BATCH'
  IF #CNT NE 0  /* not 1st time
    IF #PDA-SCR-SUFFIX = 'ATA'
      MOVE #PAGE-TOP TO #APP-REC
      PERFORM WRITE-WORK-3
    END-IF
  ELSE          /* is 1st time
    IF NOT #PDA-SCR-SUFFIX = 'BLT' OR ='AS' /*byp this format if mbr/sps
        OR ='VLT'    /* DW4    VGLI APP Ltr
      WRITE WORK 3 #APP-REC  /* this rec is to solve a problem for KEC
    END-IF
*
    RESET #AP-X2
    COMPRESS #Q 'CN' #Q ',' #Q 'NAME' #Q ',' #Q 'STREET1' #Q ','
      #Q 'STREET2' #Q ','
      #Q 'CITY' #Q ',' #Q 'ST' #Q ',' #Q 'ZIP' #Q ',' #Q 'DEAR' #Q ','
      #Q 'WHO' #Q ',' #Q 'INSURED' #Q ',' #Q 'TOT-FACE' #Q ','
      #Q 'AGE' #Q ','
      #Q 'TOBACCO' #Q ',' #Q 'AMOUNTS' #Q ',' #Q 'QUOTE01' #Q ','
      #Q 'QUOTE02' #Q ',' #Q 'QUOTE03' #Q ',' #Q 'QUOTE04' #Q ','
      #Q 'QUOTE05' #Q ',' #Q 'QUOTE06' #Q ',' #Q 'QUOTE07' #Q ','
      INTO #REC-250 LEAVING NO
    ADD 1 TO #AP-X2
    MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
    MOVE #REC-125-B TO #REC-125-A RESET #REC-125-B
*
    COMPRESS #REC-250
      #Q 'QUOTE08' #Q ',' #Q 'QUOTE09' #Q ',' #Q 'QUOTE10' #Q ','
      #Q 'QUOTE11' #Q ',' #Q 'QUOTE12' #Q ',' #Q 'QUOTE13' #Q ','
      #Q 'QUOTE14' #Q ',' #Q 'QUOTE15' #Q ',' #Q 'QUOTE16' #Q ','
      #Q 'QUOTE17' #Q ',' #Q 'QUOTE18' #Q
      INTO #REC-250 LEAVING NO
    ADD 1 TO #AP-X2
    MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
    MOVE #REC-125-B TO #REC-125-A RESET #REC-125-B
    ADD 1 TO #AP-X2
    MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
    MOVE #REC-125-B TO #REC-125-A RESET #REC-125-B
*
    WRITE WORK 4 #FULL-APP-RECORD
  END-IF
*
* separate city/state
  MOVE 'S' TO #SPLIT
  RESET #STREET #STREET-2 #CITY #STATE
  CALLNAT 'GCTSTSXX'  /* split city/state
    #SPLIT
    #ADDRESS-1
    #ADDRESS-2
    #ADDRESS-3
    #STREET
    #STREET-2
    #CITY
    #STATE
    #ERR-MSG
*
* 1st part of full file
  ADD 1 TO #CNT /* count applications produced
  MOVE #FULL-RANK TO #RANK-CONV
  PERFORM LOWER-UPPER
  COMPRESS #RANK-CONV #LAST-NAME INTO #DEAR
*
  IF #PDA-SCR-SUFFIX = 'AS ' OR = 'ASN' OR = 'AST' OR = 'ASG'
    MOVE 'spouse' TO #WHO
  ELSE
    RESET #WHO
  END-IF
*
  IF #SMOKER-FLAG = 'S'
    MOVE 'Yes' TO #TOB
  ELSE
    MOVE 'No' TO #TOB
  END-IF
*
  MOVE EDITED #TOT-FACE (EM=Z,ZZZ,ZZZ.99) TO #TOT-FACE-DISP   /*TT1 (EM=ZZZ,ZZZ.99)FROM TO (EM=Z,ZZZ,ZZZ.99)
  COMPRESS '$' #TOT-FACE-DISP INTO #TOT-FACE-DISP LEAVING NO
  RESET #FULL-APP-REC (*) #REC-250 #AP-X1 #AP-X2
  COMPRESS  #Q #PDA-SCR-ID
    #Q ',' #Q #FORMATED-NAME
    #Q ',' #Q #STREET
    #Q ',' #Q #STREET-2
    #Q ',' #Q #CITY
    #Q ',' #Q #STATE
    #Q ',' #Q #ZIP-10
    #Q ',' #Q #DEAR
    #Q ',' #Q #WHO
    #Q ',' #Q #INSURED
    #Q ',' #Q #TOT-FACE-DISP
    #Q ',' #Q #CURR-AGE
    #Q ',' #Q #TOB
    INTO #REC-250 LEAVING NO
*
  IF #REC-125-B NE ' '
    SUBTRACT 125 FROM #AP-X1
    ADD 1 TO #AP-X2
    MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
    MOVE #REC-125-B TO #REC-125-A  RESET #REC-125-B
  END-IF
*
  FOR #AP-X1 250 TO 1 STEP -1
    IF #REC-250-X (#AP-X1) NE ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  RESET #LINES
  IF #PDA-SCR-SUFFIX = 'ATA'   /* AT APPL FROM ATHPATLB
    FIND (1) AT-V WITH ID-NUMBER = #PDA-SCR-ID
    END-FIND
*
    MOVE ' ' TO #CLERK-NAME-PASS
    CALLNAT 'GCKNMSXX' AT-V.USER-SOURCE
      #INVALID
      #CLERK-NAME-PASS
*
    RESET #APP-REC
    MOVE #FORMATED-NAME TO #APP-REC-95
*
    IF   (MEMBER-CN = ' ' OR = '000000')  /* is linked to another AT
        AND (REFERING-ID-NUMBER (1) GE 500000)
      MOVE '+' TO #APP-REC-PLUS
    END-IF
*
    FOR #I1 1 TO 40
      IF  (NOTES (#I1) = MASK ('*'...'-'MM'/'DD'/'YYYY)) /* 1st line
          AND (NOTES-REST (#I1) = MASK ('W/'))
        COMPRESS NOTES-YYYY (#I1) NOTES-MM (#I1) NOTES-DD (#I1)
          INTO #YYYYMMDD-A LEAVING NO
        IF ((#YYYYMMDD = ST-NOTE-DATE (1))   /* orig note date
            OR (#YYYYMMDD = *DATN))             /* or today
            AND (#YYYYMMDD GE #2-DAYS-AGO)    /* within last 2 days
          MOVE '*' TO #APP-REC-AST
          ESCAPE BOTTOM
        END-IF
      END-IF
    END-FOR
*
    PERFORM WRITE-WORK-3
*
    RESET #APP-REC
    MOVE #STREET TO #APP-REC-95
    PERFORM WRITE-WORK-3
*
    IF #STREET-2 NE ' '
      MOVE #STREET-2 TO #APP-REC-95
      PERFORM WRITE-WORK-3
    END-IF
*
    COMPRESS #CITY #STATE #ZIP-10 INTO #APP-REC-95
    PERFORM WRITE-WORK-3
*
    RESET #APP-REC
    FOR #I1 #LINES TO 11  /* 7 lines between address & coordinator line
      PERFORM WRITE-WORK-3
    END-FOR
*
    FOR #I1 1 TO #NUM-EXT
      IF AT-V.USER-SOURCE = #EXT-USER (#I1)
        MOVE #EXT-EXT (#I1) TO #EXT
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    RESET #APP-REC
    IF #I1 GT #NUM-EXT
      PERFORM WRITE-WORK-3
    ELSE
      MOVE 'Coordinator:'    TO #APP-LABL-1
      COMPRESS #CLERK-NAME-PASS ', Direct 1-800-522-5221, ext.'
        INTO #APP-DATA-1 LEAVING NO
      COMPRESS #APP-DATA-1 #EXT INTO #APP-DATA-1
      PERFORM WRITE-WORK-3
    END-IF
*
    MOVE 'Proposed Insured:'  TO #APP-LABL-1
    MOVE #INSURED             TO #APP-DATA-1
    PERFORM WRITE-WORK-3
*
    MOVE 'Age:'    TO #APP-LABL-1
    MOVE #CURR-AGE TO #APP-DATA-1
    PERFORM WRITE-WORK-3
*
    MOVE 'Tobacco:' TO #APP-LABL-1
*
    IF #SMOKER-FLAG = 'S'
      MOVE 'Yes' TO #APP-DATA-1
    ELSE
      MOVE 'No' TO #APP-DATA-1
    END-IF
*
    PERFORM WRITE-WORK-3
*
    RESET #APP-REC
    MOVE 'Tracking Number:' TO #APP-LABL-1
    MOVE #PDA-SCR-ID        TO #APP-DATA-1
    PERFORM WRITE-WORK-3
  END-IF
END-IF
*
RESET #APP-REC
IF #PDA-SCR-SUFFIX = 'ATA'   /* AT APPL FROM ATHPATLB
* IF AT-V.ST-NOTE-DATE (1) NE 0                                        /* PM8
*   MOVE AT-V.ST-NOTE-DATE (1) TO #YYYYMMDD  /* used date made "M?"    /* PM8
* ELSE                                                                 /* PM8
  MOVE *DATN TO #YYYYMMDD
* END-IF                                                               /* PM8
  COMPRESS #MM-A #DD-A #YYYY-A INTO #RQST-DATE WITH '-'
*
* MOVE 'Date Requested:' TO #APP-LABL-1                                /* PM8
  MOVE 'Date:'           TO #APP-LABL-1                                /* PM8
  MOVE #RQST-DATE  TO #APP-DATA-1
END-IF
*
FOR #I1 1 TO 5                                                      /*TT1 BEGIN
  IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
    RESET #APP-AMTS-LST-4 (#I1)
    ESCAPE TOP
  END-IF
*
  MOVE LEFT #SCR-ADDL-COV-1 (#I1) TO #APP-AMTS-MID-5 (#I1)       
  #a := #APP-AMTS-MID-5 (#I1)
  #a := SUBSTRING(#a, 5, 1)
  IF (#a = 'k') THEN
     #a := #APP-AMTS-MID-5 (#I1)
     #a := SUBSTRING(#a, 1, 4)
     #APP-AMTS-MID-5 (#I1) := #a
     move right #APP-AMTS-MID-5(#I1) to #APP-AMTS-MID-5(#I1)
  END-IF
*
  MOVE ',000' TO #APP-AMTS-LST-4 (#I1)
  move right #app-amts-12 (#i1) to #app-amts-12 (#i1)     /* DW11   /*TT1 END
END-FOR
*
PERFORM PUT-OUT-LINE
*
IF   (*DEVICE = 'BATCH')                                            
    AND (#PDA-SCR-SUFFIX = 'ATA')   /* AT APPL FROM ATHPATLB
  RESET #APP-REC
  PERFORM WRITE-WORK-3
  PERFORM WRITE-WORK-3
  PERFORM WRITE-WORK-3
END-IF
*
RESET #FOUND-LT    /* DW3
FOR #I3 1 TO 8
  IF #SCR-OPT-DT-DUES (#I3) NE ' '
    MOVE TRUE TO #FOUND-LT
    ESCAPE BOTTOM
  END-IF
END-FOR
*
RESET #NUM-QUOTES
IF  #FOUND-LT         /* DW3
    AND (#PLAN-TO-USE (2) NE ' ')
  RESET #APP-REC
  MOVE TRUE TO #LABEL
  MOVE 'Level Term I*' TO #APP-28
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  RESET #APP-REC
  IF #SMOKER-FLAG = 'S'
    MOVE '  To age 40' TO #APP-28
  ELSE
    MOVE '  To age 50' TO #APP-28
  END-IF
*
  FOR #I1 1 TO 5                                        
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
        OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                   /* PM1
        AND #PDA-SCR-SUFFIX NE 'OVR'    /* PAM - new maximums
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-DT-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  IF #SMOKER-FLAG NE 'S'
      AND *DEVICE NE 'BATCH'                                         /* PM2
    RESET #APP-REC
    MOVE '  {After refund}' TO #APP-28
*
    FOR #I1 1 TO 5                                      
      IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
        ESCAPE TOP
      END-IF
*
      IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
          OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                 /* PM1
          AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
        ESCAPE TOP
      END-IF
*
      MOVE '{' TO #APP-AMTS-LF-BR (#I1)
      MOVE '}' TO #APP-AMTS-RT-BR (#I1)
      MOVE #SCR-OPT-DT-TTL-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
      PERFORM ADD-COMMA
    END-FOR
*
    PERFORM PUT-OUT-LINE
    ADD 1 TO #NUM-QUOTES
  END-IF
END-IF
*
RESET #FOUND-LT2
FOR #I1 1 TO 4
  MOVE #LT2-TERM-TO-USE (#I1) TO #TERM
*  WRITE 'Check LTII loop' #I1 #CURR-AGE '+' #TERM-N
  IF   (#LT2-TERM-TO-USE (#I1) NE ' ')
      AND (#CURR-AGE + #TERM-N) LE 85
*  WRITE #I1 'LTII VALID'#LT2-TERM-TO-USE (#I1) 'age' #CURR-AGE
    MOVE TRUE TO #FOUND-LT2
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #FOUND-LT2
  RESET #APP-REC
  MOVE TRUE TO #LABEL
*
  if #lt2-class-to-use = 'U'                     /* DW9
    move 'Level Term II (Standard)' TO #APP-28
  else
    if #lt2-class-to-use = 'W'                     /* DW9
      move 'Level Term II (Super-Select)' TO #APP-28
    else
*      MOVE 'Level Term II (Select)' TO #APP-28   /* Default is Select
      MOVE 'Level Term II+ (Select)' TO #APP-28   /* Default is Select    RE1
    end-if
  end-if
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  FOR #I2 1 TO 4   /* 4 lt2 terms
    MOVE #LT2-TERM-TO-USE (#I2) TO #TERM
    IF  (#LT2-TERM-TO-USE (#I2) = ' ')
        OR (#CURR-AGE + #TERM-N) GT 85
      IGNORE
    ELSE
      RESET #APP-REC
      FOR #I1 1 TO 5
        IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
          ESCAPE TOP
        END-IF
*
        IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
            OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)               /* PM1
            AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
          ESCAPE TOP
        END-IF
*
        COMPRESS #LT2-TERM-TO-USE (#I2) 'year term' INTO #APP-26
        MOVE #LTII-APA-PREMS (#I2, #I1 + 2) TO #APP-AMTS-7 (#I1)
        PERFORM ADD-COMMA
      END-FOR
*
      PERFORM PUT-OUT-LINE
      ADD 1 TO #NUM-QUOTES
    END-IF
  END-FOR
END-IF
*
RESET #FOUND-RT    /* DW3
FOR #I3 1 TO 8
  IF #SCR-OPT-RT-DUES (#I3) NE ' '
    MOVE TRUE TO #FOUND-RT
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF  #FOUND-RT      /* DW3
    AND (#PLAN-TO-USE (7) NE ' ')
  RESET #APP-REC
  MOVE TRUE TO #LABEL
  MOVE 'Five-Year Renewable Term*' TO #APP-28
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  RESET #APP-REC
  MOVE #RT-AGE-LBL (1) TO #APP-28
  FOR #I1 1 TO 5
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
        OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                   /* PM1
        AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-RT-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  IF #SMOKER-FLAG = 'N'
      AND *DEVICE NE 'BATCH'                                         /* PM2
    RESET #APP-REC
    MOVE '  {After refund}' TO #APP-28
*
    FOR #I1 1 TO 5
      IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
        ESCAPE TOP
      END-IF
*
      IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
          OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                 /* PM1
          AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
        ESCAPE TOP
      END-IF
*
      MOVE '{' TO #APP-AMTS-LF-BR (#I1)
      MOVE '}' TO #APP-AMTS-RT-BR (#I1)
      MOVE  #SCR-OPT-RT-TTL-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
      PERFORM ADD-COMMA
    END-FOR
    PERFORM PUT-OUT-LINE
    ADD 1 TO #NUM-QUOTES
  END-IF
END-IF
*
IF  ((#SCR-OPT-RT-DUES (3) NE ' ') OR (#SCR-OPT-RT-DUES (4) NE ' '))
    AND (#PLAN-TO-USE (7) NE ' ')   /* 5 years later
    AND (#RT-AGE-LBL (2) NE ' ')
  RESET #APP-REC
  MOVE #RT-AGE-LBL (2) TO #APP-28
  FOR #I1 1 TO 5
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
        OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                   /* PM1
        AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-RT-DUES-5 (#I1 + 2) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  IF #SMOKER-FLAG = 'N'
      AND *DEVICE NE 'BATCH'                                         /* PM2
    RESET #APP-REC
    MOVE '  {After refund}' TO #APP-28
*
    FOR #I1 1 TO 5
      IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
        ESCAPE TOP
      END-IF
*
      IF (#FAC-TO-USE (#I1) * 1000 + #TOT-FACE-TERM > #MAX-FACE-TERM
          OR #FAC-TO-USE (#I1) < #MINIMUM-TERM-FACE)                 /* PM1
          AND #PDA-SCR-SUFFIX NE 'OVR'   /* PAM - new maximums
        ESCAPE TOP
      END-IF
*
      MOVE '{' TO #APP-AMTS-LF-BR (#I1)
      MOVE '}' TO #APP-AMTS-RT-BR (#I1)
      MOVE  #SCR-OPT-RT-TTL-DUES-5 (#I1 + 2) TO #APP-AMTS-7 (#I1)
      PERFORM ADD-COMMA
    END-FOR
    PERFORM PUT-OUT-LINE
    ADD 1 TO #NUM-QUOTES
  END-IF
END-IF
*
IF  (#PLAN-TO-USE (8) NE ' ')
    OR (#PLAN-TO-USE (9) NE ' ')
    OR (#PLAN-TO-USE (10) NE ' ')
    OR (#PLAN-TO-USE (11) NE ' ')
    OR (#PLAN-TO-USE (12) NE ' ')
  RESET #APP-REC
  MOVE TRUE TO #LABEL
  MOVE 'Value-Added Whole Life' TO #APP-28
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  IF #PLAN-TO-USE (8) NE ' '            /* DW11
    RESET #APP-REC
    MOVE '  Pay to age 100' TO #APP-28
    FOR #I1 1 TO 5
      IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
        ESCAPE TOP
      END-IF
*
      MOVE #SCR-OPT-OL-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
      PERFORM ADD-COMMA
    END-FOR
*
    PERFORM PUT-OUT-LINE
    ADD 1 TO #NUM-QUOTES
  end-if
END-IF
*
IF #PLAN-TO-USE (9) NE ' '
  RESET #APP-REC
  MOVE '  Pay for 30 years' TO #APP-28
  FOR #I1 1 TO 5
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-30-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
END-IF
*
IF #PLAN-TO-USE (10) NE ' '
  RESET #APP-REC
  MOVE '  Pay for 20 years' TO #APP-28
  FOR #I1 1 TO 5
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-20-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
END-IF
*
IF #PLAN-TO-USE (11) NE ' '
  RESET #APP-REC
  MOVE '  Pay for 7 years' TO #APP-28
*
  FOR #I1 1 TO 5
    IF #SCR-ADDL-COV-1 (#I1) = ' ' OR = '  $0k'
      ESCAPE TOP
    END-IF
*
    MOVE #SCR-OPT-7P-DUES (#I1 + 2) TO #APP-AMTS-7 (#I1)
*    write '7p #i1=' #i1 'move #i1 +2' #SCR-OPT-7P-DUES (#I1 + 2) 'to' #i1
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
END-IF
*
IF #PLAN-TO-USE (12) NE ' '
    AND #SCR-OPT-SP-DUES (3:7) NE ' '
  RESET #APP-REC
  MOVE '  One time payment' TO #APP-28
  FOR #I1 1 TO 5
    IF #SCR-OPT-SP-DUES (#I1 + 2) = ' ' OR = '     00'
      ESCAPE TOP
    END-IF
    MOVE #SCR-OPT-SP-DUES (#I1 + 2) TO #APP-AMTS-MID-7 (#I1)
    MOVE '.00' TO #APP-AMTS-LST-3 (#I1)
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
end-if
*
reset #rebuild-gpl #num-gpl
IF #PLAN-TO-USE (13) NE ' '     /* GEN-PLUS        DW11
  for #i1 1 to 5
    if #fac-to-use (#i1) gt 50      /* must have been from WL request
      move true to #rebuild-GPL
      escape bottom
    end-if
  end-for
*
  if #rebuild-gpl
    move 10 to #fac-to-use (1)
    move 20 to #fac-to-use (2)
    move 30 to #fac-to-use (3)                 /* TT2
    move 40 to #fac-to-use (4)
    move 50 to #fac-to-use (5)
*
    reset #app-rec
    FOR #I1 1 TO 5
      IF #fac-to-use (#i1) = 0
        ESCAPE TOP
      END-IF
*
      add 1 to #num-gpl
      compress '$' #fac-to-use (#I1) ',000' inTO #APP-AMTS-12 (#I1) leaving no
      move right #app-amts-12 (#i1) to #app-amts-12 (#i1)
    END-FOR
*
    add 1 to #sc-x       /* To skip a line before GPL    DW11
    PERFORM PUT-OUT-LINE
    ADD 1 TO #NUM-QUOTES
  end-if
*
  RESET #APP-REC
  MOVE TRUE TO #LABEL
  MOVE 'Generations Plus 2 ' TO #APP-28
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  RESET #APP-REC
  MOVE '  Pay for life' TO #APP-28
  FOR #I1 1 TO 5
    MOVE #SCR-OPT-GPL-dues (#I1 + 2 ) TO #APP-AMTS-7 (#I1)
    PERFORM ADD-COMMA
  END-FOR
*
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
*
  MOVE '  Benefit @ Age 18' TO #APP-28                                       /* TT2
*
  reset #app-amts-12 (*)
  for #i1 1 to 5
    if #fac-to-use (#i1) ne 0
      compute #GPL-db = #fac-to-use (#i1) * 1000 * 2
      move edited #gpl-db (em=ZZ9,999) to #app-amts-12 (#i1)                 /* TT2
      compress '$' #app-amts-12 (#i1) into #app-amts-12 (#i1) leaving no
      move right #app-amts-12 (#i1) to #app-amts-12 (#i1)
    end-if
  end-for
  PERFORM PUT-OUT-LINE
  ADD 1 TO #NUM-QUOTES
END-IF                                    /* END DW11
*
IF  (#PLAN-TO-USE (14) NE ' ')             /* RE1  START
  FOR #I1 1 6
    IF #CURR-AGE GE AGE-RANGE (#I1,1) AND #CURR-AGE LE AGE-RANGE (#I1,2)
      RESET #APP-REC
      MOVE TRUE TO #LABEL
      MOVE 'EZ Term' TO #APP-28
      PERFORM PUT-OUT-LINE
      ADD 1 TO #NUM-QUOTES
      COMPRESS AGE-RANGE (#I1,1) AGE-RANGE (#I1,2) INTO #APP-28 WITH DELIMITER '-'
      COMPRESS '  Age' #APP-28 INTO #APP-28
      MOVE 'EZ' TO #PLAN
      MOVE #CURR-AGE TO #AGE
      FOR #I1 1 TO 5
        if #fac-to-use (#i1) ne 0
          COMPUTE #FACE-AMT = #fac-to-use (#i1) * 1000
          CALLNAT 'GPREMSXX' #SCR-COMMAND #SCR-ID #SCR-SUFFIX #PLAN
            #AGE #APRM #ISSUE-DATE #SMOKER-FLAG #FACE-AMT #SEX
          COMPUTE #MONTHLY-PREM-EZ = (#APRM / 12)
          MOVE EDITED #MONTHLY-PREM-EZ (EM=ZZ9.99) TO #VTR
          MOVE RIGHT #VTR  TO #APP-AMTS-7 (#I1)
          PERFORM ADD-COMMA
        END-IF
      END-FOR
      PERFORM PUT-OUT-LINE
      ADD 1 TO #NUM-QUOTES
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-IF                                     /* RE1 END
*
IF *DEVICE NE 'BATCH'
  IF #PDA-SCR-SUFFIX = 'PDF' /* Gotten by AT & no fill-ins on the
    PERFORM PDF-LAYOUT       /* override screen
  END-IF
  IF #CLASS SPECIFIED                     /* NG1 START
      AND #CLASS > 0
    ADD 2 TO #SC-X
    COMPRESS #RATING-CLASS #CLASS INTO #SCN-LINES (#SC-X)
  END-IF                                  /* NG1 END
  INPUT USING MAP 'C2154MPO'
  ESCAPE ROUTINE
END-IF
*
RESET #APP-REC
COMPUTE #I2 = #NUM-QUOTES + 1
IF #I2 LT 18     /* always need 18 quote lines
  FOR #I1 #I2 TO 18
    ADD 1 TO #AP-X1
    MOVE #Q TO #REC-250-X (#AP-X1)
    ADD 1 TO #AP-X1
    MOVE ',' TO #REC-250-X (#AP-X1)
    ADD 1 TO #AP-X1
    MOVE #Q TO #REC-250-X (#AP-X1)
  END-FOR
END-IF
*
ADD 1 TO #AP-X1    /* final part
MOVE #Q TO #REC-250-X (#AP-X1)
*
IF #REC-125-B NE ' '
  ADD 1 TO #AP-X2
  MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
  MOVE #REC-125-B TO #REC-125-A  RESET #REC-125-B
END-IF
*
IF #REC-125-A NE ' '
  ADD 1 TO #AP-X2
  MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
END-IF
*
WRITE WORK 4 #FULL-APP-RECORD
*
* * * * * * * * *
DEFINE ADD-COMMA
* * * * * * * * *
*
IF #APP-AMTS-7-X (#I1, 1) = MASK (N)
  MOVE #APP-AMTS-7-X (#I1, 1) TO #APP-AMTS-THOU (#I1)
  MOVE ',' TO #APP-AMTS-7-X (#I1, 1)
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE PUT-OUT-LINE
* * * * * * * * * * * * * * *
*
IF *DEVICE = 'BATCH'
  ADD 1 TO #AP-X1
  MOVE #Q TO #REC-250-X (#AP-X1)
  ADD 1 TO #AP-X1
  MOVE ',' TO #REC-250-X (#AP-X1)
  ADD 1 TO #AP-X1
  MOVE #Q TO #REC-250-X (#AP-X1)
  FOR #I1 1 TO 100
    ADD 1 TO #AP-X1
    IF #AP-X1 GT 250
      WRITE #PDA-SCR-ID 'ap-x1 > 250' #AP-X1 #APP-REC-X (#I1) /* KEEP
    ELSE
      MOVE #APP-REC-X (#I1) TO #REC-250-X (#AP-X1)
    END-IF
  END-FOR
*
  IF #AP-X1 GT 125
    SUBTRACT 125 FROM #AP-X1
    ADD 1 TO #AP-X2
    MOVE #REC-125-A TO #FULL-APP-REC (#AP-X2)
    MOVE #REC-125-B TO #REC-125-A  RESET #REC-125-B
  END-IF
*
  PERFORM WRITE-WORK-3
ELSE
  ADD 1 TO #SC-X
  IF #LABEL
    MOVE #APP-REC TO #SCN-LINES (#SC-X)
    RESET #LABEL
  ELSE
    MOVE #APP-28 TO #SCN-18 (#SC-X)
    MOVE #APP-60 TO #SCN-60 (#SC-X)
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-WORK-3
* * * * * * * * * * * * * * *
*
IF NOT #PDA-SCR-SUFFIX = 'BLT' OR ='AS' /* bypass this format if mbr/sps
    OR = 'VLT'   /* DW4
  WRITE WORK 3 #APP-REC  ADD 1 TO #LINES
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * *
DEFINE LOWER-UPPER
* * * * * * * * *
*
FOR #I1 1 TO 23
  FOR #I2 1 TO 26
    IF #RANK-CONV-X (#I1) = #LOWER (#I2)
      MOVE #UPPER (#I2) TO #RANK-CONV-X (#I1)
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE PDF-LAYOUT
* * * * * * * * * * * * * *
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #USER-DATE WITH '/'
COMPRESS #USER #USER-DATE INTO #USER-DATE
*
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER-DATE ')>>'
  INTO #FILL-BLNKS (2) LEAVING NO
COMPRESS '<</T(INSUREDNAME)/V(' #INSURED ')>>'
  '<</T(INSUREDSSN)/V(' #SCR-SSN ')>>'
  INTO #FILL-BLNKS (3) LEAVING NO
COMPRESS '<</T(DETAILS)/V('  INTO #FILL-BLNKS (4) LEAVING NO
*
MOVE 1  TO #I2  /* starting line
MOVE 15 TO #I3  /* ending line
*
MOVE 4 TO #I1 /* start variable in line 5
FOR #I2 #I2 TO 15
  IF #SCN-LINES (#I2) = ' '
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #I1
  MOVE #SCN-LINES (#I2) TO #FILL-BLNKS (#I1)
*
  IF  (#I1 GE 20)
      OR (#I2 GE #I3)
    ESCAPE BOTTOM
  END-IF
END-FOR
*
COMPRESS #FILL-BLNKS (#I1)   ')>>' INTO #FILL-BLNKS (#I1)
*
ADD 1 TO #I1
MOVE '] /F (F:\APPLICATION.PDF)>> >>'      TO #FILL-BLNKS (#I1)
ADD 1 TO #I1
MOVE 'endobj'                             TO #FILL-BLNKS (#I1)
ADD 1 TO #I1
MOVE'trailer <</Root 1 0 R >> %%EOF'     TO #FILL-BLNKS  (#I1)
*
INPUT USING MAP 'GPDFXMXX'
*
END-SUBROUTINE
*
END
