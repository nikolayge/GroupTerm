* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
* PROGRAM-ID: ATHSCAPP - Find CAP policy
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    20070724  None    Program written                             *
*                                                                      *
************************************************************************
*
DEFINE DATA
PARAMETER USING ATHLCAPP
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER           (N6)
  2 X-LOAN-DATE         (9)
  2 X-LOAN-AMOUNT       (9)
  2 C*ACTIVE-SEGMENTS
  2 STATUS              (9)
  2 CONV-DURATION       (N3/9)
*
1 #I1                   (I2)
1 #ACTIVE               (A1)   INIT <'D'>
1 #CAP-POL              (N3)   INIT <999>
*
1 #LOAN-INDICATOR-TABLE
 2 #ACTIVE-CAP-LOAN     (A3)   INIT <'ACT'>
 2 #LPO-CAP-LOAN        (A3)   INIT <'LPO'>
*
END-DEFINE
******************************************************************************
*
RESET #CAP-X
FIND (1) ST-V WITH MEMBER-KEY = #CAP-MEMBER-NUMBER-A
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  RESET #CAP-FND
  FOR #I1 1 TO C*ACTIVE-SEGMENTS
    IF ST-V.STATUS (#I1) NE #ACTIVE
      ESCAPE TOP
    END-IF
*
    IF CONV-DURATION (#I1) = #CAP-POL
      IF #CAP-FND
       AND *DEVICE = 'BATCH'
        WRITE ST-V.ID-NUMBER 'C.A.P. Mbr with 2 C.A.P. Policies'
        ESCAPE TOP
      END-IF
*
      DECIDE FOR FIRST CONDITION
        WHEN #CAP-IND = #ACTIVE-CAP-LOAN
         AND ST-V.X-LOAN-AMOUNT (#I1) GT 0
         AND ST-V.X-LOAN-DATE (#I1)    NE 0
          IGNORE
        WHEN #CAP-IND = #LPO-CAP-LOAN
         AND ST-V.X-LOAN-AMOUNT (#I1) EQ 0
         AND ST-V.X-LOAN-DATE (#I1)    NE 0
          IGNORE
        WHEN NONE
          ESCAPE TOP
      END-DECIDE
*
      #CAP-FND := TRUE
      #CAP-X   := #I1
    END-IF
  END-FOR
END-FIND
*
IF #CAP-X = 0
  #CAP-X := 1
END-IF
*
END
