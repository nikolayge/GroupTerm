* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GPRNCTST  CONVERT ADDRESSES TO CITY STATE
*
DEFINE DATA
PARAMETER
*
1 #SPLIT      (A1)   /* S=split city/state into #city and #state
1 #ADDR-1     (A25)
1 #ADDR-2     (A25)
1 #ADDR-3     (A25)
*
1 #STREET     (A25)
1 #STREET-2   (A25)
1 #CITY       (A22)
1 #STATE      (A2)
*
LOCAL
1 #I          (I2)
1 #ADDR-WORK  (A) DYNAMIC
1 #STATE-ARRAY (A2/57) INIT <
        'AL','AK','AR','AZ','CA','CO','CT','DE','DC','FL','GA','HI','ID','IL',
        'IN','IA','KS','KY','LA','ME','MD','MA','MI','MN','MS','MO','MT','NE',
        'NV','NH','NJ','NM','NY','NC','ND','OH','OK','OR','PA','RI','SC','SD',
        'TN','TX','UT','VT','VA','WA','WV','WI','WY',
        'AA','AE','AP','PR','VI','GU'>
*
END-DEFINE
* * * * * * * * *
*
IF #SPLIT = 'S'               /* split city/state into #city and #state
  #STREET := #ADDR-1
  IF #ADDR-3 NE ' '
    MOVE #ADDR-2 TO #STREET-2
    #ADDR-WORK := #ADDR-3
  ELSE
    #ADDR-WORK := #ADDR-2
    RESET #STREET-2
  END-IF
*
  IF #ADDR-WORK NE ' '
    #ADDR-WORK := *TRIM(#ADDR-WORK)
    EXAMINE DIRECTION BACKWARD #ADDR-WORK FOR ' ' POSITION #I
* If there is a space in between assume there are City and State
    IF #I NE 0
      #I := *LENGTH(#ADDR-WORK) - 1
      #STATE := SUBSTR(#ADDR-WORK,#I)
      EXAMINE #STATE-ARRAY(*) FOR #STATE INDEX #I
      IF #I NE 0
        #I := *LENGTH(#ADDR-WORK) - 3
        #CITY  := SUBSTR(#ADDR-WORK,1,#I)
      ELSE
        RESET #STATE
        #CITY := #ADDR-WORK
      END-IF
    ELSE
      #CITY := #ADDR-WORK
    END-IF
  END-IF
ELSE
  MOVE #STREET TO #ADDR-1
  IF #STREET-2 = ' '
    COMPRESS #CITY #STATE INTO #ADDR-2
    RESET #ADDR-3
  ELSE
    MOVE #STREET-2 TO #ADDR-2
    COMPRESS #CITY #STATE INTO #ADDR-3
  END-IF
END-IF
*
END
