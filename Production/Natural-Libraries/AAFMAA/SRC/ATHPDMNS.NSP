* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDMNS
*
* <<<<<   see line 2520 to un create dallas file <<<<<<<<<<<<<<,
*  THIS PROGRAM FORMATS THE MONTHLY SERVICE REQUETS AND REDIRECTS
*  THEM TO THE DALLAS AREA PRINT CENTER.
*  INSERT#
*    1    - AFSC #9 BRE
*    2    - FAS INPUT FORM
*    3    - SSA INFO RQST FORM
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11292005 SS
LOCAL
*
*  INPUT WORK FILE - SCREEN IMAGES
*
1 #LAST-SRI       (A80)
1 SCREEN-REC-IN             (A80)
1 REDEFINE SCREEN-REC-IN
 2 SRI-CARRIAGE-CTL         (A1)
 2 SRI-DATA                 (A79)
 2 REDEFINE SRI-DATA
  3 SRI-POS-1               (A1)
  3 SRI-COMMAND             (A2)
  3 SRI-FILL2               (A1)
  3 SRI-ID-NUMBER           (A6)
  3 SRI-FILL3               (A4)
  3 SRI-SUFFIX              (A3)
  3 SRI-FILL4               (A4)
  3 SRI-FORM-CHECK          (A3)
  3 SRI-FILL5               (A1)
  3 SRI-REQ-FORM-CHECK      (A3)
  3 SRI-FILL6               (A17)
  3 SRI-PAGE-CNT            (N2)
 2 REDEFINE SRI-DATA
  3 SRI-POSTAGE-CHECK-1     (A1)
  3 SRI-POSTAGE-CHECK-2     (A7)
  3 SRI-FILL7               (A37)
  3 SRI-PAGE-AREA           (A3)
 2 REDEFINE SRI-DATA
  3 SRI-NO-RET-CHECK        (A14)
 2 REDEFINE SRI-DATA
  3 SRI-DATA-X              (A1/79)
*
*  OUTPUT WORK FILE - SCREEN IMAGES
*
1 SCREEN-REC-OUT            (A80)
1 REDEFINE SCREEN-REC-OUT
 2 SRO-CARRIAGE-CTL         (A1)
 2 SRO-DATA                 (A79)
 2 REDEFINE SRO-DATA
  3 SRO-POS-1               (A1)
  3 SRO-COMMAND             (A2)
  3 SRO-FILL2               (A1)
  3 SRO-ID-NUMBER           (A6)
  3 SRO-FILL3               (A4)
  3 SRO-SUFFIX              (A3)
 2 REDEFINE SRO-DATA
  3 SRO-FILL4               (A1)
  3 SRO-MCI-CODE            (A3)
  3 SRO-FILL5               (A75)
 2 REDEFINE SRO-DATA
  3 SRO-FILL6               (A17)
  3 SRO-MCI-FORM            (A25)
  3 SRO-FILL7               (A37)
*
1 #CHECK-VARIABLES
  2 #ALL-ASTERICS           (A1) INIT <'*'>
*
1 #REPORT-SECTION
 2 #FSO-SENTENCE            (A29) INIT <'TOTAL 50 DAY FSO"S PRINTED = '>
 2 #FSO-10-SENTENCE         (A29) INIT <'TOTAL 10 MN FSO"S PRINTED  = '>
 2 #FIQ-SENTENCE            (A29) INIT <'TOTAL FIQ"S PRINTED        = '>
 2 #DAS-SENTENCE            (A29) INIT <'TOTAL DAS"S PRINTED        = '>
 2 #MED-SENTENCE            (A29) INIT <'TOTAL MED"S PRINTED        = '>
 2 #M71-SENTENCE            (A29) INIT <'TOTAL M71"S PRINTED        = '>
 2 #M70-SENTENCE            (A29) INIT <'TOTAL M70"S PRINTED        = '>
 2 #M65-SENTENCE            (A29) INIT <'TOTAL M65"S PRINTED        = '>
 2 #M62-SENTENCE            (A29) INIT <'TOTAL M62"S PRINTED        = '>
 2 #S62-SENTENCE            (A29) INIT <'TOTAL S62"S PRINTED        = '>
 2 #RET-SENTENCE            (A29) INIT <'TOTAL RET"S PRINTED        = '>
 2 #UNKNOWN-SENTENCE        (A29) INIT <'TOTAL UNKNOWNS PRINTED     = '>
 2 #TOTAL-SENTENCE          (A29) INIT <'TOTAL MAILINGS             = '>
 2 #FSO-CNT                 (P5)
 2 #FSO-10-CNT              (P5)
 2 #FIQ-CNT                 (P5)
 2 #DAS-CNT                 (P5)
 2 #MED-CNT                 (P5)
 2 #M71-CNT                 (P5)
 2 #M70-CNT                 (P5)
 2 #M65-CNT                 (P5)
 2 #M62-CNT                 (P5)
 2 #S62-CNT                 (P5)
 2 #RET-CNT                 (P5)
 2 #UNKNOWN-CNT             (P5)
 2 #TOTAL-MAILING-CNT       (N5)
 2 REDEFINE #TOTAL-MAILING-CNT
  3 #TOTAL-MAILING-CNT-A    (A5)
*
1 #CONSTANTS
 2 #UNKNOWN                 (L)
*
1 #PAGE-CNT        (N2)
1 REDEFINE #PAGE-CNT
 2 #PAGE-CNT-A     (A2)
1 #INSERT-TBL      (A7)
1 REDEFINE #INSERT-TBL
 2 #INSERT-IND (A1/7)
*
1 #INSERT-TABLE
 2 #INSERT1                 (A3)
 2 #INSERT2                 (A3)
 2 #INSERT3                 (A3)
 2 #INSERT4                 (A3)
 2 #INSERT5                 (A3)
 2 #INSERT6                 (A3)
 2 #INSERT7                 (A3)
 2 #INSERT8                 (A3)
*
1 #I1            (I2)
1 #LAST-POS-1    (A1)
1 #MCI-PAGE-CNT  (N3)
1 #DASHES        (A3)  INIT <'___'>
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR     /* SAG RH1 11292005 SS START
*             / '*E R R O R - L I N E' *ERROR-LINE     
INCLUDE GABNDCX3                                     /* SAG RH1 11292005 SS END
END-ERROR
*
FORMAT LS=133 PS=66
*
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM 2X 'TOTAL MAILINGS CHECK PAGE'
          5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
READ WORK 1 RECORD SCREEN-REC-IN
*
  IF #LAST-POS-1 = '1'    /* postal bar code
    MOVE '0' TO SRI-POS-1 /* usually on address line (to reset bar code
    RESET #LAST-POS-1
  END-IF
*
  IF SRI-POS-1 = '1'     /* postal bar-code line
    MOVE SRI-POS-1 TO #LAST-POS-1
    FOR #I1 1 TO 79
      IF SRI-DATA-X (#I1) = '!'   /* replace delimiter with "*"
        MOVE '*' TO SRI-DATA-X (#I1)
      END-IF
    END-FOR
  END-IF
*
  IF SRI-CARRIAGE-CTL = '1'  /* #FORM-FEED
    IF (SRI-COMMAND = ' ') AND (SRI-ID-NUMBER = ' ')
      ESCAPE TOP
    END-IF
*
    IF   (#TOTAL-MAILING-CNT NE 0)  /* bypass on 1st page
     AND (NOT #UNKNOWN)             /* if going to mci-worldcom
      PERFORM PROCESS-OMR-CODES
*
      IF   (SRI-COMMAND = 'AD')  /* to bump to new sheet
       AND (#MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
                       OR = 11 OR = 13 OR = 15 OR = 17 OR = 19)
        RESET SCREEN-REC-OUT
        MOVE '1' TO SRO-CARRIAGE-CTL  /* force a blank page
        MOVE 'DJDE FORM=NONE,END;' TO SRO-MCI-FORM  /* reset form
        WRITE WORK 2 SCREEN-REC-OUT
        RESET SCREEN-REC-OUT
        MOVE '1' TO SRO-CARRIAGE-CTL  /* force a blank omr page
        WRITE WORK 2 SCREEN-REC-OUT
        RESET SCREEN-REC-OUT
        MOVE 'DJDE FORM=AFSC01,END;' TO SRO-MCI-FORM /* rtn to reg form
        WRITE WORK 2 SCREEN-REC-OUT
      END-IF
*
      IF SRI-POS-1 = ' '
        MOVE '0' TO SRI-POS-1  /* to resert font
      END-IF
    END-IF
*
    IF SRI-COMMAND = 'AD'
      MOVE SCREEN-REC-IN TO #LAST-SRI
      RESET #PAGE-CNT #UNKNOWN #INSERT-TABLE #MCI-PAGE-CNT
      ADD 1 TO #TOTAL-MAILING-CNT
*
      DECIDE FOR FIRST CONDITION
        WHEN SRI-FORM-CHECK = 'FSO'
          IF SRI-REQ-FORM-CHECK = '10'    /* #FSO-10-CODE
            ADD +1 TO #FSO-10-CNT
            MOVE TRUE TO #UNKNOWN
          ELSE
            MOVE #DASHES TO #INSERT4
            IF SRI-REQ-FORM-CHECK = 'F'  /* #FSO-FAS-CODE
              MOVE #DASHES TO #INSERT1 #INSERT2
            END-IF
            ADD +1 TO #FSO-CNT
            MOVE 1 TO #PAGE-CNT
          END-IF
*
        WHEN SRI-FORM-CHECK = 'FIQ'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #FIQ-CNT
          MOVE 3 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'DAS'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #DAS-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'RET'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #RET-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'MED'
          ADD +1 TO #MED-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'M71'
          ADD +1 TO #M71-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'M70'
          ADD +1 TO #M70-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'M65'
          ADD +1 TO #M65-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'M62'
          ADD +1 TO #M62-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'S62'
          ADD +1 TO #S62-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN NONE
          MOVE TRUE TO #UNKNOWN
          ADD +1 TO #UNKNOWN-CNT
      END-DECIDE
*
    END-IF
*
    MOVE TRUE TO #UNKNOWN       /* REMOVE THIS WHEN MCI READY
    ADD 1 TO #MCI-PAGE-CNT
  END-IF
*
  MOVE SCREEN-REC-IN TO SCREEN-REC-OUT
*
  IF #UNKNOWN
    WRITE WORK 3 SCREEN-REC-OUT
  ELSE
    WRITE WORK 2 SCREEN-REC-OUT
  END-IF
END-WORK
*
IF (NOT #UNKNOWN)             /* if going to mci-worldcom
  PERFORM PROCESS-OMR-CODES
*
  IF #MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
                OR = 11 OR = 13 OR = 15 OR = 17 OR = 19
    RESET SCREEN-REC-OUT
    MOVE '1' TO SRO-CARRIAGE-CTL  /* force a blank back page
    MOVE 'DJDE FORM=NONE,END;' TO SRO-MCI-FORM  /* reset form
    WRITE WORK 2 SCREEN-REC-OUT
    RESET SCREEN-REC-OUT
    MOVE '1' TO SRO-CARRIAGE-CTL  /* force a blank omr page
    WRITE WORK 2 SCREEN-REC-OUT
  END-IF
END-IF
*
RESET SCREEN-REC-OUT
MOVE '1' TO SRO-CARRIAGE-CTL  /* to fix problem at end for charlie
MOVE 'DJDE FORM=NONE,END;' TO SRO-MCI-FORM  /* reset form
WRITE WORK 2 SCREEN-REC-OUT
MOVE '10### Use AFSC #10 outside envelope' TO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
MOVE ' 0### Inserts 1=#9 AFSC BRE envl' TO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
COMPRESS ' 0### Total Mailings =' #TOTAL-MAILING-CNT
     INTO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
*
WRITE (2) #TOTAL-SENTENCE #TOTAL-MAILING-CNT
*
WRITE #FSO-SENTENCE      #FSO-CNT
WRITE #RET-SENTENCE      #RET-CNT
WRITE #FIQ-SENTENCE      #FIQ-CNT
WRITE #DAS-SENTENCE      #DAS-CNT
WRITE #MED-SENTENCE      #MED-CNT
WRITE #M71-SENTENCE      #M71-CNT
WRITE #M70-SENTENCE      #M70-CNT
WRITE #M65-SENTENCE      #M65-CNT
WRITE #M62-SENTENCE      #M62-CNT
WRITE #S62-SENTENCE      #S62-CNT
WRITE #FSO-10-SENTENCE   #FSO-10-CNT
WRITE #UNKNOWN-SENTENCE #UNKNOWN-CNT
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-OMR-CODES  /* at bottom of pages
* * * * * * * * * * * * * * * * * *
*
IF #MCI-PAGE-CNT =  1
  DISPLAY #TOTAL-MAILING-CNT #MCI-PAGE-CNT
               #LAST-SRI (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
END-IF
*   we are processing the previous page
IF (#MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
               OR = 11 OR = 13 OR = 15 OR = 17 OR = 19)
  RESET SCREEN-REC-OUT
  MOVE '1' TO SRO-CARRIAGE-CTL       /* line 1
  MOVE '2' TO SRO-POS-1
  MOVE #DASHES TO SRO-MCI-CODE   /* gate/read verify
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 2
  MOVE '2' TO SRO-POS-1
  IF #MCI-PAGE-CNT NE 1
    MOVE #DASHES TO SRO-MCI-CODE /* demand feed (only non pg 1)
  END-IF
  WRITE WORK 2 SCREEN-REC-OUT
ELSE
  RESET SCREEN-REC-OUT
  MOVE '1' TO SRO-CARRIAGE-CTL  /* blank omr page on page 2/4/6 etc
  WRITE WORK 2 SCREEN-REC-OUT
END-IF
*
IF #MCI-PAGE-CNT = 1
  RESET SCREEN-REC-OUT               /* line 3
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT1 TO SRO-MCI-CODE     /* insert 1
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 4
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT2 TO SRO-MCI-CODE     /* insert 2
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 5
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT3  TO SRO-MCI-CODE     /* insert 3
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 6
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT4  TO SRO-MCI-CODE     /* insert 4
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 7
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT5  TO SRO-MCI-CODE     /* insert 5
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 8
  MOVE '2' TO SRO-POS-1
  MOVE #INSERT6  TO SRO-MCI-CODE     /* insert 6
  WRITE WORK 2 SCREEN-REC-OUT
END-IF
*
END-SUBROUTINE
*
END
