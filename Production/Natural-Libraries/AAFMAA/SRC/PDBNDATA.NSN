* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Program ID: PDBNDATA
* Project Persona Database
*
* PERSONA DATA COLLECT 
*
**************************************************************************
*                            MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NG    20130506        Created
*
DEFINE DATA
PARAMETER
1 #ISN            (P10)
1 #INSURED        (I2)
1 #AGE            (I2)
1 #EXCEL          (A1024)
1 #EXCEL-NoPol    (A1024)
*
LOCAL USING PDBLTTL
LOCAL
1 CNT-V VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 CONTACT-TYPE (A1)
  2 ID-NUMBER (N6)
  2 RANK-ID (N6)
  2 RANK-PROTECT (A1)
  2 MILITARY-STATUS (A1)
  2 PREFIX-TITLE (A25)
  2 FIRST-NAME (A25)
  2 MIDDLE-NAME (A25)
  2 LAST-NAME (A25)
  2 SUFFIX (A10)
  2 GENDER-CD (A1)
  2 DATE-OF-BIRTH (N8) 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-1 (A8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8) 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-1 (A8)
1 RR-V VIEW OF A-RANK-REFERENCE
  2 PAY-GRADE
1 ST-V VIEW OF A-STATUS
  2 INTRO-FLAG    (A1)
  2 STATUS        (A1/9)
  2 PLAN          (A3/9)
  2 SMOKER-FLAG   (A1/9)
  2 FACE-AMOUNT   (N7.2/9)
  2 DEATH-BENEFIT (N7.2/9)
  2 ISSUE-DATE    (N8/9)
*
1 #MARRIED        (I2)
1 #DOD            (A10)
1 #FACE-AMOUNT    (N9.2)
1 #FACE-AMOUNT-A  (A12)
1 #DEATH-BENEFIT  (N9.2)
1 #DEATH-BENEFIT-A (A12)
1 #PLAN           (A3/5)
1 #SMOKER-FLAG    (A1/5)
1 #EXP-DATE       (A10/5)
1 #PLAN-NAME      (A/0:6) DYNAMIC
1 #TAB            (A1)  CONST <H'09'>
1 YES             (A1)  CONST <'Y'>
1 #NoActivePol    (A16) CONST <'No active policy'>
1 J               (I2)
1 #J              (I2)
1 #D              (D)
1 #HAS-KIDS       (I2)
END-DEFINE
*
GET CNT-V #ISN
*
FIND (1) RR-V WITH RANK-ID = CNT-V.RANK-ID
END-FIND
RESET #FACE-AMOUNT #DEATH-BENEFIT
  J
  #PLAN (*)
  #PLAN-NAME (*)
  #EXP-DATE  (*)
*
MOVE EDITED CNT-V.DATE-OF-BIRTH-1 TO #D (EM=YYYYMMDD)
MOVE EDITED #D (EM=YYYY-MM-DD) TO #DOD
*
PERFORM USE_STATUS   /* OR USE_POLICY
*
#INSURED := J
IF #FACE-AMOUNT > 0
  MOVE EDITED #FACE-AMOUNT (EM=Z(8)9.99) TO #FACE-AMOUNT-A
ELSE
  RESET #FACE-AMOUNT-A
END-IF
IF #DEATH-BENEFIT > 0
  MOVE EDITED #DEATH-BENEFIT (EM=Z(8)9.99) TO #DEATH-BENEFIT-A
ELSE
  RESET #DEATH-BENEFIT-A
END-IF
IF #PLAN (1) > ' '
  #PLAN-NAME (0) := FUNCTION-PRODUCT-2 (< #PLAN (1) >) (1)
  #PLAN-NAME (1) := FUNCTION-PRODUCT-2 (< #PLAN (1),#SMOKER-FLAG (1) >) (2)
ELSE
  #PLAN-NAME (0) := #NoActivePol
END-IF
FOR J 2 TO 5
  IF #PLAN (J) > ' '
    #PLAN-NAME (J) := FUNCTION-PRODUCT-2 (< #PLAN (J), #SMOKER-FLAG (J) >) (2)
  END-IF
END-FOR
#MARRIED := IF_MARRIED (< CONTACT-ID >)
IF #MARRIED > 1
  #MARRIED := 1   /* DATA CLEAN UP
END-IF
*
#HAS-KIDS := HAS_KIDS (< CONTACT-ID >)
IF #INSURED <= 0
  COMPRESS FULL
    ID-NUMBER
    PREFIX-TITLE
    FIRST-NAME
    LAST-NAME
    GENDER-CD
    PAY-GRADE
    #MARRIED
    #HAS-KIDS
    #AGE
    #DOD
    INTO #EXCEL-NoPol WITH DELIMITER #TAB
END-IF
COMPRESS FULL
  ID-NUMBER
  PREFIX-TITLE
  FIRST-NAME
  LAST-NAME
  GENDER-CD
  PAY-GRADE
  #MARRIED
  #HAS-KIDS
  #AGE
  #DOD
  #INSURED
  #FACE-AMOUNT-A
  #DEATH-BENEFIT-A
  #PLAN-NAME (0)
  #PLAN-NAME (1)
  #EXP-DATE  (1)
  #PLAN-NAME (2)
  #EXP-DATE  (2)
  #PLAN-NAME (3)
  #EXP-DATE  (3)
  #PLAN-NAME (4)
  #EXP-DATE  (4)
  #PLAN-NAME (5)
  #EXP-DATE  (5)
  INTO #EXCEL WITH DELIMITER #TAB
*
DEFINE USE_STATUS
/*     ==========
FIND ST-V WITH ID-NUMBER = CNT-V.ID-NUMBER
  FOR #J 1 TO 9
    IF ST-V.STATUS  (#J)  NE 'D'
      ESCAPE TOP
    END-IF
    ADD ST-V.FACE-AMOUNT (#J)   TO #FACE-AMOUNT
    ADD ST-V.DEATH-BENEFIT (#J) TO #DEATH-BENEFIT
    ADD 1 TO J
    IF J < 6
      #PLAN (J) := ST-V.PLAN (#J)
      #SMOKER-FLAG (J) := SMOKER-FLAG (#J)
      #D := Policy_Expiration_Date (< #PLAN (J),SMOKER-FLAG (#J), ISSUE-DATE (#J),CNT-V.DATE-OF-BIRTH >)
      IF  #D > 0
        MOVE EDITED #D (EM=YYYY-MM-DD) TO #EXP-DATE (J)
      END-IF
    END-IF
  END-FOR
END-FIND
END-SUBROUTINE
*
END
