* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1060PAF   NO LONGER USED
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 SSN
 2 ID-NUMBER
 2 MEMBER-CN
 2 REDEFINE MEMBER-CN
  3 MEMBER-CN-N  (N6)
 2 NAME
 2 DATE-RECEIVED
 2 NEXT-ACTION-DATE
 2 NOTES      (A75/40)
*
1 NA-V VIEW OF A-APPL-TRACKING
 2 NEXT-ACTION-DATE
 2 INTRO-FLAG
*
1 #SCR-COMMAND     (A2/11)   /* only uses 10
1 #SCR-X           (A1/11)
1 #SCR-CV          (C/11)
1 #COMMAND-CV      (C/11)
1 #NOTES-CV        (C/11)
1 #SCR-ID         (N6/11)
1 #SCR-CN         (N6/11)
1 #SCR-SSN        (N9/11)
1 #SCR-NXT-ACT-DT (A10/11)
1 #SCR-NXT-ACT-DT-YYYYMMDD (N8/11)
1 #SCR-NAME       (A25/11)
1 #SCR-DT-RCVD    (A10/11)
1 #SCR-NOTES      (A75/11)
*
1 #SCR-NEXT-ACT-DT (A10)
*
1 #I1    (I2)
1 #I2    (I2)
1 #USER-DATE (A15)
1 #TEXT      (A75)
1 #UPDATE    (L)
*
1 #LAST-NEXT-ACT-DT (N8)
1 #YYYYMMDD (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY     (A4)
 2 #MM       (A2)
 2 #DD       (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-N   (N4)
 2 #MM-N     (N2)
 2 #DD-N     (N2)
*
END-DEFINE
*
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
*
MOVE (AD=IP) TO #NOTES-CV (*)
MOVE (AD=IP) TO #SCR-CV   (*)
MOVE (AD=IP) TO #COMMAND-CV (*)
*
READ AT-V WITH NEXT-ACTION-DATE
  IF #CGA-SCR-ID NE 0
    IF #CGA-SCR-ID LT 500000
      IF MEMBER-CN-N NE #CGA-SCR-ID
        ESCAPE TOP
      END-IF
    ELSE
      IF SSN NE #CGA-SCR-ID
        ESCAPE TOP
      END-IF
    END-IF
*
    RESET #CGA-SCR-ID
  END-IF
  IF   (NEXT-ACTION-DATE  GT *DATN)
   AND (#LAST-NEXT-ACT-DT LE *DATN)
    ADD 1 TO #I1
    MOVE 999999    TO #SCR-ID (#I1)
    MOVE 999999    TO #SCR-CN (#I1)
    MOVE 999999999 TO #SCR-SSN (#I1)
    MOVE ALL '*' TO #SCR-NAME (#I1)
    MOVE ALL '*' TO #SCR-NXT-ACT-DT (#I1)
    MOVE ALL '*' TO #SCR-DT-RCVD (#I1)
    MOVE '****** FUTURE ACTION DATES FOLLOW ******' TO #SCR-NOTES (#I1)
*
    IF #I1 GE 10
      PERFORM SHOW-SCREEN
    END-IF
  END-IF
*
  ADD 1 TO #I1
  MOVE NEXT-ACTION-DATE TO #LAST-NEXT-ACT-DT
*
  IF NEXT-ACTION-DATE LE *DATN
    MOVE (AD=P) TO #SCR-CV (#I1)
  END-IF
*
  RESET #COMMAND-CV (#I1)
  MOVE ID-NUMBER TO #SCR-ID (#I1)
  MOVE MEMBER-CN-N TO #SCR-CN (#I1)
  MOVE NAME      TO #SCR-NAME (#I1)
  MOVE SSN       TO #SCR-SSN  (#I1)
  MOVE NEXT-ACTION-DATE TO #SCR-NXT-ACT-DT-YYYYMMDD (#I1)
  MOVE NEXT-ACTION-DATE TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #SCR-NXT-ACT-DT (#I1) WITH '/'
  MOVE DATE-RECEIVED TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #SCR-DT-RCVD (#I1) WITH '/'
*
  FOR #I2 40 TO 1 STEP -1
    IF (NOTES (#I2)  = MASK ('*')) OR (#I2 = 1)
      MOVE NOTES (#I2) TO #SCR-NOTES (#I1)
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I1 GE 10
    PERFORM SHOW-SCREEN
  END-IF
END-READ
*
IF #CGA-SCR-ID NE 0
  COMPRESS 'Number entered' #CGA-SCR-ID 'not found' INTO #TEXT
  PERFORM SHOW-SCREEN
END-IF
*
IF #I1 GT 0
  PERFORM SHOW-SCREEN
END-IF
*
MOVE 'AF' TO #CGA-SCR-COMMAND
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-SCREEN
* * * * * * * * * * * * * * *
*
MOVE '*' TO #CGA-SCR-COMMAND
INPUT TEXT #TEXT USING MAP 'C1060MAF'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
FOR #I2 1 TO 10
  IF #SCR-COMMAND (#I2) NE ' '
    MOVE #SCR-COMMAND (#I2) TO #CGA-SCR-COMMAND
    MOVE #SCR-SSN (#I2) TO #CGA-SCR-ID
    FETCH 'G1000PXX'
  END-IF
END-FOR
*
RESET #UPDATE
FOR #I2 1 TO 10
  IF   (#SCR-X (#I2) NE ' ')
   AND (#SCR-NXT-ACT-DT-YYYYMMDD (#I1) LE *DATN)
    MOVE TRUE TO #UPDATE
    FIND NA-V WITH SSN = #SCR-SSN (#I2)
      IF  ((#CGA-USER-COMPANY = 'I') AND (NA-V.INTRO-FLAG = 'V'))
       OR ((#CGA-USER-COMPANY = 'S') AND (NA-V.INTRO-FLAG NE 'V'))
        ESCAPE TOP
      END-IF
*
      MOVE NA-V.NEXT-ACTION-DATE TO #YYYYMMDD
      ADD 14 TO #DD-N
      IF #DD-N GT 28
        SUBTRACT 28 FROM #DD-N
        ADD 1 TO #MM-N
        IF #MM-N GT 12
          MOVE 1 TO #MM-N
          ADD 1 TO #YYYY-N
        END-IF
      END-IF
*
      MOVE #YYYYMMDD TO NA-V.NEXT-ACTION-DATE
      COMPRESS #MM #DD #YYYY INTO #SCR-NXT-ACT-DT (#I2) WITH '/'
      UPDATE (0166)
      END TRANSACTION
    END-FIND
  END-IF
END-FOR
*
IF #UPDATE
  MOVE (AD=IP) TO #NOTES-CV (*)
  MOVE (AD=IP) TO #SCR-CV   (*)
  MOVE (AD=IP) TO #COMMAND-CV (*)
  MOVE 'Rec(s) updated - Press Enter to continue' TO #TEXT
  MOVE '*' TO #CGA-SCR-COMMAND
  INPUT TEXT #TEXT USING MAP 'C1060MAF'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
END-IF
*
RESET #I1
RESET #SCR-ID         (*)
RESET #SCR-CN         (*)
RESET #SCR-NAME       (*)
RESET #SCR-SSN        (*)
RESET #SCR-NXT-ACT-DT (*)
RESET #SCR-NXT-ACT-DT-YYYYMMDD (*)
RESET #SCR-DT-RCVD    (*)
RESET #SCR-NOTES      (*)
RESET #SCR-COMMAND    (*)
MOVE (AD=IP) TO #NOTES-CV (*)
MOVE (AD=IP) TO #SCR-CV   (*)
MOVE (AD=IP) TO #COMMAND-CV (*)
*
END-SUBROUTINE
*
END
