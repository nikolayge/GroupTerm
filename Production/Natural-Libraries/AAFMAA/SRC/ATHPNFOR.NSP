* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPNFOR - list non-forfiture stuff
*
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)
*                    EBCDIC to HEX code '09' ASCII
* DRW  09062006 DW1  Increase size of #recs-read
* RSE  09302009 RE1  Change PROCESS-IND logic for Unification
*******************************************
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #FPF-11   (A11)
1 #BADJ-11  (A11)
1 #NFOR-FILE   (A50)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 WIDOW-FLAG          (A1)
  2 PROCESS-IND
  2 MEMBER-FLAG         (A1)
  2 SSN
  2 NUMBER-MEMBERSHIPS
  2 STATUS           (9)
  2 X-BILL-ADJUST    (9)
  2 X-FIRST-PLACE-FEE (9)
  2 ISSUE-DATE          (N8/9)
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* DW1
  2 #RECS-SELECTED    (P5)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #CNT-4            (P5)
  2 #CNT-5            (P5)
  2 #CNT-6            (P5)
  2 #CNT-7            (P5)
  2 #CNT-8            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM (N6)
 2 REDEFINE #YYYYMM
  3 #YYYY   (N4)
  3 #MM     (N2)
 2 #DD     (N2)
*
*
END-DEFINE
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'EXCEPTIONS                    '
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'Has FPF or BADJ'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS 'CN'
  #TAB 'FPF'
  #TAB 'BILL-ADJUST'
  #TAB 'ISS-DATE'
      INTO #NFOR-FILE LEAVING NO
WRITE WORK 1 #NFOR-FILE
*
READ ST-V BY MEMBER-KEY FROM '001900'
  ADD 1 TO #RECS-READ
  IF  (ST-V.ID-NUMBER LE 1900)
   OR (ST-V.WIDOW-FLAG = 'Y')        /* MBR ALREADY DEAD
   OR (ST-V.NUMBER-MEMBERSHIPS = 0)       /* MBR INACTIVE
*   OR (ST-V.PROCESS-IND = 'C' OR = 'S')             /* contact entities
   OR (ST-V.PROCESS-IND = 'C')             /* contact entities  RE1
   OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)                     /* RE1
    ESCAPE TOP
  END-IF
*
  FOR #I1 1 TO 9
    IF STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    IF   (X-FIRST-PLACE-FEE (#I1) = 0)
     AND (X-BILL-ADJUST (#I1) = 0)
      ESCAPE TOP
    END-IF
*
    MOVE EDITED X-FIRST-PLACE-FEE (#I1) (EM=ZZZZ,ZZZ.99) TO #FPF-11
    MOVE EDITED X-BILL-ADJUST     (#I1) (EM=ZZZZ,ZZZ.99) TO #BADJ-11
    COMPRESS ST-V.ID-NUMBER
      #TAB #FPF-11
      #TAB #BADJ-11
      #TAB ISSUE-DATE (#I1)
         INTO #NFOR-FILE LEAVING NO
    WRITE WORK 1 #NFOR-FILE
*
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ST-V.ID-NUMBER #FPF-11 #BADJ-11 ISSUE-DATE (#I1)
    ADD 1 TO #RECS-SELECTED
  END-FOR
*
END-READ
*
WRITE  'RECS READ  ' #RECS-READ
WRITE  'RECS SEL   ' #RECS-SELECTED
*
END
