* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2001NNM
***********************************************************************************
*                             MODIFICATION LOG                                    *
* USER   DATE      TAG    REASON                                                  *
* NGG  08/18/2011  None   CRM project - create Status record (CN) from CB  *
***********************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID       (N8)
1 #ID-NUMBER        (N6)
1 #MSG              (A72)
*
LOCAL
1 #CONFIRM-CREATE     (A1)
1 #LONG-MSG           (A) DYNAMIC
1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
1 #ERROR-TIME             (A19)
*
1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER (N6)
*
END-DEFINE
*
DEFINE WINDOW Confirm-Create
  SIZE 8 * 60
  BASE 9/9 
  FRAMED ON POSITION SYMBOL AUTO
*
SET WINDOW 'Confirm-Create'
REPEAT
  INPUT WINDOW='Confirm-Create' USING MAP 'C2009MNM'
  IF *PF-KEY = 'PF3'
    ESCAPE BOTTOM
  END-IF
  IF #CONFIRM-CREATE = 'Y'
    CALLNAT 'C2002NNM'
      #CONTACT-ID
      #ID-NUMBER
      #LONG-MSG
    IF #ID-NUMBER = 0
      COMPRESS #MSG 'CN creation failed' INTO #MSG
    END-IF
    ESCAPE BOTTOM
  END-IF
END-REPEAT 
*
ON ERROR
  RESET #ID-NUMBER
  CALLNAT 'USR2001N' P_PARMS
  MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
    TO #ERROR-TIME
*
  COMPRESS
    'Error number:' P_ERROR_NUM
    'Error class:' P_ERROR_CLASS
    'Error line:' P_ERROR_LINE
    'Error program:' P_ERROR_PROGRAM
    'Error program type:' P_ERROR_PGM_TYPE
    'Error program type long:' P_ERROR_PGM_TYPE_LONG
    'Error program level:' P_ERROR_LEVEL
    'Language at error time:' P_ERROR_LANG
    'Error library:' P_ERROR_LIB
    'DBID:' P_ERROR_DBID 'FNR:' P_ERROR_FNR
    'Error type:'  P_ERROR_TYPE
    'Error time:' #ERROR-TIME
    INTO #LONG-MSG
*
  ESCAPE ROUTINE
END-ERROR
*
END
