* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM-ID: C2200SCG - Populate rank fields                          *
************************************************************************
DEFINE DATA
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  11/15/2010  NONE    Initial creation                            *
* RSE  06/07/2011  RE1     Add fields not related to RANK              *
************************************************************************
*
PARAMETER
1 #STATUS-ISN            (P8)
1 #CG-RANK
  2 #RANK-PROTECTION     (A1)
  2 #RANK-PROT-DATE      (A10)
  2 #RANK-PROT-TEXT      (A5)
  2 #RANK-DESCRIPTION    (A32)
  2 #BRANCH-DESCRIPTION  (A28)
  2 #PAY-GRADE           (A3)
  2 #PROCESS-IND         (A1)
  2 #PROCESS-IND-EDESC   (A40)
  2 #INTRO-FLAG          (A1)
  2 #INTRO-FLAG-EDESC    (A40)
  2 #MILITARY-STAT-DESC  (A28)
  2 #INS-MAIL-CODE-DESC  (A40)  /* RE1
  2 #NEWSLETTER-DESC     (A40) /* RE1
  2 #ANNUAL-MEETING-DESC (A40) /* RE1
  2 #ANN-RPT-DESC        (A40) /* RE1
  2 #BRIDGER-DESC        (A40) /* RE1
  2 #UNDERWRITING-DESC   (A40) /* RE1
  2 #PROXY-BALLOT-DESC   (A40) /* RE1
  2 #PREFIX-TITLE        (A25) /* RE1
  2 #PREFIX-TITLE-DESC   (A40) /* RE1
LOCAL

1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID  (N8)
  2 SPOUSE-CONTACT-ID  (N8)
  2 MEMBER-FLAG
  2 INTRO-FLAG
  2 PROCESS-IND
  2 WIDOW-FLAG
  2 RANK
  2 CHIEF-OF-STAFF-IND
  2 MILITARY-SERVICE
  2 INS-MAIL-CODE      /* RE1
  2 NEWSLETTER-CODE    /* RE1
  2 DATE-BOOK          /* RE1
  2 BRIDGER-IND        /* RE1
  2 PROXY              /* RE1
  2 ANNUAL-RPT-FLAG    /* RE1
  2 SUSPEND-CODE       /* RE1
*  2 MILITARY-STATUS

1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 RANK-ID
  2 RANK-PROTECT
  2 RANK-PROTECT-DATE
  2 REDEFINE RANK-PROTECT-DATE
    3 #PROTECT-DATE-YY         (A4)
    3 #PROTECT-DATE-MM         (A2)
    3 #PROTECT-DATE-DD         (A2)
  2 MILITARY-STATUS
  2 LAST-RANK-DATE-UPD
  2 LAST-RANK-USER-UPD
  2 PREV-RANK-ID
  2 SPECIAL-PAY-IND
  2 PREFIX-TITLE         /* RE1        
  2 LAST-USER-UPD
  2 LAST-DATE-UPD
  2 LAST-TIME-UPD
*
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 RANK-ID
  2 PAY-GRADE
  2 MILITARY-BRANCH
  2 MILITARY-ID
  2 RANK-DESCRIPTION
  2 SPECIAL-PAY-IND
  2 RANK-ABBR
  2 RANK-IN-SAS
  2 MESSAGE-NBR

1 YES      (A1) CONST <'Y'>
END-DEFINE

GET STATUS #STATUS-ISN

FIND (1) CONTACT-V WITH CONTACT-ID = MEMBER-CONTACT-ID
END-FIND

FIND (1) RANK-V WITH RANK-ID = CONTACT-V.RANK-ID
END-FIND

COMPRESS RANK-ABBR '<' RANK-DESCRIPTION '>'
  INTO #RANK-DESCRIPTION LEAVING NO
IF DATE-BOOK = ' '            /* RE1 START
  MOVE 'N' TO DATE-BOOK
END-IF
IF BRIDGER-IND = ' '
  MOVE 'N' TO BRIDGER-IND
END-IF
IF PROXY = ' '
  MOVE 'N' TO PROXY
END-IF
IF NEWSLETTER-CODE = ' '
  MOVE 'Y' TO NEWSLETTER-CODE
END-IF                        /* RE1 END
#PAY-GRADE       := PAY-GRADE
#RANK-PROTECTION := RANK-PROTECT
#PROCESS-IND     := STATUS.PROCESS-IND
#INTRO-FLAG      := STATUS.INTRO-FLAG
#PREFIX-TITLE    := PREFIX-TITLE           /* RE1
#PROCESS-IND-EDESC := FUNCTION-CODE-1-DESC (< '022', #PROCESS-IND >)
#INTRO-FLAG-EDESC  := FUNCTION-CODE-1-DESC (< '023', #INTRO-FLAG >)
#BRANCH-DESCRIPTION := FUNCTION-CODE-3-DESC (< '024', MILITARY-SERVICE >)
#MILITARY-STAT-DESC := FUNCTION-CODE-1-DESC (< '025', CONTACT-V.MILITARY-STATUS >)
#PREFIX-TITLE-DESC  := FUNCTION-PREFIX-DESC(< #PREFIX-TITLE >)             /* RE1 START
#INS-MAIL-CODE-DESC := FUNCTION-CODE-A-DESC (< '042', INS-MAIL-CODE >)
#NEWSLETTER-DESC    := FUNCTION-CODE-A-DESC (< '043', NEWSLETTER-CODE >)
#ANNUAL-MEETING-DESC:= FUNCTION-CODE-1-DESC (< '045', DATE-BOOK >)
#ANN-RPT-DESC       := FUNCTION-CODE-1-DESC (< '046', ANNUAL-RPT-FLAG >)
#BRIDGER-DESC       := FUNCTION-CODE-1-DESC (< '048', BRIDGER-IND >)
#UNDERWRITING-DESC  := FUNCTION-CODE-1-DESC (< '044', SUSPEND-CODE >)
#PROXY-BALLOT-DESC  := FUNCTION-CODE-1-DESC (< '047', PROXY >)             /* RE1 END
IF #RANK-PROTECTION = YES
  #RANK-PROT-TEXT := 'AS OF'
  COMPRESS #PROTECT-DATE-MM '/' #PROTECT-DATE-DD '/' #PROTECT-DATE-YY
    INTO #RANK-PROT-DATE LEAVING NO
ELSE
  RESET #RANK-PROT-TEXT #RANK-PROT-DATE
END-IF

END
