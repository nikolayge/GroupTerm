* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1097NAT - Create Status record for new application
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    07/28/2011  None    Program written                           *
* bz HD-3056 203/10/2017 added Create Dummy WEB Account
************************************************************************
DEFINE DATA
PARAMETER
1 #AT-ISN      (P8)
LOCAL USING GOSTMA01
LOCAL
1 p_GOST0N01
  2 p_CN       (N6)
  2 p_Err      (N4)
  2 p_Err_Dscr (A78)
  2 p_Mod      (A1)
1 AT-V VIEW OF A-APPL-TRACKING
  2 CONTACT-ID      (N8)
  2 MEMBER-CN      (A6) 2 REDEFINE MEMBER-CN
    3 MEMBER-CN-N  (N6)
  2 MIL-SERVICE    (A3)
  2 MIL-STATUS     (A1)
  2 RANK           (A3)
  2 APPL-SOURCE    (A7)
1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER       (N6)
  2 SSN             (N9)
  2 DATE-OF-BIRTH   (N8)
1 STATUS VIEW OF A-STATUS
  2 SSN
*  2 DATE-OF-BIRTH
*
1 #CN-PARMS
  2 CN                   (N6)
  2 ERR                  (N4)
  2 ERR-DESC             (A78)
  2 MOD                  (A1)
*
END-DEFINE
*
IF #AT-ISN <= 0
  ESCAPE ROUTINE
END-IF
GET AT-V #AT-ISN
IF MEMBER-CN-N <= 0
  PERFORM STATUS-RECORD
  UPDATE (0052)
END-IF
*
DEFINE STATUS-RECORD
/*     =============
FIND CONTACT WITH CONTACT-ID = AT-V.CONTACT-ID     /* NG1
  IF CONTACT.ID-NUMBER > 0
    AT-V.MEMBER-CN-N := CONTACT.ID-NUMBER
    ESCAPE ROUTINE
  END-IF
END-FIND
*
CALLNAT 'GOST0N01' p_GOST0N01     /* Get New CN, Create DUMMY STATUS*
CALLNAT 'GOWEB001' p_CN  p_Err    /* bz-HD-3056 Create Dummy WebAccount
*
#STATUS.ID-NUMBER := p_CN
AT-V.MEMBER-CN-N  := p_CN
IF #STATUS.ID-NUMBER > 0
  FIND CONTACT WITH CONTACT-ID = AT-V.CONTACT-ID
    CONTACT.ID-NUMBER := #STATUS.ID-NUMBER
    UPDATE
  END-FIND
END-IF
#STATUS.MEMBER-CONTACT-ID       := AT-V.CONTACT-ID
#STATUS.MEMBER-FLAG             := 'Y'
#STATUS.INTRO-FLAG              := 'A'
#STATUS.PROCESS-IND             := 'C'
#STATUS.RANK                    := AT-V.RANK
#STATUS.MILITARY-SERVICE        := AT-V.MIL-SERVICE
#STATUS.MILITARY-STATUS         := AT-V.MIL-STATUS
#STATUS.APPL-SOURCE             := AT-V.APPL-SOURCE
*
CALLNAT 'GOSTMN01' #STATUS p_Err  p_Err_Dscr p_Mod
*
FIND STATUS WITH ID-NUMBER = #STATUS.ID-NUMBER
  STATUS.SSN := CONTACT.SSN
*  STATUS.DATE-OF-BIRTH := CONTACT.DATE-OF-BIRTH
  UPDATE (0089)
END-FIND
*
#CN-PARMS.CN := p_CN            /* Synch contact information with Status
CALLNAT 'GOSTCN01'
  #CN-PARMS
*                         NG1 Start
* CALLNAT 'GOSTAN01'                        /* Synch addresses
*   #CN-PARMS
*
* CALLNAT 'GOSTPN01'                        /* Synch phone
*   #CN-PARMS
* *
* CALLNAT 'GOSTEN01'                        /* Synch email
*   #CN-PARMS
*                         NG1 End
END-SUBROUTINE
END
