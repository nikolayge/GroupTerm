* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPTRM1
*  THIS PROGRAM EXTRACTS INFO FOR COUNTING TERMINATIONS
* TMT  06/10/2010  CONTACT NORMALIZATION - Gender and DOB
DEFINE DATA
LOCAL
*
1 VA-V VIEW OF A-VA-HISTORY
 2 RECORD-KEY            (N15)
 2 REDEFINE RECORD-KEY
  3 VA-ID-NUM            (N6)
  3 REDEFINE VA-ID-NUM
   4 VA-ID-NUM-A         (A6)
  3 VA-ISS-DATE          (N8)
  3 VA-SPL-IND           (N1)
 2 TERMINATION-DATE  (P7)
 2 TERMINATION-CODE  (A3)
 2 ISSUE-AGE         (A2)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER             (N6)
 2 MEMBER-CONTACT-ID     (N8)            /* TT1
*  2 DATE-OF-BIRTH         (N8)
 2 NAME                  (A25)
 2 RANK                  (A3)
 2 MILITARY-STATUS       (A1)
 2 MILITARY-SERVICE      (A3)
 2 INTRO-FLAG            (A1)
*
1 NS-V VIEW OF A-NOTES
 2 ID-NUMBER             (N6)
 2 NOTE-TEXT             (A60/2)
*
1 #JCL-PARM              (A18)
1 REDEFINE #JCL-PARM
 2 #JCL-YYYY             (N4)
 2 #FIL-1                (A1)
 2 #JCL-START-CN         (N6)
 2 #FIL-2                (A1)
 2 #JCL-END-CN           (N6)
*
1 #VA-TERM-DATE           (N6)
1 REDEFINE #VA-TERM-DATE
 2 #VA-TERM-YYYY         (N4)
 2 #VA-TERM-MM           (N2)
*
1 #EXTRACT
 2 #EXT-TERM-TYPE         (A1)
 2 #EXT-TERM-DATE         (N6)
 2 #EXT-ISSUE-DATE        (N8)
 2 #EXT-RANK              (A3)
 2 #EXT-MIL-STAT          (A1)
 2 #EXT-MIL-SERV          (A3)
 2 #EXT-INTRO-FLAG        (A1)
 2 #EXT-ID-NUMBER         (N6)
 2 #EXT-ISSUE-AGE         (A2)
 2 #EXT-DOB               (N8)
 2 #EXT-NAME              (A25)
*
1 #TERM-TYPE             (A1)
1 #I1                    (I2)
*
1 #WYYYYMMDD     (N8)
1 REDEFINE #WYYYYMMDD
 2 #WCENT-A      (A2)
 2 #WYY-A        (A2)
 2 #WMM-A        (A2)
 2 #WDD-A        (A2)
1 DATE-OF-BIRTH  (N8.0)         /* TT1 
*
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INPUT #JCL-PARM
* READ (20000) VA-V PHYSICAL
READ VA-V PHYSICAL
  MOVE TERMINATION-DATE TO #VA-TERM-DATE
  IF  (NOT (TERMINATION-CODE = 'CSV' OR = 'LAP'))
   OR (#VA-TERM-YYYY NE #JCL-YYYY)
   OR (VA-ID-NUM LT #JCL-START-CN)
   OR (VA-ID-NUM GT #JCL-END-CN)
    ESCAPE TOP
  END-IF
*
  RESET NS-V.NOTE-TEXT (*)
  FIND (1) NS-V WITH ID-NUMBER = VA-ID-NUM
    FOR #I1 1 TO 2
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*CSV '))
       OR (NOTE-TEXT (#I1) = MASK('*CSV''D'))
       OR (TERMINATION-CODE = 'CSV')
        MOVE '1' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED NON'))
        MOVE '3' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED OVER'))
        MOVE '4' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED'))
        MOVE '5' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*RSIGN'))
        MOVE '6' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'...................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*'.....................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*1035 EX'))
        MOVE '2' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF #I1 = 2
        IF TERMINATION-CODE = 'CSV'
          MOVE '1' TO #TERM-TYPE     /* ASSUME REGULAT CASH VALUE
        ELSE
          MOVE '3' TO #TERM-TYPE     /* ASSUME LAPSE NON-PAY
        END-IF
      END-IF
    END-FOR
  END-FIND
*
  FIND (1) ST-V WITH MEMBER-KEY = VA-ID-NUM-A
  END-FIND
*
  MOVE #TERM-TYPE            TO #EXT-TERM-TYPE
  MOVE TERMINATION-DATE      TO #EXT-TERM-DATE
  MOVE VA-ISS-DATE           TO #EXT-ISSUE-DATE
*
  IF ST-V.MILITARY-STATUS = 'C'
    MOVE 'CIV' TO #EXT-RANK
  ELSE
    IF   (ST-V.RANK = MASK ('CW'))
     OR  (ST-V.RANK = MASK ('WO'))
      MOVE 'C-W' TO #EXT-RANK
    ELSE
      MOVE ST-V.RANK TO #EXT-RANK
    END-IF
  END-IF
*
  RESET DATE-OF-BIRTH
  DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)
  MOVE ST-V.MILITARY-STATUS  TO #EXT-MIL-STAT
  MOVE ST-V.MILITARY-SERVICE TO #EXT-MIL-SERV
  MOVE ST-V.INTRO-FLAG       TO #EXT-INTRO-FLAG
  MOVE VA-ID-NUM             TO #EXT-ID-NUMBER
  MOVE VA-V.ISSUE-AGE        TO #EXT-ISSUE-AGE
  MOVE DATE-OF-BIRTH        TO #EXT-DOB                   /* TT1
  MOVE ST-V.NAME             TO #EXT-NAME
  WRITE WORK 1 #EXTRACT
END-READ
*
END
