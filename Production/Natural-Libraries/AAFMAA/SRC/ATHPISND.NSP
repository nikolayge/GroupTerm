* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPISND  ** isn delete (delete md/pd recs if isn not fnd)
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* RSE  09/29/2009 RE1  Change 600K logic for Unification
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX        /* WORK FIELDS FOR 'GONERCXX'
LOCAL
*
1 MD-V VIEW OF A-MBRSHIP-MTD-DETAIL
 2 ID-NUMBER
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A  (A6)
 2 REC-ISN
 2 CODE
 2 ENTRY-DATE
 2 ISSUE-DATE
 2 USER
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
1 CL-V VIEW OF A-CLAIM
 2 ID-NUMBER
1 FR-V VIEW OF A-FIRS
 2 ID-NUMBER
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER
*
1 #DO-WRITE  (L)
1 #NOT-FND   (L)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
* MOVE TRUE TO #DO-WRITE
*
READ MD-V BY ENTRY-DATE FROM 00000000
  RESET #NOT-FND
*  IF (MD-V.ID-NUMBER GE 200000) AND (MD-V.ID-NUMBER LT 600000)                 RE1
  IF (MD-V.ID-NUMBER GE 200000) AND NOT AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1     
    FIND NT-V WITH ID-NUMBER = MD-V.ID-NUMBER
      IF NO RECORDS FOUND
        IF MD-V.ID-NUMBER NE 0
          MOVE TRUE TO #NOT-FND
        END-IF
         WRITE 'NT NOT FND USING MD ID-#' MD-V.ID-NUMBER CODE
           'MD REC-ISN=' MD-V.REC-ISN USER ENTRY-DATE ISSUE-DATE
      END-NOREC
    END-FIND
  ELSE
    FIND ST-V WITH MEMBER-KEY = MD-V.ID-NUMBER-A
      IF NO RECORDS FOUND
        FIND ST-V WITH WIDOW-KEY = MD-V.ID-NUMBER-A
          IF NO RECORDS FOUND
            IF MD-V.ID-NUMBER NE 0
              MOVE TRUE TO #NOT-FND
            END-IF
*   >>>>>>>>>>  THIS CONDITION SHOULD NEVER HAPPEN    <<<<<<<<<
            WRITE 'ST NOT FND USING MD ID-#' MD-V.ID-NUMBER CODE
               'MD REC-ISN=' MD-V.REC-ISN USER ENTRY-DATE ISSUE-DATE
*
            IF MD-V.ID-NUMBER = 0 /* USED TO FIND A CN WITH ISN & FIX
              GET ST-V REC-ISN
              WRITE ST-V.ID-NUMBER 'CN USING ISN #' REC-ISN
                      'HAS BEEN FIXED'
              MOVE ST-V.ID-NUMBER TO MD-V.ID-NUMBER
              UPDATE (0043)
              END TRANSACTION
            END-IF
*
          END-NOREC
        END-FIND
      END-NOREC
    END-FIND
  END-IF
*
  IF #NOT-FND
    WRITE 'DELETED MD ISN' REC-ISN 'CN' MD-V.ID-NUMBER
    DELETE (0043)            /* <<<<<<<<<<<<<<<<<<<<<<<<<  DELETE
  END-IF
*
  END TRANSACTION
END-READ
*
WRITE 'WE"RE DONE'
*
END
