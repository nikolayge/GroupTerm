* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: ADRNS002
* FUNCTION: Checks whether CI of Depn/Other already exists
***********************************************************
*        MODIFICATION LOG
***********************************************************
* USER   DATE       TAG  REASON
* RSE    08/23/2010      Creation
* RSE    12/08/2010 RE1  Add DEATH-CERT-CODE
* RSE    01/17/2012 RE2  Replace POC with organization name
********************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-ORIG-ID      (N6)
1 #CONTACT-ID       (N8)
1 #SSN              (N9)
1 #FIRST-NAME       (A25)
1 #MIDDLE-NAME      (A25)
1 #LAST-NAME        (A25)
1 #SUFFIX           (A10)
1 #SEX              (A1)
1 #DOB              (A8)
1 #BCC              (A1)
1 #DOD              (A8)
1 #DCC              (A1)        /* RE1
1 #DOM              (A8)
1 #DIV              (A8)
1 #CN               (N6)
1 #LST-UPD          (A10)
1 #LST-USR          (A22)
1 #NOT-FOUND        (L)
1 #PF3              (L)
LOCAL
*
1 CONT-V VIEW OF A-CONTACTS
  2 CONTACT-ID       (N8)           /* D
  2 CONTACT-TYPE                  /* RE2
  2 ID-NUMBER        (N6)           /* D
  2 FIRST-NAME       (A25)
  2 MIDDLE-NAME      (A25)
  2 LAST-NAME        (A25)
  2 SUFFIX           (A10)
  2 GENDER-CD        (A1)         /* --NA
  2 DATE-OF-BIRTH    (N8)
  2 SSN              (N9)           /* D
  2 DATE-OF-DEATH    (N8)
  2 BIRTH-CERT-CODE  (A1)
  2 DEATH-CERT-CODE  (A1)         /* RE1
  2 FIRM-ORGANIZATION-NAME        /* RE2
  2 LAST-DATE-UPD    (N8)
  2 LAST-USER-UPD    (A8)
*
1 CONT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
*
1 STATUS VIEW OF A-STATUS
  2 SPOUSE-CONTACT-ID
  2 FORMER-SPOUSE-CONTACT-ID
  2 DATE-OF-DIVORCE
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 DATE-OF-MARRIAGE
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
*
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
*
1 #MMDDYYYY   (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-C     (A2)
  2 #DD-C     (A2)
  2 #YYYY-C   (A4)
  2 REDEFINE #YYYY-C
    3 #YYYY-N (N4)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 #YYYYMMDD-2  (N8)
1 REDEFINE #YYYYMMDD-2
  2 #YYYYMMDD-2-A (A8)
1 #CID-TEXT    (A8)
1 #EXISTING-TEXT(A5)
1 #SELECTION   (A1)
1 #AGE (N8)
1 REDEFINE #AGE
  2 #FILL (A1)
  2 #AGE-2 (N3)
1 #MARK-SSN  (A1)
1 #CONT-ID-NUMBER (N6)
1 #CONT-SSN (A9)
1 #CONT-AGE (N3)
1 #CONT-DOB (A8)
1 #CONT-DOD (A8)
1 #CONT-NAME (A25)
1 #CONT-NAME-CV (C)
1 #CONT-CID (N8)
1 #CONT-ID-NUMBER-CV (C)
1 #CONTACT-ID-CV (C)
1 #CONT-CID-CV (C)
1 #CONT-DOB-CV (C)
1 #SELECTION-CV (C)
1 #SSN/TIN (A7)
END-DEFINE
*
SET KEY PF3
*
MOVE 'CONT-ID' TO #CID-TEXT
*
FIND CONT-V WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORD FOUND
    MOVE TRUE TO #NOT-FOUND
    ESCAPE ROUTINE
  END-NOREC
  MOVE (AD=I) TO #CONT-CID-CV
  MOVE ID-NUMBER TO #CONT-ID-NUMBER
  IF SSN > 0
    MOVE SSN TO #CONT-SSN
  END-IF
  RESET #AGE
  IF DATE-OF-DEATH = 0
    IF DATE-OF-BIRTH NE 0
      COMPUTE #AGE = *DATN - DATE-OF-BIRTH
    END-IF
  ELSE
    IF DATE-OF-BIRTH NE 0
      COMPUTE #AGE = DATE-OF-DEATH - DATE-OF-BIRTH
    END-IF
  END-IF
  MOVE #AGE-2 TO #CONT-AGE
  IF DATE-OF-BIRTH NE 0
    MOVE DATE-OF-BIRTH TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOB LEAVING NO
  END-IF
  IF DATE-OF-DEATH NE 0
    MOVE DATE-OF-DEATH TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOD LEAVING NO
  END-IF
  IF CONTACT-TYPE = 'O'                                                             /* RE2
    MOVE 'TIN/EIN' TO #SSN/TIN                                                      /* RE2
    #CONT-NAME := FIRM-ORGANIZATION-NAME                                            /* RE2
  ELSE                                                                              /* RE2
    MOVE '  SSN' TO #SSN/TIN                                                        /* RE2
    #CONT-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)  
  END-IF                                                                            /* RE2
  MOVE CONTACT-ID TO #CONT-CID
END-FIND
PERFORM INPUT-MAP
***************************
DEFINE SUBROUTINE INPUT-MAP
***************************
MOVE 'CI' TO #EXISTING-TEXT
MOVE (AD=U) TO #SELECTION-CV
INPUT USING MAP 'ADRM0007'
IF *PF-KEY = 'PF3'
  MOVE TRUE TO #PF3
  ESCAPE ROUTINE
END-IF
IF #SELECTION NE 'X'
  REINPUT 'Please mark with an "X".'
    MARK *#SELECTION
END-IF
FIND CONT-V WITH CONTACT-ID = #CONT-CID
  MOVE CONTACT-ID TO #CONTACT-ID
  MOVE SSN TO #SSN
  MOVE FIRST-NAME TO #FIRST-NAME
  MOVE MIDDLE-NAME TO #MIDDLE-NAME
  MOVE LAST-NAME TO #LAST-NAME
  MOVE SUFFIX TO #SUFFIX
  MOVE GENDER-CD TO #SEX
  MOVE SSN TO #SSN
  MOVE #CONT-DOB TO #DOB
  MOVE #CONT-DOD TO #DOD
  MOVE BIRTH-CERT-CODE TO #BCC
  MOVE DEATH-CERT-CODE TO #DCC       /* RE1
  MOVE ID-NUMBER TO #CN
  IF LAST-DATE-UPD NE 0
    MOVE LAST-DATE-UPD TO #YYYYMMDD
    COMPRESS #MM-A '-' #DD-A '-' #YYYY-A INTO #LST-UPD LEAVING NO
  END-IF
  FIND (1) TERM WITH CLERK-ID = LAST-USER-UPD
    MOVE COMMENTS TO #LST-USR
  END-FIND
END-FIND
RESET  #DOM #DIV
FIND STATUS WITH ID-NUMBER = #CGA-ORIG-ID
  IF SPOUSE-CONTACT-ID = #CONT-CID
    IF DATE-OF-MARRIAGE > 0
      MOVE DATE-OF-MARRIAGE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
    END-IF
    IF #DOD = ' ' AND DATE-OF-DIVORCE > 0
      MOVE DATE-OF-DIVORCE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
    END-IF
  END-IF
  IF FORMER-SPOUSE-CONTACT-ID = #CONT-CID
    IF FORMER-SPOUSE-DATE-OF-MARRIAGE > 0
      MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
    END-IF
    IF #DOD = ' ' AND FORMER-SPOUSE-DATE-OF-DIVORCE > 0
      MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
    END-IF
  END-IF
END-FIND
*
END-SUBROUTINE
*
END
