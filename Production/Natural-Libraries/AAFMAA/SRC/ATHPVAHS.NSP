* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVAHS - display va history for auditors
* TMT 06/11/2014  CONTACT NORMALIZATION - DATE OF BIRTH
*
DEFINE DATA
LOCAL
*
1 VA-V   VIEW OF A-VA-HISTORY
  2 RECORD-KEY              (N15)
  2 REDEFINE RECORD-KEY
    3 ID-NUMBER-A             (A6)
    3 REDEFINE ID-NUMBER-A
      4 ID-NUMBER-N            (N6)
    3 ISS-DATE                (N8)
    3 SPLIT-IND               (N1)
  2 ISSUE-PLAN
  2 TERMINATION-CODE         (A3)
  2 TERMINATION-DATE         (N7)
* 2 ACCUM-CASH-VALUE-M     (13)
  2 DEATH-BENEFIT-M        (13)
  2 CASH-VALUE-M           (13)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID                              /* TT1
  2 NAME
  2 MILITARY-STATUS
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 DOD-YYYY (N4)
    3 DOD-MMDD (N4)
*  2 DATE-OF-BIRTH                                /* TT1 BEGIN
*  2 REDEFINE DATE-OF-BIRTH
*   3 DOB-YYYY (N4)
*   3 DOB-MMDD (N4)                               /* TT1 END
*
1 FR-V VIEW OF A-FIRS
  2 CAUSE-OF-DEATH
  2 POL-NAME       (45)
  2 POL-AMT-PAID   (45)
  2 POL-DATE       (45)
  2 POL-AMOUNT     (45)
*
1 #HOLD-ID-NUMBER (N6)
1 #HOLD-ID-NUMBER-CSV (N6)
*
1 #COUNTERS
  2 #RECS-READ        (P5)
  2 #RECS-SELECTED    (P5)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I2               (I2)
*
1 #VA-END-RECORD-KEY     (N15)
1 REDEFINE #VA-END-RECORD-KEY
  2 #VA-EN-ID-NUMBER     (N6)
  2 #VA-EN-ISS-DATE      (N8)
  2 #VA-EN-SPLIT-IND     (N1)
*
1 #VA-START-RECORD-KEY     (N15)
1 REDEFINE #VA-START-RECORD-KEY
  2 #VA-ST-ID-NUMBER     (N6)
  2 #VA-ST-ISS-DATE      (N8)
  2 #VA-ST-SPLIT-IND     (N1)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT          (A2)
  2 #YY            (A2)
  2 #MM            (A2)
  2 #DD            (A2)
*
1 #START-KEY    (N15) INIT <001000000000000>
1 #NUM-POLS     (N3)
1 #NUM-POLS-CSV (N3)
1 #DB           (N7.2)
1 #TOT-DB       (N7.2)
1 #FACE         (N7.2)
1 #TOT-FACE     (N7.2)
1 #CSV          (N7.2)
1 #TOT-CSV      (N7.2)
1 #TOT-CSV-CSV  (N7.2)
1 #CNT-2        (N5)
1 #CNT-3        (N5)
1 #AGE          (N3)
*
1 DATE-OF-BIRTH              (N8)
1 REDEFINE DATE-OF-BIRTH
  2 DOB-YYYY (N4)
  2 DOB-MMDD (N4)
END-DEFINE
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'V/A list of deceased mbrs in 1997'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'V/A list of Lapse"d  or CSV"d in 1997'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
* MOVE 092198000000000 TO #START-KEY
MOVE 001000000000000 TO #START-KEY
READ VA-V BY RECORD-KEY FROM #START-KEY
* DISPLAY VA-V.ID-NUMBER-N TERMINATION-CODE TERMINATION-DATE
  ADD 1 TO #RECS-READ
*
  IF   (TERMINATION-CODE = 'DTH')
      AND (TERMINATION-DATE GE 199701)
*
    IF (ID-NUMBER-N NE #HOLD-ID-NUMBER) AND (#HOLD-ID-NUMBER NE 0)
      IF #NUM-POLS GT 1
        WRITE (2) '   Totals    ' #TOT-DB #TOT-CSV #TOT-FACE
      END-IF
*
      RESET #NUM-POLS #TOT-DB #TOT-FACE #TOT-CSV
    END-IF
    MOVE ID-NUMBER-N TO #HOLD-ID-NUMBER
*
    ADD 1 TO #NUM-POLS
    MOVE CASH-VALUE-M (13) TO #CSV
    RESET #DB
    FIND (1) FR-V WITH MEMBER-KEY = ID-NUMBER-A
      FOR #I1 1 TO 45
        IF   (POL-NAME (#I1) = 'AAFMAA' OR = 'AMAA')
            AND (POL-DATE (#I1) = VA-V.ISS-DATE)
          MOVE POL-AMT-PAID (#I1) TO #DB
          MOVE POL-AMOUNT   (#I1) TO #FACE
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
*
    FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
      RESET DATE-OF-BIRTH
      DATE-OF-BIRTH   := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)     /* TT1
    END-FIND
*
    COMPUTE #AGE = DOD-YYYY - DOB-YYYY
    ADD #DB   TO #TOT-DB
    ADD #FACE TO #TOT-FACE
    ADD #CSV  TO #TOT-CSV
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 'CN' VA-V.ID-NUMBER-A #DB #CSV #FACE NAME
      DATE-OF-DEATH #AGE VA-V.ISS-DATE  'PLAN' ISSUE-PLAN
*
    ADD 1 TO #RECS-SELECTED
  END-IF
*
  IF   (TERMINATION-CODE = 'LAP' OR = 'CSV')
      AND (TERMINATION-DATE GE 199701)
*
    IF (ID-NUMBER-N NE #HOLD-ID-NUMBER-CSV)
        AND (#HOLD-ID-NUMBER-CSV NE 0)
      IF #NUM-POLS-CSV GT 1
        WRITE (3) '   Totals    ' #TOT-CSV-CSV
      END-IF
*
      RESET #NUM-POLS-CSV #TOT-CSV-CSV
    END-IF
    MOVE ID-NUMBER-N TO #HOLD-ID-NUMBER-CSV
*
    ADD 1 TO #NUM-POLS-CSV
    MOVE CASH-VALUE-M (13) TO #CSV
*
    FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
    END-FIND
*
    ADD #CSV  TO #TOT-CSV-CSV
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-3 'CN' VA-V.ID-NUMBER-A #CSV NAME
      TERMINATION-CODE TERMINATION-DATE VA-V.ISS-DATE  'PLAN' ISSUE-PLAN
*
    ADD 1 TO #RECS-SELECTED
  END-IF
END-READ
*
IF (ID-NUMBER-N NE #HOLD-ID-NUMBER) AND (#HOLD-ID-NUMBER NE 0)
  IF #NUM-POLS GT 1
    WRITE (2) '   Totals    ' #TOT-DB #TOT-CSV #TOT-FACE
  END-IF
*
  RESET #NUM-POLS #TOT-DB #TOT-FACE #TOT-CSV
END-IF
*
IF (ID-NUMBER-N NE #HOLD-ID-NUMBER-CSV)
    AND (#HOLD-ID-NUMBER-CSV NE 0)
  IF #NUM-POLS-CSV GT 1
    WRITE (3) '   Totals    ' #TOT-CSV-CSV
  END-IF
END-IF
*
WRITE  'TOT-RECS-READ     = ' #RECS-READ
WRITE  'TOT-RECS-SELECTED = ' #RECS-SELECTED
*
END
