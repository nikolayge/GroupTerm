* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPATAP - List approved applications from previous month
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM  06252004  None    Program Written                             *
* SAG  12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
************************************************************************
*
DEFINE DATA
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER            (N6)
  2 USER-SOURCE          (A3)
  2 NAME                 (A25)
  2 HOME-PHONE           (N10)
  2 REDEFINE HOME-PHONE
    3 PHONE-AREA-CODE     (A3)
    3 PHONE-1-3-DIGITS    (A3)
    3 PHONE-LAST-4-DIGITS (A4)
  2 INTRO-FLAG           (A1)
  2 APPL-SOURCE          (A7)
  2 AT-STATUS            (A2)
  2 ST-NOTE-DATE         (N8/50)
  2 REDEFINE ST-NOTE-DATE
    3 NOTE-DATE-OCC       (50)
      4 NOTE-DATE-YYYY     (A4)
      4 NOTE-DATE-MM       (A2)
      4 NOTE-DATE-DD       (A2)
  2 ST-NOTE-STATUS       (A2/50)
*
1 #I1                   (I2)
1 #CNT-1                (P5)
1 #DISPLAY-PHONE        (A12)
1 #DISPLAY-DATE         (A10)
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-RECORD       (A125)
*
1 #JCL-PARM             (A32)
1 REDEFINE #JCL-PARM
  2 #JCL-MM              (N2)   /* DATE PARM 03/01/1999
  2 #JCL-X1              (A1)
  2 #JCL-DD              (N2)
  2 #JCL-X2              (A1)
  2 #JCL-YYYY            (N4)
*
1 #START-DATE           (N8)
1 REDEFINE #START-DATE
  2 #START-DATE-YYYY     (N4)
  2 #START-DATE-MM       (N2)
  2 #START-DATE-DD       (N2)
1 #END-DATE             (N8)
1 #YYYYMMDD             (N8)
1 #ACTION-FLAG          (A1) INIT <'B'>
*
END-DEFINE
*
FORMAT LS=133 PS=66
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  8X  'Approved Applications within Date Range'
  5X  'DATE:' *DAT4U 2X 'TIME:' *TIMX
  2X  'PAGE:' *PAGE-NUMBER (1)
*
COMPRESS 'Name' #TAB
  'Phone' #TAB
  'Type' #TAB
  'User' #TAB
  'App Source' #TAB
  'Date' #TAB
  'Status' #TAB
  'ATNumber' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ WORK 2 ONCE RECORD #JCL-PARM
PERFORM FORMAT-DATES
*
READ AT-V BY USER-SOURCE
*
  FOR #I1 50 TO 1 STEP -1
    IF ST-NOTE-DATE (#I1) = 0
      ESCAPE TOP
    END-IF
*
    IF NOT ST-NOTE-STATUS (#I1) = 'A'
      ESCAPE BOTTOM
    END-IF
*
    IF ST-NOTE-DATE (#I1) LT #START-DATE
        OR ST-NOTE-DATE (#I1) GT #END-DATE
      ESCAPE BOTTOM
    END-IF
*
    RESET #DISPLAY-PHONE #DISPLAY-DATE
    COMPRESS NOTE-DATE-MM (#I1) NOTE-DATE-DD (#I1) NOTE-DATE-YYYY (#I1)
      INTO #DISPLAY-DATE WITH '/'
    COMPRESS PHONE-AREA-CODE PHONE-1-3-DIGITS PHONE-LAST-4-DIGITS
      INTO #DISPLAY-PHONE WITH '-'
    ADD +1 TO #CNT-1
    DISPLAY 'Cnt' #CNT-1
      'Name' NAME
      'Phone' #DISPLAY-PHONE
      'Type' INTRO-FLAG
      'User' USER-SOURCE
      'App Source' APPL-SOURCE
      'Date' #DISPLAY-DATE
      'St' ST-NOTE-STATUS(#I1)
      'ATNum' ID-NUMBER
*
    COMPRESS NAME #TAB
      #DISPLAY-PHONE #TAB
      INTRO-FLAG #TAB
      USER-SOURCE #TAB
      APPL-SOURCE #TAB
      #DISPLAY-DATE #TAB
      ST-NOTE-STATUS(#I1) #TAB
      ID-NUMBER INTO #EXTRACT-RECORD LEAVING NO
    WRITE WORK 1 #EXTRACT-RECORD
    RESET #EXTRACT-RECORD
    ESCAPE BOTTOM
  END-FOR
END-READ
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-DATES
* * * * * * * * * * * * * * * *
*
MOVE #JCL-YYYY TO #START-DATE-YYYY
MOVE #JCL-MM   TO #START-DATE-MM
MOVE #JCL-DD   TO #START-DATE-DD
*
SUBTRACT 1 FROM #START-DATE-MM
IF #START-DATE-MM = 0
  MOVE 12 TO #START-DATE-MM
  SUBTRACT 1 FROM #START-DATE-YYYY
END-IF
*
CALLNAT 'GDATESXX'
  #END-DATE
  #START-DATE
  #YYYYMMDD
  #ACTION-FLAG
*
END-SUBROUTINE
*
END
