* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GOWNRSXY     get OWNERSHIP info for a member
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  09082005 None Cloned from GOWNRSXX for trade-up - does not take *
*                    inactive policies into account                    *
* NGG  20121002 NG1  CN Expansion Project -                            *
*                    New beneficiary database file                     *
* YAK  10172012 YK1  Use function to identify Level Term II plans      * 
* TMT  10072013 TT1  ADDRESS NORMALIZATION
************************************************************************
*
DEFINE DATA
PARAMETER
*
1 #CN      (N6)
1 REDEFINE #CN
 2 #CN-A   (A6)
1 #NON-INS-OWNER    (L) /* has at least 1 non insured owner
1 #MULTIPLE-OWNER   (L) /* more than 1 owner for this member
1 #OWNER-OCC-9 (9)
 2 #OWNER-FLAG (A1)     /* " "=owner is insured "N"=owner is not insured
 2 #OWNER-CN   (N6/10)  /* up to 10 owners for each pol
*
 2 #OWNER-CN-BY-POL (N6)    /* 1st owners of each set of policies
 2 #OWNER-CN-SEG    (I2/9)  /* all segment of pols owned by above NMO
 2 #ISSUE-DATE      (N8/9)  /* all pols owned by above non-mbr-owner
 2 #ISSUE-DATE-DISP (A10/9)
*    beneficiary info output
 2 #BN-TYPE       (A1/2)
 2 #BN-TEXT       (A60/10)
*
LOCAL
1 #HAS-INS-OWNER      (L)
1 #HAS-NON-INS-OWNER  (L)
1 #LAST-OWNER         (A6)
1 #FOUND-ST   (L)
1 #FOUND-NT   (L)
1 #OWNER-AREA  (A35)
1 REDEFINE #OWNER-AREA
 2 #OWNER-OCC-5 (5)
  3 #OWNER-X      (N6)
  3 REDEFINE #OWNER-X
   4 #OWNER-XA    (A6)
  3 #OWNER-FILL   (A1)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A   (A4)
 2 #MM-A     (A2)
 2 #DD-A     (A2)
*
1 ST-V VIEW OF A-STATUS         /*TT1 REMOVED ALL ADDRESSS AND ZIP CODE INFORMATION
 2 ID-NUMBER
 2 WIDOW-FLAG
 2 PROCESS-IND
 2 LINKED-CN   (1)
 2 REDEFINE LINKED-CN
  3 LINKED-CN-A (A6/1)
 2 INTRO-FLAG
 2 STATUS             (9)
 2 SPLIT-IND          (9)
 2 PLAN               (9)
 2 REDEFINE PLAN
  3 PLAN-OCC          (9)
   4 PLAN-1A          (A1)
   4 PLAN-2A          (A2)
 2 ISSUE-DATE         (9)
 2 REDEFINE ISSUE-DATE
  3 ISSUE-DATE-OCC   (9)
   4 ISSUE-DATE-CENT  (N2)
   4 ISSUE-DATE-YYMMDD (N6)
 2 FIRS-LOCATION-CODE (9)
*
* NT-V VIEW OF A-NOTES    /* note file used for pending beneficiaries
1 BENE-F-V VIEW OF A-BENE-FINAL   /* NG1
 2 ID-NUMBER          (N6)    /* let D = bene designation (1-4)
 2 NOTE-DATE          (N8/60) /* LET P/A = PRINCIPAL/ALTERNATE DESING
 2 REDEFINE NOTE-DATE         /* let L = linked policies
  3 BENE-DATES-6-POLS (6)
   4 BENE-ISS-DT       (N6/7)   /* (D, L)
   4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
   4 BENE-LST-UPD      (N8)     /* (D)
   4 BENE-STRT-PEND    (N8)     /* (D)
   4 BENE-FOL-UP-IND   (N1)     /* (D)
   4 BENE-OWN-TRNSF-DT (N8)     /* (D)
   4 BENE-SUC-OWN      (N1)     /* (d)
   4 BENE-NEW-LAYOUT   (N1)     /* (d)
   4 BENE-FIL1         (A4)
 2 NOTE-CLERK-ID       (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-6-POLS  (6)
   4 BENE-STATUS      (A1)     /* (D)
   4 BENE-RESTRICT    (A1)     /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)    /* (D, P/A)
    5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT          (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-6-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC    (5)
     6 BENE-TEXT        (A60)   /* (D, P/A, text-line)
     6 REDEFINE BENE-TEXT
      7 BENE-1ST-25    (A25)
      7 BENE-OWNER-AREA   (A35)
      7 REDEFINE BENE-OWNER-AREA
       8 BENE-OWNER-CN-A  (A6)    /* 1st owner
*
1 #NT-ID       (N6)
1 #1ST-POL     (I2)
1 #ID-NUMBER   (N9)
1 REDEFINE #ID-NUMBER
 2 #ID-NUMBER-3  (A3)
 2 #ID-NUMBER-A  (A6)
*
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 #I4     (I2)
1 #I5     (I2)
1 #I6     (I2)
1 #INVALID   (L)
*
1 #RET         (A5)
1 #SUFFIX      (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
*
END-DEFINE
*
* * * * * * * * * * *
* WRITE 'TOP OF GOWNRSAD DOING #' #CN
RESET #OWNER-FLAG (*)
   #OWNER-CN   (*, *)
   #NON-INS-OWNER
   #MULTIPLE-OWNER
   #OWNER-CN-BY-POL (*)
   #OWNER-CN-SEG    (*, *)
   #ISSUE-DATE      (*, *)
   #ISSUE-DATE-DISP (*, *)
   #BN-TYPE (*, *) #BN-TEXT (*, *)
*
MOVE TRUE TO #FOUND-ST
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORD FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #CN-A
      IF NO RECORD FOUND
        WRITE 'REC NOT FOUND '  #CN-A
        RESET #FOUND-ST
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
IF NOT #FOUND-ST
  ESCAPE ROUTINE
END-IF
*
#NT-ID := #CN                     /* NG1
MOVE TRUE TO #FOUND-NT
FIND BENE-F-V WITH ID-NUMBER = #NT-ID
  IF NO RECORD FOUND
    RESET #FOUND-NT
  END-NOREC
END-FIND
*
* if NOT #FOUND-NT
*   ESCAPE ROUTINE
* END-IF
*
FOR #I1 1 TO 9
  IF NOT STATUS (#I1) = 'D'
    ESCAPE TOP
  END-IF
*
* IF  (PLAN (#I1) = 'RT' OR = 'LT' OR = 'SO')                /* YK1
*  OR ((PLAN-1A (#I1) GE 'U') AND (PLAN-1A (#I1) LE 'Z'))    /* YK1
  IF (FUNCTION-TERM-POLICY(<PLAN(#I1)>) OR PLAN(#I1) = 'SO') /* YK1 
    ESCAPE TOP
  END-IF
*
LB1. FOR #I2 1 TO 6
    FOR #I3 1 TO 7
      IF   (ST-V.ISSUE-DATE-YYMMDD (#I1) NE 0)
       AND (ST-V.ISSUE-DATE-YYMMDD (#I1) = BENE-ISS-DT    (#I2, #I3))
       AND (ST-V.SPLIT-IND         (#I1) = BENE-SPLIT-IND (#I2, #I3))
*
        IF BENE-NEW-LAYOUT (#I2) = 1  /* bene is formated
          MOVE BENE-F-V.BENE-TYPE (#I2, 1) TO #BN-TYPE (#I1, 1)
          MOVE BENE-F-V.BENE-TYPE (#I2, 2) TO #BN-TYPE (#I1, 2)
          RESET #I6
          FOR #I4 1 TO 2
            IF (#I4 = 2) AND (BENE-F-V.BENE-TYPE (#I2, 2) NE 'C')
              ESCAPE BOTTOM
            END-IF
*
            FOR #I5 1 TO 5
              ADD 1 TO #I6
              MOVE BENE-F-V.BENE-TEXT(#I2, #I4, #I5) TO #BN-TEXT(#I1, #I6)
            END-FOR
          END-FOR
        END-IF
*
        MOVE BENE-F-V.BENE-OWNER-AREA (#I2, 2, 5) TO #OWNER-AREA
        IF  #OWNER-AREA = ' '
          MOVE TRUE TO #HAS-INS-OWNER
* WRITE '#HAS-INS-OWNER'
          ESCAPE BOTTOM
        END-IF
*
        IF   (#OWNER-XA (1) NE #LAST-OWNER)
         AND (#LAST-OWNER NE ' ')
          MOVE TRUE TO #MULTIPLE-OWNER
        END-IF
*
        MOVE #OWNER-XA (1) TO #LAST-OWNER
*
        FOR #I4 1 TO 5
          IF #OWNER-XA (#I4) = ' '
            ESCAPE BOTTOM
          END-IF
*
          IF #OWNER-XA (#I4) = MASK (NNNNNN)
            MOVE #OWNER-X (#I4) TO #OWNER-CN (#I1, #I4)
            MOVE 'N' TO #OWNER-FLAG (#I1)
            MOVE TRUE TO #NON-INS-OWNER
            MOVE TRUE TO #HAS-NON-INS-OWNER
            IF #I4 LT 5
              IF #OWNER-XA (#I4+1) = MASK (NNNNNN)
                MOVE TRUE TO #MULTIPLE-OWNER
* WRITE 'CN ' #CN-A ' has multiple owners for one policy'
              END-IF
            END-IF
* WRITE '#HAS-NON-INS-OWNER'
          END-IF
        END-FOR
        ESCAPE BOTTOM (LB1.)
      END-IF
    END-FOR
  END-FOR
* WRITE #I1 #OWNER-FLAG (#I1) #OWNER-CN (#I1, 1:4)
END-FOR
*
IF #HAS-INS-OWNER AND #HAS-NON-INS-OWNER
  MOVE TRUE TO #MULTIPLE-OWNER
END-IF
*
FOR #I1 1 TO 9
  IF NOT ST-V.STATUS (#I1) = 'D'
    ESCAPE TOP
  END-IF
*
* IF  (PLAN (#I1) = 'RT' OR = 'LT' OR = 'SO')                 /* YK1
*  OR ((PLAN-1A (#I1) GE 'U') AND (PLAN-1A (#I1) LE 'Z'))     /* YK1
  IF (FUNCTION-TERM-POLICY(<PLAN(#I1)>) OR PLAN(#I1) = 'SO')  /* YK1 
    ESCAPE TOP
  END-IF
*
  IF #OWNER-CN (#I1, 1) = 0
    MOVE #CN TO #OWNER-CN (#I1, 1)
  END-IF
*
  FOR #I2 1 TO 9
    IF  (#OWNER-CN (#I1, 1) = #OWNER-CN-BY-POL (#I2))
     OR ((#OWNER-CN-BY-POL (#I2) = 0) AND (#OWNER-CN-SEG (#I2, 1) = 0))
      MOVE #OWNER-CN (#I1, 1) TO #OWNER-CN-BY-POL (#I2)
      FOR #I3 1 TO 9
        IF #ISSUE-DATE (#I2, #I3) = 0
          MOVE #I1 TO #OWNER-CN-SEG (#I2, #I3)
          MOVE ISSUE-DATE (#I1) TO #ISSUE-DATE (#I2, #I3)
          MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A
                 INTO #ISSUE-DATE-DISP (#I2, #I3) WITH '/'
          ESCAPE BOTTOM
        END-IF
      END-FOR
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FOR
*
* FOR #I1 1 TO 9   /* BELOW TO SEE RESULTS
*   IF #OWNER-CN-BY-POL (#I1) = 0
*     ESCAPE TOP
*   END-IF
*
*   WRITE #I1 'OWNER=' #OWNER-CN-BY-POL (#I1)
*   FOR #I2 1 TO 5
*     IF #OWNER-CN-SEG(#I1, #I2) = 0
*       ESCAPE TOP
*     END-IF
*     WRITE #I2 'POLS ATTH' #OWNER-CN-SEG(#I1,#I2) #ISSUE-DATE(#I1,#I2)
*   END-FOR
* END-FOR
* DISPLAY #CN #MULTIPLE-OWNER #NON-INS-OWNER
*
END
