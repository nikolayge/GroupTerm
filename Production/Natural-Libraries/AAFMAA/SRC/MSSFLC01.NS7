* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MSSFLC01.
/**
/** :author nguentchev
* MD Code    MD descr
* '0'   INT-EX       0
* '1'   CONV         0
* '2'   CASH-VAL    -1
*  A    AD-INS       1
*  A    AD-CLI       0
*  C    ANNU        -1
*  D    DEATH       -1
*  F    EZ-ACDB      0
*  L    LAPSE       -1
*  N    PENDING-AT   0
*  N    NU-CONTACT   0
*  N    NU-INS       1
*  N    NU-CLI       1
*  N    NU-MBR       1
*  R    REINSTAT     1
*  S    RESIGNS     -1
*  T    LAPS-NFP    -1
*  U    LAPS-OVL    -1
*  V    LAPS-CAP    -1
*  V    CAP-COLL    -1
*  W    WID-DETH    -1
*  W    SPS-DETH     0
*  X    NM-FBR       ?
*  Y    NM-FBR       0
*  Z    AI-FBR       0
* '3'   FaceChange   0
* '4'   DBChange     0
* '5'   INS-END     -1
* '6'   PolDeleted  -1
*
DEFINE FUNCTION LifeCount RETURNS (N1)
  DEFINE DATA
  PARAMETER
  1 CODE  (A1)
  1 STATUS-ISN (P8)
  1 PRODUCT_ID (A16)
  LOCAL
  1 AP-NBR       (I2)
  1 J            (I2)
  1 #PRODUCT_ID  (A16)
  1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER (N6)
    2 C*ACTIVE-SEGMENTS
    2 STATUS (9)
  1 #KEY            (A7) INIT <'CODEMD'>
  1 GT-V VIEW OF A-GEN-TABLE
    2 GEN-ALPHA-VALUE (A50)
    2 GEN-NUMBER-VALUE (N7.4)
    2 GEN-DESCRIPTION (A75)
  END-DEFINE
/*
  GET ST-V STATUS-ISN
  RESET AP-NBR
FOR J 1 TO C*ACTIVE-SEGMENTS
  IF STATUS (J) = 'D'
    CALLNAT 'GPLNMSXX' #PRODUCT_ID  ID-NUMBER J
    IF #PRODUCT_ID NE PRODUCT_ID
      ADD 1 TO AP-NBR
    END-IF
  END-IF
END-FOR
COMPRESS #KEY CODE INTO #KEY LEAVING NO
FIND GT-V WITH SEC-KEY = #KEY
END-FIND
DECIDE FOR FIRST CONDITION
  WHEN GEN-NUMBER-VALUE = 0
    RESET LifeCount
  WHEN GEN-NUMBER-VALUE < 0
    IF AP-NBR > 0
      RESET LifeCount
    ELSE
      LifeCount := -1
    END-IF
  WHEN GEN-NUMBER-VALUE > 0
    IF AP-NBR > 0    
      RESET LifeCount
    ELSE
       LifeCount := 1
    END-IF
  WHEN NONE
    IGNORE
END-DECIDE
END-FUNCTION
END
