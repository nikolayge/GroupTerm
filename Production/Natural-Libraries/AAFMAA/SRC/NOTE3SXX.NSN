* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Delete Contact Note NOTE3SXX

DEFINE DATA
PARAMETER
1 #MAIN-NAME         (A25)
1 #ISN               (P8)
1 #UPDATED           (L)
LOCAL
1 UpdateNotes view of A-CONTACT-NOTES
  2 USER-COMPANY   (A1)
  2 USER-ID        (A3)
  2 CONTACT-ID     (N8) /* D
  2 NOTE-TIME      (N7)
  2 NOTE-DATE      (N8) 2 REDEFINE NOTE-DATE
    3 NOTE-DATE-A  (A8)
  2 C*NOTE-TEXT
  2 NOTE-TEXT      (A78/10) 2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-S  (10)
      4 NOTE-LINE   (A60)
      4 NOTE-FUTURE (A18)
  2 LAST-UPD-USER
  2 LAST-UPD-DATE
  2 LAST-UPD-TIME

1 YES      (A1) CONST <'Y'>
1 #Delete-note     (A40) CONST
  <'*** NOTE HAS BEEN DELETED ***'>
1 #Confirm1 (A60) INIT
  <'Are you sure you want to permanently delete this note?'>
1 #Confirm2 (A50) INIT
  <'Please enter Y for YES or press PF3 to cancel'>

1 #Yes             (A1)  INIT <'N'>
1 #CLERK           (A20)
1 #NOTE-DATE       (A10)
1 #D               (D)
1 #J               (I4)
1 #L               (I4)
1 #NOTE-DATE-A   (A8) 1 REDEFINE #NOTE-DATE-A
  2 #NOTE-DATE-N (N8)
1 #NOTE-TIME-A   (A7) 1 REDEFINE #NOTE-TIME-A
  2 #NOTE-TIME-N (N7)


1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS

end-define

DEFINE WINDOW Delete-Note
  SIZE 20 * 64
  BASE 1/10
  FRAMED ON POSITION SYMBOL AUTO

DEFINE WINDOW Delete-Info
  SIZE 4 * 64
  BASE 1/10
  FRAMED ON POSITION SYMBOL AUTO

SET KEY PF3 NAMED 'Exit'
RESET #UPDATED

IF #ISN = 0 ESCAPE ROUTINE END-IF
DEL-LBL.
GET UpdateNotes #ISN
IF *ISN (DEL-LBL.) = 0
  ESCAPE ROUTINE
END-IF

MOVE EDITED NOTE-DATE-A TO #D (EM=YYYYMMDD)
MOVE EDITED #D (EM=MM-DD-YYYY) TO #NOTE-DATE
FIND (1) TERM WITH CLERK-ID = UpdateNotes.USER-ID
  MOVE COMMENTS TO #CLERK
END-FIND

INPUT WINDOW='Delete-Note' USING MAP 'NOTE3MXX'

IF #Yes NE YES
  ESCAPE ROUTINE
END-IF

DELETE (DEL-LBL.)

INPUT WINDOW='Delete-Info' (IP=OFF) #Delete-note (AD=I)
ASSIGN #UPDATED = TRUE

END
