* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1603SCO - Store screen info & convert CO MR-TYPE
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM  12162004  PM1     Adjusted premiumn display to 5 digits.        *
* DRW  12272004  DW2  Increase variablw loan to handle 999,999.99.     *
* DRW  06262006  DW3  Use Dues Pmt On Letter                           *
* TMT  11082013  TT1  1 MILLION DOLLAY POLICY STABILITY PROJECT        *
************************************************************************
************************************************************************
*
DEFINE DATA
PARAMETER
1 #FIND-CO-TYPE   (L)
1 CO-RQSTS (A3/10)
1 REDEFINE CO-RQSTS
 2 CO-RQSTS-OCC    (10)
  3 CO-CERT-SEQ   (N1)
  3 CO-CONV-TYPE  (A2)
  3 REDEFINE CO-CONV-TYPE
   4 CO-CONV-TYPE-1   (A1)
   4 CO-CONV-TYPE-2   (A1)
1 #CGA-SCR-SUFFIX   (A3)
1 #SCR-SUFFIX      (A3)
1 #SCR-TYPES    (16)   /* up to 16 types on conversions
 2 #SCR-SELECT-CV         (C)
 2 #SCR-SELECT            (A1)
 2 #SCR-TO-PLAN           (A6)
 2 #SCR-NU-DB-DISP        (A12)    /* INCREASED FROM 10
* 2 #SCR-NU-PREM-DISP      (A7)
 2 #SCR-NU-PREM-DISP      (A8) /* PM1
 2 #SCR-AMT-LUMP-SUM-DISP (A11)
 2 #SCR-TAXABLE-INC-DISP  (A10)
 2 #SCR-REMARKS           (A24)       /* TT1 REDUCED FROM 30
*
1 #CT-TBL-POLS       (9) /*9 possible certs (axx must match below)
 2 #CT-POL-NUM        (A14)
 2 #CT-ISS-DT         (N8)
 2 #CT-CONV-DUR       (N3)
 2 #CT-ISSUE-DATE     (A10)
 2 #CT-CURR-PLAN      (A6)
 2 #CT-FREQ           (A11)
 2 #CT-CURR-FACE      (N7.2)
 2 #CT-CURR-CSV       (N7.2)
 2 #CT-CURR-DB        (N9.2)
 2 #CT-PRORAT-LN-INT  (N6.2)    /* DW2
*
 2 #CONV-TBL-TYPES (16)   /* 16 possible conversion types
  3 #CT-SELECT            (A1)
  3 #CT-TO-PLAN           (A6)
  3 REDEFINE #CT-TO-PLAN
   4 #CT-TO-PLAN-$        (A1)
   4 #CT-TO-PLAN-X        (N1)
   4 #CT-TO-PLAN-4        (A4)
  3 #CT-TERM-FACE         (N7.2)
  3 #CT-NU-PREM           (N5.2)
  3 #CT-NU-DB             (N9.2)
  3 #CT-AMT-LUMP-SUM      (N9.2)
  3 #CT-PREMS-PAID        (N9.2)
  3 #CT-TAXABLE-INC       (N9.2)
  3 #CT-TAMRA-IND         (A1)
  3 #CT-RPU-LOAN-IND      (A1)
  3 #CT-BYPASS            (L)
  3 #CT-INS-REQ           (L)
  3 #CT-DUES-PMT          (N3.2)   /* Dw3
*
1 SCR-CT-REMARKS-TBL   (9)
 2 #SCR-CT-REMARKS   (A24/16)         /* TT1 DECREASED FROM 30 TO 24
*
1 #CO-MR-TYPE      (A2)
1 REDEFINE #CO-MR-TYPE
 2 #CO-MR-TYPE-1  (A1)
 2 #CO-MR-TYPE-2  (A1)
*
1 #P-X   (I2)
1 #T-X   (I2)
*
LOCAL
1 #I1    (I2)
*
END-DEFINE
*
IF #FIND-CO-TYPE
  PERFORM FIND-CO-MR-TYPE
  ESCAPE ROUTINE
END-IF
*
RESET #SCR-SELECT-CV (#T-X)
*
IF #CGA-SCR-SUFFIX = 'DW*' OR (*DEVICE = 'BATCH')
  PERFORM FIND-CO-MR-TYPE
*
  FOR #I1 1 TO 10
    IF   (CO-CERT-SEQ (#I1) = #P-X)   /* check cert seq #
     AND ((CO-CONV-TYPE (#I1) = #CO-MR-TYPE) /* CHK CONV TYPE
      OR (CO-CONV-TYPE-1 (#I1) = 'E') AND (#CO-MR-TYPE-1 = 'C')
                AND (CO-CONV-TYPE-2 (#I1) = #CO-MR-TYPE-2)
      OR (CO-CONV-TYPE-1 (#I1) = 'S') AND (#CO-MR-TYPE-1 = 'R')
                AND (CO-CONV-TYPE-2 (#I1) = #CO-MR-TYPE-2))
      IF  (CO-CONV-TYPE (#I1) = MASK ('C'))  /* CSV OR CSV-1035
       OR (CO-CONV-TYPE (#I1) = MASK ('E'))  /* OR 1035-EXCHANGE
        IF CO-CONV-TYPE (#I1) = MASK ('C')  /* CSV
          MOVE '1' TO #SCR-SELECT (#T-X)
        ELSE
          MOVE '2' TO #SCR-SELECT (#T-X)
        END-IF
      ELSE
        IF ((CO-CONV-TYPE (#I1) = MASK ('R'))  /* RSGN SHORT
         AND (NOT CO-CONV-TYPE (#I1) = 'RP' OR = 'RA'))
         OR (CO-CONV-TYPE (#I1) = MASK ('S'))  /* OR rsgn long
          IF CO-CONV-TYPE (#I1) = MASK ('R')  /* rsgn short
            MOVE '1' TO #SCR-SELECT (#T-X)
          ELSE
            MOVE '2' TO #SCR-SELECT (#T-X)
          END-IF
        ELSE
          MOVE 'X' TO #SCR-SELECT (#T-X)
        END-IF
      END-IF
    END-IF
  END-FOR
END-IF
*
IF   (#CT-AMT-LUMP-SUM (#P-X, #T-X) GE -2)
 AND (#CT-AMT-LUMP-SUM (#P-X, #T-X) LE +2)
  RESET #CT-AMT-LUMP-SUM (#P-X, #T-X)
END-IF
*
MOVE #CT-TO-PLAN (#P-X, #T-X) TO #SCR-TO-PLAN (#T-X)
MOVE EDITED #CT-NU-PREM (#P-X, #T-X) (EM=ZZZZZ.99) /* PM1
                    TO #SCR-NU-PREM-DISP (#T-X)
MOVE EDITED #CT-AMT-LUMP-SUM (#P-X, #T-X) (EM=-ZZZ,ZZZ.99)
                    TO #SCR-AMT-LUMP-SUM-DISP (#T-X)
MOVE EDITED #CT-NU-DB (#P-X, #T-X) (EM=Z,ZZZ,ZZZ.99)
                    TO #SCR-NU-DB-DISP (#T-X)
*
IF #CT-TAXABLE-INC (#P-X, #T-X) GE 10.00
  MOVE EDITED #CT-TAXABLE-INC (#P-X, #T-X) (EM=ZZZ,ZZZ.99)
                      TO #SCR-TAXABLE-INC-DISP (#T-X)
END-IF
*
MOVE #SCR-CT-REMARKS (#P-X, #T-X) TO #SCR-REMARKS (#T-X)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-CO-MR-TYPE
* * * * * * * * * * * * * * * * *
*
DECIDE FOR FIRST CONDITION
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'PDUP'   MOVE 'PU' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '7-Pay'  MOVE '7P' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '20-Pay' MOVE '20' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '30-Pay' MOVE '30' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'OL' MOVE 'OL' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'RPU'
   IF #CT-RPU-LOAN-IND (#P-X, #T-X) = 'A' /* use all of loan
      MOVE 'RA' TO #CO-MR-TYPE
    ELSE
      MOVE 'RP' TO #CO-MR-TYPE
    END-IF
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'RSGN'  MOVE 'RS' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 10K' MOVE 'R1' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 15K' MOVE 'R2' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 20K' MOVE 'R3' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 25K' MOVE 'R4' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 30K' MOVE 'R5' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 35K' MOVE 'R6' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 40K' MOVE 'R7' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 45K' MOVE 'R8' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 50K' MOVE 'R9' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 60K' MOVE 'RB' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 70K' MOVE 'RC' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 75K' MOVE 'RD' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 80K' MOVE 'RE' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$ 90K' MOVE 'RF' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$100K' MOVE 'RG' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$110K' MOVE 'RH' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$120K' MOVE 'RI' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$125K' MOVE 'RJ' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$130K' MOVE 'RK' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$140K' MOVE 'RL' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$150K' MOVE 'RM' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$160K' MOVE 'RN' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$170K' MOVE 'RO' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$175K' MOVE 'RQ' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$180K' MOVE 'RR' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$190K' MOVE 'RT' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'R$200K' MOVE 'RU' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = 'CSV'   MOVE 'CV' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$ 10K' MOVE 'C1' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$ 25K' MOVE 'C2' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$ 50K' MOVE 'C3' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$ 75K' MOVE 'C4' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$100K' MOVE 'C5' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$200K' MOVE 'C6' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$300K' MOVE 'C7' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$400K' MOVE 'C8' TO #CO-MR-TYPE
  WHEN #CT-TO-PLAN (#P-X, #T-X) = '$500K' MOVE 'C9' TO #CO-MR-TYPE
  WHEN NONE WRITE 'Invalid CO type - call EDS - TYPE is '
                   #CT-TO-PLAN (#P-X, #T-X)
END-DECIDE
*
END-SUBROUTINE
*
END
