* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* CLASS  NATURAL <XML TOOLKIT>
* PMBP0001 - Portamedic Request Processing Driver
* DESCRIPTION
************************************************************************
*
DEFINE DATA
LOCAL USING GPRL0001      /* General Processes Property
LOCAL USING PMDA0001      /* Request Data
LOCAL USING PMDL0001      /* Portal Med Application Property
LOCAL
1 XML             (A) DYNAMIC
1 XML-END         (A) DYNAMIC
1 #OUTPUT-RECORD  (A20000)
1 #INPUT-RECORD   (A70)
1 #APPLICATION-NUMBER  (A) DYNAMIC
1 #ORDER-LIST     (A) DYNAMIC
1 #REMARK-LIST    (A) DYNAMIC
1 #TAIL           (A10)
1 #UNIX-FILE-NAME      (A) DYNAMIC
1 #WINDOWS-FILE-NAME   (A) DYNAMIC
1 #WINDOWS-TEST-DIR    (A) DYNAMIC INIT <'Ftp/EDS/Yelena/Portamedic/Requests/'>
1 #REC-READ       (N4)
1 #ERROR          (A1)
1 #DATN           (A8)
1 REDEFINE #DATN
  2 #YYYY         (A4)
  2 #MM           (A2)
  2 #DD           (A2)
1 #SUBMIT-DATE    (A10)
*
1 OUT             (A500)
1 II              (I4)
1 #I              (I4)
1 #I1             (I4)
1 #J              (I4)
1 #K              (I4)
*
1 OUTDYN (A) DYNAMIC
1 OBJLEN (I4)
1 OBJEND (I4)
1 OBJSTART (I4)
1 OBJLINE (I4)
1 #TAB (A1) INIT <H'09'>
*
1 #CX             (I4)
1 #CY             (I4)
1 #CZ             (I4)
1 #CGA-ID         (N6)
*
END-DEFINE
*
COMPRESS #UNIX-DIR #PORTAMED-REQUEST-FILE INTO #UNIX-FILE-NAME leaving no
DEFINE WORK FILE 1 #UNIX-FILE-NAME
COMPRESS #UNIX-DIR #UNIX-XML-FILE #REQUEST-FILE-EXTENSION INTO #UNIX-FILE-NAME leaving no
DEFINE WORK FILE 2 #UNIX-FILE-NAME TYPE 'ASCII-COMPRESSED'
*
* Check on which server process is running (Test/Prod)
IF F-IS-DEV(<>)
  #WINDOWS-REQUEST-DIR := #WINDOWS-TEST-DIR
END-IF
*
WRITE 'begin process...'
*
READ WORK FILE 1 #INPUT-RECORD
 SEPARATE #INPUT-RECORD INTO #APPLICATION-NUMBER #SUBMIT-DATE #ORDER-LIST #REMARK-LIST #TAIL WITH DELIMITER #TAB
*
RESET BO$TXLIFE #ERROR
CALLNAT 'PMBN0001' #APPLICATION-NUMBER #ORDER-LIST #ERROR BO$TXLIFE
*
  IF #ERROR NE ' '
     ESCAPE TOP
  END-IF
  RESET #OUTPUT-RECORD XML
  INCLUDE PMDCS001 "XML" "#CX" "#CY" "#CZ"
  #OUTPUT-RECORD := XML
  PERFORM WRITE-FORMATTED-XML
  COMPRESS #WINDOWS-REQUEST-DIR #DOCUMENT-NAME-PREFIX #APPLICATION-NUMBER #REQUEST-FILE-EXTENSION
                   INTO #WINDOWS-FILE-NAME LEAVING NO
  COMPRESS #UNIX-DIR #UNIX-XML-FILE #REQUEST-FILE-EXTENSION
           INTO #UNIX-FILE-NAME LEAVING NO
  CALLNAT 'GPRN0002' #FTP-SCRIPT-NAME #UNIX-FILE-NAME #WINDOWS-FILE-NAME
END-WORK
*
CLOSE WORK FILE 1
CALLNAT 'GPRN0002' #MV-SCRIPT-NAME #UNIX-DIR #PORTAMED-REQUEST-FILE
*
CLOSE WORK FILE 22
CALLNAT 'GPRN0002' #EMAIL-SCRIPT-NAME #ERROR-LOG-FILE
*
WRITE 'process ended'
*
*************************************
DEFINE SUBROUTINE WRITE-FORMATTED-XML
*************************************
*
OBJSTART := 1
EXAMINE xml FOR "&" REPLACE WITH "&amp;"
COMPRESS XML H'0A' INTO XML
EXAMINE xml FOR "><" REPLACE WITH ">" - H'0A' -"<"
EXAMINE xml FOR H'0A' GIVING POSITION OBJEND
REPEAT WHILE OBJEND NE 0
  IF OBJSTART GT 0 THEN
    ADD OBJSTART TO OBJEND
  END-IF
  OBJLEN := OBJEND - OBJSTART -1
  IF OBJLEN > 0 THEN
    OUTDYN := SUBSTRING(XML, OBJSTART, OBJLEN)
    FOR OBJLINE = 1 TO *LENGTH(OUTDYN) STEP 500
      OUT := SUBSTR (OUTDYN,OBJLINE)
      WRITE WORK FILE 2 OUT
    END-FOR
  END-IF
  OBJSTART := OBJEND
  IF OBJSTART GT *LENGTH(xml)
    ESCAPE BOTTOM
  END-IF
  EXAMINE SUBSTRING(xml,OBJSTART) FOR H'0A' GIVING POSITION OBJEND
END-REPEAT
CLOSE WORK FILE 2
END-SUBROUTINE
*
END
