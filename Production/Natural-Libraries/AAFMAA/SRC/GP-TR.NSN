* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* GP-TR  bz 02/10/2016 Write DBL FA and Ownership change to Tran register
* GPL/GP2 Proj
*                                             
DEFINE DATA 
PARAMETER
 1 p_GP-TR
   2 p_CN      (N6)
   2 p_Pol     (A16)   BY VALUE
   2 p_Old_DB  (N7.2)  BY VALUE
   2 p_New_DB  (N7.2)  BY VALUE
   2 p_Old_Own (N6)    BY VALUE
   2 p_New_Pay (N6)    BY VALUE
   2 p_RC      (N4)    
LOCAL
 1 ST-V VIEW OF A-STATUS
   2 PROCESS-IND
*  
 1 CNT-V VIEW OF A-CONTACTS
   2 LAST-NAME
   2 FIRST-NAME
   2 MIDDLE-NAME
   2 DATE-OF-BIRTH
   2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH_A      (A8)
*  
 1 TR-V VIEW OF A-TRANSACTION-REGISTER
    2 ID-NUMBER         (N6.0)
    2 NAME              (A25)
    2 DATE-LAST-UPDATE  (N8.0)
    2 TERMINAL-ID       (A8)
    2 CLERK-ID          (A3)
    2 TRANSACTION-DATN  (P9)
    2 TRANSACTION-TIMN  (P7)
   2 CHANGED-DATA-TABLE (1:32)
     3 OLD-DATA         (A125)
     3 REDEFINE OLD-DATA
       4 Old-Tag        (A10)
       4 Old-Txt        (A115)
     3 NEW-DATA         (A125)
     3 REDEFINE NEW-DATA
       4 New-Tag        (A10)
       4 New-Txt        (A115)
   2 PROCESS-IND        (A1)
   2 USER-COMPANY       (A1)
*
* 1 age                   (N2)
1 #d0                   (D)   INIT <*DATX>
1 #db                   (D)
1 A10                   (A10)
1 A07                   (A7)
* CI-CORR ISSUE-DATE 20140331      FACE-AMT     60000.00        
* CORR                             FACE-AMT     50000.00        
END-DEFINE
* -------------------------------------------------------------
ON ERROR
  WRITE NOTITLE '--' *DAT4U *TIMX *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR  / 
         3x 'CN:' p_CN 'Policy:' p_Pol 
  p_RC := *ERROR-NR
  ESCAPE ROUTINE
END-ERROR
* PRINT *PROGRAM p_GP-TR              /* --db

FIND(1) ST-V WITH ID-NUMBER = p_CN
END-FIND
FIND(1) CNT-V WITH ID-NUMBER = p_CN
END-FIND

IF DATE-OF-BIRTH_A = MASK(YYYYMMDD)
   MOVE EDITED DATE-OF-BIRTH_A TO #db(EM=YYYYMMDD)
*   COMPUTE ROUNDED age = (#d0 - #db) / 365.2425
END-IF
 MOVE EDITED #db(EM=MM/DD/YYYY) TO A10
 Old-Tag(1) := 'CI-GPL/2'
 DECIDE FOR FIRST CONDITION 
   WHEN p_Old_DB <> 0 
     New-Tag(1) := 'DB'
     MOVE EDITED p_Old_DB(EM=ZZZ,999) TO A07
     COMPRESS NUMERIC 'DOB:' A10 '   Death Benefit' A07  'Pol:' p_Pol INTO Old-Txt(1)
     MOVE EDITED p_New_DB(EM=ZZZ,999) TO A07
     COMPRESS NUMERIC FULL '                   Death Benefit'  A07 INTO New-Txt(1)

   WHEN p_Old_Own <> 0 
     New-Tag(1) := 'Owner'
     COMPRESS 'DOB:' A10 '  Owner ' p_Old_Own '  Pol:' p_Pol INTO Old-Txt(1)
     COMPRESS FULL '                  Owner' p_CN INTO New-Txt(1)

   WHEN p_New_Pay <> 0 
     New-Tag(1) := 'Payer'
     COMPRESS 'DOB:' A10 '  Payer       '      '  Pol:' p_Pol INTO Old-Txt(1)  
     COMPRESS FULL '              New Payer' p_New_Pay  INTO New-Txt(1)
     
     
   WHEN NONE
      p_RC := 1
      ESCAPE ROUTINE
   END-DECIDE

  TR-V.ID-NUMBER            := p_CN
  TR-V.PROCESS-IND          := ST-V.PROCESS-IND     /* 'I'
  COMPRESS LAST-NAME ',' INTO LAST-NAME LEAVING NO
  COMPRESS LAST-NAME FIRST-NAME MIDDLE-NAME  INTO NAME
  TR-V.DATE-LAST-UPDATE     := *DATN
  TR-V.CLERK-ID             := 'BAT'
  TR-V.TERMINAL-ID          := *INIT-ID
  TR-V.TRANSACTION-DATN     := *DATN
  TR-V.TRANSACTION-TIMN     := *TIMN
  STORE TR-V
END
