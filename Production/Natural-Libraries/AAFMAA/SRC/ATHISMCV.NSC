* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*****************************************************************
*                                                               *
* New COPYCODE member: ATHISMCV created by ACCORD on: 02/09/05  *
*                                                               *
*****************************************************************
*
*********************************************************
*       IF THIS MACRO IS CHANGED PROBABLY ATHISCNV & ATHISOEY
*       SHOULD ALSO BE CHANGED
**********************************************************
*
DEFINE SUBROUTINE CALC-OLD-MIN-CV
*
MOVE IS-ISS-AGE TO TC-ISS-X
SUBTRACT 14 FROM TC-ISS-X
MOVE TC-ISS-X TO TC-ATT-X
ADD IS-DURATION TO TC-ATT-X
*
IF TC-ATT-X > 86
  MOVE 86 TO TC-ATT-X
END-IF
*
IF IS-TYPE = 'OL'
  MOVE 82 TO TC-PUP-X
ELSE
  MOVE TC-ISS-X TO TC-PUP-X
  IF IS-TYPE = '30'
    ADD 30 TO TC-PUP-X
  ELSE
    ADD 20 TO TC-PUP-X
  END-IF
END-IF
*
IF IS-PAID-UP-SW = 'Y'
  RESET IS-NET-PREM
ELSE
  COMPUTE ROUNDED IS-NET-PREM = (1000 * TC-MX(TC-ISS-X)) / (TC-NX(TC-ISS-X) -
    TC-NX(TC-PUP-X))
END-IF
*
COMPUTE ROUNDED IS-WORK-C = ((((1000 * TC-MX(TC-ATT-X)) -
  (IS-NET-PREM * (TC-NX(TC-ATT-X) - TC-NX(TC-PUP-X))))
   * (5 * (IS-FAC-AMT / 5000)))) / TC-DX(TC-ATT-X)
*
COMPUTE ROUNDED IS-MIN-CSV = IS-WORK-C * 1
IF (IS-MIN-CSV > IS-FAC-AMT) OR ((IS-ISS-AGE + IS-DURATION) > 95)
  MOVE IS-FAC-AMT TO IS-MIN-CSV
END-IF
*
IF (IS-TYPE = '20' AND (IS-DURATION + 1) > 20) OR
    (IS-TYPE = '30'AND (IS-DURATION + 1) > 30) OR
    (IS-TYPE = 'OL'AND (IS-ISS-AGE + IS-DURATION + 1) > 96)
  RESET IS-NET-PREM
END-IF
*
ADD 1 TO TC-ATT-X
*
IF TC-ATT-X > 86
  MOVE 86 TO TC-ATT-X
END-IF
*
COMPUTE ROUNDED IS-WORK-C = ((((1000 * TC-MX(TC-ATT-X)) -
  (IS-NET-PREM * (TC-NX(TC-ATT-X) - TC-NX(TC-PUP-X))))
   * (5 * (IS-FAC-AMT / 5000)))) / TC-DX(TC-ATT-X)
*
IF (IS-WORK-C > IS-FAC-AMT) OR ((IS-ISS-AGE + IS-DURATION + 1) > 95)
  COMPUTE ROUNDED IS-NXT-YR-MIN-CSV = (IS-FAC-AMT * 1)
ELSE
  COMPUTE ROUNDED IS-NXT-YR-MIN-CSV = (IS-WORK-C * 1)
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*     C A L C - N E W - M I N - C V                             *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
DEFINE SUBROUTINE CALC-NEW-MIN-CV
*
*********************************************************
*       IF THIS MACRO IS CHANGED PROBABLY ATHISNEY
*       SHOULD ALSO BE CHANGED
**********************************************************
*
IF (IS-TYPE-2 = 'P' OR = 'F') AND IS-TYPE NE 'SP'
  RESET IS-TYPE-2
END-IF
MOVE '00' TO IS-TYPE-YEARS-XX
IF NOT (IS-TYPE = 'OL' OR = 'SP' OR = 'SO' OR = 'FS')
  IF IS-TYPE-2 = ' '
    MOVE IS-TYPE-1 TO IS-TYPE-YEARS-2
  ELSE
    MOVE IS-TYPE TO IS-TYPE-YEARS-XX
  END-IF
END-IF
*
MOVE IS-ISS-AGE TO TE-ISS-X
SUBTRACT 13 FROM TE-ISS-X
ADD 1 TO TE-ISS-X
*
IF TE-ISS-X > 87
  MOVE 87 TO TE-ISS-X
END-IF
*
IF IS-TYPE = 'OL' OR = 'SO' OR = 'FS' OR = 'SP'
  MOVE 83 TO TE-PUP-X
ELSE
  MOVE IS-ISS-AGE TO TE-PUP-X
  SUBTRACT 13 FROM TE-PUP-X
  ADD IS-TYPE-YEARS TO TE-PUP-X
END-IF
MOVE TE-ISS-X TO TF-ISS-X
ADD 14 TO TF-ISS-X
MOVE TE-PUP-X TO TF-PUP-X
ADD 14 TO TF-PUP-X
IF IS-ISS-DATE GE 19970101 AND IS-TYPE = 'OL'
  ADD 4 TO TF-PUP-X
END-IF
*
IF IS-PAID-UP-SW = 'Y'
  MOVE 1.0 TO IS-NET-PREM-RATIO
ELSE
  IF IS-ISS-YYYY > 1988
    COMPUTE ROUNDED IS-WORK-B = (TF-NX(TF-ISS-X) - TF-NX(TF-PUP-X)) / TF-DX(TF-ISS-X)
    COMPUTE ROUNDED IS-ADJ-PREM = ((((1000 * TF-MX(TF-ISS-X)) / TF-DX(TF-ISS-X))
      + 20) / IS-WORK-B) * ((IS-FAC-AMT / 5000) * 5)
    COMPUTE ROUNDED IS-WORK-C = IS-ADJ-PREM * IS-WORK-B
    COMPUTE ROUNDED IS-WORK-D = IS-ANN-PREM * IS-WORK-B
    COMPUTE ROUNDED IS-NET-PREM-RATIO = IS-WORK-C / IS-WORK-D
  ELSE
    COMPUTE ROUNDED IS-WORK-B = (TE-NX(TE-ISS-X) - TE-NX(TE-PUP-X)) / TE-DX(TE-ISS-X)
    COMPUTE ROUNDED IS-ADJ-PREM = ((((1000 * TE-MX(TE-ISS-X)) / TE-DX(TE-ISS-X))
      + 20) / IS-WORK-B) * ((IS-FAC-AMT / 5000) * 5)
    COMPUTE ROUNDED IS-WORK-C = IS-ADJ-PREM * IS-WORK-B
    COMPUTE ROUNDED IS-WORK-D = IS-ANN-PREM * IS-WORK-B
    COMPUTE ROUNDED IS-NET-PREM-RATIO = IS-WORK-C / IS-WORK-D
  END-IF
END-IF
*
MOVE IS-ISS-AGE TO TE-ATT-X
SUBTRACT 13 FROM TE-ATT-X
ADD IS-DURATION TO TE-ATT-X
IF TE-ATT-X < 2
  MOVE 2 TO TE-ATT-X
END-IF
*
IF TE-ATT-X > 87
  MOVE 87 TO TE-ATT-X
END-IF
MOVE TE-ATT-X TO TF-ATT-X
ADD 14 TO TF-ATT-X
*
MOVE IS-LUMP-PREM TO IS-WORK-PREM
*
IF (L-CI-TRAN = 604 AND IS-PAID-UP-SW NE 'Y') OR L-CI-TRAN = 606
  IF IS-ISS-YYYY > 1988
    COMPUTE ROUNDED IS-WORK-C = (IS-FAC-AMT
       * (TF-MX(TF-ATT-X) / TF-DX(TF-ATT-X))) -
      ((TF-NX (TF-ATT-X) - TF-NX (TF-PUP-X)) / TF-DX(TF-ATT-X))
       * IS-ANN-PREM * IS-NET-PREM-RATIO
    COMPUTE ROUNDED IS-MIN-CSV = IS-WORK-C * 1
  ELSE
    COMPUTE ROUNDED IS-WORK-C = (IS-FAC-AMT
       * (TE-MX(TE-ATT-X) / TE-DX(TE-ATT-X))) -
      ((TE-NX (TE-ATT-X) - TE-NX (TE-PUP-X)) / TE-DX(TE-ATT-X))
       * IS-ANN-PREM * IS-NET-PREM-RATIO
    COMPUTE ROUNDED IS-MIN-CSV = IS-WORK-C * 1
  END-IF
ELSE
  IF IS-ISS-YYYY > 1988
    COMPUTE ROUNDED IS-WORK-C = ((IS-MIN-CSV + (IS-WORK-PREM * IS-NET-PREM-RATIO))
       * (TF-DX(TF-ATT-X - 1) / TF-DX(TF-ATT-X)))
      - (((TF-MX(TF-ATT-X - 1) - TF-MX(TF-ATT-X)) / TF-DX(TF-ATT-X)) * IS-FAC-AMT)
    COMPUTE ROUNDED IS-MIN-CSV = IS-WORK-C * 1
  ELSE
    COMPUTE ROUNDED IS-WORK-C = ((IS-MIN-CSV + (IS-WORK-PREM * IS-NET-PREM-RATIO))
       * (TE-DX(TE-ATT-X - 1) / TE-DX(TE-ATT-X)))
      - (((TE-MX(TE-ATT-X - 1) - TE-MX(TE-ATT-X)) / TE-DX(TE-ATT-X)) * IS-FAC-AMT)
    COMPUTE ROUNDED IS-MIN-CSV = IS-WORK-C * 1
  END-IF
END-IF
*
IF IS-ISS-YYYY > 1988
  COMPUTE ROUNDED IS-WORK-B = IS-FAC-AMT
     * (TF-MX(TF-ATT-X) / TF-DX(TF-ATT-X))
ELSE
  COMPUTE ROUNDED IS-WORK-B = IS-FAC-AMT
     * (TE-MX(TE-ATT-X) / TE-DX(TE-ATT-X))
END-IF
*
IF (IS-MIN-CSV > IS-WORK-B)
  MOVE IS-WORK-B TO IS-MIN-CSV
END-IF
*
IF IS-MIN-CSV > IS-FAC-AMT
  MOVE IS-FAC-AMT TO IS-MIN-CSV
END-IF
*
ADD 1 TO TE-ATT-X
*
IF TE-ATT-X > 87
  MOVE 87 TO TE-ATT-X
END-IF
MOVE TE-ATT-X TO TF-ATT-X
ADD 14 TO TF-ATT-X
*
IF (IS-PAID-UP-SW = 'Y') OR ((IS-TYPE = '20')
    AND (IS-DURATION + 1 > 20)) OR ((IS-TYPE = '30')
    AND (IS-DURATION + 1 > 30)) OR ((IS-TYPE = '7P')
    AND (IS-DURATION + 1 > 7))  OR ((IS-TYPE = 'OL')
    AND (IS-ISS-AGE + IS-DURATION + 1 > 96))
  RESET IS-WORK-PREM
ELSE
  MOVE IS-ANN-PREM TO IS-WORK-PREM
END-IF
*
IF IS-ISS-YYYY > 1988
  COMPUTE ROUNDED IS-WORK-C = ((IS-MIN-CSV + (IS-WORK-PREM * IS-NET-PREM-RATIO))
     * (TF-DX(TF-ATT-X - 1) / TF-DX(TF-ATT-X)))
    - (((TF-MX(TF-ATT-X - 1) - TF-MX(TF-ATT-X)) / TF-DX(TF-ATT-X)) * IS-FAC-AMT)
  COMPUTE ROUNDED IS-NXT-YR-MIN-CSV = IS-WORK-C * 1
ELSE
  COMPUTE ROUNDED IS-WORK-C = ((IS-MIN-CSV + (IS-WORK-PREM * IS-NET-PREM-RATIO))
     * (TE-DX(TE-ATT-X - 1) / TE-DX(TE-ATT-X)))
    - (((TE-MX(TE-ATT-X - 1) - TE-MX(TE-ATT-X)) / TE-DX(TE-ATT-X)) * IS-FAC-AMT)
  COMPUTE ROUNDED IS-NXT-YR-MIN-CSV = IS-WORK-C * 1
END-IF
*
IF IS-ISS-YYYY > 1988
  COMPUTE ROUNDED IS-WORK-B = IS-FAC-AMT
     * (TF-MX(TF-ATT-X) / TF-DX(TF-ATT-X))
ELSE
  COMPUTE ROUNDED IS-WORK-B = IS-FAC-AMT
     * (TE-MX(TE-ATT-X) / TE-DX(TE-ATT-X))
END-IF
*
IF (IS-NXT-YR-MIN-CSV > IS-WORK-B)
  MOVE IS-WORK-B TO IS-NXT-YR-MIN-CSV
END-IF

*
IF IS-NXT-YR-MIN-CSV > IS-FAC-AMT
  MOVE IS-FAC-AMT TO IS-NXT-YR-MIN-CSV
END-IF
*
END-SUBROUTINE
*
********************************************************************************
*
