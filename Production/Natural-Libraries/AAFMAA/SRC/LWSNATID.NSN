* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNATID.
/**
/** :author nguentchev
/* Populate Application tracking CRM GUID
/*
/* 
/* NAME     DATE   TAG   DESCRIPTION
/* ====     ====   ===   ===========
/* NGG   20180108  NG1   SOLA Project
/*
DEFINE DATA
PARAMETER
1 #REQUEST     (I4)
1 #AT-NUMBER   (N6)
1 #GUID        (A50)   /* AT-GUID OR CONTACT-GUID
1 ErrorMsg     (A) DYNAMIC
LOCAL USING LWSLUID   /* GIUD operations
LOCAL
1 #RD-SUCCESS       (A7) CONST <'Success'>
1 AT-V VIEW OF A-APPL-TRACKING
  2 CONTACT-ID (N8)   /* NG1
  2 AT-GUID    (A50)  /* Application tracking CRM GUID
1 CNT-V VIEW OF A-CONTACTS
  2 CRM-CONTACT-GUI
1 #ISN         (P10)
END-DEFINE
/* 
ErrorMsg := #RD-SUCCESS
FL.
FIND (1) AT-V WITH ID-NUMBER = #AT-NUMBER
  IF NO RECORDS FOUND
    ErrorMsg := 'AT number not found'
    escape ROUTINE
  END-NOREC
  #ISN := *ISN(FL.)
END-FIND

DECIDE ON FIRST VALUE OF #REQUEST
  VALUE GET-AT-GUID
    #GUID := AT-V.AT-GUID
  VALUE DELETE-AT-GUID
    GET AT-V #ISN
    RESET AT-V.AT-GUID
    UPDATE
  VALUE UPDATE-AT-GUID
    GET AT-V #ISN
    AT-V.AT-GUID := #GUID
    UPDATE
  VALUE GET-CONT-GUID
    FIND (1) CNT-V WITH CONTACT-ID = AT-V.CONTACT-ID
      #GUID := CNT-V.CRM-CONTACT-GUI
    END-FIND
  VALUE DELETE-CONT-GUID
    FIND (1) CNT-V WITH CONTACT-ID = AT-V.CONTACT-ID
      RESET CNT-V.CRM-CONTACT-GUI
      UPDATE
    END-FIND
  VALUE UPDATE-CONT-GUID
    FIND (1) CNT-V WITH CONTACT-ID = AT-V.CONTACT-ID
      CNT-V.CRM-CONTACT-GUI := #GUID
      UPDATE
    END-FIND
  NONE VALUE
    ErrorMsg := 'Request is not implemented'
END-DECIDE

END
