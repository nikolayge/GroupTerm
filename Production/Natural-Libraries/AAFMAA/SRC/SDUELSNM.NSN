* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID : SDUELSNM   *** DUES LETTER FOR AFSC
*
* DUES LETTER FOR NM SCREEN
*
DEFINE DATA
*
PARAMETER
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-SCR-COMMAND (A2)
1 #INSURED         (A1)
1 #NAME            (A25)
1 #CN1-A           (A6)
1 #APREM           (N6.2)
1 #MPREM           (N5.2)
1 #EFF-DT          (N8)
1 REDEFINE #EFF-DT
 2 #EFF-DT-MM-N    (N2)
 2 #EFF-DT-DD-N    (N2)
 2 #EFF-DT-YYYY-N  (N4)
1 #FPF-START       (N8)
1 REDEFINE #FPF-START
 2 #FPFS-MM        (N2)
 2 #FPFS-DD        (N2)
 2 #FPFS-YYYY      (N4)
1 #FPF-END         (N8)
1 REDEFINE #FPF-END
 2 #FPFE-MM        (N2)
 2 #FPFE-DD        (N2)
 2 #FPFE-YYYY      (N4)
1 #MODE            (A1)
1 #FPF             (N7.2)
1 #MIL-ST          (A1)
1 #MIL-ID          (A3)
1 #FPF-DUES        (N5.2)
1 #PLAN            (A3)
1 #ALOT-AMT        (N5.2)
1 #CGA-ORIG-COMMAND (A2)
1 #E-DUES           (P5.2)
1 #G-DUES           (P5.2)
1 #A-DUES           (P5.2)
1 #S-DUES           (P5.2)
1 #Q-DUES           (P5.2)
1 #FLEX-PLAN        (L)
1 #FLEX-YEARS       (N3)
1 #CAP-IND          (A1)
1 #INSURED-POL      (A8)
1 #LINKED-CN        (N6)
1 REDEFINE #LINKED-CN
 2 #LINKED-CN-A     (A6)
1 #PAYS-DUES        (L)
1 #HAS-FBR          (L)
1 #FPF-FBR          (N3.2)
1 #FBR-MON-PREM     (N3.2)
1 #AMT-PAID         (N5.2)
1 #PAD-CODE         (A1)
1 #ISSUE-DATE       (N8)
1 REDEFINE #ISSUE-DATE
 2 #ISS-DT-YYYYMM   (N6)
 2 REDEFINE #ISS-DT-YYYYMM
  3 #ISS-DT-YYYY    (N4)
  3 #ISS-DT-MM      (N2)
 2 #ISS-DT-DD       (N2)
1 #PAID-TO-DATE     (N8)
1 REDEFINE #PAID-TO-DATE
 2 #PD-TO-DT-YYYYMM   (N6)
 2 REDEFINE #PD-TO-DT-YYYYMM
  3 #PD-TO-DT-YYYY    (N4)
  3 #PD-TO-DT-MM      (N2)
 2 #PD-TO-DT-DD       (N2)
*
LOCAL
1 #TODAY-DISP (A10)
1 #SCN-TITLE (A40) INIT <'INFORMATION ON PAYMENT OF PREMIUM'>
1 #SCN-LINE (A75/23)
1 #START-CHG  (A6)
*
1 #ASQ-DIFF (N7.2)
1 #ASQ-DIFF-X (A8)
1 #MON-DUES (N3.2)
1 #ASQ-DUES (N3.2)
1 #SCN-ROW-1 (A75)
1 REDEFINE #SCN-ROW-1
 2 #SR1-A   (A15)
 2 #SR1-B   (A58)
 2 REDEFINE #SR1-B
  3 #SR1-C  (A6)
  3 #SR1-D  (A52)
*
1 #DATE      (N24) INIT <312831303130313130313031>
1 REDEFINE #DATE
2 #DAYS   (N2/1:12)
1 #DX    (I2)
1 #MX    (I2)
1 #M-L (A108) INIT<'January  February March    April    May      '
 -'June     July     August   SeptemberOctober  November December '>
1 REDEFINE #M-L
2 #M-SCN-LIT  (A9/1:12)
*
1 #NEW-ALOT    (N5.2)
1 #WORK-AMT-X  (A8)
1 #MONTH-PREM  (N5.2)
1 #HOLD-PREM  (A6)
1 #HOLD-EFF-DD (A2)
1 #HOLD-FPFS-DD (A2)
1 #HOLD-YEAR  (A5)
1 #HOLD-ID    (A10)
1 #HOLD-NAME  (A25)
1 #WORK-DD    (N2)
1 #FREQ       (A12)
1 #FREQ-LY    (A14)
1 #COMMA   (A1) CONST<','>
1 #LAST-NAME  (A24)
1 #FIRST-NAME  (A24)
1 #OVFLOW-NAME  (A24)
1 #FPF-DISP     (A8)
*
1 #TODAY-YYYYMMDD (N8)
1 REDEFINE #TODAY-YYYYMMDD
 2 #TODAY-YYYY    (A4)
 2 #TODAY-MM      (A2)
 2 #TODAY-DD      (A2)
*
1 #EZ-PAY-START-MM (N2)
1 #EZ-PAY-START-YYYY (N4)
1 #SC-X  (I2)
1 #MON-PREM-X (A5)
1 #2-MONS-PREM   (N4.2)
1 #2-MONS-PREM-X (A8)
1 #ONLY-FBR      (L)
1 #AMT-DUE       (N5.2)
1 #AMT-DISP-5    (A5)
1 #AMT-DISP-6    (A6)
1 #STAR          (A8)
1 #MM            (N2)
1 #YYYY          (N4)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
MOVE *DATN TO #TODAY-YYYYMMDD
COMPRESS #TODAY-MM #TODAY-DD #TODAY-YYYY
 INTO #TODAY-DISP WITH '/'
*
IF *DEVICE NE 'BATCH'
  RESET #CGA-SCR-COMMAND #CGA-SCR-ID
END-IF
*
SEPARATE #NAME LEFT INTO #LAST-NAME #FIRST-NAME #OVFLOW-NAME WITH
    DELIMITER #COMMA
COMPRESS #FIRST-NAME #LAST-NAME INTO #HOLD-NAME
*
IF #OVFLOW-NAME > ' '
   COMPRESS #HOLD-NAME ',' INTO #HOLD-NAME LEAVING NO SPACE
   COMPRESS #HOLD-NAME #OVFLOW-NAME INTO #HOLD-NAME
END-IF
*
IF  (#PLAN = MASK ('1S'))    /* 1-STAR IS 1-TIME PMT
 OR (#CAP-IND = 'Y') /* 1-time pmt
  ESCAPE ROUTINE
END-IF
*
COMPRESS '(CN' #CN1-A ')' INTO #HOLD-ID LEAVING NO SPACE
*
IF #CGA-SCR-SUFFIX = 'LTR' OR = 'OVR' OR = '   '
                OR = 'CKA' OR = 'CKS' OR = 'CKQ'
  COMPRESS 'for' #HOLD-NAME #HOLD-ID INTO #SR1-B
ELSE
  COMPRESS 'for' #HOLD-NAME #HOLD-ID INTO #SR1-D
END-IF
*
COMPUTE #MONTH-PREM = #APREM / 12
MOVE #MONTH-PREM TO #MON-DUES
MOVE EDITED #MONTH-PREM (EM=Z9.99) TO #MON-PREM-X
*
IF #AMT-PAID NE 0
  ADD 1 TO #SC-X
  MOVE EDITED #AMT-PAID (EM=ZZ9.99) TO #AMT-DISP-6
  COMPRESS 'Thank you for your deposit of $' #AMT-DISP-6 'which'
           'will be applied toward'    INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  MOVE 'your account.' TO #SCN-LINE (#SC-X)
END-IF
*
IF #PLAN = MASK ('2S')
  MOVE '"2-Star"' TO #STAR
ELSE
  MOVE '"3-Star"' TO #STAR
END-IF
*
ADD 2 TO #SC-X
IF #PLAN = '2SW' OR = '3SW' OR = '2DW' OR = '3DW'
  MOVE 'Annual Fee Payment:' TO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  MOVE EDITED #APREM (EM=Z9.99) TO #MON-PREM-X
  COMPRESS 'Your $' #MON-PREM-X ' fee payment is for the'
                             INTO #SCN-LINE (#SC-X) LEAVING NO
ELSE
  MOVE 'Monthly Fee Payment:' TO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'Your $' #MON-PREM-X ' fee payment is for the'
                             INTO #SCN-LINE (#SC-X) LEAVING NO
END-IF
COMPRESS #SCN-LINE (#SC-X) #STAR 'services you requested.'
                           INTO #SCN-LINE (#SC-X)
*
IF (#MODE = '1' OR = '2' OR = '4') OR (#CGA-SCR-SUFFIX = 'CCP'
                                    OR = 'CC1' OR = 'CC2' OR = 'CC4')
  PERFORM CC-1-2-4-PMT
ELSE
  IF (#MODE = 'E' OR = 'P') OR (#CGA-SCR-SUFFIX = 'EZP')
   OR (#PAD-CODE = 'P')  /* soon to be ezpay
    PERFORM EZ-PAY-PMT
  ELSE
    IF (#MODE = 'A' OR = 'Q' OR = 'S') OR (#CGA-SCR-SUFFIX = 'CHK'
                                    OR = 'CKA' OR = 'CKS' OR = 'CKQ')
      PERFORM A-S-Q-PMT
    ELSE
      PERFORM GOVT-ALLOT-PMT
    END-IF
  END-IF
END-IF
*
RESET #CGA-SCR-SUFFIX
*
IF *DEVICE = 'BATCH'
  WRITE (4) NOTITLE USING FORM 'CDUE1FNM'
ELSE
  INPUT USING MAP 'CDUE1MNM'
END-IF
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GOVT-ALLOT-PMT
* * * * * * * * * * * * * * * * *
*
IF #ALOT-AMT = 0
  MOVE 'Start' TO #START-CHG
ELSE
  MOVE 'Change' TO #START-CHG
END-IF
*
ADD 2 TO #SC-X
MOVE 'Paying by Government Allotment:' TO #SCN-LINE (#SC-X)
*
MOVE #EFF-DT-YYYY-N TO #YYYY
COMPUTE #MM = #EFF-DT-MM-N + 2
*
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
COMPRESS #YYYY '.' INTO #HOLD-YEAR LEAVING NO SPACE
*
ADD 1 TO #SC-X
COMPRESS 'Your allotment in the amount of $' #MON-PREM-X ' shoul'
     'd start at the end of' INTO #SCN-LINE (#SC-X) LEAVING NO
*
ADD 1 TO #SC-X
COMPRESS #M-SCN-LIT (#MM) #HOLD-YEAR INTO #SCN-LINE (#SC-X)
*
ADD 2 TO #SC-X
COMPRESS 'Upon receipt of your first allotment payment, we will'
     'adjust your account' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'to accommodate any advance deposit you may have paid with'
     'your application.' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'At that time we will bill you for any fees due or refund any'
     'overpayment to' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
MOVE 'bring your account up to date.' TO #SCN-LINE (#SC-X)
*
IF #MIL-ST = 'R'
  ADD 2 TO #SC-X
  COMPRESS 'Please sign and mail the enclosed' #START-CHG
     'Form to the address on the form.' INTO #SCN-LINE (#SC-X)
ELSE
  ADD 2 TO #SC-X
  COMPRESS 'Please submit the enclosed' #START-CHG
     'Form to your Finance Office.'     INTO #SCN-LINE (#SC-X)
END-IF
*
ADD 2 TO #SC-X
MOVE 'Thank You.' TO #SCN-LINE (#SC-X)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE EZ-PAY-PMT
* * * * * * * * * * * * * * * * *
*
MOVE #EFF-DT-YYYY-N TO #YYYY
COMPUTE #MM = #EFF-DT-MM-N + 2
*
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
COMPRESS #YYYY '.' INTO #HOLD-YEAR LEAVING NO SPACE
*
ADD 2 TO #SC-X
MOVE 'Paying by Automatic Bank Draft:' TO #SCN-LINE (#SC-X)
*
ADD #MONTH-PREM TO #E-DUES   /* to get total new montly prem
*
MOVE EDITED #E-DUES (EM=Z,ZZ9.99) TO #WORK-AMT-X
ADD 1 TO #SC-X
*
COMPRESS 'Your EZ-PAY of $' #WORK-AMT-X  'should start at the'
   'beginning of' #M-SCN-LIT (#MM) #YYYY
      INTO #SCN-LINE (#SC-X)
COMPRESS #SCN-LINE (#SC-X) '.' INTO #SCN-LINE (#SC-X) LEAVING NO
*
IF #PAD-CODE = 'P'
  ADD 2 TO #SC-X
  MOVE 'Thank You for providing your blank check.' TO #SCN-LINE (#SC-X)
ELSE
  ADD 1 TO #SC-X
  COMPRESS 'To start your EZ-PAY we must have your ABA and Account'
       'Numbers. Complete' INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'the enclosed Authorization Card and return with a blank'
       'check from your'               INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  MOVE 'check book and mark it "VOID".' TO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'If you have already provided your blank check, please'
       'disregard.'        INTO #SCN-LINE (#SC-X)
END-IF
*
ADD 2 TO #SC-X
COMPRESS 'Upon receipt of your first automatic bank draft, we will'
     'adjust your' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'account to accommodate any advance deposit you may have'
     'paid with  your' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'application. At that time we will bill you for any fees'
     'due or refund any' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
MOVE 'overpayment to bring your account up to date.'
       TO #SCN-LINE (#SC-X)
*
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE A-S-Q-PMT
* * * * * * * * * * * * * * * * *
*
MOVE EDITED #MPREM (EM=ZZ9.99) TO #HOLD-PREM
*
IF #MODE = 'A' OR = 'CKA'   /* 2SW annual dues is not a multiple of 12
  MOVE #APREM TO #ASQ-DUES
  MOVE ' annual' TO #FREQ
  MOVE ' annually' TO #FREQ-LY
ELSE
  IF (#MODE = 'Q') OR (#CGA-SCR-SUFFIX = 'CKQ')
    COMPUTE #ASQ-DUES = #MON-DUES * 3
    MOVE ' quarterly' TO #FREQ
    MOVE ' quarterly' TO #FREQ-LY
  ELSE
    IF (#MODE = 'S') OR (#CGA-SCR-SUFFIX = 'CKS')
      COMPUTE #ASQ-DUES = #MON-DUES * 6
      MOVE ' semi-annual' TO #FREQ
      MOVE ' semi-annually' TO #FREQ-LY
    END-IF
  END-IF
END-IF
*
ADD 2 TO #SC-X
MOVE 'Paying by Personal Check:' TO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'We note that you wish to make your payments' #FREQ-LY
         ' at the rate' INTO #SCN-LINE (#SC-X) LEAVING NO
ADD 1 TO #SC-X
COMPRESS 'of $' #HOLD-PREM ' per period.'
INTO #SCN-LINE (#SC-X) LEAVING NO
*
IF #PD-TO-DT-YYYYMM GT #ISS-DT-YYYYMM /* 1st mode already paid
  ADD 2 TO #SC-X
  MOVE 'We will bill you for future payments.' TO #SCN-LINE (#SC-X)
ELSE
  IF #AMT-PAID NE 0
    COMPUTE #AMT-DUE = #ASQ-DUES - #AMT-PAID
*
    IF #AMT-DUE = 0
      ADD 2 TO #SC-X
      MOVE 'We will bill you for future payments.' TO #SCN-LINE (#SC-X)
    ELSE
      IF #AMT-DUE LT 0
        ADD 2 TO #SC-X
        COMPRESS 'Your deposit exceeds the current amount due. Your'
               'next bill will'    INTO #SCN-LINE (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'be adjusted accordingly.'
                                   INTO #SCN-LINE (#SC-X) LEAVING NO
      ELSE
        MOVE EDITED #AMT-DUE (EM=ZZ9.99) TO #AMT-DISP-6
        ADD 2 TO #SC-X
        COMPRESS 'Please remit the balance due at this time of $'
           #AMT-DISP-6 ' to bring your account'
              TO #SCN-LINE (#SC-X) LEAVING NO
        ADD 1 TO #SC-X
        COMPRESS 'up to date. We will bill you for future'
                 ' payments.'        INTO #SCN-LINE (#SC-X) LEAVING NO
      END-IF
    END-IF
  ELSE
    MOVE EDITED #ASQ-DUES (EM=ZZ9.99) TO #AMT-DISP-6
    ADD 2 TO #SC-X
    COMPRESS 'Please remit your first payment of $' #AMT-DISP-6
             ' to bring'          INTO #SCN-LINE (#SC-X) LEAVING NO
    ADD 1 TO #SC-X
    COMPRESS 'your account up to date. We will bill you for future'
             ' payments.'        INTO #SCN-LINE (#SC-X) LEAVING NO
  END-IF
END-IF
*
IF   (#MIL-ST = 'A' OR = 'R')
 AND (#MIL-ID = 'A' OR = 'F' OR = 'N' OR = 'M' OR = 'CG')
 AND (NOT #PLAN = '2SW' OR = '3SW' OR = '2DW' OR = '3DW')
  ADD 2 TO #SC-X
  COMPRESS 'While you may pay your fees by check, it would be'
           'appreciated if'   INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'you would consider paying by government allotment.'
           'This would help to'   INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'reduce expenses and would benefit both you and the'
           'Corporation. If'   INTO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  COMPRESS 'you choose to start an allotment, we have enclosed a'
           'completed form.'   INTO #SCN-LINE (#SC-X)
*
  IF  #MIL-ST = 'A'
    ADD 1 TO #SC-X
    COMPRESS 'Please take this to your Finance Office.'
             'Thank You.'                INTO #SCN-LINE (#SC-X)
  ELSE
    ADD 1 TO #SC-X
    COMPRESS 'Please mail this to your Defense Finance & Accounting'
             'Service.'          INTO #SCN-LINE (#SC-X)
    ADD 1 TO #SC-X
    COMPRESS 'Thank You.'
                                         INTO #SCN-LINE (#SC-X)
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CC-1-2-4-PMT     /* credit card
* * * * * * * * * * * * * * * * *
*
MOVE EDITED #MPREM (EM=ZZ9.99) TO #HOLD-PREM
*
IF #MODE = '1' OR = 'CQA'   /* 2SW annual dues is not a multiple of 12
  MOVE #APREM TO #ASQ-DUES
  MOVE ' annual' TO #FREQ
  MOVE ' annually' TO #FREQ-LY
ELSE
  IF (#MODE = '4') OR (#CGA-SCR-SUFFIX = 'CCQ')
    COMPUTE #ASQ-DUES = #MON-DUES * 3
    MOVE ' quarterly' TO #FREQ
    MOVE ' quarterly' TO #FREQ-LY
  ELSE
    IF (#MODE = '2') OR (#CGA-SCR-SUFFIX = 'CCS')
      COMPUTE #ASQ-DUES = #MON-DUES * 6
      MOVE ' semi-annual' TO #FREQ
      MOVE ' semi-annually' TO #FREQ-LY
    END-IF
  END-IF
END-IF
*
ADD 2 TO #SC-X
MOVE 'Paying by Credit Card:' TO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'We note that you wish to make your payments' #FREQ-LY
         ' at the rate' INTO #SCN-LINE (#SC-X) LEAVING NO
ADD 1 TO #SC-X
COMPRESS 'of $' #HOLD-PREM ' per period.'
INTO #SCN-LINE (#SC-X) LEAVING NO
*
IF #PD-TO-DT-YYYYMM GT #ISS-DT-YYYYMM /* 1st mode already paid
  ADD 2 TO #SC-X
  MOVE 'We will charge you for future payments.' TO #SCN-LINE (#SC-X)
ELSE
  IF #AMT-PAID NE 0
    COMPUTE #AMT-DUE = #ASQ-DUES - #AMT-PAID
*
    IF #AMT-DUE = 0
      ADD 1 TO #SC-X
     MOVE 'We will charge you for future payments.' TO #SCN-LINE (#SC-X)
    ELSE
      IF #AMT-DUE LT 0
        ADD 2 TO #SC-X
        COMPRESS 'Your deposit exceeds the current amount due. Your'
               'next charge will'    INTO #SCN-LINE (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'be adjusted accordingly.'
                                   INTO #SCN-LINE (#SC-X) LEAVING NO
      ELSE
        MOVE EDITED #AMT-DUE (EM=ZZ9.99) TO #AMT-DISP-6
        ADD 2 TO #SC-X
        COMPRESS 'Please remit the balance due at this time of $'
           #AMT-DISP-6 ' to bring your account'
              TO #SCN-LINE (#SC-X) LEAVING NO
        ADD 1 TO #SC-X
        COMPRESS 'up to date. We will charge you for future'
                 ' payments.'        INTO #SCN-LINE (#SC-X) LEAVING NO
      END-IF
    END-IF
  ELSE
    MOVE EDITED #ASQ-DUES (EM=ZZ9.99) TO #AMT-DISP-6
    ADD 2 TO #SC-X
    COMPRESS 'We will charge your first payment of $' #AMT-DISP-6
             ' to bring'          INTO #SCN-LINE (#SC-X) LEAVING NO
    ADD 1 TO #SC-X
    COMPRESS 'your account up to date. We will charge you for future'
             ' payments.'        INTO #SCN-LINE (#SC-X) LEAVING NO
  END-IF
END-IF
*
END-SUBROUTINE
*
END
