* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPBPDFAS                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : DFAS batch jobs Main Menu (Active/Retired)            *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    01102008  None    Program Written                             *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING OBJAGENT
LOCAL USING OBJAUSER
*
LOCAL
*
1 #I1                   (I2)
1 #FULL-ACCESS          (L)
1 #ADMIN-ACCESS         (L)
1 #TEST                 (L)
1 #ESCAPE               (L)
1 #SHCMD                (A250)
1 #TEXT                 (A78)
1 #ACTIVE-RETIRED       (A15)
1 #PROCESS-TO-RUN       (A1/2)
1 #FUNCTION-ATTRIBUTES  (A68)
1 REDEFINE #FUNCTION-ATTRIBUTES
  2 #PROGRAM            (A8)
  2 #SEPARATOR          (A1)
  2 #FUNCTION-PARM      (A7)
  2 #HEADER-1           (A50)
  2 #SECURITY-CODE      (N2)
*
1 #CONSTANTS
 2 #ACTIVE              (A15)   INIT <'ACTIVE MEMBERS'>
 2 #RETIRED             (A15)   INIT <'RETIRED MEMBERS'>
*
END-DEFINE
***********************************************************************
INCLUDE GONERCXX
*
DEFINE WINDOW CONFIRMATION-BOX
   SIZE 12*60
   BASE 5/7
*
IF *DATA NE 0
  INPUT #FUNCTION-ATTRIBUTES
END-IF
*
RESET #ADMIN-ACCESS #FULL-ACCESS
IF FUNCTION-ACCESS(<*USER,#SECURITY-CODE>) = 'A'
  #ADMIN-ACCESS := #FULL-ACCESS := TRUE
ELSE
  IF FUNCTION-ACCESS(<*USER,#SECURITY-CODE>) = 'U'
    #FULL-ACCESS := TRUE
  END-IF
END-IF
*
IF NOT #FULL-ACCESS
  #TEXT := 'User not authorized to perform this function. Browse access only'
END-IF
*
SET KEY PF1 = HELP
SET KEY PF3  NAMED 'Retrn'
SET KEY PF12 NAMED 'Quit'
*
REPEAT
  SET KEY PF2 = OFF
*
  INPUT TEXT #TEXT MARK *#PROCESS-TO-RUN (1) USING MAP 'GPBMDFAS'
  RESET #TEXT
*
  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'PF3'
      #FUNCTION-PARM := 'PROC '
      #PROGRAM := 'G1000PUI'
      STACK TOP DATA #FUNCTION-ATTRIBUTES
      FETCH #PROGRAM
    VALUE 'PF12'
      #CGA-SCR-COMMAND := 'GM'
      PERFORM CALL-G1000PXX
    NONE
      IGNORE
  END-DECIDE
*
  IF NOT #FULL-ACCESS
    ESCAPE TOP
  END-IF
*
  EXAMINE #PROCESS-TO-RUN (*) FOR 'X' INDEX #I1  
  IF #I1 = 0
    DECIDE FOR FIRST CONDITION        
      WHEN *CURS-LINE = 5               
        #I1 := 1
      WHEN *CURS-LINE = 11
        #I1 := 2
      WHEN NONE
        IGNORE                          
    END-DECIDE                          
  END-IF
*
  IF NOT #I1 = 1 OR = 2 
    #TEXT := 'Place CURSOR over process and press ENTER to continue or press F3/F12 to EXIT'
    ESCAPE TOP
  END-IF
*
  DECIDE FOR FIRST CONDITION
    WHEN #I1 = 1 
      #ACTIVE-RETIRED := #ACTIVE
      PERFORM TEST-OR-FINAL-RUN
      IF #TEST
        #SHCMD := '/usr/SAG/cronus/.scripts/T4CYDFAS.bsh "A"  >/dev/null'
      ELSE
        #SHCMD := '/usr/SAG/cronus/.scripts/T4CYDFAS.bsh "B"  >/dev/null'
      END-IF
      PERFORM CALL-SHCMD
    WHEN #I1 = 2 
      #ACTIVE-RETIRED := #RETIRED
      PERFORM TEST-OR-FINAL-RUN
      IF #TEST
        #SHCMD := '/usr/SAG/cronus/.scripts/T4CYDFAS.bsh "C"  >/dev/null'
      ELSE
        #SHCMD := '/usr/SAG/cronus/.scripts/T4CYDFAS.bsh "D"  >/dev/null'
      END-IF
      PERFORM CALL-SHCMD
    WHEN NONE
      IGNORE
  END-DECIDE
END-REPEAT
*
#CGA-SCR-COMMAND := 'UI'
PERFORM CALL-G1000PXX
*
***********************************************************************
DEFINE SUBROUTINE CALL-G1000PXX
***********************************************************************
*
SET KEY OFF
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE CALL-SHCMD
***********************************************************************
*
CALL 'SHCMD' #SHCMD
*
SET WINDOW 'CONFIRMATION-BOX'
  INPUT USING MAP 'GPBMDFAC'
SET WINDOW OFF
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE TEST-OR-FINAL-RUN
***********************************************************************
*
RESET #TEXT #PROCESS-TO-RUN (*) #I1
SET KEY PF2  NAMED 'Submt'
*
REPEAT
  INPUT TEXT #TEXT MARK *#PROCESS-TO-RUN (1) USING MAP 'GPBMDFAA'
  RESET #TEXT
*
  RESET #ESCAPE
  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'PF3'
      #ESCAPE := TRUE
      ESCAPE BOTTOM
    VALUE 'PF12'
      #CGA-SCR-COMMAND := 'GM'
      PERFORM CALL-G1000PXX
    NONE
      IGNORE
  END-DECIDE
*
  EXAMINE #PROCESS-TO-RUN (*) FOR 'X' INDEX #I1  
  IF #I1 = 0
    DECIDE FOR FIRST CONDITION        
      WHEN *CURS-LINE = 5               
        #I1 := 1
      WHEN *CURS-LINE = 7              
        #I1 := 2
      WHEN NONE
        IGNORE                          
    END-DECIDE                          
  END-IF
*
  IF NOT #I1 = 1 OR = 2
   OR *PF-KEY NE 'PF2'
    #TEXT := 'Place CURSOR over process and press F2 to submit or press F3/F12 to EXIT'
    ESCAPE TOP
  END-IF
*
  RESET #TEST #PROCESS-TO-RUN (*)                              
  IF #I1 = 1
    #TEST := TRUE
  END-IF
*
  ESCAPE BOTTOM
END-REPEAT
*
IF #ESCAPE
  ESCAPE TOP
END-IF
*
END-SUBROUTINE
*
END
