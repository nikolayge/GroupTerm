* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PROGRAM-ID: ATHP08P0 - ACCUM FILE SELECTED INFO
*************************************************************************
*                       MODIFICATION LOG                                *
* USER   DATE      TAG     REASON                                       *
* DRW    09282004  DW1     add CREDIT CARD processing                   *
* DRW    01312005  DW2     stop cnt at 99999                            *
* PAM    03092009  PM3     Removed old auto loan pay indicators         *
* RSE    09182009  RE1     Change PROCESS-IND/600K logic for Unification*
* RSE    10052012  RE2     CN Expansion Project -                       *
*                          Get beneficiary data from new bene files     *
* JNT    09132013  JT1     Address normalization                        *
* TMT    05162014  TT1     CONTACT NORMALIZATION  -  EMAILS             *
*************************************************************************
DEFINE DATA
LOCAL USING ADDA0001                                   /* JT1
LOCAL USING CONTPDA1
LOCAL
1 IN-V VIEW OF A-INTRO-MEMBER
  2 ID-NUMBER
  2 DATE-OF-BIRTH
  2 REDEFINE DATE-OF-BIRTH
    3 DOB-YYYY (N4)
    3 DOB-MM (N2)
    3 DOB-DD (N2)
*
1 TK-V VIEW OF A-TICKERS
  2 SYMBOL
*
1 FI-V VIEW OF A-INVESTMENTS
  2 C*INV-GEN-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL-2
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
    3 ID-NUM-A   (A6)
  2 WIDOW-FLAG
  2 PROCESS-IND
  2 INTRO-FLAG
  2 CHIEF-OF-STAFF-IND
* 2 INTERNET-ADDR         /* TT1
  2 SPS-CSRS-IND
  2 SPS-ANN-RET-ELIG-DT
  2 SPS-GOVT-ANNUITY
  2 RESTRICT-MBR-SW
  2 RANK
  2 NAME
  2 NAME-SP
*  2 ZIP-CODE                                           /* JT1 start
*  2 REDEFINE ZIP-CODE
*   3 ZIP-CODE-5       (N5)
*   3 ZIP-CODE-4       (N4)
  2 MEMBER-CONTACT-ID                                    /* JT1 end
  2 BIRTH-CERT-CODE
*  2 DATE-OF-BIRTH                    /* TT1
*  2 REDEFINE DATE-OF-BIRTH           /* TT1
*   3 DOB-YYYY (N4)                   /* TT1
*   3 DOB-MM (N2)                     /* TT1
*   3 DOB-DD (N2)                     /* TT1
*  2 DATE-OF-BIRTH-SP                 /* TT1
  2 STATUS-CODE-SP
  2 FORMER-SPOUSE-STATUS
  2 DATE-OF-DEATH
  2 X-LOAN-AMOUNT    (9)
  2 X-LOAN-AMOUNT-2  (9)
  2 MILITARY-SERVICE
  2 MILITARY-STATUS
  2 SSBP-PCT        (N2)
  2 O-E-DATE
  2 VSI-END-DT
  2 VSI-AMT
  2 RSV-NG-20-YR-CERT-LTR-DATE
  2 ACTIVE-DUTY-BASE-DATE
  2 DATE-RETIRED
  2 BASE-PAY-EF-DATE
  2 RSFPP-AMT
  2 CIV-SV-SBP-AMT
  2 DISABILITY-PCT     (P1.2)
  2 VA-DISABILITY-PCT  (P1.2)
* 2 ALT-LP-IND               /* PM3
* 2 ALT-LP-AMT               /* PM3
* 2 ALT-LP-IND-2             /* PM3
* 2 ALT-LP-AMT-2             /* PM3
  2 SOC-SEC-PIA
  2 PAD-CODE
  2 NUMBER-MEMBERSHIPS
  2 ISSUE-DATE    (9)
  2 STATUS        (9)
  2 PAY-STATUS    (9)
  2 FACE-AMOUNT   (9)
  2 EXPIRY-DAY    (9)
  2 MODE          (9)
  2 NUM-MBRSHIPS  (9)
  2 CONV-DURATION (9)
*
1 FR-V VIEW OF A-FIRS
  2 DEPN-DATE-OF-BIRTH (14)
  2 DEPN-STATUS-CD     (14)
  2 DEPN-COLL-TYPE      (14)
  2 DEPN-COLL-FUND      (P7/14)
  2 DEPN-COLL-FROM-YYYY (14)
  2 DEPN-COLL-THRU-YYYY (14)
  2 DEPN-COLL-ANN-COST  (14)
*
1 BENE-F-V VIEW OF A-BENE-FINAL         /* RE2 START
  2 ID-NUMBER                  (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-1ST    (N1)
    3 ID-NUMBER-LAST-5 (N5)
  2 NOTE-DATE          (N8/60)
  2 REDEFINE NOTE-DATE
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT      (N6/7)
      4 BENE-SPLIT-IND   (N1/7)
      4 BENE-LST-UPD     (N8)
      4 BENE-STRT-PEND   (N8)
      4 BENE-FOL-UP-IND  (N1)
      4 BENE-OWN-TRNSF-DT (N8)
      4 BENE-SUC-OWN      (N1)
      4 BENE-NEW-LAYOUT   (N1)
      4 BENE-FIL1         (A4)             /* RE2 END
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER                  (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-1ST    (N1)
    3 ID-NUMBER-LAST-5 (N5)
  2 NOTE-DATE          (N8/60)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 NT-OLDEST-MBR-CN             (N6)
    3 NT-OLDEST-MBR-AGE            (N3)
    3 NT-YOUNGEST-MBR-CN           (N6)
    3 NT-YOUNGEST-MBR-AGE          (N3)
    3 NT-OLDEST-WID-CN             (N6)
    3 NT-OLDEST-WID-AGE            (N3)
    3 NT-YOUNGEST-WID-CN           (N6)
    3 NT-YOUNGEST-WID-AGE          (N3)
    3 NT-NUM-PEBD-GTR-19860801     (N5)
    3 NT-NUM-PEBD-GTR-19800908     (N5)
    3 NT-NUM-MIL-DISAB-100-PCT     (N5)
    3 NT-NUM-VA-DISAB-100-PCT      (N5)
    3 NT-NUM-WID-MIL-DISAB-100-PCT (N5)
    3 NT-NUM-WID-VA-DISAB-100-PCT  (N5)
    3 NT-LONGEST-CN                (N6)
    3 NT-LONGEST-YEARS             (N3)
    3 NT-LONGEST-WID-CN            (N6)
    3 NT-LONGEST-WID-YEARS         (N3)
    3 NT-ZIP-ZERO                  (N6)
    3 NT-ZIP-5-DIGIT               (N6)
    3 NT-ZIP-9-DIGIT               (N6)
    3 NT-WID-ZIP-ZERO              (N6)
    3 NT-WID-ZIP-5-DIGIT           (N6)
    3 NT-WID-ZIP-9-DIGIT           (N6)
    3 NT-NUM-RSFPP                 (N5)
    3 NT-RET-B4-1957-1-1           (N5)
    3 NT-RETIREES-OVER-62          (N5)
    3 NT-RETIREES-UNDER-62         (N5)
    3 NT-NUM-ACT-BY-MIL-ID         (P5/20)
    3 NT-NUM-RET-BY-MIL-ID         (P5/20)
    3 NT-NUM-CIV-BY-MIL-ID         (P5/20)
    3 NT-NUM-20-YR-LTR             (N5)
    3 NT-NUM-DOB-PRIOR-1918        (N5)
    3 NT-NUM-SSBP                  (N5)
    3 NT-NUM-O-E-DATE              (N5)
    3 NT-NUM-MODES-M-G             (N5)
    3 NT-NUM-MODES-E               (N5)
    3 NT-NUM-MODES-Q               (N5)
    3 NT-NUM-MODES-S               (N5)
    3 NT-NUM-MODES-A               (N5)
    3 NT-NUM-SAV-INV               (N5)
    3 NT-NUM-SP-COD                (N5/19)
    3 NT-NUM-SPS-CSRS              (N5)
    3 NT-NUM-FIN-BENES             (N5)
    3 NT-NUM-FIN-BENES-POLS        (N5)
    3 NT-NUM-TICKERS               (N5)
    3 NT-NUM-CAP-MBRS              (N5)
    3 NT-LCYC-MBRS             (N5/5)
    3 NT-LCYC-PLANS            (N5/5)
    3 NT-LCYC-UNITS            (N5/5)   /* UNUSED A/O 4/1/2000
    3 NT-LCYC-FAS              (N5/5)
    3 NT-LCYC-PIA              (N5/5)
    3 NT-LCYC-ENLISTED         (N5/5)
    3 NT-LCYC-BIRTH-CERT-CDS   (N5/5)
    3 NT-LCYC-1ST-2-YRS        (N5/5)
    3 NT-LCYC-ALIVE-SPS        (N5/5)
    3 NT-LCYC-DEAD-SPS         (N5/5)
    3 NT-LCYC-NO-SPS           (N5/5)
    3 NT-LCYC-FMR-SPS          (N5/5)
    3 NT-LCYC-RESTRICTED       (N5/5)
    3 NT-LCYC-ALL-PD-UP        (N5/5)
    3 NT-LCYC-PARTIAL-PDUP     (N5/5)
    3 NT-LCYC-HAS-LOANS        (N5/5)
    3 NT-LCYC-MBRS-W-HANDI     (N5/5)
    3 NT-LCYC-DEPNS-18         (N5/5)
    3 NT-LCYC-DEPNS-18-21      (N5/5)
    3 NT-LCYC-DEPNS-22-23      (N5/5)
    3 NT-LCYC-DEPNS-OVR-23     (N5/5)
    3 NT-LCYC-AVG-AGE          (N3/5)
    3 NT-LCYC-INTERNET-ADDR    (N5/5)
    3 NT-LCYC-NU-MBR-POLS      (N5/5)
    3 NT-LCYC-ADDL-POLS        (N5/5)
    3 NT-LCYC-EDU-INFO         (N5/5)
    3 NT-LCYC-MBRS-W-DECEASED  (N5/5)
    3 NT-LCYC-ACT-MBRS         (N5/5)
    3 NT-LCYC-CURR-PLANS-OCC   (7)
      4 NT-LCYC-CURR-PLANS       (N5/5)
*
1 #RPT-LABEL    (A40)
1 #CNT          (N5)
1 #CN-WN        (A8)
1 #EXT-REC         (A76)
1 REDEFINE #EXT-REC
  2 #SEQ-AREA       (A36)
  2 REDEFINE #SEQ-AREA
    3 #SEQ-TYPE    (N2)
    3 #SEQ-REMAIN
      4 #SEQ-CN      (A8)
      4 #SEQ-FILL2   (A1)
      4 #SEQ-NAME    (A25)
  2 #EXT-REMAIN    (A40)
*
1 #MBR-W-HANDI  (L)
1 #MBR-W-DECEASED  (L)
1 #EDU-INFO     (L)
1 #ISN          (P8)
1 #FOUND-REC    (L)
1 #FIRST-POL    (L)
1 #CYC-X        (I2)
1 #I1           (I2)
1 #I2           (I2)
1 #CNT-3        (P5)
1 #INT-AMT      (P9.2)
1 #TOT-INT-AMT  (P9.2/2)
1 #CNT-FIN      (P5)
1 #CNT-FIN-POLS (P5)
1 #CNT-TICKERS  (P5)
1 #CNT-CAP-MBRS (P5)
1 #NUM-SP-CAT   (P5)
1 #NUM-PDUP     (P5)
1 #NUM-PP       (P5)
*
1 #TODAY        (N8)
1 REDEFINE #TODAY
  2 #TODAY-YYYY  (N4)
  2 #TODAY-MM    (N2)
  2 #TODAY-DD    (N2)
1 #DT-2-MON-AGO  (N8)
*
1 #OLDEST-CN     (N6)
1 #OLDEST-DOB    (N8) INIT <99999999>
1 REDEFINE #OLDEST-DOB
  2 #OLDEST-DOB-YYYY (N4)
  2 #OLDEST-DOB-MM   (N2)
  2 #OLDEST-DOB-DD   (N2)
1 #OLDEST-AGE       (N4)
1 #YOUNGEST-CN      (N6)
1 #YOUNGEST-DOB     (N8)
1 REDEFINE #YOUNGEST-DOB
  2 #YOUNGEST-DOB-YYYY (N4)
  2 #YOUNGEST-DOB-MM   (N2)
  2 #YOUNGEST-DOB-DD   (N2)
1 #YOUNGEST-AGE       (N4)
*
1 #LONGEST-CN          (N6)
1 #LONGEST-ISS-DT      (N8) INIT <99999999>
1 REDEFINE #LONGEST-ISS-DT
  2 #LONGEST-ISS-DT-YYYY (N4)
  2 #LONGEST-ISS-DT-MM   (N2)
  2 #LONGEST-ISS-DT-DD   (N2)
1 #LONGEST-YEARS        (N4)
*
1 #OLDEST-WID-CN        (N6)
1 #OLDEST-WID-DOB       (N8) INIT <99999999>
1 REDEFINE #OLDEST-WID-DOB
  2 #OLDEST-WID-DOB-YYYY (N4)
  2 #OLDEST-WID-DOB-MM   (N2)
  2 #OLDEST-WID-DOB-DD   (N2)
1 #OLDEST-WID-AGE       (N4)
1 #YOUNGEST-WID-CN      (N6)
1 #YOUNGEST-WID-DOB     (N8)
1 REDEFINE #YOUNGEST-WID-DOB
  2 #YOUNGEST-WID-DOB-YYYY (N4)
  2 #YOUNGEST-WID-DOB-MM   (N2)
  2 #YOUNGEST-WID-DOB-DD   (N2)
1 #YOUNGEST-WID-AGE       (N4)
*
1 #LONGEST-WID-CN          (N6)
1 #LONGEST-WID-MBR-DOD     (N8) INIT <99999999>
1 REDEFINE #LONGEST-WID-MBR-DOD
  2 #LONGEST-WID-MBR-DOD-YYYY (N4)
  2 #LONGEST-WID-MBR-DOD-MM   (N2)
  2 #LONGEST-WID-MBR-DOD-DD   (N2)
1 #LONGEST-WID-YEARS         (N4)
*
1 #NUM-NO-SPS              (N5)
1 #AMT-DISP                (A10)
1 #NUM-PEBD-GTR-19860801   (N5)
1 #NUM-PEBD-GTR-19800908   (N5)
1 #NUM-MIL-DISAB-100-PCT   (N5)
1 #NUM-VA-DISAB-100-PCT    (N5)
1 #NUM-WID-MIL-DISAB-100-PCT (N5)
1 #NUM-WID-VA-DISAB-100-PCT  (N5)
1 #ZIP-ZERO           (N6)
1 #ZIP-5-DIGIT        (N6)
1 #ZIP-9-DIGIT        (N6)
1 #WID-ZIP-ZERO       (N6)
1 #WID-ZIP-5-DIGIT    (N6)
1 #WID-ZIP-9-DIGIT    (N6)
1 #NUM-RSFPP          (N5)
1 #RET-B4-1957-1-1    (N5)
1 #RETIREES-OVER-62   (N5)
1 #RETIREES-UNDER-62  (N5)
1 #NUM-ACT-BY-MIL-ID  (P5/20)
1 #NUM-RET-BY-MIL-ID  (P5/20)
1 #NUM-CIV-BY-MIL-ID  (P5/20)
1 #NUM-20-YR-LTR      (N5)
1 #NUM-DOB-PRIOR-1918 (N5)
1 #NUM-SSBP           (N5)
1 #NUM-O-E-DATE       (N5)
1 #NUM-MODES-M-G      (N5)
1 #NUM-MODES-E        (N5)
1 #NUM-MODES-Q        (N5)
1 #NUM-MODES-S        (N5)
1 #NUM-MODES-A        (N5)
1 #NUM-SAV-INV        (N5)
1 #NUM-SP-COD         (N5/19)
1 #NUM-SPS-CSRS       (N5)
1 #NUM-PLANS          (N3)
*
1 #LCYC-MBRS          (N5/5)
1 #LCYC-PLANS         (N5/5)
* 1 #LCYC-UNITS         (N5/5)    /* UNUSED A/O 4/1/2000
1 #LCYC-FAS           (N5/5)
1 #LCYC-PIA           (N5/5)
1 #LCYC-ENLISTED       (N5/5)
1 #LCYC-BIRTH-CERT-CDS (N5/5)
1 #LCYC-1ST-2-YRS      (N5/5)
1 #LCYC-ALIVE-SPS      (N5/5)
1 #LCYC-DEAD-SPS       (N5/5)
1 #LCYC-NO-SPS         (N5/5)
1 #LCYC-FMR-SPS        (N5/5)
1 #LCYC-RESTRICTED     (N5/5)
1 #LCYC-ALL-PD-UP      (N5/5)
1 #LCYC-PARTIAL-PDUP   (N5/5)
1 #LCYC-HAS-LOANS      (N5/5)
1 #LCYC-MBRS-W-HANDI   (N5/5)
1 #LCYC-DEPNS-18       (N5/5)
1 #LCYC-DEPNS-18-21    (N5/5)
1 #LCYC-DEPNS-22-23    (N5/5)
1 #LCYC-DEPNS-OVR-23   (N5/5)
1 #LCYC-HASH-AGE       (N9/5)
1 #LCYC-INTERNET-ADDR  (N5/5)
1 #LCYC-NU-MBR-POLS    (N5/5)
1 #LCYC-ADDL-POLS      (N5/5)
1 #LCYC-EDU-INFO       (N5/5)
1 #LCYC-MBRS-W-DECEASED  (N5/5)
1 #LCYC-ACT-MBRS         (N5/5)
1 #LCYC-CURR-PLANS-OCC   (7)
  2 #LCYC-CURR-PLANS       (N5/5)
*
1 #HOLD-AMT      (P9.2)
1 #AGE           (P3)
1 EMAIL-ADDRESS  (A60)               /* TT1
1 #DATE-OF-BIRTH          (N8.0)     /* TT1
1 REDEFINE #DATE-OF-BIRTH            /* TT1
  2 #DOB-YYYY (N4)                     /* TT1
  2 #DOB-MM (N2)                       /* TT1
  2 #DOB-DD (N2)                       /* TT1
1 #DATE-OF-BIRTH-SP       (N8.0)     /* TT1
1 #SPOUSE-SEX             (A1)       /* TT1
*
END-DEFINE
* * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'MISC EXCEPTIONS AND COUNTS'
  5X 'DATE:' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'MISC EXCEPTIONS (Only 1st 100 for some categories) '
  #RPT-LABEL 5X 'DATE:' *DAT4U 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #TODAY
SUBTRACT 2 FROM #TODAY-MM
IF #TODAY-MM LT 1
  ADD 12 TO #TODAY-MM
  SUBTRACT 1 FROM #TODAY-YYYY
END-IF
MOVE #TODAY TO #DT-2-MON-AGO
*
MOVE *DATN TO #TODAY
IF #TODAY-DD GE 25 /*if running near end-of-month assume 1st of next mon
  MOVE 1 TO #TODAY-DD     /* for age calcs
  ADD 1 TO #TODAY-MM
  IF #TODAY-MM GT 12
    MOVE 1 TO #TODAY-MM
    ADD 1 TO #TODAY-YYYY
  END-IF
END-IF
*
HISTOGRAM TK-V SYMBOL
  ADD 1 TO #CNT-TICKERS
* DISPLAY SYMBOL #CNT-TICKERS
END-HISTOGRAM
*
READ IN-V PHYSICAL
  ADD 1 TO #LCYC-MBRS (1)
  IF IN-V.DOB-YYYY NE 0
    COMPUTE #AGE = #TODAY-YYYY - IN-V.DOB-YYYY
    ADD #AGE TO #LCYC-HASH-AGE (1)
  END-IF
*
  FIND (1) FI-V WITH ID-NUMBER = IN-V.ID-NUMBER
    IF  (FI-V.C*INV-GEN-TBL NE 0)
        OR (FI-V.C*INV-MUTUAL-STOCKS-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL-2 NE 0)
      ADD 1 TO #LCYC-FAS (1)
    END-IF
  END-FIND
END-READ
*
READ ST-V PHYSICAL
  IF  (ID-NUMBER LE 1900)
*  OR (PROCESS-IND = 'S')                       /* RE1
    ESCAPE TOP
  END-IF
*
  IF ST-V.PROCESS-IND = 'C'
    ESCAPE TOP
  END-IF
*
  IF INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    ESCAPE TOP
  END-IF
*
  RESET PDA-CONTACT                                   /* TT1
  P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID              /* TT1
  CALLNAT 'CONTS001' PDA-CONTACT  #DATE-OF-BIRTH-SP #SPOUSE-SEX   /* TT1
  #DATE-OF-BIRTH    := P-DATE-OF-BIRTH                /* TT1
*
  IF WIDOW-FLAG = 'Y'
    COMPRESS 'WN' ID-NUMBER INTO #CN-WN LEAVING NO
    MOVE 5 TO #CYC-X
  ELSE
    COMPRESS 'CN' ID-NUMBER INTO #CN-WN LEAVING NO
    IF MILITARY-STATUS = 'A'
      MOVE 2 TO #CYC-X
    ELSE
      IF #DATE-OF-BIRTH GE #TODAY - 600000          /* TT1
        MOVE 3 TO #CYC-X
      ELSE
        MOVE 4 TO #CYC-X
      END-IF
    END-IF
  END-IF
*
  ADD 1 TO #LCYC-MBRS (#CYC-X)
*
  IF WIDOW-FLAG = 'Y'     /* BELOW PROCESSING IS FOR WIDOWS ONLY
    IF STATUS-CODE-SP = 'D'    /* DEAD
      ESCAPE TOP
    END-IF
*
    IF DISABILITY-PCT = 1.00
      ADD 1 TO #NUM-WID-MIL-DISAB-100-PCT
      IF #NUM-WID-MIL-DISAB-100-PCT LT 100
        RESET #EXT-REC
        MOVE 04 TO #SEQ-TYPE
        MOVE #CN-WN TO #SEQ-CN
        MOVE NAME-SP TO #SEQ-NAME
        WRITE WORK 2 #EXT-REC
      END-IF
    END-IF
*
    IF VA-DISABILITY-PCT = 1.00
      ADD 1 TO #NUM-WID-VA-DISAB-100-PCT
      IF #NUM-WID-VA-DISAB-100-PCT LT 100
        RESET #EXT-REC
        MOVE 05 TO #SEQ-TYPE
        MOVE #CN-WN TO #SEQ-CN
        MOVE NAME-SP TO #SEQ-NAME
        WRITE WORK 2 #EXT-REC
      END-IF
    END-IF
*
*    IF ST-V.ID-NUMBER LT 600000                   /* RE1
    IF #DATE-OF-BIRTH-SP GT #YOUNGEST-WID-DOB          /* TT1
      MOVE ID-NUMBER        TO #YOUNGEST-WID-CN         
      MOVE #DATE-OF-BIRTH-SP TO #YOUNGEST-WID-DOB      /* TT1
    END-IF
*
    IF   (#DATE-OF-BIRTH-SP NE 0)                       /* TT1
        AND (#DATE-OF-BIRTH-SP LT #OLDEST-WID-DOB)      /* TT1
      MOVE ID-NUMBER        TO #OLDEST-WID-CN
      MOVE #DATE-OF-BIRTH-SP TO #OLDEST-WID-DOB         /* TT1
    END-IF
*
    IF   (DATE-OF-DEATH NE 0)
        AND (DATE-OF-DEATH LT #LONGEST-WID-MBR-DOD)
      MOVE ID-NUMBER     TO #LONGEST-WID-CN
      MOVE DATE-OF-DEATH TO #LONGEST-WID-MBR-DOD
    END-IF
*
    RESET #PDA-ADDRESS                                               /* JT1 start
    MOVE 'P' TO #ADD-TYPE
    #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
    CALLNAT 'ADDN0001' #PDA-ADDRESS
*
    IF #ADD-ZIP-CODE = 0
      ADD 1 TO #WID-ZIP-ZERO
    ELSE
      IF #ADD-ZIP4 = 0
        ADD 1 TO #WID-ZIP-5-DIGIT
      ELSE
        ADD 1 TO #WID-ZIP-9-DIGIT
      END-IF
    END-IF
*
*       IF ZIP-CODE = 0
*         ADD 1 TO #WID-ZIP-ZERO
*       ELSE
*         IF ZIP-CODE-4 = 0
*           ADD 1 TO #WID-ZIP-5-DIGIT
*         ELSE
*           ADD 1 TO #WID-ZIP-9-DIGIT
*         END-IF
*       END-IF                                                        /* JT1 end
*    END-IF                                        /* RE1
*
    IF SPS-CSRS-IND NE ' '
      ADD 1 TO #NUM-SPS-CSRS
      RESET #EXT-REC
      MOVE 14 TO #SEQ-TYPE
      COMPRESS SPS-CSRS-IND ID-NUMBER INTO #SEQ-CN
      MOVE NAME-SP TO #SEQ-NAME
      COMPRESS SPS-CSRS-IND SPS-ANN-RET-ELIG-DT SPS-GOVT-ANNUITY
        INTO #EXT-REMAIN
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
*         BELOW PROCESSING IS FOR MEMBERS AND WIDOWS
*
  IF #DOB-YYYY NE 0                                   /* TT1
    COMPUTE #AGE = #TODAY-YYYY - #DOB-YYYY            /* TT1
    ADD #AGE TO #LCYC-HASH-AGE (#CYC-X)
  END-IF
*
  RESET #NUM-PDUP #NUM-PP #NUM-PLANS
  FOR #I1 1 TO 9
    IF STATUS (#I1) = 'D'
      ADD 1 TO #NUM-PLANS
      ADD 1 TO #LCYC-PLANS (#CYC-X)
*
*     IF PAY-STATUS (#I1) = '45'    /* REMOVED 4/1/2000 - EXCEEDS 100K
*       IF ISSUE-DATE (#I1) LT 19941201
*         MOVE NUM-MBRSHIPS (#I1) TO #UNITS
*       ELSE
*         MOVE EXPIRY-DAY (#I1) TO #UNITS
*       END-IF
*     ELSE
*       COMPUTE #UNITS = FACE-AMOUNT (#I1) / 5000
*     END-IF
*     ADD #UNITS TO #LCYC-UNITS (#CYC-X)
*
      IF PAY-STATUS (#I1) GE '42'
        ADD 1 TO #NUM-PDUP
      ELSE
        ADD 1 TO #NUM-PP
      END-IF
    END-IF
*
    IF CONV-DURATION (#I1) = 999
      ADD 1 TO #CNT-CAP-MBRS
      IF #CNT-CAP-MBRS LT 100
        RESET #EXT-REC
        MOVE 16 TO #SEQ-TYPE
        MOVE #CN-WN TO #SEQ-CN
        MOVE NAME TO #SEQ-NAME
        COMPRESS 'Loan-amt' X-LOAN-AMOUNT (#I1) INTO #EXT-REMAIN
        WRITE WORK 2 #EXT-REC
      END-IF
    END-IF
  END-FOR
*
  IF #NUM-PLANS GT 0
    IF #NUM-PLANS GT 7
      MOVE 7 TO #NUM-PLANS
    END-IF
*
    ADD 1 TO #LCYC-CURR-PLANS (#NUM-PLANS, #CYC-X)
  END-IF
*
  IF (#NUM-PDUP NE 0) AND (#NUM-PP = 0)
    ADD 1 TO #LCYC-ALL-PD-UP (#CYC-X)
  ELSE
    IF #NUM-PDUP NE 0
      ADD 1 TO #LCYC-PARTIAL-PDUP (#CYC-X)
    END-IF
  END-IF
*
  FIND (1) FI-V WITH ID-NUMBER = ST-V.ID-NUMBER
    IF  (FI-V.C*INV-GEN-TBL NE 0)
        OR (FI-V.C*INV-MUTUAL-STOCKS-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL-2 NE 0)
      ADD 1 TO #NUM-SAV-INV
      ADD 1 TO #LCYC-FAS (#CYC-X)
*
      IF #CYC-X = 5
        WRITE 'widow with FAS' ID-NUMBER
      END-IF
*
      IF #NUM-SAV-INV LT 100
        RESET #EXT-REC
        MOVE 13 TO #SEQ-TYPE
        MOVE #CN-WN TO #SEQ-CN
        MOVE NAME-SP TO #SEQ-NAME
        WRITE WORK 2 #EXT-REC
      END-IF
    END-IF
  END-FIND
*
  IF SOC-SEC-PIA NE 0
    ADD 1 TO #LCYC-PIA (#CYC-X)
  END-IF
*
  IF  (RANK = MASK ('S'))
      OR (RANK = MASK ('P'))
      OR (RANK = 'CSM' OR = '1SG' OR = 'MSG' OR = 'CPL' OR = 'RCT')
      OR ((RANK GE 'E1') AND (RANK LE 'E9'))
    ADD 1 TO #LCYC-ENLISTED (#CYC-X)
  END-IF
*
  IF NOT BIRTH-CERT-CODE = ' ' OR = 'N'
    ADD 1 TO #LCYC-BIRTH-CERT-CDS (#CYC-X)
  END-IF
*
  IF ISSUE-DATE (1) GE #TODAY - 20000
    ADD 1 TO #LCYC-1ST-2-YRS (#CYC-X)
  END-IF
*
  IF STATUS-CODE-SP = 'D' OR = 'F' OR  = ' '
    ADD 1 TO #LCYC-NO-SPS (#CYC-X)
  ELSE
    ADD 1 TO #LCYC-ALIVE-SPS (#CYC-X)
  END-IF
*
  IF STATUS-CODE-SP = 'D'
    ADD 1 TO #LCYC-DEAD-SPS (#CYC-X)
  END-IF
*
  IF  (STATUS-CODE-SP = 'F')
      OR (FORMER-SPOUSE-STATUS = 'F')
    ADD 1 TO #LCYC-FMR-SPS (#CYC-X)
  END-IF
*
  IF RESTRICT-MBR-SW = 'Y'
    ADD 1 TO #LCYC-RESTRICTED (#CYC-X)
  END-IF
*
  FOR #I1 1 TO 9
    IF  (X-LOAN-AMOUNT   (#I1) NE 0)
        OR (X-LOAN-AMOUNT-2 (#I1) NE 0)
      ADD 1 TO #LCYC-HAS-LOANS (#CYC-X)
*
      IF #CYC-X = 5
        WRITE 'widow with LOAN' ID-NUMBER
      END-IF
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  RESET EMAIL-ADDRESS                                         /* TT1
  EMAIL-ADDRESS := F-PREF-EMAIL(<ST-V.MEMBER-CONTACT-ID>)     /* TT1
  IF EMAIL-ADDRESS NE ' '                                     /* TT1
    ADD 1 TO #LCYC-INTERNET-ADDR (#CYC-X)
  END-IF
*
  RESET #FOUND-REC
  IF WIDOW-FLAG = 'Y'
    FIND (1) FR-V WITH WIDOW-KEY = ID-NUM-A
      MOVE TRUE TO #FOUND-REC
    END-FIND
  ELSE
    FIND (1) FR-V WITH MEMBER-KEY = ID-NUM-A
      MOVE TRUE TO #FOUND-REC
    END-FIND
  END-IF
*
  IF #FOUND-REC
    RESET #MBR-W-HANDI #EDU-INFO #MBR-W-DECEASED
    FOR #I1 1 TO 14
      IF DEPN-STATUS-CD (#I1) = 'I' OR = 'J' OR = 'K' OR = 'L'
        MOVE TRUE TO #MBR-W-HANDI
      END-IF
*
      IF DEPN-STATUS-CD (#I1) = 'D'
        MOVE TRUE TO #MBR-W-DECEASED
      ELSE
        IF DEPN-DATE-OF-BIRTH (#I1) NE 0
          IF *DATN - DEPN-DATE-OF-BIRTH (#I1) LT 180000
            ADD 1 TO #LCYC-DEPNS-18 (#CYC-X)   /* 0 thru age 17
          ELSE
            IF *DATN - DEPN-DATE-OF-BIRTH (#I1) LT 220000
              ADD 1 TO #LCYC-DEPNS-18-21 (#CYC-X) /* 18 thru age 21
            ELSE
              IF *DATN - DEPN-DATE-OF-BIRTH (#I1) LT 240000
                ADD 1 TO #LCYC-DEPNS-22-23 (#CYC-X)  /* 22 thru age 23
              ELSE
                ADD 1 TO #LCYC-DEPNS-OVR-23 (#CYC-X)  /* 24 thru ???
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
*
      IF  (DEPN-COLL-TYPE  (#I1) NE ' ')
          OR (DEPN-COLL-FUND  (#I1) NE 0)
          OR (DEPN-COLL-FROM-YYYY (#I1) NE 0)
          OR (DEPN-COLL-THRU-YYYY (#I1) NE 0)
          OR (DEPN-COLL-ANN-COST  (#I1) NE 0)
        MOVE TRUE TO #EDU-INFO
      END-IF
    END-FOR
*
    IF STATUS-CODE-SP = 'H'
      MOVE TRUE TO #MBR-W-HANDI
    END-IF
*
    IF #MBR-W-DECEASED
      ADD 1 TO #LCYC-MBRS-W-DECEASED (#CYC-X)
    END-IF
*
    IF #MBR-W-HANDI
      ADD 1 TO #LCYC-MBRS-W-HANDI (#CYC-X)
    END-IF
*
    IF  #EDU-INFO
      ADD 1 TO #LCYC-EDU-INFO (#CYC-X)
*
      RESET #EXT-REC
      MOVE 15 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
*         BELOW PROCESSING IS FOR MEMBERS ONLY
*
  IF NUMBER-MEMBERSHIPS = 0   /* OK DRW               RE1
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #LCYC-ACT-MBRS (#CYC-X)
*
  IF RESTRICT-MBR-SW = 'Y'
    ADD 1 TO #NUM-SP-CAT
    RESET #EXT-REC
    MOVE 01 TO #SEQ-TYPE
    MOVE #CN-WN TO #SEQ-CN
    MOVE NAME TO #SEQ-NAME
    WRITE WORK 2 #EXT-REC
  END-IF
*
  RESET #PDA-ADDRESS                                               /* JT1 start
  MOVE 'P' TO #ADD-TYPE
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
*
  IF #ADD-ZIP-CODE = 0
    ADD 1 TO #ZIP-ZERO
  ELSE
    IF #ADD-ZIP4 = 0
      ADD 1 TO #ZIP-5-DIGIT
    ELSE
      ADD 1 TO #ZIP-9-DIGIT
    END-IF
  END-IF
*
*  IF ZIP-CODE = 0
*     ADD 1 TO #ZIP-ZERO
*   ELSE
*     IF ZIP-CODE-4 = 0
*       ADD 1 TO #ZIP-5-DIGIT
*     ELSE
*       ADD 1 TO #ZIP-9-DIGIT
*     END-IF
*   END-IF                                                            /* JT1 end
*
  IF MILITARY-STATUS = 'R'
    IF (BASE-PAY-EF-DATE GE 19860801)
        OR ((BASE-PAY-EF-DATE = 0)
        AND (ACTIVE-DUTY-BASE-DATE GE 19860801))
      ADD 1 TO #NUM-PEBD-GTR-19860801
      RESET #EXT-REC
      MOVE 02 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      COMPRESS 'BASD=' ACTIVE-DUTY-BASE-DATE 'PEBD=' BASE-PAY-EF-DATE
        INTO #EXT-REMAIN
      WRITE WORK 2 #EXT-REC
    END-IF
*
    IF (BASE-PAY-EF-DATE GE 19800908)
        OR ((BASE-PAY-EF-DATE = 0)
        AND (ACTIVE-DUTY-BASE-DATE GE 19800908))
      ADD 1 TO #NUM-PEBD-GTR-19800908
      RESET #EXT-REC
      MOVE 03 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      COMPRESS 'BASD=' ACTIVE-DUTY-BASE-DATE 'PEBD=' BASE-PAY-EF-DATE
        INTO #EXT-REMAIN
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF DISABILITY-PCT = 1.00
    ADD 1 TO #NUM-MIL-DISAB-100-PCT
    IF #NUM-MIL-DISAB-100-PCT LT 100
      RESET #EXT-REC
      MOVE 04 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF VA-DISABILITY-PCT = 1.00
    ADD 1 TO #NUM-VA-DISAB-100-PCT
    IF #NUM-VA-DISAB-100-PCT LT 100
      RESET #EXT-REC
      MOVE 05 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
*  IF NOT (ALT-LP-IND-2 = ' ' OR = '0')                            /* PM3
*    RESET #EXT-REC                                                /* PM3
*    MOVE 06 TO #SEQ-TYPE                                          /* PM3
*    COMPRESS ALT-LP-IND-2 ID-NUMBER INTO #SEQ-CN                  /* PM3
*    MOVE NAME TO #SEQ-NAME                                        /* PM3
*    MOVE EDITED ALT-LP-AMT-2 (EM=ZZZ.99) TO #AMT-DISP             /* PM3
*    COMPRESS 'ALP-CODE=' ALT-LP-IND-2 ' ALP-AMT-2 =$ ' #AMT-DISP  /* PM3
*                           INTO #EXT-REMAIN                       /* PM3
*   WRITE WORK 2 #EXT-REC     /* removed 11/2000 as per elb
*  END-IF                                                          /* PM3
*
*  IF NOT (ALT-LP-IND = ' ' OR = '0')                              /* PM3
*   AND (CONV-DURATION (1) NE 999)   /* cap                        /* PM3
*    RESET #EXT-REC                                                /* PM3
*    MOVE 06 TO #SEQ-TYPE                                          /* PM3
*    COMPRESS ALT-LP-IND ID-NUMBER INTO #SEQ-CN                    /* PM3
*    MOVE NAME TO #SEQ-NAME                                        /* PM3
*    MOVE EDITED ALT-LP-AMT (EM=ZZZZ.99) TO #AMT-DISP              /* PM3
*    COMPRESS 'ALP-CODE=' ALT-LP-IND ' ALP-AMT =$ ' #AMT-DISP      /* PM3
*                           INTO #EXT-REMAIN                       /* PM3
*   WRITE WORK 2 #EXT-REC    /* removed 11/2000 as per elb
*  END-IF                                                          /* PM3
*
  IF PAD-CODE NE ' '
    RESET #EXT-REC
    MOVE 07 TO #SEQ-TYPE
    COMPRESS PAD-CODE ID-NUMBER INTO #SEQ-CN
    MOVE NAME TO #SEQ-NAME
    COMPRESS 'PAD-CODE=' PAD-CODE INTO #EXT-REMAIN
*   WRITE WORK 2 #EXT-REC   /* removed 11/2000 as per elb
  END-IF
*
  IF CHIEF-OF-STAFF-IND NE ' '
    RESET #I1
    DECIDE ON FIRST CHIEF-OF-STAFF-IND
      VALUE 'A'    MOVE 1  TO #I1
      VALUE 'B'    MOVE 2  TO #I1
      VALUE 'D'    MOVE 3  TO #I1
      VALUE 'E'    MOVE 4  TO #I1
      VALUE 'F'    MOVE 5  TO #I1
      VALUE 'I'    MOVE 6  TO #I1
      VALUE 'K'    MOVE 7  TO #I1
      VALUE 'L'    MOVE 8  TO #I1
      VALUE 'P'    MOVE 9  TO #I1
      VALUE 'Q'    MOVE 10 TO #I1
      VALUE 'R'    MOVE 11 TO #I1
      VALUE 'S'    MOVE 12 TO #I1
      VALUE 'T'    MOVE 13 TO #I1
      VALUE 'U'    MOVE 14 TO #I1
      VALUE 'V'    MOVE 15 TO #I1
      VALUE 'X'    MOVE 16 TO #I1
      VALUE 'Y'    MOVE 17 TO #I1
      VALUE 'Z'    MOVE 18 TO #I1
      NONE         MOVE 19 TO #I1   /* UNKNOWN
    END-DECIDE
    ADD 1 TO #NUM-SP-COD (#I1)
*
    RESET #EXT-REC
    MOVE 08 TO #SEQ-TYPE
    COMPRESS CHIEF-OF-STAFF-IND ID-NUMBER INTO #SEQ-CN
    MOVE NAME TO #SEQ-NAME
*
    IF CHIEF-OF-STAFF-IND = 'C'
      COMPRESS 'SPC-CODE =' CHIEF-OF-STAFF-IND
        ' SPS-GOVT-ANNUITY = ' SPS-GOVT-ANNUITY INTO #EXT-REMAIN
    ELSE
      IF CHIEF-OF-STAFF-IND = 'V'
        COMPRESS 'CD =' CHIEF-OF-STAFF-IND
          'VSI:AMT=' VSI-AMT 'END-DT=' VSI-END-DT INTO #EXT-REMAIN
      ELSE
        IF CHIEF-OF-STAFF-IND = 'Z'
          COMPRESS 'SPC-CODE =' CHIEF-OF-STAFF-IND
            'CSSBP-AMT =' CIV-SV-SBP-AMT INTO #EXT-REMAIN
        ELSE
          COMPRESS 'SPECIAL-CODE =' CHIEF-OF-STAFF-IND INTO #EXT-REMAIN
        END-IF
      END-IF
    END-IF
    WRITE WORK 2 #EXT-REC
  END-IF
*
  IF STATUS-CODE-SP = 'N'
    ADD 1 TO #NUM-NO-SPS
    IF #NUM-NO-SPS LT 100
      RESET #EXT-REC
      MOVE 09 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF RSFPP-AMT NE 0
    ADD 1 TO #NUM-RSFPP
    IF #NUM-RSFPP LT 100
      RESET #EXT-REC
      MOVE 10 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF   (DATE-RETIRED LE 19570101)
      AND (DATE-RETIRED NE 0)
      AND (MILITARY-STATUS = 'R')
    ADD 1 TO #RET-B4-1957-1-1
  END-IF
*
  IF MILITARY-STATUS = 'R'
    IF (#DATE-OF-BIRTH + 620000) LE #TODAY           /* TT1
      ADD 1 TO #RETIREES-OVER-62
    ELSE
      ADD 1 TO #RETIREES-UNDER-62
    END-IF
  END-IF
*
  RESET #I1
  DECIDE ON FIRST MILITARY-SERVICE
    VALUE 'A'    MOVE 1  TO #I1
    VALUE 'AC'   MOVE 2  TO #I1
    VALUE 'ACR'  MOVE 3  TO #I1
    VALUE 'AG'   MOVE 4  TO #I1
    VALUE 'AGA'  MOVE 5  TO #I1
    VALUE 'ARA'  MOVE 5  TO #I1
    VALUE 'AR'   MOVE 6  TO #I1
    VALUE 'F'    MOVE 7  TO #I1
    VALUE 'FC'   MOVE 8  TO #I1
    VALUE 'FCR'  MOVE 9  TO #I1
    VALUE 'FG'   MOVE 10 TO #I1
    VALUE 'FGA'  MOVE 11 TO #I1
    VALUE 'FRA'  MOVE 11 TO #I1
    VALUE 'FR'   MOVE 12 TO #I1
    VALUE 'M'    MOVE 13 TO #I1
    VALUE 'MR'   MOVE 14 TO #I1
    VALUE 'MRA'  MOVE 14 TO #I1
    VALUE 'N'    MOVE 15 TO #I1
    VALUE 'NR'   MOVE 16 TO #I1
    VALUE 'NRA'  MOVE 16 TO #I1
    VALUE 'CG'   MOVE 17 TO #I1
    VALUE 'CGR'  MOVE 18 TO #I1
    VALUE 'CGA'  MOVE 18 TO #I1
    VALUE 'NOA'  MOVE 19 TO #I1
    VALUE 'PHS'  MOVE 20 TO #I1
    NONE         WRITE ID-NUMBER ' INVALID MILITARY SERVICE'
  END-DECIDE
*
  IF #I1 NE 0
    IF MILITARY-STATUS = 'A'
      ADD 1 TO #NUM-ACT-BY-MIL-ID (#I1)
    ELSE
      IF MILITARY-STATUS = 'R'
        ADD 1 TO #NUM-RET-BY-MIL-ID (#I1)
      ELSE
        ADD 1 TO #NUM-CIV-BY-MIL-ID (#I1)
      END-IF
    END-IF
  END-IF
*
  IF RSV-NG-20-YR-CERT-LTR-DATE NE 0
    ADD 1 TO #NUM-20-YR-LTR
    IF #NUM-20-YR-LTR LT 100
      RESET #EXT-REC
      MOVE 11 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF SSBP-PCT NE 0
      OR (O-E-DATE NE 0)
    IF SSBP-PCT NE 0
      ADD 1 TO #NUM-SSBP
    END-IF
*
    IF O-E-DATE NE 0
      ADD 1 TO #NUM-O-E-DATE
    END-IF
*
    IF #NUM-O-E-DATE LT 100
      RESET #EXT-REC
      MOVE 12 TO #SEQ-TYPE
      MOVE #CN-WN TO #SEQ-CN
      MOVE NAME TO #SEQ-NAME
      COMPRESS 'SSBP-% =' SSBP-PCT ' O-E-DATE =' O-E-DATE INTO #EXT-REMAIN
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF SPS-CSRS-IND NE ' '
    ADD 1 TO #NUM-SPS-CSRS
    IF #NUM-SPS-CSRS LT 100
      RESET #EXT-REC
      MOVE 14 TO #SEQ-TYPE
      COMPRESS SPS-CSRS-IND ID-NUMBER INTO #SEQ-CN
      MOVE NAME-SP TO #SEQ-NAME
      COMPRESS SPS-CSRS-IND SPS-ANN-RET-ELIG-DT SPS-GOVT-ANNUITY
        INTO #EXT-REMAIN
      WRITE WORK 2 #EXT-REC
    END-IF
  END-IF
*
  IF #DATE-OF-BIRTH LT 19180101                   /* TT1
    ADD 1 TO #NUM-DOB-PRIOR-1918
  END-IF
*
*  IF ST-V.ID-NUMBER LT 600000                    /* RE1
  IF #DATE-OF-BIRTH GT #YOUNGEST-DOB              /* TT1
    MOVE ID-NUMBER     TO #YOUNGEST-CN
    MOVE #DATE-OF-BIRTH TO #YOUNGEST-DOB           /* TT1
  END-IF
* 
  IF   (#DATE-OF-BIRTH NE 0)                       /* TT1
      AND (#DATE-OF-BIRTH LT #OLDEST-DOB)          /* TT1
    MOVE ID-NUMBER     TO #OLDEST-CN
    MOVE #DATE-OF-BIRTH TO #OLDEST-DOB             /* TT1
  END-IF
*  END-IF                                       /* RE1
*
  MOVE TRUE TO #FIRST-POL
  FOR #I1 1 TO 9
    IF ISSUE-DATE (#I1) = 0
      ESCAPE BOTTOM
    END-IF
*
*    IF ST-V.ID-NUMBER LT 600000                /* RE1
    IF ISSUE-DATE (#I1) LT #LONGEST-ISS-DT
      MOVE ID-NUMBER     TO #LONGEST-CN
      MOVE ISSUE-DATE (#I1) TO #LONGEST-ISS-DT
    END-IF
*    END-IF                                     /* RE1
*
    IF ISSUE-DATE (#I1) GE #DT-2-MON-AGO
      IF #I1 = 1
        ADD 1 TO #LCYC-NU-MBR-POLS (#CYC-X)
      ELSE
        ADD 1 TO #LCYC-ADDL-POLS (#CYC-X)
      END-IF
    END-IF
*
    IF PAY-STATUS (#I1) GE '40'
*     OR (PROCESS-IND = 'S')                 /* RE1
        OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)  /* RE1
      ESCAPE TOP
    END-IF
*
    IF #FIRST-POL
      RESET #FIRST-POL
*
      IF MODE (#I1) = 'G' OR = 'M' OR = '0'   /* DW1
        ADD 1 TO #NUM-MODES-M-G
      ELSE
        IF MODE (#I1) = 'A' OR = '1'      /* DW1
          ADD 1 TO #NUM-MODES-A
        ELSE
          IF MODE (#I1) = 'S' OR = '2'    /* DW1
            ADD 1 TO #NUM-MODES-S
          ELSE
            IF MODE (#I1) = 'Q' OR = '4'    /* DW1
              ADD 1 TO #NUM-MODES-Q
            ELSE
              IF MODE (#I1) = 'E'
                ADD 1 TO #NUM-MODES-E
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-FOR
END-READ
*
*          MEMBER YOUNGEST-OLDEST CALCS
*
COMPUTE #LONGEST-YEARS = #TODAY-YYYY - #LONGEST-ISS-DT-YYYY
IF #LONGEST-ISS-DT-MM GE #TODAY-MM
  SUBTRACT 1 FROM #LONGEST-YEARS
END-IF
*
COMPUTE #OLDEST-AGE = #TODAY-YYYY - #OLDEST-DOB-YYYY
IF #OLDEST-DOB-MM GE #TODAY-MM
  SUBTRACT 1 FROM #OLDEST-AGE
END-IF
*
COMPUTE #YOUNGEST-AGE = #TODAY-YYYY - #YOUNGEST-DOB-YYYY
IF #YOUNGEST-DOB-MM GE #TODAY-MM
  SUBTRACT 1 FROM #YOUNGEST-AGE
END-IF
*
*   WIDOW YOUNGEST-OLDEST CALCS
*
COMPUTE #LONGEST-WID-YEARS = #TODAY-YYYY - #LONGEST-WID-MBR-DOD-YYYY
IF #LONGEST-WID-MBR-DOD-MM GE #TODAY-MM
  SUBTRACT 1 FROM #LONGEST-WID-YEARS
END-IF
*
COMPUTE #OLDEST-WID-AGE = #TODAY-YYYY - #OLDEST-WID-DOB-YYYY
IF #OLDEST-WID-DOB-MM GE #TODAY-MM
  SUBTRACT 1 FROM #OLDEST-WID-AGE
END-IF
*
COMPUTE #YOUNGEST-WID-AGE = #TODAY-YYYY - #YOUNGEST-WID-DOB-YYYY
IF #YOUNGEST-WID-DOB-MM GE #TODAY-MM
  SUBTRACT 1 FROM #YOUNGEST-WID-AGE
END-IF
*
**READ NT-V BY ID-NUMBER FROM 400000     /* RE2 START
READ BENE-F-V BY ID-NUMBER
*  IF ID-NUMBER GT 599999
*   ESCAPE BOTTOM
*  END-IF                                /* RE2 END
*
  IF #CNT-FIN LT 99999  /*DW2
    ADD 1 TO #CNT-FIN
  END-IF
*
  FOR #I1 1 TO 6
    FOR #I2 1 TO 7
      IF BENE-ISS-DT (#I1, #I2) NE 0
        IF #CNT-FIN-POLS LT 99999  /*DW2
          ADD 1 TO #CNT-FIN-POLS
        END-IF
      END-IF
    END-FOR
  END-FOR
END-READ
*
*  UPDATE "NX" RECORD
*
MOVE TRUE TO #FOUND-REC
FIND (1) NT-V WITH ID-NUMBER = 800101
  IF NO RECORDS FOUND
    WRITE 'NO-NX 800101 REC FOUND'
    RESET #FOUND-REC
  END-NOREC
  MOVE *ISN TO #ISN
END-FIND
*
IF #FOUND-REC
  GET NT-V #ISN
END-IF
*
MOVE *DATN               TO NOTE-DATE (1)
MOVE #OLDEST-CN          TO NT-OLDEST-MBR-CN
MOVE #OLDEST-AGE         TO NT-OLDEST-MBR-AGE
MOVE #YOUNGEST-CN        TO NT-YOUNGEST-MBR-CN
MOVE #YOUNGEST-AGE       TO NT-YOUNGEST-MBR-AGE
WRITE 'youngest member' NT-YOUNGEST-MBR-CN NT-YOUNGEST-MBR-AGE
WRITE 'oldest member ' NT-OLDEST-MBR-CN NT-OLDEST-MBR-AGE
*
MOVE #OLDEST-WID-CN      TO NT-OLDEST-WID-CN
MOVE #OLDEST-WID-AGE     TO NT-OLDEST-WID-AGE
MOVE #YOUNGEST-WID-CN    TO NT-YOUNGEST-WID-CN
MOVE #YOUNGEST-WID-AGE   TO NT-YOUNGEST-WID-AGE
WRITE 'youngest widow' NT-YOUNGEST-WID-CN NT-YOUNGEST-WID-AGE
WRITE 'oldest   widow' NT-OLDEST-WID-CN NT-OLDEST-WID-AGE
*
MOVE #NUM-PEBD-GTR-19860801     TO NT-NUM-PEBD-GTR-19860801
MOVE #NUM-PEBD-GTR-19800908     TO NT-NUM-PEBD-GTR-19800908
MOVE #NUM-MIL-DISAB-100-PCT     TO NT-NUM-MIL-DISAB-100-PCT
MOVE #NUM-VA-DISAB-100-PCT      TO NT-NUM-VA-DISAB-100-PCT
MOVE #NUM-WID-MIL-DISAB-100-PCT TO NT-NUM-WID-MIL-DISAB-100-PCT
MOVE #NUM-WID-VA-DISAB-100-PCT  TO NT-NUM-WID-VA-DISAB-100-PCT
MOVE #LONGEST-CN           TO NT-LONGEST-CN
MOVE #LONGEST-YEARS        TO NT-LONGEST-YEARS
MOVE #LONGEST-WID-CN       TO NT-LONGEST-WID-CN
MOVE #LONGEST-WID-YEARS    TO NT-LONGEST-WID-YEARS
MOVE #ZIP-ZERO             TO NT-ZIP-ZERO
MOVE #ZIP-5-DIGIT          TO NT-ZIP-5-DIGIT
MOVE #ZIP-9-DIGIT          TO NT-ZIP-9-DIGIT
MOVE #WID-ZIP-ZERO         TO NT-WID-ZIP-ZERO
MOVE #WID-ZIP-5-DIGIT      TO NT-WID-ZIP-5-DIGIT
MOVE #WID-ZIP-9-DIGIT      TO NT-WID-ZIP-9-DIGIT
MOVE #NUM-RSFPP            TO NT-NUM-RSFPP
MOVE #RET-B4-1957-1-1      TO NT-RET-B4-1957-1-1
MOVE #RETIREES-OVER-62     TO NT-RETIREES-OVER-62
MOVE #RETIREES-UNDER-62    TO NT-RETIREES-UNDER-62
MOVE #NUM-ACT-BY-MIL-ID (*) TO NT-NUM-ACT-BY-MIL-ID (*)
MOVE #NUM-RET-BY-MIL-ID (*) TO NT-NUM-RET-BY-MIL-ID (*)
MOVE #NUM-CIV-BY-MIL-ID (*) TO NT-NUM-CIV-BY-MIL-ID (*)
MOVE #NUM-20-YR-LTR         TO NT-NUM-20-YR-LTR
MOVE #NUM-DOB-PRIOR-1918    TO NT-NUM-DOB-PRIOR-1918
MOVE #NUM-SSBP         TO NT-NUM-SSBP
MOVE #NUM-O-E-DATE     TO NT-NUM-O-E-DATE
MOVE #NUM-MODES-M-G    TO NT-NUM-MODES-M-G
MOVE #NUM-MODES-E      TO NT-NUM-MODES-E
MOVE #NUM-MODES-Q      TO NT-NUM-MODES-Q
MOVE #NUM-MODES-S      TO NT-NUM-MODES-S
MOVE #NUM-MODES-A      TO NT-NUM-MODES-A
MOVE #NUM-SAV-INV      TO NT-NUM-SAV-INV
MOVE #NUM-SP-COD (*)   TO NT-NUM-SP-COD (*)
MOVE #NUM-SPS-CSRS     TO NT-NUM-SPS-CSRS
MOVE #CNT-FIN          TO NT-NUM-FIN-BENES
MOVE #CNT-FIN-POLS     TO NT-NUM-FIN-BENES-POLS
MOVE #CNT-TICKERS      TO NT-NUM-TICKERS
MOVE #CNT-CAP-MBRS     TO NT-NUM-CAP-MBRS
*
MOVE #LCYC-MBRS  (*) TO NT-LCYC-MBRS (*)
MOVE #LCYC-PLANS (*) TO NT-LCYC-PLANS (*)
* MOVE #LCYC-UNITS (*) TO NT-LCYC-UNITS (*)   /* UNUSED
RESET NT-LCYC-UNITS (*)
MOVE #LCYC-FAS   (*) TO NT-LCYC-FAS (*)
MOVE #LCYC-PIA   (*) TO NT-LCYC-PIA (*)
MOVE #LCYC-ENLISTED  (*) TO NT-LCYC-ENLISTED (*)
MOVE #LCYC-BIRTH-CERT-CDS (*) TO NT-LCYC-BIRTH-CERT-CDS (*)
MOVE #LCYC-1ST-2-YRS (*) TO NT-LCYC-1ST-2-YRS (*)
MOVE #LCYC-ALIVE-SPS (*) TO NT-LCYC-ALIVE-SPS (*)
MOVE #LCYC-DEAD-SPS  (*) TO NT-LCYC-DEAD-SPS (*)
MOVE #LCYC-NO-SPS    (*) TO NT-LCYC-NO-SPS (*)
MOVE #LCYC-FMR-SPS   (*) TO NT-LCYC-FMR-SPS (*)
MOVE #LCYC-RESTRICTED (*) TO NT-LCYC-RESTRICTED (*)
MOVE #LCYC-ALL-PD-UP  (*) TO NT-LCYC-ALL-PD-UP (*)
MOVE #LCYC-PARTIAL-PDUP (*) TO NT-LCYC-PARTIAL-PDUP (*)
MOVE #LCYC-HAS-LOANS (*) TO NT-LCYC-HAS-LOANS (*)
MOVE #LCYC-MBRS-W-HANDI (*) TO NT-LCYC-MBRS-W-HANDI (*)
MOVE #LCYC-DEPNS-18     (*) TO NT-LCYC-DEPNS-18 (*)
MOVE #LCYC-DEPNS-18-21 (*) TO NT-LCYC-DEPNS-18-21 (*)
MOVE #LCYC-DEPNS-22-23 (*) TO NT-LCYC-DEPNS-22-23 (*)
MOVE #LCYC-DEPNS-OVR-23(*) TO NT-LCYC-DEPNS-OVR-23(*)
*
FOR #I1 1 TO 5
  IF #LCYC-MBRS (#I1) NE 0
    COMPUTE ROUNDED NT-LCYC-AVG-AGE (#I1) = #LCYC-HASH-AGE (#I1)
      / #LCYC-MBRS (#I1)
  END-IF
END-FOR
MOVE #LCYC-INTERNET-ADDR (*) TO NT-LCYC-INTERNET-ADDR (*)
MOVE #LCYC-NU-MBR-POLS   (*) TO NT-LCYC-NU-MBR-POLS   (*)
MOVE #LCYC-ADDL-POLS     (*) TO NT-LCYC-ADDL-POLS     (*)
MOVE #LCYC-EDU-INFO      (*) TO NT-LCYC-EDU-INFO      (*)
MOVE #LCYC-MBRS-W-DECEASED (*) TO NT-LCYC-MBRS-W-DECEASED (*)
MOVE #LCYC-ACT-MBRS      (*) TO NT-LCYC-ACT-MBRS (*)
MOVE #LCYC-CURR-PLANS (*, *) TO NT-LCYC-CURR-PLANS (*, *)
*
IF #FOUND-REC
  UPDATE (1192)
ELSE
  MOVE 800101 TO NT-V.ID-NUMBER
  STORE NT-V
END-IF
END TRANSACTION
*
* * * * * * READ EXTRACT FILE *
*
REPEAT
  READ WORK 2 ONCE RECORD #EXT-REC
  AT END
    ESCAPE BOTTOM
  END-ENDFILE
END-ALL
*
SORT BY #SEQ-AREA
    USING #EXT-REMAIN
*
  ADD 1 TO #CNT
  PERFORM FIND-LABEL
  WRITE (2) #CNT ' ' #SEQ-REMAIN ' ' #EXT-REMAIN
*
  AT BREAK #SEQ-TYPE
    RESET #CNT
    PERFORM FIND-LABEL
    NEWPAGE (2)
  END-BREAK
END-SORT
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-LABEL
* * * * * * * * * * * * * * * *
DECIDE ON FIRST #SEQ-TYPE
  VALUE 01 MOVE 'SPECIAL-CATEGORY MEMBERS'         TO #RPT-LABEL
  VALUE 02 MOVE 'REDUX MBRS - PEBD GTR 19860801'   TO #RPT-LABEL
  VALUE 03 MOVE 'HIGH-36 MBRS - PEBD GTR 19800908' TO #RPT-LABEL
  VALUE 04 MOVE 'MIL-DISABILITY 100 PERCENT'       TO #RPT-LABEL
  VALUE 05 MOVE 'VA-DISABILITY 100 PERCENT'        TO #RPT-LABEL
  VALUE 06 MOVE 'AUTO LOAN PAYMENT'                TO #RPT-LABEL
  VALUE 07 MOVE 'PRE-AUTH DEBIT PAYMENT'           TO #RPT-LABEL
  VALUE 08 MOVE 'MEMBERS WITH SPECIAL CODES'       TO #RPT-LABEL
  VALUE 09 MOVE 'MBRS INDICATING NO SPOUSE'        TO #RPT-LABEL
  VALUE 10 MOVE 'MBRS WITH RSFPP'                  TO #RPT-LABEL
  VALUE 11 MOVE 'MBRS WITH 20-YR LETTER DATES'     TO #RPT-LABEL
  VALUE 12 MOVE 'MBRS WITH SSBP% OR OP-ENROL INFO' TO #RPT-LABEL
  VALUE 13 MOVE 'MBRS WITH SAVINGS & INVESTMENTS'  TO #RPT-LABEL
  VALUE 14 MOVE 'MBRS/WIDS WITH SPS-RET-INFO    '  TO #RPT-LABEL
  VALUE 15 MOVE 'MBRS/WIDS WITH COLLEGE-INFO    '  TO #RPT-LABEL
  VALUE 16 MOVE 'MBRS WITH C.A.P. LOANS         '  TO #RPT-LABEL
  NONE     MOVE 'UNKNOWN'                          TO #RPT-LABEL
END-DECIDE
*
END-SUBROUTINE
*
END
