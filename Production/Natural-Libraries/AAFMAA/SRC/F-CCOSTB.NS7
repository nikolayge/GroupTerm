* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-CCOSTB                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Calculate Policy Current Cost Basis                   *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG     REASON                                     *
* PAM    11/06/2009 None    Program Written                            *
************************************************************************
*
DEFINE FUNCTION FUNCTION-CURRENT-CB RETURNS (N11.2) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #ID-NUMBER                  (N6)
1 #I1                         (I2)
1 #DATE                       (N8)
*
LOCAL USING OBJLSTAT
*
LOCAL
*
1 #DIFFERENCE                 (N7.2)
1 #WORK-ID-NUMBER             (N6)
1 REDEFINE #WORK-ID-NUMBER
 2 #WORK-ID-NUM-A             (A6)
*
1 #CVALUSXX-PARMS
 2 #SUFFIX                    (A3)
 2 #AS-OF-YYYYMMDD            (N8)
 2 #LOAN-BAL                  (N7.2/9)
 2 #LOAN-BAL-2                (N7.2/9)
 2 #ACC-INT                   (N7.2/9)
 2 #ACC-INT-2                 (N7.2/9)
 2 #CSV                       (N7.2/9)
 2 #CVALUE                    (N7.2/9)
 2 #LVALUE                    (N7.2/9)
 2 #MCSV                      (N7.2/9)
 2 #DB                        (N7.2/9)
 2 #ANN-PREM                  (N7.2/9)
 2 #FACE-AMT                  (N7.2/9)
 2 #TPP                       (N7.2/9)
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-CURRENT-CB
#WORK-ID-NUMBER := #ID-NUMBER
#AS-OF-YYYYMMDD := #DATE
*
FIND (1) ST-V WITH MEMBER-KEY = #WORK-ID-NUM-A
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  CALLNAT 'CVALUSXX'
    ST-V.ID-NUMBER
    #SUFFIX
    #AS-OF-YYYYMMDD
    #LOAN-BAL   (*)
    #LOAN-BAL-2 (*)
    #ACC-INT    (*)
    #ACC-INT-2  (*)
    #CSV        (*)
    #CVALUE     (*)
    #LVALUE     (*)
    #MCSV       (*)
    #DB         (*)
    #ANN-PREM   (*)
    #FACE-AMT   (*)
    #TPP        (*)
*
  IF ST-V.COST-BASIS (#I1) NE 0
    COMPUTE #DIFFERENCE = TOT-PREMIUMS-PAID (#I1) - COST-BASIS (#I1)
    IF #DIFFERENCE NE 0
      COMPUTE #DIFFERENCE = TOT-PREMIUMS-PAID (#I1) - TERM-CASH-VALUE
    END-IF
*
    IF #DIFFERENCE LT 1
      RESET #DIFFERENCE
    END-IF
  ELSE
    RESET #DIFFERENCE
  END-IF
*
  FUNCTION-CURRENT-CB := COST-BASIS (#I1) + #TPP (#I1) - TOT-PREMIUMS-PAID (#I1) + #DIFFERENCE
END-FIND
*
END-FUNCTION
*
END
