* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: CCSTXSVP  ** calc COST INDICES
* CALLED IN C2V10PVP
*
DEFINE DATA
PARAMETER
*
1 #SCR-COMMAND (A2)
1 #SCR-ID      (N6)
1 #SCR-SUFFIX  (A3)
1 #CLERK       (A3)
1 #FACE-AMT    (P7.2)
1 #ANN-PREM    (P5.2)
1 #PAY-STATUS  (A2)
1 #TOT-PRM-PD  (P7.2)
1 #MAX-YRS     (N3)
1 #DUES-PAID   (P7.2)
1 #DUES-PD-DOLS (P7)
1 #DB          (P6.2/20) /* 20 yrs of death bene
1 #CV          (P6.2/4)  /* 10 yr min cv/proj csv/20 yr min cv/proj csv
1 #CIX-MODE      (P2)
1 #CIX-ERROR     (A75)
1 #CIX-SCR-SC    (A8/4)   /* calculated indices
1 #CIX-SCR-NP    (A8/4)   /* calculated indices
*
LOCAL
*
1 #I1            (I2)
1 #CIX-DSC-FCT   (P3.7)
1 #CIX-MD-INT-RT (P3.7)
1 #CIX-10Y-AC-FC (P5.7)
1 #CIX-AN-PRM-MD (P5.7)
1 #PWR           (P3.7)
1 #EXP           (P5.7)
1 #LOG           (P5.7)
1 #FAC           (P11.7)
1 #CIX-ANN-PRM   (P6.2)
1 #CIX-DB        (P7)
1 #CIX-CSV-10    (P7)
1 #CIX-CSV-20    (P7)
1 #CIX-MCV-10    (P7)
1 #CIX-MCV-20    (P7)
1 #CIX-10-PRMS   (P9.5)
1 #CIX-20-PRMS   (P9.5)
1 #CIX-10-MDB    (P9.5)
1 #CIX-20-MDB    (P9.5)
1 #CIX-10-DB     (P9.5)
1 #CIX-20-DB     (P9.5)
1 #CIX-10-MIN-SC (P12.5)
1 #CIX-20-MIN-SC (P12.5)
1 #CIX-10-PRJ-SC (P12.5)
1 #CIX-20-PRJ-SC (P12.5)
1 #CIX-10-MIN-NP (P12.5)
1 #CIX-20-MIN-NP (P12.5)
1 #CIX-10-PRJ-NP (P12.5)
1 #CIX-20-PRJ-NP (P12.5)
*
END-DEFINE
* * * * * * * * * * * *
*
RESET #CIX-10-PRMS #CIX-20-PRMS #CIX-10-MDB
      #CIX-20-MDB  #CIX-10-DB   #CIX-20-DB
      #CIX-SCR-SC (*) RESET #CIX-SCR-NP (*) #CIX-ERROR
COMPUTE #PWR = 1 / #CIX-MODE
COMPUTE ROUNDED #LOG = #PWR * LOG (1.05)
COMPUTE ROUNDED #FAC = EXP (#LOG)
MOVE #FAC TO #CIX-MD-INT-RT
COMPUTE ROUNDED #CIX-DSC-FCT = 1 / #FAC
*
COMPUTE #PWR = 10 * #CIX-MODE
COMPUTE ROUNDED #LOG = #PWR * LOG (#CIX-MD-INT-RT)
COMPUTE ROUNDED #FAC = EXP (#LOG)
COMPUTE ROUNDED #CIX-10Y-AC-FC = (#FAC - 1) / (1 - #CIX-DSC-FCT)
*
MOVE #CIX-MODE TO #PWR
COMPUTE ROUNDED #LOG = #PWR * LOG (#CIX-MD-INT-RT)
COMPUTE ROUNDED #FAC = EXP (#LOG)
COMPUTE ROUNDED #CIX-AN-PRM-MD = (#ANN-PREM / #CIX-MODE)
     * (#FAC - 1) / (#CIX-MD-INT-RT - 1)
*
FOR #I1 1 TO 20
  IF #DB (#I1) = 0
    MOVE 'Cost indices not shown due to incomplete info - call EDS'
        TO #CIX-ERROR
    ESCAPE BOTTOM
  END-IF
  COMPUTE #CIX-DB = #DB (#I1) * 100
*
  RESET #CIX-ANN-PRM
  IF #PAY-STATUS = '47'
    IF #I1 = 1
      MOVE #TOT-PRM-PD TO #CIX-ANN-PRM
    END-IF
  ELSE
    IF #I1 LE #MAX-YRS
      MOVE #CIX-AN-PRM-MD TO #CIX-ANN-PRM
    END-IF
  END-IF
*
  IF #I1 LE 10
    COMPUTE #PWR = 10 - #I1 + 1
    COMPUTE ROUNDED #LOG = #PWR * LOG (1.05)
    COMPUTE ROUNDED #FAC = EXP (#LOG)
    COMPUTE ROUNDED #CIX-10-PRMS = #CIX-10-PRMS + #CIX-ANN-PRM * #FAC
    COMPUTE ROUNDED #CIX-10-MDB  = #CIX-10-MDB + #FACE-AMT * #FAC
    COMPUTE ROUNDED #CIX-10-DB   = #CIX-10-DB   + #CIX-DB * #FAC
  END-IF
*
  COMPUTE #PWR = 20 - #I1 + 1
  COMPUTE ROUNDED #LOG = #PWR * LOG (1.05)
  COMPUTE ROUNDED #FAC = EXP (#LOG)
  COMPUTE ROUNDED #CIX-20-PRMS = #CIX-20-PRMS + #CIX-ANN-PRM * #FAC
  COMPUTE ROUNDED #CIX-20-MDB  = #CIX-20-MDB + #FACE-AMT * #FAC
  COMPUTE ROUNDED #CIX-20-DB   = #CIX-20-DB   + #CIX-DB * #FAC
*
* IF   (#CLERK = 'DRW')
*  AND ((#I1 LE 10) OR (#I1 GE 18))
*  AND (*DEVICE NE 'BATCH')
*   DISPLAY #I1 (EM=Z9)
*     #CIX-10-PRMS (EM=ZZZZ,ZZZ.99)
*     #CIX-20-PRMS (EM=ZZZZ,ZZZ.99)
*     #CIX-10-MDB  (EM=ZZZZ,ZZZ.99)
*     #CIX-20-MDB  (EM=ZZZZ,ZZZ.99)
*     #CIX-10-DB   (EM=Z,ZZZ,ZZZ.99)
*     #CIX-20-DB   (EM=Z,ZZZ,ZZZ.99)
* END-IF
END-FOR
*
COMPUTE #CIX-MCV-10 = #CV (1) * 100      /* 10 year min cv
COMPUTE #CIX-CSV-10 = #CV (2) * 100      /* 10 year csv
*
IF #PAY-STATUS = '47'
  MOVE #TOT-PRM-PD TO #DUES-PAID
ELSE
  IF #MAX-YRS LT 10
    COMPUTE #DUES-PAID = #ANN-PREM * #MAX-YRS
  ELSE
    COMPUTE #DUES-PAID = #ANN-PREM * 10
  END-IF
END-IF
*
MOVE ROUNDED #DUES-PAID TO #DUES-PD-DOLS
*
IF #DUES-PD-DOLS GT #CIX-MCV-10
  MOVE #DUES-PD-DOLS TO #CIX-MCV-10
END-IF
*
IF #DUES-PD-DOLS GT #CIX-CSV-10
  MOVE #DUES-PD-DOLS TO #CIX-CSV-10
END-IF
*
COMPUTE ROUNDED #CIX-10-MIN-SC =
            (#CIX-10-PRMS - #CIX-MCV-10) * 1000 / #CIX-10-MDB
COMPUTE ROUNDED #CIX-10-PRJ-SC =
            (#CIX-10-PRMS - #CIX-CSV-10) * 1000 / #CIX-10-DB
*
COMPUTE #CIX-MCV-20 = #CV (3) * 100      /* 20 year min cv
COMPUTE #CIX-CSV-20 = #CV (4) * 100      /* 20 year csv
*
IF #PAY-STATUS  = '47'
  MOVE #TOT-PRM-PD TO #DUES-PAID
ELSE
  IF #MAX-YRS LT 20
    COMPUTE #DUES-PAID = #ANN-PREM * #MAX-YRS
  ELSE
    COMPUTE #DUES-PAID = #ANN-PREM * 20
  END-IF
END-IF
*
MOVE ROUNDED #DUES-PAID TO #DUES-PD-DOLS
*
IF #DUES-PD-DOLS GT #CIX-MCV-20
  MOVE #DUES-PD-DOLS TO #CIX-MCV-20
END-IF
*
IF #DUES-PD-DOLS GT #CIX-CSV-20
  MOVE #DUES-PD-DOLS TO #CIX-CSV-20
END-IF
*
COMPUTE ROUNDED #CIX-20-MIN-SC =
            (#CIX-20-PRMS - #CIX-MCV-20) * 1000 / #CIX-20-MDB
COMPUTE ROUNDED #CIX-20-PRJ-SC =
            (#CIX-20-PRMS - #CIX-CSV-20) * 1000 / #CIX-20-DB
*
COMPUTE ROUNDED #CIX-10-MIN-NP = #CIX-10-PRMS * 1000 / #CIX-10-MDB
COMPUTE ROUNDED #CIX-20-MIN-NP = #CIX-20-PRMS * 1000 / #CIX-20-MDB
COMPUTE ROUNDED #CIX-10-PRJ-NP = #CIX-10-PRMS * 1000 / #CIX-10-DB
COMPUTE ROUNDED #CIX-20-PRJ-NP = #CIX-20-PRMS * 1000 / #CIX-20-DB
*
IF #CIX-ERROR = ' '
  MOVE EDITED #CIX-10-MIN-SC (EM=-ZZZ.99) TO #CIX-SCR-SC (1)
  MOVE EDITED #CIX-20-MIN-SC (EM=-ZZZ.99) TO #CIX-SCR-SC (2)
  MOVE EDITED #CIX-10-PRJ-SC (EM=-ZZZ.99) TO #CIX-SCR-SC (3)
  MOVE EDITED #CIX-20-PRJ-SC (EM=-ZZZ.99) TO #CIX-SCR-SC (4)
  MOVE EDITED #CIX-10-MIN-NP (EM=-ZZZ.99) TO #CIX-SCR-NP (1)
  MOVE EDITED #CIX-20-MIN-NP (EM=-ZZZ.99) TO #CIX-SCR-NP (2)
  MOVE EDITED #CIX-10-PRJ-NP (EM=-ZZZ.99) TO #CIX-SCR-NP (3)
  MOVE EDITED #CIX-20-PRJ-NP (EM=-ZZZ.99) TO #CIX-SCR-NP (4)
*
* IF   (#CLERK = 'DRW')
*  AND (*DEVICE NE 'BATCH')
*   WRITE 'INITIAL RATES' #CIX-DSC-FCT #CIX-MD-INT-RT #CIX-10Y-AC-FC
*          #CIX-AN-PRM-MD
*   WRITE 'BEFORE CV"S"' #CV (*)
*   WRITE ' AFTER CV"S"' #CIX-MCV-10 #CIX-CSV-10 #CIX-MCV-20 #CIX-CSV-20
*   WRITE 'SURRENDER-COSTS' #CIX-10-MIN-SC (EM=-ZZZ.999999)
*                           #CIX-20-MIN-SC (EM=-ZZZ.999999)
*                           #CIX-10-PRJ-SC (EM=-ZZZ.999999)
*                           #CIX-20-PRJ-SC (EM=-ZZZ.999999)
*   WRITE 'NET PAYMENTS'    #CIX-10-MIN-NP (EM=-ZZZ.999999)
*                           #CIX-20-MIN-NP (EM=-ZZZ.999999)
*                           #CIX-10-PRJ-NP (EM=-ZZZ.999999)
*                           #CIX-20-PRJ-NP (EM=-ZZZ.999999)
* END-IF
END-IF
*
END
