* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2401SLM  Load CAP policies without existing loans to
*                   to screen in alphabetical order
************************************************************************
*                       MODIFICATION LOG
* USER   DATE      TAG     REASON
*
* PAM    07122004  None    Program Written.
* DRW    03222005  DW1     Bypass if pol not status "D"
* DRW    09032007  DW2     Bypass PAD-CODE = 'V'
* DRW    09122007  DW3     Allow for Scrolling CAP screens
*
************************************************************************
*
DEFINE DATA
PARAMETER
1 #ID-N                 (N6)
1 REDEFINE #ID-N
 2 #ID-A                (A6)
1 #SCN-ID               (N6/15)
1 #SCN-NAME             (A3/15)
1 #SCN-EFF-DT           (A8/15)
*
LOCAL
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER            (N6)
 2 NAME                 (A25)
 2 INTRO-FLAG           (A1)
 2 pad-code                              /* DW2
 2 STATUS               (9)
 2 X-LOAN-AMOUNT        (9)
 2 X-LOAN-DATE          (9)
 2 X-ALT-LP-AMT         (9)
 2 CONV-DURATION        (9)
 2 ISSUE-DATE           (9)
*
1 #last-cn              (n6)
1 #I1                   (I2)
1 #I2                   (I2)
1 #NUM-READ             (P5)
1 #CNT                  (P5)
1 #SORT-ID              (N6)
1 #SORT-NAME            (A25)
1 #SORT-EFF-DATE        (A8)
1 #YYYYMMDD             (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A              (A4)
 2 #MM-A                (A2)
 2 #DD-A                (A2)
*
END-DEFINE
************************************************************************
*
READ ST-V BY MEMBER-KEY FROM #ID-A
  ADD 1 TO #NUM-READ
*
  FOR #I1 1 TO 9
    IF   (ST-V.CONV-DURATION (#I1) = 999)
     AND (ST-V.STATUS (#I1) = 'D')
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I1 GT 9
    MOVE 1 TO #I1
  END-IF
*
  IF   (NOT ST-V.INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q')
   OR  (ST-V.STATUS (#I1) NE 'D')
   OR  (ST-V.CONV-DURATION (#I1) NE 999)
   OR  (ST-V.X-LOAN-AMOUNT (#I1) NE 0)
   OR  (ST-V.X-LOAN-DATE (#I1) NE 0)
*   or (st-v.pad-code = 'V')                     /* DW2
   OR  (ST-V.ID-NUMBER = 94761 OR = 98534 OR = 99939 OR = 100227)
*   ABOVE ARE VOIDED RECS OR ADMIN CHGD CAP RECS
    ESCAPE TOP
  END-IF
*
  MOVE ST-V.ID-NUMBER TO #SORT-ID           /* FOUND A CAP MEMBER
  MOVE ST-V.NAME TO #SORT-NAME              /*    WITH NO LOAN
  MOVE ST-V.ISSUE-DATE (#I1) TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SORT-EFF-DATE LEAVING NO
END-ALL
*
* SORT BY #SORT-NAME USING #SORT-ID #SORT-EFF-DATE    /* DW3
SORT BY #SORT-id USING #sort-name #SORT-EFF-DATE    /* DW3

  ADD +1 TO #I2
  IF #I2 GT 15
    ESCAPE BOTTOM
  END-IF
*
  MOVE #SORT-ID  TO #SCN-ID (#I2)
  MOVE #SORT-NAME TO #SCN-NAME (#I2)
  MOVE #SORT-EFF-DATE TO #SCN-EFF-DT (#I2)
  move #sort-id to #last-cn  /* Save last CN for reentry (assumes SORT is by ID)  
END-SORT
*
move #last-cn to #id-n
*
END
