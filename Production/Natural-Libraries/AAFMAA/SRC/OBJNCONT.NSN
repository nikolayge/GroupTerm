* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: OBJNCONT *** Create contact record using base information ***
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG     REASON                                     *
*                                                                      *
* PAM    04/21/2010 None    Program written                            *
* TMT    10/07/2013 TT1     ADDRESS NORMALIZATION                      *
************************************************************************
*
DEFINE DATA
PARAMETER USING OBJACONT
*
LOCAL USING OBJLCONT
LOCAL
*
1 TERM-V VIEW OF A-TERMINALS /* TT1
  2 TERMINAL-ID      (A8)
  2 COMMENTS         (A20)
  2 REDEFINE COMMENTS
    3 LAST-COID      (N8)
    3 #SP            (A2)
    3 #DESCRIPTION   (A10)
*
1 #COID-KEY          (A8)  CONST   <'MASTERCO'>
*
END-DEFINE
************************************************************************
*
DECIDE ON FIRST VALUE #ACTION
 VALUE 'ADD'
   PERFORM ADD-RECORD
 VALUE 'UPDATE'
   PERFORM UPDATE-RECORD
 VALUE 'DELETE'
   PERFORM DELETE-RECORD
 NONE
   IGNORE
END-DECIDE
*
************************************************************************
DEFINE SUBROUTINE ADD-RECORD
************************************************************************
*
FIND (1) TERM-V WITH TERMINAL-ID = #COID-KEY
  IF NO RECORDS FOUND
    RESET TERM-V
    TERMINAL-ID         := #COID-KEY
    LAST-COID           := 0
    STORE TERM-V
    ESCAPE BOTTOM
  END-NOREC
*
  ADD 1 TO LAST-COID
  UPDATE
END-FIND
*
RESET CT-V
MOVE BY NAME PARM-CT-TABLE TO CT-V
CT-V.CONTACT-ID          := TERM-V.LAST-COID
PARM-CT-TABLE.CONTACT-ID := CT-V.CONTACT-ID
CT-V.LAST-USER-UPD       := #USER
CT-V.LAST-DATE-UPD       := *DATN
CT-V.LAST-TIME-UPD       := *TIMN
*
STORE CT-V
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE UPDATE-RECORD
************************************************************************
*
CT-U. FIND (1) CT-V WITH CT-V.CONTACT-ID = PARM-CT-TABLE.CONTACT-ID
  MOVE BY NAME PARM-CT-TABLE TO CT-V
  CT-V.LAST-USER-UPD := #USER
  CT-V.LAST-DATE-UPD := *DATN
  CT-V.LAST-TIME-UPD := *TIMN
*
  UPDATE (CT-U.)
END-FIND
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DELETE-RECORD
************************************************************************
*
CT-D. FIND (1) CT-V WITH CT-V.CONTACT-ID = PARM-CT-TABLE.CONTACT-ID
  CT-V.DELETE-FLAG := 'Y'
  UPDATE (CT-D.)
END-FIND
*
END-SUBROUTINE
*
END
