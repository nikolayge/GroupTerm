* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PROGRAM-ID: ATHP07P1    - FORMAT SOME NOTICE EXTRACTS
* DOES PE/PA/P Q ASC/ACC F/A CS/GS/CCS/CGS CPA/CPL/CPT
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  07292005 DW1  Chg Text on DElq notice
* DRW  08032005 DW2  Add Count for F Notices
* DRW  09022005 DW3  ADD COUNT FOR CFL NOTICES
* DRW  09152005 DW4  Make new FPF Notice
* DRW  10042005 DW5  Bypass allotment start notices if db/cr < .99
* DRW  03212006 DW6  Chg text of FPF reminder for EZ-Pay
* DRW  05102006 DW7  Do not bypass FPF Notices if CSV exceeds FPF
* DRW  05142006 DW8  Add logic for new notices
* SAG  06302006 RH1  AB - Modified the custom coded ON ERROR stmt &
*                    replaced it with copycode GABNDCX1
* DRW  04042007 DW9  Bypass Suspend Note if Notice will not be printed
* PAM  04252007 PM10 Adjust paid-up letters for retired members without
*                    allotment authorization
* DRW  06272007 DW11 Combine Prem Notice MIP PRIN/INT
************************************************************************
*
*  THIS PROGRAM RUNS IN BATCH MODE ONLY
*
DEFINE DATA
PARAMETER
1 #ISC               (I2)
1 #SCR-TEXT          (A73/22)
1 #SCR-DUE-TEXT      (A29/4)
1 #SCR-TYPE-IND      (A14)
1 #SCR-ALLT-RQST-LN1 (A26)
1 #SCR-ALLT-RQST-LN2 (A26)
1 #SCR-ALLT-RQST-LN3 (A26)
*
1 #RUN-DT-MM-DD-YY   (A10)
1 #C-RUN-DT-YYYYMMDD (N8)
1 REDEFINE #C-RUN-DT-YYYYMMDD
 2 #C-RUN-DT-YYYYMM  (N6)
 2 REDEFINE #C-RUN-DT-YYYYMM
  3 #C-RUN-DT-YYYY    (N4)
  3 #C-RUN-DT-MM      (N2)
 2 #C-RUN-DT-DD      (N2)
1 #LAST-MONTH         (A10)
*
1 #REPT-EXTRACT          (A250)
1 REDEFINE #REPT-EXTRACT
 2 #RE-SORT-KEY             (A27)
 2 REDEFINE #RE-SORT-KEY
   3 #RE-SECTION           (A1)
   3 #RE-SUBSECTION        (A1)
   3 #RE-NAME              (A25)
 2 #RE-REMAINING-FIELDS    (A223)
 2 REDEFINE #RE-REMAINING-FIELDS
  3 #RE-ADDR1             (A25)
  3 #RE-ADDR2             (A25)
  3 #RE-ADDR3             (A25)
  3 #RE-ADDR4             (A25)
  3 #RE-ZIP-5             (A5)
  3 #RE-DASH              (A1)
  3 #RE-ZIP-LST-4         (A4)
  3 #RE-RANK              (A3)
  3 #RE-STATUS            (A1)
  3 #RE-CN                (A2)
  3 #RE-CN-NUMBER         (A6)
  3 REDEFINE #RE-CN-NUMBER
   4 #RE-CN-NUMBER-N      (N6)
  3 REDEFINE #RE-CN-NUMBER
   4 #RE-CN-NUM-0         (A1)
   4 #RE-CN-NUM-5         (A5)
  3 #RE-FILLER-1          (A1)
  3 #RE-OLD-SECT          (A1)
  3 #RE-NON-MBR-FLAG      (A1)
  3 #RE-REMAINING         (A98)
  3 REDEFINE #RE-REMAINING           /* FPF-DELINQUENT
   4 #RE-FPF-AMT          (N5.2)
   4 #RE-FPF-ISS-DT-YYYY  (A4)
   4 #RE-FPF-ISS-DT-MM    (A2)
   4 #RE-FPF-ISS-DT-DD    (A2)
   4 #RE-FPF-PDTO-DT-YYYY (A4)
   4 #RE-FPF-PDTO-DT-MM   (A2)
   4 #RE-FPF-PDTO-DT-DD   (A2)
   4 #RE-FPF-DUE-DT-YYYY  (A4)
   4 #RE-FPF-DUE-DT-MM    (A2)
   4 #RE-FPF-DUE-DT-DD    (A2)
   4 #RE-FPF-NEW-ADDL     (A1)
   4 #RE-FPF-INTRO-FLAG   (A1)
   4 #RE-FPF-SNG-PREM     (A1)
   4 #RE-FPF-APL-LAPS     (A1)
   4 #RE-FPF-SEG-NUM      (N1)   /* DW4
  3 REDEFINE #RE-REMAINING           /* PREMIUM-INFO
   4 #RE-PRM-DUE-YY       (N2)
   4 #RE-PRM-DUE-MM       (A2)
   4 #RE-PRM-MODE         (A1)
   4 #RE-PRM-SVC-ONLY     (A1)
   4 #RE-PRM-PREMADJ      (N4.2)
   4 #RE-PRM-BILLPREM     (N5.2)
   4 #RE-PRM-BILLPRIN     (N4.2)
   4 #RE-PRM-BILLINTR     (N3.2)
   4 #RE-PRM-APL-LAPS     (A1)
   4 #RE-PRM-PUSW         (A1)
   4 #RE-PRM-APL          (N2)
   4 #RE-PRM-BILLDUES     (N4.2)
   4 #RE-PRM-BILLRIDER    (N4.2)
   4 #RE-PRM-BILL-EXTRA   (N5.2)
   4 #RE-PRM-ISS-DT       (N8)
   4 #RE-PRM-PLAN         (A3)
   4 #RE-PRM-FILL         (A34)
  3 REDEFINE #RE-REMAINING           /* PAID-UP-INFO
   4 #RE-PDUP-TYPE        (A1)
   4 #RE-PDUP-MM          (A2)
   4 #RE-PDUP-DD          (A2)
   4 #RE-PDUP-YY          (N2)
   4 #RE-PDUP-AMT-BEFORE  (N4.2)
   4 #RE-PDUP-AMT-AFTER   (N4.2)
   4 #RE-PDUP-PLAN        (A3)
   4 #RE-PDUP-EFF-DT-TIME (N4)
   4 #RE-PDUP-EFF-DT-DD   (A2)
  3 REDEFINE #RE-REMAINING           /* ALLOTMENT STOP/START
   4 #RE-ALLT-ALLT-AMT    (N5.2)
   4 #RE-ALLT-DB-CR       (N5.2)
   4 #RE-ALLT-QTR-PREM    (N5.2)  /* prem + dues + fbr)
   4 #RE-ALLT-FIXED-LOAN  (N5.2)
   4 #RE-ALLT-QTR-PRM     (N5.2)
   4 #RE-ALLT-QTR-DUE     (N5.2)
   4 #RE-ALLT-QTR-FBR     (N5.2)
   4 #RE-ALLT-ISS-DT      (N8)
*
1 #PRINT-NOTICE    (L)
*
1 #TOTALS
 2 #NUM-WES           (N5)
 2 #NUM-WES-PE-Z5     (N5)
 2 #NUM-WES-XX-Z5     (N5)
 2 #NUM-WES-AFSC      (N5)
 2 #IR-IN-WES-CNT     (N5)
 2 #NUM-AAFMAA        (N5)
 2 #NUM-NO-ADDR       (N5)
 2 #NUM-0-ZIP         (N5)
*
 2 #NUM-PE     (N5)
 2 #NUM-CPA    (N5)
 2 #NUM-CS-GS  (N5)
 2 #NUM-CCS-CGS (N5)
 2 #NUM-I      (N5)
 2 #NUM-ASC    (N5)
 2 #NUM-PA     (N5)
 2 #NUM-P      (N5)
 2 #NUM-F      (N5)    /* DW2
 2 #NUM-CFL    (N5)    /* DW3
 2 #NUM-Q      (N5)
*
 2 #NUM-PREM-DU       (N5)
 2 #NUM-PREM-AFSC     (N5)
 2 #NUM-EZPA-P  (N5)
 2 #NUM-ALOT-P  (N5)
 2 #NUM-REG-P   (N5)
 2 #NUM-PAID-UP       (N5)
 2 #NUM-ALLT-STRT-CKN (N5)
 2 #NUM-FPF-REM       (N5)
 2 #NUM-FPF-REM-FPF   (N5)
 2 #NUM-FPF-REM-PUSP  (N5)
 2 #AMT-PREM-DU       (N9.2)
 2 #AMT-PREM-DU-AFSC  (N9.2)
 2 #AMT-DUES-DU       (N9.2)
 2 #AMT-ALLT-STRT-CKN (N9.2)
 2 #AMT-FPF-REM       (N9.2)
 2 #NUM-STOP-ALLT     (N5)
 2 #NUM-CERT-STOP-ALLT (N5)
 2 #NUM-STOP-ALLT-NO-LV (N5)
 2 #AMT-STOP-ALLT       (N9.2)
 2 #AMT-CRT-STOP-ALLT (N9.2)
 2 #NUM-PREM-REM      (N5)
 2 #NUM-PREM-REM-APL  (N5)
 2 #NUM-PREM-REM-LAP  (N5)
 2 #NUM-PREM-REM-AFSC (N5)
 2 #AMT-PREM-REM      (N9.2)
 2 #AMT-PREM-REM-AFSC (N9.2)
 2 #AMT-DUES-REM      (N9.2)
 2 #NEXT-MON-NEXT-YEAR (A10)
 2 #NEXT-MON-DAY-15    (N8)
*
 2 #NEXT-MON-DUE-DT   (A10)
 2 #ADDRESS-CHG-CODE  (A1)
*
1 #IS-OLD-NEW   (L)
1 #INSURED-CN  (A6)
*
1 #SCR-DUE-LINE1     (A29)
1 REDEFINE #SCR-DUE-LINE1
 2 #SCR-DUE-LINE1A   (A18)
 2 #SCR-DUE-LINE1B   (A11)
*
1 #SCR-DUE-LINE2     (A29)
1 REDEFINE #SCR-DUE-LINE2
 2 #SCR-DUE-LINE2A   (A18)
 2 #SCR-DUE-LINE2B   (A11)
*
1 #SCR-DUE-LINE      (A38)
1 REDEFINE #SCR-DUE-LINE
 2 #SCR-DUE-LINEA    (A27)
 2 #SCR-DUE-LINEB    (A11)
*
1 #VARS       /* DW8
 2 #VAR1  (A10)
 2 #VAR2  (A10)
 2 #VAR3  (A10)
 2 #VAR4  (A10)
 2 #VAR5  (A10)
 2 #VAR6  (A9)
 2 #VAR7  (A10)
 2 #VAR8  (A10)
 2 #VAR9  (A10)
 2 #VAR10 (A7)
 2 #VAR11 (A10)
 2 #VAR12 (A10)
 2 #VAR13 (A10)
 2 #VAR14 (A10)
 2 #VAR15 (A10)
 2 #VAR16 (A10)
 2 #VAR17 (A10)
 2 #VAR18 (A10)
 2 #VAR19 (A10)
 2 #VAR20 (A10)
 2 #VAR21 (A10)
 2 #VAR22 (A10)
 2 #VAR23 (A10)
 2 #VAR24 (A10)
 2 #VAR25 (A10)
 2 #VAR26 (A10)
 2 #VAR27 (A10)
 2 #VAR28 (A10)
 2 #VAR29 (A10)
 2 #VAR30 (A3)
 2 #VAR31 (A1)
 2 #VAR32 (A1)
 2 #VAR33 (A1)
 2 #VAR34 (N1)
 2 #VAR35 (A8)
 2 #VAR36 (A10)
 2 #VAR37 (A1)
*
1 #SEND-PDUP-ALOT-FORM  (L)                                            /* PM10
*
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT (A60/60)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PROCESS-IND
 2 DEBIT-CREDIT-AMT
 2 PAD-CODE
 2 DATE-ENTERED (9)  /* DW4
 2 X-ALT-LP-IND (9)
 2 X-ALT-LP-AMT (9)
 2 X-ALT-LP-IND-2 (9)
 2 X-ALT-LP-AMT-2 (9)
 2 PLAN         (9)
 2 MODE         (9)
 2 STATUS       (9)
 2 ISSUE-DATE   (9)
 2 REDEFINE ISSUE-DATE
  3 ISSUE-DATE-OCC (9)
   4 ISS-DT-YYYY  (N4)
   4 ISS-DT-MM    (N2)
   4 ISS-DT-DD    (N2)
 2 PAID-TO-DATE (9)
 2 PAID-UP-DATE (9)
 2 MONTH-ALLOC-AMT (9)
 2 ANNUAL-PREM     (9)
 2 PAY-STATUS      (9)
 2 MEC-IND         (9)
 2 OWNERSHIP-FLAG  (9)
 2 X-FIRST-PLACE-FEE (9)
*
 2 ALOT-CHG-AUTH                                                 /* PM10
 2 ALOT-AMT                                                      /* PM10
*
1 #FPF-DUE-DT    (A10)   /* DW4
1 #CAPLOAN       (N5.2)  /* DW11
1 #ISS-DT        (N8)
1 #MON-X          (I2)
1 #MON-LIST
 2 #MON-LIST-1    (A36)   INIT <'January  February March    April    '>
 2 #MON-LIST-2    (A36)   INIT <'May      June     July     August   '>
 2 #MON-LIST-3    (A36)   INIT <'SeptemberOctober  November December '>
1 REDEFINE #MON-LIST
 2 #MON-DISP       (A9/12)
*
1 #SUFFIX      (A3)
1 #AS-OF-DT    (N8)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #FACE-AMT    (N7.2/9)
1 #TPP         (N7.2/9)
1 #CSV-P      (P7.2/9)
*
1 #ISS-DT-YYYYMMDD (N8)
1 REDEFINE #ISS-DT-YYYYMMDD
 2 #ISS-DT-YYYY  (N4)
 2 #ISS-DT-MM    (N2)
 2 #ISS-DT-DD    (N2)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM         (N6)
 2 REDEFINE #YYYYMM
  3 #YYYY-A   (A4)
  3 #MM-A      (A2)
 2 REDEFINE #YYYYMM
   3 #CENT           (N2)
   3 #YY             (N2)
   3 REDEFINE #YY
    4 #YY-A          (A2)
   3 #MM             (N2)
 2 #DD             (N2)
 2 REDEFINE #DD
  3 #DD-A          (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYY           (N4)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A     (A8)
*
1 #LAST-MON-YYYYMMDD  (N8)
1 REDEFINE #LAST-MON-YYYYMMDD
 2 #LAST-MON-YYYYMM   (N6)
 2 #LAST-MOB-DD       (N2)
*
1 #TOTAL-MOD-PREM   (N5.2)
1 #MODAL-PREM    (N5.2)
*
1 #ISS-DU-DT        (A10)
1 #CALC-PD-TO       (N8)
1 #CALC-LAP-DT      (A10)
*
1 #SCR-DUE-GRID  (A73)
1 REDEFINE #SCR-DUE-GRID
 2 #SCR-ISS-DT   (A14)
 2 #F2       (A1)
 2 #SCR-LAP-DT   (A15)
 2 #F32      (A4)
 2 #SCR-AMT-DUE  (A11)
*
1 #IS-MEC-POL   (L)
1 #RT-ISS-DT     (A10)
1 #RT-MODE       (A11)
1 #YYYY-X             (N4)
1 #MM-DD-YY-1         (A10)
1 #MM-DD-YY-2         (A10)
1 #SUSPEND-DATE       (N8)
1 #SUS-DATE-MM-DD-YY  (A10)
*
1 #NEW-TOT-AMT-DUE    (P5.2)   /* DW99
1 #TOT-DUE-N          (P5.2)
1 #TOT-DUE            (A10)
1 #AMT-4-2            (A8)
1 #AMT-4-2-A          (A8)
1 #AMT-4-2-2X         (A8)
1 #AMT-4-2-3X         (A8)
1 #AMT-5-2-DUES       (A10)
1 #AMT-5-2-RIDER      (A10)
1 #AMT-5-2            (A10)
1 #AMT-UNPAID         (A10)
1 #AMT                (N5.2)
1 #AMT2               (N5.2)
1 #PRIN               (A8)
1 #PDUP-PLAN          (A8)
1 #PRM-TEXT    (A12)
1 #DUES-TEXT   (A42)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #I4              (I2)
*
1 #ABEND-CODE       (I4)
1 #BNK-CNT     (P5)
1 #ALOT-CHG-AUTH   (A1)                                          /* PM10
1 #ALOT-AMT        (P5.2)                                        /* PM10
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* WRITE 'TOP oF DRWP07P1'
ON ERROR
*    WRITE   / '*E R R O R - N R    ' *ERROR-NR         /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'CN = ' #RE-CN
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     MOVE +4095 TO #ABEND-CODE
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
#BATCH-LITERAL-1 := #RE-CN
INCLUDE GABNDCX3                               /* SAG RH1 11282005 SS - END
STOP
END-ERROR
*
MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD  /* DW4
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 1 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #LAST-MON-YYYYMMDD
* * * * * * * DETERMINE TYPE OF NOTICE
*
MOVE TRUE TO #PRINT-NOTICE
DECIDE ON FIRST VALUE OF #RE-OLD-SECT
  VALUE 'E' PERFORM PREM-PENDING-LAPSE-OR-APL
  VALUE 'F' PERFORM STOP-ALLOTMENT-NOTICE
  VALUE 'J' PERFORM FPF-REMINDER
  VALUE 'M' PERFORM STOP-ALLOTMENT-NOTICE
  VALUE 'N' PERFORM ALLOT-START-CHECK-NEEDED-NOTICE
  VALUE 'P' PERFORM PREMIUM-DUE-NOTICE
  VALUE 'Q' PERFORM PAID-UP-NOTICE
  NONE      IGNORE
END-DECIDE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PREM-PENDING-LAPSE-OR-APL  /**CPA/CPT/CPL**
* * * * * * * * * * * * * * * *
*
IF #RE-CN-NUMBER GE '600000'
  ADD 1 TO #NUM-PREM-REM-AFSC
  ADD #RE-PRM-BILLPREM TO #AMT-PREM-REM-AFSC
  ADD #RE-PRM-PREMADJ  TO #AMT-PREM-REM-AFSC
ELSE
  ADD 1                TO #NUM-PREM-REM
  ADD #RE-PRM-BILLPREM TO #AMT-PREM-REM
  ADD #RE-PRM-PREMADJ  TO #AMT-PREM-REM
  ADD #RE-PRM-BILLDUES TO #AMT-DUES-REM
*
  IF #RE-PRM-APL-LAPS = 'A'
    ADD 1 TO #NUM-PREM-REM-APL
  ELSE
    ADD 1 TO #NUM-PREM-REM-LAP
  END-IF
END-IF
*
IF #RE-SECTION = 'A'      /* MISSING ADDRESS
  MOVE FALSE TO #PRINT-NOTICE
ELSE
  IF #RE-SECTION = 'C'     /* 0 ZIPS
    ADD 1 TO #NUM-0-ZIP
    ADD 1 TO #NUM-AAFMAA
  ELSE
    IF #RE-CN-NUMBER GE '600000'
      ADD 1 TO #NUM-WES-AFSC
    ELSE
      IF #IS-OLD-NEW   /* a/o 2/1/2002 all cpas goes to aafmaa
*                      /* a/o 5/27/2005 NXT cpas go to WES
        ADD 1 TO #NUM-AAFMAA
      ELSE
        ADD 1 TO #NUM-WES
        ADD 1 TO #NUM-CPA   /* only includes wes cpa's
*       IF #RE-ZIP-LST-4 = ' '
*         ADD 1 TO #NUM-WES-XX-Z5
*       END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
COMPUTE #TOT-DUE-N = #RE-PRM-BILLPREM + #RE-PRM-BILLDUES
                                      + #RE-PRM-BILLRIDER
                                      + #RE-PRM-PREMADJ
                                      + #RE-PRM-BILLPRIN
                                      + #RE-PRM-BILLINTR
MOVE EDITED #TOT-DUE-N (EM=ZZ,ZZZ.99) TO #TOT-DUE
MOVE 'Amount payable   $' TO #SCR-DUE-LINE1A
MOVE #TOT-DUE             TO #SCR-DUE-LINE1B
*
MOVE 'Due-date' TO #SCR-DUE-LINE2A
MOVE #RE-PRM-DUE-YY TO #YYYY-X
IF #YYYY-X GT 50
  ADD 1900 TO #YYYY-X
ELSE
  ADD 2000 TO #YYYY-X
END-IF
*
COMPRESS #RE-PRM-DUE-MM '01' #YYYY-X
                                INTO #MM-DD-YY-1 WITH '/'
MOVE #MM-DD-YY-1 TO #SCR-DUE-LINE2B
MOVE #SCR-DUE-LINE2B TO #VAR2                      /* DW8   <<<<<<<<<<
*
RESET #PRM-TEXT
IF #RE-CN-NUMBER GE '600000'
  MOVE 'fees' TO #PRM-TEXT
ELSE
  IF #RE-PRM-SVC-ONLY = 'Y'
    RESET #PRM-TEXT
  ELSE
    MOVE 'premiums and' TO #PRM-TEXT
  END-IF
END-IF
*
IF #RE-PRM-MODE = 'A'
  MOVE 'Annual' TO #VAR1     /* DW8
ELSE
  IF #RE-PRM-MODE = 'S'
    MOVE 'Semi-annual' TO #VAR1    /* DW8
  ELSE
    IF #RE-PRM-MODE = 'Q'
      MOVE 'Quarterly' TO #VAR1     /* DW8
    ELSE
      MOVE  'Monthly' TO #VAR1      /* DW8
    END-IF
  END-IF
END-IF
*
COMPRESS #VAR1 #PRM-TEXT INTO #SCR-TEXT (#ISC)   /* DW8
MOVE #RE-PRM-PLAN TO #VAR30   /* DW8
*
IF #RE-CN-NUMBER GE '600000'
  COMPRESS #SCR-TEXT (#ISC) 'reminder notice' INTO #SCR-TEXT (#ISC)
ELSE
  COMPRESS #SCR-TEXT (#ISC) 'dues reminder notice' INTO #SCR-TEXT (#ISC)
END-IF
*
COMPRESS #SCR-TEXT (#ISC) 'due on' #SCR-DUE-LINE2B
        INTO #SCR-TEXT (#ISC)
*
IF #RE-CN-NUMBER GE '600000'
  ADD 1 TO #ISC
  COMPRESS 'We have not received your member fees for the period'
           'following the'  INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'date above. Please remit the payment due using the'
           'envelope'                 INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'provided. If you have a CAP loan through AAFMAA'
           'your allotment'     INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  MOVE     'does not include this fee.' TO #SCR-TEXT (#ISC)
ELSE
*
  PERFORM GRACE-PERIOD
  PERFORM GET-ISS-DTS-AND-PREMS
*
  IF #RE-PRM-APL-LAPS = 'A'  /* PREM DUE PENDING APL
    ADD 1 TO #ISC
    COMPRESS 'An automatic loan against the cash value will be'
      'applied to cover'          INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'the total amount billed. If a loan is issued, it'
       'increases the'   INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'current loan balance with a new loan date. Annual'
             'interest notices'  INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE     'are mailed one month prior to the new loan date.'
                                   TO #SCR-TEXT (#ISC)
*
    IF #IS-MEC-POL AND (#ISC LT 19) /* # lines needed
      ADD 1 TO #ISC
      COMPRESS 'If the loan principal plus accrued interest exceeds'
               'the cash value or if'  INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'your policy is a MODIFIED ENDOWMENT CONTRACT, this'
               'action creates a'   INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'taxable event. Any portion of the total debt'
               'exceeding premiums paid'   INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'will be taxable and an IRS form 1099 will be'
               'issued.'  INTO #SCR-TEXT (#ISC)
    END-IF
  ELSE                            /* PREM DUE PENDING LAPSE
    MOVE #NEXT-MON-DAY-15 TO #SUSPEND-DATE
    MOVE #RE-PRM-ISS-DT TO #ISS-DT
*
    IF #PRINT-NOTICE     /* DW9
      PERFORM UPDATE-SUSPEND-DATE
    END-IF
*
    ADD 1 TO #ISC
    COMPRESS 'There is insufficient cash value available for a'
             'premium and dues loan.'  INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'We regret to tell you that if payment is not'
*            'received as'  INTO #SCR-TEXT (#ISC)                 /* DW1
             'received by the Loan/Lapse'  INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
* COMPRESS 'outlined above, your policy(ies) will lapse. Please' /* DW1
    COMPRESS 'Date above, your policy(ies) will lapse. Please'
             'don"t let this'     INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'happen! MEMBERSHIP IN THE ASSOCIATION IS TOO VALUABLE'
             'TO LET SLIP AWAY.' INTO #SCR-TEXT (#ISC)
  END-IF
END-IF
*
ADD 1 TO #ISC
COMPRESS 'If payment has been made, please disregard this notice.'
         'Thank You!'      INTO #SCR-TEXT (#ISC)
*
MOVE #SCR-DUE-LINE1 TO #SCR-DUE-TEXT (1)
MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
MOVE 'Please enter' TO #SCR-DUE-TEXT (3)
MOVE 'amount paid __________.____' TO #SCR-DUE-TEXT (4)
*
IF #RE-PRM-APL-LAPS = 'A'        /* PREM DUE PENDING APL
  COMPRESS 'CPA-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
ELSE                           /* PREM DUE PENDING LAPSE
  COMPRESS 'CPL-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
END-IF
*
IF   (#RE-SUBSECTION = '2')
 AND (#RE-CN-NUMBER LT '600000')
  MOVE 'Y' TO #VAR37
  MOVE 'Yes ___ I am interested' TO #SCR-ALLT-RQST-LN1
  MOVE 'in converting to'        TO #SCR-ALLT-RQST-LN2
  MOVE 'Government Allotment'    TO #SCR-ALLT-RQST-LN3
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PREMIUM-DUE-NOTICE   /**PE/PA/P**
* * * * * * * * * * * * * * * * * * *
*
IF #RE-CN-NUMBER GE '600000'
  ADD 1 TO #NUM-PREM-AFSC
  ADD #RE-PRM-BILLPREM TO #AMT-PREM-DU-AFSC
  ADD #RE-PRM-PREMADJ  TO #AMT-PREM-DU-AFSC
ELSE
  ADD 1 TO #NUM-PREM-DU
  ADD #RE-PRM-BILLPREM TO #AMT-PREM-DU
  ADD #RE-PRM-PREMADJ  TO #AMT-PREM-DU
  ADD #RE-PRM-BILLDUES TO #AMT-DUES-DU
END-IF
*
IF  (#RE-SECTION = 'A')      /* MISSING ADDRESS
 OR (#ADDRESS-CHG-CODE = 'U' OR = 'V') /* undeliverable addr
  MOVE FALSE TO #PRINT-NOTICE
ELSE
  IF #RE-SECTION = 'C'      /* 0 ZIPS
    ADD 1 TO #NUM-AAFMAA
    ADD 1 TO #NUM-0-ZIP
  ELSE
    IF #RE-CN-NUMBER GE '600000'
      ADD 1 TO #NUM-WES-AFSC
    ELSE
      ADD 1 TO #NUM-WES
*
      IF #RE-SUBSECTION = '1'
        ADD 1 TO #NUM-PE
*       IF #RE-ZIP-LST-4 = ' '
*         ADD 1 TO #NUM-WES-PE-Z5
*       END-IF
      ELSE
*       IF #RE-ZIP-LST-4 = ' '
*         ADD 1 TO #NUM-WES-XX-Z5
*       END-IF
        IF #RE-SUBSECTION = '2'
          ADD 1 TO #NUM-PA
        ELSE
          ADD 1 TO #NUM-P
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
COMPUTE #TOT-DUE-N = #RE-PRM-BILLPREM + #RE-PRM-BILLDUES
                                      + #RE-PRM-BILLRIDER
                                      + #RE-PRM-PREMADJ
                                      + #RE-PRM-BILLPRIN
                                      + #RE-PRM-BILLINTR
MOVE EDITED #TOT-DUE-N (EM=ZZ,ZZZ.99) TO #AMT-5-2
MOVE 'Amount payable   $' TO #SCR-DUE-LINE1A
MOVE #AMT-5-2             TO #SCR-DUE-LINE1B
*
MOVE 'Due date' TO #SCR-DUE-LINE2A
MOVE #RE-PRM-DUE-YY TO #YYYY-X
IF #YYYY-X GT 50
  ADD 1900 TO #YYYY-X
ELSE
  ADD 2000 TO #YYYY-X
END-IF
*
COMPRESS #RE-PRM-DUE-MM '01' #YYYY-X INTO #SCR-DUE-LINE2B WITH '/'
MOVE #SCR-DUE-LINE2B TO #VAR2   /* DW8
*
RESET #PRM-TEXT #DUES-TEXT
IF #RE-CN-NUMBER GE '600000'
  MOVE 'fees' TO #PRM-TEXT
ELSE
  IF #RE-PRM-SVC-ONLY = 'Y'
    MOVE 'dues' TO #DUES-TEXT
  ELSE
    MOVE 'premiums and' TO #PRM-TEXT
    MOVE 'dues'         TO #DUES-TEXT
  END-IF
END-IF
*
IF #RE-PRM-MODE = 'A'
  MOVE 'Annual' TO #VAR1     /* DW8
ELSE
  IF #RE-PRM-MODE = 'S'
    MOVE 'Semi-annual' TO #VAR1    /* DW8
  ELSE
    IF #RE-PRM-MODE = 'Q'
      MOVE 'Quarterly' TO #VAR1     /* DW8
    ELSE
      MOVE  'Monthly' TO #VAR1      /* DW8
    END-IF
  END-IF
END-IF
MOVE #RE-PRM-PLAN TO #VAR30   /* DW8
*
COMPRESS #VAR1 #PRM-TEXT #DUES-TEXT 'notice'
        INTO #SCR-TEXT (#ISC)
*
COMPRESS #SCR-TEXT (#ISC) 'due on' #SCR-DUE-LINE2B
        INTO #SCR-TEXT (#ISC)
*
IF #RE-CN-NUMBER GE '600000'
  ADD 1 TO #ISC
  COMPRESS 'The fees for your AFSC membership are payable on'
           'or before the'               INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'date shown above. If you have a CAP loan through AAFMAA,'
           'your'               INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  MOVE     'allotment does not include this fee.'
                                            TO #SCR-TEXT (#ISC)
ELSE
  IF #RE-PRM-SVC-ONLY = 'Y'
    ADD 1 TO #ISC
    COMPRESS 'Dues for your AAFMAA membership are payable on'
             'or before the'               INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE 'date shown above.' TO #SCR-TEXT (#ISC)
  ELSE
    ADD 1 TO #ISC
    COMPRESS 'Dues for your AAFMAA membership and premiums for'
             'your life insurance'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE 'are payable on or before the date shown above.'
           TO #SCR-TEXT (#ISC)
  END-IF
*
  PERFORM GRACE-PERIOD
  PERFORM GET-ISS-DTS-AND-PREMS
END-IF
*
IF   (#RE-PRM-PUSW = '1' OR = 'R') /* R = has at least 1 RT pol
 AND (#RE-CN-NUMBER LT '600000')   /* due to renew in this pay cycle
  IF #RE-PRM-PUSW = 'R'
    RESET #RT-ISS-DT #RT-MODE
    FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN
      FOR #I1 1 TO 9 /* find pol due to renew within next year
        IF   (ST-V.STATUS (#I1) = 'D')  /* this will handle A/S/Q
         AND (ST-V.PLAN (#I1) = 'RT')
         AND (ST-V.PAID-UP-DATE (#I1) LE (#C-RUN-DT-YYYYMMDD + 10000))
          MOVE ST-V.ISSUE-DATE (#I1) TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY INTO #RT-ISS-DT WITH '/'
          IF ST-V.MODE (#I1) = 'Q'
            MOVE 'quarterly' TO #RT-MODE
          ELSE
            IF ST-V.MODE (#I1) = 'S'
              MOVE 'semi-annual' TO #RT-MODE
            ELSE
              MOVE 'annual' TO #RT-MODE
            END-IF
          END-IF
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
    MOVE 'R' TO #VAR31    /* DW8
*
    ADD 2 TO #ISC
    COMPRESS 'Your bill has been adjusted for coverage to the'
             'anniversary date of' INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'your 5 year renewable term policy issued on' #RT-ISS-DT
                            INTO #SCR-TEXT (#ISC)
    COMPRESS #SCR-TEXT (#ISC) ', which will'
              INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'complete a five year term of coverage. Payment of'
             'your next bill will' INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'automatically renew your policy for a new five year'
             'term, with your' INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'current' #RT-MODE 'payment schedule and may include'
             'any adjustment' INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE     'premiums if necessary.'   TO #SCR-TEXT (#ISC)
  ELSE
    ADD 2 TO #ISC
    COMPRESS 'Your amount due has been adjusted due to a policy'
        'becoming fully'                    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE 'paid up.' TO #SCR-TEXT (#ISC)
    MOVE 'P' TO #VAR31    /* DW8
  END-IF
END-IF
*
* IF ((#RE-PRM-BILLDUES NE 0) OR (#RE-PRM-BILLRIDER NE 0))
*  AND (#RE-CN-NUMBER LT '600000')
*   ADD 2 TO #ISC
*   MOVE 'Premiums payable $'   TO #SCR-DUE-LINE1A
*   MOVE #AMT-5-2               TO #SCR-DUE-LINE1B
*   MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
*
*   IF #RE-PRM-BILLDUES NE 0
*     ADD 1 TO #ISC
*     MOVE 'Dues payable      '   TO #SCR-DUE-LINE1A
*     MOVE #AMT-5-2-DUES       TO #SCR-DUE-LINE1B
*     MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
*   END-IF
*
*   IF #RE-PRM-BILLRIDER NE 0
*     ADD 1 TO #ISC
*     MOVE 'Fam-Rider payable '   TO #SCR-DUE-LINE1A
*     MOVE #AMT-5-2-RIDER      TO #SCR-DUE-LINE1B
*     MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
*   END-IF
*
*   ADD 1 TO #ISC
*   RESET                          #SCR-DUE-LINE1A
*   MOVE '____________      '     TO #SCR-DUE-LINE1B
*   MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
*   ADD 1 TO #ISC
*   MOVE 'Amount payable   $'   TO #SCR-DUE-LINE1A
*   MOVE #TOT-DUE            TO #SCR-DUE-LINE1B
*   MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
* ELSE
*
*
IF #RE-CN-NUMBER GE '600000'
  IF #RE-PRM-BILL-EXTRA NE 0
    MOVE #RE-PRM-BILL-EXTRA TO #AMT
    MOVE EDITED #AMT (EM=ZZ,ZZZ.99) TO #AMT-UNPAID
    ADD 1 TO #ISC
    COMPRESS 'Of the total amount due $' #AMT-UNPAID ' represents'
       ' previous unpaid fees.'  INTO #SCR-TEXT (#ISC) LEAVING NO
  END-IF
ELSE
  IF #RE-PRM-BILL-EXTRA NE 0
    MOVE EDITED #RE-PRM-BILL-EXTRA (EM=ZZ,ZZZ.99) TO #AMT-UNPAID
    MOVE #AMT-UNPAID TO #VAR4    /* DW8
*    compute #new-tot-amt-due = #tot-due-n - #re-prm-bill-extra    /* DW99
*    MOVE EDITED #new-tot-amt-due (EM=ZZ,ZZZ.99) TO #var23     /* DW8 DW99
*
    ADD 2 TO #ISC
     COMPRESS 'Of the total amount due $' #AMT-UNPAID ' represents'
       ' a previous unpaid amount.'  INTO #SCR-TEXT (#ISC) LEAVING NO
  END-IF
END-IF
*
IF   (#RE-SUBSECTION = '1')
 AND (#RE-CN-NUMBER LT '600000')
  ADD 1 TO #ISC
  MOVE '***** See enclosed form for a new payment alternative. *****'
                                                     TO #SCR-TEXT (#ISC)
ELSE
  IF #RE-SUBSECTION = '2'
    IGNORE
*      below removed 8/96 as per maggie (allot cards not use any more)
*   ADD 2 TO #ISC
*   MOVE '             *****    NOTICE    *******'  TO #SCR-TEXT (#ISC)
*   ADD 2 TO #ISC
*   MOVE 'Please read and consider the offer made on the enclosed card.'
*                                                    TO #SCR-TEXT (#ISC)
*   ADD 2 TO #ISC
*   MOVE '             *****    NOTICE    *******'  TO #SCR-TEXT (#ISC)
  END-IF
END-IF
*
MOVE #SCR-DUE-LINE1 TO #SCR-DUE-TEXT (1)
MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
MOVE 'Please enter' TO #SCR-DUE-TEXT (3)
MOVE 'amount paid _________.____' TO #SCR-DUE-TEXT (4)
*
IF   (#RE-SUBSECTION = '1')
 AND (#RE-CN-NUMBER LT '600000')
  COMPRESS 'PE-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
ELSE
  IF   (#RE-SUBSECTION = '2')
   AND (#RE-CN-NUMBER LT '600000')
    COMPRESS 'PA-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  ELSE
    COMPRESS 'P-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  END-IF
END-IF
*
IF   (#RE-SUBSECTION = '2')
 AND (#RE-CN-NUMBER LT '600000')
  MOVE 'Y' TO #VAR37
  MOVE 'Yes ___ I am interested' TO #SCR-ALLT-RQST-LN1
  MOVE 'in converting to'        TO #SCR-ALLT-RQST-LN2
  MOVE 'Government Allotment'    TO #SCR-ALLT-RQST-LN3
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PAID-UP-NOTICE   /**Q**
* * * * * * * * * * * * * * * * * *
*
RESET #ALOT-CHG-AUTH #SEND-PDUP-ALOT-FORM                        /* PM10
FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN                      /* PM10
  #ALOT-CHG-AUTH := ST-V.ALOT-CHG-AUTH                           /* PM10
  #ALOT-AMT      := ST-V.ALOT-AMT                                /* PM10
END-FIND                                                         /* PM10
*
ADD 1 TO #NUM-PAID-UP
*
IF  (#RE-SECTION = 'A')      /* MISSING ADDRESS
 OR (#ADDRESS-CHG-CODE = 'U' OR = 'V') /* undeliverable addr
  MOVE FALSE TO #PRINT-NOTICE
ELSE
* IF #RE-SECTION = 'C' /* 0 ZIPS (All go to an AFSC file a/o 12/2005)
*   ADD 1 TO #NUM-AAFMAA
*   ADD 1 TO #NUM-0-ZIP
* ELSE
*   ADD 1 TO #NUM-WES
    ADD 1 TO #NUM-Q
* END-IF
END-IF
*
IF #RE-PDUP-PLAN = '20P'
  MOVE '20 Pay -' TO #PDUP-PLAN
ELSE
  IF #RE-PDUP-PLAN = '30P'
    MOVE '30 Pay -' TO #PDUP-PLAN
  ELSE
    IF #RE-PDUP-PLAN = '7P' OR = '7PF'
      MOVE '7 Pay -' TO #PDUP-PLAN
    ELSE
      IF #RE-PDUP-PLAN = 'OL'
        MOVE 'Ordinary' TO #PDUP-PLAN
      ELSE
        MOVE 'Whole' TO #PDUP-PLAN   /* all flex plans  (2pf/31f/etc)
      END-IF
    END-IF
  END-IF
END-IF
*
IF #RE-PDUP-EFF-DT-TIME LT 0
  ADD 1200 TO #RE-PDUP-EFF-DT-TIME
END-IF
*
COMPUTE #YYYY = (#RE-PDUP-EFF-DT-TIME - 1) / 12
IF #YYYY GT 50
  ADD 1900 TO #YYYY
ELSE
  ADD 2000 TO #YYYY
END-IF
*
COMPUTE #MM = #RE-PDUP-EFF-DT-TIME - (#YY * 12)
COMPRESS #MM-A #RE-PDUP-EFF-DT-DD #YYYY      INTO #MM-DD-YY-1 WITH '/'
MOVE #RE-PDUP-YY TO #YYYY-X
IF #YYYY-X GT 50
  ADD 1900 TO #YYYY-X
ELSE
  ADD 2000 TO #YYYY-X
END-IF
COMPRESS #RE-PDUP-MM #RE-PDUP-DD #YYYY-X INTO #MM-DD-YY-2 WITH '/'
*
COMPRESS 'Your' #PDUP-PLAN 'Life Policy issued ' #MM-DD-YY-1
         'will become fully paid'           INTO #SCR-TEXT (#ISC)
ADD 1 TO #ISC
COMPRESS 'up with no further premiums and/or dues required after'
      #MM-DD-YY-2                           INTO #SCR-TEXT (#ISC)
COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
*
IF #RE-PDUP-TYPE = '5' OR = '6'
  IF #RE-PDUP-TYPE = '5'
    IF #RE-PDUP-AMT-AFTER NE 0
      MOVE EDITED #RE-PDUP-AMT-AFTER (EM=ZZ,ZZZ.99) TO #AMT-5-2
      ADD 1 TO #ISC
      COMPRESS 'You will continue to be billed for $' #AMT-5-2
              ', the remaining' INTO #SCR-TEXT (#ISC) LEAVING NO
      ADD 1 TO #ISC
      MOVE 'premium and/or dues amount.'   TO #SCR-TEXT (#ISC)
    END-IF
  ELSE                /* CASES WHERE AN ALP EXISTS
    ADD 1 TO #ISC
    MOVE EDITED #RE-PDUP-AMT-BEFORE (EM=Z,ZZZ.99) TO #AMT-4-2
    COMPRESS 'The $' #AMT-4-2 ' portion of your allotment to cover'
             ' your monthly premiums' INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'and/or dues will no longer be needed when this policy'
            'becomes paid up, we'           INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'will add this amount to your existing automatic loan'
            'payment every month.'           INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'This will result in an automatic loan payment to reduce'
            'your loan'               INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE EDITED #RE-PDUP-AMT-AFTER (EM=Z,ZZZ.99) TO #AMT-4-2
    COMPRESS 'each month in the amount of $' #AMT-4-2 '.'
                   INTO #SCR-TEXT (#ISC) LEAVING NO
  END-IF
*
  ADD 2 TO #ISC
  MOVE 'Do not hesitate to call upon us if you have any questions'
                                         TO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  MOVE 'concerning your insurance.' TO #SCR-TEXT (#ISC)
ELSE
  COMPRESS #SCR-TEXT (#ISC) 'If' INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  MOVE 'you have not done so please' TO #SCR-TEXT (#ISC)
*
  IF #ALOT-CHG-AUTH NE 'Y'                                       /* PM10
   AND #RE-STATUS = 'R'                                          /* PM10
   AND #RE-PDUP-AMT-AFTER NE #ALOT-AMT                           /* PM10
     COMPRESS #SCR-TEXT (#ISC) 'complete the enclosed form'      /* PM10
          'and return it to' INTO #SCR-TEXT (#ISC)               /* PM10
     ADD +1 TO #ISC                                              /* PM10
     MOVE 'AAFMAA. Once received, we will' TO #SCR-TEXT (#ISC)   /* PM10
     #SEND-PDUP-ALOT-FORM  :=  TRUE                              /* PM10
  END-IF                                                         /* PM10
*
  IF #RE-PDUP-AMT-AFTER NE 0
    MOVE EDITED #RE-PDUP-AMT-BEFORE (EM=Z,ZZZ.99) TO #AMT-4-2
    COMPRESS #SCR-TEXT (#ISC) 'arrange to decrease your allotment'
         'from'            INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS '$' #AMT-4-2 ' to $' INTO #SCR-TEXT (#ISC) LEAVING NO
    MOVE EDITED #RE-PDUP-AMT-AFTER (EM=Z,ZZZ.99) TO #AMT-4-2
    COMPRESS #SCR-TEXT (#ISC) #AMT-4-2 ', the remaining premiums'
        ' and/or dues amount. Upon' INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'notification from the Finance Center, we will balance'
            'your account.'              INTO #SCR-TEXT (#ISC)
  ELSE
    MOVE EDITED #RE-PDUP-AMT-BEFORE (EM=Z,ZZZ.99) TO #AMT-4-2
    COMPRESS #SCR-TEXT (#ISC) 'discontinue your allotment of $'
             #AMT-4-2      INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'as soon as possible. Upon notification from the'
             'Finance Center, we'           INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS  'will balance your account and refund any premiums'
              'and/or dues overpaid.' TO #SCR-TEXT (#ISC)
  END-IF
*
  ADD 1 TO #ISC
  MOVE 'Your cooperation is greatly appreciated.' TO #SCR-TEXT (#ISC)
END-IF
*
ADD 3 TO #ISC
MOVE 'INSURANCE PAID-UP NOTICE' TO #SCR-TEXT (#ISC)
*
COMPRESS 'Q-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ALLOT-START-CHECK-NEEDED-NOTICE   /**ASC/ACC**
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN
  MOVE ST-V.DEBIT-CREDIT-AMT TO #RE-ALLT-DB-CR
  FOR #I1 1 TO 9
    IF   (ST-V.STATUS (#I1) = 'D')
     AND (ST-V.MODE (#I1) = 'G')
     AND (ST-V.PAY-STATUS (#I1) LE '40')
      MOVE ST-V.MODE (#I1) TO #VAR33
      MOVE ST-V.PLAN (#I1) TO #VAR30
      MOVE ST-V.ISSUE-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #VAR3 WITH '/'
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
*
*
ADD 1 TO #NUM-ALLT-STRT-CKN
ADD #RE-ALLT-DB-CR TO #AMT-ALLT-STRT-CKN
*
IF  (#RE-SECTION = 'A')      /* MISSING ADDRESS
 OR (#ADDRESS-CHG-CODE = 'U' OR = 'V') /* undeliverable addr
 OR (ST-V.DEBIT-CREDIT-AMT LT .99)   /* DW5
  MOVE FALSE TO #PRINT-NOTICE
  IF ST-V.DEBIT-CREDIT-AMT LT .99   /* DW5
    WRITE ST-V.ID-NUMBER 'Allot chg Notice bypassed due to DB/CR < .99'
  END-IF
ELSE
  IF #RE-SECTION = 'C'      /* 0 ZIPS
    ADD 1 TO #NUM-AAFMAA
    ADD 1 TO #NUM-0-ZIP
  ELSE
    ADD 1 TO #NUM-WES
*   IF #RE-ZIP-LST-4 = ' '
*     ADD 1 TO #NUM-WES-XX-Z5
*   END-IF
    ADD 1 TO #NUM-ASC
  END-IF
END-IF
*
MOVE EDITED #RE-ALLT-ALLT-AMT (EM=Z,ZZZ.99)  TO #AMT-4-2
MOVE #AMT-4-2 TO #VAR5                                    /* DW8
MOVE EDITED #RE-ALLT-DB-CR    (EM=ZZ,ZZZ.99) TO #AMT-5-2
MOVE #AMT-5-2 TO #VAR7                                    /* DW8
MOVE #C-RUN-DT-MM TO #MON-X
*
MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD
ADD 2 TO #MM
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
MOVE #YYYYMMDD TO #SUSPEND-DATE
COMPRESS #MM-A #DD-A #YYYY INTO #SUS-DATE-MM-DD-YY WITH '/'
RESET #ISS-DT
*
IF #PRINT-NOTICE     /* DW9
  PERFORM UPDATE-SUSPEND-DATE
END-IF
*
MOVE #SUS-DATE-MM-DD-YY TO #MM-DD-YY-1
MOVE #MM-DD-YY-1 TO #VAR6 #VAR2                              /* DW8
*
IF #RE-SUBSECTION = '1'
  COMPRESS 'THE FINANCE CENTER HAS STARTED YOUR $' #AMT-4-2 'ALLOTMENT'
             'FOR CREDIT'                  INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'TO YOUR' #MON-DISP (#MON-X) 'PREMIUMS AND/OR DUES. HOWEVER'
           'THEY ARE IN ARREARS.'  INTO #SCR-TEXT (#ISC)
ELSE
  COMPRESS 'THE FINANCE CENTER HAS CHANGED YOUR ALLOTMENT TO $'
              #AMT-4-2 'FOR'               INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'YOUR' #MON-DISP (#MON-X) 'PREMIUMS AND/OR DUES. HOWEVER'
           'THEY ARE IN ARREARS.'   INTO #SCR-TEXT (#ISC)
END-IF
*
ADD 2 TO #ISC
COMPRESS 'TO BALANCE YOUR ACCOUNT, PLEASE FORWARD A CHECK FOR $'
      #AMT-5-2                            INTO #SCR-TEXT (#ISC)
ADD 1 TO #ISC
COMPRESS 'BY' #MM-DD-YY-1 INTO #SCR-TEXT (#ISC)
COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
*
MOVE 'AMOUNT DUE    $'   TO #SCR-DUE-LINE1A
MOVE #AMT-5-2            TO #SCR-DUE-LINE1B
*
MOVE 'DUE-DATE'       TO #SCR-DUE-LINE2A
MOVE #MM-DD-YY-1      TO #SCR-DUE-LINE2B
ADD 2 TO #ISC
MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
ADD 1 TO #ISC
MOVE #SCR-DUE-LINE2 TO #SCR-TEXT (#ISC)
*
MOVE #SCR-DUE-LINE1 TO #SCR-DUE-TEXT (1)
MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
MOVE 'PLEASE ENTER' TO #SCR-DUE-TEXT (3)
MOVE 'AMOUNT PAID _______.___' TO #SCR-DUE-TEXT (4)
*
IF #RE-SUBSECTION = '1'
  COMPRESS 'ASC-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
ELSE
  COMPRESS 'ACC-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FPF-REMINDER   /**F/A**
* * * * * * * * * * * * * * * *
*
* WRITE #RE-CN-NUMBER 'FPF-REMINDER'
ADD 1 TO #NUM-FPF-REM
ADD #RE-FPF-AMT TO #AMT-FPF-REM
ADD 1 TO #NUM-FPF-REM-FPF
*
IF  (#RE-SECTION = 'A')      /* MISSING ADDRESS
 OR (#ADDRESS-CHG-CODE = 'U' OR = 'V') /* undeliverable addr
  MOVE FALSE TO #PRINT-NOTICE
ELSE
  IF #RE-SECTION = 'C'      /* 0 ZIPS
    ADD 1 TO #NUM-AAFMAA
    ADD 1 TO #NUM-0-ZIP
  ELSE
    ADD 1 TO #NUM-WES           /* DW2
    ADD 1 TO #NUM-F             /* DW2
  END-IF
END-IF
*
COMPRESS #RE-FPF-ISS-DT-YYYY #RE-FPF-ISS-DT-MM
         #RE-FPF-ISS-DT-DD INTO #YYYYMMDD-A LEAVING NO
MOVE #YYYYMMDD TO #ISS-DT-YYYYMMDD
*
RESET #YYYYMMDD #FPF-DUE-DT
FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN   /* DW4
  MOVE DATE-ENTERED (#RE-FPF-SEG-NUM) TO #YYYYMMDD
  MOVE ST-V.MODE    (#RE-FPF-SEG-NUM) TO #VAR33
  MOVE ST-V.PLAN    (#RE-FPF-SEG-NUM) TO #VAR30
END-FIND
*
MOVE *DATN TO #AS-OF-DT
RESET  #SUFFIX
CALLNAT 'CVALUSXX'
  ST-V.ID-NUMBER
  #SUFFIX
  #AS-OF-DT
  #LOAN-BAL   (1:9)
  #LOAN-BAL-2 (1:9)
  #ACC-INT    (1:9)
  #ACC-INT-2  (1:9)
  #CSV        (1:9)
  #CVALUE     (1:9)
  #LVALUE     (1:9)
  #MCSV       (1:9)
  #DB         (1:9)
  #ANN-PREMIUM(1:9)
  #FACE-AMT   (1:9)
  #TPP        (1:9)
*
* DISPLAY #INSURED-CN #RE-FPF-SEG-NUM DATE-ENTERED (#RE-FPF-SEG-NUM)
*    #CSV (#RE-FPF-SEG-NUM)
*
IF #YYYYMMDD = 0                                  /* DW4
  WRITE #RE-CN-NUMBER 'FPF seg num/Date Ent/Iss Dt not fnd-Used Iss Dt'
  MOVE #ISS-DT-YYYYMMDD TO #YYYYMMDD
END-IF
*
IF (#YYYYMM = #LAST-MON-YYYYMM)     /* DW4
 AND (#DD GT 15)
  WRITE #RE-CN-NUMBER 'FPF Notice bypassed due to Date Ent Day gtr 15'
     #YYYYMMDD
  MOVE FALSE TO #PRINT-NOTICE
END-IF
*
IF ((PAD-CODE = 'P' OR = 'V')        /* DW4
 OR ((PAD-CODE = 'D' OR = 'X')
  AND (OWNERSHIP-FLAG (#RE-FPF-SEG-NUM) = 'D')))
  AND #PRINT-NOTICE
  WRITE #RE-CN-NUMBER 'FPF Notice bypassed due to Pending EZ-PAY'
      'pad-code =' PAD-CODE
  MOVE FALSE TO #PRINT-NOTICE
END-IF
*
* IF (#RE-FPF-AMT LT #CSV (#RE-FPF-SEG-NUM))   /* DW7
*   AND #PRINT-NOTICE
*   WRITE #RE-CN-NUMBER 'CSV is > FPF so Notice is bypassed'
*       'CSV =' #CSV (#RE-FPF-SEG-NUM)
*   MOVE FALSE TO #PRINT-NOTICE
* END-IF
*
ADD 2 TO #MM
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
COMPRESS #MM-A #DD-A #YYYY INTO #FPF-DUE-DT WITH '/'
MOVE #FPF-DUE-DT TO #VAR9                                  /* DW8
*
COMPRESS #RE-FPF-ISS-DT-MM #RE-FPF-ISS-DT-DD #RE-FPF-ISS-DT-YYYY
                                INTO #MM-DD-YY-1 WITH '/'
MOVE #MM-DD-YY-1 TO #VAR3                                  /* DW8
MOVE EDITED #RE-FPF-AMT (EM=Z,ZZZ.99)  TO #AMT-4-2
MOVE EDITED #RE-FPF-AMT (EM=ZZ,ZZZ.99) TO #AMT-5-2
MOVE #AMT-5-2 TO #VAR8                                      /* DW8
*
ADD 1 TO #ISC                                    /* DW3
COMPRESS 'When we mailed your policy with issue date' #MM-DD-YY-1
         'we informed you' INTO #SCR-TEXT (#ISC)
*
ADD 1 TO #ISC                                    /* DW3
COMPRESS 'that you had a payment due. To date we have not received'
         'that payment of'  INTO #SCR-TEXT (#ISC)
ADD 1 TO #ISC                                    /* DW3
*
IF MODE (#RE-FPF-SEG-NUM) = 'E'     /* DW6
  COMPRESS '$' #AMT-5-2 '. If your payment is not received it will b'
           'e added to your' INTO #SCR-TEXT (#ISC) LEAVING NO
  ADD 1 TO #ISC                                    /* DW3
  COMPRESS 'first EZ-Pay Draft and if that draft is returned for'
           'insufficient' INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC                                    /* DW3
  COMPRESS 'funds your policy may lapse. Please don"t let this'
           'happen! Your' INTO #SCR-TEXT (#ISC)
ELSE
  COMPRESS '$' #AMT-5-2 '. We regret to tell you that if a payment is n'
           'ot received by' INTO #SCR-TEXT (#ISC) LEAVING NO
  ADD 1 TO #ISC                                    /* DW3
  COMPRESS #FPF-DUE-DT 'your policy will lapse. Please don"t let this'
           'happen! Your' INTO #SCR-TEXT (#ISC)
END-IF
*
ADD 1 TO #ISC                                    /* DW3
COMPRESS 'insurance must be important to you and membership in the'
         'association is' INTO #SCR-TEXT (#ISC)
ADD 1 TO #ISC                                    /* DW3
MOVE     'a valuable asset.' TO #SCR-TEXT (#ISC)
ADD 2 TO #ISC                                    /* DW3
COMPRESS 'If payment has been made, please disregard this notice.'
         'Thank you.'            TO #SCR-TEXT (#ISC)
*
MOVE 'Total Due'      TO #SCR-DUE-LINE1A
MOVE 'Due-Date'       TO #SCR-DUE-LINE2A
MOVE #AMT-5-2     TO #SCR-DUE-LINE1B
MOVE #FPF-DUE-DT  TO #SCR-DUE-LINE2B
ADD 2 TO #ISC
MOVE #SCR-DUE-LINE1 TO #SCR-TEXT (#ISC)
ADD 1 TO #ISC
MOVE #SCR-DUE-LINE2 TO #SCR-TEXT (#ISC)
MOVE #SCR-DUE-LINE1 TO #SCR-DUE-TEXT (1)
MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
MOVE 'PLEASE ENTER' TO #SCR-DUE-TEXT (3)
MOVE 'AMOUNT PAID _______.___' TO #SCR-DUE-TEXT (4)
COMPRESS 'F-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE STOP-ALLOTMENT-NOTICE  /**CCS/CGS/CS/GS**
* * * * * * * * * * * * * * * * * * * *
*
FIND NT-V WITH ID-NUMBER = #RE-CN-NUMBER-N
  FOR #I1 1 TO 60
    IF NOTE-TEXT (#I1) = MASK ('*Mbr in bnkrptcy')
      MOVE FALSE TO #PRINT-NOTICE
      ADD 1 TO #BNK-CNT
      WRITE 'Stop notice bypassed for bnkrptcy' #RE-CN-NUMBER-N
           'count =' #BNK-CNT
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
*
IF #RE-OLD-SECT = 'F'      /* CERTIFIED NOTICEs (cgs/ccs)
  IF #RE-SUBSECTION = '2'   /* NOT ENOUGH AVAIL LOAN VALUE(NO NOTICE)
    WRITE #RE-CN-NUMBER ' STOP ALLOTMENT NO LOAN VALUE'
    ADD 1              TO #NUM-STOP-ALLT-NO-LV
  ELSE
    ADD 1              TO #NUM-CERT-STOP-ALLT
    ADD #RE-ALLT-QTR-PREM TO #AMT-CRT-STOP-ALLT
  END-IF
ELSE
  ADD 1                TO #NUM-STOP-ALLT
  ADD #RE-ALLT-QTR-PREM TO #AMT-STOP-ALLT
END-IF
*
FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN
  FOR #I1 1 TO 9
    IF ST-V.ISSUE-DATE (#I1) = #RE-ALLT-ISS-DT
      MOVE ST-V.MODE (#I1) TO #VAR33
      MOVE ST-V.PLAN (#I1) TO #VAR30
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND

IF #RE-SUBSECTION NE '2'   /* NOT ENOUGH AVAIL LOAN VALUE(NO NOTICE)
  IF  (#RE-SECTION = 'A')      /* MISSING ADDRESS
   OR (#ADDRESS-CHG-CODE = 'U' OR = 'V') /* undeliverable addr
   OR (NOT #PRINT-NOTICE)
    MOVE FALSE TO #PRINT-NOTICE
  ELSE
    IF #RE-SECTION= 'C'
      ADD 1 TO #NUM-AAFMAA
      ADD 1 TO #NUM-0-ZIP
    ELSE
      IF   (#RE-OLD-SECT = 'F')
       AND (#IS-OLD-NEW)
        ADD 1 TO #NUM-AAFMAA    /* (CGS/CCS - old/new)
      ELSE
        ADD 1 TO #NUM-WES      /* (CS/GS) & (CGS/CCS - nxt)
        IF #RE-OLD-SECT = 'F'
          ADD 1 TO #NUM-CCS-CGS  /* only included wes stops e ez-pay
*         IF #RE-ZIP-LST-4 = ' '
*           ADD 1 TO #NUM-WES-XX-Z5  /* no ez card
*         END-IF
        ELSE
          ADD 1 TO #NUM-CS-GS  /* only included wes stops no ez-pay
*         IF #RE-ZIP-LST-4 = ' '
*           ADD 1 TO #NUM-WES-PE-Z5  /* pe since it gets a ez card
*         END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF #RE-OLD-SECT = 'F'      /* ONLY FOR CERTIFIEDS
  MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD  /* COMPUTE SUSPEND-DATE
*
  IF #RE-SUBSECTION NE '2'   /* NOT ENOUGH AVAIL LOAN VALUE(NO NOTICE)
    PERFORM FIND-LAST-DAY-OF-MONTH
  END-IF
*
  MOVE #YYYYMMDD TO #SUSPEND-DATE
  COMPRESS #MM-A #DD-A #YYYY INTO #SUS-DATE-MM-DD-YY WITH '/'
  RESET #ISS-DT
*
  IF #PRINT-NOTICE     /* DW9
    PERFORM UPDATE-SUSPEND-DATE
  END-IF
*
  IF #RE-SUBSECTION = '2'   /* NOT ENOUGH AVAIL LOAN VALUE(NO NOTICE)
    MOVE FALSE TO #PRINT-NOTICE   /* BYPASS NOTICE
  END-IF
END-IF
*
MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD
IF #RE-OLD-SECT = 'F'        /* CERTIFIED NOTICE
  SUBTRACT 2 FROM #MM
ELSE
  SUBTRACT 1 FROM #MM
END-IF
*
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
PERFORM FIND-LAST-DAY-OF-MONTH
COMPRESS #MM-A #DD-A #YYYY  INTO #MM-DD-YY-1 WITH '/'
MOVE #MM-DD-YY-1 TO #VAR25                           /* DW8
*
MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD
IF #RE-OLD-SECT = 'F'        /* CERTIFIED NOTICE
  ADD 1 TO #MM
ELSE
  ADD 2 TO #MM
END-IF
*
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
PERFORM FIND-LAST-DAY-OF-MONTH
COMPRESS #MM-A #DD-A #YYYY  INTO #MM-DD-YY-2 WITH '/'
*
MOVE EDITED #RE-ALLT-DB-CR    (EM=ZZ,ZZZ.99) TO #AMT-5-2
*
COMPUTE #AMT = #RE-ALLT-QTR-PREM / 3
MOVE EDITED #AMT (EM=Z,ZZZ.99)  TO #AMT-4-2
*
MOVE #RE-ALLT-ISS-DT TO #YYYYMMDD       /* DW99
COMPRESS #MM-A #DD-A #YYYY-A INTO #MM-DD-YY-1 WITH '/'
MOVE #MM-DD-YY-1 TO #VAR3                                  /* DW8
*
IF #RE-OLD-SECT = 'F'        /* CERTIFIED NOTICE
  MOVE #LAST-MONTH TO #SCR-DUE-LINE1B
  IF #RE-SUBSECTION = '3'    /* CAP member     ** CCS **
    COMPUTE #AMT = #RE-ALLT-QTR-PREM / 3
    ADD 169.70 TO #AMT  /* assumes a 5000 cap loan
    MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #AMT-4-2-A /* allt stop amt
*
    COMPUTE #AMT = #RE-ALLT-QTR-PREM / 3
*
    IF #RE-ALLT-FIXED-LOAN GE 339.40 /* 2 months of 169.70
      ADD 169.70 TO #AMT  /* assumes a 5000 cap loan
    END-IF
    MOVE #AMT TO #AMT2
*
    MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #AMT-4-2
*
    COMPUTE #AMT = #AMT * 2
    MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #AMT-4-2-2X
*
    COMPUTE #AMT = #AMT2 * 3
    MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #AMT-4-2-3X
*
    COMPRESS 'No reply has been received concerning your $'
        #AMT-4-2-A ' allotment' INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'discontinuance nor a payment needed to pay premiums'
            'and/or dues' INTO #SCR-TEXT (#ISC)
*
    IF #RE-ALLT-FIXED-LOAN GE 339.40 /* should keep paying loan
      ADD 1 TO #ISC
      MOVE 'and loan payments.' TO #SCR-TEXT (#ISC)
      ADD 2 TO #ISC
      COMPRESS 'In the absence of said payment, your membership'
             'and insurance'           INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'will lapse. If we do not hear from you within'
           'thirty (30) days, we'               INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'will take appropriate legal action to collect'
           'your existing loan'          TO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'principal balance plus any accrued interest.'
               'These legal actions may'         INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'require us to inform the IRS of your outstanding'
               'loan principal'   INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      COMPRESS 'and interest accrued to date of lapse as taxable'
               'income to you in'  INTO #SCR-TEXT (#ISC)
      ADD 1 TO #ISC
      MOVE     'this tax year.' TO #SCR-TEXT (#ISC)
      MOVE 'AMOUNT DUE'   TO #SCR-DUE-LINE2A
      MOVE #AMT-4-2-3X    TO #SCR-DUE-LINE2B
      MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
    ELSE /* loan amt less than 339.40
      IF #RE-ALLT-FIXED-LOAN = 0 /* should keep paying loan
        ADD 1 TO #ISC
        COMPRESS 'in the amount of $' #AMT-4-2-2X '.'
                INTO #SCR-TEXT (#ISC) LEAVING NO
        ADD 2 TO #ISC
        MOVE #C-RUN-DT-YYYYMMDD TO #YYYYMMDD
        ADD 1 TO #MM
*
        IF #MM GT 12
          MOVE 1 TO #MM
        END-IF
*
        COMPRESS 'In the absence of said payment by the first of'
           #MON-DISP (#MM)            INTO #SCR-TEXT (#ISC)
        COMPRESS #SCR-TEXT (#ISC) ', your'
             INTO #SCR-TEXT (#ISC) LEAVING NO
        ADD 1 TO #ISC
        MOVE 'membership and insurance will lapse.' TO #SCR-TEXT (#ISC)
        MOVE 'AMOUNT DUE'   TO #SCR-DUE-LINE2A
        MOVE #AMT-4-2-3X    TO #SCR-DUE-LINE2B
        MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
      ELSE    /* loan between 0 & 339.40
        ADD 1 TO #ISC
        COMPRESS 'and to pay off your loan in the amount of $'
          #AMT-4-2-2X '. After your' INTO #SCR-TEXT (#ISC) LEAVING NO
        ADD 1 TO #ISC
        COMPRESS 'loan is paid off, you should restart an allotmen'
          't for $' #AMT-4-2 '.' INTO #SCR-TEXT (#ISC) LEAVING NO
        ADD 2 TO #ISC
        COMPRESS 'In the absence of said payment within thirty (30)'
               'days, your'              INTO #SCR-TEXT (#ISC)
        ADD 1 TO #ISC
        COMPRESS 'membership and insurance will lapse and we will'
             'take appropriate'      INTO #SCR-TEXT (#ISC)
        ADD 1 TO #ISC
        COMPRESS 'legal action to collect your existing loan principal'
             'balance plus any'            TO #SCR-TEXT (#ISC)
        ADD 1 TO #ISC
        COMPRESS 'accrued interest. These legal actions may require'
                 'us to inform the'         INTO #SCR-TEXT (#ISC)
        ADD 1 TO #ISC
        COMPRESS 'IRS of your outstanding loan principal and interest'
                 'accrued to date'  INTO #SCR-TEXT (#ISC)
        ADD 1 TO #ISC
        MOVE     'of lapse as taxable income to you in this tax year.'
                                       TO #SCR-TEXT (#ISC)
        MOVE 'AMOUNT DUE'   TO #SCR-DUE-LINE2A
        MOVE #AMT-4-2-3X    TO #SCR-DUE-LINE2B
        MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
      END-IF
    END-IF
  ELSE                                           /** CGS **
    COMPRESS 'No reply has been received concerning your  $'
              #AMT-4-2             INTO #SCR-TEXT (#ISC) LEAVING NO
    COMPRESS #SCR-TEXT (#ISC) 'allotment'
                              INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'discontinuance nor a premium and/or dues payment due on'
            #LAST-MONTH INTO #SCR-TEXT (#ISC)
    COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 2 TO #ISC
    COMPRESS 'In the absence of dues payment, the automatic loan for'
           'premiums/dues'        INTO #SCR-TEXT (#ISC)
*
    ADD 1 TO #ISC
    COMPRESS 'provision will take effect and there will be interest'
           'charged at'                       INTO #SCR-TEXT (#ISC)
*
    ADD 1 TO #ISC
    MOVE 'the variable interest rate of 7.90% on this loan.'
                                        TO #SCR-TEXT (#ISC)
    ADD 2 TO #ISC
    COMPRESS 'Unless we receive a premium and/or dues payment by'
           #SUS-DATE-MM-DD-YY                  INTO #SCR-TEXT (#ISC)
    COMPRESS #SCR-TEXT (#ISC) ', a loan'
                                    INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPUTE #AMT = #RE-ALLT-QTR-PREM + #RE-ALLT-DB-CR
    MOVE EDITED #AMT (EM=ZZ,ZZZ.99)   TO #AMT-5-2
    COMPRESS 'in the amount of $' #AMT-5-2
                                     INTO #SCR-TEXT (#ISC) LEAVING NO
*   COMPRESS #SCR-TEXT (#ISC) ' (quarterly) will be issued for'
*       'the period'          INTO #SCR-TEXT (#ISC)
*   ADD 1 TO #ISC
*   COMPRESS 'ending' #MM-DD-YY-2 INTO #SCR-TEXT (#ISC)
*   COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
    COMPRESS #SCR-TEXT (#ISC) ' will be issued for the current'
        'quarterly'          INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE 'period.' TO #SCR-TEXT (#ISC)
*
    ADD 2 TO #ISC
    COMPRESS 'Please get in touch with our Member Accounts'
         'Department if you have'        INTO #SCR-TEXT (#ISC)
*
    ADD 1 TO #ISC
    MOVE 'any questions concerning this matter.' TO #SCR-TEXT (#ISC)
  END-IF
*
  ADD 2 TO #ISC
  COMPRESS 'If you are no longer in the military, contact us as soon'
         'as possible to'           INTO #SCR-TEXT (#ISC)
  ADD 1 TO #ISC
  COMPRESS 'arrange an alternate method of payment.'
                                    INTO #SCR-TEXT (#ISC)
  ADD 2 TO #ISC
  COMPRESS 'Please disregard this notice if payment has been made.'
         'Thank You.'               INTO #SCR-TEXT (#ISC)
ELSE                         /* REMINDER NOTICE
  MOVE #RUN-DT-MM-DD-YY TO #SCR-DUE-LINE1B
  MOVE EDITED #RE-ALLT-ALLT-AMT (EM=Z,ZZZ.99) TO #AMT-4-2-A
  MOVE #AMT-4-2-A TO #VAR24 #VAR28                            /* DW8
*
  IF #RE-SUBSECTION = '3'    /* CAP member       ** CS **
    MOVE EDITED #RE-ALLT-FIXED-LOAN (EM=Z,ZZZ.99) TO #PRIN
    MOVE #PRIN TO #VAR29                                   /* DW8
*
    COMPRESS 'We have been advised by the Finance Center of the'
           'discontinuance of'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'your $' #AMT-4-2-A      INTO #SCR-TEXT (#ISC) LEAVING NO
    COMPRESS #SCR-TEXT (#ISC) 'allotment. Your premium and/or dues'
           'are now paid'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'through' #MM-DD-YY-1 INTO #SCR-TEXT (#ISC)
    COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 2 TO #ISC
    COMPRESS 'The CAP Program and the contract you signed require'
             'you to pay by'           INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'military allotment. It is important for you to'
             'restart your allotment'  INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'for $' #AMT-4-2-A '. Please do so as soon as possible!'
                                 INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 2 TO #ISC
    COMPRESS 'Please forward a payment for the interim period. Should'
             'you no longer'           INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'be a member of the military you may choose to pay by'
             'monthly automatic'       INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'bank draft, EZPay. An EZPay authorization is enclosed'
             'for your'   INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'convenience. Please contact us as soon as possible'
             'should you have'       INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'questions or need our assistance in establishing a'
             'repayment plan. The'        INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'current loan balance is $' #PRIN '.'
                                      INTO #SCR-TEXT (#ISC) LEAVING NO
*
    MOVE 'AMOUNT DUE'   TO #SCR-DUE-LINE2A
    MOVE #AMT-4-2-2X    TO #SCR-DUE-LINE2B
    MOVE #SCR-DUE-LINE2 TO #SCR-DUE-TEXT (2)
  ELSE                                                 /** GS **
    COMPRESS 'We have been advised by the Finance Center of the'
             'discontinuance of'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'your $' #AMT-4-2-A      INTO #SCR-TEXT (#ISC) LEAVING NO
    COMPRESS #SCR-TEXT (#ISC) 'allotment. Your premiums and/or dues'
           'are now paid'      INTO #SCR-TEXT (#ISC)
*
    ADD 1 TO #ISC
    COMPRESS 'through' #MM-DD-YY-1 INTO #SCR-TEXT (#ISC)
    COMPRESS #SCR-TEXT (#ISC) '.' INTO #SCR-TEXT (#ISC) LEAVING NO
*
    ADD 2 TO #ISC
    COMPRESS 'To assist you in keeping the coverage current, you'
             'will be billed'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPRESS 'quarterly until you select another method of payment.'
             'You may elect'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    COMPUTE #AMT = #RE-ALLT-QTR-PREM * 4
    MOVE EDITED #AMT (EM=Z,ZZZ.99)   TO #AMT-4-2
    MOVE #AMT-4-2 TO #VAR26                         /* DW8
    COMPRESS 'to pay annually, $' #AMT-4-2 ', semiannully, $'
                                INTO #SCR-TEXT (#ISC) LEAVING NO
    COMPUTE #AMT = #RE-ALLT-QTR-PREM * 2
    MOVE EDITED #AMT (EM=Z,ZZZ.99)   TO #AMT-4-2
    MOVE #AMT-4-2 TO #VAR27                         /* DW8
    COMPRESS #SCR-TEXT (#ISC) #AMT-4-2 ', or monthly by an'
                                INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'automatic bank debit, EZPay. An EZPay authorization'
             'is enclosed for'  INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE     'your convenience.' TO #SCR-TEXT (#ISC)
    MOVE EDITED #RE-ALLT-QTR-PREM (EM=Z,ZZZ.99) TO #AMT-4-2
    MOVE #AMT-4-2 TO #VAR8                          /* DW8
    ADD 2 TO #ISC
    COMPRESS 'The quarterly rate is $' #AMT-4-2 ' as indicated o'
              'n the attached notice.'  INTO #SCR-TEXT (#ISC) LEAVING NO
    ADD 1 TO #ISC
    COMPRESS 'Please forward a payment for the interim period and'
             'indicate a method' INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE     'for future premium payments.' TO #SCR-TEXT (#ISC)
*
    ADD 2 TO #ISC
    COMPRESS 'Please contact us as soon as possible should you'
             'have questions or need'    INTO #SCR-TEXT (#ISC)
    ADD 1 TO #ISC
    MOVE     'our assistance in establishing a repayment plan.'
                                           TO #SCR-TEXT (#ISC)
  END-IF
END-IF
*
MOVE 'DUE-DATE'     TO #SCR-DUE-LINE1A
MOVE #SCR-DUE-LINE1 TO #SCR-DUE-TEXT (1)
*
IF #RE-OLD-SECT = 'F'       /* certified
  MOVE 'PLEASE ENTER' TO #SCR-DUE-TEXT (3)
  MOVE 'AMOUNT PAID _______.___' TO #SCR-DUE-TEXT (4)
ELSE
  IF #RE-SUBSECTION = '3'  /* CS
    COMPUTE #AMT = #RE-ALLT-ALLT-AMT * 2   /* to cover 2 months pmt
    MOVE EDITED #AMT (EM=Z,ZZZ.99)   TO #AMT-4-2
  END-IF
*
  MOVE 'AMOUNT PAYABLE'     TO #SCR-DUE-LINE2A
  MOVE #AMT-4-2             TO #SCR-DUE-LINE2B
  MOVE #SCR-DUE-LINE2  TO #SCR-DUE-TEXT (2)
  MOVE 'PLEASE ENTER'  TO #SCR-DUE-TEXT (3)
  MOVE 'AMOUNT PAID _______.___' TO #SCR-DUE-TEXT (4)
END-IF
*
IF #RE-OLD-SECT =  'F'        /* CERTIFIED NOTICE
  IF #RE-SUBSECTION = '3'        /* cap mbr
    COMPRESS 'CCS-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  ELSE
    COMPRESS 'CGS-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  END-IF
ELSE
  IF #RE-SUBSECTION = '3'        /* cap mbr
    COMPRESS 'CS-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  ELSE
    COMPRESS 'GS-' #RUN-DT-MM-DD-YY INTO #SCR-TYPE-IND LEAVING NO
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-SUSPEND-DATE
* * * * * * * * * * * * * * * * * * *
*
IF #RE-SECTION GT 'J'
CALLNAT 'ATHP07P5' #INSURED-CN
                   #RE-OLD-SECT
                   #RE-SUBSECTION
                   #SUSPEND-DATE
                   #ISS-DT
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-LAST-DAY-OF-MONTH
* * * * * * * * * * * * * * * * * * * * *
*
IF #MM = 02
  MOVE '28' TO #DD-A
ELSE
  IF #MM = 09 OR = 04 OR = 06 OR = 11
    MOVE '30' TO #DD-A
  ELSE
    MOVE '31' TO #DD-A
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GET-ISS-DTS-AND-PREMS
* * * * * * * * * * * * * * * * * * * *
*
RESET #ISS-DU-DT #CALC-PD-TO
RESET #CALC-LAP-DT #IS-MEC-POL
*
FIND (1) ST-V WITH MEMBER-KEY = #INSURED-CN
END-FIND
*
IF #RE-PRM-ISS-DT = 0  /* should never happen
  ESCAPE ROUTINE
END-IF
*
FOR #I2 1 TO 9
  IF  (#RE-PRM-ISS-DT NE ST-V.ISSUE-DATE (#I2))
   OR (#RE-PRM-PLAN   NE ST-V.PLAN (#I2))
    ESCAPE TOP
  END-IF
*
  IF ST-V.MEC-IND (#I2) = 'Y'
    MOVE 'M' TO #VAR32        /* DW8
    MOVE TRUE TO #IS-MEC-POL
  END-IF
*
  IF   (#RE-OLD-SECT = 'P')      /* DW99
   AND (ST-V.X-FIRST-PLACE-FEE (#I2) NE 0)
    WRITE (4) ST-V.ID-NUMBER ST-V.ISSUE-DATE (#I2)
                      'Prem Notice with existing FPF'
  END-IF
*
  ESCAPE BOTTOM
END-FOR
*
IF #I2 GT 9
  WRITE #INSURED-CN 'Prem Due iss date not found'
  ESCAPE ROUTINE
END-IF
*
CALLNAT 'GPDTOSXX'
  ST-V.ISSUE-DATE (#I2)
  ST-V.MONTH-ALLOC-AMT (#I2)
  ST-V.PAID-TO-DATE (#I2)
  #YYYYMMDD
MOVE #YYYYMMDD TO #CALC-PD-TO
*
ADD 2 TO #MM
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
COMPRESS #MM-A #DD-A #YYYY INTO #CALC-LAP-DT WITH '/'
MOVE #CALC-LAP-DT TO #VAR9     /* DW8
MOVE #RE-PRM-ISS-DT TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY INTO #ISS-DU-DT WITH '/'
MOVE #ISS-DU-DT TO #VAR3       /* DW8
*
RESET #SCR-DUE-GRID
ADD 1 TO #ISC
MOVE 'Issue Date'      TO #SCR-ISS-DT
MOVE 'Loan/Lapse-Date' TO #SCR-LAP-DT
MOVE ' Amt Due'        TO #SCR-AMT-DUE
MOVE #SCR-DUE-GRID     TO #SCR-TEXT (#ISC)
*
ADD 1 TO #ISC
MOVE #ISS-DU-DT   TO #SCR-ISS-DT
MOVE #CALC-LAP-DT TO #SCR-LAP-DT
MOVE #RE-PRM-BILLPREM TO #AMT2
MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99) TO #SCR-AMT-DUE
MOVE #SCR-AMT-DUE TO #VAR17   /* DW8
MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
*
IF #RE-PRM-BILLDUES NE 0
  ADD 1 TO #ISC
  RESET #SCR-DUE-GRID
  MOVE 'Dues' TO #SCR-ISS-DT
  MOVE #RE-PRM-BILLDUES TO #AMT2
  MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99-) TO #SCR-AMT-DUE
  MOVE EDITED #AMT2 (EM=-ZZ,ZZZ.99) TO #VAR18    /* DW99
  MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
END-IF
*
IF #RE-PRM-BILLRIDER NE 0
  ADD 1 TO #ISC
  RESET #SCR-DUE-GRID
  MOVE 'Rider Policies' TO #SCR-ISS-DT
  MOVE #RE-PRM-BILLRIDER TO #AMT2
  MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99-) TO #SCR-AMT-DUE
  MOVE EDITED #AMT2 (EM=-ZZ,ZZZ.99) TO #VAR19   /* DW99
  MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
END-IF
*
IF #RE-PRM-BILLPRIN NE 0
  ADD 1 TO #ISC
  RESET #SCR-DUE-GRID
  MOVE 'Loan Principal' TO #SCR-ISS-DT
  MOVE #RE-PRM-BILLPRIN TO #AMT2
  MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99-) TO #SCR-AMT-DUE
*  MOVE EDITED #AMT2 (EM=-ZZ,ZZZ.99) TO #var20    /* DW11
  MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
END-IF
*
IF #RE-PRM-BILLINTR NE 0
  ADD 1 TO #ISC
  RESET #SCR-DUE-GRID
  MOVE 'Loan Interest' TO #SCR-ISS-DT
  MOVE #RE-PRM-BILLINTR TO #AMT2
  MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99-) TO #SCR-AMT-DUE
* MOVE EDITED #AMT2 (EM=-ZZ,ZZZ.99) TO #var21        /* DW11
  MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
END-IF
*
COMPUTE #CAPLOAN = #RE-PRM-BILLPRIN + #RE-PRM-BILLINTR    /* DW11
IF #CAPLOAN NE 0
  MOVE EDITED #CAPLOAN (EM=-ZZ,ZZZ.99) TO #VAR20
END-IF
*
IF #RE-PRM-PREMADJ NE 0
  ADD 1 TO #ISC
  RESET #SCR-DUE-GRID
  MOVE 'Bill-Adj' TO #SCR-ISS-DT
  MOVE #RE-PRM-PREMADJ TO #AMT2
  MOVE EDITED #AMT2 (EM=ZZ,ZZZ.99-) TO #SCR-AMT-DUE
  MOVE EDITED #AMT2 (EM=-ZZ,ZZZ.99) TO #VAR22
  MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
END-IF
*
ADD 1 TO #ISC
RESET #SCR-DUE-GRID
MOVE 'AMOUNT PAYABLE' TO #SCR-ISS-DT
MOVE #SCR-DUE-LINE1B  TO #SCR-AMT-DUE
MOVE #SCR-AMT-DUE TO #VAR23   /* DW8
MOVE #SCR-DUE-GRID TO #SCR-TEXT (#ISC)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GRACE-PERIOD
* * * * * * * * * * * * * * * *
ADD 1 TO #ISC
COMPRESS 'AAFMAA policies have a 60-day grace period. If not'
         'received, the' INTO #SCR-TEXT (#ISC)
ADD 1 TO #ISC
COMPRESS 'provision as outlined in the contract for non-payment'
         'goes into effect.'      INTO #SCR-TEXT (#ISC)
*
END-SUBROUTINE
*
END
