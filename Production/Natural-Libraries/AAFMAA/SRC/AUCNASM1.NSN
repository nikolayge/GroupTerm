* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* AUCNASM1
*
DEFINE DATA
PARAMETER USING AUCAAS01
PARAMETER
1 #POOL-ID   (N8)
1 #USER-ID   (A) DYNAMIC
LOCAL
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 ADDR-TYPE-CD    (A1)
  2 ADDR-LOCATION
  2 ADDRESS-SOURCE
*
1 J          (I4)
1 #GET       (I4) CONST <0>
1 #ADD       (I4) CONST <1>
1 #UPDATE    (I4) CONST <2>
1 #DELETE    (I4) CONST <3>
1 BLANK      (A1) CONST <' '>
*
END-DEFINE
*
FOR J 1 TO C@AddressContacts
  AddressShare.AddressPoolID (J) := #POOL-ID
  AddressShare.User (J) := #USER-ID
  IF RelationID (J) > 0             /* Delete or Update
    IF AddressTypeCode (J) = BLANK  /* Delete
      AddressShare.ActionType (J) := #DELETE
      CALLNAT 'AUCNARM1' AddressRelation (J)
    ELSE                            /* Update or NOP
      GET ADDR-REL-V AddressShare.RelationID (J)
      IF ADDR-TYPE-CD NE AddressShare.AddressTypeCode (J)
          OR ADDR-LOCATION     NE AddressShare.AddressLocationCode (J)
          OR ADDRESS-SOURCE    NE AddressShare.Source (J)
*      OR #ADDR-REL.ATTENTION-LINE    NE ADDR-REL-V.ATTENTION-LINE
*      OR #ADDR-REL.RETURNED-MAIL-IND NE ADDR-REL-V.RETURNED-MAIL-IND
*      OR #ADDR-REL.PREVENT-OVER-IND  NE ADDR-REL-V.PREVENT-OVER-IND
*      OR #ADDR-REL.STATUS            NE ADDR-REL-V.STATUS
        AddressShare.ActionType (J) := #UPDATE
        CALLNAT 'AUCNARM1' AddressRelation (J)
      ELSE                          /* NOP
        IGNORE
      END-IF
    END-IF
  ELSE                               /* Add or NOP
    IF AddressTypeCode (J) NE BLANK  /* Add
      AddressShare.ActionType (J) := #ADD
      CALLNAT 'AUCNARM1' AddressRelation (J)
    ELSE                            /* NOP
      IGNORE
    END-IF
  END-IF
END-FOR
*
END
