* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* F-GL-ACCOUNT
* Returns general Ledger account number based on Plan and SG category field
***************************************************************************
* MODIFICATION LOG
* USER   DATE      TAG     REASON
***************************************************************************                                                                      *
* YAK    04202014          Create program
***************************************************************************
*
DEFINE FUNCTION F-GL-ACCOUNT RETURNS (A/2) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #ID-NUMBER    (N6)
  1 #POL-SEG      (N1)
  1 #SG-CATEGORY  (A2)
  1 #POL-PLAN     (A3) OPTIONAL
  LOCAL USING OBJLGENT
  LOCAL
  1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 PLAN(9)
  1 #SEC-KEY       (A7)
  1 #PLAN          (A3)
  END-DEFINE
*
IF #SG-CATEGORY = 'SP'
  #PLAN := 'ALL'
ELSE
  IF NOT (#POL-PLAN SPECIFIED)
    FIND(1) ST-V WITH ST-V.ID-NUMBER = #ID-NUMBER
      #PLAN := ST-V.PLAN(#POL-SEG)
    END-FIND
  ELSE
    #PLAN := #POL-PLAN
  END-IF
END-IF
*
DECIDE FOR FIRST CONDITION
  WHEN #PLAN = MASK(NN'P') OR #PLAN = MASK(N'P') OR #PLAN = MASK(NN'F') OR #PLAN = MASK(N'F') OR #PLAN = 'OL'
    #PLAN := '99W'
  WHEN F-LT2-TRUE(<#PLAN>)
    #PLAN := 'LT2'
  WHEN NONE
    IGNORE
END-DECIDE

COMPRESS FULL 'GLNB' #PLAN INTO #SEC-KEY LEAVING NO
FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF SUBSTR(GEN-ALPHA-VALUE,1,2) = #SG-CATEGORY
  F-GL-ACCOUNT(1) := GEN-NUMBER-VALUE
  F-GL-ACCOUNT(2) := ADDL-INFO
  ESCAPE ROUTINE
END-FIND
RESET F-GL-ACCOUNT(*)
*
END-FUNCTION
END
