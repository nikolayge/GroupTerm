* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID  SAGSRT13      Sort Used by G5000PTR for sorting TR Detail lines by G/L #
************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11252005  RH1   SS - Created for implementing sorting algorithm *
* DRW 03/26/2008 DW1 Used for G5000PTR for Detail lines
* DRW 05/05/2008 DW2 Streamline sort linkage
* PAM 02/09/2010 PM3 Increased occurences from 30 to 50
************************************************************************
DEFINE DATA PARAMETER
**
1 #SRT-TABLE        (A101/50)              /* PM3
*
1 #SRT-PARM
  2 #SRT-SORT-TYPE    (A1)
**
LOCAL
**
1 #SRT-WORK         (A101/50)              /* PM3
1 REDEFINE #SRT-WORK
  2 #SRT-ROW        (A101/1:50)            /* PM3
1 REDEFINE #SRT-WORK
  2 #SRT-REC            (50)               /* PM3
    3 #SRT-KEY      (A5)
    3 #SRT-FILL     (A96)
*
1 #I                (P7)
1 #ROWS             (P7)
1 #DONE             (L)  INIT <FALSE>
1 #TEMP-ROW         (A101)
*
END-DEFINE
*
*  first eliminate any empty rows
FOR #I 1 TO 50                             /* PM3
   IF  #SRT-TABLE (#I) = ' '
    ESCAPE TOP
  ELSE
    ADD 1 TO #ROWS
    MOVE #SRT-TABLE (#I) TO #SRT-WORK (#ROWS)
  END-IF
END-FOR
*
IF #ROWS = 0
  ESCAPE ROUTINE
END-IF
**
DECIDE ON FIRST VALUE OF #SRT-SORT-TYPE
**
  VALUE 'A'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
        IF #SRT-KEY (#I) LT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  VALUE 'D'
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT AVAILABLE - EXECUTION TERMINATED'
    STOP
**
  NONE VALUE
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT RECOGNIZED - EXECUTION TERMINATED'
    STOP
**
END-DECIDE
**
RESET #SRT-TABLE (*)
FOR #I EQ 1 TO #ROWS
  MOVE #SRT-WORK (#I) TO #SRT-TABLE (#I)
END-FOR
*
END
