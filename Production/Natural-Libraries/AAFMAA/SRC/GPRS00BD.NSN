* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-BDCHEK                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Check beneficiary for contact ID notification         *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    04/13/2010  None    Program Written                           *
* BZ     03/28/2011  <N/A>   Converted to subprogram                   *
* PAM    04/11/201   PM1     Look at all designations                  *
* NGG    20121010    NG1     CN Expansion Project - beneficiary data are
*                            moved to new database files               *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID              (N8)
1 #BD-LINKS                (N6/100)
1 #BD-TYPE                 (A10/100)   OPTIONAL    /* PM1
1 #BD-POL                  (A16/100,7) OPTIONAL    /* PM1
*
LOCAL USING OBJLSTAT                               /* PM1
LOCAL
*
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
*
1 CT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
*
1 DRAFT VIEW OF A-BENE-DRAFT
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
*
  2 NOTE-DATE          (N8/60)    /* PM1
  2 REDEFINE NOTE-DATE            /* PM1
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7)
      4 BENE-SPLIT-IND    (N1/7)
      4 BENE-LST-UPD      (N8)
      4 BENE-STRT-PEND    (N8)
      4 BENE-FOL-UP-IND   (N1)
      4 BENE-OWN-TRNSF-DT (N8)
      4 BENE-SUC-OWN      (N1)
      4 BENE-NEW-LAYOUT   (N1)
      4 BENE-FIL1         (A4)
1 FINAL VIEW OF A-BENE-FINAL
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
*
  2 NOTE-DATE          (N8/60)
  2 REDEFINE NOTE-DATE
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7)
      4 BENE-SPLIT-IND    (N1/7)
      4 BENE-LST-UPD      (N8)
      4 BENE-STRT-PEND    (N8)
      4 BENE-FOL-UP-IND   (N1)
      4 BENE-OWN-TRNSF-DT (N8)
      4 BENE-SUC-OWN      (N1)
      4 BENE-NEW-LAYOUT   (N1)
      4 BENE-FIL1         (A4)
*
1 #MEMBER-TABLE            (100)
  2 #MEMBER-ID              (N6)
  2 #DESIG-TYPE             (A10)   /* PM1
  2 #POL-NUMS               (A16/7) /* PM1
*
1 #INDEXES
  2 #MEMBER-I               (I4)
  2 #NOTE-I                 (I2)
  2 #I1                     (I2)
  2 #I2                     (I2)
  2 #I3                     (I2)
  2 #BD-I                   (I2)    /* PM1
  2 #POL-I                  (I2)    /* PM1
*
1 #FOUND                   (L)
1 #WORK-ID                 (N6)
*
1 #WORK-POL-NUM            (A16)   /* PM1
1 #WORK-DATE               (N6)    /* PM1
1 REDEFINE #WORK-DATE              /* PM1
  2 #WORK-YY                (A2)    /* PM1
  2 REDEFINE #WORK-YY               /* PM1
    3 #WORK-YY-N             (N2)    /* PM1
  2 #WORK-MM                (A2)    /* PM1
  2 #WORK-DD                (A2)    /* PM1
*
1 #WORK-ISSUE-DATE         (A8)    /* PM1
1 REDEFINE #WORK-ISSUE-DATE        /* PM1
  2 #WORK-ISSUE-DATE-N      (N8)    /* PM1
*
END-DEFINE
*
************************************************************************
*
RESET #BD-LINKS (*)
*
FIND CR-V WITH CR-V.CONTACT-2-ID = #CONTACT-ID
  FIND (1) CT-V WITH CT-V.CONTACT-ID = CR-V.CONTACT-1-ID
    #WORK-ID := CT-V.ID-NUMBER
    PERFORM FIND-BD
  END-FIND
END-FIND
*
#BD-LINKS (*)   := #MEMBER-ID (*)
#BD-TYPE  (*)   := #DESIG-TYPE (*)     /* PM1
#BD-POL   (*,*) := #POL-NUMS (*,*)     /* PM1
*
************************************************************************
DEFINE SUBROUTINE FIND-BD
************************************************************************
*
IF #WORK-ID GE 600000
  ESCAPE ROUTINE
END-IF
*
FIND (1) DRAFT WITH ID-NUMBER = #WORK-ID
  FOR #I1 1 TO 6
    FOR #I2 1 TO 2
      RESET #FOUND
      FOR #I3 1 TO 5
        IF DRAFT.BENE-CONTACT-ID-R (#I1,#I2,#I3) = #CONTACT-ID
          ADD +1 TO #MEMBER-I
          #MEMBER-ID (#MEMBER-I) := #WORK-ID
          #FOUND := TRUE
          PERFORM DETERMINE-TYPE
          FIND (1) ST-V WITH ST-V.ID-NUMBER = #WORK-ID
            FOR #BD-I 1 TO 7
              IF DRAFT.BENE-ISS-DT (#I1,#BD-I) = 0
                ESCAPE BOTTOM
              END-IF
              #WORK-DATE := DRAFT.BENE-ISS-DT (#I1,#BD-I)
              IF #WORK-YY-N LT 20
                COMPRESS '20' #WORK-YY #WORK-MM #WORK-DD INTO #WORK-ISSUE-DATE LEAVING NO
              ELSE
                COMPRESS '19' #WORK-YY #WORK-MM #WORK-DD INTO #WORK-ISSUE-DATE LEAVING NO
              END-IF
              FOR #POL-I 1 TO C*ACTIVE-SEGMENTS
                IF ST-V.ISSUE-DATE (#POL-I) = #WORK-ISSUE-DATE-N
                    AND ST-V.SPLIT-IND (#POL-I) = DRAFT.BENE-SPLIT-IND (#I1,#BD-I)
                  RESET #WORK-POL-NUM
                  CALLNAT 'GPLNMSXX'
                    #WORK-POL-NUM
                    ST-V.ID-NUMBER
                    #POL-I
                  #POL-NUMS (#MEMBER-I,#BD-I) := #WORK-POL-NUM
                  ESCAPE BOTTOM
                END-IF
              END-FOR
            END-FOR
          END-FIND
          ESCAPE BOTTOM
        END-IF
      END-FOR
      IF #FOUND
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FOR
END-FIND
*
FIND (1) FINAL WITH ID-NUMBER = #WORK-ID
  FOR #I1 1 TO 6
    FOR #I2 1 TO 2
      RESET #FOUND                 /* PM1
      FOR #I3 1 TO 5
        IF FINAL.BENE-CONTACT-ID-R (#I1,#I2,#I3) = #CONTACT-ID
          ADD +1 TO #MEMBER-I
          #MEMBER-ID (#MEMBER-I) := #WORK-ID
          #FOUND := TRUE
*
          PERFORM DETERMINE-TYPE   /* PM1
          FIND (1) ST-V WITH ST-V.ID-NUMBER = #WORK-ID
            FOR #BD-I 1 TO 7
              IF FINAL.BENE-ISS-DT (#I1,#BD-I) = 0
                ESCAPE BOTTOM
              END-IF
              #WORK-DATE := FINAL.BENE-ISS-DT (#I1,#BD-I)
              IF #WORK-YY-N LT 20
                COMPRESS '20' #WORK-YY #WORK-MM #WORK-DD INTO #WORK-ISSUE-DATE LEAVING NO
              ELSE
                COMPRESS '19' #WORK-YY #WORK-MM #WORK-DD INTO #WORK-ISSUE-DATE LEAVING NO
              END-IF
              FOR #POL-I 1 TO C*ACTIVE-SEGMENTS
                IF ST-V.ISSUE-DATE (#POL-I) = #WORK-ISSUE-DATE-N
                    AND ST-V.SPLIT-IND (#POL-I) = FINAL.BENE-SPLIT-IND (#I1,#BD-I)
                  RESET #WORK-POL-NUM
                  CALLNAT 'GPLNMSXX'
                    #WORK-POL-NUM
                    ST-V.ID-NUMBER
                    #POL-I
                  #POL-NUMS (#MEMBER-I,#BD-I) := #WORK-POL-NUM
                  ESCAPE BOTTOM
                END-IF
              END-FOR
            END-FOR
          END-FIND
          ESCAPE BOTTOM
        END-IF
      END-FOR
      IF #FOUND
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FOR
END-FIND
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-TYPE                                 /* PM1
************************************************************************
*
IF #I2 = 1
  #DESIG-TYPE (#MEMBER-I)  := 'Primary'
ELSE
  #DESIG-TYPE (#MEMBER-I)  := 'Alternate'
END-IF
*
END-SUBROUTINE
*
END
