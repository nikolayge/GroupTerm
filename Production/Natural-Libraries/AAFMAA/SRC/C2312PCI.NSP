* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2312PCI
*    CI SCREEN 'LTCC' PROCESSING
*
************************************************
*                       MODIFICATION LOG
* USER   DATE      TAG     REASON
*
* vxt  02/22/2018 vt1 /Ticket HD-6299/ Initial creation
******************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
* * LOCAL USING CONTPDA1
LOCAL
1 #PAY-STAT-XX                    (A2)
1 #I2                             (I2)
*
1 #TR-REG-OLD-LTCC-GR
  2 #TR-REG-OLD-LTCC-LBL           (A08) INIT <'CI-LTCC'>
  2 #TR-REG-OLD-LTCC-5-AREAS               (5)
    3 #TR-REG-OLD-LTCC-LABEL       (A10)
    3 #TR-REG-OLD-LTCC-FILL-1      (A01)
    3 #TR-REG-OLD-LTCC-DATA        (A14)
1 REDEFINE #TR-REG-OLD-LTCC-GR
  2 #TR-REG-OLD-LTCC               (A125)
*
1 #TR-REG-NEW-LTCC-GR
  2 #TR-REG-NEW-LTCC-LBL           (A08) INIT <'LTCC '>
  2 #TR-REG-NEW-LTCC-5-AREAS               (5)
    3 #TR-REG-NEW-LTCC-LABEL       (A10)
    3 #TR-REG-NEW-LTCC-FILL-1      (A01)
    3 #TR-REG-NEW-LTCC-DATA        (A14)
1 REDEFINE #TR-REG-NEW-LTCC-GR
  2 #TR-REG-NEW-LTCC               (A125)
*
1 #OLD-STAT-XXXX     (A4)
1 #INVALID           (L)
1 #ERR-MSG           (A60)
*
END-DEFINE
*
RESET #PAY-STAT-XX
CALLNAT 'GPYSTSXX' #PAY-STAT-XX #SCR-STATUS (#ISC)
  #INVALID #ERR-MSG
*
MOVE #FILE-MATCHING-INDX (#ISC) TO #I2
* DISPLAY #ISC #I2 #SCR-STATUS (#ISC) CI-V.PAY-STATUS (#I2)

FOR #I1 1 TO 9
  IF #SCR-STATUS (#I1) = 'PODB'
    reset  #SCR-DB (#I1)
  END-IF
END-FOR
*
* DISPLAY #SCR-DB (#ISC) #TOT-DB
*
if (#scr-status(#isc) = 'PODB') and (CI-v.pay-status(#i2) ne '43')
  #scr-msg-line-1 := 'Cannot use ''PODB'' pay status for policy that is not LTCSO'
  escape routine
end-if
*
IF CI-V.ONLINE-ACTION-GN = '1' OR = '2' OR = '3'     /* ALREADY FLAGGED
  IGNORE
ELSE
  MOVE  '4' TO CI-V.ONLINE-ACTION-GN     /* INDICATE ACTION THIS MONTH
END-IF
*
CALLNAT 'GPYSTSXX' CI-V.PAY-STATUS (#IFL) #OLD-STAT-XXXX
  #INVALID #ERR-MSG
*
IF #OLD-STAT-XXXX NE #SCR-STATUS (#ISC)
  MOVE TRUE TO #REC-CHGD
  ADD 1 TO #ITR2
  MOVE 'PAY-STATUS'  TO #TR-REG-OLD-LTCC-LABEL (#ITR2)
    #TR-REG-NEW-LTCC-LABEL (#ITR2)
  MOVE #OLD-STAT-XXXX TO #TR-REG-OLD-LTCC-DATA (#ITR2)
  MOVE #SCR-STATUS (#ISC) TO #TR-REG-NEW-LTCC-DATA (#ITR2)
  MOVE #TR-REG-OLD-LTCC TO TR-REG-V.OLD-DATA (#ITR1)
  MOVE #TR-REG-NEW-LTCC TO TR-REG-V.NEW-DATA (#ITR1)
  PERFORM CHECK-FULL-TRAILER
*
  RESET CI-V.PAY-STATUS (#IFL)
  CALLNAT 'GPYSTSXX' CI-V.PAY-STATUS (#IFL) #SCR-STATUS (#ISC)
    #INVALID #ERR-MSG
END-IF
*
IF CI-V.CASH-VALUE (#IFL) NE #SCR-FACE-AMT (#ISC)
  MOVE TRUE TO #REC-CHGD
  ADD 1 TO #ITR2
  MOVE 'CASH-VALUE'  TO #TR-REG-OLD-LTCC-LABEL (#ITR2)
    #TR-REG-NEW-LTCC-LABEL (#ITR2)
  MOVE EDITED CI-V.CASH-value(#IFL) (EM=ZZZZZZ9.99)  TO #TR-REG-OLD-LTCC-DATA (#ITR2)
  MOVE #SCR-FACE-AMT (#ISC)                          TO #TR-REG-NEW-LTCC-DATA (#ITR2)
  MOVE #TR-REG-OLD-LTCC TO TR-REG-V.OLD-DATA (#ITR1)
  MOVE #TR-REG-NEW-LTCC TO TR-REG-V.NEW-DATA (#ITR1)
  PERFORM CHECK-FULL-TRAILER
*
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.CASH-VALUE            (#IFL)
  move #scr-face-amt (#isc) to ci-v.nxt-mon-cash-value    (#ifl)  
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.min-CASH-VALUE        (#IFL)
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.nxt-yr-min-CASH-VALUE (#IFL)
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.acc-CASH-VALUE        (#IFL)
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.beg-CASH-VALUE        (#IFL)
  MOVE #SCR-FACE-AMT (#ISC) TO CI-V.last-ann-cash-value   (#IFL)
  MOVE #SCR-FACE-AMT (#ISC) TO #SCR-DB                    (#ISC)
END-IF
*
IF CI-V.death-benefit (#IFL) NE #SCR-db (#ISC)
  MOVE TRUE TO #REC-CHGD
  ADD 1 TO #ITR2
  MOVE 'DB-AMT'  TO #TR-REG-OLD-LTCC-LABEL (#ITR2)
    #TR-REG-NEW-LTCC-LABEL (#ITR2)
  MOVE EDITED CI-V.death-benefit(#IFL) (EM=ZZZZZZ9.99) TO #TR-REG-OLD-LTCC-DATA (#ITR2)
  MOVE EDITED #SCR-db (#ISC)   (EM=ZZZZZZ9.99) TO #TR-REG-NEW-LTCC-DATA (#ITR2)
  MOVE #TR-REG-OLD-LTCC TO TR-REG-V.OLD-DATA (#ITR1)
  MOVE #TR-REG-NEW-LTCC TO TR-REG-V.NEW-DATA (#ITR1)
  PERFORM CHECK-FULL-TRAILER
*
  MOVE #SCR-db (#ISC) TO CI-V.DEATH-benefit (#IFL)
  MOVE CI-V.death-benefit (#IFL) TO ci-v.nxt-mon-death-benefit(#ifl)
END-IF
*
 
IF #ITR2 NE 0
  ADD 1 TO #ITR1
  MOVE 0 TO #ITR2
END-IF
*
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-FULL-TRAILER
* * * * * * * * * * * * * * * * * *
*
IF #ITR2 = 5
  ADD  1 TO #ITR1
  MOVE 0 TO #ITR2
  RESET #TR-REG-OLD-LTCC
  RESET #TR-REG-NEW-LTCC
  MOVE 'CI-CR' TO #TR-REG-OLD-LTCC-LBL
  MOVE 'LTCC'  TO #TR-REG-NEW-LTCC-LBL
END-IF
*
END-SUBROUTINE /*(0136)
*
END
