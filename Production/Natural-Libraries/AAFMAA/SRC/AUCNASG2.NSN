* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNASG2.
/** Only when it is an error
/** :author nguentchev
/*
DEFINE DATA
PARAMETER
1 #XML-OUTPUT          (A) DYNAMIC
PARAMETER USING AUCAAS01
LOCAL USING AUCAAD01
LOCAL USING AUCACR01
LOCAL
1 CONTACT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID (N8)
  2 ROLE-1-CD    (A1)
  2 CONTACT-2-ID (N8)
  2 ROLE-2-CD    (A1)
1 #XML-TEMP      (A) DYNAMIC
1 #GET           (I4) CONST <0>
1 J              (I4)
END-DEFINE
*
#XML-OUTPUT := ' '
COMPRESS #XML-OUTPUT '<?xml version="1.0" encoding="' *CODEPAGE '"?>'
  INTO #XML-OUTPUT LEAVING NO
*
COMPRESS FULL #XML-OUTPUT  '<AddressShare'
  '>' INTO #XML-OUTPUT LEAVING NO
COMPRESS  #XML-OUTPUT  '<ErrorCode'
  '>' INTO #XML-OUTPUT LEAVING NO
COMPRESS NUMERIC #XML-OUTPUT
  '99'
  '</ErrorCode>' INTO #XML-OUTPUT LEAVING NO
COMPRESS  #XML-OUTPUT
  '<ErrorDescription'
  '>' INTO #XML-OUTPUT LEAVING NO
COMPRESS  #XML-OUTPUT
  'There is address relation update error'
  '</ErrorDescription>' INTO #XML-OUTPUT LEAVING NO
*
RESET Address
Address.AddressPoolID := AddressShare.AddressPoolID (1)
Address.ContactID     := PageContactID
Address.ActionType    := #GET
CALLNAT 'AUCNADM1' Address
RESET #XML-TEMP
CALLNAT 'AUCNADS1' #XML-TEMP Address
COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
*
COMPRESS #XML-OUTPUT '<AddressContacts>' INTO #XML-OUTPUT
FOR J 1 TO C@AddressContacts
  COMPRESS #XML-OUTPUT '<AddressContact'
    '>' INTO #XML-OUTPUT LEAVING NO
  COMPRESS #XML-OUTPUT '<AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
  CALLNAT 'AUCNARS1' #XML-TEMP AddressRelation (J)
  COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
  COMPRESS #XML-OUTPUT  '</AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
  FIND CONTACT-REL-V WITH CONTACT-1-ID = PageContactID
      AND CONTACT-2-ID = AddressShare.ContactId (J)
    RESET ContactRelation
    ContactRelation.RelationID    := *ISN (0064)
    ContactRelation.ContactID     := CONTACT-REL-V.CONTACT-2-ID
    ContactRelation.ActionType    := #GET
    CALLNAT 'AUCNCRM1' ContactRelation
    CALLNAT 'AUCNCRS1' #XML-TEMP ContactRelation
    COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
  END-FIND
  COMPRESS FULL #XML-OUTPUT  '</AddressContact'
    '>' INTO #XML-OUTPUT LEAVING NO
END-FOR
COMPRESS FULL #XML-OUTPUT  '</AddressContacts'
  '>' INTO #XML-OUTPUT LEAVING NO
*
COMPRESS FULL #XML-OUTPUT  '</AddressShare'
  '>' INTO #XML-OUTPUT LEAVING NO
*
END
