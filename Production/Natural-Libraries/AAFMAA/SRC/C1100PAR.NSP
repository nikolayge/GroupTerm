* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1100PAR
* THIS PROGRAM PROCESSES THE 'AR' (APPL TRACK) CROSS-REFERENCE SCREEN
*
* MODIFICATIONS:
*
*  0989  ADD MEMBER ID TO SCREEN   PER MITCHELL   JAK
*  0592  ALLOW SUFFIX DE FOR DATA ENTRY VIEW      JAK
*  05/2000 - CR SECURITY FOR COMPANIES            PM1
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX        /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
1 #SERVICES          (A1) INIT<'S'>  /* PM1
1 #INSURANCE         (A1) INIT<'I'>  /* PM1
1 #BOTH              (A1) INIT<'B'>  /* PM1
1 #GRANDFATHERED     (A1) INIT<'G'>  /* PM1
1 #BLANK             (A1) INIT<' '>  /* PM1
1 #SERVICES-AT-FLAG  (A1) INIT<'V'>  /* PM1
1 #SCR-SELECT-CMND  (A2/18)
1 #SCR-MSG         (A50)
1 #S-NAME          (A25)
1 #SCR-SSN         (A9/18)
1 REDEFINE #SCR-SSN
 2 #SCR-SSN-N (N9/18)
* #SCR-ACTION-FLAG (A1/18)
1 #SCR-APPL-STATUS (A2/18)
1 #SCR-NAME        (A25/18)
1 #SCR-REC-DATE    (A8/18)
1 #SCR-PLAN        (A2/18)
1 #SCR-APPL-SOURCE (A7/18)
1 #SCR-TYPE        (A1/18)
1 #SCR-INQ-ID      (A6/18)
1 REDEFINE #SCR-INQ-ID
 2 #SCR-INQ-ID-N   (N6/18)
1 I (I1)
1 #I1 (I2)
1 #HOLD-REC-DATE   (A8)
1 APPL-TRACK VIEW OF A-APPL-TRACKING
  2 SSN              (N9)
  2 NAME             (A25)
  2 APPL-SOURCE      (A7)
  2 CAP-IND
  2 DATE-RECEIVED    (N8)
  2 REDEFINE DATE-RECEIVED
  3 DR-CC            (A2)
  3 DR-YY            (A2)
  3 DR-MM            (A2)
  3 DR-DD            (A2)
  2 ACTION-FLAG      (A1)
  2 AT-STATUS        (A2)
  2 ID-NUMBER        (N6)
  2 MEMBER-CN        (A6)
  2 POLICY-TYPE      (A1)
  2 INTRO-FLAG       (A1)
  2 INS-ONLY         (A1)
*
1 #CR-NAME-SSN     (A1)
1 #START-AT        (A25)
1 #ONLY-CAP        (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX             /* NATURAL ERROR ROUTINE
*
INPUT #CR-NAME-SSN #START-AT
*
IF #CR-NAME-SSN = 'S'
  FETCH 'C1150PAX' #START-AT
ELSE
  MOVE #START-AT TO #S-NAME
END-IF
*
IF #CGA-SCR-SUFFIX = 'CAP'
  MOVE TRUE TO #ONLY-CAP
END-IF
*
RESET #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPEAT
*
  IF #CGA-SCR-COMMAND NE ' '
    FETCH 'G1000PXX'
  END-IF
*
  ASSIGN I = 1
*
  READ APPL-TRACK LOGICAL WITH NAME STARTING FROM #S-NAME
*
    DECIDE FOR FIRST CONDITION      /* PM1
      WHEN #CGA-USER-COMPANY = #BOTH
        IGNORE
      WHEN #CGA-USER-COMPANY = #SERVICES
        IF APPL-TRACK.INTRO-FLAG NE #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
      WHEN NONE
        IF APPL-TRACK.INTRO-FLAG EQ #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
    END-DECIDE
*
    IF #ONLY-CAP
*    AND (APPL-SOURCE NE 'CAP')
     AND (CAP-IND NE 'Y')
      ESCAPE TOP
    END-IF
*
    IF ID-NUMBER GE 600000
      ESCAPE TOP
    END-IF
*
    MOVE EDITED SSN (EM=999999999) TO #SCR-SSN(I)
    ASSIGN #SCR-NAME(I)   = NAME
*   ASSIGN #SCR-ACTION-FLAG(I)   = ACTION-FLAG
    ASSIGN #SCR-APPL-STATUS(I)   = AT-STATUS
    COMPRESS DR-MM '/' DR-DD '/' DR-YY INTO #SCR-REC-DATE(I)
       LEAVING NO SPACE
*
    DECIDE ON FIRST APPL-TRACK.POLICY-TYPE
      VALUE 'A' MOVE 'OL' TO #SCR-PLAN (I)
      VALUE 'B' MOVE '30' TO #SCR-PLAN (I)
      VALUE 'C' MOVE '20' TO #SCR-PLAN (I)
      VALUE 'D' MOVE 'SP' TO #SCR-PLAN (I)
      VALUE 'E' MOVE '7P' TO #SCR-PLAN (I)
      VALUE 'F' MOVE 'RT' TO #SCR-PLAN (I)
      VALUE 'G' MOVE 'LT' TO #SCR-PLAN (I)
      VALUE 'H' MOVE 'SO' TO #SCR-PLAN (I)
      NONE IGNORE
    END-DECIDE
*
    IF #CGA-USER-COMPANY = #SERVICES
      IF NOT INS-ONLY = #BLANK OR = #SERVICES
        MOVE INS-ONLY TO #SCR-TYPE (I)
      END-IF
    ELSE
      IF INS-ONLY = 'I'
        IF INTRO-FLAG = 'C'
          MOVE 'P' TO #SCR-TYPE (I)
        ELSE
          MOVE 'O' TO #SCR-TYPE (I)
        END-IF
      ELSE
        MOVE INTRO-FLAG TO #SCR-TYPE (I)
      END-IF
    END-IF
*
    MOVE APPL-SOURCE TO #SCR-APPL-SOURCE (I)
*
*   IF  ((ACTION-FLAG = 'I') AND (ID-NUMBER NE 0))
*    OR  (MEMBER-CN = ' ')
    IF ID-NUMBER NE 0
      MOVE EDITED ID-NUMBER(EM=ZZZZZZ) TO #SCR-INQ-ID(I)
    ELSE
      MOVE MEMBER-CN TO #SCR-INQ-ID(I)
    END-IF
*
    IF I GE 18
      ESCAPE BOTTOM
    ELSE
      ADD 1 TO I
    END-IF
*
  END-READ /* (0110)
*
  IF I LT 18
    ASSIGN #SCR-NAME(I) = '** END OF DATA **'
    ASSIGN #CGA-SCR-COMMAND = 'AR'
  END-IF
*
  ASSIGN #S-NAME = #SCR-NAME(18)
  INPUT TEXT #SCR-MSG MARK *#S-NAME USING MAP 'C1100MAR'
*
  FOR #I1 1 TO 18
    IF #SCR-SELECT-CMND (#I1) NE ' '
      IF #SCR-SELECT-CMND (#I1) = 'X'
        MOVE 'AT' TO #CGA-SCR-COMMAND
      ELSE
        MOVE #SCR-SELECT-CMND (#I1) TO #CGA-SCR-COMMAND
      END-IF
*
      IF #SCR-INQ-ID-N (#I1) NE 0
        MOVE #SCR-INQ-ID-N (#I1) TO #CGA-SCR-ID
      ELSE
        MOVE #SCR-SSN-N (#I1) TO #CGA-SCR-ID
      END-IF
    END-IF
  END-FOR
*
  RESET #SCR-MSG
*
  RESET #SCR-SSN         (*)
  RESET #SCR-NAME        (*)
* RESET #SCR-ACTION-FLAG (*)
  RESET #SCR-APPL-STATUS (*)
  RESET #SCR-REC-DATE    (*)
  RESET #SCR-INQ-ID      (*)
  RESET #SCR-PLAN        (*)
  RESET #SCR-TYPE        (*)
  RESET #SCR-APPL-SOURCE (*)
*
END-REPEAT /* (0087)
END
