* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPYET2 - exclude existing CN's
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
************************************************************************
DEFINE DATA
LOCAL
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
*
1 #YETM-OUT-EXISTS  (A200)
1 #YETM-OUT-GONE    (A200)
1 #YETM-EXTRACT     (A200)
1 REDEFINE #YETM-EXTRACT
 2 #YE-CN4      (N4)
 2 #YE-CN4-REST (A196)
1 REDEFINE #YETM-EXTRACT
 2 #YE-CN5      (N5)
 2 #YE-CN5-REST (A195)
1 REDEFINE #YETM-EXTRACT
 2 #YE-CN6      (N6)
 2 #YE-CN6-REST (A194)
*
1 #YE-REST      (A196)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER            (N6)
 2 PROCESS-IND
 2 INTRO-FLAG
 2 ISSUE-DATE     (1)
*
1 #COUNTERS
  2 #RECS-READ        (P5)
  2 #RECS-WRITTEN     (P5)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
1 #ERR-MSG   (A60)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A        (A4)
  2 #MM-A          (A2)
  2 #DD-A          (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM        (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY          (N4)
    3 #MM            (N2)
  2 #DD            (N2)
*
1 #LAST-ID           (N6)
1 #STAT-NOT-FND       (L)
1 #NUM-ST-EXISTS      (P7)
1 #CN    (N6)
1 REDEFINE #CN
 2 #CN-A    (A6)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
* * * * * * * REPORT HEADER * * * * * *
*
FORMAT  LS=133 PS=58
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Y/E Terminated Extract Exceptions'
                        5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=58
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Y/E Terminated Extract'
                        5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=58
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Y/E Terminated Extract - ST exists'
                        5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ WORK 1 RECORD #YETM-EXTRACT
* IF #RECS-READ GT 1900
*   ESCAPE BOTTOM
* END-IF
*
  IF #YETM-EXTRACT = MASK ('CN')
    ESCAPE TOP
  END-IF
  ADD +1 TO #RECS-READ
*
  IF #YE-CN6 = MASK (NNNNNN)
    MOVE #YE-CN6 TO #CN
    MOVE #YE-CN6-REST TO #YE-REST
  ELSE
    IF #YE-CN5 = MASK (NNNNN)
      MOVE #YE-CN5 TO #CN
      MOVE #YE-CN5-REST TO #YE-REST
    ELSE
      MOVE #YE-CN4 TO #CN
      MOVE #YE-CN4-REST TO #YE-REST
    END-IF
  END-IF
*
  COMPRESS #CN-A #YE-REST INTO #YETM-OUT-EXISTS LEAVING NO
  COMPRESS #CN-A #YE-REST INTO #YETM-OUT-GONE LEAVING NO
*
  RESET #STAT-NOT-FND
  FIND (1) ST-V WITH MEMBER-KEY = #CN-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = #CN-A
        IF NO RECORDS FOUND
          ADD 1 TO #CNT-2
          DISPLAY (2) #CNT-2
              'CN'        #CN
              'REMAINING' #YE-REST
               (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
*
          WRITE WORK FILE 2 #YETM-OUT-GONE
          ADD 1 TO #RECS-WRITTEN
          MOVE TRUE TO #STAT-NOT-FND
        END-NOREC
      END-FIND
    END-NOREC
  END-FIND
*
  IF #STAT-NOT-FND
    ESCAPE TOP
  END-IF
*
  WRITE WORK FILE 3 #YETM-OUT-EXISTS
  ADD 1 TO #NUM-ST-EXISTS
  ADD 1 TO #CNT-3
  DISPLAY (3) #CNT-3
    'CN' ST-V.ID-NUMBER
    'Int/Flg' ST-V.INTRO-FLAG
    'Prc/Ind' ST-V.PROCESS-IND
    'Iss/Date' ST-V.ISSUE-DATE (1)
*
END-WORK
*
WRITE 'Recs-read =' #RECS-READ
WRITE 'num-recs  ='  #RECS-WRITTEN
WRITE 'ST exists ='  #NUM-ST-EXISTS
*
END
