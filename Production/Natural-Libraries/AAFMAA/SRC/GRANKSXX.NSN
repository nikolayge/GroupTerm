* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GRANKSXX
*    DOES THE RANK FIELD EDIT
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG REASON
*
* PAM  09212004 PM1 Allow applications to be created for Navy
*                   and Coast Guard, but no New Members
*                   (existing members only) per Lisa Millman.
* DRW  09202006 DW2 IF INPUT RANK-DISPLAY is 'SBS' Return GRADE (O6)
*                   in RANK-PASS
* DRW  11292006  DW3 Replace O11 with O10 of SBS processing
* DRW 07/29/2008 DW4 Chg Rank Test for NM and AT
* bz  10/2/2014  bz  add CDT for NAVY
************************************************************************
*
DEFINE DATA PARAMETER
1 #PASS-COMMAND  (A2)
1 #RANK-PASS     (A3)
1 #MIL-ID-PASS   (A3)
1 #INVALID       (L)
1 #RANK-PAY-TBL-DISPL (P2)
1 #RANK-LITERAL-PASS  (A23)
1 #RANK-DISPLAY       (A5)
*
LOCAL          /*          PAY FULL
1 #RANK-TABLE  /* RNK  LBL DSP RANK    ARMY/AIRF /MARINE/NAVY/CST GRD
 2 #R1  (A64)INIT<'GA -O11-01-General                -'
                                    -'GEN  -GEN  -GEN  -     -     '>
 2 #R2  (A64)INIT<'GEN-O10-02-General                -'
                                    -'GEN  -GEN  -GEN  -     -     '>
 2 #R3  (A64)INIT<'LTG-O9 -03-General                -'
                                    -'LTG  -LGen -LTGEN-     -     '>
 2 #R4  (A64)INIT<'MG -O8 -04-General                -'
                                    -'MG   -MGen -MGEN -     -     '>
 2 #R5  (A64)INIT<'BG -O7 -05-General                -'
                                    -'BG   -BGen -BGEN -     -     '>
 2 #R6  (A64)INIT<'COL-O6 -06-Colonel                -'
                                    -'COL  -COL  -COL  -     -     '>
 2 #R7  (A64)INIT<'LTC-O5 -07-Colonel                -'
                                    -'LTC  -LtCol-LTCOL-     -     '>
 2 #R8  (A64)INIT<'MAJ-O4 -08-Major                  -'
                                    -'MAJ  -MAJ  -MAJ  -     -     '>
 2 #R9  (A64)INIT<'CPT-O3 -09-Captain                -'
                                    -'CPT  -Capt -CAPT -     -     '>
 2 #R10 (A64)INIT<'1LT-O2 -10-Lieutenant             -'
                                    -'1LT  -1LT  -1STLT-     -     '>
 2 #R11 (A64)INIT<'2LT-O1 -11-Lieutenant             -'
                                    -'2LT  -2LT  -2NDLT-     -     '>
 2 #R12 (A64)INIT<'CDT-O1 -11-Cadet                  -'
                                    -'CDT  -CDT  -     -CDT  -     '>  /* bz
 2 #R13 (A64)INIT<'O3E-O3 -12-Captain                -'
                                    -'CPT  -CPT  -CPT  -LT   -LT   '>
 2 #R14 (A64)INIT<'O2E-O2 -13-Lieutenant             -'
                                    -'1LT  -1LT  -1LT  -LJG  -LJG  '>
 2 #R15 (A64)INIT<'O1E-O1 -14-Lieutenant             -'
                                    -'2LT  -2LT  -2LT  -ENS  -ENS  '>
 2 #R16 (A64)INIT<'CW5-WO5-15-Chief Warrant Officer  -'
                                    -'CW5  -CW5  -CWO5 -CWO5 -CWO5 '>
 2 #R17 (A64)INIT<'MW4-WO4-16-Warrant Officer        -'
                                    -'MW4  -MW4  -     -     -     '>
 2 #R18 (A64)INIT<'CW4-WO4-16-Chief Warrant Officer  -'
                                    -'CW4  -CW4  -CWO4 -CWO4 -CWO4 '>
 2 #R19 (A64)INIT<'CW3-WO3-17-Chief Warrant Officer  -'
                                    -'CW3  -CW3  -CWO3 -CWO3 -CWO3 '>
 2 #R20 (A64)INIT<'CW2-WO2-18-Chief Warrant Officer  -'
                                    -'CW2  -CW2  -CWO2 -CWO2 -CWO2 '>
 2 #R21 (A64)INIT<'WO1-WO1-19-Warrant Officer        -'
                                    -'WO1  -WO1  -     -     -     '>
 2 #R22 (A64)INIT<'SMA-E10-30-Sergeant Major         -'
                                    -'SMA  -     -     -     -     '>
 2 #R23 (A64)INIT<'SGM-E9 -20-Sergeant Major         -'
                                    -'SGM  -     -     -     -     '>
 2 #R24 (A64)INIT<'CSM-E9 -20-Sergeant Major         -'
                                    -'CSM  -     -     -     -     '>
 2 #R25 (A64)INIT<'MSG-E8 -21-Sergeant               -'
                                    -'MSG  -     -     -     -     '>
 2 #R26 (A64)INIT<'1SG-E8 -21-First Sergeant         -'
                                    -'1SG  -     -     -     -     '>
 2 #R27 (A64)INIT<'SFC-E7 -22-Sergeant               -'
                                    -'SFC  -     -     -     -     '>
 2 #R28 (A64)INIT<'PSG-E7 -22-Sergeant               -'
                                    -'PSG  -     -     -     -     '>
 2 #R29 (A64)INIT<'SSG-E6 -23-Sergeant               -'
                                    -'SSG  -     -     -     -     '>
 2 #R30 (A64)INIT<'SGT-E5 -24-Sergeant               -'
                                    -'SGT  -     -     -     -     '>
 2 #R31 (A64)INIT<'CPL-E4 -25-Corporal               -'
                                    -'CPL  -     -     -     -     '>
 2 #R32 (A64)INIT<'SPC-E4 -25-Specialist             -'
                                    -'SPC  -     -     -     -     '>
 2 #R33 (A64)INIT<'PFC-E3 -26-Private                -'
                                    -'PFC  -     -     -     -     '>
 2 #R34 (A64)INIT<'PV2-E2 -27-Private                -'
                                    -'PV2  -     -     -     -     '>
 2 #R35 (A64)INIT<'PV1-E1 -28-Private                -'
                                    -'PV1  -     -     -     -     '>
 2 #R36 (A64)INIT<'ADM-O10-02-Admiral                -'
                                    -'     -     -     -ADM  -ADM  '>
 2 #R37 (A64)INIT<'VDM-O9 -03-Admiral                -'
                                    -'     -     -     -VADM -VADM '>
 2 #R38 (A64)INIT<'RDM-O8 -04-Admiral                -'
                                    -'     -     -     -RADM -RADM '>
 2 #R39 (A64)INIT<'RDL-O7 -05-Admiral                -'
                                    -'     -     -     -RADM -RADM '>
 2 #R40 (A64)INIT<'CPN-O6 -06-Captain                -'
                                    -'     -     -     -CAPT -CAPT '>
 2 #R41 (A64)INIT<'CDR-O5 -07-Commander              -'
                                    -'     -     -     -CDR  -CDR  '>
 2 #R42 (A64)INIT<'LCD-O4 -08-Commander              -'
                                    -'     -     -     -LCDR -LCDR '>
 2 #R43 (A64)INIT<'LT -O3 -09-Lieutenant             -'
                                    -'     -     -     -LT   -LT   '>
 2 #R44 (A64)INIT<'LJG-O2 -10-Lieutenant             -'
                                    -'     -     -     -LTJG -LTJG '>
 2 #R45 (A64)INIT<'ENS-O1 -11-Ensign                 -'
                                    -'     -     -     -ENS  -ENS  '>
 2 #R46 (A64)INIT<'O11-O11-01-                       -'
                                    -'GEN  -GEN  -GEN  -     -     '>
 2 #R47 (A64)INIT<'O10-O10-02-                       -'
                                    -'GEN  -GEN  -GEN  -ADM  -ADM  '>
 2 #R48 (A64)INIT<'O9 -O9 -03-                       -'
                                    -'LTG  -LGen -LTGEN-VADM -VADM '>
 2 #R49 (A64)INIT<'O8 -O8 -04-                       -'
                                    -'MG   -MGen -MGEN -RADM -RADM '>
 2 #R50 (A64)INIT<'O7 -O7 -05-                       -'
                                    -'BG   -BGen -BGEN -RADM -RADM '>
 2 #R51 (A64)INIT<'O6 -O6 -06-                       -'
                                    -'COL  -COL  -COL  -CAPT -CAPT '>
 2 #R52 (A64)INIT<'O5 -O5 -07-                       -'
                                    -'LTC  -LtCol-LTCOL-CDR  -CDR  '>
 2 #R53 (A64)INIT<'O4 -O4 -08-                       -'
                                    -'MAJ  -MAJ  -MAJ  -LCDR -LCDR '>
 2 #R54 (A64)INIT<'O3 -O3 -09-                       -'
                                    -'CPT  -Capt -CAPT -LT   -LT   '>
 2 #R55 (A64)INIT<'O2 -O2 -10-                       -'
                                    -'1LT  -1LT  -1STLT-LTJG -LTJG '>
 2 #R56 (A64)INIT<'O1 -O1 -11-                       -'
                                    -'2LT  -2LT  -2NDLT-ENS  -ENS  '>
 2 #R57 (A64)INIT<'E10-E10-30-                       -'
                                    -'SMA  -CMSAF-SGTMA-MCPON-MCPOG'>
 2 #R58 (A64)INIT<'E9 -E9 -20-                       -'
                                    -'SGM  -CMSgt-MGYSG-MCPO -MCPO '>
 2 #R59 (A64)INIT<'E9S-E9 -20-                       -'
                                    -'     -     -SGTMA-     -     '>
 2 #R60 (A64)INIT<'E8 -E8 -21-                       -'
                                    -'MSG  -SMSgt-1STSG-SCPO -SCPO '>
 2 #R61 (A64)INIT<'E8S-E8 -21-                       -'
                                    -'     -     -MSGT -     -     '>
 2 #R62 (A64)INIT<'E7 -E7 -22-                       -'
                                    -'SFC  -MSgt -GYSGT-CPO  -CPO  '>
 2 #R63 (A64)INIT<'E6 -E6 -23-                       -'
                                    -'SSG  -TSgt -SSGT -PO1  -PO1  '>
 2 #R64 (A64)INIT<'E5 -E5 -24-                       -'
                                    -'SGT  -SSgt -SGT  -PO2  -PO2  '>
 2 #R65 (A64)INIT<'E4 -E4 -25-                       -'
                                    -'CPL  -SRA  -CPL  -PO3  -PO3  '>
 2 #R66 (A64)INIT<'E3 -E3 -26-                       -'
                                    -'PFC  -A1C  -LCPL -SN   -SN   '>
 2 #R67 (A64)INIT<'E2 -E2 -27-                       -'
                                    -'PV2  -AMN  -PFC  -SA   -SA   '>
 2 #R68 (A64)INIT<'E1 -E1 -28-                       -'
                                    -'PV1  -AB   -PVT  -SR   -SR   '>
 2 #R69 (A64)INIT<'CAF-E10-30-Chief                  -'
                                    -'     -CMSAF-     -     -     '>
 2 #R70 (A64)INIT<'CMS-E9 -20-Chief                  -'
                                    -'     -CMSgt-     -     -     '>
 2 #R71 (A64)INIT<'SMS-E8 -21-Sergeant               -'
                                    -'     -SMSgt-     -     -     '>
 2 #R72 (A64)INIT<'MSG-E7 -22-Sergeant               -'
                                    -'     -MSgt -     -     -     '>
 2 #R73 (A64)INIT<'TSG-E6 -23-Sergeant               -'
                                    -'     -TSgt -     -     -     '>
 2 #R74 (A64)INIT<'SSG-E5 -24-Sergeant               -'
                                    -'     -SSgt -     -     -     '>
 2 #R75 (A64)INIT<'SRA-E4 -25-Airman                 -'
                                    -'     -SRA  -     -     -     '>
 2 #R76 (A64)INIT<'A1C-E3 -26-Airman                 -'
                                    -'     -A1C  -     -     -     '>
 2 #R77 (A64)INIT<'AMN-E2 -27-Airman                 -'
                                    -'     -AMN  -     -     -     '>
 2 #R78 (A64)INIT<'AB -E1 -28-Airman                 -'
                                    -'     -AB   -     -     -     '>
 2 #R79 (A64)INIT<'DR -   -99-Dr.                    -'
                                    -'     -     -     -     -     '>
 2 #R80 (A64)INIT<'MR -   -99-Mr.                    -'
                                    -'     -     -     -     -     '>
 2 #R81 (A64)INIT<'MRS-   -99-Mrs.                   -'
                                    -'     -     -     -     -     '>
 2 #R82 (A64)INIT<'MS -   -99-Ms.                    -'
                                    -'     -     -     -     -     '>
 2 #R83 (A64)INIT<'REV-   -99-Reverend               -'
                                    -'     -     -     -     -     '>
 2 #R84 (A64)INIT<'HON-   -99-Honorable              -'
                                    -'     -     -     -     -     '>
1 REDEFINE #RANK-TABLE
 2 #RANK-OCC        (84)
  3 #RANK-3    (A3)
  3 #FILL-1    (A1)
  3 #LABEL     (A3)
  3 #FILL-2    (A1)
  3 #PAY-DISPL (N2)
  3 #FILL-3    (A1)
  3 #FULL-RANK (A23)
  3 #FILL-4    (A1)
  3 #RANK-5-A  (A5)
  3 #FILL-5    (A1)
  3 #RANK-5-F  (A5)
  3 #FILL-6    (A1)
  3 #RANK-5-M  (A5)
  3 #FILL-7    (A1)
  3 #RANK-5-N  (A5)
  3 #FILL-8    (A1)
  3 #RANK-5-C  (A5)
*
1 #I1       (I2)
1 #INDEX    (I2)
1 #SBS-PROCESS   (L)
*
END-DEFINE
*
* WRITE 'top of GRANKSXX' #MIL-ID-PASS #RANK-PASS #RANK-PAY-TBL-DISPL
*                         #RANK-DISPLAY
*
IF #RANK-DISPLAY = 'SBS' OR = 'SBW'
  MOVE TRUE TO #SBS-PROCESS
END-IF
*
RESET #INVALID
FOR #I1 1 TO 84
  IF #RANK-PASS = ' '  /* find rank from displacement
    IF   (#MIL-ID-PASS = MASK ('N'))   /* navy
*   IF   (#MIL-ID-PASS = MASK ('N'))   /* navy or NOA
*    OR  (#MIL-ID-PASS = MASK ('C'))   /* Coast Guard
*    OR  (#MIL-ID-PASS = MASK ('P'))   /* Phs
     AND (#RANK-PAY-TBL-DISPL GE  2)   /* officer displ
     AND (#RANK-PAY-TBL-DISPL LE 11)
     AND ((#I1 LT 46) OR (#I1 GT 68))   /* not Oxx & Exx
     AND #I1 LT 36    /* 1ST NAVY OFFICER RANK
      ESCAPE TOP
    END-IF
*
    IF  ((#MIL-ID-PASS = MASK ('F'))   /* airforce
     OR  (#MIL-ID-PASS = MASK ('M'))   /* Marines
     OR  (#MIL-ID-PASS = MASK ('N'))   /* Navy
     OR  (#MIL-ID-PASS = MASK ('CG'))) /* Coast-guard
     AND (#RANK-PAY-TBL-DISPL GE 20)   /* enlisted displ
     AND (#RANK-PAY-TBL-DISPL LE 30)
     AND ((#I1 LT 46) OR (#I1 GT 68))   /* not Oxx & Exx
     AND #I1 LT 69    /* 1st airforce enlisted rank
      ESCAPE TOP
    END-IF
*
* DISPLAY #RANK-PAY-TBL-DISPL #MIL-ID-PASS
    IF   (#PAY-DISPL (#I1) = #RANK-PAY-TBL-DISPL)
     AND ((#I1 GE 46) AND (#I1 LE 68))
      MOVE #I1 TO #INDEX
      IF #MIL-ID-PASS = MASK ('N')   /* navy
        MOVE #RANK-5-N (#I1) TO #RANK-PASS #RANK-DISPLAY
      ELSE
        IF #MIL-ID-PASS = MASK ('F')   /* air force
          MOVE #RANK-5-F (#I1) TO #RANK-PASS #RANK-DISPLAY
        ELSE
          IF #MIL-ID-PASS = MASK ('C')   /* coast-guard
            MOVE #RANK-5-C (#I1) TO #RANK-PASS #RANK-DISPLAY
          ELSE
            IF #MIL-ID-PASS = MASK ('M')   /* marine
              MOVE #RANK-5-M (#I1) TO #RANK-PASS #RANK-DISPLAY
            ELSE
              MOVE #RANK-5-A (#I1) TO #RANK-PASS #RANK-DISPLAY
            END-IF
          END-IF
        END-IF
      END-IF
*
      ESCAPE BOTTOM
    END-IF
*
    IF   (#PAY-DISPL (#I1) = #RANK-PAY-TBL-DISPL)
     AND (#PAY-DISPL (#I1) NE 0)
     AND ((#I1 LT 46) OR (#I1 GT 68))   /* not Oxx & Exx
      MOVE #RANK-3 (#I1) TO #RANK-PASS #RANK-DISPLAY
      MOVE #I1 TO #INDEX
      ESCAPE BOTTOM
    END-IF
  ELSE
    IF #RANK-3 (#I1) = #RANK-PASS  /* find  displ/rank-display from rank
      IF   (#MIL-ID-PASS = MASK ('F'))    /* AIR-FORCE
       AND (#RANK-PASS = 'MSG' OR = 'SSG') /* RANKS WITH DUPLICATION
       AND #I1 LT 69
        ESCAPE TOP
      END-IF
*
      MOVE #I1 TO #INDEX
      ESCAPE BOTTOM
    END-IF
  END-IF
END-FOR
*
IF #INDEX = 0
  MOVE #RANK-PASS TO #RANK-DISPLAY #RANK-LITERAL-PASS
  MOVE TRUE TO #INVALID
  ESCAPE ROUTINE
END-IF
*
IF #SBS-PROCESS                  /* DW2
  MOVE #LABEL (#INDEX) TO #RANK-DISPLAY #RANK-PASS /* USE GRADE (O?/E?)
*
  IF #RANK-PASS = 'O11'  /* Never show O11    DW3
    MOVE 'O10' TO #RANK-DISPLAY #RANK-PASS
  END-IF
*
* WRITE 'replaced rank with grade' #RANK-PASS
  ESCAPE ROUTINE
ELSE
  IF #MIL-ID-PASS = MASK ('F')     /* air-force
    MOVE #RANK-5-F (#INDEX) TO #RANK-DISPLAY
  ELSE
    IF #MIL-ID-PASS = MASK ('M')     /* marines
      MOVE #RANK-5-M (#INDEX) TO #RANK-DISPLAY
    ELSE
      IF  (#MIL-ID-PASS = MASK ('N'))     /* navy
       OR (#MIL-ID-PASS = 'PHS' OR = 'NOA')
        MOVE #RANK-5-N (#INDEX) TO #RANK-DISPLAY
      ELSE
        IF #MIL-ID-PASS = MASK ('C')     /* coast-guard
          MOVE #RANK-5-C (#INDEX) TO #RANK-DISPLAY
        ELSE
          IF   (#MIL-ID-PASS = 'XXX') /* mil-id not know (frmr husband)
           AND (#RANK-5-A (#INDEX) = ' ') /* not an army rank
           AND (#RANK-5-N (#INDEX) NE ' ')       /* is a navy rank
            MOVE #RANK-5-N (#INDEX) TO #RANK-DISPLAY  /*( NAVY
          ELSE
            MOVE #RANK-5-A (#INDEX) TO #RANK-DISPLAY  /* army
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
MOVE #PAY-DISPL (#INDEX) TO #RANK-PAY-TBL-DISPL
*
IF   (#RANK-DISPLAY = ' ')
 AND (#RANK-PAY-TBL-DISPL NE 99)    /*  civilian
  MOVE TRUE TO #INVALID
END-IF
*
IF #FULL-RANK (#INDEX) NE ' '
  MOVE #FULL-RANK (#INDEX) TO #RANK-LITERAL-PASS
ELSE
  IF #RANK-DISPLAY NE ' '
    MOVE #RANK-DISPLAY TO #RANK-LITERAL-PASS
  ELSE
    MOVE #RANK-PASS TO #RANK-LITERAL-PASS
  END-IF
END-IF
*
* for AFSC #pass-command will not be NM/AT on nm/at command       
IF   (#PASS-COMMAND = 'NM' OR = 'AT')  /* verify rank on new    /* PM1 DW4
* IF   (#PASS-COMMAND = 'NM')  /* verify rank on new members        /* PM1
* AND (#INDEX GE 36) AND (#INDEX LE 68) /* navy/coast grd/oxx/exx /* DW4  
 AND (#INDEX GE 46) AND (#INDEX LE 68) /* oxx/exx
  MOVE TRUE TO #INVALID                /* NOT ALLOWED
END-IF
*
END
