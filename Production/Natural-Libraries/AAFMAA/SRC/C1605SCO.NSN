* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1605SCO - Access Authorization & Ownership Verification
******************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  03052005 DW1  Add AL (Annuity Life) PROCESSING
* NGG  20121002 NG1  CN Expansion Project -
*                    New beneficiary database file
* TMT  10142014 TT1  ANNUITYLife Harmonization
******************************************************************
*
DEFINE DATA
*
PARAMETER
*
1 #CGA-ISN           (P8)
1 #CGA-SCR-COMMAND   (A2)
1 #CGA-SCR-ID        (N9)
1 #CGA-SCR-SUFFIX    (A3)
1 #CGA-ID-G          (A6)
1 REDEFINE #CGA-ID-G
  2 #CGA-ID-N         (N6)
1 #CGA-CLERK         (A3)
1 #FORMATED-NAME (A35)
*
LOCAL
*
1 #SCR-LINES    (A78/9)
1 #SCR-LINES-CV (C/9)
1 #SCR-ACCESS   (A78/2)
1 #SCR-MR-ACC   (A1)
1 #SCR-MR-ACS   (A1)
*
1 #CERT-USED       (L/9)
1 #ISS-DATE        (N8/9)
1 REDEFINE #ISS-DATE
  2 #ISS-DATE-OCC      (9)
    3 #ISS-DT-CENT    (N2)
    3 #ISS-DT-YYMMDD  (N6)
1 #POL-NUM         (A14/9)
1 #FACE-DISP       (A8/9)
*
1 ST-X VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME
*
1 OBSOLETE-STATUS-FIELDS
  2 DATE-OF-BIRTH (N8)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID   /* NG2
  2 INTRO-FLAG
  2 ACCESS-AUTH-CODE
* 2 SEX
  2 SSN
  2 NAME
  2 NAME-SP
  2 STATUS-CODE-SP
  2 STATUS          (9)
  2 ISSUE-DATE      (9)
  2 SPLIT-IND       (9)
  2 CONV-DURATION   (9)
  2 CASH-VALUE      (9)
  2 FACE-AMOUNT     (9)
  2 NUM-MBRSHIPS    (9)
  2 EXPIRY-DAY      (9)
  2 PAY-STATUS      (9)
  2 PLAN            (9)
  2 REDEFINE PLAN
    3 PLAN-A1        (A1/9)
    3 PLAN-A2        (A2/9)
  2 FIRS-LOCATION-CODE  (9)
  2 DEATH-BENEFIT   (9)
  2 BEG-CASH-VALUE  (9)
  2 PAID-UP-DATE    (9)
  2 ISSUE-AGE       (9)
  2 SMOKER-FLAG     (9)
*  2 DATE-OF-BIRTH
*
1 FR-V VIEW OF A-FIRS
  2 DOCUMENT   (A37/72)
*
1 NT-V VIEW OF A-BENE-FINAL   /* New final beneficiary file  NG1
  2 ID-NUMBER          (N6)    /* let D = bene designation (1-4)
  2 NOTE-DATE          (N8/60) /* LET P/A = PRINCIPAL/ALTERNATE DESING
  2 REDEFINE NOTE-DATE         /* let L = linked policies
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7)   /* (D, L)
      4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
      4 BENE-LST-UPD      (N8)     /* (D)
      4 BENE-STRT-PEND    (N8)     /* (D)
      4 BENE-FOL-UP-IND   (N1)     /* (D)
      4 BENE-OWN-TRNSF-DT (N8)     /* (D)
      4 BENE-SUC-OWN      (N1)     /* (d)
      4 BENE-NEW-LAYOUT   (N1)     /* (d)
      4 BENE-FIL1         (A4)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)     /* (D)
      4 BENE-RESTRICT    (A1)     /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1)    /* (D, P/A)
        5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 BENE-TEXTS-6-POLS (6)
      4 BENE-TEXT-PRIM-SEC (2)
        5 BENE-TEXT-OCC    (5)
          6 BENE-TEXT        (A60)   /* (D, P/A, text-line)
          6 REDEFINE BENE-TEXT
            7 BENE-TEXT-1ST-34   (A34)
            7 BENE-TEXT-LST-26   (A26)
          6 REDEFINE BENE-TEXT
            7 BENE-TEXT-1ST-25   (A25)
            7 BENE-TEXT-LST-35   (A35)
            7 REDEFINE BENE-TEXT-LST-35
              8 BENE-TEXT-6        (A6)
*
1 MR-V VIEW OF A-REQUEST
  2 ID-NUMBER        (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A    (A6)
  2 MEMBER-NAME      (A20)
  2 CLERK-ID         (A3)
  2 ZIP-CODE         (N5)
  2 REDEFINE ZIP-CODE
    3 FIR-IND         (N1)
    3 MSG-IND         (N1)
    3 RET-IND         (N1)
    3 FIN-IND         (N1)
    3 FAS-IND         (N1)
  2 RQST-TABLE       (A10)   /* add to g3601pmr also
  2 REDEFINE RQST-TABLE
    3 BEN-IND         (A1)
    3 MAL-IND         (A1)   /* mbr accts letter
    3 BEN-SHO-IND     (A1)
    3 ACC-IND         (A1)
    3 ALI-IND         (A1)
    3 APP-IND         (A1)
    3 PIL-IND         (A1)
    3 CO-IND          (A1)  /* created by "co"
  2 REQUEST-DATE     (N4)
  2 ASSUM-RET-DATE   (N8)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
*
1 #ISS-DT-FND      (P3)
1 #NT-NUM          (N6)
1 #Y-N             (A1)
1 #PROGRAM         (A8)
1 #TO-PLAN             (A2)
1 #SCN-POLICY-NUMBER (A16)
1 #W-PLAN            (A2)
1 #PAY-STAT          (A4)
1 #EFF-DATE          (N8)
*
1 #W-K           (P2)
1 #TEXT          (A78)
1 #HOLD-PLAN     (A6)
1 #CASH-VALUE    (P7.2)
1 #NUM           (A3)
*
1 #NAME-1        (A25)
1 #NAME-2        (A25)
1 #NAME-3        (A25)
1 #NAME-4        (A25)
1 #WORK-NAME     (A24)
*
1 #FIRST-LAST    (A25)
1 #LAST-NAME     (A24)
1 #EDIT-INVALID (L)
1 #RANK-LITERAL (A23)
1 #RANK-DISPLAY (A5)
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #WORK-AMT   (N5.5)
1 #SCR-SSN     (A11)
1 #SCR-ISSUE-DATE        (A10)
1 #SCR-FACE-DISP         (A8)
*
1 #TOT-POLS    (N1)
1 #P-X         (I2)
1 #IFL         (I2)
1 #I1          (I2)
1 #I2          (I2)
1 #I3          (I2)
1 #I4          (I2)
1 #LAST-I3     (I2)
*
1 #SSN             (N9)
1 REDEFINE #SSN
  2 #SSN-1    (N3)
  2 #SSN-2    (N2)
  2 #SSN-3    (N4)
1 #WHOLE-DOLLARS   (P5)
*
1 #TOT-FACE         (P7)
1 #TOT-ACTUAL-FACE      (P7)
1 #FACE-REMAIN          (N7)
*
1 #FIRST/MID    (A25)
1 #SUFFIX       (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
1 #APRM        (N5.2)
*
1 #TODAY-YYYYMMDD (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY    (N4)
  2 #TODAY-MMDD    (N4)
*
1 #MONTHS (A36)  INIT
  <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
*
END-DEFINE
* * * * * * * * * * * * * * *
*
MOVE *DATN TO #TODAY-YYYYMMDD
GET ST-V #CGA-ISN
OBSOLETE-STATUS-FIELDS.DATE-OF-BIRTH :=
  GET-DATE-OF-BIRTH (< ST-V.MEMBER-CONTACT-ID >)
*
MOVE SSN TO #SSN
COMPRESS #SSN-1 #SSN-2 #SSN-3 INTO #SCR-SSN WITH '-'
*
FOR #IFL 1 TO 9
  IF  (STATUS (#IFL) NE 'D')
*  OR (PAY-STATUS (#IFL)= '44')  /* eti
    ESCAPE TOP
  END-IF
*
  MOVE *DATN TO #EFF-DATE
  IF ((PLAN (#IFL) = 'LT') AND (DATE-OF-BIRTH LE #EFF-DATE - 400000))
      OR ((PLAN (#IFL) = 'RT') AND (ISSUE-AGE (#IFL) GT '65'))
      OR  (((PLAN-A1 (#IFL) GE 'U') AND (PLAN-A1 (#IFL) LE 'Z'))
      AND ((ISSUE-DATE(#IFL) + 40000) LE #EFF-DATE))
      OR  (IS-ANNUITY-LIFE(<PLAN(#IFL)>))   /* DW1                    /* TT1
*
    CALLNAT 'GDROPSTM' USING
      PLAN          (#IFL)
      SMOKER-FLAG   (#IFL)
      OBSOLETE-STATUS-FIELDS.DATE-OF-BIRTH
      ISSUE-DATE    (#IFL)
      PAID-UP-DATE  (#IFL)
      #EFF-DATE
      ISSUE-AGE     (#IFL)
      #PAY-STAT
      FACE-AMOUNT   (#IFL)
      DEATH-BENEFIT (#IFL)
      CASH-VALUE    (#IFL)
      BEG-CASH-VALUE(#IFL)
  END-IF
*
  ADD 1 TO #TOT-POLS
*
  IF PAY-STATUS (#IFL) = '45'  /* rpu
    IF ISSUE-DATE (#IFL) LT 19941201
      COMPUTE #TOT-FACE = #TOT-FACE + (NUM-MBRSHIPS (#IFL) * 5000)
    ELSE
      COMPUTE #TOT-FACE = #TOT-FACE + (EXPIRY-DAY (#IFL) * 5000)
    END-IF
  ELSE
    ADD FACE-AMOUNT (#IFL) TO #TOT-FACE
  END-IF
  ADD FACE-AMOUNT (#IFL) TO #TOT-ACTUAL-FACE
END-FOR
*
FOR #IFL 1 TO 9
  IF  (STATUS (#IFL) NE 'D')
*  OR (PAY-STATUS (#IFL)= '44')
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #P-X
  MOVE ISSUE-DATE (#IFL) TO #ISS-DATE (#P-X)
  MOVE EDITED FACE-AMOUNT (#IFL) (EM=ZZZ,ZZ9) TO #FACE-DISP (#P-X)
  COMPRESS '$' #FACE-DISP (#P-X) INTO #FACE-DISP (#P-X) LEAVING NO
  RESET #TEXT
*
  CALLNAT 'GPLNMSXX' #SCN-POLICY-NUMBER ST-V.ID-NUMBER #IFL
  MOVE #SCN-POLICY-NUMBER TO #POL-NUM (#P-X)
*
  MOVE EDITED FACE-AMOUNT(#IFL) (EM=ZZZ,ZZZ) TO #SCR-FACE-DISP
  COMPRESS '$' #SCR-FACE-DISP INTO #SCR-FACE-DISP LEAVING NO
END-FOR
*
RESET #I2
FIND (1) FR-V WITH MEMBER-KEY = #CGA-ID-G
  FOR #I1 1 TO 72
    IF  (DOCUMENT (#I1) = MASK ('#AUTH'))
        OR (DOCUMENT (#I1) = MASK ('*AUTH'))
        OR (DOCUMENT (#I1) = MASK ('AUTH'))
        OR (DOCUMENT (#I1) = MASK ('#SPOUSE'))
        OR (DOCUMENT (#I1) = MASK ('*SPOUSE'))
        OR (DOCUMENT (#I1) = MASK ('LTR AUTH'))
        OR (DOCUMENT (#I1) = MASK ('LETTER AUTH'))
      ADD 1 TO #I2
      MOVE DOCUMENT (#I1) TO #SCR-ACCESS (#I2)
*
      IF DOCUMENT (#I1 + 1) = MASK ('.')
        ADD 1 TO #I1
        COMPRESS #SCR-ACCESS (#I2) DOCUMENT (#I1)
          INTO #SCR-ACCESS (#I2)
      END-IF
*
      IF DOCUMENT (#I1 + 1) = MASK ('.')
        ADD 1 TO #I1
        ADD 1 TO #I2
        MOVE DOCUMENT (#I1) TO #SCR-ACCESS (#I2)
      END-IF
*
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
*
IF #I2 = 0
  ADD 1 TO #I2
  IF ACCESS-AUTH-CODE = 'S'
    PERFORM SPS-NAME-HUNT
    COMPRESS 'Spouse' #WORK-NAME 'has access to file'
      INTO #SCR-ACCESS (#I2)
  ELSE
    IF ACCESS-AUTH-CODE = 'Y'
      MOVE 'Check "DD" Screen for Authorized Accessee"s'
        TO #SCR-ACCESS (#I2)
    ELSE
      MOVE 'No Authorized Access given' TO #SCR-ACCESS (#I2)
    END-IF
  END-IF
END-IF
*
**COMPUTE #NT-NUM = #CGA-SCR-ID + 400000       /* ng1
#NT-NUM := #CGA-SCR-ID                         /* ng1
FIND (1) NT-V WITH ID-NUMBER = #NT-NUM
  FOR #I1 1 TO 6    /* 6 POSSIBLE DESIGNATIONS
    IF BENE-ISS-DT (#I1, 1) = 0 /* no more designations
      ESCAPE BOTTOM
    END-IF
*
    ADD 1 TO #I4
    RESET #ISS-DT-FND
    FOR #I2 1 TO 7  /* 7 possible certs in each desig (can't be made 9)
      IF BENE-ISS-DT (#I1, #I2) = 0
        ESCAPE BOTTOM
      END-IF
*
      FOR #I3 1 TO 9  /* 9 possible certs on status file
        IF   (BENE-ISS-DT    (#I1, #I2) = #ISS-DT-YYMMDD (#I3))
            AND (BENE-SPLIT-IND (#I1, #I2) = SPLIT-IND (#I3))
          MOVE TRUE TO #CERT-USED (#I3)
          ADD 1 TO #ISS-DT-FND
*
          IF #ISS-DT-FND = 4
            ADD 1 TO #I4
          END-IF
*
          IF #ISS-DT-FND GT 1
            COMPRESS #SCR-LINES (#I4) '/' INTO #SCR-LINES (#I4)
          END-IF
*
          IF #I4 LE 9
            COMPRESS #SCR-LINES (#I4) #POL-NUM (#I3) #FACE-DISP (#I3)
              INTO #SCR-LINES (#I4)
          END-IF
          MOVE #I3 TO #LAST-I3
        END-IF
      END-FOR
    END-FOR
*
    ADD 1 TO #I4
*
    IF BENE-TEXT-LST-35 (#I1, 2, 5) = ' '
*     WRITE 'i4=' #I4
      IF #I4 LE 9
        IF INTRO-FLAG = 'S' OR = 'K' OR = 'G'
          MOVE ' Insured is the Owner' TO #SCR-LINES (#I4)
        ELSE
          MOVE ' Member is the Owner' TO #SCR-LINES (#I4)
        END-IF
      END-IF
*
      IF (#LAST-I3 = 0)   /* status policy exists
          OR FIRS-LOCATION-CODE (#LAST-I3) = '1' OR = '2' OR = '3' OR = '4'
        IF #I4 LE 9
          MOVE (AD=I) TO #SCR-LINES-CV (#I4)
          MOVE ' Unknown Non-Member Owner' TO #SCR-LINES (#I4)
        END-IF
      END-IF
*
      IF (#LAST-I3 NE 0)
          AND (CONV-DURATION (#LAST-I3) = 888)
        MOVE ' AAFMAA is the Owner' TO #SCR-LINES (#I4)
      END-IF
    ELSE
      MOVE (AD=I) TO #SCR-LINES-CV (#I4)
      MOVE ' Owner:' TO #SCR-LINES (#I4)
*
      IF BENE-TEXT-LST-35 (#I1, 2, 5) = MASK ('*')
        COMPRESS #SCR-LINES (#I4) BENE-TEXT (#I1, 1, 1)
          INTO #SCR-LINES (#I4) LEAVING NO
      ELSE
        IF BENE-TEXT-LST-35 (#I1, 2, 5) = MASK (NNNNNN)
          FIND (1) ST-X WITH MEMBER-KEY = BENE-TEXT-6 (#I1, 2, 5)
            COMPRESS #SCR-LINES (#I4) ST-X.NAME 'CN' ST-X.ID-NUMBER
              INTO #SCR-LINES (#I4)
          END-FIND
        ELSE
          COMPRESS #SCR-LINES (#I4) BENE-TEXT-LST-35 (#I1, 2, 5)
            INTO #SCR-LINES (#I4) LEAVING NO
        END-IF
      END-IF
    END-IF
*
    IF BENE-RESTRICT (#I1) = 'Y'
      COMPRESS #SCR-LINES (#I4) '*** Changes are Restricted ***'
        INTO #SCR-LINES (#I4)
    END-IF
  END-FOR
END-FIND
*
FOR #I1 1 TO 9
  IF (#CERT-USED (#I1))
      OR (#ISS-DATE (#I1) = 0)
    ESCAPE TOP
  END-IF
*
  IF #I4 LT 9
    ADD 1 TO #I4
  END-IF
*
  COMPRESS #SCR-LINES (#I4) #POL-NUM (#I1) #FACE-DISP (#I1)
    INTO #SCR-LINES (#I4)
*
  IF #I4 LT 9
    ADD 1 TO #I4
  END-IF
*
  IF FIRS-LOCATION-CODE (#I1) = '1' OR = '2' OR = '3' OR = '4'
    MOVE (AD=I) TO #SCR-LINES-CV (#I4)
    COMPRESS #SCR-LINES (#I4) ' Unknown Non-Member Owner'
      INTO #SCR-LINES (#I4)
  ELSE
    COMPRESS #SCR-LINES (#I4) ' Member is the Owner'
      INTO #SCR-LINES (#I4)
  END-IF
*
  IF CONV-DURATION (#I1) = 888
    MOVE ' AAFMAA is the Owner' TO #SCR-LINES (#I4)
  END-IF
END-FOR
*
MOVE '*' TO #CGA-SCR-COMMAND
*
INPUT MARK *#SCR-MR-ACC USING MAP 'C1605MCO'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF (#SCR-MR-ACC NE ' ') AND (#SCR-MR-ACS NE ' ')
  REINPUT 'Only 1 can be selected'
    MARK *#SCR-MR-ACC
END-IF
*
IF (#SCR-MR-ACS NE ' ')
    AND ((NAME-SP = ' ') OR (STATUS-CODE-SP = 'D'))
  REINPUT 'No spouse exists'
    MARK *#SCR-MR-ACS
END-IF
*
IF (#SCR-MR-ACC NE ' ') OR (#SCR-MR-ACS NE ' ')
  FIND (1) MR-V WITH ID-NUMBER = #CGA-ID-N
    IF NO RECORDS FOUND
      PERFORM SET-ACC-MR
      STORE MR-V
      ESCAPE BOTTOM
    END-NOREC
*
    PERFORM SET-ACC-MR
    UPDATE (0482)
  END-FIND
*
  END TRANSACTION
*
  MOVE 'MR Completed' TO #TEXT
  INPUT TEXT #TEXT USING MAP 'C1605MCO'
*
  IF #CGA-SCR-COMMAND = '*'
    MOVE 'CO' TO #CGA-SCR-COMMAND
  END-IF
*
  FETCH 'G1000PXX'
END-IF
*
MOVE 'CO' TO #CGA-SCR-COMMAND
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-ACC-MR
* * * * * * * * * * * * * * *
*
MOVE #CGA-ID-N      TO MR-V.ID-NUMBER
MOVE ST-V.NAME      TO MR-V.MEMBER-NAME
MOVE #CGA-CLERK     TO MR-V.CLERK-ID
MOVE #TODAY-MMDD    TO MR-V.REQUEST-DATE
*
IF #SCR-MR-ACC NE ' '
  MOVE 1 TO ACC-IND
END-IF
*
IF #SCR-MR-ACS NE ' '
  MOVE 2 TO ACC-IND
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SPS-NAME-HUNT
* * * * * * * * * * * * * * * *
*
SEPARATE NAME-SP INTO #NAME-1 #WORK-NAME #NAME-3 #NAME-4
  WITH  DELIMITER ','
RESET #NAME-1 #NAME-2 #NAME-3 #NAME-4
SEPARATE #WORK-NAME INTO #NAME-1 #NAME-2 #NAME-3 #NAME-4
  WITH  DELIMITER ' '
*
IF   (#NAME-1 = MASK (..'             '))  /* 1ST INITIAL
    AND (#NAME-2 NE ' ')
  MOVE #NAME-2 TO #WORK-NAME
ELSE
  MOVE #NAME-1 TO #WORK-NAME
END-IF
*
END-SUBROUTINE /*(6590)
*
END
