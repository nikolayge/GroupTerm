* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1102SAI   ** DETERMINE IF POLICY IS AUTO ISSUABLE
*
********************************************************************************
*   DATE      INITIALS    TAG      REASON                                      *
*   20070328    PAM       None     Program Written                             *
*   09262012    YAK       YK1      Use function to identify Level Term II plans*
*   11152012    YAK       YK2      Add No Medical Term plan (EZ)               *
*   01/29/2014  vxt       vt1      Add variable to  the PDA for call to C1002sat
********************************************************************************
*
DEFINE DATA
PARAMETER USING C1100LAI
PARAMETER USING C1002LAT
*
LOCAL
*
1 #YES                         (A1)  INIT <'Y'>
1 #NO                          (A1)  INIT <'N'>
1 #SEND-TO-UNDERWRITING        (A78) INIT <'* Send to Underwriting'>
1 #PLAN-EXCLUSION-LIST
  2 #PLAN-1                     (A3)  INIT <'LT2'>
  2 #PLAN-2                     (A3)  INIT <'RT'>
  2 #PLAN-3                     (A3)  INIT <'EZ'>                 /* YK2
*
1 REDEFINE #PLAN-EXCLUSION-LIST
  2 #PLAN-OCCS                  (03)                              /* YK2 change from 2 to 3
    3 #PLAN-ID                   (A3)
*
1 #NUMBER-PLAN-OCCS            (I2)  INIT <3>                    /* YK2 change from 2 to 3
1 #I1                          (I2)
* 1 #LTII-START                  (A1)  INIT <'U'>                 /* YK1
* 1 #LTII-END                    (A1)  INIT <'Z'>                 /* YK1
1 #LTII                        (A3)  INIT <'LT2'>
1 #WORK-PLAN                   (A3)
1 REDEFINE #WORK-PLAN
  2 #WORK-PLAN-1                (A1)
  2 #WORK-PLAN-REST             (A2)
*
END-DEFINE
********************************************************************************
*
PERFORM DETERMINE-UNDERWRITING
*
PERFORM DETERMINE-AUTO-ISSUE
*
********************************************************************************
DEFINE SUBROUTINE DETERMINE-UNDERWRITING
********************************************************************************
*
if #yes-on-med = 'Y'                                  /* vt1 \/
  move true to #yes-on-med-l
else
  reset #yes-on-med-l
end-if                                              
move #MEMBER-MILITARY-STATUS to #mil-status-member   /* vt1 /\
*
CALLNAT 'C1002SAT'
  #PDA-C1002SAT
*
END-SUBROUTINE
*
********************************************************************************
DEFINE SUBROUTINE DETERMINE-AUTO-ISSUE
********************************************************************************
*
#AUTO-ISSUE := #YES
*
DECIDE FOR FIRST CONDITION
  WHEN #YES-ON-MED = #YES
    #AUTO-ISSUE := #NO
  WHEN #UNDERWRITING = #SEND-TO-UNDERWRITING
    #AUTO-ISSUE := #NO
  WHEN #POLICY-AMOUNT > #REMAINING-AUTO-ISSUE
    #AUTO-ISSUE := #NO
  WHEN NONE
    IGNORE
END-DECIDE
*
#WORK-PLAN := #PLAN-1002
*
* IF    ((#WORK-PLAN-1 GE #LTII-START) AND (#WORK-PLAN-1 LE #LTII-END))   /* YK1
IF F-LT2-TRUE(<#WORK-PLAN>)                                               /* YK1
    AND (#WORK-PLAN = MASK (.NN))
  MOVE #LTII TO #WORK-PLAN
END-IF
*
IF #WORK-PLAN = ' ' OR #POLICY-AMOUNT = 0
  RESET #AUTO-ISSUE
ELSE
  FOR #I1 1 TO #NUMBER-PLAN-OCCS
    IF #WORK-PLAN = #PLAN-ID (#I1)
      #AUTO-ISSUE := #NO
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-IF
*
END-SUBROUTINE
*
END
