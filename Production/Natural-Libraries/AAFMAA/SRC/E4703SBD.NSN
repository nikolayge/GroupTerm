* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: E4703SBD      ** beneficiary processing (sps & deps)
*****************************************************************
*                       MODIFICATION LOG                        *
* USER   DATE     TAG  REASON                                   *
* DRW  03/09/2005 DW1  Add all depn search processing           *
* PAM  04/10/2010 PM2  Added selection screen and new dependant *
*                      file area                                *
* PAM  04/28/2010 PM3  Adjust relationship codes                *
* PAM  05/17/2010 PM4  Expanded relationship code changes       *
* PAM  08/16/2010 PM5  Added ORGANIZATION role                  *
* PAM  05/19/2011 PM6  Added new roles and organization type    *
* TMT  06/16/2014 TT1  Contact Normalization                    *
*****************************************************************
DEFINE DATA
PARAMETER
1 #CGA-ID       (N6)
1 REDEFINE #CGA-ID
 2 #CGA-ID-A     (A6)
1 #PA-X          (I2)  /* 1 or 2 (prim or alt)  DW1
1 #B-X           (I2)  /* 1 to 5 (5 benes)      DW1
1 #CONTACT-ID    (N8)                        /* PM2
1 #RELATIONSHIP  (A1/5)
1 #BD-NAME       (A25/5)
1 #BD-SSN        (A9/5)
1 #BD-DOB        (A8/5)
1 #BD-CONTACT-ID (N8/5)                      /* PM2
1 #BD-GENDER     (A1/5)                      /* PM2
*
LOCAL USING GDATEAXX                         /* PM2
LOCAL
1 #BD-ALL-NAMES   (A25/14)   /* DW1
1 #BD-ALL-SSNS    (A9/14)
1 #BD-ALL-DOBS    (A8/14)
*
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS       /* PM2
 2 CONTACT-1-ID                              /* PM2
 2 ROLE-1-CD                                 /* PM2
 2 CONTACT-2-ID                              /* PM2
 2 ROLE-2-CD                                 /* PM2
*
1 CT-V VIEW OF A-CONTACTS                    /* PM2
 2 ID-NUMBER                                 /* PM2
 2 CONTACT-ID                                /* PM2
 2 FIRST-NAME                                /* PM2
 2 MIDDLE-NAME                               /* PM2
 2 LAST-NAME                                 /* PM2
 2 SUFFIX                                    /* PM2
 2 GENDER-CD                                 /* PM2
 2 DATE-OF-BIRTH                             /* PM2
 2 SSN                                       /* PM2
 2 DELETE-FLAG                               /* PM2
 2 CONTACT-TYPE                              /* PM6
 2 FIRM-ORGANIZATION-NAME                    /* PM6
 2 FIRM-ESTABLISHED-DATE                     /* PM6
*
1 ST-V VIEW OF A-STATUS
 2 NAME
 2 DATE-OF-DEATH
 2 NAME-SP
 2 STATUS-CODE-SP
*  2 DATE-OF-BIRTH-SP                 /* TT1
 2 SSN-SP
 2 REDEFINE SSN-SP
  3 SSN-SP-A (A9)
*
1 FR-V VIEW OF A-FIRS
 2 DEPN-NAME   (14)
 2 DEPN-DATE-OF-BIRTH  (N8/14)
 2 DEPN-SSN     (N9/14)
 2 REDEFINE DEPN-SSN
  3 DEPN-SSN-A   (A9/14)
 2 DEPN-STATUS-CD (A1/14)
*
1 #ERR-MSG      (A60)
1 #NAME      (A25)
1 #LAST      (A25)
1 REDEFINE #LAST
 2 #LAST-X  (A1/25)
1 #FIRST-20  (A20)
1 #MI        (A1)
1 #I1        (I2)
1 #I2        (I2)
1 #I3        (I2)
1 #ST-FND    (L)
1 #FR-FND    (L)
1 #FIRST-NAME    (A25)
1 REDEFINE #FIRST-NAME
 2 #FIRST-NAME-X  (A1/25)
1 #FIRST-NAME-BD (A25)
1 #FIRST-NAME-FR (A25)
1 #EXTRA-1       (A25)
1 #EXTRA-2       (A25)
1 #EXTRA-3       (A25)
1 #EXTRA-4       (A25)
1 #EXTRA-5       (A25)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY       (A4)
 2 #MM         (A2)
 2 #DD         (A2)
*
1 #DEP-ENTRIES        (P3)                   /* PM2
1 #DEP-TABLE          (500)                  /* PM2
 2 #DEP-SEL           (A1)                   /* PM2
 2 #DEP-CONTACT-ID    (N8)                   /* PM2
 2 #DEP-ROLE          (A1)                   /* PM2
 2 #DEP-ID-NUMBER     (N6)                   /* PM2
 2 #DEP-FIRST-NAME    (A25)                  /* PM2
 2 #DEP-MIDDLE-NAME   (A25)                  /* PM2
 2 #DEP-LAST-NAME     (A25)                  /* PM2
 2 #DEP-SUFFIX        (A10)                  /* PM2
 2 #DEP-PASS-NAME     (A25)                  /* PM2
 2 #DEP-GENDER-CD     (A1)                   /* PM2
 2 #DEP-DATE-OF-BIRTH (N8)                   /* PM2
 2 #DEP-SSN           (N9)                   /* PM2
*
1 #SCR-COUNT          (P5)                   /* PM2
1 #SCR-PAGE           (P5)                   /* PM2
1 #SCR-IDX            (I2)                   /* PM2
1 #SCR-TABLE          (15)                   /* PM2
 2 #SCR-SEL           (A1)                   /* PM2
 2 #SEL-CV            (C)                    /* PM2
 2 #SCR-NAME          (A25)                  /* PM2
 2 #SCR-ROLE          (A10)                  /* PM2
 2 #SCR-DOB           (A8)                   /* PM2
 2 #SCR-SSN           (A9)                   /* PM2
 2 #SCR-GENDER        (A1)                   /* PM2
*
1 #MAX                (P3)                   /* PM2
1 #CNT                (P3)                   /* PM2
1 #TEXT               (A60)                  /* PM2
*
END-DEFINE
*****************************************************************
*
FIND CR-V WITH CR-V.CONTACT-1-ID = #CONTACT-ID
  FIND (1) CT-V WITH CT-V.CONTACT-ID = CR-V.CONTACT-2-ID
    IF CT-V.DELETE-FLAG NE 'Y'
      ADD +1 TO #DEP-ENTRIES
      #DEP-CONTACT-ID (#DEP-ENTRIES)     := CT-V.CONTACT-ID
      #DEP-ROLE  (#DEP-ENTRIES)          := CR-V.ROLE-2-CD
      #DEP-ID-NUMBER  (#DEP-ENTRIES)     := CT-V.ID-NUMBER
      #DEP-SSN (#DEP-ENTRIES)            := CT-V.SSN
      IF CT-V.CONTACT-TYPE = 'O'                                           /* PM6
        #DEP-LAST-NAME (#DEP-ENTRIES)    := CT-V.FIRM-ORGANIZATION-NAME    /* PM6
        IF CT-V.FIRM-ESTABLISHED-DATE NE 0                                 /* PM6
          #TO-DATE                       := CT-V.FIRM-ESTABLISHED-DATE     /* PM6
          #REQUEST                       := 'C'                            /* PM6
          CALLNAT 'GDATESXX'                                               /* PM6
            #PDA-GDATESXX                                                  /* PM6
          #DEP-DATE-OF-BIRTH (#DEP-ENTRIES)  := #RETURN-CALC               /* PM6
        END-IF                                                             /* PM6
      ELSE                                                                 /* PM6
        #DEP-FIRST-NAME (#DEP-ENTRIES)     := CT-V.FIRST-NAME
        #DEP-MIDDLE-NAME (#DEP-ENTRIES)    := CT-V.MIDDLE-NAME
        #DEP-LAST-NAME (#DEP-ENTRIES)      := CT-V.LAST-NAME
        #DEP-SUFFIX  (#DEP-ENTRIES)        := CT-V.SUFFIX
        #DEP-GENDER-CD  (#DEP-ENTRIES)     := CT-V.GENDER-CD
        #DEP-DATE-OF-BIRTH (#DEP-ENTRIES)  := CT-V.DATE-OF-BIRTH
*
        #TO-DATE                           := CT-V.DATE-OF-BIRTH
        #REQUEST                           := 'C'
        CALLNAT 'GDATESXX'
          #PDA-GDATESXX
        #DEP-DATE-OF-BIRTH (#DEP-ENTRIES)  := #RETURN-CALC
      END-IF                                                            /* PM6
    END-IF
  END-FIND
END-FIND
*
#MAX   := 5    /* Primary
IF #PA-X = 2   /* Alternate
  #MAX := 4
END-IF
*
FOR #I2 1 TO 5
  IF #BD-NAME (#I2) NE ' '
    ESCAPE TOP
  END-IF
*
  ESCAPE BOTTOM
END-FOR
*
COMPUTE #SCR-COUNT = #DEP-ENTRIES / 15 + 1
#SCR-PAGE                := 1
*
SET KEY ENTR NAMED 'SAVE'
SET KEY PF3  NAMED 'EXIT'
SET KEY PF7  NAMED 'UP'
SET KEY PF8  NAMED 'DOWN'
*
PERFORM NEXT-SCREEN
*
REPEAT
  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'PF3'
      ESCAPE ROUTINE
    VALUE 'PF8', 'PF7'
      PERFORM NEXT-SCREEN
    VALUE 'ENTR'
      ESCAPE BOTTOM
    NONE
      IGNORE
  END-DECIDE
END-REPEAT
*
FOR #I1 1 TO #DEP-ENTRIES
  IF #DEP-SEL (#I1) NE ' '
    DECIDE FOR FIRST CONDITION                                    /* PM4
      WHEN #DEP-ROLE (#I1) = 'K'
        DECIDE ON FIRST VALUE OF #DEP-GENDER-CD (#I1)             /* PM3
          VALUE 'F'                                               /* PM3
            #RELATIONSHIP (#I2) := 'D'                            /* PM3
          VALUE 'M'                                               /* PM3
            #RELATIONSHIP (#I2) := 'N'                            /* PM3
          NONE                                                    /* PM3
           #RELATIONSHIP (#I2)  := 'C'
        END-DECIDE                                                /* PM3
*
      WHEN #DEP-ROLE (#I1) = 'D'                                  /* PM4
        #RELATIONSHIP (#I2)     := 'E'                            /* PM4
*
      WHEN #DEP-ROLE (#I1) = 'I'                                  /* PM4
        #RELATIONSHIP (#I2)     := 'R'                            /* PM4
*
      WHEN #DEP-ROLE (#I1) = 'A' OR = 'E' OR = 'T' OR = 'O' OR = 'C' /* PM4 PM5 PM6
        #RELATIONSHIP (#I2)     := ' '                            /* PM4
      WHEN #DEP-ROLE (#I1) = 'J'                                  /* PM6
        #RELATIONSHIP (#I2)     := 'T'                            /* PM6
      WHEN #DEP-ROLE (#I1) = 'V'                                  /* PM6
        #RELATIONSHIP (#I2)     := 'I'                            /* PM6
      WHEN NONE                                                   /* PM4
        #RELATIONSHIP (#I2)     :=  #DEP-ROLE (#I1)
    END-DECIDE                                                    /* PM4
*
    #BD-NAME        (#I2)       :=  #DEP-PASS-NAME (#I1)
    #BD-CONTACT-ID  (#I2)       :=  #DEP-CONTACT-ID (#I1)
    #BD-GENDER      (#I2)       :=  #DEP-GENDER-CD (#I1)
    IF #DEP-DATE-OF-BIRTH (#I1) = 0
      RESET #BD-DOB (#I2)
    ELSE
      MOVE EDITED #DEP-DATE-OF-BIRTH (#I1) (EM=99999999) TO #BD-DOB (#I2)
    END-IF
    IF #DEP-SSN (#I1) = 0
      RESET #BD-SSN (#I2)
    ELSE
      MOVE EDITED #DEP-SSN (#I1) (EM=999999999) TO #BD-SSN (#I2)
    END-IF
    ADD +1 TO #I2
  END-IF
END-FOR
*
************************************************************************
DEFINE SUBROUTINE NEXT-SCREEN                                     /* PM2
************************************************************************
*
IF *PF-KEY = 'PF8'
  IF #SCR-PAGE LT #SCR-COUNT
    #SCR-PAGE := #SCR-PAGE + 1
  END-IF
*
  IF #SCR-PAGE = #SCR-COUNT
   AND #SCR-COUNT GT 1
    #TEXT := 'Last Screen'
  END-IF
ELSE
  IF #SCR-PAGE GT 1
    #SCR-PAGE := #SCR-PAGE - 1
  END-IF
*
  IF #SCR-PAGE = 1
   AND #SCR-COUNT GT 1
    #TEXT   := 'First Screen'
  END-IF
END-IF
*
COMPUTE #I1 = (#SCR-PAGE - 1) * 15
RESET #SCR-TABLE (*)
MOVE (AD=P) TO #SEL-CV (*)
FOR #SCR-IDX 1 TO 15
  IF #I1 = #DEP-ENTRIES
    ESCAPE BOTTOM
  ELSE
    ADD +1 TO #I1
    MOVE (AD=U) TO #SEL-CV (#SCR-IDX)
  END-IF
*
  #SCR-ROLE    (#SCR-IDX)  := FUNCTION-DISP-RL-DESC(<#DEP-ROLE(#I1)>)
  #SCR-GENDER  (#SCR-IDX)  := #DEP-GENDER-CD (#I1)
  MOVE EDITED #DEP-SSN (#I1)(EM=999999999) TO #SCR-SSN (#SCR-IDX)
*
  MOVE EDITED #DEP-DATE-OF-BIRTH (#I1) (EM=99999999) TO #SCR-DOB (#SCR-IDX)
*
  #SCR-NAME    (#SCR-IDX)  := FUNCTION-DISP-CT-NAME(<#DEP-FIRST-NAME (#I1),#DEP-MIDDLE-NAME (#I1),
                                                     #DEP-LAST-NAME (#I1),#DEP-SUFFIX (#I1)>)
  #DEP-PASS-NAME (#I1) := #SCR-NAME (#SCR-IDX)
END-FOR
*
INPUT TEXT #TEXT USING MAP 'BDOM4703'
*
FOR #SCR-IDX 1 TO 15
  COMPUTE #I1 = (#SCR-PAGE - 1) * 15 + #SCR-IDX
  IF #I1 GT #DEP-ENTRIES
    ESCAPE BOTTOM
  END-IF
*
  IF #SCR-SEL (#SCR-IDX) NE ' '
    #DEP-SEL (#I1) := #SCR-SEL (#SCR-IDX)
  ELSE
    RESET #DEP-SEL (#SCR-IDX)
  END-IF
END-FOR
*
FOR #I1 1 TO #DEP-ENTRIES
  IF #DEP-SEL (#I1) NE ' '
    ADD +1 TO #CNT
    IF #CNT + (#I2 - 1) GT #MAX
      REINPUT 'Exceeds maximum - please select less benficiaries'
    END-IF
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
