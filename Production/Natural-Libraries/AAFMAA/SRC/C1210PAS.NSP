* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1210PAS
* THIS PROGRAM WILL SHOW AAFMAA A SUMMARY OF THE SOURCE OF APPL
*
*
*
* MODIFICATIONS:
*     0989  ADD MEDICAL APPORVAL FIELD PER MITCHELL   JAK
*     1089  ADD FORMS FOR MONTH-END    PER MITCHELL   JAK
*     1089  CHANGE ACC PROCESSING      JAK
*     1189  ADD GROUP 'Q' TO DIRECT MAIL NEW SUM  PER MITCHELL  JAK
*     0193  ADD GROUP 'W' FOR PENTAGOM VISITS     PER MITCHELL  DRW
*
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX        /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING CSRCGLXX        /* WORK FIELDS FOR GROUP CODES
LOCAL USING C1210LAS        /* WORK FIELDS FOR THIS PROGRAM
LOCAL
1 #SCN-TITLE   (A14)
1 #SCN-DATE   (N6)
1 REDEFINE #SCN-DATE
 2 #SD-YYYY    (N4)
 2 #SD-MM      (N2)
1 REDEFINE #SCN-DATE
 2 #SD-YYYY-A   (A4)
1 #END-DATE   (N6)
1 REDEFINE #END-DATE
 2 #ED-YYYY    (N4)
 2 #ED-MM      (N2)
*
1 #T-NEW      (A6)
1 #T-ADL      (A6)
1 #T-INQ      (A6)
1 #C-NEW      (A6)
1 #C-ADL      (A6)
1 #C-INQ      (A6)
1 #V-NEW      (A6)
1 #V-ADL      (A6)
1 #V-INQ      (A6)
1 #R-NEW      (A6)
1 #R-ADL      (A6)
1 #R-INQ      (A6)
1 #F-NEW      (A6)
1 #F-ADL      (A6)
1 #F-INQ      (A6)
1 #B-NEW      (A6)
1 #B-ADL      (A6)
1 #B-INQ      (A6)
1 #M-NEW      (A6)
1 #M-ADL      (A6)
1 #M-INQ      (A6)
1 #Q-NEW      (A6)
1 #Q-ADL      (A6)
1 #Q-INQ      (A6)
1 #W-NEW      (A6)
1 #W-ADL      (A6)
1 #W-INQ      (A6)
1 #P-NEW      (A6)
1 #P-ADL      (A6)
1 #P-INQ      (A6)
1 #I-NEW      (A6)
1 #I-ADL      (A6)
1 #I-INQ      (A6)
1 #O-NEW      (A6)
1 #O-ADL      (A6)
1 #O-INQ      (A6)
1 #Z-NEW      (A6)
1 #Z-ADL      (A6)
1 #Z-INQ      (A6)
1 #INQ-NEW    (A6)
1 #APPL-NEW   (A6)
1 #APPL-ADL   (A6)
1 #APPL-SPEC-CAT   (A6)
1 #APPL-REJ-NEW    (A6)
1 #APPL-REJ-ADL    (A6)
1 #APPL-MED-REQT-NEW (A6)
1 #APPL-MED-REQT-ADL (A6)
1 #SCN-MED-APPR-DOCTOR  (A6)
1 #SCN-MED-APPR-INHOUSE  (A6)
1 #INQ-TO-APPL-NEW (A6)
1 #START-GROUP   (A1)
1 #HOLD-TODAY-DATE  (N8)
1 REDEFINE #HOLD-TODAY-DATE
 2 #YYYY     (A4)
 2 #MM       (N2)
 2 #DD       (N2)
1 REDEFINE #HOLD-TODAY-DATE
 2 #HTD-YYYYMM  (N6)
1 REDEFINE #HOLD-TODAY-DATE
 2 #YYYY-N   (N4)
1 #MONTH-LITERAL  (A108) INIT<'JANUARY  FEBRUARY MARCH    APRIL    '
   -'MAY      JUNE     JULY     AUGUST   SEPTEMBEROCTOBER  NOVEMBER '
   -'DECEMBER '>
1 REDEFINE #MONTH-LITERAL
 2 #MONTH-ENTRY  (A9/1:12)
1 #INDEX    (I2)
1 #AP-INDEX    (I2)
1 #MTD-FLAG (L)
1 #CUR-YEAR (L)
1 #SUM-NEW  (P5)
1 #SUM-ADDL  (P5)
1 #SUM-INQ   (P5)
1 #HOLD-M-NEW  (P5)
1 #HOLD-M-INQ  (P5)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX             /* NATURAL ERROR ROUTINE
*
IF #CGA-USER-COMPANY = 'S'  /* PAM - SPLIT COMPANIES
  FETCH 'S1210PAS'
END-IF
*
RESET #CGA-SCR-SUFFIX
RESET #CGA-SCR-ID
MOVE '*' TO #CGA-SCR-COMMAND
MOVE *DATN TO #HOLD-TODAY-DATE
MOVE #HTD-YYYYMM TO #SCN-DATE
*
IF *DEVICE = 'BATCH'
  MOVE FALSE TO #MTD-FLAG
  MOVE TRUE TO #CUR-YEAR
  MOVE #MM TO #AP-INDEX
ELSE
  MOVE 08 TO *CURS-LINE
*
  IF #MM GT 09
    MOVE 53 TO *CURS-COL
  ELSE
    MOVE 54 TO *CURS-COL
  END-IF
*
  INPUT MARK *#SCN-DATE USING MAP 'C1210MAS'
  IF #CGA-SCR-COMMAND = '*'
    IGNORE
  ELSE
    FETCH 'G1000PXX'
  END-IF
*
  IF (#SD-YYYY = #YYYY-N) OR (#SD-YYYY = #YYYY-N - 1)
    IGNORE
  ELSE
    REINPUT 'CURRENT AND PRIOR YEAR ONLY IS AVAILABLE'
    MARK *#SCN-DATE
  END-IF
*
  IF #SD-MM = 0
    MOVE FALSE TO #MTD-FLAG
    MOVE #MM TO #AP-INDEX
  ELSE
*    IF #SD-MM > 13   /*CHANGED - MISSING EDIT CHECK FOR 13
     IF #SD-MM GE 13
       REINPUT WITH 'INVALID MONTH SELECTION; MUST BE 0 TO 12'
       MARK *#SCN-DATE
     ELSE
       MOVE TRUE TO #MTD-FLAG
       MOVE #SD-MM TO #AP-INDEX
     END-IF
  END-IF
*
  IF #SD-YYYY = #YYYY-N
    MOVE TRUE TO #CUR-YEAR
  ELSE
    MOVE FALSE TO #CUR-YEAR
  END-IF
END-IF
*
IF #MTD-FLAG
   COMPRESS #MONTH-ENTRY(#AP-INDEX) #SD-YYYY-A INTO #SCN-TITLE
ELSE
  COMPRESS 'ALL OF' #SD-YYYY-A INTO #SCN-TITLE
END-IF
*
MOVE 'E' TO #START-GROUP
READ S-VIEW LOGICAL BY SOURCE-CODE-GROUP FROM #START-GROUP
                                         THRU #START-GROUP
    RESET #SUM-NEW #SUM-ADDL #SUM-INQ
    DECIDE FOR FIRST CONDITION
     WHEN SOURCE-CODE = 'EDSZ' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #Z-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #Z-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #Z-INQ
     WHEN SOURCE-CODE = 'EDST' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #T-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #T-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #T-INQ
     WHEN SOURCE-CODE = 'EDSC' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #C-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #C-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #C-INQ
     WHEN SOURCE-CODE = 'EDSV' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #V-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #V-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #V-INQ
     WHEN SOURCE-CODE = 'EDSW' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #W-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #W-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #W-INQ
     WHEN SOURCE-CODE = 'EDSR' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #R-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #R-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #R-INQ
     WHEN SOURCE-CODE = 'EDSF' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #F-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #F-ADL
     WHEN SOURCE-CODE = 'EDSB' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #B-ADL
     WHEN SOURCE-CODE = 'EDSM' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #M-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #M-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #M-INQ
     WHEN SOURCE-CODE = 'EDSQ' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #Q-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #Q-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #Q-INQ
     WHEN SOURCE-CODE = 'EDSP' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #P-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #P-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #P-INQ
     WHEN SOURCE-CODE = 'EDSI' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #I-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #I-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #I-INQ
     WHEN SOURCE-CODE = 'EDSO' PERFORM ACCUM-CNTS
        MOVE #SCN-DATE TO #END-DATE
        IF #SD-MM = 00        /* REQUEST FOR ENTIRE YEAR
          MOVE 99 TO #ED-MM
        END-IF
*                 get Reinstatemnts
        READ MS-V BY MONTH-KEY FROM #SCN-DATE THRU #END-DATE
          ADD GAIN-MBRS (20)          TO #SUM-NEW
          ADD SPS-GEN-GAIN-PLANS (13) TO #SUM-NEW
        END-READ
*
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #O-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #O-ADL
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #O-INQ
     WHEN SOURCE-CODE = 'EDSINW' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-INQ  (EM=ZZ,ZZ9) TO #INQ-NEW
     WHEN SOURCE-CODE = 'EDSANE' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #APPL-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #APPL-ADL
     WHEN SOURCE-CODE = 'EDSASC' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #APPL-SPEC-CAT
     WHEN SOURCE-CODE = 'EDSARJ' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #APPL-REJ-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #APPL-REJ-ADL
     WHEN SOURCE-CODE = 'EDSAMR' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #APPL-MED-REQT-NEW
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #APPL-MED-REQT-ADL
     WHEN SOURCE-CODE = 'EDSIAC' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #INQ-TO-APPL-NEW
     WHEN SOURCE-CODE = 'EDSMAP' PERFORM ACCUM-CNTS
        MOVE EDITED #SUM-NEW  (EM=ZZ,ZZ9) TO #SCN-MED-APPR-INHOUSE
        MOVE EDITED #SUM-ADDL (EM=ZZ,ZZ9) TO #SCN-MED-APPR-DOCTOR
     WHEN NONE  IGNORE
    END-DECIDE
END-READ
*
* MOVE EDITED #HOLD-M-NEW (EM=ZZ,ZZ9) TO #M-NEW
* MOVE EDITED #HOLD-M-INQ (EM=ZZ,ZZ9) TO #M-INQ
*
IF *DEVICE = 'BATCH'
  WRITE NOTITLE USING FORM 'C1211FAS'
ELSE
  MOVE 'AS' TO #CGA-SCR-COMMAND
  INPUT USING MAP 'C1211MAS'
  FETCH 'G1000PXX'
END-IF
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-CNTS
* * * * * * * * * * * * * * * *
*
IF #MTD-FLAG AND #CUR-YEAR
  ADD S-VIEW.SOURCE-CUR-NEW-MBR   (#AP-INDEX) TO #SUM-NEW
  ADD S-VIEW.SOURCE-CUR-ADDL-MBR  (#AP-INDEX) TO #SUM-ADDL
  ADD S-VIEW.SOURCE-CUR-INQUIRIES (#AP-INDEX) TO #SUM-INQ
ELSE
  IF #MTD-FLAG
    ADD S-VIEW.SOURCE-LST-NEW-MBR   (#AP-INDEX) TO #SUM-NEW
    ADD S-VIEW.SOURCE-LST-ADDL-MBR  (#AP-INDEX) TO #SUM-ADDL
    ADD S-VIEW.SOURCE-LST-INQUIRIES (#AP-INDEX) TO #SUM-INQ
  ELSE
    IF #CUR-YEAR
      FOR #INDEX 1 TO #AP-INDEX
         ADD S-VIEW.SOURCE-CUR-NEW-MBR(#INDEX) TO #SUM-NEW
         ADD S-VIEW.SOURCE-CUR-ADDL-MBR(#INDEX) TO #SUM-ADDL
         ADD S-VIEW.SOURCE-CUR-INQUIRIES(#INDEX) TO #SUM-INQ
      END-FOR
    ELSE
      FOR #INDEX 1 TO 12
        ADD S-VIEW.SOURCE-LST-NEW-MBR(#INDEX) TO #SUM-NEW
        ADD S-VIEW.SOURCE-LST-ADDL-MBR(#INDEX) TO #SUM-ADDL
        ADD S-VIEW.SOURCE-LST-INQUIRIES(#INDEX) TO #SUM-INQ
      END-FOR
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
END
