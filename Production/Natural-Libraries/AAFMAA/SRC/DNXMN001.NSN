* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM ID DNXMN001                                                  *
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  12/09/2010  None    Program created                             *
************************************************************************
DEFINE DATA
PARAMETER
1 #CGA-USER-COMPANY        (A1)
1 #CGA-SECURITY-CODE       (N2)
1 #CLERK-ID                (A3)
1 #MSG                       (A72) 1 redefine #MSG
  2 #GLOBAL-USER-COMPANY     (A1)
  2 #GLOBAL-SECURITY-CODE    (N2)
1 #CONTACT-ID              (N8)
1 #SELECTED-ITEM-NBR       (I4)
1 #ICD                     (I4)
1 #ITEM-COLLECTION-DATA    (1:V)
  2 #NAME     (A10)
  2 #ENT-SIZE (I4)
  2 #ENT-BRWS (I4)
  2 #SPACE-LN (L)
  2 #ENT-ISN  (P8/99)
  2 #TEXT     (A67/5)
LOCAL
1 #ISN              (P8)
1 #MODE             (A1)   INIT <'U'>

1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 RANK-ID
1 #J                  (I4)
1 #CR                 (I4) CONST <9>
1 #DN-ITEM-VIEW       (A8)
1 #PGM-UPDATE-NAME    (A8/#CR) INIT <
  'DNXUN001',
  'DNXUN002',
  'DNXUN003',
  'DNXUN004',
  'DNXUN005',
  'DNXUN006',
  'DNXUN007',
  'DNXUN009',
  'DNXUN010'
  >
1 #ITEM-COLLECTION-NAMES (A/#CR) DYNAMIC INIT <
  'Rank',
  'VA',
  'Spouse',
  'FRM Spouse',
  'Death Info',
  'Notifd By',
  'Funeral',
  'NOK',
  'Claimants'
  >
1 #PGM-F12-NAME    (A8/#CR) INIT <
  'NA',
  'NA',
  'NA',
  'NA',
  'NA',
  'DNXUN06D',
  'NA',
  'DNXUN09D',
  'DNXUN10D'
  >
1 #SPECIAL-IND    (L)

END-DEFINE

SET KEY PF3 NAMED 'Exit'
SET KEY PF7 NAMED 'Prev'
SET KEY PF8 NAMED 'Next'
IF #ICD > #CR
  #ICD := #CR
END-IF
IF #SELECTED-ITEM-NBR > #CR
  #SELECTED-ITEM-NBR := 1
END-IF

IF #MSG = 'PF12'
  PERFORM F12-LOOP
ELSE
  PERFORM REGULAR-LOOP
END-IF
IF #MSG = 'PF3' OR= 'PF7' OR= 'PF8'
  RESET #MSG
END-IF

DEFINE REGULAR-LOOP
/*     ============
REPEAT
  #ISN := #ITEM-COLLECTION-DATA.#ENT-ISN ( #SELECTED-ITEM-NBR,1 )
  IF #ISN = 0
    #MODE := 'N'
    FIND CONTACT-V WITH CONTACT-ID = #CONTACT-ID
      #ISN := *ISN (0103)
    END-FIND
  ELSE
    #MODE := 'U'
  END-IF
  #DN-ITEM-VIEW := #PGM-UPDATE-NAME ( #SELECTED-ITEM-NBR )
  #GLOBAL-USER-COMPANY  := #CGA-USER-COMPANY
  #GLOBAL-SECURITY-CODE := #CGA-SECURITY-CODE
  IF #CLERK-ID = 'NG'
    PRINT #SELECTED-ITEM-NBR #DN-ITEM-VIEW #GLOBAL-USER-COMPANY #GLOBAL-SECURITY-CODE
  END-IF
  CALLNAT #DN-ITEM-VIEW
    #ISN
    #CLERK-ID
    #MODE
    #MSG
  DECIDE FOR FIRST CONDITION
    WHEN *PF-KEY = 'PF8' OR #MSG = 'PF8'
      ADD 1 TO #SELECTED-ITEM-NBR
      IF #SELECTED-ITEM-NBR > #ICD
        #SELECTED-ITEM-NBR := 1
      END-IF
    WHEN *PF-KEY = 'PF7' OR #MSG = 'PF7'
      SUBTRACT 1 FROM #SELECTED-ITEM-NBR
      IF #SELECTED-ITEM-NBR < 1
        #SELECTED-ITEM-NBR := #ICD
      END-IF
    WHEN *PF-KEY = 'PF3' OR #MSG = 'PF3'
      ESCAPE BOTTOM
    WHEN NONE
      ESCAPE BOTTOM
  END-DECIDE
  UNTIL *PF-KEY = 'PF3'
END-REPEAT
END-SUBROUTINE

DEFINE F12-LOOP
/*     ========
PERFORM NEXT-F12
REPEAT
  UNTIL *PF-KEY = 'PF3' OR #DN-ITEM-VIEW = 'NA'
  #ISN := #ITEM-COLLECTION-DATA.#ENT-ISN ( #SELECTED-ITEM-NBR,1 )
  IF #ISN = 0
    #MODE := 'N'
    FIND CONTACT-V WITH CONTACT-ID = #CONTACT-ID
      #ISN := *ISN (0148)
    END-FIND
  ELSE
    #MODE := 'U'
  END-IF
*  #DN-ITEM-VIEW := #PGM-UPDATE-NAME ( #SELECTED-ITEM-NBR )
  #GLOBAL-USER-COMPANY  := #CGA-USER-COMPANY
  #GLOBAL-SECURITY-CODE := #CGA-SECURITY-CODE
  IF #CLERK-ID = 'NG'
    PRINT #SELECTED-ITEM-NBR #DN-ITEM-VIEW #GLOBAL-USER-COMPANY #GLOBAL-SECURITY-CODE
  END-IF
  CALLNAT #DN-ITEM-VIEW
    #ISN
    #CLERK-ID
    #MODE
    #MSG
  DECIDE FOR FIRST CONDITION
    WHEN *PF-KEY = 'PF8' OR #MSG = 'PF8'
      PERFORM NEXT-F12
    WHEN *PF-KEY = 'PF7' OR #MSG = 'PF7'
      PERFORM PREV-F12
    WHEN *PF-KEY = 'PF3' OR #MSG = 'PF3'
      ESCAPE BOTTOM
    WHEN NONE
      ESCAPE BOTTOM
  END-DECIDE
END-REPEAT
END-SUBROUTINE

DEFINE NEXT-F12
/*     ========
RESET #J
#DN-ITEM-VIEW := #PGM-F12-NAME ( #SELECTED-ITEM-NBR )
REPEAT UNTIL #DN-ITEM-VIEW NE 'NA' OR #J > #CR
  ADD 1 TO #SELECTED-ITEM-NBR
  ADD 1 TO #J
  IF #SELECTED-ITEM-NBR > #ICD
    #SELECTED-ITEM-NBR := 1
  END-IF
  #DN-ITEM-VIEW := #PGM-F12-NAME ( #SELECTED-ITEM-NBR )
END-REPEAT
* PRINT '+' #DN-ITEM-VIEW #SELECTED-ITEM-NBR
END-SUBROUTINE

DEFINE PREV-F12
/*     ========
RESET #J
#DN-ITEM-VIEW := #PGM-F12-NAME ( #SELECTED-ITEM-NBR )
REPEAT UNTIL #DN-ITEM-VIEW NE 'NA' OR #J > #CR
  ADD 1 TO #J
  SUBTRACT 1 FROM #SELECTED-ITEM-NBR
  IF #SELECTED-ITEM-NBR < 1
    #SELECTED-ITEM-NBR := #ICD
  END-IF
  #DN-ITEM-VIEW := #PGM-F12-NAME ( #SELECTED-ITEM-NBR )
END-REPEAT
* PRINT '-' #DN-ITEM-VIEW #SELECTED-ITEM-NBR
END-SUBROUTINE

END
