* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPPSR2 - report for Policy services
* Auto Convert Pending from LT to RT note and payment mode G
*********************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  07262007 Initial creation
* RSE  09222009 RE1  Change PROCESS-IND/600k logic for Unification
* JNT  09172013 JT1  Address normalization    
*********************************************************************
*
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-FLAG         (A1)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 DEBIT-CREDIT-AMT
  2 BILL-ADJUST
  2 NAME         (A25)
*   2 ADDRESS-1    (A25)                                  /* JT1 
  2 LAST-FM-DATE
  2 ISSUE-DATE          (N8/9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC    (9)
      4 ID-YYYYMM   (N6)
      4 REDEFINE ID-YYYYMM
        5 ID-YYYY    (N4)
        5 ID-MM      (N2)
      4 ID-DD       (N2)
  2 PAY-STATUS          (9)
  2 X-BILL-ADJUST       (9)
  2 PLAN                (9)
  2 STATUS              (9)
  2 MODE                (9)
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER
  2 C*NOTE-TABLE
  2 NOTE-DATE     (60)
  2 NOTE-CLERK-ID (60)
  2 NOTE-TEXT (A60/60)   /* 3600 CHARACTERS
*
1 #NOTE-FOUND               (L)
1 #ID-NUMBER                (N6)
1 #RECS-READ                (N6)
1 #RECS-LT-RT-WRITTEN       (N6)
1 #I                        (N2)
1 #I1                       (N2)
1 #REPORT-RECORD            (A125)
1 #STATUS                   (A8) INIT <'ACTIVE'>
1 #TAB                      (A1) INIT <H'09'>
* * * * * * * * * * * * * * * * * * * * * * *
END-DEFINE
*
FORMAT LS=133 PS=76
WRITE TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Program Messages' 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS ' ' #TAB 'PROGRAM: ' *PROGRAM #TAB 'DATE: ' *DATN #TAB 'TIME: ' *TIMX #TAB
    INTO #REPORT-RECORD
         WRITE WORK FILE 1 #REPORT-RECORD
COMPRESS ' ' #TAB 'AUTO CONVERT LT TO RT NOTE WITH MODE "G" ' #TAB INTO #REPORT-RECORD
         WRITE WORK FILE 1 #REPORT-RECORD
*
COMPRESS 'CN' #TAB 'NAME' #TAB 'POLICY' #TAB 'PAYMENT' #TAB 'ISSUE-DATE' #TAB
     INTO #REPORT-RECORD
        WRITE WORK FILE 1 #REPORT-RECORD
COMPRESS ' ' #TAB ' ' #TAB 'STATUS' #TAB 'MODE' #TAB INTO #REPORT-RECORD
        WRITE WORK FILE 1 #REPORT-RECORD
*
READ ST-V BY MEMBER-KEY FROM '001900'
*
*  IF PROCESS-IND = 'S'              /* OR = 'C'      RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)            /* RE1     
       ESCAPE TOP
  END-IF
*  IF ST-V.ID-NUMBER GE 600000                     /* RE1
*      ESCAPE BOTTOM                               /* RE1
*  END-IF                                          /* RE1
  RESET #NOTE-FOUND #REPORT-RECORD
*
  #ID-NUMBER := ID-NUMBER
  ADD 1 TO #RECS-READ
  EXAMINE MODE(*) FOR 'G' GIVING INDEX #I
  IF #I NE 0 AND #I LE 9
    PERFORM FIND-LT-RT-NOTE
    IF #NOTE-FOUND
      #RECS-LT-RT-WRITTEN := #RECS-LT-RT-WRITTEN + 1
     COMPRESS ID-NUMBER #TAB NAME #TAB #STATUS #TAB MODE(#I)
     #TAB ISSUE-DATE(#I) #TAB INTO #REPORT-RECORD
     WRITE WORK FILE 1 #REPORT-RECORD
    END-IF
  END-IF
END-READ
*
RESET #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
*
COMPRESS ' ' #TAB 'RECORDS WITH AUTO-CONVERT LT TO RT NOTE' #TAB #RECS-LT-RT-WRITTEN
    #TAB INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
*
*********************************
DEFINE SUBROUTINE FIND-LT-RT-NOTE
*********************************
*
RESET #NOTE-FOUND
FIND (1) NT-V WITH ID-NUMBER = #ID-NUMBER
  EXAMINE NOTE-TEXT(*) FOR '* AUTO CONVERT PENDING FROM LT TO RT'
   GIVING INDEX #I1
    IF #I1 NE 0 AND #I1 LE 60
      MOVE TRUE TO #NOTE-FOUND
    END-IF
END-FIND
*
END-SUBROUTINE
*
END
