* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNEMM1.
/**
/** :author nguentchev
/*
DEFINE DATA
PARAMETER USING AUCAEM01
LOCAL USING OBJLCEML
LOCAL USING OBJADBTS
LOCAL
1 #CN-PARMS
  2 CN                   (N6)
  2 ERR                  (N4)
  2 ERR-DESC             (A78)
  2 MOD                  (A1)
*
1 #GET    (I4) CONST <0>
1 #ADD    (I4) CONST <1>
1 #UPDATE (I4) CONST <2>
1 #DELETE (I4) CONST <3>
1 BLANK   (A1) CONST <' '>
*
1 #UNSUPPORTED (A) DYNAMIC CONST <'UNSUPPORTED REQUEST'>
1 #REQUIRED    (A) DYNAMIC CONST <'CONTACT ID REQUIRED'>
1 #UPDATECONFLICT (A) DYNAMIC CONST <'UPDATE CONFLICT'>
END-DEFINE
*
RESET ErrorCode
  ErrorDescription
*
#DB.#FILE-NAME := 'A-CONTACT-EMAIL'
*
IF ActionType NE #ADD  /* New email
  FIND EMAIL-V WITH EMAIL-ID = Email.EmailID
    IF NO RECORDS FOUND
      ErrorCode := 1
      COMPRESS *PROGRAM #REQUIRED INTO ErrorDescription
      ESCAPE ROUTINE
    END-NOREC
    MOVE *ISN TO #DB.#FILE-REC-ISN
    #DB.#REQUEST   := #GET
    CALLNAT 'OBJNDBTS' #DB  /* Get last update date/time
  END-FIND
END-IF
*
DECIDE ON FIRST VALUE OF ActionType
  VALUE #GET    PERFORM GET-RECORD
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
*   ANY VALUE
*     PERFORM SYNC-OPERATIONS
  NONE VALUE
    ErrorCode := 2
    ErrorDescription := #UNSUPPORTED
END-DECIDE
*
DEFINE GET-RECORD
/*     ==========
GET EMAIL-V #DB.#FILE-REC-ISN
LastUpdatedDate := #DB.LAST-DATE-UPD
LastUpdatedTime := #DB.LAST-TIME-UPD
LastUpdatedUser := #DB.LUU-NAME
EmailPreferred  := EMAIL-PREFERRED-IND
EmailType       := EMAIL-TYPE
EmailAddress    := EMAIL-ADDRESS
COMPRESS EMAIL-V.REMARKS (*) INTO Email.Remarks LEAVING NO
EmailUndeliverable := EMAIL-UNDELIV
EmailStatus     := STATUS
EmailStatusDate := STATUS-DATE
EmailStartDate  := START-DATE
EmailEndDate    := END-DATE
END-SUBROUTINE
*
DEFINE ADD-RECORD
/*     ==========
IF Email.EmailID = 0
  RESET EMAIL-V
  CALLNAT 'ADRNT002' EMAIL-V.EMAIL-ID
  Email.EmailID        := EMAIL-V.EMAIL-ID
  EMAIL-PREFERRED-IND := EmailPreferred
  EMAIL-TYPE          := EmailType
  EMAIL-ADDRESS       := EmailAddress
*  EMAIL-V.REMARKS (*) := Email.Remarks (*)
*    ShowRemarks := Function
  EMAIL-UNDELIV       := EmailUndeliverable
  STATUS              := EmailStatus
  STATUS-DATE         := EmailStatusDate
  START-DATE          := EmailStartDate
  END-DATE            := EmailEndDate
  STORE-R.
  STORE EMAIL-V
  MOVE *ISN (STORE-R.) TO #DB.#FILE-REC-ISN
  #DB.#REQUEST   := #ADD
  CALLNAT 'OBJNDBTS' #DB
  LastUpdatedDate := #DB.LAST-DATE-UPD
  LastUpdatedTime := #DB.LAST-TIME-UPD
END-IF
END-SUBROUTINE
*
DEFINE UPDATE-RECORD
/*     =============
IF #DB.LAST-DATE-UPD = LastUpdatedDate
    AND #DB.LAST-TIME-UPD = LastUpdatedTime
  UPDATE-R.
  GET EMAIL-V #DB.#FILE-REC-ISN
  EMAIL-PREFERRED-IND := EmailPreferred
  EMAIL-TYPE          := EmailType
  EMAIL-ADDRESS       := EmailAddress
*  EMAIL-V.REMARKS (*) := Email.Remarks
*    ShowRemarks := Function
  EMAIL-UNDELIV       := EmailUndeliverable
  STATUS              := EmailStatus
  STATUS-DATE         := EmailStatusDate
  START-DATE          := EmailStartDate
  END-DATE            := EmailEndDate
  UPDATE (UPDATE-R.)
  #DB.#REQUEST   := #UPDATE
  CALLNAT 'OBJNDBTS' #DB
  LastUpdatedDate := #DB.LAST-DATE-UPD
  LastUpdatedTime := #DB.LAST-TIME-UPD
ELSE
  ErrorCode := 99
  COMPRESS *PROGRAM #UPDATECONFLICT INTO ErrorDescription
END-IF
*
END-SUBROUTINE
*
DEFINE DELETE-RECORD
/*     =============
IF #DB.LAST-DATE-UPD = LastUpdatedDate
    AND #DB.LAST-TIME-UPD = LastUpdatedTime
  GET EMAIL-V #DB.#FILE-REC-ISN
  DELETE
ELSE
  ErrorCode := 99
  COMPRESS *PROGRAM #UPDATECONFLICT INTO ErrorDescription
END-IF
END-SUBROUTINE
*
* DEFINE SYNC-OPERATIONS
* /*     ===============
* * STATUS
* * CRM
* * TR
* IF ActionType NE #GET
*   CALLNAT 'GOSTEN01'              /* Synch email
*     #CN-PARMS
* END-IF
* END-SUBROUTINE
*
END
