* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* created: bz 07/07/2010 Boris
* Maintain steplib
DEFINE DATA
LOCAL
1 USR1040L
     2 FUNCTION     (A1) INIT <"G">
     2 UDB          (N5) (EM=999)
     2 RC           (I4)
1 USR1025L
     2 ACTION        (A1)       /* 'D', 'A', 'I', 'P'
     2 STEPLIB-ID    (A8/1:8)   /* ID of steplib
     2 DBID          (N5/1:8)   /* DBID of steplib
     2 FNR           (N5/1:8)   /* FNR of steplib
     2 PASSWORD      (A8)       /* Input for ACTION 'A'
     2 CIPHERCODE    (N8)       /* Input for ACTION 'A'
     2 POSIT         (N1)       /* Position of lib to be added/deleted
     2 SV-STEPLIB    (A8)        /* Value of *STEPLIB
     2 SV-DBID       (N5)        /* DBID of *STEPLIB
     2 SV-FNR        (N5)        /* FNR of *STEPLIB
     2 NUMBER-ACTIVE (N1)       /* Number of active steplibs
     2 RESPONSE      (N4)       /* Error code
     2 INFOTEXT      (A65)       /* Text for RESPONSE field
 1 Env
   2 Env_F        (A1)   INIT <'G'>
   2 Env_N        (A80)  INIT <'NATUSER'>
   2 Env_Txt      (A253)
   2 Env_RC       (I4)
1 #ACTION            (A1)
1 #STEPLIB-ID        (A8)
1 #DBID              (N5)
1 #FNR               (N5)
1 WIN_Title          (A70)
1 v_MSG              (A50)
END-DEFINE
ON ERROR
  WRITE "Err:" *ERROR-NR "Err-ln:" *ERROR-LINE *PROGRAM /
  ESCAPE ROUTINE
END-ERROR
DEFINE WINDOW YY-Box
  SIZE 22 * 70
  BASE 3/5
  TITLE WIN_Title
  CONTROL WINDOW
  FRAMED ON

CALLNAT 'USR1040N' USR1040L
SET KEY ALL
COMPRESS *STARTUP "- Maintain steplibs -" INTO WIN_Title
*
REPEAT
*
    ACTION := 'D' #ACTION := " "
    CALLNAT 'USR1025N'  USR1025L
  INPUT WINDOW='YY-Box' (AD=OL IP=OFF ZP=OFF SG=OFF) WITH TEXT v_MSG
      'Actions A >> Add, D >> Del, X >> Exit'(I) 5x "DB:" UDB (AD=I EM=999)
   / '_' (75)
   // 'Action (A,D,X) '  #ACTION (AD=MIT'_')    4X 'No. of steplibs ' NUMBER-ACTIVE (AD=OI ZP=ON)
    / 'Add/Delete No. ' POSIT (AD=MIT'_' ZP=ON) 4X 'Response        ' RESPONSE      (AD=OI ZP=ON)
   // 'Steplibs: Library  DBID  FNR   '
    / 'Add new: ' #STEPLIB-ID (AD=MIT'_' AL=8 ) 2x #DBID (AD=MIT'_' ZP=ON NL=2) 4x #FNR (AD=MIT'_' ZP=ON NL=2)
   // 8x '1.' STEPLIB-ID(1) DBID(1) FNR(1)
    / 8x '2.' STEPLIB-ID(2) DBID(2) FNR(2)
    / 8x '3.' STEPLIB-ID(3) DBID(3) FNR(3)
    / 8x '4.' STEPLIB-ID(4) DBID(4) FNR(4)
    / 8x '5.' STEPLIB-ID(5) DBID(5) FNR(5)
    / 8x '6.' STEPLIB-ID(6) DBID(6) FNR(6)
    / 8x '7.' STEPLIB-ID(7) DBID(7) FNR(7)
    / 8x '8.' STEPLIB-ID(8) DBID(8) FNR(8)
    / '*STEPLIB  ' SV-STEPLIB SV-DBID  SV-FNR
    / INFOTEXT
*
  IF *PF-KEY NE 'ENTR' OR #ACTION = 'X'
        ESCAPE ROUTINE
  END-IF
  v_MSG := " "
  IF #ACTION = 'A' ACTION := 'A'
     STEPLIB-ID(1) := #STEPLIB-ID  DBID(1):=#DBID  FNR(1) := #FNR
     CALLNAT 'USR1025N'  USR1025L
     #STEPLIB-ID :=  " " #DBID := 0 #FNR := 0
  END-IF
  IF #ACTION = 'D' AND POSIT <> 0 ACTION := 'P'
     #STEPLIB-ID :=  STEPLIB-ID(POSIT)  #DBID := DBID(POSIT) #FNR := FNR(POSIT)
     CALLNAT 'USR1025N'  USR1025L
  END-IF
    IF #ACTION = 'S'
     COMPRESS "Parm:" *PARM-USER "*" "ETID:" *ETID "*" "PID:" *PID 
          INTO v_MSG
    END-IF 
    IF #ACTION = 'E'
       v_MSG := 'EnvVar'
       CALLNAT 'USR6202N'  Env
       IF Env_RC <> 0 v_MSG := 'EnvVar got error'
       ELSE
       v_Msg := Env_Txt
       END-IF
    END-IF
    IF #ACTION = '?'
       INPUT WINDOW='YY-Box' Env_N (AD=UM Al=20)
    END-IF
END-REPEAT
END
