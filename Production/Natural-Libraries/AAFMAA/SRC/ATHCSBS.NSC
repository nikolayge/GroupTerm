* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Copycode to EDIT SBS INPUT
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE EDIT-SBS-INPUT
* * * * * * * * * * * * * * * * *
*
RESET #ERR-MSG
*
IF  (#SBS-TRAN-ID-A = ' ' OR = '0000000000')
 OR (#SBS-TRAN-ID-A NE MASK (NNNNNNNNNN))
  COMPRESS '10 Digit Tran ID # must be entered' INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF  NOT #SBS-TRAN-CODE = 'BA' OR = 'RP'
  COMPRESS 'Tran Code must be "BA" or "RP"' INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*    Soldier Info Below
*
RESET #RANK-DISPL
CALLNAT 'GRANKSXX' #CGA-ORIG-COMMAND #SBS-RANK
       #SBS-MIL-ID #INVALID #RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
IF #INVALID
  COMPRESS 'Invalid Rank' #SBS-RANK INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF NOT #SBS-SBP-OPT = 'A' OR = 'C' OR = ' '
  COMPRESS 'SBP-Option must be A or C' #SBS-SBP-OPT INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-AS-OF-DATE-A = ' ' OR ='00000000'
  MOVE *DATN TO #SBS-AS-OF-DATE
ELSE
  IF  (#SBS-AS-OF-DATE-A NE MASK (YYYYMMDD))
   OR (#SBS-AS-OF-DATE LT 20060101)
   OR (#SBS-AS-OF-DATE GT (*DATN + 300000)) /* 30 years into the future
    COMPRESS 'Invalid As Of Date' #SBS-AS-OF-DATE INTO #ERR-MSG
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF  (#SBS-DOB-A NE MASK (YYYYMMDD))
 OR (#SBS-DOB LT 19000101)
  COMPRESS 'Invalid Date of Birth' #SBS-DOB INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF (#SBS-DOB GT (#SBS-AS-OF-DATE - 170000))  /* Must be at least age 17
  COMPRESS 'Must be at least 17 years old' #SBS-DOB INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF NOT #SBS-MIL-ID = 'A'
  COMPRESS 'Invalid Military Service' #SBS-MIL-ID INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-BASD-A = ' '
  MOVE 0 TO #SBS-BASD
END-IF
*
IF #SBS-1405-DT-A = ' '
  MOVE 0 TO #SBS-1405-DT
END-IF
*
IF #SBS-PEBD-A = ' '
  MOVE 0 TO #SBS-PEBD
END-IF
*
IF #SBS-DIEMS-A = ' '
  MOVE 0 TO #SBS-DIEMS
END-IF
*
IF #SBS-DOP-A = ' '
  MOVE 0 TO #SBS-DOP
END-IF
*
IF #SBS-P-DOP-A = ' '
  MOVE 0 TO #SBS-P-DOP
END-IF
*
IF #SBS-SPS-DOB-A = ' '
  MOVE 0 TO #SBS-SPS-DOB
END-IF
*
IF #SBS-SPS-DOM-A = ' '
  MOVE 0 TO #SBS-SPS-DOM
END-IF
*
IF #SBS-F-SPS-DOB-A = ' '
  MOVE 0 TO #SBS-F-SPS-DOB
END-IF
*
IF #SBS-F-SPS-DOM-A = ' '
  MOVE 0 TO #SBS-F-SPS-DOM
END-IF
*
IF (#SBS-BASD-A NE MASK (YYYYMMDD))
  COMPRESS 'BASD is missing or invalid' #SBS-BASD INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF   (#SBS-PEBD NE 0)
 AND (#SBS-PEBD-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid PEBD' #SBS-PEBD INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF   (#SBS-DIEMS NE 0)
 AND (#SBS-DIEMS-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid DIEMS' #SBS-DIEMS INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF   (#SBS-1405-DT NE 0)
 AND (#SBS-1405-DT-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid 1405-Date' #SBS-1405-DT INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-1405-DT = 0
  MOVE #SBS-BASD TO #SBS-1405-DT
END-IF
*
IF #SBS-PEBD = 0
  MOVE #SBS-1405-DT TO #SBS-PEBD
END-IF
*
IF #SBS-DIEMS = 0
  MOVE #SBS-PEBD TO #SBS-DIEMS
END-IF
*
IF #SBS-PEBD LT #SBS-DIEMS
  COMPRESS 'DIEMS must preceed PEBD' #SBS-DIEMS #SBS-PEBD INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-1405-DT LT #SBS-PEBD
  COMPRESS 'PEBD must preceed 1405-DT' #SBS-PEBD #SBS-1405-DT
      INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-BASD LT #SBS-1405-DT
  COMPRESS '1405-DT must preceed BASD' #SBS-1405-DT #SBS-BASD
      INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-DIEMS LT (#SBS-DOB + 130000)
  COMPRESS 'DIEMS must be 13 yrs after DOB' #SBS-1405-DT #SBS-BASD
      INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF   (#SBS-DOP NE 0)
 AND (#SBS-DOP-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Date of Rank' #SBS-DOP INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF   (#SBS-P-DOP NE 0)
 AND (#SBS-P-DOP-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Prior Date of Rank' #SBS-DOP INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-DOP LT #SBS-P-DOP
  COMPRESS 'Prev Date of Rank must preceed Date of Rank' #SBS-P-DOP
       INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF NOT #SBS-CSB-IND = 'Y' OR = 'N' OR = ' '   /* DW1
  COMPRESS 'CSB Indicator must be "Y", "N", or Blank' #SBS-CSB-IND
       INTO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #SBS-TRAN-CODE = 'RP'
  IF NOT #SBS-RP-IND = 'R' OR = 'D' /* RP RETIRED OR DEAD
    COMPRESS 'RP Indicator must be "R" or "D"' #SBS-RP-IND
        INTO #ERR-MSG
  END-IF
*
  IF   (#SBS-RP-IND = 'R')
   AND (#SBS-BASD GT (#SBS-AS-OF-DATE - 200000))
     COMPRESS 'Must have 20 Years Active Service for Retired Calc'
       #SBS-BASD #SBS-AS-OF-DATE INTO #ERR-MSG
    ESCAPE ROUTINE
  END-IF
*
  IF (NOT #SBS-SPC-CODE = ' ' OR = 'G' OR = 'Y')
*                                  /* Normal/force lst pay/COS
    COMPRESS 'Special Code must be " " or "G" or "Y"' #SBS-SPC-CODE
        INTO #ERR-MSG
  END-IF
END-IF
*
IF #SBS-TRAN-CODE = 'BA'
  IF (NOT #SBS-SPC-CODE = ' ' OR = 'G') /* Normal or force last pay
    COMPRESS 'Special Code must be " " or "G"' #SBS-SPC-CODE
        INTO #ERR-MSG
  END-IF
*
* IF (NOT #SBS-FAM-IND = ' ' OR = 'P') /* Cur FAM or Other Family
*   COMPRESS 'Family Indicator must be " " or "P"' #SBS-FAM-IND
*       INTO #ERR-MSG
* END-IF
END-IF
*
*   SPS Info below
*
IF   (#SBS-SPS-DOB NE 0)
 AND (#SBS-SPS-DOB-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Spouse Date of Birth' #SBS-SPS-DOB INTO #ERR-MSG
END-IF
*
IF   (#SBS-SPS-DOM NE 0)
 AND (#SBS-SPS-DOM-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Sps Date of Marriage' #SBS-SPS-DOM INTO #ERR-MSG
END-IF
*
IF   (NOT #SBS-SPS-STAT = ' ' OR = 'S' OR = 'F' OR = 'M' OR = 'D')
  COMPRESS 'Invalid Spouse Status' #SBS-SPS-STAT INTO #ERR-MSG
END-IF
*
IF  (#SBS-SPS-NAME NE ' ')
 OR (#SBS-SPS-STAT NE ' ')
 OR (#SBS-SPS-DOB NE 0)
 OR (#SBS-SPS-DOM NE 0)
  IF  (#SBS-SPS-NAME = ' ')
   OR (#SBS-SPS-DOB = 0)
    MOVE 'Spouse Info must have Name and DOB and STATUS' TO #ERR-MSG
  END-IF
END-IF
*
*   Former SPS Info below
*
IF   (#SBS-F-SPS-DOB NE 0)
 AND (#SBS-F-SPS-DOB-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Former Spouse Date of Birth' #SBS-SPS-DOB
                  INTO #ERR-MSG
END-IF
*
IF   (#SBS-F-SPS-DOM NE 0)
 AND (#SBS-F-SPS-DOM-A NE MASK (YYYYMMDD))
  COMPRESS 'Invalid Former Spouse Date of Marriage' #SBS-SPS-DOM
        INTO #ERR-MSG
END-IF
*
IF NOT #SBS-F-SPS-STAT = ' ' OR = 'F' OR = 'D'
  COMPRESS 'Invalid Former Spouse Status' #SBS-F-SPS-STAT INTO #ERR-MSG
END-IF
*
IF  (#SBS-F-SPS-NAME NE ' ')
 OR (#SBS-F-SPS-STAT NE ' ')
 OR (#SBS-F-SPS-DOB NE 0)
 OR (#SBS-F-SPS-DOM NE 0)
  IF  (#SBS-F-SPS-NAME = ' ')
   OR (NOT #SBS-F-SPS-STAT = 'F' OR = 'D')
   OR (#SBS-F-SPS-DOB = 0)
    COMPRESS 'Former Spouse Info must have Name/DOB and status of "F"'
        INTO #ERR-MSG
  END-IF
END-IF
*
*    DEPN Info Below
*
RESET #NUM-DEPS-P #NUM-DEPS-BLK
FOR #I1 1 TO 10
  MOVE #SBS-DEP-OCC (#I1) TO #SBS-DEP-AREA
  IF   (#SBS-DEP-DOB-A NE ' ')
   AND (#SBS-DEP-DOB-A NE MASK (YYYYMMDD))
    COMPRESS 'Invalid DEP Date of Birth DEP #' #I1
     #SBS-DEP-NAME  #SBS-DEP-DOB-A
       INTO #ERR-MSG
    ESCAPE BOTTOM
  END-IF
*
  IF   (NOT #SBS-DEP-STAT = ' ' OR = 'P' OR = 'D'
              OR = 'I' OR = 'Y' OR = 'M')    /* Handicapped
    COMPRESS 'Invalid DEPN Status DEP #' #I1 #SBS-DEP-STAT
     #SBS-DEP-NAME INTO #ERR-MSG
    ESCAPE BOTTOM
  END-IF
*
  IF  (#SBS-DEP-NAME NE ' ')
   OR (#SBS-DEP-DOB NE 0)
   OR (#SBS-DEP-STAT NE ' ')
    IF  (#SBS-DEP-NAME = ' ')
     OR (#SBS-DEP-DOB  = 0)
      COMPRESS 'Depn Info must be complete DEP #' #I1
           'STAT=' #SBS-DEP-STAT 'DOB=' #SBS-DEP-DOB
           'NAME=' #SBS-NAME
       INTO #ERR-MSG
      ESCAPE BOTTOM
    END-IF
  END-IF
*
  WRITE 'CHEK P KIDS' #SBS-DEP-DOB  #SBS-AS-OF-DATE #SBS-DEP-STAT
  IF (#SBS-DEP-DOB  GE (#SBS-AS-OF-DATE - 260000)) /* Must be age 26
   OR (#SBS-DEP-STAT = 'I' OR = 'Y')    /* Handicapped
    IF #SBS-DEP-STAT = 'P' OR = 'Y'
      ADD 1 TO #NUM-DEPS-P
    ELSE
      IF #SBS-DEP-STAT NE 'D'
        ADD 1 TO #NUM-DEPS-BLK
      END-IF
    END-IF
  END-IF
END-FOR
WRITE 'tots p/blk' #NUM-DEPS-P #NUM-DEPS-BLK
*
IF  ((#SBS-SPS-NAME NE  ' ') AND (#SBS-SPS-STAT NE 'D'))
 OR (#NUM-DEPS-BLK GE 1)
  MOVE TRUE TO #HAS-CURRENT-BENES
END-IF
*
IF  ((#SBS-F-SPS-NAME NE ' ') AND (#SBS-F-SPS-STAT NE 'D'))
 OR (#NUM-DEPS-P GE 1)
  MOVE TRUE TO #HAS-FORMER-BENES
END-IF
*
IF  #SBS-TRAN-CODE = 'BA'
  IF #HAS-CURRENT-BENES OR #HAS-FORMER-BENES
    IGNORE
  ELSE
    MOVE 'No Current or Former Family dependents eligible for benefits'
                  TO #ERR-MSG
  END-IF
END-IF
*
END-SUBROUTINE
