* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GROLESXX
* FUNCTION: Prompts user when trying to update Org Role 
***********************************************************
*        MODIFICATION LOG
***********************************************************
* USER   DATE       TAG  REASON
* RSE    01/05/2012      Creation
********************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-1-ID     (N8)
1 #CONTACT-2-ID     (N8)
1 #OLD-ROLE-CD      (A1)
1 #NEW-ROLE-CD      (A1)
1 #OLD-CONTACT-TYPE (A1)
1 #NEW-CONTACT-TYPE (A1)
1 #CN-NO            (N2)
1 #RESPONSE         (A1)
1 #CHANGE-REASON    (A47)
LOCAL
*
1 CNT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
*
1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
*
1 GEN-V VIEW OF A-GEN-TABLE
  2 GEN-ALPHA-VALUE                   (A50)
  2 ADDL-INFO
*
1 #CN-LINE-1        (A46)
1 #CN-LINE-2        (A46)
1 #OLD-TRUST-LINE   (A47)
1 #NEW-TRUST-LINE   (A50)
1 #TRUST-MESSAGE    (A47)
END-DEFINE
* 
DEFINE WINDOW Confirm-Role-Change
  SIZE 15 * 54
  BASE 9/5
  FRAMED ON POSITION SYMBOL AUTO
*
MOVE 'N' TO #RESPONSE
RESET #CHANGE-REASON
*
FIND CNT-REL-V WITH CONTACT-2-ID = #CONTACT-2-ID
  ACCEPT IF CONTACT-1-ID NE #CONTACT-1-ID
  FIND CNT-V WITH CONTACT-ID = CONTACT-1-ID
    ADD 1 TO #CN-NO
    IF #CN-NO < 8
      COMPRESS #CN-LINE-1 ID-NUMBER INTO #CN-LINE-1
    ELSE
      COMPRESS #CN-LINE-2 ID-NUMBER INTO #CN-LINE-2
    END-IF
  END-FIND
END-FIND
*
IF #CN-NO > 0
  IF #OLD-ROLE-CD NE #NEW-ROLE-CD
    FIND GEN-V WITH SEC-KEY = 'CODE041' WHERE GEN-ALPHA-VALUE = #OLD-ROLE-CD
      MOVE ADDL-INFO TO #OLD-TRUST-LINE
    END-FIND
    FIND GEN-V WITH SEC-KEY = 'CODE041' WHERE GEN-ALPHA-VALUE = #NEW-ROLE-CD
      MOVE ADDL-INFO TO #NEW-TRUST-LINE
    END-FIND
    COMPRESS 'Changing the Role Code will affect other' TO #TRUST-MESSAGE
  ELSE
    IF #OLD-CONTACT-TYPE = 'I'
      MOVE 'Individual' TO #OLD-TRUST-LINE
    ELSE
      IF #OLD-CONTACT-TYPE = 'O'
        MOVE 'Organization' TO #OLD-TRUST-LINE
      ELSE
        MOVE 'Not in table' TO #OLD-TRUST-LINE
      END-IF
    END-IF
    IF #NEW-CONTACT-TYPE = 'I'
      MOVE 'Individual' TO #NEW-TRUST-LINE
    ELSE
      IF #NEW-CONTACT-TYPE = 'O'
        MOVE 'Organization' TO #NEW-TRUST-LINE
      ELSE
        MOVE 'Not in table' TO #NEW-TRUST-LINE
      END-IF
    END-IF
    COMPRESS 'Changing the Contact Type will affect other' TO #TRUST-MESSAGE
  END-IF        
  COMPRESS H'22' #OLD-TRUST-LINE H'22' INTO #OLD-TRUST-LINE LEAVING NO
  COMPRESS H'22' #NEW-TRUST-LINE H'22' INTO #NEW-TRUST-LINE LEAVING NO
  COMPRESS 'This' #OLD-TRUST-LINE 'will be changed to' INTO #OLD-TRUST-LINE
  COMPRESS #NEW-TRUST-LINE 'for the following CN(s) too:' INTO #NEW-TRUST-LINE  
  SET WINDOW 'Confirm-Role-Change'  
  INPUT WINDOW='Confirm-Role-Change' USING MAP 'GROLEMXX'
  IF NOT (#RESPONSE = 'Y' OR = 'N')
    REINPUT 'Invalid response.'
  END-IF
  IF #RESPONSE = 'Y' 
    IF #CHANGE-REASON = ' '
      REINPUT 'Please enter reason for change.' 
        MARK *#CHANGE-REASON
    ELSE
      MOVE LEFT #CHANGE-REASON TO #CHANGE-REASON
      IF #CHANGE-REASON = MASK(.'                                              ')
        REINPUT 'Please enter a more descriptive reason for change.'
          MARK *#CHANGE-REASON
      END-IF
    END-IF
  END-IF
END-IF
SET WINDOW OFF
*
END
