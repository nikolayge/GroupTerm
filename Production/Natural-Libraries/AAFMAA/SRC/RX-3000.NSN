* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* RX-20 RX Retrievet
* bz Oct 1, 2017. Ver-2.4(10/09/2017)
* ===================================
DEFINE DATA
PARAMETER USING RX000PDA
PARAMETER
1 p_Parms-Det
  2 El_B  (A10/300)
  2 El_E  (A10/300)
  2 Fills (999)
    3 Fl_r      (A/17) DYNAMIC
    3 it_Name   (A58/20)
    3 it_Cd     (A20/20)
LOCAL USING RX000LDA
LOCAL
1 AT-V VIEW of A-APPL-TRACKING
  2 NAME
  2 PLAN
  2 MEMBER-CN
* -----------------
1  f_Template_html   (A) DYNAMIC INIT <'/usr/SAG/APPS/RX/HTML.html'>
1  f_html            (A) DYNAMIC INIT <'/usr/SAG/APPS/RX/RESULT/Details_@@AT.html'>
1 f_index           (A) DYNAMIC INIT <'/usr/SAG/APPS/RX/RESULT/upload_index.xml@@AT'>
* -----------------
1 FTP_CMD               (A) DYNAMIC INIT <'/usr/SAG/BV/bv-put @@OPT @@LOCAL FTP/NewBiz/rxReports'>
1 FTP_CMD_DAL           (A) DYNAMIC INIT <'/usr/SAG/BV/bv-put @@OPT @@LOCAL FTP/EDS/Downloads/dale/RX-@@AT'>
1 FTP_CMD_IDX           (A) DYNAMIC INIT <'/usr/SAG/BV/bv-put @@OPT @@LOCAL FTP/EDS/Downloads/dale/RX-@@AT upload_index.xml'>
* -----------------
1 v_A                   (A) DYNAMIC
1 v_B                   (A) DYNAMIC
1 v_C                   (A) DYNAMIC
1 v_Dt                  (A) DYNAMIC
1 v_D                   (D)
1 HTML                  (A) DYNAMIC
1 l_Test                (L)
1 #i          (I2) (EM=z9)
1 #k          (I2) (EM=z9)
1 #r          (I2) (EM=z9)
1 #rr         (I2)
1 L@          (L)
1 id_x        (A4)

1 Drug_Fills (99)
  2 Drug_Name (A) DYNAMIC
  2 MM_1-12 (A7/1:5,1:12)  INIT ALL <'&#9633;'>

1 #F             (I2)

1 Fills_5Y  (1:5)
  2 YYYY_1_5  (A4)
  2 Hi        (N2/1:12)
  2 H12       (A) DYNAMIC
  2 Me        (N2/1:12)
  2 M12       (A) DYNAMIC
  2 Lo        (N2/1:12)
  2 L12       (A) DYNAMIC
  2 Al        (N3/1:12)     /* Total
  2 A12       (A) DYNAMIC   /* new
1 hi_0        (N2)
1 me_0        (N2)
1 lo_0        (N2)
1 Al_0        (N3)      /* Total Prior
1 id_Al_0     (A5)      /* Total Prior color
1 id_Al       (A5)      /* Total class for every cell
1 H2          (A2)
1 M2          (A2)
1 L2          (A2)
1 A2          (A)   DYNAMIC
1 YYYY_now    (N4)
1 YYYY        (N4)
1 MM          (N2)
1 A04         (A04)

1 dm          (A15) INIT  <'|'>
1 bl          (A6)  INIT  <'&nbsp;'>
1 ys          (A7)  INIT <'&#9632;'>
1 docs        (30)
  2 Doc       (A) DYNAMIC
  2 Drug     (40)
    3  Fill   (A) DYNAMIC
    3  Prio   (A) DYNAMIC
    3  Date   (A) DYNAMIC
    3  Doc_Drug_No (N2)
1 #D          (I2)  (EM=z9)
* ---------------------------------------------------------------------------------------
1 xml_TMPL       (A80/12)
  INIT<
  '<?xml version="1.0" encoding="utf-8"?>',
  '<DOCUMENTS>',
  '<DOCUMENT>',
  '<DOCTITLE>RXR</DOCTITLE>',
  '<CN>@@CN</CN>',
  '<POL-NUM>@@POL</POL-NUM>',
  '<SUBMITDATE>@@DATE</SUBMITDATE>',
  '<FILENAME>@@FILE</FILENAME>',
  '<DESCRIPTION>Underwriting</DESCRIPTION>',
  '<MEMBERACCESSIBLE>FALSE</MEMBERACCESSIBLE>',
  '</DOCUMENT>',
  '</DOCUMENTS>'
  >
END-DEFINE

ON ERROR
  PRINT *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR /  '='#r  '='#i   '='#F '='#k '=' MM              /* --db
  COMPRESS *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR INTO p_Message
  p_RC := 5
  ESCAPE ROUTINE
END-ERROR
* - - - - - - - - - - - - - - - - - -
* FORMAT PS=23

IF p_Mode = 'T' l_Test := TRUE  END-IF
IF l_Test
  EXAMINE FTP_CMD     FOR '@@OPT' REPLACE '-t'
  EXAMINE FTP_CMD_DAL FOR '@@OPT' REPLACE '-t -m'
  EXAMINE FTP_CMD_IDX FOR '@@OPT' REPLACE '-t'
  COMPRESS p_AT '-T' INTO v_A LEAVING NO SPACE
ELSE
  EXAMINE FTP_CMD     FOR '@@OPT' REPLACE '-p'
  EXAMINE FTP_CMD_DAL FOR '@@OPT' REPLACE '-p -m'
  EXAMINE FTP_CMD_IDX FOR '@@OPT' REPLACE '-p'
  v_A := p_AT
END-IF
EXAMINE f_html   FOR '@@AT' REPLACE v_A
EXAMINE f_index  FOR '@@AT' REPLACE v_A
EXAMINE FTP_CMD_DAL  FOR '@@AT' REPLACE v_A
EXAMINE FTP_CMD_IDX  FOR '@@AT' REPLACE v_A
EXAMINE FTP_CMD     FOR '@@LOCAL' REPLACE f_html
EXAMINE FTP_CMD_DAL FOR '@@LOCAL' REPLACE f_html
EXAMINE FTP_CMD_IDX FOR '@@LOCAL' REPLACE f_index
DEFINE WORK FILE 1 f_Template_html TYPE 'UNFORMATTED'
READ WORK 1 ONCE HTML
CLOSE WORK 1
FIND(1) AT-V WITH ID-NUMBER = p_AT END-FIND
FIND(1) RX-V WITH RX-AT = p_AT

  EXAMINE HTML FOR '@@AT'       REPLACE v_A  /* inherided from above
  v_A := *TRIM(AT-V.NAME)
  EXAMINE HTML FOR '@@NAME' REPLACE v_A
  EXAMINE HTML FOR '@@PLAN' REPLACE AT-V.PLAN
  EXAMINE HTML FOR '@@CN' REPLACE AT-V.MEMBER-CN
  MOVE EDITED RX-UPD(EM=99999999) TO v_A
  MOVE EDITED v_A  TO v_D (EM=YYYYMMDD)
  MOVE EDITED v_D(EM=DD/MM/YYYY) TO v_A
  EXAMINE HTML FOR '@@RX-DATE'  REPLACE v_A

  EXAMINE HTML FOR '@@RX-CLERK' REPLACE RX-CLERK
  EXAMINE HTML FOR '@@RX-STATUS' REPLACE RX-STATUS

  EXAMINE HTML FOR '@@URL'      REPLACE RX-URL
  EXAMINE HTML FOR '@@S-MAX'    REPLACE S-MAX   /* Max Priority @@S-MAX
  v_A := S-RX                                 /* Fills @@S-RX
  EXAMINE HTML FOR '@@S-RX'     REPLACE v_A
  v_A := S-HIGH                               /* High @@S-HIGH
  EXAMINE HTML FOR '@@S-HIGH'   REPLACE v_A
  v_A := S-MEDIUM                             /* Medium @@S-MEDIUM
  EXAMINE HTML FOR '@@S-MEDIUM' REPLACE v_A
  v_A := S-LOW                                /* Low @@S-LOW
  EXAMINE HTML FOR '@@S-LOW'    REPLACE v_A
  v_A := S-ELIG                               /* Eligibility @@S-ELIG
  EXAMINE HTML FOR '@@S-ELIG'   REPLACE v_A
  DECIDE ON FIRST VALUE OF S-MAX
    VALUE 'HIGH'   id_x := 'id_h'
    VALUE 'MEDIUM' id_x := 'id_m'
    VALUE 'LOW'    id_x := 'id_l'
    NONE           id_x := 'id_n'
  END-DECIDE
  EXAMINE HTML FOR '@@id_x'   REPLACE id_x
  PERFORM sbr_Sum

  PERFORM sbr_Det
  EXAMINE HTML FOR '@@DET'   REPLACE v_Dt

  PERFORM sbr_ELIG
  EXAMINE HTML FOR '@@ELIG'   REPLACE v_Dt

  PERFORM sbr_DOC
  EXAMINE HTML FOR '@@DOC'   REPLACE v_Dt

  DEFINE WORK FILE 1 f_html TYPE 'UNFORMATTED'
  L@ := FN_Exist(<f_html,1>)
  WRITE WORK FILE 1 VARIABLE HTML
  CLOSE WORK FILE 1
END-FIND

call 'shcmd' FTP_CMD
p_RC := RET('shcmd')
IF p_RC <> 0
  PRINT 'FTP report fail code:' p_RC
  ESCAPE ROUTINE
END-IF

** ESCAPE ROUTINE                                     /* Bypass DAL  --db

call 'shcmd' FTP_CMD_DAL
p_RC := RET('shcmd')
IF p_RC <> 0
  PRINT 'FTP DAL fail code:' p_RC
  ESCAPE ROUTINE
END-IF

PERFORM sbr_DAL

call 'shcmd' FTP_CMD_IDX
p_RC := RET('shcmd')
IF p_RC <> 0
  PRINT 'FTP DAL Index fail with code:' p_RC /
    FTP_CMD_IDX
  ESCAPE ROUTINE
END-IF
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE sbr_Sum
*      -------
MOVE EDITED *DATX (EM=YYYY) TO A04
YYYY_now := val(A04)
FOR #i =  0 TO 4
  YYYY_1_5(5 - #i)  :=  YYYY_Now - #i
END-FOR
EXAMINE HTML FOR '@@_1' REPLACE YYYY_1_5(1)
EXAMINE HTML FOR '@@_2' REPLACE YYYY_1_5(2)
EXAMINE HTML FOR '@@_3' REPLACE YYYY_1_5(3)
EXAMINE HTML FOR '@@_4' REPLACE YYYY_1_5(4)
EXAMINE HTML FOR '@@_5' REPLACE YYYY_1_5(5)

FOR #r = 1 TO S-RX
  IF Fl_r(#r,1) <> MASK(YYYY'-'MM) ESCAPE TOP END-IF
  A04   := SUBSTR(Fl_r(#r,1),1,4)
  YYYY  := val(A04)
  A04   := SUBSTR(Fl_r(#r,1),6,2)
  MM    := val(A04)
  #k := YYYY_now - YYYY
  IF #k > 5
    #k := 5
  END-IF
  #k := 5 - #k
* -----------------------------
  IF #k > 0 /* No indication for prior years
    EXAMINE Drug_Name(*) FOR Fl_r(#r,3) GIVING INDEX #rr
    IF #rr > 0
      MM_1-12(#rr,#k,MM) := ys
    ELSE  /* New Drug
      #F := #F + 1
      Drug_Name(#F) := Fl_r(#r,3)
      MM_1-12(#F,#k,MM) := ys
    END-IF
  END-IF
* -----------------------------
  DECIDE ON FIRST VALUE OF Fl_r(#r,8)
    VALUE 'HIGH'   IF #k = 0  hi_0 := hi_0 + 1  ELSE hi(#k,MM) := hi(#k,MM) + 1  END-IF
    VALUE 'MEDIUM' IF #k = 0  me_0 := me_0 + 1  ELSE me(#k,MM) := me(#k,MM) + 1  END-IF
    VALUE 'LOW'    IF #k = 0  lo_0 := lo_0 + 1  ELSE lo(#k,MM) := lo(#k,MM) + 1  END-IF
    ANY            IF #k = 0  al_0 := al_0 + 1  ELSE al(#k,MM) := al(#k,MM) + 1  END-IF  /* All
    NONE IGNORE
  END-DECIDE
END-FOR

FOR #i = 1 TO 5
  IF  Al(#i,1:12) <> 0
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
  /*   new Assign id_A  <B
  FOR #k = 1 TO 12
    /* color for Total
    id_Al := ' '
    DECIDE FOR FIRST CONDITION
      WHEN hi(#i,#k) <> 0
        id_Al := 'id_ht'
      WHEN me(#i,#k) <> 0 AND id_Al <> 'id_ht'
        id_Al := 'id_mt'
      WHEN lo(#i,#k) <> 0 AND id_Al = ' '
        id_Al := 'id_lt'
      WHEN NONE id_Al := 'id_nt'
    END-DECIDE

    DECIDE FOR FIRST CONDITION
      WHEN  al(#i,#k) = 0
        COMPRESS A12(#i) '<i class="id_nt">' bl '</i>' INTO A12(#i) LEAVING NO SPACE
        IF hi(#i,*) <> 0
          COMPRESS H12(#i) '<i class="id_ht">' bl '</i>' INTO H12(#i) LEAVING NO SPACE
        END-IF
        IF me(#i,*) <> 0
          COMPRESS M12(#i) '<i class="id_ht">' bl '</i>' INTO M12(#i) LEAVING NO SPACE
        END-IF
        IF lo(#i,*) = 0
          COMPRESS L12(#i) '<i class="id_ht">' bl '</i>' INTO L12(#i) LEAVING NO SPACE
        END-IF

      WHEN al(#i,#k) < 10
        COMPRESS A12(#i) '<i class="' id_Al '">' al(#i,#k) '</i>'  INTO A12(#i) LEAVING NO SPACE
        /* hi
        IF hi(#i,1:12) <> 0
          IF hi(#i,#k) > 0
            COMPRESS H12(#i) '<i class="id_ht">' hi(#i,#k) '</i>' INTO H12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS H12(#i) '<i class="id_ht">' bl        '</i>' INTO H12(#i) LEAVING NO SPACE
          END-IF
        END-IF
        /* me
        IF me(#i,1:12) <> 0
          IF me(#i,#k) > 0
            COMPRESS M12(#i) '<i class="id_mt">' me(#i,#k) '</i>' INTO M12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS M12(#i) '<i class="id_mt">' bl        '</i>' INTO M12(#i) LEAVING NO SPACE
          END-IF
        END-IF
        /* lo
        IF lo(#i,1:12) <> 0
          IF lo(#i,#k) > 0
            COMPRESS L12(#i) '<i class="id_lt">' lo(#i,#k) '</i>' INTO L12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS L12(#i) '<i class="id_lt">' bl        '</i>' INTO L12(#i) LEAVING NO SPACE
          END-IF
        END-IF
      WHEN al(#i,#k) > 9
        COMPRESS A12(#i) '<i class="' id_Al '">' al(#i,#k) '</i>'  INTO A12(#i) LEAVING NO SPACE
        /* hi
        IF hi(#i,1:12) <> 0
          IF hi(#i,#k) > 9
            COMPRESS H12(#i) '<i class="id_ht">'    hi(#i,#k) '</i>' INTO H12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS H12(#i) '<i class="id_ht">' bl hi(#i,#k) '</i>' INTO H12(#i) LEAVING NO SPACE
          END-IF
        END-IF
        /* me
        IF me(#i,1:12) <> 0
          IF me(#i,#k) > 9
            COMPRESS M12(#i) '<i class="id_mt">'   me(#i,#k) '</i>' INTO M12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS M12(#i) '<i class="id_mt">' bl me(#i,#k) '</i>' INTO M12(#i) LEAVING NO SPACE
          END-IF
        END-IF
        /* lo
        IF lo(#i,1:12) <> 0
          IF lo(#i,#k) > 9
            COMPRESS L12(#i) '<i class="id_lt">'    lo(#i,#k) '</i>' INTO L12(#i) LEAVING NO SPACE
          ELSE
            COMPRESS L12(#i) '<i class="id_lt">' bl lo(#i,#k) '</i>' INTO L12(#i) LEAVING NO SPACE
          END-IF
        END-IF
      WHEN NONE IGNORE
    END-DECIDE
  END-FOR
END-FOR /* for every year

al_0 := hi_0 + me_0 + lo_0

MOVE EDITED hi_0 (EM=ZZ) TO H2
MOVE EDITED me_0 (EM=ZZ) TO M2
MOVE EDITED lo_0 (EM=ZZ) TO L2

EXAMINE HTML FOR '@@Y_0H' REPLACE H2
EXAMINE HTML FOR '@@Y_1H' REPLACE H12(1)
EXAMINE HTML FOR '@@Y_2H' REPLACE H12(2)
EXAMINE HTML FOR '@@Y_3H' REPLACE H12(3)
EXAMINE HTML FOR '@@Y_4H' REPLACE H12(4)
EXAMINE HTML FOR '@@Y_5H' REPLACE H12(5)

EXAMINE HTML FOR '@@Y_0M' REPLACE M2
EXAMINE HTML FOR '@@Y_1M' REPLACE M12(1)
EXAMINE HTML FOR '@@Y_2M' REPLACE M12(2)
EXAMINE HTML FOR '@@Y_3M' REPLACE M12(3)
EXAMINE HTML FOR '@@Y_4M' REPLACE M12(4)
EXAMINE HTML FOR '@@Y_5M' REPLACE M12(5)

EXAMINE HTML FOR '@@Y_0L' REPLACE L2
EXAMINE HTML FOR '@@Y_1L' REPLACE L12(1)
EXAMINE HTML FOR '@@Y_2L' REPLACE L12(2)
EXAMINE HTML FOR '@@Y_3L' REPLACE L12(3)
EXAMINE HTML FOR '@@Y_4L' REPLACE L12(4)
EXAMINE HTML FOR '@@Y_5L' REPLACE L12(5)

EXAMINE HTML FOR '@@Y_0T' REPLACE A2
EXAMINE HTML FOR '@@Y_1T' REPLACE A12(1)
EXAMINE HTML FOR '@@Y_2T' REPLACE A12(2)
EXAMINE HTML FOR '@@Y_3T' REPLACE A12(3)
EXAMINE HTML FOR '@@Y_4T' REPLACE A12(4)
EXAMINE HTML FOR '@@Y_5T' REPLACE A12(5)

END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE sbr_DET
*      -------
IF S-RX = 0 v_Dt := ' ' ESCAPE ROUTINE END-IF
v_dt :='<tr class="tr_h"><td>Product/Generic Name</td><td>Priority</td><td>Fill Date</td><td>Doctor</td><td>Quantity</td><td>Days<br />Supply</td></tr>'
COMPRESS v_dt '<tbody id="tb_DET">' H'0A' INTO v_dt LEAVING NO SPACE
FOR #r = 1 TO S-RX
  DECIDE ON FIRST VALUE OF Fl_r(#r,8)
    VALUE 'HIGH'   id_x := 'id_h'
    VALUE 'MEDIUM' id_x := 'id_m'
    VALUE 'LOW'    id_x := 'id_l'
    NONE IGNORE
  END-DECIDE

  COMPRESS v_Dt '<tr class="tr_b"> <td> <span class="' id_x  '">' '<b>' Fl_r(#r,3) '</b>' '</span>' '<br />' Fl_r(#r,5)'</td>'
    '<td class=' id_x '>' Fl_r(#r,8)'</td> <td>' Fl_r(#r,1)'</td>'
    '<td>' Fl_r(#r,9)'</td> <td>' Fl_r(#r,6)'</td> <td>' Fl_r(#r,7)'</td> </tr>' H'0A'
    INTO v_Dt LEAVE NO SPACE
/**/
  FOR #i = 1 TO 20
    IF it_Name(#r,#i) = ' ' ESCAPE TOP END-IF
    IF #i = 1 v_A := it_Name(#r,#i) ESCAPE TOP END-IF
    COMPRESS v_A '&ast;' it_Name(#r,#i) INTO v_A
  END-FOR
  COMPRESS v_dt '<tr class="i_v"> <td colspan="6" style="padding-left:180px">'  v_A '</td></tr>' H'0A' INTO v_dt
* ------------------------------------------------ summ for one grug >>
  EXAMINE Drug_Name(*) FOR Fl_r(#r,3) INDEX #rr
  IF #rr = 0
    IGNORE  /* PRINT 'Error find'
  ELSE
    COMPRESS v_Dt '<tr>' '<td colspan="6">'
      MM_1-12(#rr,1,1:12) dm
      MM_1-12(#rr,2,1:12) dm
      MM_1-12(#rr,3,1:12) dm
      MM_1-12(#rr,4,1:12) dm
      MM_1-12(#rr,5,1:12) dm
      '</td></tr>' H'0A'
      INTO v_Dt LEAVE NO SPACE
    Drug_Name(#rr) := '---'
  END-IF
* ------------------------------------------------ summ for one grug <<
END-FOR
COMPRESS v_dt '</tbody>' H'0A' INTO v_dt LEAVING NO SPACE
/**/
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE sbr_ELIG
*      -------
IF S-ELIG = 0 v_Dt := ' ' ESCAPE ROUTINE END-IF
v_Dt :='<thead><tr><th>Begins</th><th>End Date</th></tr></thead>'
FOR #i = 1 TO S-ELIG
  COMPRESS v_Dt '<tr> <td>' El_b(#i) '</td> <td>' El_e(#i)'</td></tr>' INTO v_Dt LEAVE NO SPACE
END-FOR
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE sbr_DOC
*      -------
IF S-RX = 0 v_Dt := ' ' ESCAPE ROUTINE END-IF
#i := 0
FOR #r = 1 TO S-RX
  DECIDE ON FIRST VALUE OF Fl_r(#r,8)
    VALUE 'HIGH'   id_x := 'id_h'
    VALUE 'MEDIUM' id_x := 'id_m'
    VALUE 'LOW'    id_x := 'id_l'
    NONE IGNORE
  END-DECIDE

  EXAMINE Doc(*) FOR Fl_r(#r,9) GIVING INDEX #k
  IF #k > 0
    FOR #i = 1 TO 30
      IF Fill(#k, #i) <> ' ' AND Fill(#k, #i) <> Fl_r(#r,3)
        ESCAPE TOP
      END-IF
      IF Fill(#k, #i) <> ' '  AND Fill(#k, #i) = Fl_r(#r,3)
        Doc_drug_No(#k, #i) := Doc_drug_No(#k, #i) + 1
        IF Date(#k, #i) <  Fl_r(#r,1)
          Date(#k, #i) := Fl_r(#r,1)
        END-IF
        ESCAPE BOTTOM
      END-IF
      IF Fill(#k, #i) = ' '
        Fill(#k, #i) := Fl_r(#r,3)
        COMPRESS '<i class="' id_x '">' Fl_r(#r,8)'</i>' INTO Prio(#k, #i) LEAVING NO SPACE
        Date(#k, #i) := Fl_r(#r,1)
        ESCAPE BOTTOM
      END-IF
    END-FOR
  ELSE  /* New Doc
    #D := #D + 1
    Doc(#D) := Fl_r(#r,9)
    Fill(#D, 1) := Fl_r(#r,3)
    Doc_drug_No(#D, 1) := 1
    COMPRESS '<i class="' id_x '">' Fl_r(#r,8)'</i>' INTO Prio(#D, 1) LEAVING NO SPACE
    Date(#D, 1) := Fl_r(#r,1)
  END-IF
END-FOR
* Now printing
v_dt :='<tr class="tr_h"><td>Doctor</td><td>Drugs</td><td>Priority</td><td>Last Fill Date</td></tr>'

FOR #i = 1 TO #D
  v_A := ' ' v_B := ' ' v_C := ' ' A2 := ' '
  FOR #k = 1 TO 30
    IF Fill(#i, #k) = ' ' ESCAPE BOTTOM END-IF
    COMPRESS v_A '<span>' Fill(#i, #k) '</span><br />' INTO v_A
    COMPRESS v_B Prio(#i, #k) '<br />' INTO v_B
    IF Doc_Drug_No(#i, #k) > 1
      COMPRESS Date(#i, #k) '{' Doc_Drug_No(#i, #k) '}' INTO Date(#i, #k)
    END-IF
    COMPRESS v_C Date(#i, #k) '<br />' INTO v_C
  END-FOR
  A2 := Doc(#i)
  EXAMINE Fl_r(*,9) FOR A2 GIVING INDEX #r #rr
  FOR #k = 10 TO 15
    IF Fl_r(#r,#k) <> ' '
      COMPRESS A2 '<br />' Fl_r(#r,#k) INTO A2 LEAVING NO SPACE
    END-IF
  END-FOR
  COMPRESS v_Dt '<tr><td>' A2 '</td><td>' v_A '</td><td>' v_B '</td><td>' v_C INTO v_Dt LEAVE NO SPACE
END-FOR

END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE sbr_DAL
v_A := AT-V.MEMBER-CN
EXAMINE xml_TMPL(*) FOR '@@CN'   REPLACE v_A
COMPRESS v_A '-P' INTO v_A LEAVING NO SPACE
EXAMINE xml_TMPL(*) FOR '@@POL'  REPLACE v_A
COMPRESS *DAT4U INTO v_A
EXAMINE xml_TMPL(*) FOR '@@DATE' REPLACE v_A
EXAMINE DIRECTION BACKWARD f_html  FOR '/' POSITION #i
#i := #i + 1
v_A := SUBSTRING(f_html, #i)
EXAMINE xml_TMPL(*) FOR '@@FILE' REPLACE v_A
DEFINE WORK FILE 1 f_index TYPE 'ASCII-COMPRESSED'
L@ := FN_Exist(<f_index,1>)

FOR #i = 1 TO *OCC(xml_TMPL)
  WRITE WORK FILE 1 xml_TMPL(#i)
END-FOR
CLOSE WORK FILE 1
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*         '<FillDate>',                    /*  01
*         '<NDC>',                         /*  02
*         '<DrugBrandName>',               /*  03
*         '<DrugGenericName>',             /*  04
*         '<DrugGenericNameAndDosage>',    /*  05
*         '<Quantity>',                    /*  06
*         '<DaysSupply>',                  /*  07
*         '<DrugPriority>',                /*  08
*         '<PhysicianName>',               /*  09
*         '<PhysicianSpecialty>',          /*  10
*         '<PhysicianAddress1>',           /*  11
*         '<PhysicianAddress2>',           /*  12
*         '<PhysicianAddress3>',           /*  13
*         '<PhysicianAddress4>',           /*  14
*         '<PhysicianPhone>',              /*  15
* /usr/SAG/BV/bv-put -t /usr/SAG/APPS/RX/RESULT/Details_500000-p.html FTP/NewBiz/BORIS
END
