* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2001PNM
***********************************************************************************
*                             MODIFICATION LOG                                    *
* USER   DATE      TAG    REASON                                                  *
* PAM  ??/??/2011  None   CRM project - revised NM CON screen for correct layout  *
***********************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING GOADA001
LOCAL USING GOADA002
LOCAL USING GOEMA001
LOCAL USING GOCOA001
LOCAL USING GOPHA001
LOCAL USING GPRA0004
LOCAL USING GPRAMINF
LOCAL USING ADDA0001
LOCAL USING C1009AAT
LOCAL USING OBJAGENT
LOCAL USING GDATEAXX
LOCAL USING GOSTMA01
*
LOCAL
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL
  2 ID-NUMBER
  2 ENTRY-DATE
  2 REC-ISN
  2 CODE
  2 ISSUE-DATE
  2 NUM-MBRSHIPS
*
1 RR-V VIEW OF A-RANK-REFERENCE
  2 MIL-ABBR-KEY
  2 RANK-ID
  2 RANK-IN-SAS
*
1 #MIL-ID                             (A1)
1 #MIL-ABBR-KEY                       (A26)
1 #FOUND                              (L)
1 #SCR-PREFIX-TITLE                   (A10)
1 #PREFIX-TITLE-DESC                  (A40)
1 #HELP-DESC                          (A20)
*
1 #CT
  2 CONTACT-TYPE-DISP                 (A20)
  2 CONTACT-ID                        (N8.0)
  2 ID-NUMBER                         (N6)
  2 CONTACT-TYPE                      (A1)
  2 RANK-ST                           (A3)
  2 RANK-ABBR                         (A10)
  2 RANK-ID                           (N6)
  2 MILITARY-SERVICE                  (A3)
  2 MILITARY-STATUS                   (A1)
  2 PREFIX-TITLE                      (A25)
  2 ORGANIZATION-NAME                 (A75)
  2 WEBSITE                           (A70)
  2 FULL-NAME
    3 FIRST-NAME                      (A25)
    3 MIDDLE-NAME                     (A25)
    3 LAST-NAME                       (A25)
    3 SUFFIX                          (A10)
  2 GENDER-CD                         (A1)
  2 DATE-OF-BIRTH                     (N8.0) 2 REDEFINE DATE-OF-BIRTH
    3 #MM                             (N2)
    3 #DD                             (N2)
    3 #YYYY                           (N4)
  2 SSN                               (N9.0)
  2 REDEFINE SSN
    3 SSN-1 (A3)
    3 SSN-2 (A2)
    3 SSN-3 (A4)
  2 FIRM-ESTABLISHED-DATE             (N8)
*
1 CONTACT VIEW OF A-CONTACTS
  2 PREFIX-TITLE
  2 FIRM-ESTABLISHED-DATE
1 #UPDATED                            (L)
*
1 #PH
  2 #HOME
    3 #H_PH1                          (N3)
    3 #H_PH2                          (N3)
    3 #H_PH3                          (N4)
    3 #H_PH4                          (N6)
    3 #H_PH_ID                        (N8)
  2 #WORK
    3 #W_PH1                          (N3)
    3 #W_PH2                          (N3)
    3 #W_PH3                          (N4)
    3 #W_PH4                          (N6)
    3 #W_PH_ID                        (N8)
  2 #CELL
    3 #C_PH1                          (N3)
    3 #C_PH2                          (N3)
    3 #C_PH3                          (N4)
    3 #C_PH_ID                        (N8)
*
1 #PHS                                (A16)
1 REDEFINE #PHS
  2 #PH1                              (A3)
  2 #PH2                              (A3)
  2 #PH3                              (A4)
  2 #PH4                              (A6)
*
1 #ZIP-5                              (A5)
1 #ZIP-4                              (A4)
*
1 #TR-HOME-PH                         (A20)
1 #TR-WORK-PH                         (A20)
1 #TR-CELL-PH                         (A20)
1 #COUNTRY                            (A50)
1 #STATE                              (A25)
1 #HOLD-CONTACT-TYPE                  (A1)
1 #HOLD-COUNTRY-CODE                  (A2)
1 #SCR-CV                             (C)
1 #SCR-CONTACT-ID                     (N8)
1 #SCR-MEMBER-CN                      (N6)
1 #TR-FUNCTION                        (A)  DYNAMIC
1 #MIL-I                              (I4)
1 #ADDR-VALID-TXT                     (A15)
*
1 #CN-PARMS
  2 CN                                 (N6)
  2 ERR                                (N4)
  2 ERR-DESC                           (A78)
  2 MOD                                (A1)
*
END-DEFINE
**********************************************************************************
*
INCLUDE GONERCXX
*
#ADDR-POOL.COUNTRY-CODE := 'US'
#SCR-CV := (AD=U)
#UI.#ID-NUMBER := 999999
*
#CT.CONTACT-TYPE := 'I'
#CT.CONTACT-TYPE-DISP := 'Individual'
PERFORM NEW-CONTACT
*
RESET #CGA-SCR-SUFFIX
#CGA-SCR-COMMAND := 'CG'
#CGA-SCR-ID      := #CT.ID-NUMBER
*
FETCH 'G1000PXX'
*
**********************************************************************************
DEFINE SUBROUTINE NEW-CONTACT
**********************************************************************************
*
#HOLD-CONTACT-TYPE    := #CT.CONTACT-TYPE
#HOLD-COUNTRY-CODE    := #ADDR-POOL.COUNTRY-CODE
#COUNTRY := FUNCTION-CNRY-CHECK (< #ADDR-POOL.COUNTRY-CODE >)
*
DECIDE FOR FIRST CONDITION
  WHEN #CT.CONTACT-TYPE = 'I' AND #ADDR-POOL.COUNTRY-CODE = 'US'
    INPUT MARK *#CT.LAST-NAME USING MAP 'C200AMNM'
  WHEN #CT.CONTACT-TYPE = 'I' AND #ADDR-POOL.COUNTRY-CODE NE 'US'
    INPUT MARK *#ADDR-POOL.COUNTRY-CODE USING MAP 'C200BMNM'
  WHEN #CT.CONTACT-TYPE = 'O' AND #ADDR-POOL.COUNTRY-CODE = 'US'
    INPUT MARK *#CT.ORGANIZATION-NAME USING MAP 'C200CMNM'
  WHEN #CT.CONTACT-TYPE = 'O' AND #ADDR-POOL.COUNTRY-CODE NE 'US'
    INPUT MARK *#ADDR-POOL.COUNTRY-CODE USING MAP 'C200DMNM'
  WHEN NONE
    INPUT MARK *#CT.LAST-NAME USING MAP 'C200AMNM'
END-DECIDE
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF NOT #CT.CONTACT-TYPE = 'I' OR = 'O'
  REINPUT FULL 'Please choose "I" for individual or "O" for organization'
    MARK *#CT.CONTACT-TYPE
END-IF
*
IF #HOLD-CONTACT-TYPE NE #CT.CONTACT-TYPE
    OR #HOLD-COUNTRY-CODE NE #ADDR-POOL.COUNTRY-CODE
  IF #CT.CONTACT-TYPE = 'I'
    #CT.CONTACT-TYPE-DISP := 'Individual'
*
    RESET #CT.ORGANIZATION-NAME
      #CT.WEBSITE
  ELSE
    #CT.CONTACT-TYPE-DISP := 'Organization'
  END-IF
*
  IF #ADDR-POOL.COUNTRY-CODE = 'US'
    RESET #ADDR-POOL.INTL-REGION
      #ADDR-POOL.POSTAL-CODE
  ELSE
    RESET #ADDR-POOL.STATE-CODE
      #STATE
      #ADDR-POOL.ZIP-CODE
  END-IF
*
  PERFORM NEW-CONTACT
END-IF
*
PERFORM SCREEN-EDITS
*
PERFORM UPDATE-RECORD
PERFORM WRITE-TRLOG
*
#SCR-CONTACT-ID := #CT.CONTACT-ID
#SCR-MEMBER-CN  := #CT.ID-NUMBER
*
#SCR-CV := (AD=P)
*
END TRANSACTION
*
RESET MBRSHIP-MTD-DET-V
COMPUTE MBRSHIP-MTD-DET-V.ENTRY-DATE = 99999999 - *DATN
MBRSHIP-MTD-DET-V.CODE        := 'N'
MBRSHIP-MTD-DET-V.ID-NUMBER   := #CT.ID-NUMBER
STORE MBRSHIP-MTD-DET-V
*
END TRANSACTION
*
PERFORM SYNCH-STATUS-FILE
*
* DECIDE FOR FIRST CONDITION
*  WHEN #CT.CONTACT-TYPE = 'I' AND #ADDR-POOL.COUNTRY-CODE = 'US'
*    INPUT MARK *#CGA-SCR-COMMAND USING MAP 'C200AMNM'
*  WHEN #CT.CONTACT-TYPE = 'I' AND #ADDR-POOL.COUNTRY-CODE NE 'US'
*    INPUT MARK *#CGA-SCR-COMMAND USING MAP 'C200BMNM'
*  WHEN #CT.CONTACT-TYPE = 'O' AND #ADDR-POOL.COUNTRY-CODE = 'US'
*    INPUT MARK *#CGA-SCR-COMMAND USING MAP 'C200CMNM'
*  WHEN #CT.CONTACT-TYPE = 'O' AND #ADDR-POOL.COUNTRY-CODE NE 'US'
*    INPUT MARK *#CGA-SCR-COMMAND USING MAP 'C200DMNM'
*  WHEN NONE
*    INPUT MARK *#CGA-SCR-COMMAND USING MAP 'C200AMNM'
* END-DECIDE
*
END-SUBROUTINE
*
**********************************************************************************
DEFINE SUBROUTINE SCREEN-EDITS
**********************************************************************************
*
RESET #CT.RANK-ID #CT.RANK-ST
*
IF #CT.CONTACT-TYPE = 'I'
*
  IF #CT.LAST-NAME = ' '
    REINPUT FULL 'Please enter a name for the individual'
      MARK *#CT.LAST-NAME
  END-IF
  IF #CT.FIRST-NAME = ' '
    REINPUT FULL 'Please enter a name for the individual'
      MARK *#CT.FIRST-NAME
  END-IF
  IF NOT #CT.GENDER-CD = 'F' OR = 'M'
    REINPUT FULL 'Gender must be F OR M OR Blank'
*    REINPUT FULL USING HELP
      MARK *#CT.GENDER-CD
  END-IF
*
  IF #CT.SSN = 0
    REINPUT FULL 'Please enter a social security number for the individual'
      MARK *#CT.SSN
  END-IF
*
  IF #CT.DATE-OF-BIRTH = 0
    REINPUT FULL 'Please enter a date of birth for the individual (MMDDYYYY)'
      MARK *#CT.DATE-OF-BIRTH
  END-IF
*
  IF #CT.DATE-OF-BIRTH NE MASK (MMDDYYYY)
    REINPUT FULL 'Please enter a valid date of birth for the individual (MMDDYYYY)'
      MARK *#CT.DATE-OF-BIRTH
  END-IF
*
  IF #YYYY < 1900
    REINPUT FULL 'Please enter a date greater than 12/31/1899'
      MARK *#CT.DATE-OF-BIRTH
  END-IF
*
ELSE
*
  IF #CT.ORGANIZATION-NAME = ' '
    REINPUT FULL 'Please enter a name for the entity'
      MARK *#CT.ORGANIZATION-NAME
  END-IF
*
  IF #CT.SSN = 0
    REINPUT FULL 'Please enter a social security/tax id number for the entity'
      MARK *#CT.SSN
  END-IF
*
  IF #CT.DATE-OF-BIRTH NE 0
      AND #CT.DATE-OF-BIRTH NE MASK (MMDDYYYY)
    REINPUT FULL 'Please enter a valid date of birth for the individual (MMDDYYYY)'
      MARK *#CT.DATE-OF-BIRTH
  END-IF
*
  IF #CT.FIRM-ESTABLISHED-DATE NE 0
      AND #CT.FIRM-ESTABLISHED-DATE NE MASK (MMDDYYYY)
    REINPUT FULL 'Please enter a valid date of firm established (MMDDYYYY)'
      MARK *#CT.FIRM-ESTABLISHED-DATE
  END-IF
*
  IF #CT.GENDER-CD NE ' '
      AND NOT #CT.GENDER-CD = 'F' OR = 'M'
    REINPUT FULL 'Please enter the gender of the individual'
      MARK *#CT.GENDER-CD
  END-IF
END-IF
*
IF #SCR-PREFIX-TITLE NE ' '
  #CT.PREFIX-TITLE   := #SCR-PREFIX-TITLE
  #PREFIX-TITLE-DESC := FUNCTION-PREFIX-DESC(<#CT.PREFIX-TITLE>)
  IF #PREFIX-TITLE-DESC = ' '
    REINPUT FULL USING HELP
      MARK *#SCR-PREFIX-TITLE
  END-IF
  IF #CT.GENDER-CD = 'F' AND #CT.PREFIX-TITLE = 'MR' or = 'Mr'
    REINPUT 'Gender and Prefix mismatch'
      MARK *#CT.PREFIX-TITLE
  ELSE
    IF #CT.GENDER-CD = 'M'
        AND #CT.PREFIX-TITLE = 'MISS' OR = 'MRS' OR = 'MS' OR = 'Miss' OR = 'Mrs' OR = 'Ms'
      REINPUT 'Gender and Prefix mismatch'
        MARK *#CT.PREFIX-TITLE
    END-IF
  END-IF
END-IF
*
IF #CT.MILITARY-SERVICE NE ' ' OR #CT.RANK-ABBR NE ' '
    OR #CT.MILITARY-STATUS NE ' '
*
  IF #CT.MILITARY-SERVICE = ' '
    REINPUT FULL USING HELP
      MARK *#CT.MILITARY-SERVICE
  END-IF
*
  IF #CT.RANK-ABBR = ' '
    REINPUT FULL USING HELP
      MARK *#CT.RANK-ABBR
  END-IF
*
  IF #CT.MILITARY-STATUS = ' '
    REINPUT FULL USING HELP
      MARK *#CT.MILITARY-STATUS
  END-IF
*
  EXPAND ARRAY #ERROR-CODE    TO (*:10)
  EXPAND ARRAY #VALIDATE-FLAG TO (*:20)
  RESET #ERROR-CODE (*)
    #VALIDATE-FLAG (*)
  #MILITARY-INFO.MILITARY-SERVICE := #CT.MILITARY-SERVICE
  #MILITARY-INFO.MILITARY-STATUS  := #CT.MILITARY-STATUS
  #MILITARY-INFO.RANK-ABBR        := #CT.RANK-ABBR
  #OTHERS.#VALIDATE-FLAG (1)      := 'Y'
  #OTHERS.#VALIDATE-FLAG (2)      := 'Y'
  #OTHERS.#VALIDATE-FLAG (18)     := 'Y'
*
  CALLNAT 'GPRNMINF'
    #MILITARY-INFO
    #OTHERS
*
  IF #ERROR-CODE(1) NE ' '
    DECIDE ON FIRST VALUE #OTHERS.#ERROR-CODE (1)
      VALUE '100'
        REINPUT FULL USING HELP
          MARK *#CT.MILITARY-STATUS
      VALUE '101'
        REINPUT FULL USING HELP
          MARK *#CT.MILITARY-SERVICE
      VALUE '145'
        REINPUT FULL USING HELP
          MARK *#CT.RANK-ABBR
      NONE VALUE
        IGNORE
    END-DECIDE
  END-IF
*
  RESET #FOUND
  #MIL-ID := #CT.MILITARY-SERVICE
  COMPRESS #MIL-ID #CT.RANK-ABBR INTO #MIL-ABBR-KEY LEAVING NO
  FIND (1) RR-V WITH RR-V.MIL-ABBR-KEY = #MIL-ABBR-KEY
    #FOUND := TRUE
    #CT.RANK-ID := RR-V.RANK-ID
    #CT.RANK-ST := RR-V.RANK-IN-SAS
  END-FIND
*
  IF NOT #FOUND
    REINPUT FULL USING HELP
      MARK *#CT.RANK-ABBR
  END-IF
END-IF
*
IF #ADDR-POOL.STATE-CODE NE ' '
  PARM-GEN-TABLE.#ACTION  := 'READ'
  PARM-GEN-TABLE.#KEY     := 'STCD'
  PARM-GEN-TABLE.#SUB-KEY := #ADDR-POOL.STATE-CODE
  CALLNAT 'OBJNGENT'
    PARM-GEN-TABLE
  #STATE                 := PARM-GEN-TABLE.GEN-ALPHA-VALUE (1)
ELSE
  RESET #STATE
END-IF
*
#ADDR-REL.ADDR-TYPE-CD   := 'P'
#ADDR-REL.ADDRESS-SOURCE := 'A'
#ADDR-REL.ADDR-LOCATION  := 'H'
*
RESET #ADDRESS-UI.#ZIP
IF #ADDR-POOL.ZIP-5 NE 0
  MOVE EDITED #ADDR-POOL.ZIP-5(EM=999999) TO #ZIP-5
  #ADDRESS-UI.#ZIP       := #ZIP-5
*
  IF #ADDR-POOL.ZIP-4 NE 0
    MOVE EDITED #ADDR-POOL.ZIP-4 (EM=9999) TO #ZIP-4
    COMPRESS #ADDRESS-UI.#ZIP '-' #ZIP-4 INTO #ADDRESS-UI.#ZIP LEAVING NO
  END-IF
END-IF
*
CALLNAT 'GOADN003' #ADDR-POOL #ADDR-REL #CGA-CLERK #MSG
  #ADDRESS-UI
*
IF #MSG NE ' '
  REINPUT FULL #MSG
    MARK #ADDRESS-UI.#POS
END-IF
*
RESET #ADDR-VALID-TXT
DECIDE ON FIRST VALUE OF #ADDR-POOL.ADDR-VALID-IND
  VALUE 'Y'
    #ADDR-VALID-TXT := 'CASS'
  VALUE 'N'
    #ADDR-VALID-TXT := 'No CASS'
  VALUE 'F'
    #ADDR-VALID-TXT := '<Failed CASS>'
  NONE VALUE
    IGNORE
END-DECIDE
*
END-SUBROUTINE
*
**********************************************************************************
DEFINE SUBROUTINE UPDATE-RECORD
**********************************************************************************
*
#PDA-GDATESXX.#REQUEST     := 'D'
#PDA-GDATESXX.#TO-DATE     := #CT.DATE-OF-BIRTH
CALLNAT 'GDATESXX'
  #PDA-GDATESXX
*
**   GET STATUS RECORD NUMBER ***
*
CALLNAT 'GOST0N01'
  #CN-PARMS
*
#CT.ID-NUMBER := #CN-PARMS.CN
*
**   UPDATE CONTACT FILE ***
*
#FORCE-NEW                      := TRUE
RESET #CONTACT.CONTACT-ID
#CONTACT.ID-NUMBER              := #CT.ID-NUMBER
#CONTACT.CONTACT-TYPE           := #CT.CONTACT-TYPE
#CONTACT.RANK-ID                := #CT.RANK-ID
#CONTACT.MILITARY-STATUS        := #CT.MILITARY-STATUS
#CONTACT.PREFIX-TITLE           := #CT.PREFIX-TITLE
#CONTACT.FIRST-NAME             := #CT.FIRST-NAME
#CONTACT.MIDDLE-NAME            := #CT.MIDDLE-NAME
#CONTACT.LAST-NAME              := #CT.LAST-NAME
#CONTACT.SUFFIX                 := #CT.SUFFIX
#CONTACT.GENDER-CD              := #CT.GENDER-CD
#CONTACT.DATE-OF-BIRTH          := #PDA-GDATESXX.#RETURN-CALC
#CONTACT.SSN                    := #CT.SSN
#CONTACT.FIRM-ORGANIZATION-NAME := #CT.ORGANIZATION-NAME
#CONTACT.WEBSITE-URL            := #CT.WEBSITE
*
CALLNAT 'GOCON001' #CONTACT #CGA-CLERK #MSG #UPDATED
*
#CT.CONTACT-ID                  := #CONTACT.CONTACT-ID
IF #SCR-PREFIX-TITLE = ' '
  FIND (1) CONTACT WITH CONTACT-ID = #CONTACT.CONTACT-ID
    CONTACT.PREFIX-TITLE := F-DEFAULT-PREFIX (< #CONTACT.CONTACT-ID >)
    UPDATE
  END-FIND
END-IF
IF #CT.FIRM-ESTABLISHED-DATE NE 0
  #PDA-GDATESXX.#TO-DATE := #CT.FIRM-ESTABLISHED-DATE
  CALLNAT 'GDATESXX'
    #PDA-GDATESXX
  FIND (1) CONTACT WITH CONTACT-ID = #CONTACT.CONTACT-ID
    CONTACT.FIRM-ESTABLISHED-DATE := #PDA-GDATESXX.#RETURN-CALC
    UPDATE
  END-FIND
END-IF
*
**   UPDATE STATUS FILE  ***
*
#STATUS.ID-NUMBER               := #CT.ID-NUMBER
#STATUS.MEMBER-CONTACT-ID       := #CT.CONTACT-ID
#STATUS.MEMBER-FLAG             := 'Y'
#STATUS.INTRO-FLAG              := 'O'
#STATUS.PROCESS-IND             := 'C'
#STATUS.RANK                    := #CT.RANK-ST
#STATUS.MILITARY-SERVICE        := #CT.MILITARY-SERVICE
#STATUS.MILITARY-STATUS         := #CT.MILITARY-STATUS
*
CALLNAT 'GOSTMN01'
  #STATUS
  #CN-PARMS.ERR
  #CN-PARMS.ERR-DESC
  #CN-PARMS.MOD
*
**   UPDATE ADDRESS FILE ***
*
#ADDR-REL.CONTACT-ID              := #CONTACT.CONTACT-ID
CALLNAT 'GOADN001' #ADDR-POOL #ADDR-REL #CGA-CLERK #MSG #UPDATED
*
**   UPDATE EMAIL FILE ***
*
IF #EMAIL.EMAIL-ADDRESS NE ' '
  RESET #EMAIL.EMAIL-ID
  #EMAIL.CONTACT-ID               := #CONTACT.CONTACT-ID
  #EMAIL.EMAIL-TYPE               := 'P'
  #EMAIL.EMAIL-PREFERRED-IND      := 'Y'
*
  CALLNAT 'GOEMN001' #EMAIL #CGA-CLERK #MSG #UPDATED
END-IF
*
**   UPDATE PHONE FILE ***
*
IF #H_PH1 NE 0 OR #H_PH2 NE 0 OR #H_PH3 NE 0 OR #H_PH4 NE 0 OR #H_PH_ID NE 0
  RESET #PHS #PHONE
  MOVE EDITED #H_PH1 (EM=999)  TO #PH1
  MOVE EDITED #H_PH2 (EM=999)  TO #PH2
  MOVE EDITED #H_PH3 (EM=9999) TO #PH3
  IF #H_PH4 > 0
    MOVE EDITED #H_PH4 (EM=999999) TO #PH4
  END-IF
  #PHONE.CONTACT-ID              := #CONTACT.CONTACT-ID
  #PHONE.PHONE-TYPE-CODE         := 'H'
  #PHONE.INTL-DIAL-NUMBER-A      := #PHS
  MOVE RIGHT JUSTIFIED #PHONE.INTL-DIAL-NUMBER-A TO #PHONE.INTL-DIAL-NUMBER-A
  #PHONE.PREFERRED-PHONE-IND     := 'Y'
  CALLNAT 'GOPHN001' #PHONE #CGA-CLERK #MSG #UPDATED
  #TR-HOME-PH := #PHS
END-IF
*
IF #W_PH1 NE 0 OR #W_PH2 NE 0 OR #W_PH3 NE 0 OR #W_PH4 NE 0 OR #W_PH_ID NE 0
  RESET #PHS #PHONE
  MOVE EDITED #W_PH1 (EM=999)  TO #PH1
  MOVE EDITED #W_PH2 (EM=999)  TO #PH2
  MOVE EDITED #W_PH3 (EM=9999) TO #PH3
  IF #W_PH4 > 0
    MOVE EDITED #W_PH4 (EM=999999) TO #PH4
  END-IF
  #PHONE.CONTACT-ID              := #CONTACT.CONTACT-ID
  #PHONE.PHONE-TYPE-CODE         := 'B'
  #PHONE.INTL-DIAL-NUMBER-A      := #PHS
  MOVE RIGHT JUSTIFIED #PHONE.INTL-DIAL-NUMBER-A TO #PHONE.INTL-DIAL-NUMBER-A
  #PHONE.PREFERRED-PHONE-IND     := 'Y'
  CALLNAT 'GOPHN001' #PHONE #CGA-CLERK #MSG #UPDATED
  #TR-WORK-PH := #PHS
END-IF
*
IF #C_PH1 NE 0 OR #C_PH2 NE 0 OR #C_PH3 NE 0 OR #C_PH_ID NE 0
  RESET #PHS #PHONE
  MOVE EDITED #C_PH1 (EM=999)  TO #PH1
  MOVE EDITED #C_PH2 (EM=999)  TO #PH2
  MOVE EDITED #C_PH3 (EM=9999) TO #PH3
  #PHONE.CONTACT-ID              := #CONTACT.CONTACT-ID
  #PHONE.PHONE-TYPE-CODE         := 'C'
  #PHONE.INTL-DIAL-NUMBER-A      := #PHS
  MOVE RIGHT JUSTIFIED #PHONE.INTL-DIAL-NUMBER-A TO #PHONE.INTL-DIAL-NUMBER-A
  #PHONE.PREFERRED-PHONE-IND     := 'Y'
  CALLNAT 'GOPHN001' #PHONE #CGA-CLERK #MSG #UPDATED
  #TR-CELL-PH := #PHS
END-IF
*
END-SUBROUTINE
*
**********************************************************************************
DEFINE SUBROUTINE WRITE-TRLOG
**********************************************************************************
*
#TR-NAME                := FUNCTION-DISP-CT-NAME
  (<#CONTACT.FIRST-NAME,#CONTACT.MIDDLE-NAME,#CONTACT.LAST-NAME,#CONTACT.SUFFIX>)
#TR-ID-NUMBER           := #CT.ID-NUMBER
#LOG-DESCRIPTION        := 'NM-CON-NM'
#TR-CLERK-ID            := 'EDS'
#TR-DATE-LAST-UPDATE    := #DATE
*
#FIELD-DESCRIPTION (1)  := 'CONTACT'
#FIELD-DESCRIPTION (2)  := 'ADDRESS 1'
#FIELD-DESCRIPTION (3)  := 'ADDRESS 2'
#FIELD-DESCRIPTION (4)  := 'ADDRESS 3'
#FIELD-DESCRIPTION (5)  := 'ZIP CODE'
#FIELD-DESCRIPTION (6)  := 'HOME PHONE'
#FIELD-DESCRIPTION (7)  := 'WORK PHONE'
#FIELD-DESCRIPTION (8)  := 'CELL PHONE'
#FIELD-DESCRIPTION (9)  := 'EMAIL'
*
#SYNC-FIELD-TABLE  (2)  := 2
#SYNC-FIELD-TABLE  (3)  := 3
#SYNC-FIELD-TABLE  (4)  := 4
#SYNC-FIELD-TABLE  (5)  := 5
*
RESET #OLD-DATA (*)
*
IF #CT.CONTACT-TYPE = 'O'
  #NEW-DATA (1)         := #CONTACT.FIRM-ORGANIZATION-NAME
ELSE
  COMPRESS #TR-NAME #CONTACT.GENDER-CD #CT.MILITARY-SERVICE #CT.RANK-ABBR #CONTACT.MILITARY-STATUS
    #CONTACT.PREFIX-TITLE INTO #NEW-DATA (1)
END-IF
*
#ADD-CONTACT-ID         := #CT.CONTACT-ID
#ADD-TYPE               := 'P'
CALLNAT 'ADDN0001'
  #PDA-ADDRESS
*
#NEW-DATA(2)            := #ADD-LINE-1
#NEW-DATA(3)            := #ADD-LINE-2-NO-ZIP
#NEW-DATA(4)            := #ADD-LINE-3-NO-ZIP
#NEW-DATA(5)            := #ADDR-POOL.ZIP-CODE
#NEW-DATA(6)            := #TR-HOME-PH
#NEW-DATA(7)            := #TR-WORK-PH
#NEW-DATA(8)            := #TR-CELL-PH
#NEW-DATA(9)            := #EMAIL.EMAIL-ADDRESS
*
CALLNAT 'GPRN0004'
  #TR-FIELDS #SYNC-FIELD-TABLE(*)
  #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
*
END-SUBROUTINE
*
**********************************************************************************
DEFINE SUBROUTINE SYNCH-STATUS-FILE
**********************************************************************************
*
#CN-PARMS.CN            := #CT.ID-NUMBER
* CALLNAT 'GOSTAN01'                        /* Synch addresses
*   #CN-PARMS
* END TRANSACTION
*
CALLNAT 'GOSTCN01'                        /* Synch contact information
  #CN-PARMS
END TRANSACTION
*
* CALLNAT 'GOSTPN01'                        /* Synch phone
*   #CN-PARMS
* END TRANSACTION
* *
* IF #EMAIL.EMAIL-ADDRESS NE ' '
*   CALLNAT 'GOSTEN01'                      /* Synch email
*     #CN-PARMS
*   END TRANSACTION
* END-IF
*
END-SUBROUTINE
*
END
