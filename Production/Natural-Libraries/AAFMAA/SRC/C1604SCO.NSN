* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C1604SCO - Co Mr Processing
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    12162004  PM1     Adjusted premiumn display to 5 digits.      *
* DRW    06262006  DW2     Use Dues Pmt on letter
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-ID           (N6)
1 #CGA-SCR-ID       (N9)
1 #CGA-SCR-SUFFIX   (A3)
1 #SCR-SUFFIX       (A3)
1 #CGA-CLERK        (A3)
1 #FIRST-LAST       (A25)
1 #TODAY-YYYYMMDD-N (N8)
1 #COMP-1035        (A50)
1 #FIND-CO-TYPE     (L)
1 #TEXT     (A78)
1 #SCR-TYPES    (16)   /* up to 16 types on conversions
 2 #SCR-SELECT-CV         (C)
 2 #SCR-SELECT            (A1)
 2 #SCR-TO-PLAN           (A6)
 2 #SCR-NU-DB-DISP        (A10)
* 2 #SCR-NU-PREM-DISP      (A7)
  2 #SCR-NU-PREM-DISP      (A8) /* PM1
 2 #SCR-AMT-LUMP-SUM-DISP (A11)
 2 #SCR-TAXABLE-INC-DISP  (A10)
 2 #SCR-REMARKS           (A30)
*
1 #SCR-CT-REMARKS-TBL (9)
 2 #SCR-CT-REMARKS    (A30/16)
*
1 #CT-TBL-POLS       (9) /*9 possible certs (axx must match below)
 2 #CT-POL-NUM        (A14)
 2 #CT-ISS-DT         (N8)
 2 #CT-CONV-DUR       (N3)
 2 #CT-ISSUE-DATE     (A10)
 2 #CT-CURR-PLAN      (A6)
 2 #CT-FREQ           (A11)
 2 #CT-CURR-FACE      (N7.2)
 2 #CT-CURR-CSV       (N7.2)
 2 #CT-CURR-DB        (N9.2)
 2 #CT-PRORAT-LN-INT  (N5.2)
*
 2 #CONV-TBL-TYPES (16)   /* 16 possible conversion types
  3 #CT-SELECT            (A1)
  3 #CT-TO-PLAN           (A6)
  3 REDEFINE #CT-TO-PLAN
   4 #CT-TO-PLAN-$        (A1)
   4 #CT-TO-PLAN-X        (N1)
   4 #CT-TO-PLAN-4        (A4)
  3 #CT-TERM-FACE         (N7.2)
  3 #CT-NU-PREM           (N5.2)
  3 #CT-NU-DB             (N9.2)
  3 #CT-AMT-LUMP-SUM      (N9.2)
  3 #CT-PREMS-PAID        (N9.2)
  3 #CT-TAXABLE-INC       (N9.2)
  3 #CT-TAMRA-IND         (A1)
  3 #CT-RPU-LOAN-IND      (A1)
  3 #CT-BYPASS            (L)
  3 #CT-INS-REQ           (L)    /* DW99
  3 #CT-DUES-PMT          (N3.2) /*  DW2
*
1 #CO-MR-TYPE      (A2)
1 REDEFINE #CO-MR-TYPE
 2 #CO-MR-TYPE-1  (A1)
 2 #CO-MR-TYPE-2  (A1)
*
LOCAL
*
1 MR-V VIEW OF A-REQUEST
 2 ID-NUMBER        (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A    (A6)
 2 MEMBER-NAME      (A20)
 2 CLERK-ID         (A3)
 2 ZIP-CODE         (N5)
 2 REDEFINE ZIP-CODE
  3 FIR-IND         (N1)
  3 MSG-IND         (N1)
  3 RET-IND         (N1)
  3 FIN-IND         (N1)
  3 FAS-IND         (N1)
 2 RQST-TABLE       (A10)   /* add to g3601pmr also
 2 REDEFINE RQST-TABLE
  3 BEN-IND         (A1)
  3 MAL-IND         (A1)   /* mbr accts letter
  3 BEN-SHO-IND     (A1)
  3 ACC-IND         (A1)
  3 ALI-IND         (A1)
  3 APP-IND         (A1)
  3 PIL-IND         (A1)
  3 CO-IND          (A1)
 2 REQUEST-DATE     (N4)
 2 REDEFINE REQUEST-DATE
  3 REQUEST-DATE-A  (A4)
 2 ASSUM-RET-DATE   (N8)
 2 INFO          (A50)
 2 CO-RQSTS      (A3/10)
 2 REDEFINE CO-RQSTS
  3 CO-RQSTS-OCC  (10)
   4 CO-CERT-SEQ  (N1)
   4 CO-CONV-TYPE (A2)
   4 REDEFINE CO-CONV-TYPE
    5 CO-CONV-TYPE-1 (A1)
    5 CO-CONV-TYPE-2 (A1)
*
1 #I1       (I2)
1 #P-X      (I2)
1 #T-X      (I2)
*
1 #WYYYYMMDD
 2 #WCENT    (N2)
 2 #WYY      (N2)
 2 #WMM      (N2)
 2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
 2 #WYYYYMMDD-N (N8)
1 REDEFINE #WYYYYMMDD
 2 #WYYYY-N   (N4)
 2 #WMMDD-N   (N4)
1 REDEFINE #WYYYYMMDD
 2 #WCENT-A   (A2)
 2 #WYY-A     (A2)
 2 #WMM-A     (A2)
 2 #WDD-A     (A2)
*
END-DEFINE
* * * * * * * * *
*
FIND (1) MR-V WITH ID-NUMBER = #CGA-ID
  IF NO RECORD FOUND
    PERFORM BUILD-MR-REC
    STORE MR-V
    ESCAPE BOTTOM
  END-NOREC
*
  PERFORM BUILD-MR-REC
  UPDATE (0140)
END-FIND
*
END TRANSACTION
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-MR-REC
* * * * * * * * * * * * * * * * * *
*
MOVE 'They have been "MR"d' TO #TEXT
MOVE #CGA-SCR-ID   TO MR-V.ID-NUMBER
MOVE #FIRST-LAST   TO MR-V.MEMBER-NAME
MOVE #CGA-CLERK    TO MR-V.CLERK-ID
MOVE #TODAY-YYYYMMDD-N TO #WYYYYMMDD-N
COMPRESS #WMM-A #WDD-A INTO MR-V.REQUEST-DATE-A LEAVING NO
MOVE '1'           TO CO-IND
MOVE #COMP-1035    TO INFO
*
RESET #I1 CO-RQSTS (*)
*
FOR #P-X 1 TO 9
  FOR #T-X 1 TO 16
    IF #CT-SELECT (#P-X, #T-X) NE ' '
      ADD 1 TO #I1
*
      IF #I1 GT 10
        MOVE 'Only 1st 10 "MR" requests were processed' TO #TEXT
        ESCAPE BOTTOM
      END-IF
*
      MOVE #P-X TO CO-CERT-SEQ (#I1)
      MOVE TRUE TO #FIND-CO-TYPE
      PERFORM STORE-FOR-SCREEN
      RESET #FIND-CO-TYPE
      MOVE #CO-MR-TYPE TO CO-CONV-TYPE (#I1)
*
      IF   (#CT-SELECT (#P-X, #T-X) = '2')
       AND ((#CT-TO-PLAN (#P-X, #T-X) = 'CSV')
        OR  (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$')))
        MOVE 'E' TO CO-CONV-TYPE-1 (#I1)
      END-IF
*
      IF   (#CT-SELECT (#P-X, #T-X) = '2')
       AND ((#CT-TO-PLAN (#P-X, #T-X) = 'RSGN')
        OR  (#CT-TO-PLAN (#P-X, #T-X) = MASK ('R$')))
        MOVE 'S' TO CO-CONV-TYPE-1 (#I1)
      END-IF
    END-IF
  END-FOR
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE STORE-FOR-SCREEN
* * * * * * * * * * * * * * * * *
*
CALLNAT 'C1603SCO'
  #FIND-CO-TYPE
  CO-RQSTS (*)
  #CGA-SCR-SUFFIX
  #SCR-SUFFIX
  #SCR-TYPES    (*)
  #CT-TBL-POLS  (*)
  #SCR-CT-REMARKS-TBL (*)
  #CO-MR-TYPE
  #P-X
  #T-X
*
END-SUBROUTINE
*
END
