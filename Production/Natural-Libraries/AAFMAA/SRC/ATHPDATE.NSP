* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDATE - READ AND FORMAT DATE RANGES FOR BOTH ANNUAL
*                        MEMBER AND WIDOW FIRS
*
DEFINE DATA
LOCAL
*
1 TE-V VIEW OF A-TERMINALS
 2 TERMINAL-ID                    (A8)
 2 CLERK-ID                       (A3)
 2 SECURITY-CODE                  (N2)
 2 COMMENTS                       (A20)
 2 REDEFINE COMMENTS
  3 #FROM-DATE                    (A4)
  3 #FILLER                       (A4)
  3 #THRU-DATE                    (A4)
  3 #FILLER-2                     (A1)
  3 #YR-                          (A3)
  3 #YEAR                         (N4)
 2 LST-CMND-DT                    (N8)
*
1 #WORK-DATE                     (N8)
1 REDEFINE #WORK-DATE
 2 #WORK-YYYY                    (N4)
 2 #WORK-MM                      (N2)
 2 #WORK-DD                      (N2)
*
1 #WORK-THRU-MMDD                (N4)
1 REDEFINE #WORK-THRU-MMDD
 2 #WORK-THRU-MM                 (N2)
 2 #WORK-THRU-DD                 (N2)
*
1 #WORK-FROM-MMDD                (N4)
1 REDEFINE #WORK-FROM-MMDD
 2 #WORK-FROM-MM                 (N2)
 2 #WORK-FROM-DD                 (N2)
1 REDEFINE #WORK-FROM-MMDD
 2 #WORK-FROM-MMDD-A             (A4)
*
1 #VARIABLES
 2 #ANNFIR                       (A6)  INIT <'ANNFIR'>
 2 #PART-1                       (A24) INIT <'ANNUAL FIRS PRODUCED FOR'>
 2 #PART-2                       (A02) INIT <'TO'>
*
1 #REPORT-SENTENCE               (A50)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
MOVE *DATN       TO #WORK-DATE
*
FIND (1) TE-V WITH TERMINAL-ID = #ANNFIR
*
  MOVE *DATN       TO LST-CMND-DT
  MOVE #THRU-DATE  TO #WORK-FROM-MMDD-A
*
  ADD +1 TO #WORK-FROM-DD
  IF #WORK-FROM-MM = 1 OR = 3 OR = 5 OR = 7 OR = 8 OR = 10 OR = 12
    IF #WORK-FROM-DD GT 31
      SUBTRACT 31 FROM #WORK-FROM-DD
      ADD +1 TO #WORK-FROM-MM
      IF #WORK-FROM-MM GT 12
       SUBTRACT 12 FROM #WORK-FROM-MM
      END-IF
    END-IF
  ELSE
    IF #WORK-FROM-MM = 2
      IF #WORK-FROM-DD GT 28
        SUBTRACT 28 FROM #WORK-FROM-DD
        ADD +1 TO #WORK-FROM-MM
         IF #WORK-FROM-MM GT 12
           SUBTRACT 12 FROM #WORK-FROM-MM
         END-IF
      END-IF
    ELSE
      IF #WORK-FROM-DD GT 30
        SUBTRACT 30 FROM #WORK-FROM-DD
        ADD +1 TO #WORK-FROM-MM
         IF #WORK-FROM-MM GT 12
           SUBTRACT 12 FROM #WORK-FROM-MM
         END-IF
      END-IF
    END-IF
  END-IF
*
  IF #WORK-FROM-MMDD = 0101
    ADD +1 TO #YEAR
  END-IF
*
  IF #WORK-YYYY NE #YEAR
    MOVE 12 TO #WORK-THRU-MM
    MOVE 31 TO #WORK-THRU-DD
  ELSE
    MOVE #WORK-MM TO #WORK-THRU-MM
    MOVE #WORK-DD TO #WORK-THRU-DD
  END-IF
*
  MOVE EDITED #WORK-FROM-MMDD (EM=9999) TO #FROM-DATE
  MOVE EDITED #WORK-THRU-MMDD (EM=9999) TO #THRU-DATE
*
  COMPRESS #PART-1 #WORK-FROM-MMDD #PART-2 #WORK-THRU-MMDD
           INTO #REPORT-SENTENCE
  WRITE #REPORT-SENTENCE
*
  UPDATE
  END TRANSACTION
*
END-FIND
*
END
