* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: C1200PAC
* THIS PROGRAM WILL ALLOW AAFMAA TO CHOSE A DETAIL LISTING FOR GROUPS
* OF CODES EITHER ON MONTH-TO-DATE OR YEAR-TO-DATE TIME-SPAN
********************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11242005  RH1   SS - Replaced call to assembler program WAASSRT1 with
*                      Natural subprogram SAGSRT1.
********************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX        /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING CSRCGLXX        /* WORK FIELDS FOR GROUP CODES
LOCAL USING C1200LAC        /* WORK FIELDS FOR THIS PROGRAM
LOCAL
1 #SCN-ACTION-T    (A1) INIT <'X'>
1 #SCN-ACTION-C    (A1)
1 #SCN-ACTION-M    (A1)
1 #SCN-ACTION-Q    (A1)
1 #SCN-ACTION-PYNA (A1)
1 #SCN-ACTION-I    (A1)
1 #SCN-ACTION-O    (A1)
1 #SCN-ACTION-F    (A1)
1 #SCN-ACTION-Z    (A1)
1 #SCN-ACTION-V    (A1)
1 #SCN-ACTION-W    (A1)
1 #SCN-MONTH-LIT   (A20)
1 #SCN-ACTION-DATE (A6)
1 REDEFINE #SCN-ACTION-DATE
  2 #SAD-YYYY      (A4)
  2 REDEFINE #SAD-YYYY
   3 #SAD-YYYY-N   (N4)
  2 REDEFINE #SAD-YYYY
   3 #SAD-CENT     (N2)
   3 #SAD-YY       (N2)
  2 #SAD-MM        (N2)
*
1 #MON-TBL (A36) INIT <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MON-TBL
 2 #MON (A3/12)
*
1 #SRT-TABLE       (150)
 2 #SRT-YYYY                 (N4)
 2 #SRT-MM                   (N2)
 2 #SRT-SOURCE-CODE          (A6)
*
1 #SRT-PARM
 2 #SRT-NUM-ENTRIES  (I2) INIT <00>
 2 #SRT-ENTRY-LENGTH (I2) INIT <12>
 2 #SRT-KEY-LENGTH   (I2) INIT <6>
 2 #SRT-KEY-POSITION (I2) INIT <0>
 2 #SRT-SORT-TYPE    (A1) INIT <'D'>
*
1 #I1              (I2)
1 #I2              (I2)
1 #REAL-TRIP-NUM   (N3)
1 #START-TRIP-KEY  (A6)
1 REDEFINE #START-TRIP-KEY
 2 #START-TRIP-KEY-YY   (N2)
 2 REDEFINE #START-TRIP-KEY-YY
  3 #START-TRIP-KEY-YY-A (A2)
 2 #START-TRIP-KEY-NUM  (N3)
 2 #START-TRIP-KEY-CODE (A1)
*
1 #SCN-TITLE     (A70)
1 #SCN-DETAIL    (A78/1:21)
1 #SD-CV         (C/1:21)
1 #S-INDEX       (I2)
1 #START-GROUP   (A1)
1 #HOLD-NUMBER   (A3)
1 #SCN-DETAIL-LINE  (A78)
1 REDEFINE #SCN-DETAIL-LINE
  2 #TRIP-DETAIL
    3 #TRIP-NUMBER (A5)
    3 #TRIPB       (A1)
    3 #TRIP-YEAR   (N4)
    3 #TRIPC       (A1)
    3 #TRIP-DESC   (A40)
    3 #TRIPD       (A1)
    3 #TRIP-NEW    (A6)
    3 #TRIPE       (A3)
    3 #TRIP-ADDL   (A6)
    3 #TRIPF       (A3)
    3 #TRIP-INQ    (A6)
1 REDEFINE #SCN-DETAIL-LINE
  2 #CONV-DETAIL
    3 #CONV-DESC   (A40)
    3 REDEFINE #CONV-DESC
     4 #CONV-DESC-FST-29    (A29)
     4 #CONV-DESC-AAAAA     (A5)
     4 #CONV-DESC-LST-6     (A6)
     4 REDEFINE #CONV-DESC-LST-6
      5 #CONV-DESC-MON      (A3)
      5 #CONV-DESC-FILL     (A1)
      5 #CONV-DESC-YY       (N2)
    3 #CONVA       (A1)
    3 #CONV-DATE   (A10)
    3 #CONVB       (A1)
    3 #CONV-NEW    (A6)
    3 #CONVC       (A3)
    3 #CONV-ADDL   (A6)
    3 #CONVD       (A4)
    3 #CONV-INQ    (A6)
1 REDEFINE #SCN-DETAIL-LINE
  2 #SPER-DETAIL
    3 #SPERA       (A6)
    3 #SPER-DESC   (A40)
    3 #SPERB       (A2)
    3 #SPER-INQ    (A6)
    3 SPERC        (A4)
    3 #SPER-NEW    (A6)
    3 #SPERD       (A4)
    3 #SPER-ADDL   (A6)
1 REDEFINE #SCN-DETAIL-LINE
  2 #SPER-DETAIL2
    3 #SPER-DESC2  (A46)
1 REDEFINE #SCN-DETAIL-LINE
  2 #SPER-DETAIL3
    3 #SPER        (A10)
    3 #SPER-DESC3  (A36)
1 #TRIP-TITLE1-LIT
  2 #TT1A          (A27) INIT<'**TRIP**   OFFICER-LOCATION'>
  2 #TT1B          (A24)
  2 #TT1C          (A27) INIT<'APPROVED APPROVED INQUIRIES'>
1 REDEFINE #TRIP-TITLE1-LIT
  2 #TRIP-TITLE1   (A78)
1 #TRIP-TITLE2-LIT
  2 #TT2A          (A10) INIT<'NMBR  YEAR'>
  2 #TT2B          (A44)
  2 #TT2C          (A24) INIT  <'NEW      ADDL   RECEIVED'>
1 REDEFINE #TRIP-TITLE2-LIT
  2 #TRIP-TITLE2  (A78)
*
1 #CONV-TITLE1-LIT
  2 #CT1A          (A10) INIT<'CONVENTION'>
  2 #CT1B          (A33)
  2 #CT1C          (A35) INIT<'        APPROVED APPROVED INQUIRIES'>
1 REDEFINE #CONV-TITLE1-LIT
  2 #CONV-TITLE1   (A78)
1 #CONV-TITLE2-LIT
  2 #CT2B          (A53)
  2 #CT2C          (A25) INIT<'NEW      ADDL    RECEIVED'>
1 REDEFINE #CONV-TITLE2-LIT
  2 #CONV-TITLE2  (A78)
*
1 #OTH-TITLE1-LIT
  2 #OT1A          (A13) INIT<'MISCELLANEOUS'>
  2 #OT1B          (A30)
  2 #OT1C          (A35) INIT<'        APPROVED APPROVED INQUIRIES'>
1 REDEFINE #OTH-TITLE1-LIT
  2 #OTH-TITLE1   (A78)
1 #OTH-TITLE2-LIT
  2 #OT2B          (A53)
  2 #OT2C          (A25) INIT<'NEW      ADDL    RECEIVED'>
1 REDEFINE #OTH-TITLE2-LIT
  2 #OTH-TITLE2  (A78)
*
1 #CAP-TITLE1-LIT
  2 #CP1A          (A13) INIT<'C. A. P.     '>
  2 #CP1B          (A30)
  2 #CP1C          (A35) INIT<'        APPROVED APPROVED INQUIRIES'>
1 REDEFINE #CAP-TITLE1-LIT
  2 #CAP-TITLE1   (A78)
1 #CAP-TITLE2-LIT
  2 #CP2B          (A53)
  2 #CP2C          (A25) INIT<'NEW      ADDL    RECEIVED'>
1 REDEFINE #CAP-TITLE2-LIT
  2 #CAP-TITLE2  (A78)
*
1 #CADT-TITLE1-LIT
  2 #IT1A          (A17) INIT<'CONVERSION SOURCE'>
  2 #IT1B          (A30)
  2 #IT1C          (A21) INIT<'APPLICATIONS APPROVED'>
1 REDEFINE #CADT-TITLE1-LIT
  2 #CADT-TITLE1   (A68)
1 #MAIL-TITLE1-LIT
  2 #MT1A          (A10) INIT<'MAILING   '>
* 2 #MT1B          (A33) INIT<'                         DATE    '>
  2 #MT1B          (A33) INIT<'                                 '>
  2 #MT1C          (A35) INIT<'LETTERS  APPROVED APPROVED INQUIRY '>
1 REDEFINE #MAIL-TITLE1-LIT
  2 #MAIL-TITLE1   (A78)
1 #MAIL-TITLE2-LIT
  2 #MT2B          (A42)
  2 #MT2C          (A12) INIT <'  SENT      '>
  2 #MT2D          (A23) INIT<'NEW      ADDL   RECEIVD'>
1 REDEFINE #MAIL-TITLE2-LIT
  2 #MAIL-TITLE2  (A77)
1 #SPER-TITLE1-LIT
  2 #PT1A          (A47)
  2 #PT1B          (A29) INIT<'INQUIRIES  APPROVED  APPROVED'>
1 REDEFINE #SPER-TITLE1-LIT
  2 #SPER-TITLE1   (A76)
1 #SPER-TITLE2-LIT
  2 #PT2B          (A47)
  2 #PT2C          (A29) INIT<'RECEIVED     NEW       ADDL  '>
1 REDEFINE #SPER-TITLE2-LIT
  2 #SPER-TITLE2  (A76)
1 #SPER-TITLE3    (A19) INIT<'  I. PERIODICALS   '>
1 #SPER-TITLE4    (A20) INIT<' II. BASE NEWSPAPERS'>
*
1 #DATN           (N8)
1 REDEFINE #DATN
 2 #DATN-CENT     (N2)
 2 #DATN-YY       (N2)
 2 #DATN-MM       (N2)
 2 #DATN-DD       (N2)
*
1 #HOLD-TODAY-DATE  (N8)
1 REDEFINE #HOLD-TODAY-DATE
2 #YYYY     (A4)
2 REDEFINE #YYYY
 3 #YYYY-N   (N4)
2 REDEFINE #YYYY
 3 #CENT     (N2)
 3 #YY       (N2)
2 #MM       (N2)
2 #DD       (N2)
1 REDEFINE #HOLD-TODAY-DATE
2 #YYYYMM   (A6)
1 #MONTH-LITERAL  (A108) INIT<'JANUARY  FEBRUARY MARCH    APRIL    '
   -'MAY      JUNE     JULY     AUGUST   SEPTEMBEROCTOBER  NOVEMBER '
   -'DECEMBER '>
1 REDEFINE #MONTH-LITERAL
2 #MONTH-ENTRY  (A9/1:12)
1 #INDEX    (I2)
1 #AP       (I2)
1 #CUR-YEAR (L)
1 #MTD-FLAG (L)
1 #G-NEW-SUM   (P5)
1 #G-ADDL-SUM  (P5)
1 #G-INQ-SUM   (P5)
1 #S-NEW-SUM   (P5)
1 #S-ADDL-SUM  (P5)
1 #S-INQ-SUM   (P5)
1 #SUM-NEW     (P5)
1 #SUM-ADDL    (P5)
1 #SUM-INQ     (P5)
1 #TOT-7       (P7)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX             /* NATURAL ERROR ROUTINE
*
IF #CGA-USER-COMPANY = 'S'
  FETCH 'S1200PAC'
END-IF
*
RESET #CGA-SCR-SUFFIX
RESET #CGA-SCR-ID
MOVE '*' TO #CGA-SCR-COMMAND
MOVE *DATN TO #HOLD-TODAY-DATE
              #DATN
MOVE #MM TO #INDEX
COMPRESS #MONTH-ENTRY(#INDEX) #DD INTO #SCN-MONTH-LIT
COMPRESS #SCN-MONTH-LIT ',' #YYYY INTO #SCN-MONTH-LIT LEAVING NO SPACE
MOVE #YYYYMM TO #SCN-ACTION-DATE
IF *DEVICE = 'BATCH'
   MOVE FALSE TO #MTD-FLAG
   MOVE TRUE TO #CUR-YEAR
   PERFORM PRINT-PERIODICALS
   WRITE NOTITLE USING FORM 'C1201FAC'
   RESET #SCN-DETAIL(*)
   PERFORM PRINT-TRIPS
   WRITE NOTITLE USING FORM 'C1201FAC'
   RESET #SCN-DETAIL(*)
   PERFORM PRINT-CONVENTIONS
   WRITE NOTITLE USING FORM 'C1201FAC'
   RESET #SCN-DETAIL(*)
   PERFORM PRINT-DIRECT-MAIL-CAMPAIGNS
   WRITE NOTITLE USING FORM 'C1201FAC'
   RESET #SCN-DETAIL(*)
   PERFORM PRINT-CADET-CONVERSIONS
   WRITE NOTITLE USING FORM 'C1201FAC'
ELSE
  MOVE 05 TO *CURS-LINE
*
  IF #MM GT 9
    MOVE 50 TO *CURS-COL
  ELSE
    MOVE 51 TO *CURS-COL
  END-IF
*
  INPUT MARK *#SCN-ACTION-DATE USING MAP 'C1200MAC'
  IF #CGA-SCR-COMMAND = '*'
    IGNORE
  ELSE
    FETCH 'G1000PXX'
  END-IF
*
  IF  (#SAD-YYYY-N = #YYYY-N OR = #YYYY-N -1)
    IGNORE
  ELSE
    REINPUT 'CURRENT AND PRIOR YEAR ONLY IS AVAILABLE'
    MARK *#SCN-ACTION-DATE
  END-IF
*
  IF #SAD-MM = 0
    MOVE FALSE TO #MTD-FLAG
  ELSE
    IF #SAD-MM < 13
      MOVE TRUE TO #MTD-FLAG
      MOVE #SAD-MM TO #INDEX
    ELSE
      REINPUT 'INVALID MONTH SELECTION'
      MARK *#SCN-ACTION-DATE
    END-IF
  END-IF
  IF #SAD-YYYY = #YYYY
     MOVE TRUE TO #CUR-YEAR
  ELSE
     MOVE #SAD-YYYY TO #YYYY
     MOVE FALSE TO #CUR-YEAR
  END-IF
*
 DECIDE FOR FIRST CONDITION
  WHEN #SCN-ACTION-T NE ' '    PERFORM PRINT-TRIPS
  WHEN #SCN-ACTION-C NE ' '    PERFORM PRINT-CONVENTIONS
  WHEN #SCN-ACTION-M NE ' '    PERFORM PRINT-DIRECT-MAIL-CAMPAIGNS
  WHEN #SCN-ACTION-Q NE ' '    PERFORM PRINT-DIRECT-MAIL-INQUIRIES
  WHEN #SCN-ACTION-PYNA NE ' ' PERFORM PRINT-PERIODICALS
  WHEN #SCN-ACTION-I NE ' '    PERFORM PRINT-CADET-CONVERSIONS
  WHEN #SCN-ACTION-O NE ' '    PERFORM PRINT-OTHERS
  WHEN #SCN-ACTION-F NE ' '    PERFORM PRINT-FIRS-MR-APPS
  WHEN #SCN-ACTION-Z NE ' '    PERFORM PRINT-CAP
  WHEN #SCN-ACTION-V NE ' '    PERFORM PRINT-VISIT-FORT-MYER
  WHEN #SCN-ACTION-W NE ' '    PERFORM PRINT-PENTAGON
  WHEN NONE
   REINPUT WITH 'CHOOSE A DETAIL REPT BY PLACING AN X IN FRONT OF IT'
   MARK *#SCN-ACTION-T
 END-DECIDE
*
 MOVE 'AC' TO #CGA-SCR-COMMAND
 INPUT USING MAP 'C1201MAC'
 FETCH 'G1000PXX'
END-IF
* * * * * * * * * * ** * * * * * * * * * ** * * * * * * * * * *
DEFINE SUBROUTINE PRINT-TRIPS
*
IF #MTD-FLAG
  COMPRESS 'TRIP SOURCE OF APPLICATION DETAIL FOR'
     #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'TRIP SOURCE OF APPLICATION DETAIL FOR ALL OF'
       #YYYY INTO #SCN-TITLE
END-IF
*
ASSIGN #S-INDEX = 3
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #TRIP-TITLE1 TO #SCN-DETAIL(1)
MOVE #TRIP-TITLE2 TO #SCN-DETAIL(2)
*
RESET #G-NEW-SUM
      #G-ADDL-SUM
      #G-INQ-SUM
*
IF #CUR-YEAR
  MOVE '000000' TO #START-TRIP-KEY
ELSE
  MOVE 'T' TO #START-TRIP-KEY-CODE
  MOVE 000 TO #START-TRIP-KEY-NUM
* COMPUTE #START-TRIP-KEY-YY = 99 - (#DATN-YY -1)  /*CHANGED Y2K
  IF #DATN-YY GE 50
    COMPUTE #START-TRIP-KEY-YY = 149 - (#DATN-YY - 1)
  ELSE
    COMPUTE #START-TRIP-KEY-YY = 49 - (#DATN-YY - 1)
  END-IF
  MOVE EDITED #START-TRIP-KEY-YY (EM=99) TO #START-TRIP-KEY-YY-A
END-IF
*
READ SOURCE-APPL-V BY TRIP-KEY FROM #START-TRIP-KEY
  IF SOURCE-CODE-GROUP NE 'T'
    ESCAPE TOP
  END-IF
*
  RESET #SUM-NEW
        #SUM-INQ
        #SUM-ADDL
*
  IF (#MTD-FLAG AND #CUR-YEAR)
   AND (SOURCE-CUR-NEW-MBR(#INDEX)   = 0)
   AND (SOURCE-CUR-ADDL-MBR(#INDEX)  = 0)
   AND (SOURCE-CUR-INQUIRIES(#INDEX) = 0)
    ESCAPE TOP
  END-IF
*
  IF  (#MTD-FLAG AND NOT #CUR-YEAR)
   AND (SOURCE-LST-NEW-MBR(#INDEX)   = 0)
   AND (SOURCE-LST-ADDL-MBR(#INDEX)  = 0)
   AND (SOURCE-LST-INQUIRIES(#INDEX) = 0)
    ESCAPE TOP
  END-IF
*
  COMPUTE #REAL-TRIP-NUM = 999 - SC-TRIP  /* REVERSE TRIP NUMBER
* COMPUTE #TRIP-YEAR     = 99  - SC-TRIP-YY  /* CHANGED Y2K
  IF SC-TRIP-YY GE 50
     COMPUTE #TRIP-YEAR     = 149 - SC-TRIP-YY /* REVERSE TRIP YEAR
     ADD 1900 TO #TRIP-YEAR
  ELSE
     COMPUTE #TRIP-YEAR     = 49  - SC-TRIP-YY
     ADD 2000 TO #TRIP-YEAR
  END-IF
*
  IF SC-A = 'T'
    MOVE EDITED #REAL-TRIP-NUM (EM=ZZ9) TO #TRIP-NUMBER
  ELSE
    MOVE EDITED #REAL-TRIP-NUM (EM=ZZ9) TO #HOLD-NUMBER
    COMPRESS #HOLD-NUMBER '-' SC-A INTO #TRIP-NUMBER LEAVING NO SPACE
  END-IF
*
  MOVE SOURCE-DESCRIPTION TO #TRIP-DESC
  IF #MTD-FLAG AND #CUR-YEAR
    MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-NEW
    MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-ADDL
    MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-INQ
    ADD SOURCE-CUR-NEW-MBR   (#INDEX) TO #G-NEW-SUM
    ADD SOURCE-CUR-ADDL-MBR  (#INDEX) TO #G-ADDL-SUM
    ADD SOURCE-CUR-INQUIRIES (#INDEX) TO #G-INQ-SUM
  ELSE
    IF #MTD-FLAG
      MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-NEW
      MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-ADDL
      MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #TRIP-INQ
      ADD SOURCE-LST-NEW-MBR   (#INDEX) TO #G-NEW-SUM
      ADD SOURCE-LST-ADDL-MBR  (#INDEX) TO #G-ADDL-SUM
      ADD SOURCE-LST-INQUIRIES (#INDEX) TO #G-INQ-SUM
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR  (#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR (#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
*
        IF #TRIP-YEAR NE #DATN-YY
         AND (#SUM-NEW = 0) AND (#SUM-ADDL = 0) AND (#SUM-INQ = 0)
          ESCAPE TOP
        END-IF
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR  (#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR (#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #TRIP-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #TRIP-ADDL
      MOVE EDITED #SUM-INQ (EM=ZZ,ZZ9) TO #TRIP-INQ
      ADD #SUM-NEW  TO #G-NEW-SUM
      ADD #SUM-ADDL TO #G-ADDL-SUM
      ADD #SUM-INQ  TO #G-INQ-SUM
    END-IF
  END-IF
*
  MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
  ADD 1 TO #S-INDEX
  RESET #SCN-DETAIL-LINE
  PERFORM SCN-RESET-CHECK
END-READ
*
RESET #SCN-DETAIL-LINE
MOVE '** TOTALS **' TO #TRIP-DESC
MOVE EDITED #G-NEW-SUM  (EM=ZZ,ZZ9) TO #TRIP-NEW
MOVE EDITED #G-ADDL-SUM (EM=ZZ,ZZ9) TO #TRIP-ADDL
MOVE EDITED #G-INQ-SUM  (EM=ZZ,ZZ9) TO #TRIP-INQ
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
ADD 1 TO #S-INDEX
RESET #SCN-DETAIL-LINE
PERFORM SCN-RESET-CHECK
*
END-SUBROUTINE
*
* * * * * * * * * * ** * * * * * *
DEFINE SUBROUTINE PRINT-CONVENTIONS
* * * * * * * * * * ** * * * * * *
*
RESET #I1
READ SOURCE-APPL-V BY SOURCE-CODE-GROUP FROM 'C' THRU 'C'
  ADD 1 TO #I1
*
  MOVE SOURCE-CODE    TO #SRT-SOURCE-CODE (#I1)
  MOVE SOURCE-DESC-YY TO #SRT-YYYY        (#I1)
*
  IF #SRT-YYYY (#I1) LT 50    /* PUT IN CENTURY
    ADD 2000 TO #SRT-YYYY (#I1)
  ELSE
    ADD 1900 TO #SRT-YYYY (#I1)
  END-IF
*
  FOR #I2 1 TO 12
    IF #MON (#I2) = SOURCE-DESC-MONTH
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I2 LE 12
    MOVE #I2 TO #SRT-MM (#I1)
  ELSE
    MOVE 99   TO #SRT-MM   (#I1)
    MOVE 9999 TO #SRT-YYYY (#I1)
  END-IF
END-READ
*
IF #I1 GT 1
  MOVE #I1 TO #SRT-NUM-ENTRIES
*   CALL 'WAASSRT1' #SRT-YYYY (1) #SRT-PARM      /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT1' #SRT-TABLE(*) #SRT-PARM       /* SAG RH1 11242005 SS
END-IF
*
IF #MTD-FLAG
  COMPRESS 'CONVENTION SOURCE OF APPLICATION DETAIL FOR'
      #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'CONVENTION SOURCE OF APPLICATION DETAIL FOR ALL OF'
       #YYYY INTO #SCN-TITLE
END-IF
*
ASSIGN #S-INDEX = 3
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
ASSIGN #SCN-DETAIL(1) = #CONV-TITLE1
ASSIGN #SCN-DETAIL(2) = #CONV-TITLE2
RESET #G-NEW-SUM
      #G-ADDL-SUM
      #G-INQ-SUM
*
FOR #I1 1 TO #SRT-NUM-ENTRIES
  RESET #SUM-NEW
        #SUM-INQ
        #SUM-ADDL
*
  FIND SOURCE-APPL-V WITH SOURCE-CODE = #SRT-SOURCE-CODE (#I1)
    MOVE SOURCE-DESCRIPTION TO #CONV-DESC
*
    IF SOURCE-APPL-V.SOURCE-CODE-GROUP NE MASK (A)
      ESCAPE TOP
    END-IF
*
    IF   (#CONV-DESC-YY = MASK (NN))
     AND (#CONV-DESC-YY GT #SAD-YY)
      ESCAPE TOP
    END-IF
*
    IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
     ADD SOURCE-CUR-NEW-MBR   (#INDEX) TO #G-NEW-SUM
     ADD SOURCE-CUR-ADDL-MBR  (#INDEX) TO #G-ADDL-SUM
     ADD SOURCE-CUR-INQUIRIES (#INDEX) TO #G-INQ-SUM
    ELSE
     IF #MTD-FLAG
      MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
      ADD SOURCE-LST-NEW-MBR   (#INDEX) TO #G-NEW-SUM
      ADD SOURCE-LST-ADDL-MBR  (#INDEX) TO #G-ADDL-SUM
      ADD SOURCE-LST-INQUIRIES (#INDEX) TO #G-INQ-SUM
     ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED #SUM-INQ(EM=ZZ,ZZ9) TO #CONV-INQ
      ADD #SUM-NEW  TO #G-NEW-SUM
      ADD #SUM-ADDL TO #G-ADDL-SUM
      ADD #SUM-INQ  TO #G-INQ-SUM
     END-IF
    END-IF
*
    ESCAPE BOTTOM
*
  END-FIND
*
  MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
  ADD 1 TO #S-INDEX
  RESET #SCN-DETAIL-LINE
  PERFORM SCN-RESET-CHECK
END-FOR
*
MOVE '** TOTALS **' TO #CONV-DESC
MOVE EDITED #G-NEW-SUM  (EM=ZZ,ZZ9) TO #CONV-NEW
MOVE EDITED #G-ADDL-SUM (EM=ZZ,ZZ9) TO #CONV-ADDL
MOVE EDITED #G-INQ-SUM  (EM=ZZ,ZZ9) TO #CONV-INQ
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
ADD 1 TO #S-INDEX
RESET #SCN-DETAIL-LINE
PERFORM SCN-RESET-CHECK
*
END-SUBROUTINE
*
* * * * * * * * * * ** * * * * * * * * * ** * * * * * * * * * *
DEFINE SUBROUTINE PRINT-DIRECT-MAIL-CAMPAIGNS   /* group M
* * * * * * * * * * ** * * * * * * * * * ** * * * * * * * * * *
*
RESET #I1
*
READ SOURCE-APPL-V BY SOURCE-CODE-GROUP FROM 'M' THRU 'M'
  ADD 1 TO #I1
*
  MOVE SOURCE-CODE    TO #SRT-SOURCE-CODE (#I1)
  MOVE SOURCE-DESC-YY TO #SRT-YYYY        (#I1)
*
  IF #SRT-YYYY (#I1) LT 50    /* PUT IN CENTURY
    ADD 2000 TO #SRT-YYYY (#I1)
  ELSE
    ADD 1900 TO #SRT-YYYY (#I1)
  END-IF
*
  FOR #I2 1 TO 12
    IF #MON (#I2) = SOURCE-DESC-MONTH
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I2 LE 12
    MOVE #I2 TO #SRT-MM (#I1)
  ELSE
    MOVE 99   TO #SRT-MM   (#I1)
    MOVE 9999 TO #SRT-YYYY (#I1)
  END-IF
END-READ
*
IF #I1 GT 1
  MOVE #I1 TO #SRT-NUM-ENTRIES
*   CALL 'WAASSRT1' #SRT-YYYY (1) #SRT-PARM        /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT1' #SRT-TABLE(*) #SRT-PARM         /* SAG RH1 11242005 SS
END-IF
*
IF #MTD-FLAG
  COMPRESS 'DIRECT MAIL SOURCE OF APPLICATION DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'DIRECT MAIL SOURCE OF APPLICATION DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #MAIL-TITLE1 TO #SCN-DETAIL(1)
ASSIGN #SCN-DETAIL(2) = #MAIL-TITLE2
*
RESET #G-NEW-SUM
      #G-ADDL-SUM
      #G-INQ-SUM
*
FOR #I1 1 TO #SRT-NUM-ENTRIES
*
  RESET #SUM-NEW
        #SUM-INQ
        #SUM-ADDL
  FIND SOURCE-APPL-V WITH SOURCE-CODE = #SRT-SOURCE-CODE (#I1)
*
  IF SOURCE-APPL-V.SOURCE-CODE-GROUP NE MASK (A)
    ESCAPE TOP
  END-IF
*
  MOVE SOURCE-DESCRIPTION TO #CONV-DESC
  IF   (#CONV-DESC-YY = MASK (NN))
   AND (#CONV-DESC-YY GT #SAD-YY)
     ESCAPE TOP
  END-IF
*
  IF SOURCE-DESC-NNNNN = MASK (NNNNN)
    MOVE SOURCE-DESC-NNNNN TO #TOT-7
    MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
    RESET #CONV-DESC-AAAAA
  ELSE
     IF SOURCE-DESC-NNNN = MASK (NNNN)
       MOVE SOURCE-DESC-NNNN TO #TOT-7
       MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
       RESET #CONV-DESC-AAAAA
     ELSE
       IF SOURCE-DESC-NNN = MASK (NNN)
         MOVE SOURCE-DESC-NNN TO #TOT-7
         MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
         RESET #CONV-DESC-AAAAA
       ELSE
         IF SOURCE-DESC-NN = MASK (NN)
           MOVE SOURCE-DESC-NN TO #TOT-7
           MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
           RESET #CONV-DESC-AAAAA
         ELSE
           IF SOURCE-DESC-N = MASK (N)
             MOVE SOURCE-DESC-N TO #TOT-7
             MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
             RESET #CONV-DESC-AAAAA
           END-IF
         END-IF
       END-IF
     END-IF
   END-IF
*
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
*
     IF SOURCE-CODE = 'DMM'
       MOVE SOURCE-CUR-INQUIRIES (#INDEX) TO #TOT-7
       MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
     END-IF
   ELSE
    IF #MTD-FLAG
      MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
*
      IF SOURCE-CODE = 'DMM'
        MOVE SOURCE-LST-INQUIRIES (#INDEX) TO #TOT-7
        MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
     END-IF
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED #SUM-INQ (EM=ZZ,ZZ9) TO #CONV-INQ
*
      IF SOURCE-CODE = 'DMM'
        MOVE #SUM-INQ TO #TOT-7
        MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
      END-IF
    END-IF
   END-IF
*
  ESCAPE BOTTOM
*
  END-FIND
*
  MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
  ADD 1 TO #S-INDEX
  RESET #SCN-DETAIL-LINE
  PERFORM SCN-RESET-CHECK
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * ** * * * * * * * * * ** * * * * * * * * * *
DEFINE SUBROUTINE PRINT-DIRECT-MAIL-INQUIRIES /* group Q
* * * * * * * * * * ** * * * * * * * * * ** * * * * * * * * * *
*
RESET #I1
*
READ SOURCE-APPL-V BY SOURCE-CODE-GROUP FROM 'Q' THRU 'Q'
  ADD 1 TO #I1
*
  MOVE SOURCE-CODE    TO #SRT-SOURCE-CODE (#I1)
  MOVE SOURCE-DESC-YY TO #SRT-YYYY        (#I1)
*
  IF #SRT-YYYY (#I1) LT 50    /* PUT IN CENTURY
    ADD 2000 TO #SRT-YYYY (#I1)
  ELSE
    ADD 1900 TO #SRT-YYYY (#I1)
  END-IF
*
  FOR #I2 1 TO 12
    IF #MON (#I2) = SOURCE-DESC-MONTH
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #I2 LE 12
    MOVE #I2 TO #SRT-MM (#I1)
  ELSE
    MOVE 99   TO #SRT-MM   (#I1)
    MOVE 9999 TO #SRT-YYYY (#I1)
  END-IF
END-READ
*
MOVE #I1 TO #SRT-NUM-ENTRIES
IF #I1 GT 1
*   CALL 'WAASSRT1' #SRT-YYYY (1) #SRT-PARM      /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT1' #SRT-TABLE(*) #SRT-PARM       /* SAG RH1 11242005 SS
END-IF
*
IF #MTD-FLAG
  COMPRESS 'DIRECT MAIL (INQUIRY) SOURCE OF APPL DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'DIRECT MAIL (INQUIRY) SOURCE OF APPL DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #MAIL-TITLE1 TO #SCN-DETAIL(1)
ASSIGN #SCN-DETAIL(2) = #MAIL-TITLE2
*
RESET #G-NEW-SUM
      #G-ADDL-SUM
      #G-INQ-SUM
*
FOR #I1 1 TO #SRT-NUM-ENTRIES
*
  RESET #SUM-NEW
        #SUM-INQ
        #SUM-ADDL
  FIND SOURCE-APPL-V WITH SOURCE-CODE = #SRT-SOURCE-CODE (#I1)
*
  IF SOURCE-APPL-V.SOURCE-CODE-GROUP NE MASK (A)
    ESCAPE TOP
  END-IF
*
  MOVE SOURCE-DESCRIPTION TO #CONV-DESC
  IF   (#CONV-DESC-YY = MASK (NN))
   AND (#CONV-DESC-YY GT #SAD-YY)
     ESCAPE TOP
  END-IF
*
  IF SOURCE-DESC-NNNNN = MASK (NNNNN)
    MOVE SOURCE-DESC-NNNNN TO #TOT-7
    MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
    RESET #CONV-DESC-AAAAA
  ELSE
     IF SOURCE-DESC-NNNN = MASK (NNNN)
       MOVE SOURCE-DESC-NNNN TO #TOT-7
       MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
       RESET #CONV-DESC-AAAAA
     ELSE
       IF SOURCE-DESC-NNN = MASK (NNN)
         MOVE SOURCE-DESC-NNN TO #TOT-7
         MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
         RESET #CONV-DESC-AAAAA
       ELSE
         IF SOURCE-DESC-NN = MASK (NN)
           MOVE SOURCE-DESC-NN TO #TOT-7
           MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
           RESET #CONV-DESC-AAAAA
         ELSE
           IF SOURCE-DESC-N = MASK (N)
             MOVE SOURCE-DESC-N TO #TOT-7
             MOVE EDITED #TOT-7 (EM=ZZZZ,ZZ9) TO #CONV-DATE
             RESET #CONV-DESC-AAAAA
           END-IF
         END-IF
       END-IF
     END-IF
   END-IF
*
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
   ELSE
    IF #MTD-FLAG
      MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED #SUM-INQ (EM=ZZ,ZZ9) TO #CONV-INQ
    END-IF
   END-IF
*
   ESCAPE BOTTOM
*
  END-FIND
*
  MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
  ADD 1 TO #S-INDEX
  RESET #SCN-DETAIL-LINE
  PERFORM SCN-RESET-CHECK
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * ** * * * *
DEFINE SUBROUTINE PRINT-OTHERS
* * * * * * * * * * ** * * * *
*
IF #MTD-FLAG
  COMPRESS 'OTHER SOURCE OF APPLICATION DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'OTHER SOURCE OF APPLICATION DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #OTH-TITLE1 TO #SCN-DETAIL(1)
MOVE #OTH-TITLE2 TO #SCN-DETAIL(2)
MOVE 'O' TO #START-GROUP
PERFORM READ-SOURCE-APPL
*
END-SUBROUTINE
*
* * * * * * * * * * ** * *
DEFINE SUBROUTINE PRINT-CAP
* * * * * * * * * * ** * *
*
IF #MTD-FLAG
  COMPRESS 'C.A.P. SOURCE OF APPLICATION DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'C.A.P. SOURCE OF APPLICATION DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #CAP-TITLE1 TO #SCN-DETAIL(1)
MOVE #CAP-TITLE2 TO #SCN-DETAIL(2)
MOVE 'Z' TO #START-GROUP
PERFORM READ-SOURCE-APPL
*
END-SUBROUTINE
*
* * * * * * * * * * ** * *
DEFINE SUBROUTINE PRINT-VISIT-FORT-MYER
* * * * * * * * * * ** * *
*
IF #MTD-FLAG
  COMPRESS 'VISIT FORT MYER SOURCE OF APPLICATION DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'VISIT FORT MYER SOURCE OF APPLICATION DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #CAP-TITLE1 TO #SCN-DETAIL(1)
MOVE #CAP-TITLE2 TO #SCN-DETAIL(2)
MOVE 'V' TO #START-GROUP
PERFORM READ-SOURCE-APPL
*
END-SUBROUTINE
*
* * * * * * * * * * ** * *
DEFINE SUBROUTINE PRINT-PENTAGON
* * * * * * * * * * ** * *
*
IF #MTD-FLAG
  COMPRESS 'PENTAGON SOURCE OF APPLICATION DETAIL FOR'
                        #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
  COMPRESS 'PENTAGON SOURCE OF APPLICATION DETAIL FOR ALL OF'
                                             #YYYY INTO #SCN-TITLE
END-IF
*
MOVE 3 TO #S-INDEX
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #CAP-TITLE1 TO #SCN-DETAIL(1)
MOVE #CAP-TITLE2 TO #SCN-DETAIL(2)
MOVE 'W' TO #START-GROUP
PERFORM READ-SOURCE-APPL
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-CADET-CONVERSIONS
* * * * * * * * * * * * * * * * * * * * *
*
IF #MTD-FLAG
    COMPRESS 'CADET CONVERSION SOURCE OF APPLICATION DETAIL FOR'
      #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
    COMPRESS 'CADET CONVERSION SOURCE OF APPLICATION DETAIL FOR ALL OF'
       #YYYY INTO #SCN-TITLE
END-IF
ASSIGN #S-INDEX = 3
MOVE #CADT-TITLE1 TO #SCN-DETAIL(1)
MOVE (AD=I) TO #SD-CV(1)
MOVE 'I' TO #START-GROUP
PERFORM READ-SOURCE-APPL
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-FIRS-MR-APPS
* * * * * * * * * * * * * * * * *
*
IF #MTD-FLAG
    COMPRESS 'FIRS & MR APPS SOURCE OF APPLICATION DETAIL FOR'
      #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
    COMPRESS 'FIRS & MR APPS SOURCE OF APPLICATION DETAIL FOR ALL OF'
       #YYYY INTO #SCN-TITLE
END-IF
ASSIGN #S-INDEX = 3
MOVE #CADT-TITLE1 TO #SCN-DETAIL(1)
MOVE (AD=I) TO #SD-CV(1)
MOVE 'F' TO #START-GROUP
PERFORM READ-SOURCE-APPL
*
END-SUBROUTINE
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-PERIODICALS
*
IF #MTD-FLAG
    COMPRESS 'SOURCE OF APPLICATION DETAIL FOR'
      #MONTH-ENTRY(#INDEX) #YYYY INTO #SCN-TITLE
ELSE
    COMPRESS 'SOURCE OF APPLICATION DETAIL FOR ALL OF'
       #YYYY INTO #SCN-TITLE
END-IF
ASSIGN #S-INDEX = 4
MOVE (AD=I) TO #SD-CV(1)
MOVE (AD=I) TO #SD-CV(2)
MOVE #SPER-TITLE1 TO #SCN-DETAIL(1)
MOVE #SPER-TITLE2 TO #SCN-DETAIL(2)
MOVE #SPER-TITLE3 TO #SCN-DETAIL(3)
MOVE 'P' TO #START-GROUP
PERFORM READ-SOURCE-APPL
MOVE 'TOTAL OF PERIODICALS' TO #SPER-DESC3
MOVE EDITED #G-NEW-SUM(EM=ZZ,ZZ9) TO #SPER-NEW
MOVE EDITED #G-ADDL-SUM(EM=ZZ,ZZ9) TO #SPER-ADDL
MOVE EDITED #G-INQ-SUM(EM=ZZ,ZZ9) TO #SPER-INQ
ADD #G-NEW-SUM TO #S-NEW-SUM
ADD #G-ADDL-SUM TO #S-ADDL-SUM
ADD #G-INQ-SUM TO #S-INQ-SUM
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
MOVE (AD=I) TO #SD-CV(#S-INDEX)
ADD 1 TO #S-INDEX
PERFORM SCN-RESET-CHECK
MOVE #SPER-TITLE4 TO #SCN-DETAIL(#S-INDEX)
ADD 1 TO #S-INDEX
PERFORM SCN-RESET-CHECK
MOVE 'N' TO #START-GROUP
PERFORM READ-SOURCE-APPL
MOVE 'TOTAL OF BASE NEWSPAPERS' TO #SPER-DESC3
MOVE EDITED #G-NEW-SUM(EM=ZZ,ZZ9) TO #SPER-NEW
MOVE EDITED #G-ADDL-SUM(EM=ZZ,ZZ9) TO #SPER-ADDL
MOVE EDITED #G-INQ-SUM(EM=ZZ,ZZ9) TO #SPER-INQ
ADD #G-NEW-SUM TO #S-NEW-SUM
ADD #G-ADDL-SUM TO #S-ADDL-SUM
ADD #G-INQ-SUM TO #S-INQ-SUM
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
MOVE (AD=I) TO #SD-CV(#S-INDEX)
ADD 1 TO #S-INDEX
PERFORM SCN-RESET-CHECK
MOVE 'Y' TO #START-GROUP
PERFORM READ-SOURCE-APPL
ADD #G-NEW-SUM TO #S-NEW-SUM
ADD #G-ADDL-SUM TO #S-ADDL-SUM
ADD #G-INQ-SUM TO #S-INQ-SUM
MOVE 'A' TO #START-GROUP
PERFORM READ-SOURCE-APPL
ADD #G-NEW-SUM TO #S-NEW-SUM
ADD #G-ADDL-SUM TO #S-ADDL-SUM
ADD #G-INQ-SUM TO #S-INQ-SUM
MOVE 'TOTAL OF ALL ADVERTISEMENTS' TO #SPER-DESC3
MOVE EDITED #S-NEW-SUM(EM=ZZ,ZZ9) TO #SPER-NEW
MOVE EDITED #S-ADDL-SUM(EM=ZZ,ZZ9) TO #SPER-ADDL
MOVE EDITED #S-INQ-SUM(EM=ZZ,ZZ9) TO #SPER-INQ
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
MOVE (AD=I) TO #SD-CV(#S-INDEX)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-SOURCE-APPL
* * * * * * * * * * * * * * * * * * * * *
*
RESET #G-NEW-SUM
      #G-ADDL-SUM
      #G-INQ-SUM
READ SOURCE-APPL-V LOGICAL BY SOURCE-CODE-GROUP FROM #START-GROUP
                                                THRU #START-GROUP
RESET #SUM-NEW
      #SUM-INQ
      #SUM-ADDL
*
IF #START-GROUP = 'F' OR = 'Z' OR = 'V' OR = 'W'
   MOVE SOURCE-DESCRIPTION TO #CONV-DESC
*
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
   ELSE
    IF #MTD-FLAG
     MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED #SUM-INQ(EM=ZZ,ZZ9) TO #CONV-INQ
    END-IF
  END-IF
END-IF
*
IF #START-GROUP = 'O'
   MOVE SOURCE-DESCRIPTION TO #CONV-DESC
   IF   (#CONV-DESC-YY = MASK (NN))
    AND (#CONV-DESC-YY GT #SAD-YY)
     ESCAPE TOP
    END-IF
*
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
   ELSE
    IF #MTD-FLAG
     MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
     MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-ADDL
     MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #CONV-INQ
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #CONV-ADDL
      MOVE EDITED #SUM-INQ(EM=ZZ,ZZ9) TO #CONV-INQ
    END-IF
  END-IF
END-IF
*
IF #START-GROUP = 'I'
   MOVE SOURCE-DESCRIPTION TO #CONV-DESC
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
   ELSE
    IF #MTD-FLAG
     MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #CONV-NEW
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #CONV-NEW
    END-IF
   END-IF
END-IF
*
IF #START-GROUP = 'P' OR = 'N'
   MOVE SOURCE-DESCRIPTION TO #SPER-DESC
   IF #MTD-FLAG AND #CUR-YEAR
     MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-NEW
     MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-ADDL
     MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #SPER-INQ
     ADD SOURCE-CUR-NEW-MBR(#INDEX) TO #G-NEW-SUM
     ADD SOURCE-CUR-INQUIRIES(#INDEX) TO #G-INQ-SUM
     ADD SOURCE-CUR-ADDL-MBR(#INDEX) TO #G-ADDL-SUM
   ELSE
    IF #MTD-FLAG
     MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-NEW
     MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-ADDL
     MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #SPER-INQ
     ADD SOURCE-LST-NEW-MBR(#INDEX) TO #G-NEW-SUM
     ADD SOURCE-LST-INQUIRIES(#INDEX) TO #G-INQ-SUM
     ADD SOURCE-LST-ADDL-MBR(#INDEX) TO #G-ADDL-SUM
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #SPER-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #SPER-ADDL
      MOVE EDITED #SUM-INQ(EM=ZZ,ZZ9) TO #SPER-INQ
      ADD #SUM-NEW TO #G-NEW-SUM
      ADD #SUM-ADDL TO #G-ADDL-SUM
      ADD #SUM-INQ TO #G-INQ-SUM
    END-IF
   END-IF
END-IF
*
IF #START-GROUP = 'Y' OR = 'A' OR = 'R' OR = 'D'
  DECIDE FOR FIRST CONDITION
   WHEN SOURCE-CODE-GROUP = 'Y'
     COMPRESS 'III.' SOURCE-DESCRIPTION INTO #SPER-DESC2
   WHEN SOURCE-CODE-GROUP = 'A'
        COMPRESS ' IV.' SOURCE-DESCRIPTION INTO #SPER-DESC2
   WHEN SOURCE-CODE-GROUP = 'R'
        COMPRESS '  V.' SOURCE-DESCRIPTION INTO #SPER-DESC2
   WHEN SOURCE-CODE-GROUP = 'D'
        COMPRESS ' VI.' SOURCE-DESCRIPTION INTO #SPER-DESC2
*  WHEN SOURCE-CODE-GROUP = 'O'
*       COMPRESS 'VII.' SOURCE-DESCRIPTION INTO #SPER-DESC2
   WHEN NONE IGNORE
   END-DECIDE
   IF #MTD-FLAG AND #CUR-YEAR
    MOVE EDITED SOURCE-CUR-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-NEW
    MOVE EDITED SOURCE-CUR-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-ADDL
    MOVE EDITED SOURCE-CUR-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #SPER-INQ
     ADD SOURCE-CUR-NEW-MBR(#INDEX) TO #G-NEW-SUM
     ADD SOURCE-CUR-INQUIRIES(#INDEX) TO #G-INQ-SUM
     ADD SOURCE-CUR-ADDL-MBR(#INDEX) TO #G-ADDL-SUM
   ELSE
    IF #MTD-FLAG
     MOVE EDITED SOURCE-LST-NEW-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-NEW
     MOVE EDITED SOURCE-LST-ADDL-MBR(#INDEX)(EM=ZZ,ZZ9) TO #SPER-ADDL
     MOVE EDITED SOURCE-LST-INQUIRIES(#INDEX)(EM=ZZ,ZZ9) TO #SPER-INQ
     ADD SOURCE-LST-NEW-MBR(#INDEX) TO #G-NEW-SUM
     ADD SOURCE-LST-INQUIRIES(#INDEX) TO #G-INQ-SUM
     ADD SOURCE-LST-ADDL-MBR(#INDEX) TO #G-ADDL-SUM
    ELSE
      IF #CUR-YEAR
        FOR #AP 1 TO #INDEX
          ADD SOURCE-CUR-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-CUR-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-CUR-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      ELSE
        FOR #AP 1 TO 12
          ADD SOURCE-LST-NEW-MBR(#AP) TO #SUM-NEW
          ADD SOURCE-LST-ADDL-MBR(#AP) TO #SUM-ADDL
          ADD SOURCE-LST-INQUIRIES(#AP) TO #SUM-INQ
        END-FOR
      END-IF
      MOVE EDITED #SUM-NEW (EM=ZZ,ZZ9) TO #SPER-NEW
      MOVE EDITED #SUM-ADDL(EM=ZZ,ZZ9) TO #SPER-ADDL
      MOVE EDITED #SUM-INQ(EM=ZZ,ZZ9) TO #SPER-INQ
      ADD #SUM-NEW TO #G-NEW-SUM
      ADD #SUM-ADDL TO #G-ADDL-SUM
      ADD #SUM-INQ TO #G-INQ-SUM
    END-IF
   END-IF
END-IF
*
MOVE #SCN-DETAIL-LINE TO #SCN-DETAIL(#S-INDEX)
ADD 1 TO #S-INDEX
RESET #SCN-DETAIL-LINE
PERFORM SCN-RESET-CHECK
*
END-READ
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SCN-RESET-CHECK
* * * * * * * * * * * * * * * * * * * *
*
IF #S-INDEX > 21
   IF *DEVICE = 'BATCH'
     WRITE NOTITLE USING FORM 'C1201FAC'
   ELSE
     INPUT USING MAP 'C1201MAC'
   END-IF
   IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
   END-IF
   RESET #SCN-DETAIL(3:21)
   MOVE 3 TO #S-INDEX
END-IF
*
END-SUBROUTINE
END
