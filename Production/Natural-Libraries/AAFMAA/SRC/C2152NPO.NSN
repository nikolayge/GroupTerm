* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : C2152NPO                                              *
* DESCRIPTION  : Format output LTII re-new rates
************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG     REASON
* NGG    20121025         Program Written
* NGG    20130325    NG1 Change navigation for LTII+
************************************************************************
*
DEFINE DATA
PARAMETER
1 #DESIRED-FACE-AMT (P7.2/3)
1 #VT-TERM          (N2)
1 #CURR-AGE         (P03.0)
1 #ELIGIBLE-MSG     (A078)
1 #MBR-SPS-GEN      (A012)
1 #DESIRED-LINES    (A078/12)
1 #INSURED          (A035)
1 #USER             (A003)
1 #PDA-SCR-COMMAND  (A002)
1 #PDA-SCR-ID       (N09.0)
1 #PDA-SCR-SUFFIX   (A003)
1 #REFUND-1-ONLINE  (A078)
1 #SCR-AOD-X        (A011)
1 #SCR-DOB-X        (A011)
1 #SCR-SSN          (A011)
1 #SEX              (A001)
1 #TOP-LINE         (A056)
LOCAL
1 #AGE              (N2)
1 #LTIILINE         (A78/6)
1 #LTIILINES        (A79/18)
1 #TITLE-LINE       (A79/2)
1 #TYPE-TEXT        (A12)
1 #EXTEND           (N2)
1 #CURRENT          (N5.2/10)
1 #GUARANTEED       (N5.2/10)
1 #LTIIRV           (A7/10)
1 #FACE-A           (A8)
1 #BACK             (L)
1 J                 (I2)
1 K                 (I2)
1 L                 (I2)
1 #TEXT             (A78)
END-DEFINE
*
#PDA-SCR-COMMAND := '*'
#EXTEND := 5
K := 1
#AGE := #CURR-AGE
FOR K 1 TO 3
  IF #DESIRED-FACE-AMT (K) > 0
    CALLNAT 'C2150NPO'
      #DESIRED-FACE-AMT (K)
      #AGE
      #VT-TERM
      #EXTEND
      #CURRENT (*)
      #GUARANTEED (*)
    FOR J 1 TO #EXTEND
      MOVE EDITED #GUARANTEED (J) (EM=ZZZ9.99) TO #LTIIRV (J)
    END-FOR
    J := 2 * K
    MOVE EDITED #DESIRED-FACE-AMT (K) (EM=ZZZZZ9) TO #FACE-A
    COMPRESS '$' #FACE-A INTO #LTIILINE (J) LEAVING NO
    COMPRESS
      #LTIILINE (J)
      'Maximum Monthly Re-new:'
      #LTIIRV (*)
      INTO #LTIILINE (J)
  END-IF
END-FOR
INPUT TEXT #TEXT USING MAP 'C2159MPO'   /* new simple screen from C2151SPO
IF  #PDA-SCR-COMMAND NE '*'
  ESCAPE ROUTINE
END-IF
IF #DESIRED-FACE-AMT (1) > 0
  K := 1
  #PDA-SCR-COMMAND := '*'
  CALLNAT 'C2151NPO'
    #DESIRED-FACE-AMT (K)
    #AGE
    #VT-TERM
    #SEX
    #TITLE-LINE (*)
    #LTIILINES (*)
  RESET #BACK
  RPTL.
  REPEAT UNTIL *PF-KEY = 'PF3'
    INPUT USING MAP 'C2160MPO'
    IF #PDA-SCR-COMMAND NE '*'
      ESCAPE BOTTOM
    END-IF
    REPEAT
      IF #BACK
        SUBTRACT 1 FROM K
        IF K < 1 ASSIGN K = 1; RESET #BACK END-IF
      ELSE
        ADD 1 TO K
        IF K > 3
          ASSIGN K = 3;
          #BACK := TRUE
          MOVE 'PO' TO #PDA-SCR-COMMAND  /* NG1
          ESCAPE BOTTOM (RPTL.)          /* NG1
        END-IF
      END-IF
      UNTIL #DESIRED-FACE-AMT (K) > 0
    END-REPEAT
    CALLNAT 'C2151NPO'
      #DESIRED-FACE-AMT (K)
      #AGE
      #VT-TERM
      #SEX
      #TITLE-LINE (*)
      #LTIILINES (*)
  END-REPEAT
END-IF
END
