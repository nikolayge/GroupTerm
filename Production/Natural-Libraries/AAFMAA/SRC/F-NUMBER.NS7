* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************************************
* NAME         : F-POL-NUMBER (F-NUMBER)
* DESCRIPTION  : Compile Policy Number
********************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG   REASON
* YAK    07/27/2008        Program Written
* YAK    04/01/2015  YK1   Trim before return
********************************************************************************
DEFINE FUNCTION F-POL-NUMBER RETURNS (A) DYNAMIC BY VALUE
  DEFINE DATA
  PARAMETER
  1 #POL-NUM-PARM
    2 #ID-NUMBER     (N6)
    2 #INDEX         (N2)
  LOCAL
  1 #POL-NUMBER    (A) DYNAMIC
  1 #ISSUE-DATE    (N8)
  1 #POL-PRE       (A4)
  1 #POL-SUFF      (A5)
  1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 NUM-MBRSHIPS (9)
    2 ISSUE-DATE  (9)
    2 POL-NUM-PRE (9)
    2 POL-NUM-SUF (9)
  END-DEFINE
*
FIND(1) ST-V WITH ID-NUMBER = #ID-NUMBER
  #ISSUE-DATE := ST-V.ISSUE-DATE(#INDEX)
  #POL-PRE    := ST-V.POL-NUM-PRE(#INDEX)
  #POL-SUFF   := ST-V.POL-NUM-SUF(#INDEX)
END-FIND
*
IF ST-V.ISSUE-DATE(#INDEX) = 0    /* POL NO LONGER AVAILABLE
  MOVE #ID-NUMBER TO #POL-NUMBER
ELSE
  IF ST-V.ISSUE-DATE(#INDEX) GE 19941201
    COMPRESS #ID-NUMBER '-' ST-V.NUM-MBRSHIPS(#INDEX)
      INTO #POL-NUMBER LEAVING NO
  ELSE
    COMPRESS #POL-PRE #ID-NUMBER #POL-SUFF INTO #POL-NUMBER LEAVING NO
  END-IF
END-IF
F-POL-NUMBER := *TRIM(#POL-NUMBER)                /* YK1
END-FUNCTION
END
