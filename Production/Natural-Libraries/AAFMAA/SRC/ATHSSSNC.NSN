* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHSSSNC *** Create Generic SSN based on CN number ***********
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    12092004  None    Program written.                            *
* PAM    06152010  PM1     Added generic SSN security                  *
* PAM    06242010  PM2     Removed PM1 - logic moved to C1000PAT       *
*                                                                      *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #SSN                    (N9)
1 #ID-NUMBER              (A6)
1 REDEFINE #ID-NUMBER
 2 #ID-NUMBER-N           (N6)
*
LOCAL
1  NM-T01 VIEW OF A-TERMINALS
 2 TERMINAL-ID            (A8)
 2 COMMENTS               (A20)
 2 REDEFINE COMMENTS
  3 LAST-CN               (N6)
*
1 #I1                     (I2)
1 #CREATE-SSN             (A1)
1 #DISPLAY-SSN            (A9)
1 #HOLD-SSN               (N9)
1 #TEXT                   (A70)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
DEFINE WINDOW CREATE-SSN
  SIZE 10 * 77
  BASE TOP LEFT
  FRAMED ON POSITION SYMBOL AUTO
*
IF #SSN GT 0
  ESCAPE ROUTINE
END-IF
*
SET WINDOW 'CREATE-SSN'
*
RESET #DISPLAY-SSN #HOLD-SSN
MOVE 'N' TO #CREATE-SSN
COMPRESS 'Please enter a valid SSN or allow the system to'
         'generate a generic SSN' INTO #TEXT
*
IF #ID-NUMBER-N = 0
  FIND (1) NM-T01 WITH TERMINAL-ID = 'CNMASTER'
    MOVE LAST-CN TO #HOLD-SSN
  END-FIND
ELSE
  MOVE #ID-NUMBER-N TO #HOLD-SSN
END-IF
*
MOVE EDITED #HOLD-SSN (EM=999999999) TO #DISPLAY-SSN
*
INPUT TEXT #TEXT MARK *#SSN USING MAP 'ATHMSSNC'
IF #CREATE-SSN = 'Y' OR = 'y'
  FIND (1) NM-T01 WITH TERMINAL-ID = 'CNMASTER' /* Make sure number is
    MOVE LAST-CN TO #SSN                        /* current
  END-FIND
ELSE
  IF #SSN = 0
    REINPUT WITH TEXT #TEXT MARK *#CREATE-SSN
  END-IF
END-IF
*
SET WINDOW OFF
*
END
