* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-ACHRE1                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Determine if ACH reversed in previous month           *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE FUNCTION FUNCTION-ACH-PREV-REVERSED RETURNS (L) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #MEMBER-NUMBER            (N6)
1 #DATE                     (N8)
*
LOCAL
*
1 #DATE-FIELDS
 2 #WORK-DATE               (N8)
 2 REDEFINE #WORK-DATE
  3 #WORK-DATE-YYYY         (N4)
  3 #WORK-DATE-MM           (N2)
  3 #WORK-DATE-DD           (N2)
 2 #WORK-END-DATE           (N8)
 2 #RETURN-CALC             (N8)
 2 #REQUEST                 (A1)   INIT <'B'>
*
1 AR-V VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER               (N6)
  2 TRANSACTION-DATN        (P9)
  2 CASH-CODE
  2 STATUS-TYPE
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-ACH-PREV-REVERSED
#WORK-DATE    := #DATE
#WORK-DATE-DD := 01
SUBTRACT 1 FROM #WORK-DATE-MM
IF #WORK-DATE-MM LT 1
  ADD 12 TO #WORK-DATE-MM
  SUBTRACT 1 FROM #WORK-DATE-YYYY
END-IF
*
CALLNAT 'GDATESXX'
  #WORK-END-DATE
  #WORK-DATE
  #RETURN-CALC
  #REQUEST
*
READ AR-V BY ID-NUMBER FROM #MEMBER-NUMBER
  IF AR-V.ID-NUMBER GT #MEMBER-NUMBER
    ESCAPE BOTTOM
  END-IF
*
  IF  (AR-V.TRANSACTION-DATN < #WORK-DATE)
   OR (AR-V.TRANSACTION-DATN > #WORK-END-DATE)
   OR (AR-V.CASH-CODE NE 'E')
   OR (AR-V.STATUS-TYPE NE 'RV')
    ESCAPE TOP
  END-IF
*
  FUNCTION-ACH-PREV-REVERSED := TRUE
  ESCAPE BOTTOM
END-READ
*
END-FUNCTION
*
END
