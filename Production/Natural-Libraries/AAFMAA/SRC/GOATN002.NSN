* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GOATN002 - Create Contact relation and defaults for GEN
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    08/23/2011  None    Program written                           *
* NGG    11/04/2011  NG1     BLANK PHONES/EMAILS FIX
************************************************************************
DEFINE DATA
PARAMETER
1 #AT-ISN            (P8)
LOCAL USING GOCRA001           /* Contact relations PDA
LOCAL USING GOADA001
LOCAL USING GOADA002
LOCAL USING GOPHA001
LOCAL USING GOEMA001
LOCAL
1 #UPDATED           (L)
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 CONTACT-ID         (N8)
  2 REFERING-ID-NUMBER (1)
  2 INTRO-FLAG         (A1)
  2 USER-SOURCE        (A3)
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
*
1 BLANK     (A1) CONST <' '>
END-DEFINE
*
GET AT-V #AT-ISN
IF *ISN = 0
  ESCAPE ROUTINE
END-IF
*
IF AT-V.REFERING-ID-NUMBER (1) NE 0
    AND AT-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
IF AT-V.CONTACT-ID = 0
  ESCAPE ROUTINE
END-IF
*
FIND (1) ST-V WITH ID-NUMBER = AT-V.REFERING-ID-NUMBER (1)
  #CONTACT-REL.CONTACT-1-ID := MEMBER-CONTACT-ID
END-FIND
*
* Create contact relation
*
#CONTACT-REL.CONTACT-2-ID := AT-V.CONTACT-ID
DECIDE ON FIRST VALUE OF AT-V.INTRO-FLAG
  VALUE 'S'
    #CONTACT-REL.ROLE-1-CD := 'S'
  VALUE 'K'
    #CONTACT-REL.ROLE-1-CD := 'P'
  VALUE 'G'
    #CONTACT-REL.ROLE-1-CD := 'H'
  NONE VALUE IGNORE
END-DECIDE
#CONTACT-REL.ROLE-2-CD := AT-V.INTRO-FLAG
CALLNAT 'GOCRN001' #CONTACT-REL #MSG #UPDATED
*
* Link primary address
*
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := AT-V.CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'P'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF STREET = BLANK  /* No primary address - default to the sponsor addr
  #ADDR-REL.ADDRESS-POOL-ID :=
    F-GET-ADR-POOL-ID (< MEMBER-CONTACT-ID, #ADDR-REL.ADDR-TYPE-CD >)
  #ADDR-REL.ADDRESS-SOURCE := 'A'
  CALLNAT 'GOADN004' #ADDR-REL USER-SOURCE #MSG #UPDATED
END-IF
*
* Link secondary address
*
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := AT-V.CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'S'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF STREET = BLANK  /* No primary address - default to the sponsor addr
  #ADDR-REL.ADDRESS-POOL-ID :=
    F-GET-ADR-POOL-ID (< MEMBER-CONTACT-ID, #ADDR-REL.ADDR-TYPE-CD >)
  #ADDR-REL.ADDRESS-SOURCE := 'A'
  CALLNAT 'GOADN004' #ADDR-REL USER-SOURCE #MSG #UPDATED
END-IF
*
* Get phones
*
RESET #PHONE
#PHONE.CONTACT-ID := AT-V.CONTACT-ID
#PHONE.PHONE-TYPE-CODE := 'H'
CALLNAT 'GOPHN002' #PHONE #MSG
IF #PHONE.PHONE-ID = 0
  #PHONE.CONTACT-ID := MEMBER-CONTACT-ID
  CALLNAT 'GOPHN002' #PHONE #MSG
  IF #PHONE.PHONE-ID > 0                 /* NG1
    RESET #PHONE.PHONE-ID
    #PHONE.CONTACT-ID := AT-V.CONTACT-ID
    #MSG := 'CREATE CLONE'
    CALLNAT 'GOPHN001' #PHONE USER-SOURCE #MSG #UPDATED
  END-IF
END-IF
RESET #PHONE
#PHONE.CONTACT-ID := AT-V.CONTACT-ID
#PHONE.PHONE-TYPE-CODE := 'B'
CALLNAT 'GOPHN002' #PHONE #MSG
IF #PHONE.PHONE-ID = 0
  #PHONE.CONTACT-ID := MEMBER-CONTACT-ID
  CALLNAT 'GOPHN002' #PHONE #MSG
  IF #PHONE.PHONE-ID > 0                /* NG1
    RESET #PHONE.PHONE-ID
    #PHONE.CONTACT-ID := AT-V.CONTACT-ID
    #MSG := 'CREATE CLONE'
    CALLNAT 'GOPHN001' #PHONE USER-SOURCE #MSG #UPDATED
  END-IF
END-IF
RESET #PHONE
#PHONE.CONTACT-ID := AT-V.CONTACT-ID
#PHONE.PHONE-TYPE-CODE := 'C'
CALLNAT 'GOPHN002' #PHONE #MSG
IF #PHONE.PHONE-ID = 0
  #PHONE.CONTACT-ID := MEMBER-CONTACT-ID
  CALLNAT 'GOPHN002' #PHONE #MSG
  IF #PHONE.PHONE-ID > 0               /* NG1
    RESET #PHONE.PHONE-ID
    #PHONE.CONTACT-ID := AT-V.CONTACT-ID
    #MSG := 'CREATE CLONE'
    CALLNAT 'GOPHN001' #PHONE USER-SOURCE #MSG #UPDATED
  END-IF
END-IF
*
* Get e-mail
*
RESET #EMAIL
#EMAIL.CONTACT-ID := AT-V.CONTACT-ID
CALLNAT 'GOEMN002' #EMAIL #MSG
IF #EMAIL.EMAIL-ADDRESS = BLANK
  #EMAIL.CONTACT-ID := MEMBER-CONTACT-ID
  CALLNAT 'GOEMN002' #EMAIL #MSG
  IF #EMAIL.EMAIL-ID > 0              /* NG1
    RESET #EMAIL.EMAIL-ID
    #EMAIL.CONTACT-ID := AT-V.CONTACT-ID
    CALLNAT 'GOEMN001'  #EMAIL USER-SOURCE #MSG #UPDATED
  END-IF
END-IF
*
ON ERROR
  #MSG := 'Natural abend'
  ESCAPE ROUTINE
END-ERROR
*
END
