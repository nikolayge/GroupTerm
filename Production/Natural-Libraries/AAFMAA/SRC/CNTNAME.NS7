* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* DESCRIPTION  : Format name                                           *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    07/11/2011  PM1     Added contact-type                        *
* bz-Name 07/07/2017     Fine Names   Return all in mixed case
************************************************************************
*
DEFINE FUNCTION FUNCTION-CONTACT-NAME RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID      (N8)
  1 #VERSION         (N8) OPTIONAL
  LOCAL
  1 CNT-V VIEW OF A-CONTACTS
    2  FULL-NAME
      3  FIRST-NAME
      3  MIDDLE-NAME
      3  LAST-NAME
      3  SUFFIX
    2 CONTACT-TYPE                                             /* PM1
    2 FIRM-ORGANIZATION-NAME
    2 EX-LAST-NAME                      /* formatted 
  1 HC-V VIEW OF A-HISTORY-CONTACTS
    2 FIRST-NAME                        (A25)
    2 MIDDLE-NAME                       (A25)
    2 LAST-NAME                         (A25)
    2 SUFFIX                            (A10)
    2 CONTACT-TYPE
    2 FIRM-ORGANIZATION-NAME
    2 VERSION
  1 BLANK         (A1) CONST <' '>
  END-DEFINE

IF #VERSION SPECIFIED
    AND #VERSION > 0
  FIND HC-V WITH CONTACT-ID =  #CONTACT-ID
    IF HC-V.VERSION = #VERSION
      MOVE BY NAME HC-V TO CNT-V
      ESCAPE BOTTOM
    END-IF
  END-FIND
ELSE
  FIND (1) CNT-V WITH CONTACT-ID = #CONTACT-ID
  END-FIND
END-IF

IF CNT-V.CONTACT-TYPE = 'O'                              /* PM1
  FUNCTION-CONTACT-NAME := EDIT_NAME_LOWCASE(<CNT-V.FIRM-ORGANIZATION-NAME>)   /* bz-Name
ELSE                                                       /* PM1

  IF CNT-V.LAST-NAME = *TRANSLATE(EX-LAST-NAME, UPPER)          /* S bz-Name
       CNT-V.LAST-NAME := EX-LAST-NAME
  ELSE 
       CNT-V.LAST-NAME := EDIT_NAME_LOWCASE(<CNT-V.LAST-NAME>)
  END-IF   
  CNT-V.FIRST-NAME := EDIT_NAME_LOWCASE(<CNT-V.FIRST-NAME>)
  CNT-V.MIDDLE-NAME := EDIT_NAME_LOWCASE(<CNT-V.MIDDLE-NAME>)
  CNT-V.SUFFIX := EDIT_SFX_LOWCASE(<CNT-V.SUFFIX>)
  FUNCTION-CONTACT-NAME := EDIT_NAME_LFMS(<FULL-NAME>)
*   FUNCTION-CONTACT-NAME := FUNCTION-DISP-CT-NAME (<
*     CNT-V.FIRST-NAME,
*     CNT-V.MIDDLE-NAME,
*     CNT-V.LAST-NAME,
*     CNT-V.SUFFIX>)                                            /* E bz-Name 
END-IF                                                     /* PM1

END-FUNCTION
END
