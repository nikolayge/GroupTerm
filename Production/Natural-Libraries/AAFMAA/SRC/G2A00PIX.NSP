* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G2A00PIX
* THIS PROGRAM PROCESSES THE 'IX' (CADET SSN) CROSS-REFERENCE SCREEN
*
*
* * * * * * * * *
* M O D I F I C A T I O N    L O G
*
* DRW 10/8/2009  DW1 AFF:l Unification
* * * * * * * ** 
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX       /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
1 #SAS-IAS   (A8/7)         /* DW1
*
1 #SKIP-TO         (A25)
1 REDEFINE #SKIP-TO
 2 #SKIP-TO-N      (N9)
1 #START-AT        (A25)
*
1 #RANK-LBL        (A4) INIT <'Rank'>
1 #SCR-SELECT-CMND (A2/7)
1 #SCR-INTERNET-C  (C)
1 #SCR-NAME-C      (C)
1 #SCR-SSN-C      (C)
1 #SCR-ZIP-CODE-C (C)
1 #S-NAME          (A25)
1 #SCR-MIL-SERV    (A4/7)
1 #SCR-SSN         (N9/7)
1 #SCR-NUM-MBRSHPS (A2/7)
1 #S-NUM-MBRSHPS (A2)
1 REDEFINE #S-NUM-MBRSHPS
  2 #SNM   (N1)
  2 #M     (A1)
1 #SCR-ADDR1       (A25/7)
1 #SCR-ADDR2       (A25/7)
1 #SCR-ADDR3       (A25/7)
1 #SCR-ZIP-5       (A5/7)
1 #SCR-ZIP-4       (A5/7)
1 #SCR-NAME        (A25/7)
1 #SCR-NAME-DECEASED (A25/7)
1 #SCR-ID-NO       (N6/7)
1 #SCR-RANK        (A3/7)
1 #SCR-APPL-SOURCE (A6/7)
1 #SCR-INTERNET    (A60/7)
1 #SCR-STATUS      (A1/7)
1 I (I1)
1 #I1   (I2)
1 CR-I01 VIEW OF A-INTRO-MEMBER
  2 ID-NUMBER        (N6)
  2 RANK             (A3)
  2 STATUS           (A1)
  2 APPL-SOURCE      (A6)
  2 MIL-SERVICE      (A3)
  2 SSN              (N9)
  2 NAME             (A25)
  2 ADDRESS-1        (A25)
  2 ADDRESS-2        (A25)
  2 ADDRESS-3        (A25)
 2 ZIP-CODE         (N9)
 2 REDEFINE ZIP-CODE
  3 ZIP-5  (A5)
  3 ZIP-4  (A4)
  3 REDEFINE ZIP-4
   4 ZIP-4-N   (N4)
END-DEFINE
* * * * * * * * * * * * * * *
INCLUDE GONERCXX
*
INPUT #START-AT
MOVE #START-AT TO #SKIP-TO
MOVE '*' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX
MOVE (AD=I) TO #SCR-SSN-C
*
* * * * * * * * * * * * * * * * *
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  ASSIGN #SCR-SSN (1) = #SKIP-TO-N
  MOVE 1 TO I
*
  READ (7) CR-I01 LOGICAL WITH SSN STARTING FROM #SCR-SSN(1)
    RESET #SAS-IAS   (I)    /* Does not apply here but needed for map    DW1
*
    ASSIGN #SCR-SSN(I)    = SSN
    ASSIGN #SCR-NAME(I)   = NAME
    ASSIGN #SCR-RANK(I)   = RANK
    ASSIGN #SCR-SSN(I)    = SSN
    ASSIGN #SCR-APPL-SOURCE (I)    = APPL-SOURCE
    ASSIGN #SCR-NAME(I)   = NAME
    ASSIGN #SCR-RANK(I)   = RANK
    ASSIGN #SCR-ID-NO(I)  = ID-NUMBER
    ASSIGN #SCR-STATUS(I) = STATUS
    ASSIGN #SCR-ADDR1(I)  = ADDRESS-1
    ASSIGN #SCR-ADDR2(I)  = ADDRESS-2
    ASSIGN #SCR-ADDR3(I)  = ADDRESS-3
    MOVE ZIP-5 TO #SCR-ZIP-5 (I)
    RESET #SCR-ZIP-4 (I)
    IF ZIP-4-N NE 0
      COMPRESS '-' ZIP-4 INTO #SCR-ZIP-4 (I) LEAVING NO
    END-IF
*
    IF MIL-SERVICE = MASK ('A')
      MOVE 'ARMY'  TO #SCR-MIL-SERV (I)
    ELSE
      IF MIL-SERVICE = MASK ('F')
        MOVE 'USAF'  TO #SCR-MIL-SERV (I)
      ELSE
        IF MIL-SERVICE = MASK ('N')
          MOVE 'USN'  TO #SCR-MIL-SERV (I)
        ELSE
          IF MIL-SERVICE = MASK ('M')
            MOVE 'USMC'  TO #SCR-MIL-SERV (I)
          ELSE
            MOVE 'USCG'  TO #SCR-MIL-SERV (I)
          END-IF
        END-IF
      END-IF
    END-IF
*
    ADD 1 TO I
  END-READ /* (0091)
*
  IF I LE 7
    ASSIGN #SCR-NAME (I) = '** END OF DATA **'
    MOVE 'CR' TO #CGA-SCR-COMMAND
  END-IF
*
  ASSIGN #SKIP-TO-N = #SCR-SSN (7)
  INPUT MARK *#START-AT USING MAP 'G2A01MCR'
*
  FOR #I1 1 TO 7
    IF #SCR-SELECT-CMND (#I1) NE ' '
      IF #SCR-SELECT-CMND (#I1) = 'X'
        MOVE 'IM' TO #CGA-SCR-COMMAND
      ELSE
        MOVE #SCR-SELECT-CMND (#I1) TO #CGA-SCR-COMMAND
      END-IF
*
      MOVE #SCR-ID-NO (#I1) TO #CGA-SCR-ID
    END-IF
  END-FOR
*
  RESET #SCR-SSN         (1:7)
  RESET #SCR-NUM-MBRSHPS (1:7)
  RESET #SCR-APPL-SOURCE (1:7)
  RESET #SCR-ADDR1       (1:7)
  RESET #SCR-ADDR2       (1:7)
  RESET #SCR-ADDR3       (1:7)
  RESET #SCR-NAME        (1:7)
  RESET #SCR-ID-NO       (1:7)
  RESET #SCR-RANK        (1:7)
  RESET #SCR-STATUS      (1:7)
  RESET #SCR-MIL-SERV    (1:7)
*
END-REPEAT /* (0083)
END
