* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PLAN-AFA TRUE INDICATES that a SVC-MEMBER-PLAN record
* exists for this CN and hence is an AFA client
* * * * * * * * *
* M O D I F I C A T I O N  L O G
* * * * * * * * *
* USER   DATE      TAG  REASON
* RSE 11-15-2010   RE1  Skip Suspended-Test when "3SR"
* NGG 20111214     NG1  Remove Segment index - new Service Plan
*
DEFINE DATA
PARAMETER   /* Object PDA A-IFAFFL with this PARAMETER
1 #CN                     (N6)
1 F-IF-AFFL-DATA
  2 #AFFL-CLIENT (L)              /*Record found by CN
  2 #HAS-ACTIVE-AFFL-PLANS (L)    /*Found by CN and is active
  2 #NUM-ACTIVE-AFFL-PLANS (N3.0) /*Number found ctive rec.
  2 #HAS-3SW (L)                  /*Bill by AFA
  2 #HAS-3SC (L)                  /*Bill by AFA
  2 #HAS-3S (L)                   /*Bill by AFA
  2 #HAS-3SR (L)                  /*Bill AAFMAA
  2 #HAS-ER (L)                   /*Has new serv ?ER
  2 #SAS-SUSPENDED (L)            /*Not paid since 02 2 mo back
  2 #HAS-CC (L)                   /*using Credit card

LOCAL USING OBJLMB01   /* 1 S-PLAN-V View of DDM A-SVC-MEMBER
*
LOCAL
1 #I1          (I2)
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
*
1 ACTIVE    (A1) CONST <'A'>
1 YES       (A1) CONST <'Y'>
END-DEFINE
*
RESET
  #AFFL-CLIENT
  #HAS-ACTIVE-AFFL-PLANS
  #NUM-ACTIVE-AFFL-PLANS
  #HAS-3SW
  #HAS-3SC
  #HAS-3S
  #HAS-3SR
  #HAS-ER
  #SAS-SUSPENDED
  #HAS-CC
*
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #CN        /* NG1
  MOVE TRUE TO #AFFL-CLIENT
*
    IF SVC-PLAN = ' '
      ESCAPE TOP
    END-IF
    IF SVC-STATUS = ACTIVE
      MOVE TRUE TO #HAS-ACTIVE-AFFL-PLANS
      ADD 1 TO #NUM-ACTIVE-AFFL-PLANS
*
      IF SVC-MODE = '0' OR = '1' OR = '2' OR = '4' /*Has a Credit Card Pol
        MOVE TRUE TO #HAS-CC
      END-IF
*
      DECIDE ON FIRST VALUE SVC-PLAN 
        VALUE '3S'   MOVE TRUE TO #HAS-3S
        VALUE '3SW'  MOVE TRUE TO #HAS-3SW
        VALUE '3SC'  MOVE TRUE TO #HAS-3SC
        VAlUE '3SR'  MOVE TRUE TO #HAS-3SR
        ANY PERFORM SAS-SUSPENDED-TEST
        NONE  IF SVC-PLAN = MASK ('.ER')
            MOVE TRUE TO #HAS-ER
          END-IF
      END-DECIDE
    END-IF
END-FIND
*  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE SAS-SUSPENDED-TEST
IF NOT #HAS-3SR                                /* RE1
  IF SVC-PAY-UP NE YES
    MOVE *DATN TO #YYYYMMDD
    MOVE 1     TO #DD
    SUBTRACT 2 FROM #MM   /* was 2
    IF #MM LT 1
      ADD 12 TO #MM
      SUBTRACT 1 FROM #YYYY
    END-IF
*
    IF  SVC-PAID-TO-DATE LT #YYYYMMDD
      MOVE TRUE TO #SAS-SUSPENDED
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
END
