* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPTRM4
*  THIS PROGRAM COUNTS TERMINATIONS (BY RANK)
DEFINE DATA
LOCAL
*
1 #JCL-PARM              (A18)
1 REDEFINE #JCL-PARM
 2 #JCL-YYYY             (N4)
 2 #FIL-1                (A1)
 2 #JCL-START-CN         (N6)
 2 #FIL-2                (A1)
 2 #JCL-END-CN           (N6)
*
1 #EXTRACT
 2 #EXT-TERM-TYPE         (A1)
 2 #EXT-TERM-DATE         (N6)
 2 #EXT-ISSUE-DATE        (N8)
 2 #EXT-RANK              (A3)
 2 #EXT-MIL-STAT          (A1)
 2 #EXT-MIL-SERV          (A3)
 2 #EXT-INTRO-FLAG        (A1)
 2 #EXT-ID-NUMBER         (N6)
 2 #EXT-ISSUE-AGE         (A2)
 2 #EXT-DOB               (N8)
 2 #EXT-NAME              (A25)
*
1 #LAST-CN               (N6)
1 #LAST-RANK             (A3)
1 #TERM-TYPE-LABEL       (A20)
1 #TERM-AGE-MONS         (P5)
1 #DUR-MONS              (P5)
1 #AVG-TERM-AGE          (P9)
1 #TERM-DECIMAL          (P9.2)
1 #AVG-DURATION          (P9)
1 #DUR-DECIMAL           (P9.2)
*
1 #TERM-TOTS      (4)
 2 #TBL-MBRS           (P5)
 2 #TBL-CNT            (P5)
 2 #TBL-HASH-TERM-AGE  (P9)
 2 #TBL-HASH-DUR-MONS  (P9)
*
1 #VA-TERM-DATE      (N6)
1 REDEFINE #VA-TERM-DATE
 2 #VA-TERM-YYYY     (N4)
 2 #VA-TERM-MM       (N2)
*
1 #WYYYYMMDD     (N8)
1 REDEFINE #WYYYYMMDD
 2 #WCENT-A      (A2)
 2 #WYY-A        (A2)
 2 #WMM-A        (A2)
 2 #WDD-A        (A2)
*
1 #WYYYYMMDD-LOW
 2 #WYYYY-LOW   (N4)
 2 #WMM-LOW     (N2)
 2 #WDD-LOW     (N2)
1 REDEFINE #WYYYYMMDD-LOW
 2 #WYYYYMMDD-LOW-N (N8)
*
1 #WYYYYMMDD-HIGH
 2 #WYYYY-HIGH  (N4)
 2 REDEFINE #WYYYY-HIGH
  3 #WCENT-HIGH (N2)
  3 #WYY-HIGH   (N2)
  3 REDEFINE #WYY-HIGH
   4 #WYY-HIGH-A (A2)
 2 #WMM-HIGH    (N2)
 2 REDEFINE #WMM-HIGH
  3 #WMM-HIGH-A   (A2)
 2 #WDD-HIGH    (N2)
1 REDEFINE #WYYYYMMDD-HIGH
 2 #WYYYYMMDD-HIGH-N (N8)
1 REDEFINE #WYYYYMMDD-HIGH
 2 #DATE-DIFF        (N8)
*
1 #YRS-AREA-1        (A5)
1 #YRS-AREA-2        (A5)
1 #ISS-DT-DISP       (A8)
1 #DOB-DISP          (A8)
1 #TERM-DT-DISP      (A5)
1 #CNT               (P5)
*
1 #I1             (I2)
1 #NEW-PAGE       (L)
1 #JUST-WROTE-DASHES (L)
1 #RANGE             (A24)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INPUT #JCL-PARM
IF #JCL-START-CN NE 000000
  COMPRESS 'CN' #JCL-START-CN 'THRU CN' #JCL-END-CN INTO #RANGE
ELSE
  MOVE 'ALL MEMBERS' TO #RANGE
END-IF
*
FORMAT (2) LS=133 PS=72
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM
           2X #RANGE
           2X #TERM-TYPE-LABEL
           2X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE' *PAGE-NUMBER (2)
FORMAT (3) LS=133 PS=72
WRITE  (3) TITLE LEFT 'PROGRAM:' *PROGRAM
           2X #RANGE
           2X 'TERMINATION SUMMARY (BY RANK)'
           2X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE' *PAGE-NUMBER (3)
*
READ WORK 1 RECORD #EXTRACT
END-ALL
*
SORT BY #EXT-RANK #EXT-MIL-STAT
        #EXT-MIL-SERV #EXT-INTRO-FLAG #EXT-ID-NUMBER
   USING #EXT-ISSUE-AGE
         #EXT-DOB
         #EXT-NAME
         #EXT-ISSUE-DATE
         #EXT-TERM-DATE
         #EXT-TERM-TYPE
*
  AT BREAK #EXT-MIL-SERV
    RESET #LAST-CN
    IF NOT #JUST-WROTE-DASHES
*     WRITE (2) '------------------------------------------'
      MOVE TRUE TO #JUST-WROTE-DASHES
    END-IF
  END-BREAK
*
  AT BREAK #EXT-MIL-STAT
    RESET #LAST-CN
    IF NOT #JUST-WROTE-DASHES
*     WRITE (2) '------------------------------------------'
      MOVE TRUE TO #JUST-WROTE-DASHES
    END-IF
  END-BREAK
*
  AT BREAK #EXT-RANK
    RESET #LAST-CN
    COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (1) / #TBL-CNT (1)
    DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPUTE ROUNDED #TERM-DECIMAL = #AVG-TERM-AGE / 12
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
    COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (1) / #TBL-CNT (1)
    DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPUTE ROUNDED #DUR-DECIMAL = #AVG-DURATION / 12
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
*   WRITE (2) 'RANK-TOTALS  - UNITS = ' #TBL-CNT (1)
*                           ' MEMBERS = ' #TBL-MBRS (1)
*                           ' AVG TERM AGE = ' #YRS-AREA-1 #TERM-DECIMAL
*                           ' AVG DURATION = ' #YRS-AREA-2 #DUR-DECIMAL
    WRITE (3) 'RANK-TOTS - '
               #LAST-RANK
                            ' UNITS = ' #TBL-CNT (1)
                            ' MEMBERS = ' #TBL-MBRS (1)
                            ' AVG TERM AGE = ' #YRS-AREA-1 #TERM-DECIMAL
                            ' AVG DURATION = ' #YRS-AREA-2 #DUR-DECIMAL
    RESET #TBL-MBRS          (1)
          #TBL-CNT           (1)
          #TBL-HASH-TERM-AGE (1)
          #TBL-HASH-DUR-MONS (1)
*   WRITE (2) '******************************************'
  END-BREAK
*
  IF #NEW-PAGE
    RESET #NEW-PAGE
    NEWPAGE (2)
  END-IF
*
  PERFORM SET-LABELS
*
  MOVE #EXT-TERM-DATE TO #VA-TERM-DATE
  MOVE #EXT-ISSUE-DATE  TO #WYYYYMMDD-LOW-N
  MOVE #VA-TERM-DATE TO #WYYYYMMDD-HIGH-N
  MULTIPLY #WYYYYMMDD-HIGH-N BY 100
  ADD 15 TO #WYYYYMMDD-HIGH-N
  PERFORM CALC-DATE-DIFF
  COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
  COMPUTE #DUR-MONS = (#WYY-HIGH  * 12) + #WMM-HIGH
*
  MOVE #EXT-DOB TO #WYYYYMMDD-LOW-N
  MOVE #VA-TERM-DATE      TO #WYYYYMMDD-HIGH-N
  MULTIPLY #WYYYYMMDD-HIGH-N BY 100
  ADD 15 TO #WYYYYMMDD-HIGH-N
  PERFORM CALC-DATE-DIFF
  COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
  COMPUTE #TERM-AGE-MONS = (#WYY-HIGH  * 12) + #WMM-HIGH
*
  MOVE #EXT-ISSUE-DATE TO #WYYYYMMDD
  COMPRESS #WMM-A #WDD-A #WYY-A INTO #ISS-DT-DISP WITH '-'
*
  MOVE #EXT-DOB TO #WYYYYMMDD
  COMPRESS #WMM-A #WDD-A #WYY-A INTO #DOB-DISP WITH '-'
*
  MOVE #VA-TERM-DATE TO #WYYYYMMDD
  MULTIPLY #WYYYYMMDD BY 100
  COMPRESS #WMM-A #WYY-A INTO #TERM-DT-DISP WITH '-'
*
  IF #EXT-ID-NUMBER NE #LAST-CN
    ADD 1 TO #TBL-MBRS (*)
  END-IF
  MOVE #EXT-ID-NUMBER TO #LAST-CN
  MOVE #EXT-RANK      TO #LAST-RANK
*
  ADD 1              TO #TBL-CNT           (*)
  ADD #TERM-AGE-MONS TO #TBL-HASH-TERM-AGE (*)
  ADD #DUR-MONS      TO #TBL-HASH-DUR-MONS (*)
*
  ADD 1 TO #CNT
* DISPLAY (2) 'COUNT'           #TBL-CNT (1)
*             'TERM/DT'         #TERM-DT-DISP
*             'ISS-DT'          #ISS-DT-DISP
*             'DOB'             #DOB-DISP
*             'ISS/AGE'         #EXT-ISSUE-AGE
*             'YRS-MONS/DUR'    #YRS-AREA-1
*             'TERM/AGE'        #YRS-AREA-2
*             'ID'              #EXT-ID-NUMBER
*             'MIL/ID'          #EXT-MIL-SERV
*             'MIL/STAT'        #EXT-MIL-STAT
*             'RANK'            #EXT-RANK
*             '       NAME'     #EXT-NAME
*             'INTRO/FLAG'      #EXT-INTRO-FLAG
  RESET #JUST-WROTE-DASHES
END-SORT
*
COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (4) / #TBL-CNT (4)
DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
COMPUTE ROUNDED #TERM-DECIMAL = #AVG-TERM-AGE / 12
COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (4) / #TBL-CNT (4)
DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
COMPUTE ROUNDED #DUR-DECIMAL = #AVG-DURATION / 12
COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
* ITE (2) 'GRAND-TOTALS - UNITS = ' #TBL-CNT (4)
*                       ' MEMBERS = ' #TBL-MBRS (4)
*                       ' AVG TERM AGE = ' #YRS-AREA-1 #TERM-DECIMAL
*                       ' AVG DURATION = ' #YRS-AREA-2 #DUR-DECIMAL
WRITE (3) ' '
WRITE (3) 'GRAND-TOTS -    '
                        ' UNITS = ' #TBL-CNT (4)
                        ' MEMBERS = ' #TBL-MBRS (4)
                        ' AVG TERM AGE = ' #YRS-AREA-1 #TERM-DECIMAL
                        ' AVG DURATION = ' #YRS-AREA-2 #DUR-DECIMAL
*
* * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-DATE-DIFF
* * * * * * * * * * * * * * * * * * * * * * * *
*
IF #WDD-LOW GT #WDD-HIGH
  SUBTRACT 1 FROM #WMM-HIGH
  ADD      1 TO   #WDD-HIGH
  IF #WMM-HIGH = 02
    ADD 28 TO #WDD-HIGH
  ELSE
    IF #WMM-HIGH = 04 OR = 06 OR = 09 OR = 11
      ADD 30 TO #WDD-HIGH
    ELSE
      ADD 31 TO #WDD-HIGH
    END-IF
  END-IF
END-IF
*
IF #WMM-LOW GT #WMM-HIGH
  SUBTRACT 1 FROM #WYYYY-HIGH
  ADD 12 TO #WMM-HIGH
END-IF
*
SUBTRACT #WYYYYMMDD-LOW-N FROM #WYYYYMMDD-HIGH-N
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-LABELS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
DECIDE ON FIRST VALUE OF #EXT-TERM-TYPE
  VALUE '1'  MOVE 'CASH-VALUES'    TO #TERM-TYPE-LABEL
  VALUE '2'  MOVE '1035-EXCHANGES' TO #TERM-TYPE-LABEL
  VALUE '3'  MOVE 'LAPSE NON-PMT'  TO #TERM-TYPE-LABEL
  VALUE '4'  MOVE 'LAPSE OVERLOAN' TO #TERM-TYPE-LABEL
  VALUE '5'  MOVE 'REG LAPSES'     TO #TERM-TYPE-LABEL
  VALUE '6'  MOVE 'RESIGNATIONS'   TO #TERM-TYPE-LABEL
  NONE       MOVE 'UNKNOWN'        TO #TERM-TYPE-LABEL
END-DECIDE
*
END-SUBROUTINE
*
END
