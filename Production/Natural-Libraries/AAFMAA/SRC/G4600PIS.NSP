* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: G4600PIS - INTRO MBR SOURCE OF APPL COUNTS
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
*
1 #SCR-CURR-YEAR (A5)
1 REDEFINE #SCR-CURR-YEAR
 2 #SCR-HDR-CURR-YYYY (N4)
 2 #SCR-HDR-SLASH   (A1)
1 #SCR-NEXT-YEAR (A5)
1 REDEFINE #SCR-NEXT-YEAR
 2 #SCR-HDR-NEXT-YYYY (N4)
 2 #SCR-HDR-PLUS    (A1)
*
* * * *   N O T E   F I L E  * * * * * * * * * * * * * * * * *
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 NOTE-DATE      (N8/1)
  2 REDEFINE NOTE-DATE
   3 NOTE-DATE-YYYY (A4/1)
   3 NOTE-DATE-MM   (A2/1)
   3 NOTE-DATE-DD   (A2/1)
  2 NOTE-TEXT      (A60/40)
*
  2 REDEFINE NOTE-TEXT             /* "IS" DEFINITION
   3 IS-S-O-A       (40)   /* EACH 2 LINES IS A SOURCE-OF-APPL
    4 IS-SOURCE-CODE (A6)
    4 IS-SEQ         (N1)  /* 1-1ST 4 SCHOOLS    2-ARMY OCS AND A-F OTS
    4 IS-TOT-CNT     (P5)  /* TOTAL SOURCE COUNT
    4 IS-SCHOOL    (4)     /* MIL-ACAD/A-F-ACAD/AR ROTC/A-F ROTC
     5 IS-YEAR     (4)     /* CURR-YR/+1/+2/OTHER YEAR
      6 IS-NBR-INTRO-MBRS (P5)
    4 FILL              (A2)
* * * *   S O U R C E - O F - A P P L   F I L E   * * * * * * *
1 SA-V VIEW OF A-SOURCE-OF-APPL
  2 SOURCE-CODE               (A6)
  2 SOURCE-DESCRIPTION        (A40)
*
1 #COUNTERS
  2 #IP-REC-FOUND     (L)
  2 #IS-REC-FOUND     (L)
  2 #IP-REC-ISN       (P8)
  2 #IS-REC-ISN       (P8)
  2 #RECS-READ        (P5)
  2 #NOTE-NUM         (P6)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #I4               (I2)
  2 #I5               (I2)
  2 #SPACE-INDX       (I2)
  2 #UNKNOWN-INDX     (I2)
*
1 #IS-UPDATE-DT   (A10)
1 #IS-TOTAL-MBRS  (P5)
1 #IS-TOTAL-CNT   (A6/13)   /* ACCUMS FOR "IS" SCREEN (NX 800020/29)
1 #IS-TOTAL       (P4/13)
1 #IS-DESCRIPTION (A40/13)
1 #IS-USMA-CNTS   (A8/13)
1 #IS-USAFA-CNTS  (A8/13)
1 #IS-AROTC-CNTS  (A8/13)
1 #IS-AFROTC-CNTS (A8/13)
1 #IS-AOCS-CNTS   (A8/13)
1 #IS-AFOTS-CNTS  (A8/13)
*
1 #IS-USMA-TOTS   (A8)
1 #IS-USAFA-TOTS  (A8)
1 #IS-AROTC-TOTS  (A8)
1 #IS-AFROTC-TOTS (A8)
1 #IS-AOCS-TOTS   (A8)
1 #IS-AFOTS-TOTS  (A8)
*
1 #IS-USMA-TOT-CURR    (P5)
1 #IS-USMA-TOT-OTHER   (P5)
1 #IS-USAFA-TOT-CURR   (P5)
1 #IS-USAFA-TOT-OTHER  (P5)
1 #IS-AROTC-TOT-CURR   (P5)
1 #IS-AROTC-TOT-OTHER  (P5)
1 #IS-AFROTC-TOT-CURR  (P5)
1 #IS-AFROTC-TOT-OTHER (P5)
1 #IS-AOCS-TOT-CURR    (P5)
1 #IS-AOCS-TOT-OTHER   (P5)
1 #IS-AFOTS-TOT-CURR   (P5)
1 #IS-AFOTS-TOT-OTHER  (P5)
*
1 #IS-CVT-TOTAL       (P5)
1 #IS-CVT-TOTS        (A8/6)
*
1 #TOT-CNT         (A4)
1 #CNT-CURR-YR     (P5)
1 #CNT-OTHER-YRS   (P5)
*
1 #COUNT-FORMAT    (A8)
1 REDEFINE #COUNT-FORMAT
 2 #CURR-YEAR-CNT   (A4)
 2 #SLASH           (A1)
 2 #OTHER-YEAR-CNT  (A3)
*
1 #TEXT            (A70)
1 #TODAY-YYYYMMDD  (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY    (N4)
  2 REDEFINE #TODAY-YYYY
   3 #TODAY-CENT    (N2)
   3 #TODAY-YY      (N2)
  2 #TODAY-MMDD    (N4)
*
END-DEFINE
*****************
INCLUDE GONERCXX
*****************
*
MOVE *DATN TO #TODAY-YYYYMMDD
MOVE #TODAY-YYYY TO #SCR-HDR-CURR-YYYY
*
*
MOVE '/'       TO #SCR-HDR-SLASH
COMPUTE #SCR-HDR-NEXT-YYYY = #SCR-HDR-CURR-YYYY + 1
MOVE '+'        TO #SCR-HDR-PLUS
*
MOVE 800019 TO #NOTE-NUM     /* TO BEGIN AT 800020
MOVE 99 TO #I4               /* TO FORCE READ OF NX RECORD
PERFORM GET-NEXT-SOA              /* GET INITIAL NX RECORD
ADD 1 TO #I4
*
REPEAT
  RESET #IS-TOTAL-CNT   (*)
        #IS-DESCRIPTION (*)
        #IS-USMA-CNTS   (*)
        #IS-USAFA-CNTS  (*)
        #IS-AROTC-CNTS  (*)
        #IS-AFROTC-CNTS (*)
        #IS-AOCS-CNTS   (*)
        #IS-AFOTS-CNTS  (*)
  FOR #I1 1 TO 13
    IF   (IS-SOURCE-CODE (#I4) = ' ')        /* THERE ARE NO BLANK
     AND (IS-TOT-CNT (#I4) = 0)              /* SOURCE OF APPLS
      PERFORM GET-NEXT-SOA            /* BYPASS 2ND HALF OF THE SOA
      PERFORM GET-NEXT-SOA            /* GET NEXT REAL SOA
      SUBTRACT 1 FROM #I1
      ESCAPE TOP
    END-IF
*
    IF   (IS-SOURCE-CODE (#I4) = '??????')   /* THERE ARE NO UNKNOWN
     AND (IS-TOT-CNT (#I4) = 0)              /* SOURCE OF APPLS
      PERFORM GET-NEXT-SOA            /* BYPASS 2ND HALF OF THE SOA
      PERFORM GET-NEXT-SOA            /* GET NEXT REAL SOA
      SUBTRACT 1 FROM #I1
      ESCAPE TOP
    END-IF
*
    IF IS-SOURCE-CODE (#I4) = 'CONVRT'    /* LAST ENTRY
      PERFORM ACCUM-CONVERT-TOTALS
      PERFORM ACCUM-TOTALS
      ESCAPE BOTTOM
    END-IF
*
    MOVE IS-TOT-CNT (#I4) TO #IS-TOTAL (#I1)
    MOVE EDITED IS-TOT-CNT (#I4) (EM=ZZZ9) TO #TOT-CNT
    COMPRESS '(' #TOT-CNT ')' INTO #IS-TOTAL-CNT (#I1) LEAVING NO
*
    IF IS-SOURCE-CODE (#I4) = ' '
      MOVE 'OTHER SOURCES' TO #IS-DESCRIPTION (#I1)
    ELSE
      IF IS-SOURCE-CODE (#I4) = '??????'
        MOVE 'INVL SRCE-OF-APPL' TO #IS-DESCRIPTION (#I1)
      ELSE
        FIND (1) SA-V WITH SOURCE-CODE = IS-SOURCE-CODE (#I4)
          IF NO RECORDS FOUND
            MOVE 'S-O-A REC NOT FOUND' TO #IS-DESCRIPTION (#I1)
            ESCAPE BOTTOM
          END-NOREC
*
          MOVE SOURCE-DESCRIPTION TO #IS-DESCRIPTION (#I1)
        END-FIND
      END-IF
    END-IF
*
    PERFORM ACCUM-6-SCHOOLS      /* ACCUM THE 6 SCHOOLS
    PERFORM GET-NEXT-SOA
  END-FOR
*
  IF IS-SOURCE-CODE (#I4) NE 'CONVRT'
    MOVE '*' TO #CGA-SCR-COMMAND
    MOVE 'PRESS ENTER TO SEE REMAINING SOURCE CODES' TO #TEXT
  END-IF
*
  INPUT TEXT #TEXT USING MAP 'G4600MIS'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
END-REPEAT /* (0136)
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GET-NEXT-SOA
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #I4 LT 40            /* IF NOT AT END OF NX RECORD
  ADD 1 TO #I4          /* BUMP TO NEXT RECORD
  ESCAPE ROUTINE
END-IF
*
ADD 1 TO #NOTE-NUM
MOVE TRUE TO #IS-REC-FOUND
FIND (1) NT-V WITH ID-NUMBER = #NOTE-NUM
  IF NO RECORDS FOUND
    RESET #IS-REC-FOUND
    WRITE 'NO NX REC FOUND - CALL EDS'
    ESCAPE ROUTINE
  END-NOREC
  COMPRESS NOTE-DATE-MM (1) '/' NOTE-DATE-DD (1) '/' NOTE-DATE-YYYY (1)
                                INTO #IS-UPDATE-DT LEAVING NO
END-FIND
*
RESET #I4    /* NOTE RECORD LINE INDEX
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-6-SCHOOLS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
* * * * * * * ACCUM 1ST 4 SCHOOLS * * *
FOR #I2 1 TO 4
  RESET #CNT-OTHER-YRS
  FOR #I3 1 TO 4
    IF #I3 = 1
      MOVE IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-CURR-YR
      ADD  IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-TOTAL-MBRS
    ELSE
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-OTHER-YRS
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-TOTAL-MBRS
    END-IF
  END-FOR
*
  PERFORM FORMAT-COUNTS
*
  IF #I2 = 1
    MOVE #COUNT-FORMAT TO #IS-USMA-CNTS (#I1)
    ADD #CNT-CURR-YR   TO #IS-USMA-TOT-CURR
    ADD #CNT-OTHER-YRS TO #IS-USMA-TOT-OTHER
  ELSE
    IF #I2 = 2
      MOVE #COUNT-FORMAT TO #IS-USAFA-CNTS (#I1)
      ADD #CNT-CURR-YR   TO #IS-USAFA-TOT-CURR
      ADD #CNT-OTHER-YRS TO #IS-USAFA-TOT-OTHER
    ELSE
      IF #I2 = 3
        MOVE #COUNT-FORMAT TO #IS-AROTC-CNTS (#I1)
        ADD #CNT-CURR-YR   TO #IS-AROTC-TOT-CURR
        ADD #CNT-OTHER-YRS TO #IS-AROTC-TOT-OTHER
      ELSE
        MOVE #COUNT-FORMAT TO #IS-AFROTC-CNTS (#I1)
        ADD #CNT-CURR-YR   TO #IS-AFROTC-TOT-CURR
        ADD #CNT-OTHER-YRS TO #IS-AFROTC-TOT-OTHER
      END-IF
    END-IF
  END-IF
END-FOR
*
* * * * * * * ACCUM LAST 2 SCHOOLS * * *
ADD 1 TO #I4
MOVE 4 TO #I5
FOR #I2 1 TO 2
  ADD 1 TO #I5
  RESET #CNT-OTHER-YRS
  FOR #I3 1 TO 4
    IF #I3 = 1
      MOVE IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-CURR-YR
      ADD  IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-TOTAL-MBRS
    ELSE
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-OTHER-YRS
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-TOTAL-MBRS
    END-IF
  END-FOR
*
  PERFORM FORMAT-COUNTS
*
  IF #I5 = 5
    MOVE #COUNT-FORMAT TO #IS-AOCS-CNTS (#I1)
    ADD #CNT-CURR-YR   TO #IS-AOCS-TOT-CURR
    ADD #CNT-OTHER-YRS TO #IS-AOCS-TOT-OTHER
  ELSE
    MOVE #COUNT-FORMAT TO #IS-AFOTS-CNTS (#I1)
    ADD #CNT-CURR-YR   TO #IS-AFOTS-TOT-CURR
    ADD #CNT-OTHER-YRS TO #IS-AFOTS-TOT-OTHER
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-TOTALS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
MOVE #IS-USMA-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-USMA-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-USMA-TOTS
*
MOVE #IS-USAFA-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-USAFA-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-USAFA-TOTS
*
MOVE #IS-AROTC-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-AROTC-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-AROTC-TOTS
*
MOVE #IS-AFROTC-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-AFROTC-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-AFROTC-TOTS
*
MOVE #IS-AOCS-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-AOCS-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-AOCS-TOTS
*
MOVE #IS-AFOTS-TOT-CURR  TO #CNT-CURR-YR
MOVE #IS-AFOTS-TOT-OTHER TO #CNT-OTHER-YRS
PERFORM FORMAT-COUNTS
MOVE #COUNT-FORMAT TO #IS-AFOTS-TOTS
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-CONVERT-TOTALS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
* * * * * * * ACCUM 1ST 4 SCHOOLS * * *
FOR #I2 1 TO 4
  RESET #CNT-OTHER-YRS
  FOR #I3 1 TO 4
    IF #I3 = 1
      MOVE IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-CURR-YR
      ADD  IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-CVT-TOTAL
    ELSE
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-OTHER-YRS
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-CVT-TOTAL
    END-IF
  END-FOR
*
  PERFORM FORMAT-COUNTS
  MOVE #COUNT-FORMAT TO #IS-CVT-TOTS (#I2)
END-FOR
*
ADD 1 TO #I4
MOVE 4 TO #I5
*
* * * * * * * ACCUM LAST 2 SCHOOLS  * *
FOR #I2 1 TO 2
  ADD 1 TO #I5
  FOR #I3 1 TO 4
    IF #I3 = 1
      MOVE IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-CURR-YR
      ADD  IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-CVT-TOTAL
    ELSE
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #CNT-OTHER-YRS
      ADD IS-NBR-INTRO-MBRS (#I4, #I2, #I3) TO #IS-CVT-TOTAL
    END-IF
  END-FOR
*
  PERFORM FORMAT-COUNTS
  MOVE #COUNT-FORMAT TO #IS-CVT-TOTS (#I5)
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-COUNTS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
MOVE EDITED #CNT-CURR-YR  (EM=ZZZZ) TO #CURR-YEAR-CNT
MOVE EDITED #CNT-OTHER-YRS (EM=ZZZ) TO #OTHER-YEAR-CNT
*
RESET #SLASH
IF (#CNT-CURR-YR NE 0) AND (#CNT-OTHER-YRS NE 0)
  MOVE '/' TO #SLASH
END-IF
*
END-SUBROUTINE
*
END
