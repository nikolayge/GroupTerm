* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPBNTM   list BD POLS mismatched (must be terminated)
DEFINE DATA
*
LOCAL
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 MEMBER-FLAG
 2 NUMBER-MEMBERSHIPS
 2 DATE-OF-DEATH
 2 STATUS      (9)
 2 ISSUE-DATE  (9)
 2 REDEFINE ISSUE-DATE
  3 ISS-DT-OCC (9)
   4 ISS-DT-CC  (N2)
   4 ISS-DT-YYMMDD (N6)
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER          (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-1ST    (N1)
  3 ID-NUMBER-LAST-5 (N5)
  2 NOTE-DATE          (N8/60) /* LET P/A = PRIMARY/ALTERNATE DESING
  2 REDEFINE NOTE-DATE         /* let L = linked policies
   3 BENE-DATES-6-POLS (6)
    4 BENE-ISS-DT        (N6/7)   /* (D, L)
    4 BENE-SPLIT-IND     (N1/7)   /* (D, L)
    4 BENE-LST-UPD       (N8)     /* (D)
    4 BENE-STRT-PEND     (N8)     /* (D)
    4 BENE-FOL-UP-IND    (N1)     /* (D)
    4 BENE-OWN-TRNSF-DT  (N8)     /* (D)
    4 BENE-SUC-OWN       (N1)     /* (D)
    4 BENE-NEW-LAYOUT    (N1)     /* (D)
    4 BENE-FIL1          (A4)
  2 NOTE-CLERK-ID      (A3/60)
  2 REDEFINE NOTE-CLERK-ID
   3 BENE-MISC-6-POLS  (6)
    4 BENE-STATUS      (A1)     /* (D)
    4 BENE-RESTRICT    (A1)     /* (D)
    4 BENE-MISC-PRIM-SEC (2)
     5 BENE-TYPE        (A1)    /* (D, P/A)
     5 BENE-SHARE-TYP   (A1)
     5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
   3 BENE-TEXTS-6-POLS (6)
    4 BENE-TEXT-PRIM-SEC (2)
     5 BENE-TEXT-OCC    (5)    /* (D, P/A, TEXT-LINE)
      6 BENE-TEXT        (A60)
      6 REDEFINE BENE-TEXT
       7 BENE-TEXT-1ST-25 (A25)
       7 BENE-TEXT-LST-35 (A35)
*
1 #ST-ID             (N6)
1 REDEFINE #ST-ID
 2 #ST-ID-A          (A6)
*
1 #I1                (I2)
1 #I2                (I2)
1 #I3                (I2)
1 #I4                (I2)
1 #CNT-2             (P5)
1 #CNT-3             (P5)
1 #CNT-4             (P5)
1 #RECS-READ         (P5)
*
1 #ST-NOT-FOUND    (L)
1 #POL-FOUND       (L)
1 #NUM-BD      (P5)
1 #NUM-POLS    (P5)
1 #NUM-POL-DEL (P5)
1 #NUM-BD-DEL  (P5)
1 #NUM-DEATH-POLS (P5)
1 #UPDATE      (L)
1 #POL-NOT-FOUND (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'BD Exceptions'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'BD Policies Mismatched'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'BD Number not found - Record Deleted'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'Death BN"s retained - within 13 Months'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ NT-V BY ID-NUMBER FROM 400000
  IF ID-NUMBER GE 600000
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #NUM-BD
  RESET #ST-NOT-FOUND
  MOVE NT-V.ID-NUMBER TO #ST-ID
  SUBTRACT 400000 FROM #ST-ID
  FIND (1) ST-V WITH MEMBER-KEY = #ST-ID-A
    IF NO RECORD FOUND
      MOVE TRUE TO #ST-NOT-FOUND
    END-NOREC
  END-FIND
*
  IF #ST-NOT-FOUND
    ADD 1 TO #NUM-BD-DEL
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-3 NT-V.ID-NUMBER
    DELETE (0104)
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  RESET #UPDATE #POL-NOT-FOUND
  FOR #I1 1 TO 6
    FOR #I2 1 TO 7
      RESET #POL-FOUND
      IF BENE-ISS-DT (#I1, #I2) NE 0
        ADD 1 TO #NUM-POLS
        FOR #I3 1 TO 9
          IF   (BENE-ISS-DT (#I1, #I2) = ST-V.ISS-DT-YYMMDD (#I3))
           AND (STATUS (#I3) = 'D')
            MOVE TRUE TO #POL-FOUND
            ESCAPE BOTTOM
          END-IF
        END-FOR
*
        IF NOT #POL-FOUND
          MOVE TRUE TO #POL-NOT-FOUND
          ADD 1 TO #NUM-POL-DEL
          ADD 1 TO #CNT-2
          DISPLAY (2) #CNT-2 ST-V.ID-NUMBER
             'Desig/NUM' #I1 'SEG/NUM' #I2 NT-V.BENE-ISS-DT (#I1, #I2)
             DATE-OF-DEATH
*
          IF BENE-ISS-DT (6, 1) NE 0 /* ONLY COMPRESS IF ALL 6 FILLED
            MOVE TRUE TO #UPDATE
            IF #I2 LT 7
              FOR #I4 #I2 TO 6
           MOVE BENE-ISS-DT   (#I1, #I4 + 1) TO BENE-ISS-DT   (#I1, #I4)
           MOVE BENE-SPLIT-IND(#I1, #I4 + 1) TO BENE-SPLIT-IND(#I1, #I4)
              END-FOR
            END-IF
            RESET BENE-ISS-DT    (#I1, 7)
            RESET BENE-SPLIT-IND (#I1, 7)
          END-IF
        END-IF
      END-IF
    END-FOR
  END-FOR
*
  IF BENE-ISS-DT (6, 1 ) NE 0
    IF #POL-NOT-FOUND
      WRITE NT-V.ID-NUMBER 'HAS 6 BD RECS - POL NOT FOUND'
    ELSE
      WRITE NT-V.ID-NUMBER 'HAS 6 BD RECS'
    END-IF
  END-IF
*
  IF NOT #UPDATE
   AND ST-V.ID-NUMBER NE 28649    /* <<<<<<<<<<<<<<<<<<<<<,
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  FOR #I1 1 TO 6
    IF (BENE-ISS-DT (#I1, 1) = 0)
      FOR #I2 1 TO 7
        IF BENE-ISS-DT (#I1, #I2) NE 0
          WRITE NT-V.ID-NUMBER #I1 'Pol 1 is zero but 2-7 has iss-dt'
          ESCAPE TOP
        END-IF
      END-FOR
*
      IF  (BENE-TEXT (#I1, 1, 1) NE ' ')
       OR (BENE-TYPE (#I1, 1) NE ' ')
       OR (BENE-STATUS (#I1) NE ' ')
        MOVE TRUE TO #UPDATE
        WRITE NT-V.ID-NUMBER #I1 'seg - has been cleared'
        RESET BENE-ISS-DT      (#I1, *)
        RESET BENE-SPLIT-IND   (#I1, *)
        RESET BENE-LST-UPD     (#I1)
        RESET BENE-STRT-PEND   (#I1)
        RESET BENE-FOL-UP-IND  (#I1)
        RESET BENE-OWN-TRNSF-DT(#I1)
        RESET BENE-SUC-OWN     (#I1)
        RESET BENE-NEW-LAYOUT  (#I1)
        RESET BENE-STATUS      (#I1)
        RESET BENE-RESTRICT    (#I1)
        RESET BENE-TYPE        (#I1, *)
        RESET BENE-SHARE-TYP   (#I1, *)
        RESET BENE-TEXT        (#I1, *, *)
      END-IF
*
      IF (#I1 LT 6) AND (BENE-ISS-DT (#I1 + 1, 1) NE 0)
        WRITE NT-V.ID-NUMBER 'SHIFTING OCC' #I1 'FROM NEXT OCC'
        FOR #I3 #I1 TO 5
          MOVE BENE-ISS-DT      (#I3 + 1, *) TO BENE-ISS-DT    (#I3, *)
          MOVE BENE-SPLIT-IND   (#I3 + 1, *) TO BENE-SPLIT-IND (#I3, *)
          MOVE BENE-LST-UPD     (#I3 + 1)    TO BENE-LST-UPD   (#I3)
          MOVE BENE-STRT-PEND   (#I3 + 1)    TO BENE-STRT-PEND (#I3)
          MOVE BENE-FOL-UP-IND  (#I3 + 1)    TO BENE-FOL-UP-IND (#I3)
          MOVE BENE-OWN-TRNSF-DT(#I3 + 1)    TO BENE-OWN-TRNSF-DT (#I3)
          MOVE BENE-SUC-OWN     (#I3 + 1)    TO BENE-SUC-OWN     (#I3)
          MOVE BENE-NEW-LAYOUT  (#I3 + 1)    TO BENE-NEW-LAYOUT  (#I3)
          MOVE BENE-STATUS      (#I3 + 1)    TO BENE-STATUS      (#I3)
          MOVE BENE-RESTRICT    (#I3 + 1)    TO BENE-RESTRICT    (#I3)
          MOVE BENE-TYPE        (#I3 + 1, *) TO BENE-TYPE      (#I3, *)
          MOVE BENE-SHARE-TYP   (#I3 + 1, *) TO BENE-SHARE-TYP (#I3, *)
          MOVE BENE-TEXT     (#I3 + 1, *, *) TO BENE-TEXT    (#I3, *, *)
        END-FOR
*
        MOVE TRUE TO #UPDATE
        RESET BENE-ISS-DT      (6, *)
        RESET BENE-SPLIT-IND   (6, *)
        RESET BENE-LST-UPD     (6)
        RESET BENE-STRT-PEND   (6)
        RESET BENE-FOL-UP-IND  (6)
        RESET BENE-OWN-TRNSF-DT(6)
        RESET BENE-SUC-OWN     (6)
        RESET BENE-NEW-LAYOUT  (6)
        RESET BENE-STATUS      (6)
        RESET BENE-RESTRICT    (6)
        RESET BENE-TYPE        (6, *)
        RESET BENE-SHARE-TYP   (6, *)
        RESET BENE-TEXT     (6, *, *)
*
        RESET #I1   /* to do the shifted seg again
      END-IF
    END-IF
  END-FOR
*
  IF BENE-ISS-DT (1, 1) = 0
    WRITE ST-V.ID-NUMBER 'No Ben pols - should be deleted'
  END-IF
*
  IF #UPDATE
    UPDATE (0104)
  END-IF
*
  END TRANSACTION
END-READ
*
WRITE (2) 'BD RECS               =' #NUM-BD
WRITE (2) 'BD policies           =' #NUM-POLS
WRITE (2) 'BD pol deleted        =' #NUM-POL-DEL
WRITE (2) 'BD recs deleted       =' #NUM-BD-DEL
WRITE (2) 'BD death-pols retained=' #NUM-DEATH-POLS
*
END
