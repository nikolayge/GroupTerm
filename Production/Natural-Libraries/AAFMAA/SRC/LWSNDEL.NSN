* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNDEL.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 #INSURED-CI  (N8)
1 #AT-NUMBER   (N6)
1 #POLICY-ID   (A16)
1 ErrorMsg     (A) DYNAMIC
LOCAL USING LWSLRQST
LOCAL
1 BLANK           (A1) CONST <' '>
1 #REQUEST        (I4)
1 #DESIGNATION-ID (N8)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 DATE-OF-DEATH
  2 C*ACTIVE-SEGMENTS  (N3)
  2 STATUS    (9)
  2 MODE      (9)
  2 POLICY-ID (9)
1 I   (I4)
END-DEFINE
/* 
DECIDE FOR FIRST CONDITION
  WHEN #POLICY-ID NE BLANK
    PERFORM DELETE-POLICY
  WHEN #INSURED-CI > 0
    FIND ST-V WITH MEMBER-CONTACT-ID = #INSURED-CI
      FOR I 1 TO C*ACTIVE-SEGMENTS
        #POLICY-ID := ST-V.POLICY-ID (I)
        PERFORM DELETE-POLICY
      END-FOR
    END-FIND
  WHEN #AT-NUMBER > 0
    CALLNAT 'LWSNSRV' APPLCTN-DELETE-REQ
      #INSURED-CI
      #POLICY-ID
      #AT-NUMBER
      #DESIGNATION-ID
      ErrorMsg
  WHEN ANY
    IF ErrorMsg = 'Success'
      END TRANSACTION
    ELSE
      BACKOUT TRANSACTION
    END-IF
  WHEN NONE
    ErrorMsg := 'No policy data'
END-DECIDE
/*
DEFINE DELETE-POLICY
/*     -------------
CALLNAT 'LWSNSRV' POROLE-DELETE-REQ
  #INSURED-CI
  #POLICY-ID
  #AT-NUMBER
  #DESIGNATION-ID
  ErrorMsg

CALLNAT 'LWSNSRV' POLICY-DELETE-REQ
  #INSURED-CI
  #POLICY-ID
  #AT-NUMBER
  #DESIGNATION-ID
  ErrorMsg
CALLNAT 'LWSNSRV' POLICY-GET-REQ
  #INSURED-CI
  #POLICY-ID
  #AT-NUMBER
  #DESIGNATION-ID
  ErrorMsg
IF SUBSTR(ErrorMsg,1,4) EQ 'GUID' /* CLEAN UP
    AND *LENGTH(ErrorMsg) GT 36
  CALLNAT 'LWSNSRV' POLICY-DELETE-REQ
    #INSURED-CI
    #POLICY-ID
    #AT-NUMBER
    #DESIGNATION-ID
    ErrorMsg
END-IF
END-SUBROUTINE
/*
END
