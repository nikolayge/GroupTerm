* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ATHPCCST - Sort CC Detail File by CC NUMBER
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* DRW    10102005  DW0  Create program
* DRW    02282006  DW1  Sort by tran code so INPx preceeds PRNx
************************************************************************
*
DEFINE DATA
LOCAL
*
1 #CC-REC     (A100)   /* INPUT
1 REDEFINE #CC-REC
 2 #CC-EDS-INFO
  3 #CC-CN        (A6)
  3 #CC-TRAN-CD   (A4)
  3 #CC-ISS-DT    (N8)
  3 #CC-PLAN      (A3)
 2 #CC-NUM        (A16)
 2 #CC-AMT       (N5.2)
 2 #CC-EXP-DT    (A4)
 2 #CC-REMAIN    (A52)
*
1 #COUNTERS
 2 #RECS-READ        (P5)
 2 #RECS-OUT         (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Sorted Detail Trans'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD #CC-REC  /* read CC Detail record
END-ALL
*
SORT BY #CC-NUM #CC-CN #CC-ISS-DT #CC-TRAN-CD /* tran code added  DW1
  USING #CC-REC  /* 2/28/2006 so INT would preceed PRN to prevent
*            abends by prn clearing laon before int is applied
*            so sort will be DUEP/INPx/PRNx/PREP/RIDP
*
  WRITE WORK 3 #CC-REC
*
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2 'CN'#CC-CN
    'ISS-DT' #CC-ISS-DT
    'PLAN'   #CC-PLAN
    'TR-CD'  #CC-TRAN-CD
    'Amt'    #CC-AMT
    'CC-NUMBER' #CC-NUM
    'EXP-DATE' #CC-EXP-DT
*
END-SORT
*
END
