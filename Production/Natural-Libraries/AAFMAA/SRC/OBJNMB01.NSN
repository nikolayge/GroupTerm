* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram OBJNMB01.
/**
/** :author nguentchev
/*
DEFINE DATA
PARAMETER
1 #REQUEST      (A) DYNAMIC
1 #MSG          (A) DYNAMIC
PARAMETER USING OBJAMB01
LOCAL USING OBJLMB01
LOCAL USING ERROR_L                    /* YK1
LOCAL
1 BLANK           (A1) CONST <' '>
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE 'ADD'    PERFORM ADD-RECORD
  VALUE 'UPDATE' PERFORM UPDATE-RECORD
  VALUE 'DELETE' PERFORM DELETE-RECORD
  VALUE 'GET'    PERFORM GET-RECORD
  VALUE 'GET-BY-CN' PERFORM GET-BY-CN
  VALUE 'GET_BY_AT' PERFORM GET-BY-AT
  VALUE 'EXIST'  PERFORM CHECK-EXIST
  VALUE 'GET_BY_DATE'  PERFORM GET-BY-DATE
  VALUE 'CONTRACT'  PERFORM GET-BY-CONTRACT
  NONE VALUE
    IGNORE
END-DECIDE
*
DEFINE GET-BY-CONTRACT
/*     -----------
FIND SVC-MEMBER-V WITH SVC-CONTRACT-NUMBER = #SVC-MEMBER.SVC-CONTRACT-NUMBER
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'       YK1
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME SVC-MEMBER-V TO #SVC-MEMBER
END-FIND
*
END-SUBROUTINE
/*
DEFINE CHECK-EXIST
/*     -----------
F-NUMBER.
FIND NUMBER SVC-MEMBER-V WITH SVC-PLAN = #SVC-MEMBER.SVC-PLAN
  AND SVC-ID-NUMBER = #SVC-MEMBER.SVC-ID-NUMBER
*
END-SUBROUTINE
/*
DEFINE ADD-RECORD
/*     ----------
PERFORM CHECK-EXIST
IF *NUMBER ( F-NUMBER.) > 0
  #MSG := #DUPLICATE-KEY               /* 'No duplicate key allowed - record exist'   YK1
ELSE
  MOVE BY NAME #SVC-MEMBER TO SVC-MEMBER-V
  SVC-MEMBER-V.LAST-UPD-USER := *USER
  SVC-MEMBER-V.LAST-UPD-DATE := *DATN
  SVC-MEMBER-V.LAST-UPD-TIME := *TIMN
  STORE SVC-MEMBER-V
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
/*     -------------
FIND SVC-MEMBER-V WITH SVC-CONTRACT-NUMBER = #SVC-MEMBER.SVC-CONTRACT-NUMBER
    AND SVC-PLAN = #SVC-MEMBER.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := #NOTHING-UPDATE      /* 'No record exist for update'  /* YK1
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #SVC-MEMBER TO SVC-MEMBER-V
  SVC-MEMBER-V.LAST-UPD-DATE := *DATN
  SVC-MEMBER-V.LAST-UPD-TIME := *TIMN
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE DELETE-RECORD
/*     -------------
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #SVC-MEMBER.SVC-ID-NUMBER
    AND SVC-PLAN = #SVC-MEMBER.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := #NOTHING-DELETE      /* 'No record to delete'     /* YK1
    ESCAPE BOTTOM
  END-NOREC
  IF #SVC-MEMBER.SVC-STATUS NE BLANK
      AND #SVC-MEMBER.SVC-STATUS NE SVC-MEMBER-V.SVC-STATUS
    ESCAPE TOP
  END-IF
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE GET-RECORD
/*     ----------
#MSG := #NOT-EXIST          /* 'No record exist'
FIND SVC-MEMBER-V WITH SVC-PLAN = #SVC-MEMBER.SVC-PLAN
    AND SVC-ID-NUMBER = #SVC-MEMBER.SVC-ID-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  IF #SVC-MEMBER.SVC-STATUS NE BLANK
      AND #SVC-MEMBER.SVC-STATUS NE SVC-MEMBER-V.SVC-STATUS
    ESCAPE TOP
  END-IF
  RESET #MSG
  MOVE BY NAME SVC-MEMBER-V TO #SVC-MEMBER
  ESCAPE BOTTOM
END-FIND
END-SUBROUTINE
/**
DEFINE GET-BY-AT
/*     ----------
#MSG := #NOT-EXIST
FIND SVC-MEMBER-V WITH SVC-AT-NUMBER = #SVC-MEMBER.SVC-AT-NUMBER
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'       YK1
    ESCAPE BOTTOM
  END-NOREC
  IF #SVC-MEMBER.SVC-STATUS NE BLANK
      AND #SVC-MEMBER.SVC-STATUS NE SVC-MEMBER-V.SVC-STATUS
    ESCAPE TOP
  END-IF
  RESET #MSG
  MOVE BY NAME SVC-MEMBER-V TO #SVC-MEMBER
  ESCAPE BOTTOM
END-FIND
END-SUBROUTINE
/**
DEFINE GET-BY-CN                                              /* YK1 START
#MSG := #NOT-EXIST
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #SVC-MEMBER.SVC-ID-NUMBER
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST          /* 'No record exist'          /* YK1
    ESCAPE BOTTOM
  END-NOREC
  IF #SVC-MEMBER.SVC-STATUS NE BLANK
      AND #SVC-MEMBER.SVC-STATUS NE SVC-MEMBER-V.SVC-STATUS
    ESCAPE TOP
  END-IF
  RESET #MSG
  MOVE BY NAME SVC-MEMBER-V TO #SVC-MEMBER
  ESCAPE BOTTOM
END-FIND
END-SUBROUTINE
/**
DEFINE GET-BY-DATE
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #SVC-MEMBER.SVC-ID-NUMBER
  IF NO RECORDS FOUND
    #MSG := #NOT-EXIST
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF SVC-MEMBER-V.SVC-ISSUE-DATE = #SVC-MEMBER.SVC-ISSUE-DATE
  MOVE BY NAME SVC-MEMBER-V TO #SVC-MEMBER
END-FIND
END-SUBROUTINE                                                /* YK1 END
END
