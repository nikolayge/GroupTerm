* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPSRTT - Sort TRANs (Accounting) RECORDS
*
DEFINE DATA
LOCAL
*
1 ACCT-FILE    (A204)
1 REDEFINE ACCT-FILE
 2 TIN1-SECT      (A1)
 2 TIN1-FILL-1    (A3)
 2 TIN1-AR-ID     (A9)
 2 REDEFINE TIN1-AR-ID
  3 TIN1-AR-ID-CN-LBL  (A2)
  3 TIN1-AR-ID-CN  (A6)
  3 TIN1-AR-ID-FILL(A1)
 2 TIN1-DATE      (A6)
 2 TIN1-TRAN      (A4)
 2 TIN1-COL       (A1)
 2 TIN1-AR-NAME   (A25)
 2 TIN1-FILL-2    (A1)
 2 TIN1-AR-CERT   (A5)
 2 TIN1-ACCTNO    (A3)
 2 TIN1-LTRANDT   (A6)
 2 TIN1-DEBIT     (N7.2)
 2 TIN1-CREDIT    (N7.2)
 2 TIN1-REF       (A10)
 2 TIN1-DESC      (A20)
 2 TIN1-MSG       (A1)
 2 TIN1-PROCESS-DATE (A6)
 2 TIN1-FILL-3    (A4)
 2 TIN1-AALP-CD   (A1)
 2 TIN1-CLERK     (A4)
 2 TIN1-TRDATE    (A6)
 2 TIN1-TRTIME    (A6)
 2 TIN1-TERMINAL  (A4)
*
1 #RECS-IN    (P7)
1 #RECS-OUT   (P7)
1 #TOT-EZ-ALP-PRIN  (P9.2)
1 #TOT-EZ-ALP-INT   (P9.2)
1 #NUM-ELP3     (P7)
1 #NUM-ELP4     (P7)
1 #NUM-ELP-PRIN (P7)
1 #NUM-ELP-INT  (P7)
1 #TOT-EZ-PREMS-DUES-RIDER (P9.2)
1 #TOT-EZ-PAY   (P9.2)
*
1 #TOT-ALLOT-PRIN  (P9.2)
1 #TOT-ALLOT-INT   (P9.2)
1 #NUM-ALP1     (P7)
1 #NUM-ALP2     (P7)
1 #NUM-ALP-PRIN (P7)
1 #NUM-ALP-INT  (P7)
*
1 #EZ-DUES-SO   (P9.2)
1 #EZ-DUES      (P9.2)
1 #EZ-PREMS     (P9.2)
*
1 #ONLINE-IND   (A1)
1 #TOT-OL-LN-DISBURSED   (P9.2)
1 #TOT-OL-LN-REPAID      (P9.2)
1 #TOT-OL-LN-DISBURSED-FIXED   (P9.2)
1 #TOT-OL-LN-REPAID-FIXED      (P9.2)
1 #CNT-2    (P5)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'Accounting trans SORT'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT  (2) LS=133 PS=76
WRITE   (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
            'LOANS DISBURSED total debits'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD ACCT-FILE
  IF TIN1-SECT NE 'B'
    ESCAPE TOP
  END-IF
*
  RESET #ONLINE-IND
* IF  (TIN1-SECT NE 'B')
  IF  (TIN1-MSG  NE 'A')
   OR (NOT TIN1-ACCTNO = '110' OR = '410' OR = '801' OR = '420'
     OR = '115' OR = '430' OR = '440' OR = '445' OR = '450' OR = '999'
     OR = '190' OR = '422' OR = '432' OR = '434' OR = '436')
    MOVE 'Y' TO #ONLINE-IND
  ELSE
    IF  (TIN1-TRAN = 'EZPR' OR = 'EZDU')  /* EZ-PAY
     OR (TIN1-AALP-CD = '3' OR = '4')       /* totals
*
     OR (TIN1-TRAN = 'ALRD' OR = 'ALDU' OR = 'ALDC')  /* ALLOTMENT
     OR (TIN1-AALP-CD = '1' OR = '2')                 /* totals
      IGNORE
    ELSE
      MOVE 'Y' TO #ONLINE-IND
    END-IF
  END-IF
*
  ADD 1 TO #RECS-IN
END-ALL
*
SORT BY TIN1-AR-ID-CN
  USING ACCT-FILE #ONLINE-IND
*
  IF #ONLINE-IND = 'Y'
    WRITE WORK 5 ACCT-FILE    /* ONLINE TANSACTIONS
*
    IF   (TIN1-TRAN = 'NEWL' OR = 'ADDL')
     OR  (TIN1-TRAN = MASK ('PAD'))
     OR  (TIN1-TRAN = MASK ('CAD'))
*    OR  (TIN1-TRAN = 'UPDI' OR = 'CVIN')
      IF  (TIN1-DESC = MASK ('CAP MBR'))
       OR (TIN1-DESC = MASK ('IRT 01500'))
        ADD TIN1-DEBIT  TO #TOT-OL-LN-DISBURSED-FIXED
        ADD TIN1-CREDIT TO #TOT-OL-LN-REPAID-FIXED
      ELSE
        ADD TIN1-DEBIT  TO #TOT-OL-LN-DISBURSED
        ADD TIN1-CREDIT TO #TOT-OL-LN-REPAID
      END-IF
*
      ADD 1 TO #CNT-2
      DISPLAY (2) #CNT-2 TIN1-TRAN TIN1-AR-ID-CN
       'COL' TIN1-COL
       'DEBIT'   TIN1-DEBIT  (EM=ZZZ,ZZZ.99-)
       'CREDIT'  TIN1-CREDIT (EM=ZZZ,ZZZ.99-)
       'DESC'    TIN1-DESC
    END-IF
    ESCAPE TOP
  END-IF
*
  IF  (TIN1-TRAN = 'EZPR' OR = 'EZDU')
   OR (TIN1-AALP-CD = '3' OR = '4')
    WRITE WORK 3 ACCT-FILE
  ELSE              /* must be allotment recs
    WRITE WORK 4 ACCT-FILE
  END-IF
  ADD 1 TO #RECS-OUT
*
  IF TIN1-AALP-CD = '1' OR = '2' OR = '3' OR = '4'
    IF TIN1-DEBIT NE 0
      WRITE TIN1-AR-ID-CN 'DEBIT=' TIN1-DEBIT
    END-IF
*
    IF TIN1-CREDIT LT 0
      WRITE TIN1-AR-ID-CN 'credit=' TIN1-CREDIT
    END-IF
  END-IF
*     EZ-PAY PROCESSING
  IF TIN1-AALP-CD = '3'
    ADD 1 TO #NUM-ELP3
    ADD 1 TO #NUM-ELP-PRIN
    ADD TIN1-CREDIT TO #TOT-EZ-ALP-PRIN
    WRITE 'ACCUM EZP-PRIN' TIN1-AR-ID TIN1-CREDIT #TOT-EZ-ALP-PRIN
  END-IF
*
  IF TIN1-AALP-CD = '4'
    ADD 1 TO #NUM-ELP4
    IF TIN1-TRAN = MASK ('PRN')
     AND (NOT TIN1-TRAN = 'PRNP' OR = 'PRNF' OR = 'PRNV')
      WRITE TIN1-AR-ID 'not PRNP/PRNF/PRNV' TIN1-TRAN
    END-IF
*
    IF TIN1-TRAN = 'PRNP' OR = 'PRNF' OR = 'PRNV'
      ADD 1 TO #NUM-ELP-PRIN
      ADD TIN1-CREDIT TO #TOT-EZ-ALP-PRIN
      WRITE 'ACCUM EZP-PRIN' TIN1-AR-ID TIN1-CREDIT #TOT-EZ-ALP-PRIN
    ELSE
      ADD 1 TO #NUM-ELP-INT
      ADD TIN1-CREDIT TO #TOT-EZ-ALP-INT
      WRITE 'ACCUM EZP-int' TIN1-AR-ID TIN1-CREDIT #TOT-EZ-ALP-INT
    END-IF
  END-IF
*
  IF TIN1-TRAN = 'EZDU'
    MOVE TIN1-CREDIT TO #EZ-DUES-SO
    MOVE TIN1-DEBIT  TO #EZ-DUES
    WRITE '*'
    WRITE 'EZ-DUES-so=' TIN1-CREDIT (EM=Z,ZZZ,ZZZ.99)
    WRITE 'EZ-DUES   =' TIN1-DEBIT  (EM=Z,ZZZ,ZZZ.99)
  END-IF
*
  IF TIN1-TRAN = 'EZPR'
    WRITE 'EZ-RIDER   =' TIN1-DEBIT  (EM=Z,ZZZ,ZZZ.99)
    COMPUTE #EZ-PREMS = TIN1-CREDIT -TIN1-DEBIT -#EZ-DUES-SO -#EZ-DUES
    WRITE 'EZ-PREMS   =' #EZ-PREMS   (EM=Z,ZZZ,ZZZ.99)
    WRITE 'EZPR-REC   =' TIN1-CREDIT (EM=Z,ZZZ,ZZZ.99)
        'includes prems/rid/dues'
    MOVE TIN1-CREDIT TO #TOT-EZ-PREMS-DUES-RIDER
  END-IF
*     ALLOTMENT PROCESSING
  IF TIN1-AALP-CD = '1'
    ADD 1 TO #NUM-ALP1
    ADD 1 TO #NUM-ALP-PRIN
    ADD TIN1-CREDIT TO #TOT-ALLOT-PRIN
*   WRITE 'ACCUM ALOT-PRIN' TIN1-AR-ID TIN1-CREDIT #TOT-ALLOT-PRIN
*           TIN1-AALP-CD
  END-IF
*
  IF TIN1-AALP-CD = '2'
    ADD 1 TO #NUM-ALP2
    IF TIN1-TRAN = MASK ('PRN')
     AND (NOT TIN1-TRAN = 'PRNP' OR = 'PRNF' OR = 'PRNV')
      WRITE TIN1-AR-ID 'not PRNP/PRNF/PRNV' TIN1-TRAN
    END-IF
*
    IF TIN1-TRAN = 'PRNP' OR = 'PRNF' OR = 'PRNV'
      ADD 1 TO #NUM-ALP-PRIN
      ADD TIN1-CREDIT TO #TOT-ALLOT-PRIN
*     WRITE 'ACCUM ALOT-PRIN' TIN1-AR-ID TIN1-CREDIT #TOT-ALLOT-PRIN
*           TIN1-AALP-CD
    ELSE
      ADD 1 TO #NUM-ALP-INT
      ADD TIN1-CREDIT TO #TOT-ALLOT-INT
*     WRITE 'ACCUM ALOT-INT' TIN1-AR-ID TIN1-CREDIT #TOT-ALLOT-int
    END-IF
  END-IF
*
  IF TIN1-TRAN = 'ALRD'
    WRITE 'ALLOT-PREMS =' TIN1-CREDIT  (EM=Z,ZZZ,ZZZ.99)
    WRITE 'ALOT-RIDER  =' TIN1-DEBIT   (EM=Z,ZZZ,ZZZ.99)
  END-IF
*
  IF TIN1-TRAN = 'ALDU'
    WRITE '*'
    WRITE 'ALOT-DUES-so=' TIN1-CREDIT  (EM=Z,ZZZ,ZZZ.99)
    WRITE 'ALLOT-DUES  ='   TIN1-DEBIT (EM=Z,ZZZ,ZZZ.99)
  END-IF
*
  IF TIN1-TRAN = 'ALDC'
    WRITE '*'
    WRITE 'ALOT-Underages =' TIN1-CREDIT  (EM=Z,ZZZ,ZZZ.99)
    WRITE 'ALOT-Overages  =' TIN1-DEBIT  (EM=Z,ZZZ,ZZZ.99)
  END-IF
END-SORT
*
WRITE '*'
WRITE 'RECS in =' #RECS-IN  (EM=ZZZ,ZZ9)
WRITE 'RECS out=' #RECS-OUT (EM=ZZZ,ZZ9)
WRITE '*'
WRITE 'ELP-prin   =' #TOT-EZ-ALP-PRIN (EM=Z,ZZZ,ZZZ.99)
WRITE 'ELP-INT    =' #TOT-EZ-ALP-INT  (EM=Z,ZZZ,ZZZ.99)
COMPUTE #TOT-EZ-PAY = #TOT-EZ-PREMS-DUES-RIDER
     + #TOT-EZ-ALP-PRIN + #TOT-EZ-ALP-INT
WRITE 'TOT-EZ-PAY =' #TOT-EZ-PAY      (EM=Z,ZZZ,ZZZ.99)
WRITE 'NUM ELP3   =' #NUM-ELP3     (EM=ZZZ,ZZ9)
WRITE 'NUM ELP4   =' #NUM-ELP4     (EM=ZZZ,ZZ9)
WRITE 'NUM ELP-prn=' #NUM-ELP-PRIN (EM=ZZZ,ZZ9)
WRITE 'NUM ELP-int=' #NUM-ELP-INT
WRITE '*'
*
WRITE 'ALP-prin   =' #TOT-ALLOT-PRIN (EM=Z,ZZZ,ZZZ.99)
WRITE 'ALP-int    =' #TOT-ALLOT-INT  (EM=Z,ZZZ,ZZZ.99)
WRITE 'NUM ALP1   =' #NUM-ALP1      (EM=ZZZ,ZZ9)
WRITE 'NUM ALP2   =' #NUM-ALP2      (EM=ZZZ,ZZ9)
WRITE 'NUM ALP-prn=' #NUM-ALP-PRIN  (EM=ZZZ,ZZ9)
WRITE 'NUM ALP-int=' #NUM-ALP-INT    (EM=ZZZ,ZZ9)
*
WRITE (2) 'TOTAL OL ln-disbursed fixed' #TOT-OL-LN-DISBURSED-FIXED
WRITE (2) 'TOTAL OL ln-repaid    fixed' #TOT-OL-LN-REPAID-FIXED
WRITE (2) 'TOTAL OL ln-disbursed      ' #TOT-OL-LN-DISBURSED
WRITE (2) 'TOTAL OL ln-repaid         ' #TOT-OL-LN-REPAID
*
END
