* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPANNT - Makes ATHPANNS output into a TAB delimited file 
************************************************************************ 
*                       MODIFICATION LOG                               * 
* USER   DATE      TAG     REASON                                      * 
* DRW    09242006  xxx     created 
* SAG-AB 20061022    Replace EBCDIC Hex '05' with Hex '09' for ASCII 
* DRW    10312006  DW1     Add Rp Orig-Iss-Age 
* DRW  11302006 DW2  Add SVS/LVS/OO-CREDIT (SVC-FEE Breakout) to output 
* vxt  10222013 vt1 Changes for 2013 
************************************************************************ 
* 
DEFINE DATA 
* GLOBAL USING BCOMMGXX WITH MASTER-BLOCK 
* LOCAL USING G3500AAD 
LOCAL 
**1 #TAB       (A1) INIT <H'05'>   /* SAG-AB 20061022 
1 #TAB       (A1) INIT <H'09'>     /* SAG-AB 20061022 
1 #AMT-7-2 (N7.2) 
1 #AMT-12 (A12) 
* 
1 #NUM-RECS-OUT    (P7) 
1 #DATA-OUTPUT (A250)                     /* vt2 was a253 
1 #DATA-INFO    /* length  3 + 6 + 8 + 1 + (8 * 6) + (9 * 4) + 14 = 116 
  2 #DATA-POL-TYPE     (A3)  /*                48        36 
  2 #DATA-CN           (N6) 
  2 #DATA-ISS-DT       (N8)    /*   below added 12/2002 
  2 #DATA-SPL-IND      (N1)    /*    + (9 * 6)   + 13            = 183 
*  #DATA-P-I excludes cap fixed p-i 
  2 #DATA-P-I          (N6.2)  /*   below added 12/2004 
  2 #DATA-TPP          (N6.2)  /*    + 5 + 8 + 8  = 21           = 204 
  2 #DATA-YTD-PREMS    (N6.2)  /*   below added 11/2004 
  2 #DATA-YTD-COI      (N6.2)  /*    + 3 + 3 + 7 = 13            = 217 
  2 #DATA-YTD-EQU      (N7.2)  /*   below added 10/2005                     /* vt1 was n6.2 
  2 #DATA-YTD-INT      (N6.2)  /*    + 8 + 5 + 3 = 16            = 233 
  2 #DATA-BEG-CSV      (N7.2)  /*   below added 12/2005 
  2 #DATA-CURR-CSV     (N7.2)  /*    + 8         = 8             = 241 
  2 #DATA-BEG-DB       (N7.2)  /* Used for STM Info but NOT in ATHPANNP 
  2 #DATA-CURR-DB      (N7.2) /*    + 2         = 2             = 243 
  2 #DATA-CURR-RPU     (A1)   /* rpu'd last year 
  2 #DATA-CSV-DIFF     (N7.2) /*   below added 12/2006                     /* vt1 was n6.2 
  2 #DATA-PLAN         (A3) /*   5  + 5  + 5  = 15            = 258 
  2 #DATA-PAY-STATUS   (A2) 
*   added 12/2002 
  2 #DATA-DB-GROWTH    (N7.2) 
  2 #DATA-CSV-GROWTH   (N7.2) 
  2 #DATA-PRIOR-YE-CSV (N7.2) 
  2 #DATA-BEG-OF-YR-DB (N7.2)  /* Beg of year DB 
  2 #DATA-AVL-CSV      (N7.2) 
  2 #DATA-AVL-DB       (N7.2) 
  2 #DATA-TPP-EXC-CSV  (a1)                 /* VT1   WAS L 
  2 #DATA-RPU-IND      (A3) 
  2 #DATA-POC-IND      (A3) 
  2 #DATA-UPP-IND      (A3) 
  2 #DATA-NIO-IND      (A3) 
*   added 11/2004                    3 + 3 + 7 = 10 
  2 #DATA-MEC-IND      (A3) 
  2 #DATA-CAP-IND      (A3) 
  2 #DATA-ANN-PREM     (N5.2) 
*   added 12/2004                    5 + 8 + 8 = 21 
  2 #DATA-SVC-FEE      (N3.2) 
  2 #DATA-COI          (N6.2) 
  2 #DATA-ADMIN        (N6.2) 
*   added 10/2005                    8 + 5 + 3 = 16 
  2 #DATA-ACQ-COST     (N6.2)   /* DW8 
  2 #DATA-DUES         (N3.2)   /* DW8 
  2 #DATA-OPT-IND      (A3)     /* DW8 
*   added 12/2005                    8 = 8 
  2 #DATA-CAP-P-I      (N6.2) /* Only CAP Fixed P-I * DW11 
*   added 12/2005                    8 = 8 
  2 #DATA-RT-ORIG-ISS-AGE (N2) /*     2 + 2     /* DW1 
*   added 11/2006                   5 + 5 + 5 = 15 
  2 #DATA-LVS             (N3.2)                  /*  DW2 
  2 #DATA-SAS             (N3.2)                  /*  DW2 
  2 #DATA-OO-CR           (N3.2)                  /*  DW2 
  2 #DATA-OLD-NEW-NEXT    (A10)                                       /*  vt1 
  2 #DATA-PUP-DT          (N8)                                        /*  vt1 
* 
1 #COUNTERS 
  2 #RECS-READ        (P7) 
  2 #RECS-WRITTEN     (P5) 
  2 #CNT              (P5) 
  2 #CNT-2            (P5) 
  2 #CNT-3            (P5) 
  2 #CNT-4            (P5) 
  2 #I1               (I2) 
  2 #I2               (I2) 
* 
1 #YYYYMMDD        (N8) 
1 REDEFINE #YYYYMMDD 
  2 #CENT-A        (A2) 
  2 #YY-A          (A2) 
  2 #MM-A          (A2) 
  2 #DD-A          (A2) 
1 REDEFINE #YYYYMMDD 
  2 #YYYYMM        (N6) 
  2 REDEFINE #YYYYMM 
    3 #YYYY         (N4) 
    3 #MM           (N2) 
  2 #DD            (N2) 
1 REDEFINE #YYYYMMDD 
  2 #YYYYMMDD-A    (A6) 
* 
END-DEFINE 
* DEFINE WORK FILE 2 '/usr/SAG/AAFMAA/batch/workfiles/PERMDA/ATH/ANNSDATA.TEST.TXT' TYPE 'ASCII' 
* DEFINE WORK FILE 3 '/usr/SAG/AAFMAA/batch/workfiles/PERMDA/ATH/ANNSDATT.TEST.TXT' TYPE 'ASCII' 
* * * * * * * REPORT HEADER * * * * * * 
FORMAT  LS=133 PS=76 
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 
  'V/A Annual Statement Create Delimited File Exceptions' 
  5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX 
* 
COMPRESS 'TYPE' 
  #TAB 'CN' 
  #TAB 'ISS' 
  #TAB 'SP-IND' 
  #TAB 'PI' 
  #TAB 'TPP' 
  #TAB 'YTD-PRM' 
  #TAB 'YTD-COI' 
  #TAB 'YTD-EQU' 
  #TAB 'YTD-INT' 
  #TAB 'BG-CSV' 
  #TAB 'CR-CSV' 
  #TAB 'BG-DB' 
  #TAB 'CR-DB' 
  #TAB 'CR-RPU' 
  #TAB 'CSV-DIF' 
  #TAB 'PLAN' 
  #TAB 'PAY-STAT' 
  #TAB 'DB-GR' 
  #TAB 'CSV-GR' 
  #TAB 'P-YE-CSV' 
  #TAB 'BOY-DB' 
  #TAB 'AVL-CSV' 
  #TAB 'AVL-DB' 
   #TAB 'TPP-EXC-CSV'   /* cannot compress logicals   vt 
  #TAB 'RPU' 
  #TAB 'POC' 
  #TAB 'UPP' 
  #TAB 'NIO' 
  #TAB 'MEC' 
  #TAB 'CAP' 
  #TAB 'AN-PR' 
  #TAB 'SV-FE' 
  #TAB 'COI' 
  #TAB 'ADMIN' 
  #TAB 'ACQ' 
  #TAB 'DUES' 
  #TAB 'OPT' 
  #TAB 'CAP-P-I' 
  #TAB 'RP-IsAg' 
  #TAB 'LVS'       /* DW2 
  #TAB 'SAS'       /* DW2 
  #TAB 'OO-CR'     /* DW2 
  #tab 'old-new-next'                                /* vt2 
  #tab 'paid-up-date'                                /* vt2 
  INTO #DATA-OUTPUT LEAVING NO 
WRITE WORK 3 #DATA-OUTPUT 
* 
READ WORK 2 RECORD #DATA-INFO 
  COMPRESS #DATA-POL-TYPE 
    #TAB #DATA-CN 
    #TAB #DATA-ISS-DT 
    #TAB #DATA-SPL-IND 
    INTO #DATA-OUTPUT LEAVING NO 
* 
  MOVE #DATA-P-I       TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-TPP       TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-YTD-PREMS TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-YTD-COI   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-YTD-EQU   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-YTD-INT   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-BEG-CSV   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-CURR-CSV  TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-BEG-DB    TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-CURR-DB   TO #AMT-7-2 PERFORM LOAD-AMT 
* 
  COMPRESS #DATA-OUTPUT 
    #TAB #DATA-CURR-RPU 
    INTO #DATA-OUTPUT LEAVING NO 
* 
  MOVE #DATA-CSV-DIFF TO #AMT-7-2 PERFORM LOAD-AMT 
* 
  COMPRESS #DATA-OUTPUT 
    #TAB #DATA-PLAN 
    #TAB #DATA-PAY-STATUS 
    INTO #DATA-OUTPUT LEAVING NO 
* 
  MOVE #DATA-DB-GROWTH    TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-CSV-GROWTH   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-PRIOR-YE-CSV TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-BEG-OF-YR-DB TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-AVL-CSV      TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-AVL-DB       TO #AMT-7-2 PERFORM LOAD-AMT 
  COMPRESS #DATA-OUTPUT 
*    #TAB #DATA-TPP-EXC-CSV     /* cannot compress logicals 
    #TAB #DATA-RPU-IND 
    #TAB #DATA-POC-IND 
    #TAB #DATA-UPP-IND 
    #TAB #DATA-NIO-IND 
    #TAB #DATA-MEC-IND 
    #TAB #DATA-CAP-IND 
    INTO #DATA-OUTPUT LEAVING NO 
  MOVE #DATA-ANN-PREM  TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-SVC-FEE   TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-COI       TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-ADMIN     TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-ACQ-COST  TO #AMT-7-2 PERFORM LOAD-AMT 
  MOVE #DATA-DUES      TO #AMT-7-2 PERFORM LOAD-AMT 
* 
  COMPRESS #DATA-OUTPUT 
    #TAB #DATA-OPT-IND 
    INTO #DATA-OUTPUT LEAVING NO 
  MOVE #DATA-CAP-P-I   TO #AMT-7-2 PERFORM LOAD-AMT 
* 
  COMPRESS #DATA-OUTPUT          /* DW1 
    #TAB #DATA-RT-ORIG-ISS-AGE 
    INTO #DATA-OUTPUT LEAVING NO 
  MOVE #DATA-LVS  TO #AMT-7-2 PERFORM LOAD-AMT   /* DW2 
  MOVE #DATA-SAS  TO #AMT-7-2 PERFORM LOAD-AMT   /* DW2 
  MOVE #DATA-OO-CR TO #AMT-7-2 PERFORM LOAD-AMT   /* DW2 
* 
  compress #DATA-OUTPUT  #tab                             /* vt1 
    #data-old-new-next #tab 
    #data-pup-dt #tab 
    into #DATA-OUTPUT  leaving no                          /* vt1 
  WRITE WORK 3 #DATA-OUTPUT 
* 
  ADD 1 TO #NUM-RECS-OUT 
END-WORK 
* 
WRITE '#num-recs-oyt=' #NUM-RECS-OUT 
* 
* * * * * * * * * * * * * ** 
DEFINE SUBROUTINE LOAD-AMT 
* * * * * * * * * * * * * ** 
* 
MOVE EDITED #AMT-7-2 (EM=-ZZZZZZZ.99) TO #AMT-12 
MOVE LEFT #AMT-12 TO #AMT-12 
COMPRESS #DATA-OUTPUT #TAB #AMT-12 INTO #DATA-OUTPUT LEAVING NO 
* 
END-SUBROUTINE 
* 
END 
