* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP03P1
*
*  THIS PROGRAM RUNS IN BATCH MODE ONLY AND IS USED
*  TO EXTRACT AND SORT INFO FROM THE CURRENT MONTHS
*  TRANSACTION-REGISTER
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* SAG    11282005  RH1  SS - Modified the custom coded ON ERROR stmt & *
*                       replaced it with copycode GABNDCX4             *
************************************************************************
*
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 #WORK-FIELDS
  2 #ABEND-CODE       (I4) INIT<4095>
  2 #IN-CNT           (P7)
  2 #OUT-CNT          (P7)
  2 #CNT              (P5)
  2 #CNT-NET          (P5)
  2 #LAST-ID          (P6)
*
  2 #TIME-P7          (P7)
  2 REDEFINE #TIME-P7
    3 #TIME-A3        (A3)
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER        (N6)
  2 NAME             (A25)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (N9)
  2 REDEFINE TRANSACTION-DATN
    3 TD-FILL        (N1)
    3 TD-YYYY        (N4)
    3 TD-MM          (N2)
    3 TD-DD          (N2)
  2 TRANSACTION-TIMN (N7)
  2 REDEFINE TRANSACTION-TIMN
    3 TRANSACTION-TIMN-A6 (A6)
  2 C*CHANGED-DATA-TABLE
  2 OLD-DATA         (A125/1)
  2 NEW-DATA         (A125/1)
1 #TRX               (I2)
1 #DS                (A2)
*
1 #DATN              (N8)
1 REDEFINE #DATN
  2 #YYYYMMDD        (A8)
  2 REDEFINE #YYYYMMDD
    3 #YYYY        (A4)
    3 #MM          (A2)
    3 #DD          (A2)
1 REDEFINE #DATN
  2 #DATN-YYYYMM   (N6)
  2 #DATN-DAY      (N2)
*
1 #TIMN            (N7)
1 REDEFINE #TIMN
  2 #TIMN-A4       (A4)
*
1 #BATCH-PARM         (A10)
1 REDEFINE #BATCH-PARM
 2 #BATCH-PARM-MM     (N2)
 2 #FILL-1            (A1)
 2 #BATCH-PARM-DD     (N2)
 2 #FILL-2            (A1)
 2 #BATCH-PARM-YYYY   (N4)
*
1 #CUTOFF-YYYYMMDD-G
 2 #CUTOFF-YYYY        (N4)
 2 #CUTOFF-MM          (N2)
 2 #CUTOFF-DD          (N2) INIT <01>
1 REDEFINE #CUTOFF-YYYYMMDD-G
 2 #CUTOFF-YYYYMMDD     (N8)
*
1 #LAST-CLERK (A3)
1 #CNT-DS    (P5)
1 #CLK       (A3/60)
1 #TRNS      (P5/60)
1 #TRNS-NET  (P5/60)
1 #DSS       (P5/60)
1 #ITR       (I2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR              /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'C U R R E N T  R C D' TRLOG.ID-NUMBER
*             / '                    ' TRLOG.TRANSACTION-DATN
*             / '                    ' TRLOG.TRANSACTION-TIMN
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
INCLUDE GABNDCX4                                              /* SAG RH1 11282005 SS - END
END-ERROR
*
READ WORK 2 ONCE RECORD #BATCH-PARM  /* DATE (10/01/1999)
*
MOVE #BATCH-PARM-YYYY TO #CUTOFF-YYYY   /* TAKE ATHP05P0 DATE PARM
*                                     /* AND DEVELOP  THE 1ST DAY OF
                                      /* THE PREVIOUS MONTH
MOVE #BATCH-PARM-MM TO #CUTOFF-MM
*
SUBTRACT 1 FROM #CUTOFF-MM
*
IF #CUTOFF-MM LT 01
  MOVE 12 TO #CUTOFF-MM
  SUBTRACT 1 FROM #CUTOFF-YYYY
END-IF
*
FORMAT LS=133
PS=66
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
FORMAT (2) LS=133 PS=66
WRITE (2)TITLE LEFT 'PROGRAM:' *PROGRAM 5X 'TRANSACTIONS BY USER'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
READ TRLOG PHYSICAL
  ADD 1 TO #IN-CNT
*
  IF  (TRLOG.CLERK-ID = 'ANK') AND (TRLOG.ID-NUMBER = 71821)
   OR (TRLOG.CLERK-ID = 'BJS') AND (TRLOG.ID-NUMBER = 43549)
   OR (TRLOG.CLERK-ID = 'JJF') AND (TRLOG.ID-NUMBER = 40486)
   OR (TRLOG.CLERK-ID = 'WEM') AND (TRLOG.ID-NUMBER = 75427)
   OR (TRLOG.CLERK-ID = 'WJW') AND (TRLOG.ID-NUMBER = 39386)
   OR (TRLOG.CLERK-ID = 'RRK') AND (TRLOG.ID-NUMBER = 64971)
   OR (TRLOG.CLERK-ID = 'WRL') AND (TRLOG.ID-NUMBER = 87971)
   OR (TRLOG.CLERK-ID = 'RCC') AND (TRLOG.ID-NUMBER = 69313)
    ESCAPE TOP
  END-IF
*
  IF   (TD-MM = #CUTOFF-MM)
   AND (TD-YYYY = #CUTOFF-YYYY)
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
*
  IF OLD-DATA (1) = 'DOCUMENT SIGNOUT'
    MOVE 'DS' TO #DS
  ELSE
    RESET #DS
  END-IF
END-ALL
*
SORT BY CLERK-ID TRANSACTION-DATN TRANSACTION-TIMN
USING ID-NUMBER
      NAME
      #DS
  ADD 1 TO #CNT
  ADD 1 TO #IN-CNT
*
  IF ID-NUMBER NE #LAST-ID
    ADD 1 TO #CNT-NET
    MOVE ID-NUMBER TO #LAST-ID
  END-IF
*
  IF #DS = 'DS'
    ADD 1 TO #CNT-DS
  END-IF
*
  MOVE TRANSACTION-TIMN TO #TIMN
  DISPLAY (2) NOTITLE GIVE SYSTEM FUNCTIONS
         #CNT
         'UPDATE/CNS'    #CNT-NET
         'USER'          CLERK-ID
         'CN/NUMBER'     ID-NUMBER
         'NAME'          NAME
         'DATE'          TRANSACTION-DATN
         'TIME'          #TIMN-A4
         'DOC/SIGNOUT'   #DS
*
  AT BREAK CLERK-ID
    ADD 1 TO #ITR
    MOVE #LAST-CLERK TO #CLK  (#ITR)
    MOVE #CNT        TO #TRNS (#ITR)
    MOVE #CNT-NET    TO #TRNS-NET (#ITR)
    MOVE #CNT-DS     TO #DSS  (#ITR)
*
    RESET #CNT #CNT-NET
    RESET #CNT-DS
    NEWPAGE (2)
  END-BREAK
*
  MOVE CLERK-ID TO #LAST-CLERK
END-SORT
* * * * * * *   E O F   * * * * * * * * * * * * *
NEWPAGE (2)
WRITE (2) 'TRANSACTION-REGISTER READ' #IN-CNT
WRITE (2)  ' '
WRITE (2)  'USER-INIT  UPDATE-TRANS    UPDATE-CNS   DS-TRANS'
*
FOR #ITR 1 TO 60
  WRITE (2) '  ' #CLK (#ITR) '    ' #TRNS (#ITR) '     '
                              #TRNS-NET (#ITR) '      ' #DSS (#ITR)
END-FOR
*
END
