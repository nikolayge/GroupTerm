* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM-ID: SVCN0001 - Determine AAFMAA services information         *
*                        (Re-grandfather)                              *
************************************************************************
DEFINE DATA
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM  07/19/2010  None    Program created                             *
* NGG  12/16/2011  NG1     Changes for new DDM A-SVC-MEMBER            *
* PAM  12/30/2011  PM1     Added information for use by VW             *
************************************************************************
*
PARAMETER USING SVCA0001
*
LOCAL USING OBJLMB01    /* NG1
LOCAL USING OBJLSTAT
*
LOCAL
1 #I1            (I2)
1 #I2            (I2)
1 #TERM          (L)
1 #YYYYMMDD      (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM       (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY        (N4)
    3 #MM          (N2)
  2 #DD           (N2)
1 #PAID-YYYYMMDD (N8)
1 REDEFINE #PAID-YYYYMMDD
  2 #PAID-YYYYMM  (N6)
  2 REDEFINE #PAID-YYYYMM
    3 #PAID-YYYY   (N4)
    3 #PAID-MM     (N2)
  2 #PAID-DD      (N2)
*
1 #MONS-TO-PAY   (N4)
1 #YEARS-TO-PAY  (N4)
*
END-DEFINE
************************************************************************
*
RESET #SERVICES #SERVICES-AMOUNT-DUE #SERVICES-PAYMENT-MODE
  #SERVICES-PAID-TO-DATE #OL-IND #OL-INDEX
*
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #SERVICES-CN     /* NG1
    AND SVC-PLAN = '3SR'
  IF SVC-STATUS = 'A'
    #SERVICES := TRUE
    FIND (1) ST-V WITH ST-V.ID-NUMBER = #SERVICES-CN
      FOR #I2 1 TO C*ACTIVE-SEGMENTS
        IF ST-V.STATUS (#I2) NE 'D'
          ESCAPE TOP
        END-IF
        #TERM := FUNCTION-TERM-POLICY (<ST-V.PLAN (#I2)>)
        IF NOT #TERM
          #OL-IND   := TRUE
          #OL-INDEX := #I2
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
    #SERVICES-PAYMENT-MODE := SVC-MODE
    #SERVICES-PAID-TO-DATE := SVC-PAID-TO-DATE
    #SERVICES-ISSUE-DATE   := SVC-ISSUE-DATE              /* PM1
    #SERVICES-TERMINATION-DATE := SVC-TERMINATION-DATE    /* PM1
    IF NOT #OL-IND
      PERFORM CALCULATE-AMOUNT-DUE
    END-IF
    ESCAPE BOTTOM
  END-IF
END-FIND
*
************************************************************************
DEFINE SUBROUTINE CALCULATE-AMOUNT-DUE
************************************************************************
*
RESET #MONS-TO-PAY #YEARS-TO-PAY
*
#YYYYMMDD        := #SERVICES-PARM-DATE
#PAID-YYYYMMDD   := #SERVICES-PAID-TO-DATE   /* NG1
*
IF #YYYYMM GE #PAID-YYYYMM
  ESCAPE ROUTINE
END-IF
*
SUBTRACT #PAID-YYYY FROM #YYYY GIVING #YEARS-TO-PAY
*
SUBTRACT #PAID-MM FROM #MM GIVING #MONS-TO-PAY
IF #MONS-TO-PAY LT 0
  ADD 12 TO #MONS-TO-PAY
  SUBTRACT 1 FROM #PAID-YYYY
END-IF
*
COMPUTE #MONS-TO-PAY = #MONS-TO-PAY + (#YEARS-TO-PAY * 12)
*
END-SUBROUTINE
*
END
