* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: G4104PMS  DISPLAY MTD DEMOGRAPHICS TO SCREEN
*
* MODIFICATIONS:
* vxt 06062012 vt1 increase the size of the field. 
* RSE 11082012 RE1 Screen modifications for LT II Annual Renewable Requirements
*                  Added new plans (LT II AR & EZ Term (EZ))
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G4105LMS
LOCAL USING GONERLXX        /* WORK FIELDS FOR 'GONERCXX'
LOCAL
1 #WORK-K    (A3)
1 REDEFINE #WORK-K
  2 #WORK-K-AMT (A3)
* 2 #WORK-K-K   (A1)
*
1 #WORK-K-5    (A4)
1 REDEFINE #WORK-K-5
  2 #WORK-K-AMT-5 (A4)
* 2 #WORK-K-K-5   (A1)
*
1 #DATN-DISP   (A11)
1 #POL-STATS-SEX           (P5/2)
1 #POL-STATS-SEX-AGE       (P7/2)
1 #POL-STATS-SEX-FACE      (N7/2)
1 REDEFINE #POL-STATS-SEX-FACE
  2 #PS-SEX-FACE-OCC   (2)
    3 #PS-SEX-FACE-THOUS  (N4)
    3 #PS-SEX-FACE-REST   (A3)
1 #PS-SEX-FACE-K    (A4/2)
1 #POL-STATS-CAT           (P5/7)
1 #POL-STATS-CAT-AGE       (P7/7)
1 #POL-STATS-CAT-FACE      (N6/7)
1 REDEFINE #POL-STATS-CAT-FACE
  2 #PS-CAT-FACE-OCC   (7)
    3 #PS-CAT-FACE-THOUS  (N3)
    3 #PS-CAT-FACE-REST   (A3)
1 #PS-CAT-FACE-K    (A3/7)
1 #POL-STATS-CAP-AGE       (P7/2)
1 #POL-STATS-CAP-FACE      (N7/2)                /* vt1 was (n6/2)
1 REDEFINE #POL-STATS-CAP-FACE
  2 #PS-CAP-FACE-OCC   (2)
    3 #PS-CAP-FACE-THOUS  (N4)                   /* vt1 was (n3)
    3 #PS-CAP-FACE-REST   (A3)
1 #PS-CAP-FACE-K    (A3/2)
1 #POL-STATS-CAP-MALE      (P5/2)
1 #POL-STATS-CAP-FEMALE    (P5/2)
1 #POL-STATS-CAP-MALE-TOT  (P7)
1 #POL-STATS-CAP-FEMALE-TOT(P7)
1 #POL-STATS-CAP-FACE-TOT  (P9)
1 #POL-STATS-POL-TYPE      (P5/6)                /* RE1 was 4
1 #POL-STATS-POL-AGE       (P7/6)                /* RE1 was 4
1 #POL-STATS-POL-TYPE-TOT-FACE (N6/6)            /* RE1 was 4
1 REDEFINE #POL-STATS-POL-TYPE-TOT-FACE
  2 #PS-POL-FACE-OCC   (6)                       /* RE1 was 4  
    3 #PS-POL-FACE-THOUS  (N3)
    3 #PS-POL-FACE-REST   (A3)
1 #PS-POL-TYPE-TOT-FACE-K    (A3/6)              /* RE1 was 4
1 #POL-STATS-STAT          (P5/3)
1 #POL-STATS-STAT-AGE      (P7/3)
1 #POL-STATS-STAT-FACE     (N6/3)
1 REDEFINE #POL-STATS-STAT-FACE
  2 #PS-STAT-FACE-OCC   (3)
    3 #PS-STAT-FACE-THOUS  (N3)
    3 #PS-STAT-FACE-REST   (A3)
1 #PS-STAT-FACE-K    (A3/3)
1 #NEW-MBR-IND              (A1)
1 #ADL-INS-IND              (A1)
1 #ALL-INS-IND              (A1)
1 #POL-STATS-SEX-AVG-AGE     (P7)
1 #POL-STATS-CAT-AVG-AGE     (P7)
1 #POL-STATS-CAP-AVG-AGE     (P7)
1 #POL-STATS-POL-AVG-AGE     (P7)
1 #POL-STATS-STAT-AVG-AGE    (P7)
1 #POL-STATS-SEX-AVG-FACE    (N7)
1 REDEFINE #POL-STATS-SEX-AVG-FACE
  2 #PS-SEX-AVG-FACE-THOUS  (N4)
  2 #PS-SEX-AVG-FACE-REST   (A3)
1 #PS-SEX-AVG-FACE          (A4)
1 #POL-STATS-CAT-AVG-FACE   (N6)
1 REDEFINE #POL-STATS-CAT-AVG-FACE
  2 #PS-CAT-AVG-FACE-THOUS  (N3)
  2 #PS-CAT-AVG-FACE-REST   (A3)
1 #PS-CAT-AVG-FACE          (A3)
1 #POL-STATS-CAP-AVG-FACE    (N6)
1 REDEFINE #POL-STATS-CAP-AVG-FACE
  2 #PS-CAP-AVG-FACE-THOUS  (N3)
  2 #PS-CAP-AVG-FACE-REST   (A3)
1 #PS-CAP-AVG-FACE          (A3)
1 #POL-STATS-POL-AVG-FACE    (N6)
1 REDEFINE #POL-STATS-POL-AVG-FACE
  2 #PS-POL-AVG-FACE-THOUS  (N3)
  2 #PS-POL-AVG-FACE-REST   (A3)
1 #PS-POL-AVG-FACE          (A3)
1 #POL-STATS-STAT-AVG-FACE   (N6)
1 REDEFINE #POL-STATS-STAT-AVG-FACE
  2 #PS-STAT-AVG-FACE-THOUS  (N3)
  2 #PS-STAT-AVG-FACE-REST   (A3)
1 #PS-STAT-AVG-FACE          (A3)
1 #POL-STATS-CAP            (P5/2)
1 #POL-STATS-SEX-TOT        (P7)
1 #POL-STATS-CAT-TOT        (P7)
1 #POL-STATS-CAP-TOT        (P7)
1 #POL-STATS-POL-TYPE-TOT   (P7)
1 #POL-STATS-STAT-TOT       (P7)
1 #POL-TYPE-DESC1           (A69)
1 #POL-TYPE-DESC2           (A69)
1 #POL-TYPE-DESC3           (A69)
1 #ACCUM-TOT-SEX            (P7)
1 #ACCUM-TOT-SEX-AGE        (P7)
1 #ACCUM-TOT-SEX-FACE       (P9)
1 #ACCUM-TOT-CAT            (P7)
1 #ACCUM-TOT-CAT-AGE        (P7)
1 #ACCUM-TOT-CAT-FACE       (P9)
1 #ACCUM-TOT-CAP-CNT        (P7/2)
1 #ACCUM-TOT-CAP-AGE        (P7)
1 #ACCUM-TOT-CAP-FACE       (P9)
1 #ACCUM-TOT-CAP-MALE-FEMALE (P9)
1 #ACCUM-TOT-POL            (P7)
1 #ACCUM-TOT-POL-AGE        (P7)
1 #ACCUM-TOT-POL-FACE       (P9)
1 #ACCUM-TOT-STAT           (P7)
1 #ACCUM-TOT-STAT-AGE       (P7)
1 #ACCUM-TOT-STAT-FACE      (P9)
*
1 #MONTH-DISP         (A4)
1 #TEXT              (A60)
1 #I0                 (I2)
1 #I1                 (I2)
1 #I2                 (I2)
1 #I3                 (I2)
1 #I4                 (I2)
1 #I5                 (I2)
1 #1                  (I2)
1 #2                  (I2)
1 #HLD-IDX            (I2)
1 #ARMY               (L)
1 #AIRF               (L)
1 #OTHER              (L)
1 #ALL                (L)
1 #HOLD-MM            (N2)
1 #MTD-DEMO-SERV      (A1/3)
1 #DESC-DEMO         (A16) INIT <'DEMOGRAPHICS OF '>
1 #DESC-MTH          (A13) INIT <'FOR MONTH OF '>
1 #DESC-NEW          (A4) INIT <'NEW '>
1 #DESC-ADDL         (A5) INIT <'ADDL '>
1 #DESC-OTH          (A6) INIT <'OTHER '>
1 #DESC-TOTAL        (A6) INIT <'TOTAL '>
1 #DESC-ARMY         (A5) INIT <'ARMY '>
1 #DESC-AIRF         (A9) INIT <'AIRFORCE '>
1 #DESC-PH           (A13) INIT <'POLICYHOLDERS'>
1 #LAST-YYYY          (N4)
1 #FROM-DATE          (A16)
1 #TO-DATE            (A8)
1 REDEFINE #TO-DATE
  2 #TO-DATE-MM        (N2)
  2 #TO-DATE-SL-1      (A1)
  2 #TO-DATE-DD        (N2)
  2 #TO-DATE-SL-2      (A1)
  2 #TO-DATE-YY        (N2)
*
1 #TO-DATE-MON-DISP   (A3)
*
1 #DATN-YYYYMMDD      (N8)
1 REDEFINE #DATN-YYYYMMDD
  2 #DATN-YYYYMM       (N6)
  2 #DATN-DD           (N2)
*
1 #WORK-YYYYMM        (N6)
1 REDEFINE #WORK-YYYYMM
  2 #WORK-YYYY         (N4)
  2 #WORK-MM           (N2)
*
1 #YYYYMMDD           (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM            (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY             (N4)
    3 #MM               (N2)
  2 #DD                (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A    (A4)
  2 #MM-A      (A2)
  2 #DD-A      (A2)
*
1 #MONTH-G (A48)
  INIT <'JAN FEB MAR APR MAY JUN JUL AUG SEP OCT NOV DEC '>
1 REDEFINE #MONTH-G
  2 #MONTH-OCC     (12)
    3 #MONTH        (A3)
    3 #FILL         (A1)
1 #MONTHS (A36) INIT <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON (A3/12)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX            /* NATURAL ERROR ROUTINE
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #DATN-DISP
MOVE *DATN TO #DATN-YYYYMMDD
*
INPUT #YYYYMM #MTD-DEMO-SERV(*)
*
MOVE '*' TO #CGA-SCR-COMMAND
MOVE #YYYYMM TO #WORK-YYYYMM
*
******************************************************** 082803
* IF #WORK-YYYYMM GE #DATN-YYYYMM
*   SUBTRACT 1 FROM #WORK-MM
*   IF #WORK-MM LT 1
*     SUBTRACT 1 FROM #WORK-YYYY
*     MOVE 12 TO #WORK-MM
*   END-IF
*   MOVE #MONTH(#WORK-MM) TO #MONTH-DISP
*   COMPRESS #MONTH (#WORK-MM) #WORK-YYYY INTO #TO-DATE
* END-IF
******************************************************** 082803
MOVE #YYYYMM TO #WORK-YYYYMM
* IF #WORK-YYYYMM LT #DATN-YYYYMM                        082803
MOVE #MONTH(#WORK-MM) TO #MONTH-DISP
COMPRESS #MONTH (#WORK-MM) #WORK-YYYY INTO #TO-DATE
ADD 1 TO #WORK-MM
IF #WORK-MM GT 12
  SUBTRACT 12 FROM #WORK-MM
  ADD 1 TO #WORK-YYYY
END-IF
* END-IF                                                 082803
*
IF #MTD-DEMO-SERV(1) NE ' '
  MOVE 3 TO #HLD-IDX
  MOVE 3 TO #1
  MOVE 4 TO #2
  MOVE TRUE TO #ARMY
ELSE
  IF #MTD-DEMO-SERV(2) NE ' '
    MOVE 5 TO #HLD-IDX
    MOVE 5 TO #1
    MOVE 6 TO #2
    MOVE TRUE TO #AIRF
  ELSE
*   IF #MTD-DEMO-SERV(3) NE ' '
*    MOVE 7 TO #HLD-IDX
*    MOVE 7 TO #1
*    MOVE 8 TO #2
*    MOVE TRUE TO #OTHER
*   ELSE
    MOVE 1 TO #HLD-IDX
    MOVE 1 TO #1
    MOVE 2 TO #2
    MOVE TRUE TO #ALL
*   END-IF
  END-IF
END-IF
*
REPEAT
  FOR #I0 1 TO 3
    IF #I0 = 1
      MOVE #HLD-IDX TO #I1
    END-IF
*
    IF #I0 = 2
      MOVE #HLD-IDX TO #I1
      ADD 1  TO #I1
    END-IF
*
    IF #I0 = 3
      MOVE #HLD-IDX TO #I1
    END-IF
*
    FIND (1) MS-V WITH MONTH-KEY = #WORK-YYYYMM
      IF NO RECORDS FOUND
*   IF *DEVICE = 'BATCH'                                  082803
        IGNORE
*   ELSE                                                  082803
*     COMPRESS 'MS REC NOT FOUND FOR' #YYYYMM INTO #TEXT  082803
*     MOVE 'MS'   TO #CGA-SCR-COMMAND                     082803
*     FETCH 'G1000PXX'                                    082803
*   END-IF                                                082803
      END-NOREC
*
      IF #I0 = 3
        FOR #I2 1 TO 2
          ADD POL-ST-SEX-CNT (#2,#I2)     TO POL-ST-SEX-CNT (#1,#I2)
          ADD POL-ST-SEX-TOT-AGE(#2,#I2)  TO POL-ST-SEX-TOT-AGE(#1, #I2)
          ADD POL-ST-SEX-TOT-FACE(#2,#I2) TO POL-ST-SEX-TOT-FACE(#1,#I2)
          ADD POL-ST-CAP-CNT(#2, #I2)     TO POL-ST-CAP-CNT(#1, #I2)
          ADD POL-ST-CAP-TOT-AGE(#2, #I2) TO POL-ST-CAP-TOT-AGE(#1, #I2)
          ADD POL-ST-CAP-TOT-FACE(#2,#I2) TO POL-ST-CAP-TOT-FACE(#1,#I2)
        END-FOR
*
        FOR #I2 1 TO 7
          ADD POL-ST-CAT-CNT(#2,#I2)      TO POL-ST-CAT-CNT(#1,#I2)
          ADD POL-ST-CAT-TOT-AGE(#2,#I2)  TO POL-ST-CAT-TOT-AGE(#1,#I2)
          ADD POL-ST-CAT-TOT-FACE(#2,#I2) TO POL-ST-CAT-TOT-FACE(#1,#I2)
        END-FOR
*
        FOR #I2 1 TO 6                                                    /* RE1 was 4
          ADD POL-ST-POL-CNT(#2,#I2)      TO POL-ST-POL-CNT(#1,#I2)
          ADD POL-ST-POL-TOT-AGE(#2,#I2)  TO POL-ST-POL-TOT-AGE(#1,#I2)
          ADD POL-ST-POL-TOT-FACE(#2,#I2) TO POL-ST-POL-TOT-FACE(#1,#I2)
        END-FOR
*
        FOR #I2 1 TO 3
          ADD POL-ST-STAT-CNT(#2,#I2)     TO POL-ST-STAT-CNT(#1,#I2)
          ADD POL-ST-STAT-TOT-AGE(#2,#I2) TO POL-ST-STAT-TOT-AGE(#1,#I2)
          ADD POL-ST-STAT-TOT-FACE(#2,#I2)
            TO POL-ST-STAT-TOT-FACE(#1,#I2)
        END-FOR
      END-IF
*
      FOR #I2 1 TO 2
        MOVE POL-ST-SEX-CNT (#I1, #I2) TO #POL-STATS-SEX (#I2)
        COMPUTE ROUNDED #POL-STATS-SEX-AGE (#I2) =
          (POL-ST-SEX-TOT-AGE (#I1,#I2) / POL-ST-SEX-CNT (#I1,#I2))
        COMPUTE ROUNDED #POL-STATS-SEX-FACE(#I2) =
          (POL-ST-SEX-TOT-FACE(#I1,#I2) / POL-ST-SEX-CNT (#I1,#I2))
*
        MOVE EDITED #PS-SEX-FACE-THOUS (#I2) (EM=ZZZ9) TO #WORK-K-AMT-5
*   MOVE 'K' TO #WORK-K-K-5
        MOVE #WORK-K-5 TO #PS-SEX-FACE-K (#I2)
*
        COMPUTE ROUNDED #ACCUM-TOT-SEX =
          (#ACCUM-TOT-SEX + POL-ST-SEX-CNT(#I1,#I2))
        COMPUTE ROUNDED #ACCUM-TOT-SEX-AGE =
          (#ACCUM-TOT-SEX-AGE + POL-ST-SEX-TOT-AGE(#I1,#I2))
        COMPUTE ROUNDED #ACCUM-TOT-SEX-FACE =
          (#ACCUM-TOT-SEX-FACE + POL-ST-SEX-TOT-FACE(#I1,#I2))
        MOVE #ACCUM-TOT-SEX TO #POL-STATS-SEX-TOT
*
        MOVE POL-ST-CAP-CNT (#I1, #I2) TO #POL-STATS-CAP (#I2)
        COMPUTE ROUNDED #ACCUM-TOT-CAP-CNT(#I2) =
          (#ACCUM-TOT-CAP-CNT(#I2) + POL-ST-CAP-CNT(#I1,#I2))
        COMPUTE ROUNDED #POL-STATS-CAP-TOT =
          (#POL-STATS-CAP-TOT + #ACCUM-TOT-CAP-CNT(#I2))
        COMPUTE ROUNDED #POL-STATS-CAP-AGE (#I2) =
          (POL-ST-CAP-TOT-AGE (#I1,#I2) / #ACCUM-TOT-CAP-CNT(#I2))
        COMPUTE ROUNDED #POL-STATS-CAP-FACE(#I2) =
          (POL-ST-CAP-TOT-FACE(#I1,#I2) / #ACCUM-TOT-CAP-CNT(#I2))
*
        MOVE EDITED #PS-CAP-FACE-THOUS (#I2) (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
        MOVE #WORK-K TO #PS-CAP-FACE-K (#I2)
*
        MOVE #ACCUM-TOT-CAP-CNT(#I2)      TO #POL-STATS-CAP(#I2)
        ADD POL-ST-CAP-TOT-AGE(#I1, #I2)  TO #ACCUM-TOT-CAP-AGE
        ADD POL-ST-CAP-TOT-FACE(#I1, #I2) TO #ACCUM-TOT-CAP-FACE
*
*   WRITE 'POL-CAP-TOT-FMALE: ' POL-ST-CAP-FEMALE(#I1,#I2)
*         'POL-CAP-TOT-MFMLE: ' #ACCUM-TOT-CAP-CNT(#I2)
*         'POL-CAP-TOT-AGE  : ' POL-ST-CAP-TOT-AGE (#I1,#I2)
*         'POL-CAP-TOT-FACE : ' POL-ST-CAP-TOT-FACE(#I1,#I2)
      END-FOR
*
      COMPUTE ROUNDED #POL-STATS-CAP-AVG-AGE =
        (#ACCUM-TOT-CAP-AGE / #POL-STATS-CAP-TOT)
      COMPUTE ROUNDED #POL-STATS-CAP-AVG-FACE =
        (#ACCUM-TOT-CAP-FACE / #POL-STATS-CAP-TOT)
      COMPUTE ROUNDED #POL-STATS-SEX-AVG-AGE =
        (#ACCUM-TOT-SEX-AGE / #ACCUM-TOT-SEX)
      COMPUTE ROUNDED #POL-STATS-SEX-AVG-FACE =
        (#ACCUM-TOT-SEX-FACE / #ACCUM-TOT-SEX)
*
      MOVE EDITED #PS-CAP-AVG-FACE-THOUS (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
      MOVE #WORK-K TO #PS-CAP-AVG-FACE
*
      MOVE EDITED #PS-SEX-AVG-FACE-THOUS (EM=ZZZ9) TO #WORK-K-AMT-5
*   MOVE 'K' TO #WORK-K-K-5
      MOVE #WORK-K-5 TO #PS-SEX-AVG-FACE
*
      FOR #I3 1 TO 7
        MOVE POL-ST-CAT-CNT (#I1, #I3) TO #POL-STATS-CAT (#I3)
        COMPUTE ROUNDED #POL-STATS-CAT-AGE (#I3) =
          (POL-ST-CAT-TOT-AGE (#I1,#I3) / POL-ST-CAT-CNT (#I1,#I3))
        COMPUTE ROUNDED #POL-STATS-CAT-FACE(#I3) =
          (POL-ST-CAT-TOT-FACE(#I1,#I3) / POL-ST-CAT-CNT (#I1,#I3))
*
        MOVE EDITED #PS-CAT-FACE-THOUS (#I3) (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
        MOVE #WORK-K TO #PS-CAT-FACE-K (#I3)
*
        COMPUTE ROUNDED #ACCUM-TOT-CAT =
          (#ACCUM-TOT-CAT + POL-ST-CAT-CNT(#I1,#I3))
        COMPUTE ROUNDED #ACCUM-TOT-CAT-AGE =
          (#ACCUM-TOT-CAT-AGE + POL-ST-CAT-TOT-AGE(#I1,#I3))
        COMPUTE ROUNDED #ACCUM-TOT-CAT-FACE =
          (#ACCUM-TOT-CAT-FACE + POL-ST-CAT-TOT-FACE(#I1,#I3))
        MOVE #ACCUM-TOT-CAT TO #POL-STATS-CAT-TOT
*
*  WRITE 'POL-CAT-TOT-AGE  : ' POL-ST-CAT-TOT-AGE (#I1,#I3)
*        'POL-CAT-TOT-CNT  : ' POL-ST-CAT-CNT(#I1,#I3)
      END-FOR
*
      COMPUTE ROUNDED #POL-STATS-CAT-AVG-AGE =
        (#ACCUM-TOT-CAT-AGE / #ACCUM-TOT-CAT)
      COMPUTE ROUNDED #POL-STATS-CAT-AVG-FACE =
        (#ACCUM-TOT-CAT-FACE / #ACCUM-TOT-CAT)
*
      MOVE EDITED #PS-CAT-AVG-FACE-THOUS (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
      MOVE #WORK-K TO #PS-CAT-AVG-FACE
*
      FOR #I4 1 TO 6                                                  /* RE1 was 4
        MOVE POL-ST-POL-CNT(#I1,#I4) TO #POL-STATS-POL-TYPE(#I4)
        COMPUTE ROUNDED #POL-STATS-POL-AGE (#I4) =
          (POL-ST-POL-TOT-AGE (#I1,#I4) / POL-ST-POL-CNT (#I1,#I4))
        COMPUTE ROUNDED #POL-STATS-POL-TYPE-TOT-FACE (#I4) =
          (POL-ST-POL-TOT-FACE(#I1,#I4) / POL-ST-POL-CNT(#I1,#I4))
*
        MOVE EDITED #PS-POL-FACE-THOUS (#I4) (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
        MOVE #WORK-K TO #PS-POL-TYPE-TOT-FACE-K (#I4)
*
        COMPUTE ROUNDED #ACCUM-TOT-POL =
          (#ACCUM-TOT-POL + POL-ST-POL-CNT(#I1,#I4))
        COMPUTE ROUNDED #ACCUM-TOT-POL-AGE =
          (#ACCUM-TOT-POL-AGE + POL-ST-POL-TOT-AGE(#I1,#I4))
        COMPUTE ROUNDED #ACCUM-TOT-POL-FACE =
          (#ACCUM-TOT-POL-FACE + POL-ST-POL-TOT-FACE(#I1,#I4))
        ADD POL-ST-POL-CNT(#I1, #I4) TO #POL-STATS-POL-TYPE-TOT
*   WRITE 'POL-TYPE-TOT-FACE: ' POL-ST-POL-TOT-FACE(#I1,#I4)
*         'POL-TYPE-TOT-CNT : ' POL-ST-POL-CNT(#I1,#I4)
      END-FOR
*
      COMPUTE ROUNDED #POL-STATS-POL-AVG-AGE =
        (#ACCUM-TOT-POL-AGE / #ACCUM-TOT-POL)
      COMPUTE ROUNDED #POL-STATS-POL-AVG-FACE =
        (#ACCUM-TOT-POL-FACE / #ACCUM-TOT-POL)
*
      MOVE EDITED #PS-POL-AVG-FACE-THOUS (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
      MOVE #WORK-K TO #PS-POL-AVG-FACE
*
      FOR #I5 1 TO 3
        MOVE POL-ST-STAT-CNT (#I1, #I5) TO #POL-STATS-STAT (#I5)
        COMPUTE ROUNDED #POL-STATS-STAT-AGE (#I5) =
          (POL-ST-STAT-TOT-AGE (#I1,#I5) / POL-ST-STAT-CNT (#I1,#I5))
        COMPUTE ROUNDED #POL-STATS-STAT-FACE(#I5) =
          (POL-ST-STAT-TOT-FACE(#I1,#I5) / POL-ST-STAT-CNT (#I1,#I5))
*
        MOVE EDITED #PS-STAT-FACE-THOUS (#I5) (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
        MOVE #WORK-K TO #PS-STAT-FACE-K (#I5)
*
        COMPUTE ROUNDED #ACCUM-TOT-STAT =
          (#ACCUM-TOT-STAT + POL-ST-STAT-CNT(#I1,#I5))
        COMPUTE ROUNDED #ACCUM-TOT-STAT-AGE =
          (#ACCUM-TOT-STAT-AGE + POL-ST-STAT-TOT-AGE(#I1,#I5))
        COMPUTE ROUNDED #ACCUM-TOT-STAT-FACE =
          (#ACCUM-TOT-STAT-FACE + POL-ST-STAT-TOT-FACE(#I1,#I5))
        MOVE #ACCUM-TOT-STAT TO #POL-STATS-STAT-TOT
*  WRITE 'POL-STAT-TOT-AGE  : ' POL-ST-STAT-TOT-AGE (#I1,#I5)
*        'POL-STAT-TOT-CNT  : ' POL-ST-STAT-CNT(#I1,#I5)
      END-FOR
*
      COMPUTE ROUNDED #POL-STATS-STAT-AVG-AGE =
        (#ACCUM-TOT-STAT-AGE / #ACCUM-TOT-STAT)
      COMPUTE ROUNDED #POL-STATS-STAT-AVG-FACE =
        (#ACCUM-TOT-STAT-FACE / #ACCUM-TOT-STAT)
*
      MOVE EDITED #PS-STAT-AVG-FACE-THOUS (EM=ZZ9) TO #WORK-K-AMT
*   MOVE 'K' TO #WORK-K-K
      MOVE #WORK-K TO #PS-STAT-AVG-FACE
*
    END-FIND
*
    IF #I1 = 1
      COMPRESS #DESC-DEMO #DESC-NEW #DESC-TOTAL #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 2
      COMPRESS #DESC-DEMO #DESC-ADDL #DESC-TOTAL #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 3
      COMPRESS #DESC-DEMO #DESC-NEW #DESC-ARMY #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 4
      COMPRESS #DESC-DEMO #DESC-ADDL #DESC-ARMY #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 5
      COMPRESS #DESC-DEMO #DESC-NEW #DESC-AIRF #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 6
      COMPRESS #DESC-DEMO #DESC-ADDL #DESC-AIRF #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 7
      COMPRESS #DESC-DEMO #DESC-NEW #DESC-OTH #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    IF #I1 = 8
      COMPRESS #DESC-DEMO #DESC-ADDL #DESC-OTH #DESC-PH #DESC-MTH
        #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
*
    IF #I0 = 3
      IF #ALL
        COMPRESS #DESC-DEMO #DESC-TOTAL #DESC-ARMY '/ AIRF ' #DESC-PH
          #DESC-MTH #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
      IF #ARMY
        COMPRESS #DESC-DEMO #DESC-TOTAL #DESC-ARMY #DESC-PH #DESC-MTH
          #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
      IF #AIRF
        COMPRESS #DESC-DEMO #DESC-TOTAL #DESC-AIRF #DESC-PH #DESC-MTH
          #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
      IF #OTHER
        COMPRESS #DESC-DEMO #DESC-TOTAL #DESC-OTH #DESC-PH #DESC-MTH
          #MONTH-DISP #YYYY INTO #POL-TYPE-DESC1 END-IF
    END-IF
*
    INPUT USING MAP 'G4104MMS'  /* MTD DEMOGRAPHICS
    IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
    END-IF
*
    RESET #POL-STATS-SEX       (*)
      #POL-STATS-SEX-AGE       (*)
      #POL-STATS-SEX-FACE      (*)
      #POL-STATS-CAT           (*)
      #POL-STATS-CAT-AGE       (*)
      #POL-STATS-CAT-FACE      (*)
      #POL-STATS-CAP-AGE       (*)
      #POL-STATS-CAP-FACE      (*)
      #POL-STATS-CAP-MALE      (*)
      #POL-STATS-CAP-FEMALE    (*)
      #POL-STATS-CAP-TOT
      #POL-STATS-CAP-MALE-TOT
      #POL-STATS-CAP-FEMALE-TOT
      #POL-STATS-CAP-FACE-TOT
      #POL-STATS-POL-TYPE      (*)
      #POL-STATS-POL-AGE       (*)
      #POL-STATS-POL-TYPE-TOT-FACE (*)
      #POL-STATS-STAT          (*)
      #POL-STATS-STAT-AGE      (*)
      #POL-STATS-STAT-FACE     (*)
      #POL-STATS-SEX-AVG-AGE
      #POL-STATS-CAP-AVG-AGE
      #POL-STATS-CAT-AVG-AGE
      #POL-STATS-POL-AVG-AGE
      #POL-STATS-STAT-AVG-AGE
      #POL-STATS-SEX-AVG-FACE
      #POL-STATS-CAP-AVG-FACE
      #POL-STATS-CAT-AVG-FACE
      #POL-STATS-POL-AVG-FACE
      #POL-STATS-STAT-AVG-FACE
      #POL-STATS-CAT-TOT
      #POL-STATS-POL-TYPE-TOT
      #POL-TYPE-DESC1
      #POL-TYPE-DESC2
      #POL-TYPE-DESC3
      #ACCUM-TOT-SEX
      #ACCUM-TOT-SEX-AGE
      #ACCUM-TOT-SEX-FACE
      #ACCUM-TOT-CAT
      #ACCUM-TOT-CAT-AGE
      #ACCUM-TOT-CAT-FACE
      #ACCUM-TOT-CAP-CNT        (*)
      #ACCUM-TOT-CAP-AGE
      #ACCUM-TOT-CAP-FACE
      #ACCUM-TOT-CAP-MALE-FEMALE
      #ACCUM-TOT-POL
      #ACCUM-TOT-POL-AGE
      #ACCUM-TOT-POL-FACE
      #ACCUM-TOT-STAT
      #ACCUM-TOT-STAT-AGE
      #ACCUM-TOT-STAT-FACE
  END-FOR
END-REPEAT
END
