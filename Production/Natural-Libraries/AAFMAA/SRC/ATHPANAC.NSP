* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPANAC - Modify Fiche Files being sent to ANACOMP
********************************************************************
*                  MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  08222006      Created
* DRW  11012006 DW1  Do not replace 1st record if Parm = 'STATUS'
* DRW  11012006 DW2  Replace UNIX Page-Top with '1' Carr Ctl
************************************************************************
*
DEFINE DATA
LOCAL
1 #INPUT    (A133)
1 REDEFINE #INPUT
 2 #INPUT-POS-1   (A1)
 2 #INPUT-132     (A132)
*
1 #LAST-INPUT   (A133)
1 REDEFINE #LAST-INPUT
 2 #LAST-INPUT-POS-1   (A1)
*
1 #OUTPUT    (A133)
*
1 #PARM   (A8)     /* STATUS/MAINT/ACCTHIST
1 #NUM-INPUT    (N9)    /* vt was n7 08012012
1 #NUM-OUTPUT   (N9)    /* vt was n7 08012012
1 #BYP-POS-1    (N9)    /* vt was nt 08012012
*
1 #PAGE-TOP  (A1) INIT <H'0C'>    /* DW2
1 #BLANKS      (N2)
*
END-DEFINE
*
READ WORK 1 ONCE RECORD #PARM
WRITE 'PARM=' #PARM
*
READ WORK 2 RECORD #INPUT
  ADD 1 TO #NUM-INPUT
*
  IF #PARM = 'STATUS'          /* DW2
    IF #INPUT-POS-1 = #PAGE-TOP
*     WRITE 'Replace' #INPUT-POS-1 'With "1"'
*          #INPUT (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
      MOVE '1' TO #INPUT-POS-1
      RESET #BLANKS
    ELSE
      IF #INPUT = ' '
        ADD 1 TO #BLANKS
        ESCAPE TOP
      ELSE
        MOVE #INPUT TO #INPUT-132  /* Shift 1 pos to the right
        RESET #INPUT-POS-1
        IF #BLANKS NE 0
          IF #BLANKS = 1
            MOVE '0' TO #INPUT-POS-1   /* skip 1 line
          ELSE
            MOVE '-' TO #INPUT-POS-1   /* skip 2 lines
          END-IF
*
*         WRITE 'Shift to right' #BLANKS 'Lines'
*          #INPUT (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
          RESET #BLANKS
        END-IF
      END-IF
    END-IF
  END-IF      /* DW2
*
  IF #NUM-INPUT = 1  /* Replace or Create 1st rec (Control for ANACOMP)
    WRITE 'PARM=' #PARM
    IF #PARM = 'STATUS'
      MOVE '    Q1   R2901002' TO #OUTPUT   /* STATUS
    ELSE
      IF #PARM = 'MAINT'
        MOVE '    Q1   R2901004' TO #OUTPUT  /* ACCT-REG/FIL-MAIN/DIR-BL
      ELSE
        MOVE '    Q1   R2901003' TO #OUTPUT  /* ACCT HISTORY
      END-IF
    END-IF
    WRITE '1st record =' #OUTPUT (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
*
    IF #PARM = 'STATUS'   /* Put out Orig 1st record on STATUS  DW1
      MOVE #OUTPUT TO #LAST-INPUT   /* afer Control Record
      WRITE WORK FILE 3 #OUTPUT
      ADD 1 TO #NUM-OUTPUT
      MOVE #INPUT TO #OUTPUT
    END-IF
  ELSE
    IF   (#INPUT-POS-1 = '1')
     AND (#LAST-INPUT-POS-1 = '1')
     AND (#INPUT-132 = ' ')
*     WRITE 'bypassed=' #INPUT (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
*     WRITE 'prior rec=' #LAST-INPUT (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
      ADD 1 TO #BYP-POS-1
      ESCAPE TOP
    END-IF
*
    MOVE #INPUT TO #OUTPUT
  END-IF
*
  MOVE #OUTPUT TO #LAST-INPUT
  WRITE WORK FILE 3 #OUTPUT
  ADD 1 TO #NUM-OUTPUT
END-WORK
*
WRITE '#recs-read    =' #NUM-INPUT
WRITE '#recs-written =' #NUM-OUTPUT
WRITE '#recs-bypassed=' #BYP-POS-1
*
END
