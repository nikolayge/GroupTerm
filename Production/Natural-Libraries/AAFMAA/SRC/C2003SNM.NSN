* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2003SNM - Perform Dependant Edits and Updates
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    08242004    None  Program Written.                            *
* PAM    10062004    PM1   Changed logic to update existing FIR record *
*                          with info if created.                       *
* DRW    08262005    DW2   Chg SGLI amt from 250,000 to 400,000        *
* PAM    04/21/2010  PM3   Removed dependant updates - added contact   *
*                          file                                        *
* bz  09/23/2011 bz1 Add member CI to FIR 
************************************************************************
*
DEFINE DATA
PARAMETER
1 #ACTION-FLAG            (A1)
1 #ID-NUMBER              (N6)
1 REDEFINE #ID-NUMBER
 2 #ID-NUMBER-A           (A6)
1 #MILITARY-STATUS        (A1)
1 #MILITARY-SERVICE       (A3)
1 #INTRO-FLAG             (A1)
1 #ERR-MSG                (A60)
1 #ERR-SEG                (I2)
*

LOCAL
*
1 FR-V VIEW OF A-FIRS
 2 ID-NUMBER              (N6.0)
 2 CONTACT-ID-NUMBER 
 2 MEMBER-FLAG            (A1)
 2 INS-LAST-FM-DATE       (N8)
 2 DP-LAST-FM-DATE        (N8)
 2 DOCU-LAST-FM-DATE      (N8)
 2 POL-CODE               (A1/1)
 2 POL-NAME               (A19/1)
 2 POL-NUMBER             (A14/1)
 2 POL-TYPE               (A4/1)
 2 POL-AMOUNT             (P7/1)
 2 POL-DATE               (N8/1)
 2 DATE-TIME-STAMP
  3 DTS-DATN              (P9)
  3 DTS-TIMN              (P7)
*
1 #STORE-FIRS             (L)
1 #FIR-FOUND              (L) /* PM1
1 #I1                     (I2)
1 #ISN                    (P8) /* PM1
*
1 CNT-V VIEW OF A-CONTACTS          /* bz1
  2 CONTACT-ID                      /* bz1  
END-DEFINE
************************************************************************
*
IF #MILITARY-STATUS = 'A'
  MOVE TRUE TO #STORE-FIRS
END-IF
*
IF #STORE-FIRS
  RESET #FIR-FOUND                                                /* PM1
  FIND FR-V WITH FR-V.MEMBER-KEY = #ID-NUMBER-A                   /* PM1
    MOVE TRUE TO #FIR-FOUND                                       /* PM1
    MOVE *ISN TO #ISN                                             /* PM1
    ESCAPE BOTTOM                                                 /* PM1
  END-FIND                                                        /* PM1
*
  IF #FIR-FOUND                                                   /* PM1
    GET FR-V #ISN                                                 /* PM1
  ELSE                                                            /* PM1
    RESET FR-V                                                    /* PM1
  END-IF                                                          /* PM1
*
  MOVE #ID-NUMBER TO FR-V.ID-NUMBER
  MOVE 'Y' TO FR-V.MEMBER-FLAG
  MOVE *DATN TO FR-V.DTS-DATN
  MOVE *TIMN TO FR-V.DTS-TIMN
*
  IF   (#MILITARY-STATUS = 'A')
   AND (NOT #MILITARY-SERVICE = 'ACR' OR = 'FCR')
   AND (NOT #INTRO-FLAG = 'S' OR = 'K' OR = 'G')
    MOVE 'SGLI' TO FR-V.POL-NAME(1)
    MOVE 400000 TO FR-V.POL-AMOUNT(1)            /* DW2
    MOVE 'GPTM' TO FR-V.POL-TYPE(1)
    MOVE *DATN TO FR-V.INS-LAST-FM-DATE
  END-IF
*
   IF CONTACT-ID-NUMBER = 0                                      /* bz1   
      FIND (1) CNT-V WITH ID-NUMBER = #ID-NUMBER                 /* bz1
        CONTACT-ID-NUMBER := CONTACT-ID                          /* bz1
      END-FIND                                                   /* bz1
   END-IF                                                        /* bz1  

  IF #FIR-FOUND                                                   /* PM1
     UPDATE (0075)                                                /* PM1
  ELSE                                                            /* PM1
    STORE FR-V
  END-IF                                                          /* PM1
END-IF
*
END
