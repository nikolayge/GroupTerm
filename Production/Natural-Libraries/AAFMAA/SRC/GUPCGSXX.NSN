* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GUPCGSXX (Clone of GUPCISXX)
* DESCRIPTION: PF4 FUNCTION - Update/Add
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* NGG  04-14-2010       Creation
* RSE  04-22-2010  RE1  Remove restriction to display CI; Display blanks when SSN = 0;
*                       Remove mandatory entry of First Name; Check duplicate SSN within
*                       program
* RSE  04-25-2010  RE2  Validate Last Name
* RSE  05-09-2010  RE3  Check that Gender is not blank
* BZ   09/11/2010  bz   Name fields validation
********************************************************
*
DEFINE DATA
PARAMETER
1 #API
  2 #CO-ID-1          (N8)
  2 #CO-ID-2          (N8)
  2 #CGA-ORIG-COMMAND (A2)
  2 #CGA-ORIG-ID      (N6)
  2 #CGA-USER-COMPANY (A1)
  2 #CGA-CLERK        (A3)
  2 #MODE             (A1)  /* U - Update, V - View
  2 #MSG              (A72)
LOCAL
1 #DELETE               (L)
1 #UPDATE               (L)
1 #UPDATED              (L)
1 #NO-ERRORS            (L)
1 #CONFIRM-LN           (A1)
1 #FIELDX               (I4)
1 #CMD                  (A1)
1 #UI
  2 #STATUS-ISN         (P8)
  2 #CONTACT-ISN        (P8)
  2 #FIRS-ISN           (P8)
  2 #FIRST-NAME         (A25)
  2 #MIDDLE-NAME        (A25)
  2 #LAST-NAME          (A25)
  2 #SUFFIX             (A10)
  2 #SSN                (N9)
  2 REDEFINE #SSN
    3 #SSN-A            (A9)
  2 #GENDER-CD          (A1)
  2 #BIRTH-CERT-CODE    (A1)
  2 #MARRIAGE-CERT-CODE (A1)
  2 #ORG-NAME           (A50)
  2 #CN                 (N6)
  2 #CID                (N8)
  2 #DOB         (A8)
  2 REDEFINE #DOB
    3 #DOB-N     (N8)
  2 REDEFINE #DOB
    3 #MM-B      (A2)
    3 #DD-B      (A2)
    3 #YYYY-B    (A4)
  2 #DOD         (A8)
  2 REDEFINE #DOD
    3 #DOD-N     (N8)
  2 REDEFINE #DOD
    3 #MM-D      (A2)
    3 #DD-D      (A2)
      3#YYYY-D    (A4)
  2 #DOM         (A8)
  2 REDEFINE #DOM
    3 #DOM-N     (N8)
  2 REDEFINE #DOM
    3 #MM-M      (A2)
    3 #DD-M      (A2)
    3 #YYYY-M    (A4)
  2 #DIV         (A8)
  2 REDEFINE #DIV
    3 #DIV-N     (N8)
  2 REDEFINE #DIV
    3 #MM-V      (A2)
    3 #DD-V      (A2)
    3 #YYYY-V    (A4)
  2 #SCR-ROLE-2-CD  (A1)
  2 #ORIG-ROLE-2-CD (A1)
  2 #ROLE-DESC      (A15)
  2 #LST-UPD        (A10)
  2 #LST-USR        (A20)
  2 #RL-TEXT        (A18)
  2 #CN-TEXT        (A3)
  2 #DOM-TEXT       (A18)
  2 #DOM-CD-TEXT    (A5)
  2 #DIV-TEXT       (A18)
  2 #DT-FORMAT-TEXT (A10)
  2 #ORG-DISPLAYED  (L)
  2 #DEFAULT-LN     (A25)

1 #CONTROL-VARS  (C/7) 1 REDEFINE #CONTROL-VARS
  2 #mod#                (C)
  2 #CNT-CV              (C)
  2 #role-mod#           (C)
  2 #BCC-MOD#            (C)
  2 #DATE-CV             (C)
  2 #ORG-NAME-CV         (C)
  2 #DOM-CV              (C)

1 #D                     (D)
1 #LST-UPDATE            (A10)
1 #PRESS-PF3-TEXT        (A40)
1 #ENTER-PF3-TEXT        (A34)
1 #ENTER-PF3-TEXT-2      (A35)
1 #ACTION-TEXT           (A25)
1 #PF-TEXT               (A25)
1 #CONTACT-TEXT          (A3)
1 #SAVE-SSN              (N9)
1 #SAVE-LAST-NAME        (A25)
1 #FIRST-TIME            (L)
1 #NOT-FOUND             (L)
1 #PF3                   (L)
1 #DUP-SSN               (L)
1 #BD-LINKS              (N6/100)
1 #I                     (I4)
1 #CID-PARM              (N8)
1 #MMDDYYYY              (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-C     (A2)
  2 #DD-C     (A2)
  2 #YYYY-C   (A4)
  2 REDEFINE #YYYY-C
    3 #YYYY-N (N4)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 #YYYYMMDD-2  (N8)
1 REDEFINE #YYYYMMDD-2
  2 #YYYYMMDD-2-A (A8)

1 STATUS VIEW OF A-STATUS
  2 STATUS-CODE-SP       (A1)
  2 FORMER-SPOUSE-STATUS (A1)

1 CNT VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 SSN
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 GENDER-CD
  2 BIRTH-CERT-CODE
  2 DATE-OF-DEATH

1 CNT-2 VIEW OF A-CONTACTS
  2 SSN
1 #FUNCTION      (A1)
1 v_A25          (A25)
END-DEFINE

DEFINE WINDOW Contact
  SIZE 20 * 53
  BASE 4/3
  FRAMED ON POSITION SYMBOL AUTO
*
DEFINE WINDOW Last-Name
  SIZE 9 * 48
  BASE 9/5
  FRAMED ON POSITION SYMBOL AUTO
*
SET KEY ALL
FORMAT IP=OFF

FIND CNT WITH CONTACT-ID = #CO-ID-2
  MOVE LAST-NAME TO #SAVE-LAST-NAME
END-FIND
*
MAIN.
*
REPEAT
  CALLNAT 'GUP001'
    #API
    #UI
    #CONTROL-VARS (*)
  #BD-LINKS (*) := FUNCTION-BD-LINKS(<#CO-ID-2>)(*)
  MOVE #SSN TO #SAVE-SSN                                 /* RE1
  IF #BD-LINKS (*) NE 0
    COMPRESS 'Beneficiary of CN' #BD-LINKS (1) TO #MSG
    FOR #I 2 5
      IF #BD-LINKS (#I) = 0
        ESCAPE BOTTOM
      END-IF
      COMPRESS #MSG #BD-LINKS (#I) TO #MSG
    END-FOR
    COMPRESS #MSG '.' TO #MSG LEAVING NO
  ELSE
    RESET #MSG
  END-IF
  IF #UPDATED
    COMPRESS #MSG 'Transaction completed.' TO #MSG
  END-IF
  REPEAT
    PERFORM INPUT-MAP
    IF *PF-KEY = 'PF3'
        or #cmd = '3'
      ESCAPE BOTTOM (MAIN.)
    END-IF
    PERFORM VALIDATE-FIELDS
    IF #CONFIRM-LN = 'N' OR = 'Y'
      RESET #CONFIRM-LN
      ESCAPE TOP
    END-IF
    IF #NO-ERRORS                /* Update Contact cluster
      PERFORM UPDATE-CONTACT-CLUSTER
    END-IF
    UNTIL #NO-ERRORS
  END-REPEAT
  ESCAPE BOTTOM
END-REPEAT

SET WINDOW OFF
IF #UPDATED
  RESET #MSG
END-IF

*****************************
DEFINE UPDATE-CONTACT-CLUSTER
*****************************
IF *PF-KEY = 'PF5'
    OR #CMD = '5'
  ASSIGN  #DELETE = TRUE     /* Delete database record
ELSE
  RESET #DELETE
END-IF
CALLNAT 'GUP002'
  #DELETE
  #UPDATE
  #CGA-CLERK
  #CGA-ORIG-ID          /* Parent/Member ID
  #CO-ID-1              /* Parent/Member Contact ID
  #MSG
  #UI
IF #UPDATE
  ASSIGN #UPDATED = TRUE
END-IF
END-SUBROUTINE

DEFINE INPUT-MAP
*      =========
**IF *INIT-USER = 'RESTABI' OR = 'SVERMA' OR = 'LRASEKH' OR = 'JCUDJOE' OR = 'GUENTCH'   /* RE1
MOVE 'CI:' TO #CONTACT-TEXT
MOVE #CO-ID-2 TO #CID
**END-IF
*
IF #MODE = 'V'
  MOVE 'Press ENTER or PF3 to exit.' TO #PRESS-PF3-TEXT #ENTER-PF3-TEXT-2
  MOVE 'Exit---Exit--Cnt--Reln' TO #ACTION-TEXT
  MOVE 'Enter--PF3---PF4--PF12' TO #PF-TEXT
  MOVE 'Organization Name:' TO #ENTER-PF3-TEXT    /* For Organization map
ELSE
  MOVE 'Press PF3 to exit without changes.' TO #PRESS-PF3-TEXT
  MOVE 'Enter or modify Organization Name.' TO #ENTER-PF3-TEXT
  MOVE 'Press Enter to save, PF3 to cancel.' TO #ENTER-PF3-TEXT-2
  IF #CO-ID-2 = 0
      or #CGA-ORIG-COMMAND = 'CG'
    MOVE 'Exit---Exit--Cnt--Reln' TO #ACTION-TEXT
    MOVE 'Enter--PF3---PF4--PF12' TO #PF-TEXT
  ELSE
    MOVE 'Exit---Exit--Cnt--Reln' TO #ACTION-TEXT
    MOVE 'Enter--PF3---PF4--PF12' TO #PF-TEXT
  END-IF
END-IF
*
MOVE (AD=PI) TO #DATE-CV

INPUT WINDOW='Contact' (CV=#mod#) WITH TEXT #MSG MARK #FIELDX
  "-----------------Contact Details------------------" (AD=IO) /
  1x #UI.#RL-TEXT    (AD=IO)
  #UI.#SCR-ROLE-2-CD (AD=IO HE='HELPRLCD')
  #UI.#ROLE-DESC     (AD=IO)
  /
  1x "Last Name        :" (AD=IT) #UI.#LAST-NAME   (AD=M CV=#CNT-CV)    /
  1x "First Name       :" (AD=IT) #UI.#FIRST-NAME  (AD=M CV=#CNT-CV)    /
  1x "Middle Name      :" (AD=IT) #UI.#MIDDLE-NAME (AD=M CV=#CNT-CV)    /
  1x "Suffix           :" (AD=IT) #UI.#SUFFIX      (AD=M CV=#CNT-CV)    /
  1x "Gender           :" (AD=I) #UI.#GENDER-CD   (AD=M CV=#CNT-CV)    /
  1x "SSN              :" (AD=I) #UI.#SSN-A       (AD=M CV=#CNT-CV) "(999999999)"  (AD=I) /
  1x "Date of Birth    :" (AD=I) #UI.#DOB         (AD=M CV=#DATE-CV)              " (MMDDYYYY)"  (AD=I)
  1x "Code:"              (AD=I) #UI.#BIRTH-CERT-CODE (AD=M CV=#DATE-CV HE='HELPCTCD')                /
  1x "Date of Death    :" (AD=I) #UI.#DOD         (AD=M CV=#DATE-CV)     " (MMDDYYYY)"   (AD=I)       /
  1x #UI.#DOM-TEXT       (AD=IO) #UI.#DOM         (AD=M CV=#DATE-CV) 2x #DT-FORMAT-TEXT (AD=IO) 1X
  #UI.#DOM-CD-TEXT        (AD=IO)
  #UI.#MARRIAGE-CERT-CODE (AD=M CV=#DATE-CV HE='HELPCTCD')                                             /
  1x  #UI.#DIV-TEXT      (AD=IO) #UI.#DIV (AD=M CV=#DATE-CV) 2x #DT-FORMAT-TEXT (AD=IO)                /
  #PRESS-PF3-TEXT (AD=IO)                                                                              /
  "--------------------------------------------------" (AD=I)
  / 1x #PF-TEXT  (AD=IO)
  12x #UI.#CN-TEXT   (AD=IO)      #UI.#CN        (AD=O EM=ZZZZZZ)
  / 1x #ACTION-TEXT (AD=IO)
  12x #CONTACT-TEXT  (AD=IO)      #UI.#CID       (AD=O EM=ZZZZZZZZ)
  / 1x 'Updated on'  (AD=IO)
  #UI.#LST-UPD   (AD=O) 'by'  (AD=IO)   #UI.#LST-USR   (AD=O)

*
RESET #MSG
IF *PF-KEY = 'PF1'
  REINPUT 'No HELP available at this time.'
END-IF
IF *PF-KEY = 'PF3'
    OR #CMD = '3'
  ESCAPE ROUTINE
END-IF
IF *PF-KEY = 'PF12'
  CALLNAT 'GPF12SXX' #CO-ID-2
  ESCAPE ROUTINE
END-IF
IF *PF-KEY = 'PF4'
  CALLNAT 'GFUNCSXX' #CO-ID-2 CNT.ID-NUMBER #UPDATE #FUNCTION
  ESCAPE ROUTINE
END-IF
IF #CONTROL-VARS (*) NOT MODIFIED AND #SSN = #SAVE-SSN             /* RE1
    AND *PF-KEY NE 'PF5' AND #UI.#LAST-NAME = #SAVE-LAST-NAME
    AND (#UI.#GENDER-CD = 'M' OR = 'F')                        /* RE3
  REINPUT "No changes made. Press PF3 to exit."
END-IF

END-SUBROUTINE

*********************************
DEFINE SUBROUTINE VALIDATE-FIELDS
*********************************
RESET #NO-ERRORS
IF *PF-KEY = 'PF5'
    or #cmd = '5'
  ESCAPE ROUTINE
END-IF

GET STATUS #STATUS-ISN
IF #CONTACT-ISN > 0
  GET CNT #CONTACT-ISN
END-IF
*
IF #CO-ID-1 NE #CO-ID-2 AND #UI.#LAST-NAME = ' '                           /* RE2
  SET WINDOW 'Last-Name'
  INPUT WINDOW='Last-Name' USING MAP 'C2236MCB'   /* with inline rule to validate #CONFIRM-LN
  SET WINDOW OFF
  IF #CONFIRM-LN = 'Y'
    MOVE #DEFAULT-LN TO #UI.#LAST-NAME
  END-IF
  ESCAPE ROUTINE
END-IF
*
#MSG := " "                                /*  bz1
v_A25 := #UI.#LAST-NAME                    /*  bz1
PERFORM VALIDATE-NAMES                     /*  bz1
IF #MSG <>  " "                            /*  bz1
    COMPRESS "Last Name." #MSG INTO #MSG   /*  bz1 
    #FIELDX := POS(#UI.#LAST-NAME)         /*  bz1
      ESCAPE ROUTINE                       /*  bz1
END-IF                                     /*  bz1
v_A25 := #UI.#FIRST-NAME                   /*  bz1
PERFORM VALIDATE-NAMES                     /*  bz1
IF #MSG <>  " "                            /*  bz1
    COMPRESS "First Name." #MSG INTO #MSG   /*  bz1 
    #FIELDX := POS(#UI.#FIRST-NAME)        /*  bz1
      ESCAPE ROUTINE                       /*  bz1
END-IF                                     /*  bz1


IF #SSN NE #SAVE-SSN                                     /* RE1
*    DISPLAY #SSN #SAVE-SSN
  FIND CNT-2 WITH SSN = #SSN
*    DISPLAY 'IN FIND'
    ASSIGN #MSG = 'SSN already exists in the system. Please verify.'
    ASSIGN #FIELDX = POS(#SSN)
    ESCAPE ROUTINE
  END-FIND
END-IF
*
MOVE #DOB TO #MMDDYYYY
IF #DOB NE ' '
  IF  #DOB-N NE MASK (MMDDYYYY)
    COMPRESS  #DOB '- format error. MMDDYYYY is expected' INTO #MSG
    ASSIGN #FIELDX = POS( #DOB )
    ESCAPE ROUTINE
  END-IF
  IF #YYYY-N < 1800
    COMPRESS  #DOB 'Year should be greater then 1800' INTO #MSG
    ASSIGN #FIELDX = POS( #DOB )
    ESCAPE ROUTINE
  END-IF
END-IF
*
MOVE #DOD TO #MMDDYYYY
IF (#DOD NE ' ' AND #DOD-N NE MASK (MMDDYYYY)) OR
    (#DOD NE ' ' AND #YYYY-N < 1900)
  COMPRESS  #DOB '- format error. MMDDYYYY is expected' INTO #MSG
  ASSIGN #FIELDX = POS( #DOD )
  ESCAPE ROUTINE
ELSE
  IF CNT.ID-NUMBER > 0 AND #DOD NE ' ' AND CNT.DATE-OF-DEATH = 0
    ASSIGN #MSG =  'For this member please use DN command'
    ASSIGN #FIELDX = POS( #DOD )
    ESCAPE ROUTINE
  END-IF
END-IF
*
MOVE #DOM TO #MMDDYYYY
IF (#DOM NE ' ' AND #DOM-N NE MASK (MMDDYYYY)) OR
    (#DOM NE ' ' AND #YYYY-N < 1900)
  ASSIGN #MSG =  'Invalid date format - must be MMDDYYYY'
  ASSIGN #FIELDX = POS( #DOM )
  ESCAPE ROUTINE
END-IF
*
IF #DOM NE ' ' AND (#SCR-ROLE-2-CD = 'K' OR = 'G' OR = 'D')
  ASSIGN #MSG =  'Date of Marriage not allowed for this Role.'
  ASSIGN #FIELDX = POS( #DOM )
  ESCAPE ROUTINE
END-IF
*
MOVE #DOM TO #MMDDYYYY
COMPRESS #YYYY-C #MM-C #DD-C  INTO #YYYYMMDD-A LEAVING NO
IF #YYYYMMDD GT *DATN
  ASSIGN #MSG =  'Future date invalid.'
  ASSIGN #FIELDX = POS( #DOM )
  ESCAPE ROUTINE
END-IF
*
MOVE #DIV TO #MMDDYYYY
IF (#DIV NE ' ' AND #DIV-N NE MASK (MMDDYYYY)) OR
    (#DIV NE ' ' AND #YYYY-N < 1900)
  ASSIGN #MSG =  'Invalid date format - must be MMDDYYYY'
  ASSIGN #FIELDX = POS( #DIV )
  ESCAPE ROUTINE
END-IF
*
IF #DIV NE ' ' AND (#SCR-ROLE-2-CD = 'K' OR = 'G' OR = 'D')
  ASSIGN #MSG =  'Date of Divorce not allowed for this Role.'
  ASSIGN #FIELDX = POS( #DIV )
  ESCAPE ROUTINE
END-IF
*
IF #DOM NE ' ' AND #DIV NE ' '
  MOVE #DIV TO #MMDDYYYY
  COMPRESS #YYYY-C #MM-C #DD-C  INTO #YYYYMMDD-2-A LEAVING NO
  IF #YYYYMMDD-2 LE #YYYYMMDD
    ASSIGN #MSG =  'Marriage must precede divorce.'
    ASSIGN #FIELDX = POS( #DOM )
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF #DOD NE ' ' AND #DOM NE ' '
  MOVE #DOD TO #MMDDYYYY
  COMPRESS #YYYY-C #MM-C #DD-C  INTO #YYYYMMDD-2-A LEAVING NO
  IF #YYYYMMDD-2 LE #YYYYMMDD
    ASSIGN #MSG =  'Marriage must precede death.'
    ASSIGN #FIELDX = POS( #DOM )
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF #DIV NE ' ' AND #SCR-ROLE-2-CD NE 'X'
  ASSIGN #MSG =  'Role must be "X" if Date of Divorce is entered.'
  ASSIGN #FIELDX = POS( #DIV )
  ESCAPE ROUTINE
END-IF

ASSIGN #NO-ERRORS = TRUE
DECIDE FOR FIRST CONDITION
  WHEN #UI.#GENDER-CD = ' ' AND #CO-ID-1 = #CO-ID-2
    ASSIGN #MSG = 'Enter Gender, please'
    ASSIGN #FIELDX = POS(#UI.#GENDER-CD)
  WHEN #UI.#SCR-ROLE-2-CD = ' '
    ASSIGN #MSG = 'Enter Role/Relationship'
    ASSIGN #FIELDX = POS(#SCR-ROLE-2-CD)
  WHEN NOT (#SCR-ROLE-2-CD = 'A' OR = 'D' OR = 'E' OR = 'F' OR = 'G' OR = 'I' OR = 'K'
      OR = 'M' OR = 'P' OR = 'B' OR = 'R' OR = 'S' OR = 'T' OR = 'X')
    ASSIGN #FIELDX = POS(#SCR-ROLE-2-CD)
    ASSIGN #MSG = 'Invalid Role/Relationship'
  WHEN #SCR-ROLE-2-CD = 'S' AND #ORIG-ROLE-2-CD NE 'S'
      AND STATUS-CODE-SP = 'S'
    ASSIGN #MSG = 'Spouse already exists for Member.'
    ASSIGN #FIELDX = POS(#SCR-ROLE-2-CD)
  WHEN #SCR-ROLE-2-CD = 'X' AND #ORIG-ROLE-2-CD NE 'X'
      AND FORMER-SPOUSE-STATUS = 'F'
    ASSIGN #MSG = 'Former spouse already exists for Member.'
    ASSIGN #FIELDX = POS(#SCR-ROLE-2-CD)
  WHEN #SCR-ROLE-2-CD NE ' ' AND #LAST-NAME = ' '
    ASSIGN #MSG = 'Enter Last Name'
    ASSIGN #FIELDX = POS(#LAST-NAME)
*  WHEN #SCR-ROLE-2-CD NE ' ' AND #FIRST-NAME = ' '          /* RE1
*    ASSIGN #MSG = 'Enter First Name'
*    ASSIGN #FIELDX = POS(#FIRST-NAME)
  WHEN NOT #GENDER-CD = 'F' OR = 'M'                         /* RE3
    ASSIGN #MSG = 'Gender must be F OR M'                    /* RE3
    ASSIGN #FIELDX = POS(#GENDER-CD)
  WHEN #SSN NE 0
      AND #SSN NE MASK (NNNNNNNNN)
    ASSIGN #MSG = 'SSN must be numeric'
    ASSIGN #FIELDX = POS(#SSN)
  WHEN NOT (#BIRTH-CERT-CODE = 'N' OR = 'A' OR = 'B' OR = 'U' OR = 'V' OR = 'I' OR = ' ')
    ASSIGN #MSG = 'Invalid Birth Cert. Code'
    ASSIGN #FIELDX = POS(#BIRTH-CERT-CODE)
  WHEN NOT (#BIRTH-CERT-CODE = 'N' OR = 'A' OR = 'B' OR = 'U' OR = 'V' OR = 'I' OR = ' ')
    ASSIGN #MSG =  'Invalid Birth Cert. Code'
    ASSIGN #FIELDX = POS( #BIRTH-CERT-CODE )
  WHEN ANY
    RESET #NO-ERRORS
    ESCAPE ROUTINE
  WHEN NONE IGNORE
END-DECIDE
*
**IF #CONTACT-ISN > 0                 /* RE1
*    AND #SSN NE CNT.SSN
*  MOVE #CO-ID-1 TO #CID-PARM
*  CALLNAT 'GCSSNSXX'
*    #CID-PARM
*    #UI.#SSN
*    CNT.FIRST-NAME
*    CNT.MIDDLE-NAME
*    CNT.LAST-NAME
*    CNT.SUFFIX
*    CNT.GENDER-CD #DOB
*    CNT.BIRTH-CERT-CODE
*    #DOD
*    CNT.ID-NUMBER
*    #LST-UPD
*    #LST-USR
*    #NOT-FOUND
*    #PF3
*  IF #NOT-FOUND
*    IGNORE
*  ELSE
*    MOVE TRUE TO #DUP-SSN
*    IF NOT #PF3
*      MOVE #CID-PARM TO #CID
*    END-IF
*    ESCAPE ROUTINE
*  END-IF
**END-IF
*
END-SUBROUTINE
DEFINE SUBROUTINE VALIDATE-NAMES                   /*  bz1
                                                   /*  bz1
IF v_A25 = MASK(' ')                               /*  bz1
    #MSG := "First symbol is blank"                /*  bz1
    ESCAPE ROUTINE                                 /*  bz1
END-IF                                             /*  bz1
EXAMINE FULL v_A25 FOR FULL " " REPLACE "Z"        /*  bz1
EXAMINE v_A25 FOR '-' REPLACE 'Z'                  /*  bz1
EXAMINE v_A25 FOR "'" REPLACE 'Z'                  /*  bz1
EXAMINE v_A25 FOR '"' REPLACE 'Z'                  /*  bz1
EXAMINE v_A25 FOR '/' REPLACE 'Z'                  /*  bz1
IF  v_A25 NE MASK(UUUUUUUUUUUUUUUUUUUUUUUUU)       /*  bz1
    #MSG := "Incorrect symbol"                     /*  bz1
    ESCAPE ROUTINE                                 /*  bz1
END-IF                                             /*  bz1
END-SUBROUTINE                                     /*  bz1


END
