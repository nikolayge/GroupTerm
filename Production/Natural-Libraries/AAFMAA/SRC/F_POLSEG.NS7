* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F_POLSEG                                              *
* DESCRIPTION  : Find policy segment from policy-number                *
* HISTORY      :                                                       *
* 06/23/2011 VXT VT1 Return active policy segment if it exists otherwise
*                    return inactive one.                              *
* 11/05/2017 YAK YK1 Re-write function using POLICY-ID field           *
************************************************************************
DEFINE FUNCTION F_POLICY_SEGMENT RETURNS (I2)
*
  DEFINE DATA
  PARAMETER
  1 #POLICY-NUMBER         (A16) BY VALUE
  1 #ID-NUMBER             (A6)  BY VALUE
  LOCAL
  1 #ID-NUMBER-N   (N6)
  1 #WORK-POLICY-NUMBER    (A16)
  1 #I1                    (I2)
*
  1 ST-V VIEW OF A-STATUS
    2 ID-NUMBER
    2 C*ACTIVE-SEGMENTS
    2 STATUS    (9)                              /* vt1
    2 POLICY-ID (9)
*
  END-DEFINE
************************************************************************
*
RESET F_POLICY_SEGMENT
#ID-NUMBER-N := VAL(#ID-NUMBER)
FIND(1) ST-V WITH ID-NUMBER = #ID-NUMBER-N
  FOR #I1 = 1 TO C*ACTIVE-SEGMENTS
    IF ST-V.POLICY-ID(#I1) = #POLICY-NUMBER
      IF ST-V.STATUS(#I1) = 'D'
        F_POLICY_SEGMENT := #I1
        ESCAPE ROUTINE
      ELSE
        IF ST-V.STATUS(#I1) = 'E' OR = 'F'
          F_POLICY_SEGMENT := #I1
        END-IF
      END-IF
    END-IF
  END-FOR
END-FIND
END-FUNCTION
END
