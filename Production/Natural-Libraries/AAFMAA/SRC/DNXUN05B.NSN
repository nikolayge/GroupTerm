* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
****************************************************************************************
* SUBPROGRAM-ID: DNXUN05B - formerly W5700SWN - Create file with Death notification report
* bz 08/25/2010
****************************************************************************************
* DESCRIPTION: RANK MAINTENANCE
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  03/30/2011  RE1  Call function in place of hardcoded ID's; call function to 
*                       determine environment (dev or prod)
* RSE  04/29/2011  RE2  Add Death Status Code 'B' for Beneficiary Final
* RSE  08/15/2011  RE3  Change function to check by environment
* bz1              (OI-1495)  change code for email
**************************************************************************
*
DEFINE DATA
PARAMETER USING DNXUA005
PARAMETER
1 p_CLERK (A3)
1 v_Mode   (A1)
LOCAL USING GPRL0EML
LOCAL
1 TRM-V VIEW OF A-TERMINALS
  2 CLERK-ID              (A3)     /* F D
  2 INIT-USER
  2 COMMENTS
  2 EMAIL                 (A60)
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID (N8)
  2 ID-NUMBER (N6)
  2 RANK-ID (N6)
  2 RANK-PROTECT (A1)
  2 RANK-PROTECT-DATE (N8)
  2 LAST-RANK-DATE-UPD (N8)
  2 LAST-RANK-USER-UPD (A8)
  2 PREV-RANK-ID (N6)
  2 MILITARY-STATUS (A1)
  2 SSN-PROTECT (A1)
  2 SSN-PROTECT-DATE (N8)
  2 PREFIX-TITLE (A25)
  2 FULL-NAME
    3 FIRST-NAME (A25)
    3 MIDDLE-NAME (A25)
    3 LAST-NAME (A25)
    3 SUFFIX (A10)
  2 GENDER-CD (A1)
  2 DATE-OF-BIRTH (N8)
  2 SSN (N9)
  2 DATE-OF-DEATH (N8)
  2 DATE-OF-DEATH-REPORTED (N8)
  2 DATE-OF-DEATH-STATUS-CODE (A1)
  2 DATE-OF-DEATH-STATUS-DATE (N8)
  2 DEATH-NOTIFIED-BY-CONTACT-ID (N8)
  2 WEBSITE-URL (A70)
  2 HOMICIDE-SUICIDE-ACCIDENT-IND (A1)
  2 BIRTH-CERT-CODE (A1)
  2 DEATH-CERT-CODE (A1)
  2 BURIAL-LOC (A1)
  2 BURIAL-INFO (A35)
  2 PLACE-OF-DEATH (A50)
  2 CAUSE-OF-DEATH (A50)
  2 PLACE-OF-BIRTH (A50)
  2 DELETE-FLAG (A1)
  2 SPECIAL-PAY-IND (A1)
  2 LAST-USER-UPD (A8)
  2 LAST-DATE-UPD (N8)
  2 LAST-TIME-UPD (N7)
  2 FIRM-ORGANIZATION-NAME (A75)
  2 DEATH-DOCUMENT-VALIDATION-CODE (A10)
  2 CONTACT-REMARKS (A75/1:3)
*
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 CONTACT-2-ID
  2 ROLE-1-CD
  2 ROLE-2-CD
*
1 CODES VIEW OF A-GEN-TABLE
  2 GEN-ALPHA-VALUE
  2 GEN-NUMBER-VALUE
  2 ADDL-INFO
*
1 v_EMAIL
  2 v_From_L (A10) INIT <"From:">
  2 v_From  (A60)
  2 v_Lbr1  (A2)   INIT <H'0D0A'>
  2 v_To_L  (A10)  INIT <"To:">
  2 v_To    (A100) INIT <"deathclaim@aafmaa.com">
  2 v_Lbr2  (A2)   INIT <H'0D0A'>
  2 v_Cc_L  (A10) INIT <"Cc:">
  2 v_Cc    (A100)
  2 v_Lbr3  (A2)   INIT <H'0D0A'>
  2 v_S_L   (A10)  INIT <"Subject:">
  2 v_S     (A100) INIT <"Death Notification ">
  2 v_Lbr4  (A2)   INIT <H'0D0A'>
  2 v_Empty (A80) INIT <" ">
  2 v_Lbr5  (A2)   INIT <H'0D0A'>
*
1 #status         (a25)
1 nl              (A2)     INIT <H'0D0A'>
1 v_Line          (A79)
1 v_shell         (A) DYNAMIC
1 rc              (N3)
1 v_PN            (I1)     INIT <21>
1 v_RC            (I4)
*
1 v_PRT_NAME      (A80)
1 File_DIR        (A60) INIT <"/usr/SAG/AAFMAA/batch/workfiles/TEMP">
1 File_Name       (A79)
1 File_Email_Name   (A) DYNAMIC
*
1 v_FTP           (A79)       INIT <"/usr/SAG/AAFMAA/batch/workfiles/PROD/PGM/ftpSAS">
1 v_FTP_WIN       (A) DYNAMIC /* INIT <"/ftp/Death_Notify">      RE1
*
1 v_MSG (A60)
1 #SCR-MESSAGE    (A75)
1 #FUNERAL-CONTACT-ID (N8)
1 #NOTIFY-CI      (N8)
1 #ROLE-DESC      (A15)
1 #GENDER-DESC    (A6)
1 #PREFIX-TITLE   (A6)
1 #PREFIX-TITLE-DESC(A18)
1 #SCR-FIRM-ORGANIZATION-NAME (A75)
1 #SCR-FIRST-NAME (A25)
1 #SCR-GENDER-CD (A1)
1 #SCR-LAST-NAME (A25)
1 #SCR-MIDDLE-NAME (A25)
1 #SCR-PREFIX-TITLE (A25)
1 #SCR-ROLE (A1)
1 #SCR-SUFFIX (A10)
1 #SCR-WEBSITE-URL (A70)
1 #CONTACT-REMARKS (A75/3)
**1 #N               (N3)    /* RE1    RE3
1 v_A7     (A7)  /* bz1
END-DEFINE
*
CALLNAT 'ADRN0004' #CONTACT-ID #H1 #H2
*
IF F-IS-DEV(<>)
  v_FTP_WIN := '/ftp/TEST'  
ELSE
  v_FTP_WIN := '/ftp/Death_Notify'
END-IF                                             /*  RE3 END
*
if #scr-dod-st = 'P'
  move 'Pending Confirmation' to #status #scr-dod-st-txt
*  if NOT (*INIT-USER = 'RESTABI' OR = 'LRASEKHI' OR = 'JCUDJOE' OR = 'SVERMA' OR = 'BZASLAV'
*    OR = 'KATZENB' OR = 'GUENTCH' OR = 'VENETKA' OR = 'XZBSL7')
*  IF #N = 223                            /* RE1           RE3
  IF NOT F-IS-DEV(<>)                                   /* RE3
    FIND CODES WITH SEC-KEY = 'CODE038'
      COMPRESS GEN-ALPHA-VALUE ',' p_To INTO p_To
    END-FIND
  END-IF
else
  IF #scr-dod-st = 'C'                                  /* RE2
    move 'Confirmed' to #status #scr-dod-st-txt
    v_A7 := 'CODE039'                                   /* bz1
  ELSE                                                  /* RE2
    move 'Beneficiary Final' to #status #scr-dod-st-txt /* RE2
    v_A7 := 'CODE066'                                   /* bz1
  END-IF                                                /* RE2

  IF ( NOT F-IS-DEV(<>) ) 
      FIND CODES WITH SEC-KEY = v_A7         /* 'CODE039'  bz1
        COMPRESS GEN-ALPHA-VALUE ',' p_To INTO p_To
      END-FIND
   END-IF 
END-IF

*
 FIND (1) TRM-V WITH CLERK-ID = p_CLERK
   IF NO RECORD FOUND
     WRITE "Clerk not found" p_CLERK
     ESCAPE ROUTINE
*     IGNORE /* Error No records for CLERK-ID
   END-NOREC
*  IF #N = 223                            /* RE1            RE3
   IF NOT F-IS-DEV(<>)                                   /* RE3
     IGNORE
   ELSE
     MOVE EMAIL TO p_To                          
   END-IF                                 /* RE1 END
 END-FIND
*
  FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID
    IF ROLE-2-CD = 'W'
        AND ROLE-1-CD = 'M'
      #FUNERAL-CONTACT-ID := CONTACT-2-ID
      ESCAPE BOTTOM
    END-IF
  END-FIND

  FIND CONTACT WITH CONTACT-ID = #FUNERAL-CONTACT-ID
    #SCR-PREFIX-TITLE          := PREFIX-TITLE
    #SCR-FIRST-NAME            := FIRST-NAME
    #SCR-MIDDLE-NAME           := MIDDLE-NAME
    #SCR-LAST-NAME             := LAST-NAME
    #SCR-SUFFIX                := SUFFIX
    #SCR-WEBSITE-URL           := WEBSITE-URL
    #SCR-GENDER-CD             := GENDER-CD
    #SCR-FIRM-ORGANIZATION-NAME := FIRM-ORGANIZATION-NAME
    #CONTACT-REMARKS (*)       := CONTACT-REMARKS (*)
    #LAST-DATE-UPD             := LAST-DATE-UPD
    #LAST-USER-UPD             := LAST-USER-UPD
  END-FIND

  FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
    MOVE DEATH-NOTIFIED-BY-CONTACT-ID TO #NOTIFY-CI
  END-FIND

  FIND CONTACT-REL WITH CONTACT-2-ID = #NOTIFY-CI
    IF CONTACT-1-ID = #CONTACT-ID
      #ROLE-DESC := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
    END-IF
  END-FIND

  FIND CONTACT WITH CONTACT-ID = #NOTIFY-CI
    IF GENDER-CD = 'M'
      MOVE 'Male' TO #GENDER-DESC
    ELSE
      MOVE 'Female' TO #GENDER-DESC
    END-IF
    MOVE PREFIX-TITLE TO #PREFIX-TITLE
    FIND CODES WITH SEC-KEY = 'CODE050'
      IF GEN-ALPHA-VALUE = PREFIX-TITLE
        MOVE ADDL-INFO TO #PREFIX-TITLE-DESC
        ESCAPE BOTTOM
      END-IF
    END-FIND
  END-FIND

    v_PRT_NAME := PRT_FN_GET(<v_PN, v_RC>)
  COMPRESS File_DIR "/" "PRT_21_" p_CLERK ".txt" INTO v_PRT_NAME LEAVE NO SPACE
  v_RC := PRT_FN_PUT(<v_PN, v_PRT_NAME>)
    IF v_RC <> 0         /* Error in Get_Printer_Name
      v_MSG := " Warning: Automated process is not available"
      PERFORM DO-ERROR
      ESCAPE ROUTINE
  END-IF

  DEFINE PRINTER (21) OUTPUT 'LPT21'
  FORMAT (21) PS=100
  WRITE (21) NOTITLE USING FORM 'DNXUF005'
  WRITE (21) NOTITLE ' '
  WRITE (21) NOTITLE USING FORM 'DNXUF07A'
  WRITE (21) NOTITLE ' '
  WRITE (21) NOTITLE USING FORM 'DNXUF006'
  IF v_Mode = '1'
    WRITE (21) #SCR-MESSAGE // "Updated by " COMMENTS "on" *DATV 2x *TIME (EM=XXXXXXXX)
  ELSE
    WRITE (21) #SCR-MESSAGE // "Corrected by " COMMENTS "on" *DATV 2x *TIME (EM=XXXXXXXX) /
    "Email notification was not sent"
  END-IF
  CLOSE PRINTER (21)

COMPRESS "Death_Notify_CN_" #ID-NUMBER ".rtf" INTO File_Name LEAVING NO SPACE
COMPRESS v_FTP " " v_PRT_NAME " " File_Name " " v_FTP_WIN INTO v_shell
call 'shcmd' v_shell
rc := RET('shcmd')

IF rc <> 0
   v_MSG := " Warning: FTP Error. Automated process is not available"
   PERFORM DO-ERROR
   ESCAPE ROUTINE
END-IF
*
IF v_Mode = '1'  /* 1 stands for the first death notification
  COMPRESS File_DIR "/" File_Name INTO File_Email_Name LEAVING NO SPACE
  IF TRM-V.EMAIL = " "
    THEN  p_Cc := "bzaslavskiy@aafmaa.com"
          COMPRESS "Email address for " INIT-USER "-" COMMENTS " is blank" INTO v_MSG
    ELSE  p_Cc := TRM-V.EMAIL
  END-IF
  COMPRESS "'" v_S #status "  CN:" #ID-NUMBER "  from " COMMENTS  *DATV  "'" INTO p_Subj

  COMPRESS "\\192.168.50.71" v_FTP_WIN "\" File_Name INTO v_Line LEAVING NO SPACE
  EXAMINE v_line FOR "/" REPLACE "\"
*  COMPRESS "This is a SYSTEM GENERATED E-MAIL, please do not respond to the E-MAIL address specified above." INTO p_MSG(1)
  COMPRESS  "To access report file click link below" INTO p_MSG(1)
  COMPRESS  v_Line INTO p_MSG(2)
  IF v_MSG <> " "
    COMPRESS nl nl "Note" nl nl v_MSG INTO p_MSG(3)
  END-IF
END-IF
**print '=' p_to /
**'=' p_subj /
**'=' p_cc  /
**'=' p_rc /
**'=' p_msg(1) /
**'=' p_msg(2) /
**'=' p_msg(3)
COMPRESS "'" P_to "'" INTO P_TO
CALLNAT 'GPRS0EML' p_EMAIL
IF p_RC <> 0 THEN WRITE  "Execution error"
                  IF p_RC = 101  WRITE  "Error: Address 'To' is blank" END-IF
END-IF
*
DEFINE SUBROUTINE DO-ERROR
    WRITE NOTITLE
          5/5 "Sorry, erorr ocurred during automated process"
          7/5 v_MSG
          9/5 "Please procceed with Print-Screen"
          15/5 "Press ENTER to continue" (I)
END-SUBROUTINE
END
