* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPBDSS ** Find SSN WITHIN BENEFICIARIES
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* RSE  09212009 RE1  Change PROCESS-IND/600K logic for Unification     *
************************************************************************
DEFINE DATA
LOCAL
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER     (N6)    /* D = bene desig (1-6)
 2 NOTE-DATE     (N8/60) /* P/A = PRIN/ALT DESIG
 2 REDEFINE NOTE-DATE    /* L = linked pols
  3 BENE-DATES-6-POLS (6)
   4 BENE-ISS-DT       (N6/7) /* (D, L)
   4 BENE-SPLIT-IND    (N1/7) /* (D, L)
   4 BENE-LST-UPD      (N8)   /* (D)
   4 BENE-STRT-PEND    (N8)   /* (D)
   4 BENE-FOL-UP-IND   (N1)   /* (D)
   4 BENE-OWN-TRNSF-DT (N8)   /* (D)
   4 BENE-SUC-OWN      (N1)   /* (d)
   4 BENE-NEW-LAYOUT   (N1)   /* (D)  /* was -setl-opt
   4 BENE-COM-DIS      (N2)   /* (D)  /* Common disaster
   4 BENE-PER-STR      (N1)   /* (D)  /* Per Stirpes
   4 BENE-FIL1         (A1)
 2 NOTE-CLERK-ID       (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-6-POLS  (6)
   4 BENE-STATUS      (A1)  /* (D)
   4 BENE-RESTRICT    (A1)  /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1) /* (D, P/A)
    5 BENE-SHARE-TYP   (A1) /* (D, P/A)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT          (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-6-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC    (5)
     6 BENE-TEXT   (A60) /* (D, P/A, text-line)
     6 REDEFINE BENE-TEXT
      7 BENE-TEXT-1ST-25   (A25)
      7 BENE-TEXT-LST-35   (A35)
 2 LST-BD-FINALIZATION-USER (A3)
*
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 WIDOW-FLAG
 2 DATE-OF-DEATH
 2 INTRO-FLAG
 2 PROCESS-IND
 2 NUMBER-MEMBERSHIPS
 2 ISSUE-DATE     (9)
 2 REDEFINE ISSUE-DATE
  3 ISSUE-DT-OCC  (9)
   4 ISS-DT-CENT     (N2)
   4 ISS-DT-YYMMDD   (N6)
 2 SPLIT-IND      (9)
 2 STATUS         (9)
 2 NUM-MBRSHIPS   (9)
*
1 #YYYYMMDD  (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (A4)
 2 REDEFINE #YYYY
   3 #CENT-N (N2)
   3 #YY-N   (N2)
 2 #MM     (A2)
 2 #DD     (A2)
*
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 #I4     (I2)
1 #I6     (I2)
*
1 #NT-ID   (N6)
1 #BENE-TEXT   (A60/10)
1 #NEW-LAYOUT         (L)
1 #NEW-LAYOUT-YN      (A1)
1 #FND-SSN            (L)
1 #FND-SSN-NEW-LAYOUT (L)
1 #HIDE-SSN           (L)
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
*
1 #NUM-RECS          (P6)
1 #BENES-250    (A250)
1 REDEFINE #BENES-250
 2 #BENES-125-A  (A125)
 2 #BENES-125-B  (A125)
*
1 #BENES
 2 #BENES-125-X  (A125/5)
*
END-DEFINE
* * * * * * * * *
MOVE TRUE TO #HIDE-SSN
COMPRESS 'CN'
 #TAB   'NT-CN'
 #TAB   'Iss-date'
 #TAB   'Type'
 #TAB   'Nu-LAYOUT'
 #TAB   'bene1'
 #TAB   'bene2'
 #TAB   'bene3'
 #TAB   'bene4'
 #TAB   'bene5'
 #TAB   'bene6'
 #TAB   'bene7'
 #TAB   'bene8'
 #TAB   'bene9'
 #TAB   'bene10'
  INTO #BENES-250  LEAVING NO
WRITE WORK FILE 2 #BENES
RESET #BENES-250
*
READ ST-V WITH MEMBER-KEY = '001900'
*  IF ST-V.ID-NUMBER GE 600000                      /* RE1
*    ESCAPE BOTTOM                                  /* RE1
*  END-IF                                           /* RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)             /* RE1
     ESCAPE TOP                                     /* RE1
  END-IF                                            /* RE1
*
*  WRITE 'Just read' ST-V.ID-NUMBER
  IF NUMBER-MEMBERSHIPS NE 0
    COMPUTE #NT-ID = ST-V.ID-NUMBER + 400000
    FIND NT-V WITH ID-NUMBER = #NT-ID
*     WRITE 'Just read NT' NT-V.ID-NUMBER
*
      FOR #I1 1 TO 6
        IF BENE-TEXT (#I1, 1, 1) = ' '
          ESCAPE TOP
        END-IF
*
        FOR #I2 1 TO 2
          FOR #I3 1 TO 5
            IF #I2 = 1
              MOVE #I3 TO #I4
            ELSE
              COMPUTE #I4 = #I3 + 5
            END-IF
*
            MOVE BENE-TEXT (#I1, #I2, #I3) TO #BENE-TEXT (#I4)
          END-FOR
        END-FOR
*
        IF BENE-NEW-LAYOUT (#I1) = 1
          MOVE TRUE TO #NEW-LAYOUT
        ELSE
          RESET #NEW-LAYOUT
        END-IF
        CALLNAT 'ATHSBDSS' #BENE-TEXT (1:10)
                #NEW-LAYOUT
                #FND-SSN
                #FND-SSN-NEW-LAYOUT
                #HIDE-SSN
*
        IF #FND-SSN-NEW-LAYOUT
          MOVE 'Y' TO #NEW-LAYOUT-YN
        ELSE
          RESET #NEW-LAYOUT-YN
        END-IF
*
        IF #FND-SSN OR #FND-SSN-NEW-LAYOUT
          WRITE ST-V.ID-NUMBER 'found SSN in beneficiary'
          PERFORM WRITE-FILE
*
*         FOR #I4 1 TO 10
*           IF #BENE-TEXT (#I4) NE ' '
*             DISPLAY
*              'CN' ST-V.ID-NUMBER
*               'I1' #I1
*               'I4' #I4
*               'new/lo' #NEW-LAYOUT
*               'fnd/ssn' #FND-SSN
*               'fnd/ssn/nu/lo' #FND-SSN-NEW-LAYOUT
*               #BENE-TEXT (#I4)
*           END-IF
*         END-FOR
        END-IF
      END-FOR
    END-FIND
  END-IF
END-READ
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-FILE
* * * * * * * * * * * * * * *
*
RESET #I6
COMPRESS ST-V.ID-NUMBER
 #TAB   NT-V.ID-NUMBER
 #TAB   BENE-ISS-DT (#I1, 1)
 #TAB   BENE-TYPE   (#I1, 1)
 #TAB   #NEW-LAYOUT-YN
 #TAB   #BENE-TEXT (1)
  INTO #BENES-250 LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
COMPRESS #BENES-250
 #TAB   #BENE-TEXT (2)
 #TAB   #BENE-TEXT (3)
  INTO #BENES-250 LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
COMPRESS #BENES-250
 #TAB   #BENE-TEXT (4)
 #TAB   #BENE-TEXT (5)
  INTO #BENES-250 LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
COMPRESS #BENES-250
 #TAB   #BENE-TEXT (6)
 #TAB   #BENE-TEXT (7)
  INTO #BENES-250 LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
COMPRESS #BENES-250
 #TAB   #BENE-TEXT (8)
 #TAB   #BENE-TEXT (9)
  INTO #BENES-250 LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
COMPRESS #BENES-250
 #TAB   #BENE-TEXT (10)
  INTO #BENES-250  LEAVING NO
*
PERFORM CHECK-OVERFLOW
*
IF #BENES-125-A NE ' '
  ADD 1 TO #I6
  MOVE #BENES-125-A TO #BENES-125-X (#I6)
  RESET #BENES-125-A
END-IF
*
IF #FND-SSN-NEW-LAYOUT
  WRITE WORK FILE 3 #BENES
ELSE
  WRITE WORK FILE 2 #BENES
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-OVERFLOW
* * * * * * * * * * * * * * * * *
*
IF #BENES-125-B NE ' '
  ADD 1 TO #I6
  MOVE #BENES-125-A TO #BENES-125-X (#I6)
*
  MOVE #BENES-125-B TO #BENES-125-A
  RESET #BENES-125-B
END-IF
*
END-SUBROUTINE
*
END
