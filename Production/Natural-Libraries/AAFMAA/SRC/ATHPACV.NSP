* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPACV   ** ADJUST ACV - Batch Version of C2350PVA **
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    04292004  None    Program written - cloned from C2350PVA      *
* DRW    08022005  DW1     Add #MISS-OVER to get clean Stow
* SAG  12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
************************************************************************
*
DEFINE DATA
LOCAL  USING C2350LVA
LOCAL
1 #MISS-OVER     (A1)     /*DW1
1 #SCR-CV                 (C)
1 #SCR-ISSUE-DATE         (A8)
1 REDEFINE #SCR-ISSUE-DATE
  2 #SCR-ISSUE-DATE-MM     (A2)
  2 #SCR-ISSUE-DATE-DD     (A2)
  2 #SCR-ISSUE-DATE-YYYY   (A4)
1 #SCR-ACV-PREM-ADJ-S     (A1)
1 #SCR-ACV-PREM-ADJ       (N6.2)
1 #SCR-ACV-INT-ADJ-S      (A1)
1 #SCR-ACV-INT-ADJ        (N5.2)
1 #SCR-ACV-COI-ADJ-S      (A1)
1 #SCR-ACV-COI-ADJ        (N3.2)
1 #SCR-ACV-TOT-ADJ        (N6.2)
1 #SCR-PREM-SHORTAGE      (N6.2)
1 #SCR-DATE-PAID          (N8)
1 REDEFINE #SCR-DATE-PAID
  2 #DATE-PAID-MM          (N2)
  2 #DATE-PAID-DD          (N2)
  2 #DATE-PAID-YYYY        (N4)
  2 REDEFINE #DATE-PAID-YYYY
    3 #DATE-PAID-YYYY-A     (A4)
1 #SCR-DB-DATE-PAID       (N7.2)
1 #SCR-ACV-DATE-PAID      (N7.2)
1 #SCR-COI-DATE-PAID      (N6.2)
1 #SCR-OVR-DATE           (N8)
1 #HISTORY-KEY            (N15)
1 REDEFINE #HISTORY-KEY
  2 #HISTORY-KEY-A         (A15)
*
1 #WYYYYMMDD
  2 #WCENT    (N2)
  2 #WYY      (N2)
  2 #WMM      (N2)
  2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N (N8)
*
1 #WMMDDYYYY
  2 #WMM      (N2)
  2 #WDD      (N2)
  2 #WCENT    (N2)
  2 #WYY      (N2)
1 REDEFINE #WMMDDYYYY
  2 #WMMDDYYYY-A (A8)
1 REDEFINE #WMMDDYYYY
  2 #WMMDDYYYY-N (N8)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #TOT-ADJ         (A7)
1 #TOT-PREM        (A10)
1 #TOT-INT         (A9)
1 #TOT-COI         (A6)
1 #TOT-PREM-W-SIGN (A11)
1 #TOT-INT-W-SIGN  (A11)
1 #TOT-COI-W-SIGN  (A8)
1 #PRIOR-PREM-ADJ  (P7.2)
1 #PRIOR-INT-ADJ   (P5.2)
1 #PRIOR-COI-ADJ   (P5.2)
1 #T-PRM-SGN       (A1)
1 #T-INT-SGN       (A1)
1 #T-COI-SGN       (A1)
*
1 #INT             (I2)
1 #NOTE-TEXT       (A60/10)
1 #DATN            (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY      (N4)
  2 #DATN-MM        (N2)
  2 #DATN-DD        (N2)
*
1 #INPUT-RECORD    (A40)
1 REDEFINE #INPUT-RECORD
  2 #CN-A           (A6)
  2 #TAB-1          (A1)
  2 #ISSUE-DATE     (A8)
  2 REDEFINE #ISSUE-DATE
    3 #ISSUE-DATE-N  (N8)
  2 #TAB-2          (A1)
  2 #MISS-PREM      (A7)
  2 REDEFINE #MISS-PREM
    3 #PREM-AMT      (N5.2)
  2 #TAB-3          (A1)
  2 #DATE-IN        (A8)
  2 REDEFINE #DATE-IN
    3 #DATE-MM       (N2)
    3 #SLASH-1       (A1)
    3 #DATE-DD       (N2)
    3 #SLASH-2       (A1)
    3 #DATE-YY       (A2)
  2 #REST           (A8)
*
1 #CNT-1           (P5)
1 #TOTAL-INT-ADJ   (N7.2)
1 #TOTAL-COI-ADJ   (N7.2)
1 #TOTAL-PRM-ADJ   (N7.2)
1 #TOTAL-ADJ       (N7.2)
1 #WORK-ISSUE-DATE (N8)
1 REDEFINE #WORK-ISSUE-DATE
  2 #WORK-DATE-YYYY  (A4)
  2 #WORK-DATE-MM    (A2)
  2 #WORK-DATE-DD    (A2)
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-RECORD   (A125)
1 #DISP-TOT-ADJ     (A10)
*
END-DEFINE
* * * * * * * * *
*
FORMAT  (1) LS=133 PS=76
WRITE   (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'ACV Adjustments for missed Premiums Paid'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS 'Cnt-1' #TAB
  'Id Number' #TAB
  'Issue Date' #TAB
  'Plan' #TAB
  'Prem Adj' #TAB
  'Int Adj' #TAB
  'Coi Adj' #TAB
  'Total Adj' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 2 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ WORK 1 #INPUT-RECORD
*
  FIND ST-V WITH MEMBER-KEY = #CN-A
*
    IF NO RECORDS FOUND
      WRITE (1) #CN-A 'Not Found'
      ESCAPE BOTTOM
    END-NOREC
*
    FOR #I1 1 TO 9
      IF ST-V.ISSUE-DATE (#I1) = 0
        WRITE (1) #CN-A 'Policy' #ISSUE-DATE-N 'Not Found'
        ESCAPE BOTTOM
      END-IF
*
      IF ST-V.ISSUE-DATE (#I1) = #ISSUE-DATE-N
        IF ST-V.PLAN (#I1)  = 'LT' OR = 'RT'
            OR (ST-V.PLAN-A1 (#I1) GE 'U'
            AND ST-V.PLAN-A1 (#I1) LE 'Z')
          ESCAPE TOP
        END-IF
*
        PERFORM PROCESS-POLICY
*
        MOVE EDITED #SCR-ACV-TOT-ADJ (EM=ZZZ,ZZZ.99) TO #DISP-TOT-ADJ
        IF #SCR-ACV-TOT-ADJ LT 0
          COMPRESS '-' #DISP-TOT-ADJ INTO #DISP-TOT-ADJ LEAVING NO
        END-IF
        ADD +1 TO #CNT-1
        DISPLAY (1) 'Cnt-1' #CNT-1
          'Id Number' ST-V.ID-NUMBER
          'Issue Date' #SCR-ISSUE-DATE
          'Plan' ST-V.PLAN (#I1)
          'Prem Adj' #TOT-PREM-W-SIGN
          'Int Adj' #TOT-INT-W-SIGN
          'Coi Adj' #TOT-COI-W-SIGN
          'Total Adj' #DISP-TOT-ADJ
*
        COMPRESS  #CNT-1 #TAB
          ST-V.ID-NUMBER #TAB
          #SCR-ISSUE-DATE #TAB
          ST-V.PLAN (#I1) #TAB
          #TOT-PREM-W-SIGN #TAB
          #TOT-INT-W-SIGN #TAB
          #TOT-COI-W-SIGN #TAB
          #DISP-TOT-ADJ INTO #EXTRACT-RECORD LEAVING NO
        WRITE WORK 2 #EXTRACT-RECORD
        RESET #EXTRACT-RECORD
*
        UPDATE RECORD (0151)
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
*
  END TRANSACTION
END-WORK
*
WRITE (1) 'Total Premium Adjustment  = ' #TOTAL-PRM-ADJ
WRITE (1) 'Total Interest Adjustment = ' #TOTAL-INT-ADJ
WRITE (1) 'Total COI Adjustment      = ' #TOTAL-COI-ADJ
WRITE (1) 'Total Adjustment          = ' #TOTAL-ADJ
*
* * * * * ** * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-POLICY
* * * * * * * * * * * * * * * *
*
RESET #SCR-ACV-TOT-ADJ #SCR-ACV-DATE-PAID
  #SCR-COI-DATE-PAID #SCR-DB-DATE-PAID
*
PERFORM APPLY-MISSED-PREMIUMS
*
IF   (#SCR-ACV-PREM-ADJ NE 0)
    AND (NOT #SCR-ACV-PREM-ADJ-S = '+' OR = '-')
  WRITE (1) ST-V.ID-NUMBER
  WRITE (1) 'SIGN MUST BE + OR -'
  ESCAPE BOTTOM
END-IF
*
IF   (#SCR-ACV-INT-ADJ NE 0)
    AND (NOT #SCR-ACV-INT-ADJ-S = '+' OR = '-')
  WRITE ST-V.ID-NUMBER
  WRITE 'SIGN MUST BE + OR -'
  ESCAPE BOTTOM
END-IF
*
IF   (#SCR-ACV-COI-ADJ NE 0)
    AND (NOT #SCR-ACV-COI-ADJ-S = '+' OR = '-')
  WRITE ST-V.ID-NUMBER
  WRITE 'SIGN MUST BE + OR -'
  ESCAPE BOTTOM
END-IF
*
IF ST-V.MTD-ACV-PREM-ADJ (#I1) = 9999999.99
  RESET ST-V.MTD-ACV-PREM-ADJ (#I1)
END-IF
*
IF ST-V.MTD-ACV-INT-ADJ (#I1) = 99999.99
  RESET ST-V.MTD-ACV-INT-ADJ (#I1)
END-IF
*
IF ST-V.MTD-ACV-COI-ADJ (#I1) = 99999.99
  RESET ST-V.MTD-ACV-COI-ADJ (#I1)
END-IF
*
MOVE ST-V.MTD-ACV-PREM-ADJ (#I1) TO #PRIOR-PREM-ADJ
MOVE ST-V.MTD-ACV-INT-ADJ (#I1) TO #PRIOR-INT-ADJ
MOVE ST-V.MTD-ACV-COI-ADJ (#I1) TO #PRIOR-COI-ADJ
*
IF #I1 NE 1            /* FILLUP PRECEEDING MULTIPLE FIELDS
  COMPUTE #I3 = #I1 - 1
  FOR #I2 1 TO #I3
    IF ST-V.MTD-ACV-PREM-ADJ (#I2) = 0
      MOVE 9999999.99 TO ST-V.MTD-ACV-PREM-ADJ (#I2)
    END-IF
*
    IF ST-V.MTD-ACV-INT-ADJ (#I2) = 0
      MOVE 99999.99 TO ST-V.MTD-ACV-INT-ADJ  (#I2)
    END-IF
*
    IF ST-V.MTD-ACV-COI-ADJ (#I2) = 0
      MOVE 99999.99 TO ST-V.MTD-ACV-COI-ADJ  (#I2)
    END-IF
  END-FOR
END-IF
*
IF #SCR-ACV-PREM-ADJ-S = '+'
  ADD #SCR-ACV-PREM-ADJ TO ST-V.MTD-ACV-PREM-ADJ (#I1)
  ADD #SCR-ACV-PREM-ADJ TO #SCR-ACV-TOT-ADJ
  ADD #SCR-ACV-PREM-ADJ TO #TOTAL-PRM-ADJ
ELSE
  SUBTRACT #SCR-ACV-PREM-ADJ FROM ST-V.MTD-ACV-PREM-ADJ (#I1)
  SUBTRACT #SCR-ACV-PREM-ADJ FROM #SCR-ACV-TOT-ADJ
  SUBTRACT #SCR-ACV-PREM-ADJ FROM #TOTAL-PRM-ADJ
END-IF
*
IF #SCR-ACV-INT-ADJ-S = '+'
  ADD #SCR-ACV-INT-ADJ TO ST-V.MTD-ACV-INT-ADJ (#I1)
  ADD #SCR-ACV-INT-ADJ TO #SCR-ACV-TOT-ADJ
  ADD #SCR-ACV-INT-ADJ TO #TOTAL-INT-ADJ
ELSE
  SUBTRACT #SCR-ACV-INT-ADJ FROM ST-V.MTD-ACV-INT-ADJ (#I1)
  SUBTRACT #SCR-ACV-INT-ADJ FROM #SCR-ACV-TOT-ADJ
  SUBTRACT #SCR-ACV-INT-ADJ FROM #TOTAL-INT-ADJ
END-IF
*
IF #SCR-ACV-COI-ADJ-S = '+'
  ADD      #SCR-ACV-COI-ADJ TO   ST-V.MTD-ACV-COI-ADJ (#I1)
  SUBTRACT #SCR-ACV-COI-ADJ FROM #SCR-ACV-TOT-ADJ
  SUBTRACT #SCR-ACV-COI-ADJ FROM #TOTAL-COI-ADJ
ELSE
  SUBTRACT #SCR-ACV-COI-ADJ FROM ST-V.MTD-ACV-COI-ADJ (#I1)
  ADD #SCR-ACV-COI-ADJ TO #SCR-ACV-TOT-ADJ
  ADD #SCR-ACV-COI-ADJ TO #TOTAL-COI-ADJ
END-IF
*
ADD #SCR-ACV-TOT-ADJ TO #TOTAL-ADJ
*
MOVE EDITED #SCR-ACV-TOT-ADJ  (EM=ZZZ.99-) TO #TOT-ADJ
MOVE EDITED #SCR-ACV-PREM-ADJ (EM=ZZZ,ZZZ.99) TO #TOT-PREM
MOVE EDITED #SCR-ACV-INT-ADJ  (EM=ZZ,ZZZ.99)  TO #TOT-INT
MOVE EDITED #SCR-ACV-COI-ADJ  (EM=ZZZ.99)  TO #TOT-COI
COMPRESS #SCR-ACV-PREM-ADJ-S #TOT-PREM INTO #TOT-PREM-W-SIGN LEAVING NO
COMPRESS #SCR-ACV-INT-ADJ-S  #TOT-INT  INTO #TOT-INT-W-SIGN  LEAVING NO
COMPRESS #SCR-ACV-COI-ADJ-S  #TOT-COI  INTO #TOT-COI-W-SIGN  LEAVING NO
COMPRESS '## V/A ADJ' #SCR-ISSUE-DATE 'PRM' #TOT-PREM-W-SIGN 'INT'
  INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) #TOT-INT-W-SIGN INTO #NOTE-TEXT (1) LEAVING NO
COMPRESS #NOTE-TEXT (1) 'COI' INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) #TOT-COI-W-SIGN INTO #NOTE-TEXT (1) LEAVING NO
*
MOVE 1 TO #INT
CALLNAT 'E4205SDM' ST-V.ID-NUMBER #INT 'EDS' #NOTE-TEXT (1:10)
*
MOVE ST-V.NAME      TO TR-REG-V.NAME
MOVE ST-V.PROCESS-IND TO TR-REG-V.PROCESS-IND
MOVE ST-V.ID-NUMBER TO TR-REG-V.ID-NUMBER
MOVE ST-V.DTS-DATN  TO TR-REG-V.DATE-LAST-UPDATE
MOVE 'EDS'          TO TR-REG-V.CLERK-ID
MOVE *INIT-ID       TO TR-REG-V.TERMINAL-ID
MOVE *DATN          TO TR-REG-V.TRANSACTION-DATN
MOVE *TIMN          TO TR-REG-V.TRANSACTION-TIMN
*
IF #PRIOR-PREM-ADJ LT 0
  MOVE '-' TO #T-PRM-SGN
ELSE
  MOVE '+' TO #T-PRM-SGN
END-IF
*
IF #PRIOR-INT-ADJ LT 0
  MOVE '-' TO #T-INT-SGN
ELSE
  MOVE '+' TO #T-INT-SGN
END-IF
*
IF #PRIOR-COI-ADJ LT 0
  MOVE '-' TO #T-COI-SGN
ELSE
  MOVE '+' TO #T-COI-SGN
END-IF
*
COMPRESS 'VA-ACV ADJUSTMENT PRIOR PREM MTD =' #T-PRM-SGN #PRIOR-PREM-ADJ
  'INT MTD =' #T-INT-SGN #PRIOR-INT-ADJ
  'COI MTD =' #T-COI-SGN #PRIOR-COI-ADJ
  INTO TR-REG-V.OLD-DATA (1)
*
IF ST-V.MTD-ACV-PREM-ADJ (#I1) LT 0
  MOVE '-' TO #T-PRM-SGN
ELSE
  MOVE '+' TO #T-PRM-SGN
END-IF
*
IF ST-V.MTD-ACV-INT-ADJ (#I1) LT 0
  MOVE '-' TO #T-INT-SGN
ELSE
  MOVE '+' TO #T-INT-SGN
END-IF
*
IF ST-V.MTD-ACV-COI-ADJ (#I1) LT 0
  MOVE '-' TO #T-COI-SGN
ELSE
  MOVE '+' TO #T-COI-SGN
END-IF
*
COMPRESS 'VA-ACV ADJUSTMENT   NEW PREM MTD =' #T-PRM-SGN
  ST-V.MTD-ACV-PREM-ADJ(#I1)
  'INT MTD =' #T-INT-SGN
  ST-V.MTD-ACV-INT-ADJ(#I1)
  'COI MTD =' #T-COI-SGN
  ST-V.MTD-ACV-COI-ADJ(#I1)
  INTO TR-REG-V.NEW-DATA (1)
*
STORE TR-REG-V
*
IF ST-V.MTD-ACV-PREM-ADJ (#I1) = 0
  MOVE 9999999.99 TO ST-V.MTD-ACV-PREM-ADJ (#I1)
END-IF
*
IF ST-V.MTD-ACV-INT-ADJ (#I1) = 0
  MOVE 99999.99 TO ST-V.MTD-ACV-INT-ADJ (#I1)
END-IF
*
IF ST-V.MTD-ACV-COI-ADJ (#I1) = 0
  MOVE 99999.99 TO ST-V.MTD-ACV-COI-ADJ (#I1)
END-IF
*
MOVE *DATN   TO ST-V.LAST-FM-DATE
MOVE *DATN   TO ST-V.DTS-DATN
MOVE *TIMN   TO ST-V.DTS-TIMN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE FLIP-DATE
* * * * * * * * * * * * * *
*
MOVE BY NAME #WYYYYMMDD TO #WMMDDYYYY
IF #WMMDDYYYY-A = '00000000'
  MOVE ' ' TO #WMMDDYYYY-A
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE APPLY-MISSED-PREMIUMS
* * * * * * * * * * * * * * * * * * * *
*
MOVE #DATE-MM TO #DATE-PAID-MM
MOVE #DATE-DD TO #DATE-PAID-DD
COMPRESS '20' #DATE-YY INTO #DATE-PAID-YYYY-A LEAVING NO
MOVE #PREM-AMT TO #SCR-PREM-SHORTAGE
MOVE ST-V.ISSUE-DATE (#I1) TO #WORK-ISSUE-DATE
MOVE #WORK-DATE-YYYY TO #SCR-ISSUE-DATE-YYYY
MOVE #WORK-DATE-MM TO #SCR-ISSUE-DATE-MM
MOVE #WORK-DATE-DD TO #SCR-ISSUE-DATE-DD
*
IF #SCR-DATE-PAID NE 0
  IF #SCR-OVR-DATE NE 0
    IF #SCR-OVR-DATE NE MASK (MMDDYYYY)
      WRITE (1) ST-V.ID-NUMBER
      WRITE (1) 'Please enter the override calc date in MMDDYYYY format'
      ESCAPE BOTTOM
    END-IF
*
    MOVE #SCR-OVR-DATE TO #WMMDDYYYY-N
    MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
    MOVE #WYYYYMMDD-N TO #DATN
  ELSE
    MOVE *DATN TO #DATN
    MOVE 01 TO #DATN-DD     /* default to first day of current month
    MOVE #DATN TO #WYYYYMMDD-N
    MOVE BY NAME #WYYYYMMDD TO #WMMDDYYYY
    MOVE #WMMDDYYYY-N TO #SCR-OVR-DATE
  END-IF
*
  IF #SCR-DATE-PAID NE MASK (MMDDYYYY)
    WRITE (1) ST-V.ID-NUMBER
    WRITE (1) 'Please enter the Premiums Paid Date in MMDDYYYY format'
    WRITE (1) #SCR-DATE-PAID
    ESCAPE BOTTOM
  END-IF
*
  IF #DATE-PAID-YYYY GE #DATN-YYYY
      AND #DATE-PAID-MM GE #DATN-MM
    WRITE (1) ST-V.ID-NUMBER
    WRITE (1) 'Please enter a date prior to the current month/year'
    ESCAPE BOTTOM
  END-IF
*
  IF #SCR-PREM-SHORTAGE LE 0
    WRITE (1) ST-V.ID-NUMBER
    WRITE (1) 'Please enter the Premiums to be applied'
    ESCAPE BOTTOM
  END-IF
*
  IF #SCR-ACV-DATE-PAID EQ 0
      AND #SCR-COI-DATE-PAID EQ 0
      AND #SCR-DB-DATE-PAID EQ 0
    COMPRESS ST-V.ID-NUMBER-A #SCR-ISSUE-DATE-YYYY #SCR-ISSUE-DATE-MM
      #SCR-ISSUE-DATE-DD ST-V.SPLIT-IND (#I1)
      INTO #HISTORY-KEY-A LEAVING NO
  ELSE
    RESET #HISTORY-KEY
  END-IF
*
  MOVE #SCR-DATE-PAID TO #WMMDDYYYY-N
  MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
  MOVE 'M' TO #MISS-OVER   /* DW1
*
  CALLNAT 'C2351SVA'
    #MISS-OVER                /*DW1
    #SCR-ISSUE-DATE
    #SCR-PREM-SHORTAGE
    #WYYYYMMDD-N
    #SCR-DB-DATE-PAID
    #SCR-ACV-DATE-PAID
    #SCR-COI-DATE-PAID
    #SCR-ACV-PREM-ADJ-S
    #SCR-ACV-PREM-ADJ
    #SCR-ACV-INT-ADJ-S
    #SCR-ACV-INT-ADJ
    #SCR-ACV-COI-ADJ-S
    #SCR-ACV-COI-ADJ
    #HISTORY-KEY
    #DATN
*
* IF #SCR-ACV-DATE-PAID EQ 0
*  OR #SCR-COI-DATE-PAID EQ 0
  IF #SCR-DB-DATE-PAID EQ 0
    IF #HISTORY-KEY = 0
      WRITE (1) ST-V.ID-NUMBER
      WRITE (1) 'Please enter the ACV/COI/and DB or leave all blank'
      ESCAPE BOTTOM
    ELSE
      WRITE (1) ST-V.ID-NUMBER
      WRITE (1) 'History not found. Please enter the ACV/COI/DB manually'
      WRITE (1) #WYYYYMMDD-N
      ESCAPE BOTTOM
    END-IF
  END-IF
*
END-IF
*
END-SUBROUTINE
*
END
