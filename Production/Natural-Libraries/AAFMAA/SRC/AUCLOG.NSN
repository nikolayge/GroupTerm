* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCLOG.
/**
/** :author nguentchev
/* TODO: Enter your code here
* Logging communication messages with MS DYNAMICS CRM
*
DEFINE DATA
PARAMETER
1 #ERROR-CODE    (I4)
1 #MESSAGE       (A) DYNAMIC
1 #LOG-REQUEST   (I4)
1 #MESSAGE-ID    (A) DYNAMIC OPTIONAL
LOCAL USING GPRL0EML
LOCAL
1 #ERROR-DESCRIPTION  (A) DYNAMIC
1 ERROR-LOG           (A32000)  /* Have a work log file
1 #TRL-FILE-NAME      (A) DYNAMIC
1 #EFFECTIVE-DATE-A   (A8)
1 BLANK               (A1) CONST <' '>
1 #DATE               (A8) 1 REDEFINE #DATE
  2 #DATE-N           (N8)
1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
1 #ERROR-TIME             (A19)
*
1 #MESSAGE-LOG      (I4) CONST <2>
1 #ERROR-LOG        (I4) CONST <3>
1 #REQUEST-LOG      (I4) CONST <4>
1 #UPDATE-LOG       (I4) CONST <5>
1 #Timeout          (A2) CONST <'-2'>
1 #Natural-error    (A2) CONST <'-3'>
*
END-DEFINE
MOVE EDITED *DATX (EM=YYYYMMDD) TO #EFFECTIVE-DATE-A
*
assign #TRL-FILE-NAME = '/usr/SAG/tmp/AUC/'
compress #TRL-FILE-NAME 'AUC_LOG' #EFFECTIVE-DATE-A '.log' into
  #TRL-FILE-NAME leaving no
DEFINE WORK FILE 6 #TRL-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
DECIDE ON FIRST VALUE OF #LOG-REQUEST
  VALUE #ERROR-LOG
    ERROR-LOG := #MESSAGE
    WRITE WORK 6 ERROR-LOG
    PERFORM REPORT-ERROR
    IF #ERROR-CODE = 0
      #ERROR-CODE := P_ERROR_NUM
      #MESSAGE := #ERROR-DESCRIPTION
    END-IF
  VALUE #MESSAGE-LOG, #REQUEST-LOG
    ERROR-LOG := #MESSAGE
    WRITE WORK 6 ERROR-LOG
    CLOSE WORK 6
  NONE VALUE IGNORE
END-DECIDE
*
DEFINE REPORT-ERROR
/*     ============
CALLNAT 'USR2001N' P_PARMS
IF P_ERROR_NUM = 3145
  #ERROR-DESCRIPTION := 'Record on hold in Admin System'
ELSE
  MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
    TO #ERROR-TIME
  COMPRESS
    'Error number:' P_ERROR_NUM
    'Error class:' P_ERROR_CLASS
    'Error line:' P_ERROR_LINE
    'Error program:' P_ERROR_PROGRAM
    'Error program type:' P_ERROR_PGM_TYPE
    'Error program type long:' P_ERROR_PGM_TYPE_LONG
    'Error program level:' P_ERROR_LEVEL
    'Language at error time:' P_ERROR_LANG
    'Error library:' P_ERROR_LIB
    'DBID:' P_ERROR_DBID 'FNR:' P_ERROR_FNR
    'Error type:'  P_ERROR_TYPE
    'Error time:' #ERROR-TIME
    INTO #ERROR-DESCRIPTION
  P_TO     := 'nguentchev@aafmaa.com'
* P_CC     := '7037861865@messaging.sprintpcs.com'
  P_SUBJ   := '"AUC Error"'
  P_MSG(1) := 'Error processing request from AUC - Testing server 250'
  P_MSG(2) := SUBSTRING(ERROR-LOG,1,150)
  P_MSG(3) := SUBSTRING(ERROR-LOG,151,150)
  P_MSG(4) := SUBSTRING(ERROR-LOG,301,150)
  CALLNAT 'GPRS0EML' p_EMAIL
END-IF
IF #MESSAGE-ID SPECIFIED
  COMPRESS '<' #MESSAGE-ID '>'
    '<ErrorCode>' P_ERROR_NUM '</ErrorCode>' -
    '<ErrorDescription>' #ERROR-DESCRIPTION '</ErrorDescription>' -
    '</' #MESSAGE-ID '>'
INTO #MESSAGE LEAVING NO /* Message will be passed back to calling system
  ERROR-LOG := #MESSAGE
ELSE
  ERROR-LOG := #ERROR-DESCRIPTION
END-IF
WRITE WORK 6 ERROR-LOG
CLOSE WORK 6
*
END-SUBROUTINE
*
END
