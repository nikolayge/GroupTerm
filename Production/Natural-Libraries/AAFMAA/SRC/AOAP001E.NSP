* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP001E - TAS TRIGGER BUILD - ZIP CODE REQUESTS
*
*   THE PARM FOR THIS PROGRAM SHOULD BE LOADED INTO THE NATURAL STACK
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
*   THIS PROGRAM CREATES THE 'ZIP CODE MAILOUT REPORT'
*                        THE 'ZIP-CODE MAILING EXCLUSION REPORT'
*                            LABEL EXTRACT FILE FOR LABELS
*********************************************************************************
* USER DATE        TAG  MODIFICATION
* bz   06/09/2013       Rebranding (Search for AAFMAA)
* JNT  09/11/2013  JT1  Address normalization
*
*********************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING ADDA0001                                      /* JT1
LOCAL
*
1 #PARM-ACTIVE-RETIRED  (A1)   /* THIS VALUE IS FROM THE NATURAL STACK
1 #PARM-SERVICE         (A1)   /* THIS VALUE IS FROM THE NATURAL STACK
*
1 #PARM-INPUT        (A11)     /* LAST FROM AND TO  ZIP MUST BE 99999
1 REDEFINE #PARM-INPUT
  2 #PARM-FROM-ZIP-CODE  (N5)
  2 #PARM-FILLER         (A1)
  2 #PARM-TO-ZIP-CODE    (N5)
*
1 #ZIP-TABLE          (21)      /* ALLOWS 20 ZIP CODE RANGES
  2 #FROM-ZIP       (N5)
  2 #TO-ZIP         (N5)
*
* * * * LABEL INFO * * *
*
1 #W-ID-NUMBER         (A6)
1 #LABEL-EXTR-CNT      (P5)
*
1 #LABEL-EXTRACT-RECORD     (A151)   /* WORK RECORD (4)
1 REDEFINE #LABEL-EXTRACT-RECORD
  2 #LER-ID                  (A9)
  2 #LER-MBRSHPS             (N1)
  2 #LER-NAME                (A25)
  2 #LER-RANK                (A4)
  2 #LER-MILITARY-STATUS     (A1)
  2 #LER-ADDRESS-1           (A25)
  2 #LER-ADDRESS-2           (A25)
  2 #LER-ADDRESS-3           (A25)
  2 #LER-ZIP-CODE            (A10)
  2 REDEFINE #LER-ZIP-CODE
    3 #LER-ZIP-1-5            (N5)
    3 #LER-DASH               (A1)
    3 #LER-ZIP-6-9            (A4)
  2 #LER-MILITARY-SERVICE    (A3)
  2 #LER-LINE4               (A22)
  2 #LER-SPUSE               (A1)
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 XX-S01 VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 SUSPEND-CODE
  2 WIDOW-FLAG
  2 MEMBER-FLAG
* 2 RANK
  2 O-E-DATE
  2 MILITARY-SERVICE
  2 REDEFINE MILITARY-SERVICE
    3 MILITARY-SERVICE-1    (A1)
  2 MILITARY-STATUS
  2 HANDLE-FLAG
  2 S-ADDRESS-CODE                       /* JT1
  2 MEMBER-CONTACT-ID                      /* JT1
  2 MAILING-FLAG-SP
  2 STATUS-CODE-SP
  2 DI-BA-MAIL-CD
  2 NUMBER-MEMBERSHIPS
  2 NAME
  2 REDEFINE NAME
    3 NAME-20      (A20)
*   2 ADDRESS-1                           /* JT1
* 2 ADDRESS-2
* 2 ADDRESS-3
*   2 ZIP-CODE         (N9)               /* JT1 start
*   2 REDEFINE ZIP-CODE
*     3 ZIP-CODE-1-5   (N5)
*     3 ZIP-CODE-6-9   (N4)
*   2 S-ZIP-CODE       (N9)
*   2 REDEFINE S-ZIP-CODE
*     3 S-ZIP-CODE-1-5 (N5)               /* JT1 end
* 2 DATE-OF-DEATH-SP
* 2 MAILING-FLAG-SP
  2 ACTIVE-DUTY-BASE-DATE
* 2 STATUS-CODE-SP
* 2 NAME-SP
* 2 REDEFINE NAME-SP
*   3 NAME-20-SP     (A20)
*
1 FI-V VIEW OF A-INVESTMENTS
  2 C*INV-GEN-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL-2
*
1 AP-V VIEW OF A-ADDRESS-POOL                                     /* JT1 start
  2 ADDRESS-POOL-ID
  2 ZIP-CODE        (N9)
  2 REDEFINE ZIP-CODE
    3 ZIP-CODE-1-5  (N5)
    3 ZIP-CODE-6-9  (N4)
*
1 AR-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID
  2 ADDRESS-POOL-ID
  2 ADDR-TYPE-CD
*
1 FC-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
*
1 #INPUT-FROM-ZIP  (N9)         INIT <000000000>
1 REDEFINE #INPUT-FROM-ZIP
  2 #INPUT-FROM-ZIP-5   (N5)
  2 #INPUT-FROM-ZIP-4   (N4)
*
1 #INPUT-TO-ZIP    (N9)         INIT <999999999>
1 REDEFINE #INPUT-TO-ZIP
  2 #INPUT-TO-ZIP-5     (N5)
  2 #INPUT-TO-ZIP-4     (N4)                                      /* JT1 end
*
* * * *   T R I G G E R   F I L E   * * * * * * * * * * * * * *
1 TRIGGER-FILE
  2 TF-ID-NUMBER   (N6)
  2 TF-ISN         (P8)
  2 TF-ZIP-CODE    (P9)
  2 TF-AGE-62-FLAG (A1)
  2 TF-RQST-INDS   (N5)
*
* * * *   REPORT FIELDS           * * * * * * * * * * * * * * *
1 #REPORT-FIELDS   (3)
  2 #RPT-CN          (A8)
  2 #RPT-NAME        (A20)
  2 #RPT-CLERK       (A3)
  2 #RPT-ZIP         (A5)
  2 #RPT-DATE        (A5)
*
* * * *   EXCLUDE FIELDS          * * * * * * * * * * * * * * *
1 #EXCLUDE-FIELDS   (6)
  2 #EXC-CN          (A8)
  2 #EXC-REASON      (A6)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 #DD     (A2)
*
1 #ZIP-NOT-FOUND          (L)
1 #SBP-OPEN-ENROLL-SEARCH (L)
1 #RESV-GRD-SEARCH        (L)
1 #WIDOW-SEARCH           (L)
1 #ONLY-IF-HAS-FAS-INFO       (L)
1 #ONLY-IF-HAS-FAS-INFO-NOT   (L)
1 #NO-INVESTMENTS         (L)
1 #FIN-ISN                (N8)
1 #REPORT-DATE  (A8)
1 #I1           (I2)
1 #ISN          (P8)
1 #ZIP-CODE     (P9)
*
1 #COUNTERS
  2 #MEMBER-CNT     (P5)
  2 #MAIL-CODE-CNT  (P5)
  2 #SUSPEND-CNT    (P5)
  2 #LOOP-CNT       (I2)
  2 #EXCLUDE-CNT    (I2)
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=66
FORMAT (5) LS=133 PS=66
FORMAT (7) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INPUT #PARM-ACTIVE-RETIRED
INPUT #PARM-SERVICE
*
IF #PARM-SERVICE = 'S'
  MOVE TRUE TO #SBP-OPEN-ENROLL-SEARCH
  MOVE '*' TO #PARM-SERVICE
END-IF
*
IF #PARM-SERVICE = 'R'
  MOVE TRUE TO #RESV-GRD-SEARCH
  MOVE '*' TO #PARM-SERVICE
END-IF
*
IF #PARM-SERVICE = 'W'
  MOVE TRUE TO #WIDOW-SEARCH
  MOVE '*' TO #PARM-SERVICE
END-IF
*
IF #PARM-SERVICE = 'F'
  MOVE TRUE TO #ONLY-IF-HAS-FAS-INFO
  MOVE '*' TO #PARM-SERVICE
END-IF
*
IF #PARM-SERVICE = 'N'
  MOVE TRUE TO #ONLY-IF-HAS-FAS-INFO-NOT
  MOVE '*' TO #PARM-SERVICE
END-IF
*
* FOR #I1 1 TO 21                                               /* JT1 start
*   INPUT #PARM-INPUT
* *
*   MOVE #PARM-FROM-ZIP-CODE TO #FROM-ZIP (#I1)
*   MOVE #PARM-TO-ZIP-CODE   TO #TO-ZIP   (#I1)
* *
*   IF #PARM-FROM-ZIP-CODE = 99999
*     ESCAPE BOTTOM
*   END-IF
* END-FOR
*
INPUT #PARM-INPUT
MOVE #PARM-FROM-ZIP-CODE TO #INPUT-FROM-ZIP-5
MOVE #PARM-TO-ZIP-CODE   TO #INPUT-TO-ZIP-5                   /* JT1 end
*
MOVE *DATN TO #DATN
COMPRESS #MM '/' #DD '/' #YY INTO #REPORT-DATE LEAVING NO
*
WRITE (5) TITLE  *PROGRAM '- 5'
  8X 'AAFMAA'
  'ZIP-CODE REQUEST MAILOUT REPORT RUN ON'
  #REPORT-DATE 12X *PAGE-NUMBER (5) /
*
WRITE (7) TITLE  *PROGRAM '- 7'
  8X 'AAFMAA --'
  'ZIP-CODE EXCLUSION REPORT'
  12X *PAGE-NUMBER (7)
  / 'RUN ON' #REPORT-DATE
*
* - - - - - - - - READ STATUS - - - - - - - - - - - - - - - - - - - - *
IF #SBP-OPEN-ENROLL-SEARCH
  MOVE 'S' TO TF-AGE-62-FLAG        /* TO INDICATE SBP-open-enroll ltr
ELSE
  IF #RESV-GRD-SEARCH
    MOVE 'R' TO TF-AGE-62-FLAG      /* TO INDICATE resv-guard ltr
  ELSE
    IF #WIDOW-SEARCH
      MOVE 'W' TO TF-AGE-62-FLAG  /* TO INDICATE WIDOW zip-code select
    ELSE
      MOVE 'Z' TO TF-AGE-62-FLAG  /* TO INDICATE MBR ZIP-CODE SELECT
    END-IF
  END-IF
END-IF
*
READ AP-V BY ZIP-CODE                                                                /* JT1 start
    STARTING FROM #INPUT-FROM-ZIP THRU #INPUT-TO-ZIP
*
  FIND AR-V WITH AR-V.ADDRESS-POOL-ID = AP-V.ADDRESS-POOL-ID
    ACCEPT IF ADDR-TYPE-CD = 'P'
*
    FIND FC-V WITH FC-V.CONTACT-ID = AR-V.CONTACT-ID
      IF FC-V.ID-NUMBER > 0
        FIND XX-S01 WITH MEMBER-CONTACT-ID = FC-V.CONTACT-ID
*               FIND XX-S01 WITH ID-NUMBER = FC-V.ID-NUMBER                         /* JT1 end
*
* check g3500pad as to which letter to preceed the firs mailout
* READ RECORDS XX-S01 BY ZIP-CODE-KEY FROM #FROM-ZIP (1)                              /* JT1
          IF  (ID-NUMBER LE 1900)      /* BYPASS SAMPLES
              OR (INTRO-FLAG = 'S' OR = 'K' OR = 'G')
              OR (PROCESS-IND = 'I' OR = 'O' OR = 'D'  OR = 'C')
            ESCAPE TOP
          END-IF
*
          IF #WIDOW-SEARCH
            IF WIDOW-FLAG NE 'Y'
              ESCAPE TOP
            END-IF
          ELSE
            IF WIDOW-FLAG = 'Y'
              ESCAPE TOP
            END-IF
          END-IF
*
          IF #PARM-ACTIVE-RETIRED NE '*'       /* ONLY PROCESS SELECTED TYPE
            IF MILITARY-STATUS NE #PARM-ACTIVE-RETIRED
              ESCAPE TOP
            END-IF
          END-IF
*
          IF #PARM-SERVICE NE '*'       /* ONLY PROCESS SELECTED SERVICE
            IF MILITARY-SERVICE-1 NE #PARM-SERVICE
              ESCAPE TOP
            END-IF
          END-IF
*
          IF #SBP-OPEN-ENROLL-SEARCH
            IF  (O-E-DATE LT #FROM-ZIP (1) + 19900000)
                OR (O-E-DATE GT #TO-ZIP   (1) + 19900000)
              ESCAPE TOP
            END-IF
          ELSE
            IF #RESV-GRD-SEARCH
              IF NOT (MILITARY-SERVICE = 'AG' OR = 'AGA' OR = 'AR' OR = 'NR'
                  OR = 'FRA' OR = 'CGR' OR = 'FG' OR = 'FGA' OR = 'FR' OR = 'MR')
                  OR ((ACTIVE-DUTY-BASE-DATE NE 0)
                  AND (MILITARY-SERVICE = 'AGA' OR = 'FGA' OR = 'FRA')
                  AND ((*DATN - ACTIVE-DUTY-BASE-DATE) GE 200000)) /* OVER 20 YRS
                ESCAPE TOP
              END-IF
*     ELSE                                                                 /* JT1 start
*       RESET #ZIP-NOT-FOUND
*       FOR #I1 1 TO 21
*         IF #FROM-ZIP (#I1) = 99999
*           MOVE TRUE TO #ZIP-NOT-FOUND
*           ESCAPE BOTTOM
*         END-IF
* *
*         IF   (ZIP-CODE-1-5 GE #FROM-ZIP (#I1))
*          AND (ZIP-CODE-1-5 LE #TO-ZIP   (#I1))
*           ESCAPE BOTTOM
*         END-IF
*       END-FOR
* *
*       IF #ZIP-NOT-FOUND
*         ESCAPE TOP
*       END-IF                                                            /* JT1 end
            END-IF
          END-IF
*
          IF #ONLY-IF-HAS-FAS-INFO OR #ONLY-IF-HAS-FAS-INFO-NOT
            RESET #NO-INVESTMENTS
            FIND (1) FI-V WITH FI-V.ID-NUMBER = ID-NUMBER
              IF NO RECORDS FOUND
                MOVE TRUE TO #NO-INVESTMENTS
                ESCAPE BOTTOM
              END-NOREC
*
              MOVE *ISN TO #FIN-ISN
              IF   (C*INV-GEN-TBL = 0)
                  AND (C*INV-MUTUAL-STOCKS-BONDS-TBL=0)
                  AND (C*INV-E-BONDS-TBL = 0)
                  AND (C*INV-E-BONDS-TBL-2 = 0)
                MOVE TRUE TO #NO-INVESTMENTS
              END-IF
            END-FIND
*
* IF ACTIVE-DUTY-BASE-DATE GT 19780201 /* temporary <<<<<<<<<<<
*   ESCAPE TOP
* END-IF
*
            IF #ONLY-IF-HAS-FAS-INFO AND #NO-INVESTMENTS
              ESCAPE TOP
            END-IF
*
            IF #ONLY-IF-HAS-FAS-INFO-NOT AND NOT #NO-INVESTMENTS
              ESCAPE TOP
            END-IF
          END-IF
*
          IF #EXCLUDE-CNT = 6
            PERFORM WRITE-MEMBER-EXCLUSION
            RESET #EXCLUDE-FIELDS (1:6)
            RESET #EXCLUDE-CNT
          END-IF
*
          IF #WIDOW-SEARCH
            IF  (MAILING-FLAG-SP = '9')
                OR (STATUS-CODE-SP = 'D' OR = 'G' OR = 'N')
              ADD +1 TO #EXCLUDE-CNT
              ADD +1 TO #SUSPEND-CNT
              COMPRESS 'CN' ID-NBR INTO #EXC-CN (#EXCLUDE-CNT) LEAVING NO
              MOVE 'bypass' TO #EXC-REASON (#EXCLUDE-CNT)
              ESCAPE TOP
            END-IF
          ELSE
            IF  (HANDLE-FLAG = '9')
                OR (S-ADDRESS-CODE = '9')                                              /* JT1
              ADD +1 TO #EXCLUDE-CNT
              ADD +1 TO #SUSPEND-CNT
              COMPRESS 'CN' ID-NBR INTO #EXC-CN (#EXCLUDE-CNT) LEAVING NO
              COMPRESS 'MAIL=' HANDLE-FLAG
                INTO #EXC-REASON (#EXCLUDE-CNT) LEAVING NO
              ESCAPE TOP
            END-IF
*
            IF SUSPEND-CODE = '3'
              ADD +1 TO #EXCLUDE-CNT
              ADD +1 TO #SUSPEND-CNT
              COMPRESS 'CN' ID-NBR INTO #EXC-CN (#EXCLUDE-CNT) LEAVING NO
              COMPRESS 'SUSP=' SUSPEND-CODE
                INTO #EXC-REASON (#EXCLUDE-CNT) LEAVING NO
              ESCAPE TOP
            END-IF
*
            IF DI-BA-MAIL-CD = ' ' OR = 'M' OR = 'N' OR = 'B' OR = 'V'
              IGNORE
            ELSE
              ADD +1 TO #EXCLUDE-CNT
              ADD +1 TO #MAIL-CODE-CNT
              COMPRESS 'CN' ID-NBR INTO #EXC-CN (#EXCLUDE-CNT) LEAVING NO
              COMPRESS 'MAIL=' DI-BA-MAIL-CD
                INTO #EXC-REASON (#EXCLUDE-CNT) LEAVING NO
              ESCAPE TOP
            END-IF
          END-IF
*
          IF #LOOP-CNT = 3
            PERFORM WRITE-MEMBER-DETAIL
            RESET #REPORT-FIELDS (1:3)
            RESET #LOOP-CNT
          END-IF
*
          ADD +1 TO #LOOP-CNT
          COMPRESS 'CN' ID-NBR INTO #RPT-CN (#LOOP-CNT) LEAVING NO
          MOVE AP-V.ZIP-CODE-1-5 TO #RPT-ZIP (#LOOP-CNT)                                   /* JT1
*   MOVE ZIP-CODE-1-5 TO #RPT-ZIP     (#LOOP-CNT)                                  /* JT1
          MOVE NAME-20      TO #RPT-NAME    (#LOOP-CNT)
*
* * * * * * CHESHIRE LABEL PROCESSING * * * *
*
* RESET #LABEL-EXTRACT-RECORD
*
* MOVE EDITED ID-NUMBER (EM=999999) TO #W-ID-NUMBER
* COMPRESS 'CN' #W-ID-NUMBER INTO #LER-ID LEAVING NO SPACE
* MOVE NUMBER-MEMBERSHIPS      TO #LER-MBRSHPS
* MOVE NAME                    TO #LER-NAME
* MOVE RANK                    TO #LER-RANK
* MOVE MILITARY-STATUS         TO #LER-MILITARY-STATUS
* MOVE MILITARY-SERVICE        TO #LER-MILITARY-SERVICE
* MOVE HANDLE-FLAG             TO #LER-SPUSE
* MOVE ADDRESS-1               TO #LER-ADDRESS-1
* MOVE ADDRESS-2               TO #LER-ADDRESS-2
* MOVE ADDRESS-3               TO #LER-ADDRESS-3
* MOVE ZIP-CODE-1-5            TO #LER-ZIP-1-5
*
* IF ZIP-CODE-6-9 GT 0
*   MOVE '-'         TO #LER-DASH
*   MOVE EDITED ZIP-CODE-6-9 (EM=9999) TO #LER-ZIP-6-9
* END-IF
*
* WRITE WORK 4 #LABEL-EXTRACT-RECORD
* ADD +1 TO #LABEL-EXTR-CNT
*
          MOVE *ISN (0282)       TO #ISN
*
          MOVE AP-V.ZIP-CODE     TO #ZIP-CODE                                        /* JT1 start
*   IF ADDRESS-1 = ' '
*     MOVE S-ZIP-CODE TO #ZIP-CODE
*   ELSE
*     MOVE ZIP-CODE   TO #ZIP-CODE
*   END-IF                                                               /* JT1 end
*
* WRITE (2) 'AOAP001E PRE-SORT ' XX-S01.ID-NUMBER #ISN
* END-ALL            /* USE THIS TO SORT BY NAME
*
* SORT BY NAME
*   USING XX-S01.ID-NUMBER #ISN #ZIP-CODE
*
          MOVE XX-S01.ID-NUMBER  TO TF-ID-NUMBER
          MOVE #ISN              TO TF-ISN
          MOVE #ZIP-CODE         TO TF-ZIP-CODE
* WRITE (2) 'AOAP001E POST-SORT ' XX-S01.ID-NUMBER
*
          WRITE WORK 1 TRIGGER-FILE
          ADD +1 TO #MEMBER-CNT
* END-SORT
* END-READ                                                             /* JT1
        END-FIND                                               /* JT1
      END-IF                                                   /* JT1
    END-FIND                                                    /* JT1
  END-FIND                                                        /* JT1
END-READ                                                            /* JT1
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
PERFORM WRITE-MEMBER-DETAIL
PERFORM WRITE-MEMBER-EXCLUSION
*
WRITE (5) / 12T 'NUMBER OF ZIP-CODE MAILOUTS' #MEMBER-CNT /
*
WRITE (7) / 12T 'CERTS REVIEWED'
  5X 'EXCLUSIONS-M/C' 5X 'EXCLUSIONS-SUS'
  5X 'BALANCE NUMBER'
  / 12T *COUNTER (0282)
  12X #MAIL-CODE-CNT   13X #SUSPEND-CNT
  13X #MEMBER-CNT /
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-MEMBER-DETAIL
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
DISPLAY (5) NOTITLE 12T
  'CERT NO' #RPT-CN(1)
  1X '----MEMBER--NAME----' #RPT-NAME(1)
  2X 'ZIP-' #RPT-ZIP(1)
  2X 'CERT NO' #RPT-CN(2)
  1X '----MEMBER--NAME----' #RPT-NAME(2)
  2X 'ZIP-' #RPT-ZIP(2)
  2X 'CERT NO' #RPT-CN(3)
  1X '----MEMBER--NAME----' #RPT-NAME(3)
  2X 'ZIP-' #RPT-ZIP(3)
*
END-SUBROUTINE  /* (0509)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-MEMBER-EXCLUSION
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
DISPLAY (7) NOTITLE 12T
  'CERT NO' #EXC-CN (1)  1X  'REASON' #EXC-REASON (1) 5X
  'CERT NO' #EXC-CN (2)  1X  'REASON' #EXC-REASON (2) 5X
  'CERT NO' #EXC-CN (3)  1X  'REASON' #EXC-REASON (3) 5X
  'CERT NO' #EXC-CN (4)  1X  'REASON' #EXC-REASON (4) 5X
  'CERT NO' #EXC-CN (5)  1X  'REASON' #EXC-REASON (5) 5X
  'CERT NO' #EXC-CN (6)  1X  'REASON' #EXC-REASON (6)
*
END-SUBROUTINE  /* (0525)
END
