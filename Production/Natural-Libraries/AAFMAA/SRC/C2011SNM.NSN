* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2011SNM    *** Load Deposits to #DEP-RCVD **
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    05042005  None    Program written.                            *
* PAM    07202005  PM1     Changed to format Deposit Received for      *
*                          all processes.                              *
* PAM    09122005  PM2     Allow open items and credit card deposit    *
*                          totals.                                     *
* PAM    11162007  PM3     Credit card deposits now on file. No open   *
*                          items allowed - stripped and archive pgm    *
************************************************************************
DEFINE DATA
*
PARAMETER
1 #SSN                       (N9)
1 #DEP-RCVD                  (N7.2)
1 #DEP-CREDIT                (N7.2)                               
1 #DEP-CASH                  (N7.2)                               
*
LOCAL
*
1 DP-V VIEW OF A-DEPOSITS
 2 SSN                       (P9)
 2 DEPOSIT-OCC               (99)
  3 AMOUNT                   (P7.2)
  3 DATE                     (N8)
  3 SENT-BY                  (A1)
  3 TYPE                     (A1)
  3 CHECK-NUM                (A6)
  3 SPLIT                    (A1)
  3 APPLIED-DATE             (N8)
  3 APPLIED-CN               (N6)
*
1 #I1                        (I2)
1 #TOTAL-DEPOSIT             (N7.2)
*
END-DEFINE
************************************************************************
*
RESET #TOTAL-DEPOSIT #DEP-RCVD #DEP-CREDIT #DEP-CASH /* PM3
*
FIND DP-V WITH DP-V.SSN = #SSN
  IF NO RECORDS
    ESCAPE BOTTOM
  END-NOREC
*
  FOR #I1 1 TO 99
    IF DP-V.AMOUNT (#I1) = 0
     OR DP-V.APPLIED-DATE (#I1) NE 0
      ESCAPE TOP
    END-IF
*
    ADD DP-V.AMOUNT (#I1) TO #TOTAL-DEPOSIT
    IF DP-V.TYPE (#I1) = 'K'                         /* PM3
      ADD DP-V.AMOUNT (#I1) TO #DEP-CREDIT           /* PM3
    ELSE                                             /* PM3
      ADD DP-V.AMOUNT (#I1) TO #DEP-CASH             /* PM3
    END-IF                                           /* PM3
  END-FOR
END-FIND
*
IF #TOTAL-DEPOSIT GT 0
  MOVE #TOTAL-DEPOSIT TO #DEP-RCVD
END-IF
*
END
