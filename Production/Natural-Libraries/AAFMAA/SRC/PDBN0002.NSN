* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Program ID: PDBNDATA
* Project Persona Database
* PDBN0002   Campaign data collection
*
**************************************************************************
*                            MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NG    20130506        Created
* bz-Name 07/07/2017     Fine Names
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID      (N8)
1 #CAMPAIGN        (A1024) 
LOCAL USING ADDA0001
LOCAL
1 #LETTER          (A7) CONST <'!LETTER'> /* bz-Name
1 #SALUTE          (A6) CONST <'SALUTE'>
1 Letter           (A) DYNAMIC
1 Salutation       (A) DYNAMIC
1 #ZIP             (A) DYNAMIC
1 #PREF-EMAIL      (A) DYNAMIC
1 #PREF-PHONE      (A) DYNAMIC
1 CNT-V VIEW OF A-CONTACTS
  2  FULL-NAME
    3  FIRST-NAME
    3  MIDDLE-NAME
    3  LAST-NAME
    3  SUFFIX
1 Primary          (A1) CONST <'P'>
1 #TAB             (A1) CONST <H'09'>
1 #ZIP5            (A5)
1 #ZIP4            (A4)
END-DEFINE
**  \/\/\/\/ Salutation EnvelopeFormat FirstName LastName
*
Letter := #LETTER
CALLNAT 'GPRS0CNT' #CONTACT-ID Letter
Salutation := #SALUTE
CALLNAT 'GPRS0CNT' #CONTACT-ID Salutation
* Salutation := EDIT_NAME_LOWCASE (< Salutation >)          /*  bz-Name
* MOVE *TRANSLATE(Salutation, UPPER) TO Salutation
FIND (1) CNT-V WITH CONTACT-ID = #CONTACT-ID
END-FIND
**  \/\/\/\/ Preferred Email
*
#PREF-EMAIL := F-PREF-EMAIL (< #CONTACT-ID >)
*
**  \/\/\/\/ Preferred Phone
*
#PREF-PHONE := F-PREF-PHONE (< #CONTACT-ID >)
*
**  \/\/\/\/ Primary Address 1,2,3 City State Zip
*
RESET #PDA-ADDRESS
#ADD-CONTACT-ID := #CONTACT-ID
#ADD-POOL-ID    := F-GET-ADR-POOL-ID (< #CONTACT-ID,Primary >)
CALLNAT 'ADDN0001' #PDA-ADDRESS
IF #ADD-ZIP-CODE > 0
  MOVE EDITED #ADD-ZIP5 (EM=99999) TO #ZIP5
  MOVE EDITED #ADD-ZIP4 (EM=9999)  TO #ZIP4
  COMPRESS #ZIP5 #ZIP4 INTO #ZIP WITH DELIMITER '-'
ELSE
  #ZIP := #ADD-POSTAL-CODE
END-IF
*
COMPRESS FULL
  Salutation
  Letter
  #PREF-EMAIL
  #PREF-PHONE
  #ADD-STATE
  #ADD-CITY
  #ZIP
  #ADD-LINE-1
  #ADD-LINE-2
  #ADD-LINE-3
  INTO #CAMPAIGN WITH DELIMITER #TAB
END
