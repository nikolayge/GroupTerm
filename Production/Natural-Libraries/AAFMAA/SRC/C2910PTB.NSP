* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*********************************************
* PROGRAM-ID: C2910PTB
*         TI: TAX BENEFICIARY 1099-INTEREST
*
* MODIFICATIONS:
*
* 20131010 NG1 Rewriten for new 1099 Claimants process and Address normalization
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER    DATE      TAG     REASON                                     *
* NGG    20141104   NG1     ISSUE 3166 TB Screen 1099 Error            *
* TMT    12/01/2015 TT1     1099 Project
* TMT    05/09/2016 TT2     OI-4311 PD fix
* bz     04/12/2017 bz-HD209 Add to Examine clause FULL 
* bz-Name-C 07/07/2017     Fine Names. Keep all capital 
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING ADDA0001
LOCAL
1 #PRIMARY         (A1) CONST <'P'>
1 #FINAL-PAYMENT-DATE (N8) 1 REDEFINE #FINAL-PAYMENT-DATE
  2 #FINAL-PAYMENT-DATE-A   (A8)
1 REDEFINE #FINAL-PAYMENT-DATE
  2 #FP-MM      (A2)
  2 #FP-DD      (A2)
  2 #FP-YYYY    (A4)
1 INT-1099-V VIEW OF A-INT-1099
  2 SSN                    (N9)
  2 YEAR                   (N4)
  2 AMOUNT                 (P9.2)
  2 SSN-TAX-ID-IND         (A1)
  2 RELATED-ID-NUMBER      (N6)
  2 INT-TYPE               (A1)
  2 GROSS-AMT              (P9.2)
  2 INSURED                (A25)
  2 TYPE-1099              (A4)
  2 DATE-ENTERED           (N8)
  2 BANKRUPTCY-IND         (A1)
  2 CONTACT-ID             (N8)
  2 CLAIMANT-ID            (N8)
  2 FINAL-PAYMENT-AMT      (P9.2)
  2 FINAL-PAYMENT-DATE     (N8) 2 REDEFINE FINAL-PAYMENT-DATE
    3 FP-YYYY   (A4)
    3 FP-MM     (A2)
    3 FP-DD     (A2)
  2 FINAL-PAYMENT-TID      (A20)
  2 NAME-2                 (A25)
  2 SETTLEMENT-TYPE                /* TT1
  2 SETTLEMENT-DATE                /* TT1
  2 SHARE                          /* TT1
  2 POLICY-NUMBER                  /* TT1

1 INT-1099-V-LEG VIEW OF A-INT-1099
  2 SSN                    (N9)
  2 YEAR                   (N4)
  2 AMOUNT                 (P9.2)
  2 SSN-TAX-ID-IND         (A1)
  2 RELATED-ID-NUMBER      (N6)
  2 INT-TYPE               (A1)
  2 GROSS-AMT              (P9.2)
  2 INSURED                (A25)
  2 TYPE-1099              (A4)
  2 DATE-ENTERED           (N8)
  2 BANKRUPTCY-IND         (A1)
  2 CONTACT-ID             (N8)
  2 CLAIMANT-ID            (N8)
  2 FINAL-PAYMENT-AMT      (P9.2)
  2 FINAL-PAYMENT-DATE     (N8) 2 REDEFINE FINAL-PAYMENT-DATE
    3 FP-YYYY   (A4)
    3 FP-MM     (A2)
    3 FP-DD     (A2)
  2 FINAL-PAYMENT-TID      (A20)
  2 NAME-2                 (A25)
  2 SETTLEMENT-TYPE                /* TT1
  2 SETTLEMENT-DATE                /* TT1
  2 SHARE                          /* TT1
  2 POLICY-NUMBER                  /* TT1


1 C-V VIEW OF A-CONTACTS
  2 SSN
1 #LETTER          (A7) CONST <'!LETTER'>   /* bz-Name-C
1 Letter           (A) DYNAMIC
1 #NAME            (A50)
1 #INSURED         (A50)
1 #SCR-MSG         (A60)
1 #SCR-ID          (A9)
1 #SCR-CV          (C)
1 #SCR-DEL-CV      (C)
1 #SCR-R-U-SURE-CV (C)
1 #SCR-DEL-MSG      (A29)
1 #SCR-R-U-SURE-MSG (A29)
1 #SCR-DEL-COD      (A1)
1 #SCR-R-U-SURE-COD (A1)
1 #FIR-ISN          (P10)             /* TT1
1 #MOD-CV  (C)                        /* TT1
1 #MOD-LEG-CV    (C)
*
1 #INT-1099-KEY     (A13)
1 REDEFINE #INT-1099-KEY
  2 #INT-1099-SSN         (N9)
  2 #INT-1099-YYYY        (N4)
  2 REDEFINE #INT-1099-YYYY
    3 #INT-1099-CENT        (N2)
    3 #INT-1099-YY          (A2)
*
1 #YEAR-INT-TYPE-ID-KEY   (A14)
1 REDEFINE #YEAR-INT-TYPE-ID-KEY
  2 #YEAR-INT-YEAR            (N4)
  2 #YEAR-INT-TYPE            (A1)
  2 #YEAR-INT-SSN             (N9)
*
1 I-V-1099 VIEW OF A-INT-1099
  2 YEAR-INT-TYPE-ID-KEY
*
1 FIRS-V VIEW OF A-FIRS               /* TT1 BEGIN
  2 ID-NUMBER (N6.0) /*
  2 C*POL-BENEFIC (1:1)
  2 C*POLICY-TABLE
  2 POLICY-TABLE (1:45) /*PE
    3 POL-BENEFIC (A20/1:3) /*MU-FIELD
  2 POL-MODE (A1/1:45) /* PE
  2 POL-712 (A1/1:45) /* PE
  2 POL-CODE (A1/1:45) /* PE
  2 POL-NAME (A19/1:45) /* PE
  2 POL-NUMBER (A14/1:45) /* PE
  2 POL-TYPE (A4/1:45) /* PE
  2 POL-REMARKS (A21/1:45) /* PE
  2 POL-AMOUNT (P7.0/1:45) /* PE
  2 POL-DATE (N8.0/1:45) /* PE
  2 POL-AMT-PAID (P7.2/1:45) /* PE
  2 POL-DATE-SENT-TO-WIDOW (N8.0/1:45) /* PE
  2 POL-DATE-CLM-FILED (N8.0/1:45) /* PE
  2 POL-DATE-CLM-SETTLED (N8.0/1:45) /* PE
  2 POL-INSTAL (P7.2/1:45) /* PE
  2 CONTESTABLE-IND (A1/1:45) /* PE
  2 DATE-TIME-STAMP /* GROUP
    3 DTS-DATN (P9.0)
    3 DTS-TIMN (P7.0)
  2 POLICY-FIELDS /* GROUP
    3 INS-LAST-FM-DATE (N8.0)
    3 BENEFIT-PAID (P7.2) /*
    3 DATE-PAID (N8.0) /*
    3 CHECK-NUMBER (N6.0) /*
    3 PAID-BY (A4) /*              /* TT1 END
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL /* TT2 begin
  2 ID-NUMBER
  2 ENTRY-DATE
  2 REC-ISN
  2 CODE
  2 ISSUE-DATE
  2 NUM-MBRSHIPS
  2 USER
  2 USER-COMPANY                               /* TT2 End
*
1 #TODAY-YYYYMMDD         (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY           (N4)
  2 #TODAY-MM             (N2)
  2 #TODAY-DD             (N2)
*
1 #YYYYMMDD               (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY                 (A4)
  2 #MM                   (A2)
  2 #DD                   (A2)
*
1 #MMDDYYYY               (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-2    (N2)
  2 #DD-2    (N2)
  2 #YYYY-2  (N4)
1 #REC-NOT-FOUND          (L)
1 #EDIT-FLAG              (A1)
1 #ISN                    (P8)
1 #HOLD-INT               (A1)
1 #HOLD-STRING (A8)
1 #SCN-INT-TYPE           (A1)
1 #FIR-SEGMENT            (I2)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 ISSUE-DATE   (9)
  2 POLICY-ID    (9)
*
1 I (I4)
1 #SUFFIX    (A3)
1 REDEFINE #SUFFIX
  2 #SUF-C   (A1) /* for copy to hew year
  2 #SUF-YY  (N2) /* year to copy to
*
1 #SCR-SETTLEMENT-DATE (A08)                      /* TT1 BEGIN
1 #SCR-SETTLEMENT-OPTION (A002)
1 #SETTLEMENT-OPTION-N1  (N1)
1 #FACE-AMOUNT                (A10)
1 #DEATH-BENEFIT              (A12)
1 #POLICY-TYPE                (A6)
1 #SCR-DB (A012)
1 #SCR-TYPE (A006)
1 #DEATH-CLAIMS     (A25)
1 #CLAIMANT-CI              (N8)
1 #DEAD-INSURED-CN          (N6)
1 #SCR-DB-TOTAL             (N9.2)
1 #SCR-INT-AMOUNT           (N9.2)
1 #TOTAL-SETTLED            (I2)
1 #TOTAL-PENDING            (I2)
1 #INDEX                    (I4)
1 #SEGMENT      (I4)
1 #HAS-CONTESTABILITY   (L)                    /* DW4
1 #NO-LONGER-CONTESTABLE (L)         /* DW5
1 #CONTESTABLE-BUT-PAID  (L)
* 1 #CODE-DESCRIPTION (A24)
* 1 #CODE       (A2)
1 #SUB-KEY-A  (A3)
1 #CODE-TEXT  (A24)
1 #CODE       (A2)
1 #CLAIMANT-ID   (N8)
1 #ORIG-CN       (N6)
1 #POLICIES   (A16/1:9)
1 K (I4)
1 #STRING  (A60)
1 #RESPONSE   (A1)
1 #ALL-POLICIES  (L)
1 #FINAL-PAYMENT (N8)
1 #SET-DATE      (N8)
1 #SET-CODE      (A1)
1 #CHECK-NUM     (A20)
1 #ALL-SETTLED   (L)
1 L              (I4)                    /* TT1 END
1 #USE-OLD-FORMAT   (L)
END-DEFINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
INCLUDE GONERCXX       /* NATURAL ERROR ROUTINE
*
DEFINE WINDOW CONFIRMATION   /* TT1 BEGIN
  SIZE 6*50
  BASE 10/20
  TITLE 'Confirmation'
  FRAMED ON                  /* TT1 END
*
MOVE *DATN       TO #TODAY-YYYYMMDD
MOVE (AD=P) TO #SCR-CV
MOVE (AD=P) TO #SCR-DEL-CV
MOVE (AD=P) TO #SCR-R-U-SURE-CV
MOVE (AD=U) TO #MOD-CV #MOD-LEG-CV
MOVE #CGA-SCR-ID TO #SCR-ID
IF  #CGA-SCR-ID NE MASK (NNNNNN) OR #CGA-SCR-ID = 0
  INPUT TEXT 'Invalid Recipient"s ID NUMBER - Must enter 6 Digits'
    MARK *#CGA-SCR-ID USING MAP 'C2911MTB'                             /* TT1
  FETCH 'G1000PXX'
END-IF
*
IF   (#CGA-SCR-SUFFIX NE ' ')
    AND (#CGA-SCR-SUFFIX NE MASK (NN))
  INPUT TEXT 'INVALID YEAR - MUST BE YY'
    MARK *#CGA-SCR-SUFFIX USING MAP 'C2911MTB'                           /* TT1
  FETCH 'G1000PXX'
END-IF
*
IF #CGA-SCR-SUFFIX = ' '
  MOVE #TODAY-YYYY TO #INT-1099-YYYY
ELSE
  MOVE #CGA-SCR-SUFFIX TO #INT-1099-YY
*
  IF #INT-1099-YY GT '50'
    MOVE 19 TO #INT-1099-CENT
  ELSE
    MOVE 20 TO #INT-1099-CENT
  END-IF
END-IF
*
CALLNAT 'C1099SXX'                   /* TT1
  #CGA-SCR-COMMAND
  #CGA-SCR-ID
  #CGA-SCR-SUFFIX
  #INT-1099-KEY
  #YEAR-INT-TYPE-ID-KEY
  #ISN
  #FACE-AMOUNT
  #DEATH-BENEFIT
  #POLICY-TYPE
  #CLAIMANT-CI
  #DEAD-INSURED-CN
  #SEGMENT
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
RESET #USE-OLD-FORMAT
IF #ISN <= 0
  MOVE TRUE TO #REC-NOT-FOUND
ELSE
  XX. GET INT-1099-V #ISN                                        /* TT1
  #CLAIMANT-ID := INT-1099-V.CLAIMANT-ID                         /* TT1
  #ORIG-CN     := INT-1099-V.RELATED-ID-NUMBER                   /* TT1
  IF INT-TYPE = '2' OR = 'C' OR = 'M' OR = 'N' OR = 'P'
      OR = 'L'
    MOVE 'MUST USE "TC" COMMAND FOR THIS RECORD' TO #SCR-MSG
    MOVE 'TC' TO #CGA-SCR-COMMAND
    INPUT TEXT #SCR-MSG USING MAP 'G1000MXX'
    FETCH 'G1000PXX'
  ELSE
    MOVE INT-1099-V.INT-TYPE       TO #HOLD-INT
    MOVE 'ENTER UPDATED 1099 INFO' TO #SCR-MSG
    #SCR-DB     := #DEATH-BENEFIT                                /* TT1 BEGIN
    #SCR-TYPE   := #POLICY-TYPE
    PERFORM GET-CLAIMANT-INFO
  END-IF
END-IF
*
* GET INT-1099-V #ISN
MOVE (AD=U) TO #SCR-CV
MOVE '*' TO #CGA-SCR-COMMAND
IF NOT #REC-NOT-FOUND
  IF INT-1099-V.FINAL-PAYMENT-AMT = 0
    INT-1099-V.FINAL-PAYMENT-AMT := INT-1099-V.AMOUNT
  END-IF
  #FP-YYYY := FP-YYYY
  #FP-MM   := FP-MM
  #FP-DD   := FP-DD
  PERFORM TRANSLATE-SET-OPTIONS-2
  INPUT TEXT #SCR-MSG MARK *SSN-TAX-ID-IND  USING MAP 'C2911MTB'
  PERFORM CHECK-IF-MODIFIED
*
ELSE
  RESET #SCR-DB-TOTAL
    #SCR-INT-AMOUNT
  FIND INT-1099-V WITH RELATED-ID-NUMBER = #DEAD-INSURED-CN
      AND CLAIMANT-ID = #CLAIMANT-CI
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
    IF FINAL-PAYMENT-DATE <= 20151224 AND FINAL-PAYMENT-DATE > 0
      #ISN := *ISN
      #FP-YYYY := FP-YYYY
      #FP-MM   := FP-MM
      #FP-DD   := FP-DD
      #USE-OLD-FORMAT := TRUE
      ESCAPE BOTTOM
    END-IF
    IF SETTLEMENT-DATE = 0
      ADD 1 TO #TOTAL-PENDING
    ELSE
      ADD 1 TO #TOTAL-SETTLED
      ADD INT-1099-V.GROSS-AMT TO #SCR-DB-TOTAL
      ADD INT-1099-V.AMOUNT TO #SCR-INT-AMOUNT
    END-IF
  END-FIND
  PERFORM GET-CLAIMANT-INFO
  IF #USE-OLD-FORMAT
    INPUT TEXT #SCR-MSG MARK *SSN-TAX-ID-IND  USING MAP 'C2912MTB'
    #CGA-SCR-COMMAND := '*'
    MOVE BY NAME INT-1099-V TO INT-1099-V-LEG
  ELSE
    COMPRESS #TOTAL-SETTLED 'Settled, ' #TOTAL-PENDING 'Pending***' INTO #DEATH-CLAIMS
    INPUT TEXT #SCR-MSG MARK *SSN-TAX-ID-IND  USING MAP 'C2910MTB'
  END-IF
END-IF
IF #CGA-SCR-COMMAND NE '*'
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
*
MOVE #CGA-SCR-SUFFIX TO #SUFFIX
IF (#SUF-C = 'C') AND (#SUF-YY = MASK (NN))
  COMPUTE YEAR = #SUF-YY + 1900
  IF YEAR LT 50
    ADD 100 TO YEAR
  END-IF
  STORE INT-1099-V
  COMPRESS '1099 record copied to year' YEAR  TO #SCR-MSG
ELSE
  IF NOT #REC-NOT-FOUND
*     UPDATE (0304)
    MOVE '1099 RECORD UPDATED'   TO #SCR-MSG
  END-IF
END-IF
*
END TRANSACTION
*
MOVE (AD=P) TO #SCR-CV
MOVE (AD=P) TO #SCR-DEL-CV
MOVE (AD=P) TO #SCR-R-U-SURE-CV
*
MOVE 'TB' TO #CGA-SCR-COMMAND
IF #REC-NOT-FOUND
  IF #USE-OLD-FORMAT
    PERFORM CHECK-IF-MODIFIED
    END TRANSACTION
    GET INT-1099-V #ISN
    INPUT TEXT #SCR-MSG MARK *SSN-TAX-ID-IND  USING MAP 'C2912MTB'
  ELSE
    INPUT TEXT #SCR-MSG  USING MAP 'C2910MTB'
  END-IF
ELSE
  GET INT-1099-V #ISN
  INPUT TEXT #SCR-MSG USING MAP 'C2911MTB'
END-IF
*
FETCH 'G1000PXX'
*
DEFINE SUBROUTINE CHECK-IF-MODIFIED
* ---------------------------------
IF #MOD-CV MODIFIED
  PERFORM VALIDATE-INPUT
  #MMDDYYYY := #SCR-SETTLEMENT-DATE
  COMPRESS FULL #YYYY-2 #MM-2 #DD-2 INTO #HOLD-STRING LEAVING NO
  SETTLEMENT-DATE := VAL(#HOLD-STRING)
  PERFORM TRANSLATE-SET-OPTIONS-1
  #FINAL-PAYMENT  := VAL(#HOLD-STRING)
  #SET-DATE       := VAL(#HOLD-STRING)
  #SET-CODE       := INT-1099-V.SETTLEMENT-TYPE
  #CHECK-NUM      := INT-1099-V.FINAL-PAYMENT-TID
  UPDATE(XX.)
  PERFORM POP-UP
  IF #ALL-POLICIES
    FIND INT-1099-V WITH 'CLAIMAINTS-POLICIES'

*     CLAIMANT-ID = #CLAIMANT-ID
*         AND RELATED-ID-NUMBER = #ORIG-CN
      SETTLEMENT-TYPE     := #SET-CODE
      FINAL-PAYMENT-DATE  := #FINAL-PAYMENT
      SETTLEMENT-DATE     := #SET-DATE
      SETTLEMENT-DATE     := #SET-DATE
      FINAL-PAYMENT-TID   := #CHECK-NUM
      UPDATE
    END-FIND
    END TRANSACTION
  END-IF
  PERFORM CHECK-IF-ALL-SETTLED
END-IF
IF #MOD-LEG-CV MODIFIED
  PERFORM VALIDATE-INPUT
  ZZ. GET INT-1099-V #ISN
  MOVE BY NAME INT-1099-V-LEG TO INT-1099-V
  INT-1099-V.FP-YYYY := #FP-YYYY
  INT-1099-V.FP-MM:= #FP-MM
  INT-1099-V.FP-DD := #FP-DD
  UPDATE (ZZ.)
  MOVE '1099 RECORD UPDATED'   TO #SCR-MSG
END-IF
END-SUBROUTINE
* ------------

DEFINE SUBROUTINE CHECK-IF-ALL-SETTLED
* ------------------------------------
FIND INT-1099-V WITH RELATED-ID-NUMBER = #ORIG-CN
  IF POLICY-NUMBER <> ' '
    IF POLICY-NUMBER = #POLICIES(*)
      IGNORE
    ELSE
      ADD 1 TO K
      #POLICIES(K) := POLICY-NUMBER
    END-IF
  END-IF
END-FIND
*
FOR I 1 TO K
  #ALL-SETTLED := TRUE
  FIND INT-1099-V WITH RELATED-ID-NUMBER = #ORIG-CN
      AND INT-1099-V.POLICY-NUMBER = #POLICIES(I)
    IF NO RECORDS FOUND               /* TT2
      #ALL-SETTLED := FALSE           /* TT2
    END-NOREC                         /* TT2
    IF INT-1099-V.SETTLEMENT-DATE = 0
      RESET #ALL-SETTLED
      ESCAPE BOTTOM
    END-IF
  END-FIND
  IF #ALL-SETTLED
    PERFORM CHECK-FIR
  END-IF
END-FOR
END-SUBROUTINE
* ------------

DEFINE SUBROUTINE GET-CLAIMANT-INFO
* ---------------------------------
MOVE (AD=U) TO #SCR-DEL-CV
MOVE 'ENTER (D) TO DELETE RECORD  -' TO #SCR-DEL-MSG
Letter := #LETTER
CALLNAT 'GPRS0CNT' CLAIMANT-ID Letter
#NAME := Letter
Letter := #LETTER                     /* NG1
CALLNAT 'GPRS0CNT' CONTACT-ID Letter
#INSURED := Letter
RESET #PDA-ADDRESS
MOVE CLAIMANT-ID TO #ADD-CONTACT-ID
MOVE #PRIMARY TO #ADD-TYPE
CALLNAT 'ADDN0001' #PDA-ADDRESS
#YYYYMMDD := SETTLEMENT-DATE
COMPRESS #MM #DD #YYYY INTO #SCR-SETTLEMENT-DATE LEAVING NO
COMPRESS FULL #YYYY-2 #MM-2 #DD-2 INTO #HOLD-STRING LEAVING NO
FIND (1) C-V WITH CONTACT-ID = CLAIMANT-ID
END-FIND
END-SUBROUTINE
* ------------

DEFINE SUBROUTINE CHECK-FIR
* -------------------------
FIND(1) ST-V WITH MEMBER-KEY = #CGA-ID-G
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  EXAMINE FULL ST-V.POLICY-ID(*) FOR FULL #POLICIES(I) GIVING INDEX #SEGMENT  /* bz-HD209
END-FIND
*
FIND (1) FIRS-V WITH WIDOW-KEY = #CGA-ID-G
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  #FIR-ISN := *ISN
END-FIND
*
FOR L 1 TO 45
  IF ST-V.ISSUE-DATE(#SEGMENT) = POL-DATE(L)
    #FIR-SEGMENT := L
    ESCAPE BOTTOM
  END-IF
END-FOR
*
FIR.
GET FIRS-V #FIR-ISN
* 
FIRS-V.POL-DATE-CLM-SETTLED(#FIR-SEGMENT) := INT-1099-V.SETTLEMENT-DATE           /* TT2 BEGIN bz-HD209
UPDATE (FIR.)
ABC.                                     
FIND MBRSHIP-MTD-DET-V WITH MBRSHIP-MTD-DET-V.ID-NUMBER = ST-V.ID-NUMBER
  ACCEPT IF FIRS-V.POL-DATE-CLM-FILED(#FIR-SEGMENT) = ENTRY-DATE
  IF MBRSHIP-MTD-DET-V.ISSUE-DATE <> #SEGMENT
    ESCAPE TOP
  END-IF
  DELETE(ABC.)
END-FIND
END-SUBROUTINE                                                                     /* TT2 END
* ------------


DEFINE SUBROUTINE VALIDATE-INPUT
* ------------------------------
IF #SCR-SETTLEMENT-DATE <> '00000000' AND #SCR-SETTLEMENT-DATE <> ' '
  IF #SCR-SETTLEMENT-DATE NE MASK (MMDDYYYY)
    #SCR-MSG := 'Invalid Date format.  Must be in form MMDDYYYY'
    REINPUT WITH #SCR-MSG MARK *#SCR-MSG
  END-IF
  IF #SCR-SETTLEMENT-OPTION = ' '
    #SCR-MSG := 'Must have a settlment option with a settlment date'
    REINPUT WITH #SCR-MSG MARK *#SCR-SETTLEMENT-OPTION
  END-IF
END-IF

IF #SCR-SETTLEMENT-OPTION <> ' '
  IF #SCR-SETTLEMENT-OPTION  = 'A1' /* ANNUITY Life/1
      OR= 'A2' /* ANNUITY Life/2'
      OR= 'IO' /* Interest Only'
      OR= 'LA' /* Life Annuity'
      OR= 'LT' /* Long Term Care'
      OR= 'L1' /* LA Certain 10 years'
      OR= 'L2' /* LA certain 15 years'
      OR= 'L3' /* LA Certain 20 years'
      OR= 'LS'
      OR= ' '
    IGNORE
  ELSE
    #SCR-MSG := 'Invalid Settlement option'
    REINPUT WITH #SCR-MSG MARK *#SCR-SETTLEMENT-OPTION
  END-IF
END-IF



IF NOT SSN-TAX-ID-IND = 'S' OR = 'T'
  REINPUT WITH 'SSN/TAX-ID IND MUST BE S OR T'
    MARK *SSN-TAX-ID-IND
END-IF
*
IF NOT INT-TYPE = 'A' OR = 'I' OR = 'E' OR = 'D'
  REINPUT USING HELP
    MARK *INT-TYPE
END-IF
*
IF INT-TYPE = 'A'
  IF AMOUNT > 0
    IGNORE
  ELSE
    REINPUT WITH 'WHEN INT TYPE A AN AMOUNT MUST BE ENTERED'
      MARK *AMOUNT
  END-IF
END-IF
*
IF #FINAL-PAYMENT-DATE > 0
  IF #FINAL-PAYMENT-DATE-A NE MASK (MMDDYYYY)
    REINPUT WITH 'PLEASE ENTER FINAL PAYMENT DATE IN MMDDYYYY FORMAT'
      MARK *#FINAL-PAYMENT-DATE-A
  END-IF
  FP-YYYY := #FP-YYYY
  FP-MM:= #FP-MM
  FP-DD := #FP-DD
ELSE
  RESET FINAL-PAYMENT-DATE
END-IF
*
END-SUBROUTINE
* ------------


DEFINE SUBROUTINE TRANSLATE-SET-OPTIONS-1
* --------------------------------------------
DECIDE ON FIRST VALUE OF #SCR-SETTLEMENT-OPTION
  VALUE 'A1'
    SETTLEMENT-TYPE := 7
  VALUE 'A2'
    SETTLEMENT-TYPE := 9
  VALUE 'IO'
    SETTLEMENT-TYPE := 5
  VALUE 'L1'
    SETTLEMENT-TYPE := 2
  VALUE 'L2'
    SETTLEMENT-TYPE := 3
  VALUE 'L3'
    SETTLEMENT-TYPE := 4
  VALUE 'LA'
    SETTLEMENT-TYPE := 1
  VALUE 'LT'
    SETTLEMENT-TYPE := 8
  VALUE 'LS'
    SETTLEMENT-TYPE := 6
  NONE VALUE
    SETTLEMENT-TYPE := 0
END-DECIDE

* ------------
END-SUBROUTINE

DEFINE SUBROUTINE TRANSLATE-SET-OPTIONS-2
* -----------------------------------------------
DECIDE ON FIRST VALUE SETTLEMENT-TYPE
  VALUE '7'
    #SCR-SETTLEMENT-OPTION := 'A1'
  VALUE '9'
    #SCR-SETTLEMENT-OPTION := 'A2'
  VALUE '5'
    #SCR-SETTLEMENT-OPTION := 'IO'
  VALUE '2'
    #SCR-SETTLEMENT-OPTION := 'L1'
  VALUE '3'
    #SCR-SETTLEMENT-OPTION := 'L2'
  VALUE '4'
    #SCR-SETTLEMENT-OPTION := 'L3'
  VALUE '1'
    #SCR-SETTLEMENT-OPTION := 'LA'
  VALUE '8'
    #SCR-SETTLEMENT-OPTION := 'LT'
  VALUE '6'
    #SCR-SETTLEMENT-OPTION := 'LS'
  NONE VALUE
    #SCR-SETTLEMENT-OPTION := ' '
END-DECIDE
END-SUBROUTINE
* ------------


DEFINE SUBROUTINE POP-UP
* ----------------------
NUM. FIND NUMBER INT-1099-V WITH CLAIMANT-ID = #CLAIMANT-ID
  AND RELATED-ID-NUMBER = #ORIG-CN
  RETAIN AS 'CLAIMAINTS-POLICIES'
IF ( *NUMBER(NUM.) > 1 )
  INPUT WINDOW = 'Confirmation'
    '  Do you wish to apply these Settlement options  ' /
    '         accross ALL Claimants policies:   '       /
    '                (Y/N) '#RESPONSE (AD=UMT)
  IF #RESPONSE = 'N'
    RESET #ALL-POLICIES
  ELSE
    #ALL-POLICIES := TRUE
  END-IF
  SET WINDOW OFF
END-IF
END-SUBROUTINE
* ------------                                       /* TT1 END



END
