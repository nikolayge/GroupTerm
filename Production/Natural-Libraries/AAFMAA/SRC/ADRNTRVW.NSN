* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A/1:V) DYNAMIC
LOCAL USING ADDA0001
LOCAL
1 #J     (I4)
1 A-ADD-POOL VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID (N8)
  2 STREET          (A75)
  2 UNIT            (A25)
  2 CITY            (A50)
  2 STATE-CODE      (A2)
  2 ZIP-CODE        (N9) 2 REDEFINE ZIP-CODE
    3 ZIP-5         (N5)
    3 ZIP-4         (N4)
  2 COUNTRY-CODE    (A2)
  2 INTL-REGION     (A50)
  2 POSTAL-CODE     (A25)
  2 URBANIZATION    (A50)
  2 ADDR-FORMAT-TYPE-CD (A1)
  2 ADDR-VALID-IND      (A1)

1 A-ADD-REL VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID        (N8)
  2 ADDRESS-POOL-ID   (N8)
  2 ADDR-TYPE-CD      (A1)
  2 RETURNED-MAIL-IND (A1)
  2 PREVENT-OVER-IND  (A1)
  2 ADDR-LOCATION     (A1)
  2 ADDRESS-SOURCE    (A1)
  2 STATUS            (A1)
  2 STATUS-DATE       (N8) 2 REDEFINE STATUS-DATE
    3 STATUS-DATE-A   (A8)
1 #ADDR-LOCATION-TXT  (A20)
1 #ADDR-RETURNED-TXT  (A20)
1 #ADDR-TYPE-TXT      (A20)
1 #ADDR-VALID-TXT     (A20)
1 #ADDR-PROTECT-TXT   (A20)
1 #CODE-NAME          (A) DYNAMIC
1 #STATUS-DESC        (A) DYNAMIC
1 #SOURCE-DESC        (A) DYNAMIC
1 #PURGE-DATE         (A10)
1 #LINE               (A67) 1 REDEFINE #LINE
  2 LINE-LP (A1/67)
1 #D                  (D)
1 BLANK               (A1) CONST <' '>
1 #CASS               (A5) CONST <' CASS'>
END-DEFINE
*
RESET #TEXT (*)
GET A-ADD-POOL #ISN
*
FIND (1) A-ADD-REL WITH CONTACT-ID = #CONTACT-ID
    AND ADDRESS-POOL-ID = A-ADD-POOL.ADDRESS-POOL-ID
END-FIND
*
IF A-ADD-REL.STATUS NE 'D'
  #ADD-CONTACT-ID := #CONTACT-ID
  #ADD-TYPE       := ADDR-TYPE-CD
  #ADD-POOL-ID    := A-ADD-POOL.ADDRESS-POOL-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  #TEXT  (1) := #ADD-LINE-1
  #TEXT  (2) := #ADD-LINE-2
  #TEXT  (3) := #ADD-LINE-3
*
  #CODE-NAME := 'ADDR-LOCATION'
#ADDR-LOCATION-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,A-ADD-REL.ADDR-LOCATION>)
*
 #SOURCE-DESC := FUNCTION-CODE-1-DESC (<'017',A-ADD-REL.ADDRESS-SOURCE>)
  #STATUS-DESC := FUNCTION-CODE-1-DESC (<'018',A-ADD-REL.STATUS>)
  IF A-ADD-REL.RETURNED-MAIL-IND = 'Y'
    #ADDR-RETURNED-TXT  := '<Undeliverable>'
  END-IF
  #CODE-NAME    := 'ADDR-TYPE'
  #ADDR-TYPE-TXT :=
    FUNCTION-ADDR-CODES(<#CODE-NAME,A-ADD-REL.ADDR-TYPE-CD>)
  DECIDE ON FIRST VALUE OF A-ADD-POOL.ADDR-VALID-IND
    VALUE 'Y'
      #ADDR-VALID-TXT := #CASS
    VALUE 'N'
      COMPRESS 'No' #CASS into #ADDR-VALID-TXT
    VALUE 'F'
      COMPRESS '<Failed' #CASS '>' into #ADDR-VALID-TXT LEAVING NO
    NONE VALUE RESET #ADDR-VALID-TXT
  END-DECIDE
  DECIDE ON FIRST VALUE OF A-ADD-REL.PREVENT-OVER-IND
    VALUE 'Y'
      #ADDR-PROTECT-TXT := 'Protected'
    VALUE 'S'
      #ADDR-PROTECT-TXT := 'Secured'
    NONE VALUE RESET #ADDR-PROTECT-TXT
  END-DECIDE
*
  IF #ADDR-TYPE-TXT NE BLANK
      OR #ADDR-LOCATION-TXT NE BLANK
      OR #ADDR-RETURNED-TXT NE BLANK
      OR #ADDR-VALID-TXT    NE BLANK
    COMPRESS
      #ADDR-TYPE-TXT
      #ADDR-LOCATION-TXT
      INTO #LINE
    COMPRESS '(' #LINE ')' INTO #LINE LEAVING NO
    COMPRESS #TEXT (1) #LINE INTO #TEXT (1)
    COMPRESS
      #ADDR-PROTECT-TXT
      #ADDR-RETURNED-TXT
      #ADDR-VALID-TXT
      INTO #LINE
    COMPRESS '(' #LINE ')' INTO #LINE LEAVING NO
    COMPRESS #TEXT (2) #LINE INTO #TEXT (2)
  END-IF
  IF #STATUS-DESC NE BLANK
      OR #SOURCE-DESC NE BLANK
    COMPRESS '(' #STATUS-DESC #SOURCE-DESC ')' INTO #LINE LEAVING NO
    COMPRESS #TEXT (3) #LINE INTO #TEXT (3)
  END-IF
IF *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA'
    COMPRESS
      #TEXT  (3)
      '{'
      ADDRESS-POOL-ID
      '}'
      INTO #TEXT  (3)
  END-IF
END-IF
*
END
