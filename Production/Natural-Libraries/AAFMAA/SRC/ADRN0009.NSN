* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRN0009
* DESCRIPTION: CLONE OF ADRN0003 - USED FOR RANK MAINTENANCE
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RCE  08/27/2010  None Creation
* NGG  07/13/2011  NG1  Default rulles
DEFINE DATA
PARAMETER
1 #CONTACT-ID    (N8)
1 #RANK-ID       (N6)
1 #ID-NUMBER     (N6)
1 #PAY-GRADE     (A3)
1 #SPC-PAY-IND   (A1)
1 #CODES
  2  #RANK-MIL-ID      (A3)
  2  #RANK-STATUS      (A1)
  2  #RANK-ABBR        (A10)
  2  #RANK-PROTECT     (A1)
1 #CODE-NAMES
  2  #RANK-MIL-ID-TXT      (A50)
  2  #RANK-STATUS-TXT      (A64)
  2  #RANK-ABBR-TXT        (A20)
  2  #RANK-PROTECT-TXT     (A20)
LOCAL
1 #CODE-NAME         (A) DYNAMIC
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 MILITARY-ID
  2 RANK-DESCRIPTION
  2 RANK-ABBR
  2 PAY-GRADE
  2 SPECIAL-PAY-IND
  2 MESSAGE-NBR
*
1 STATUS-V VIEW OF A-STATUS
  2 MILITARY-STATUS
*
1 CONTACT-V VIEW OF A-CONTACTS
  2 RANK-PROTECT
  2 MILITARY-STATUS
*
1 #RANK-PARM  (A10)
1 BLANK       (A1)  CONST <' '>
END-DEFINE
*
FIND STATUS-V WITH ID-NUMBER = #ID-NUMBER
END-FIND
FIND CONTACT-V WITH CONTACT-ID = #CONTACT-ID
END-FIND
*
*  'RANK STATUS',             /* 25
*
#RANK-PARM := CONTACT-V.MILITARY-STATUS
#CODE-NAME   := 'RANK-STATUS'
#RANK-STATUS-TXT := FUNCTION-RANK-CODES(<#CODE-NAME,#RANK-PARM>)
MOVE #RANK-PARM TO #RANK-STATUS
*
*  'RANK MILITARY ID',        /*  24
*
IF #RANK-STATUS = 'C'
    AND #RANK-MIL-ID = BLANK
  #RANK-MIL-ID := 'V'
  IF #RANK-ID = 0
    #RANK-ID := 1616
  END-IF
END-IF
#RANK-PARM := #RANK-MIL-ID
#CODE-NAME     := 'RANK-MIL-ID'
#RANK-MIL-ID-TXT := FUNCTION-RANK-CODES(<#CODE-NAME,#RANK-PARM>)
MOVE #RANK-PARM TO #RANK-MIL-ID
*
*  'RANK ABBR',            /* 26
*
IF #RANK-ID > 0
  FIND RANK-V WITH RANK-ID = #RANK-ID
  END-FIND
END-IF
#RANK-ABBR-TXT := RANK-V.RANK-DESCRIPTION
MOVE RANK-V.RANK-ABBR TO #RANK-ABBR
*
*  'RANK PROTECT',        /* 27
*
#RANK-PARM := CONTACT-V.RANK-PROTECT
#CODE-NAME    := 'RANK-PROTECTION'
#RANK-PROTECT-TXT := FUNCTION-RANK-CODES(<#CODE-NAME,#RANK-PARM>)
MOVE #RANK-PARM TO #RANK-PROTECT
IF #RANK-ID > 0
  MOVE RANK-V.PAY-GRADE TO #PAY-GRADE
  IF MESSAGE-NBR = 0
    MOVE RANK-V.SPECIAL-PAY-IND TO #SPC-PAY-IND
  END-IF
END-IF
*
END
