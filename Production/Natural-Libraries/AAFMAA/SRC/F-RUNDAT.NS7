* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* FUNCTION-RUN-DATE
* Saves and retreives information about last run of the process
* on the GEN-TABLE
* Process 'B' - Credit Card
* Action Types: W - save on the file
*               P - retrieve last run date in YYYYMMDD format
*               R - retrieve last run date in MMDDYYYY format
*******************************************************************
* MODIFICATION LOG
* USER   DATE      TAG     REASON
*******************************************************************                                                                      *
* YAK    09012008          Create program
*******************************************************************
* YAK    02162010  YK1   Save USER-ID with the request date
*****************************************************************
DEFINE FUNCTION FUNCTION-RUN-DATE RETURNS (A8)
DEFINE DATA
PARAMETER
1 #RUN-DATE (N8)
1 #ACTION-TYPE (A1)
1 #PROCESS-TYPE (N2)
LOCAL USING OBJLGENT
LOCAL
1 #SEC-KEY       (A7)
1 #LAST-RUN-INFO (A150)
1 REDEFINE #LAST-RUN-INFO
  2 #LAST-RUN-DATE (N8/4)
  2 #REQUEST-USER-ID (A8)
END-DEFINE
*
COMPRESS 'PROC' 'B' INTO #SEC-KEY LEAVING NO
F.
FIND(1) GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  DECIDE ON FIRST VALUE #ACTION-TYPE
     VALUE 'W'                     /* save new date
        #LAST-RUN-INFO := GEN-TABLE-V.ADDL-INFO
        #LAST-RUN-DATE(#PROCESS-TYPE) := #RUN-DATE
        #REQUEST-USER-ID              := *USER         /* YK1
        GEN-TABLE-V.ADDL-INFO         := #LAST-RUN-INFO
        UPDATE (F.)
     VALUE 'R'                     /* retrieve saved date
        #LAST-RUN-INFO := GEN-TABLE-V.ADDL-INFO
        #RUN-DATE := #LAST-RUN-DATE(#PROCESS-TYPE)
        FUNCTION-RUN-DATE := #REQUEST-USER-ID          /* YK1
     VALUE 'S'                     /* reset dates
        RESET GEN-TABLE-V.ADDL-INFO
        UPDATE (F.)
     NONE VALUE
        IGNORE
  END-DECIDE
  END TRANSACTION
  ESCAPE ROUTINE
END-FIND
*
END-FUNCTION
END
