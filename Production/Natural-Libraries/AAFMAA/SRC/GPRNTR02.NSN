* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************
* SUBPROGRAM-ID: GPRNTR02 - TRANSACTION LOG GENERIC MODULE 3
*                           Insurance transactions
***************************************************************
*        MODIFICATION LOG
***************************************************************
* USER   DATE      TAG  REASON
* YAK    10222012       Initial Creation (for Annual Renewable)
* YAK    02252017  YK1  Add MODE-CHANGE-TRAN and DISCLAIMER-TRAN method
* YAK    05252017  YK2  Add PAYER-CHANGE-TRAN method
***************************************************************
*
DEFINE DATA
PARAMETER
1 #FUNCTION         (A) DYNAMIC
PARAMETER USING GPRATR02
LOCAL USING GPRA0004       /* PDA to pass data to TR log subprogram
LOCAL USING GPRL0004
LOCAL
1 #LOG-AR-CONV-TRAN        (A) DYNAMIC CONST <'ARConversion'>
1 #LOG-EZ-RENEWAL-TRAN     (A) DYNAMIC CONST <'EZRenewal'>
1 #MODE-CHANGE-TRAN        (A) DYNAMIC CONST <'ModeChange'>    /* YK1
1 #DISCLAIMER-TRAN         (A) DYNAMIC CONST <'Disclaimer'>    /* YK1
1 #PAYER-CHANGE-TRAN       (A) DYNAMIC CONST <'PayerChange'>   /* YK2
1 #Fixed                   (A1) CONST <'F'>
1 #Variable                (A1) CONST <'V'>
1 #CAP                     (A1) CONST <'C'>
1 #LOOK-UP-KEY      (A7)
1 #LOOK-UP-VALUE    (A1)
1 TR_DESCRIPTION    (A) DYNAMIC
1 #WORK             (A) DYNAMIC
1 #I  (I2)
1 #II (I2)
*
END-DEFINE
*
DECIDE ON FIRST VALUE #FUNCTION
  VALUE #LOG-AR-CONV-TRAN
*   TR_DESCRIPTION := #CONVERSION
    PERFORM LOG-AR-CONV-TRAN
  VALUE #LOG-EZ-RENEWAL-TRAN
    TR_DESCRIPTION := #CONVERSION
    PERFORM LOG-EZ-RENEWAL-TRAN
  VALUE #MODE-CHANGE-TRAN                      /* YK1
    RESET TR_DESCRIPTION
    PERFORM LOG-MODE-CHANGE-TRAN
  VALUE #DISCLAIMER-TRAN                       /* YK1
    RESET TR_DESCRIPTION
    PERFORM LOG-DISCLAIMER-TRAN
  VALUE #PAYER-CHANGE-TRAN                     /* YK2
    RESET TR_DESCRIPTION
    PERFORM LOG-PAYER-CHANGE
  NONE VALUE
    IGNORE
END-DECIDE
*
PERFORM WRITE-TRAN-LOG
*
********************************
DEFINE SUBROUTINE WRITE-TRAN-LOG
********************************
IF #I NE 0
  #TR-NAME               := TR_NAME
  #TR-ID-NUMBER          := TR_ID_NUMBER
  #LOG-DESCRIPTION       := TR_DESCRIPTION
  #TR-CLERK-ID           := TR_CLERK
  #TR-DATE-LAST-UPDATE   := BEFORE_IMAGE.DATE-LAST-UPDATE
  #NEW-DATA-TAG(1)       := TR_DESCRIPTION
  CALLNAT 'GPRN0004'
    #TR-FIELDS #SYNC-FIELD-TABLE(*)
    #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
END-IF
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE LOG-AR-CONV-TRAN
**********************************
*
#I := #I + 1
#FIELD-DESCRIPTION(#I) := #CONVERSION
COMPRESS 'Issue Date' BEFORE_IMAGE.ISSUE-DATE 'Plan' BEFORE_IMAGE.PLAN
  'Face Amount'  BEFORE_IMAGE.FACE-AMT TO #OLD-DATA(#I)
COMPRESS 'Issue Date' AFTER_IMAGE.ISSUE-DATE 'Plan' AFTER_IMAGE.PLAN
  'Face Amount'  AFTER_IMAGE.FACE-AMT TO #NEW-DATA(#I)
#I := #I + 1
#FIELD-DESCRIPTION(#I) := #CONVERSION
COMPRESS 'Mode' BEFORE_IMAGE.MODE 'Monthly Premium'  BEFORE_IMAGE.MONTHLY-PREM TO #OLD-DATA(#I)
COMPRESS 'Mode' AFTER_IMAGE.MODE 'MOnthly Premiun'  AFTER_IMAGE.MONTHLY-PREM TO #NEW-DATA(#I)
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE LOG-EZ-RENEWAL-TRAN
*************************************
*
#I := #I + 1
#FIELD-DESCRIPTION(#I) := #CONVERSION
COMPRESS 'ISSUE DATE ' BEFORE_IMAGE.ISSUE-DATE ' PLAN ' BEFORE_IMAGE.PLAN
  '  FACE AMOUNT  '  BEFORE_IMAGE.FACE-AMT '  MODE ' BEFORE_IMAGE.MODE
  '  MONTHLY-PREMIUM '  BEFORE_IMAGE.MONTHLY-PREM TO #OLD-DATA(#I) LEAVING NO
COMPRESS 'ISSUE DATE ' AFTER_IMAGE.ISSUE-DATE ' PLAN ' AFTER_IMAGE.PLAN
  '  FACE AMOUNT  '  AFTER_IMAGE.FACE-AMT '  MODE ' AFTER_IMAGE.MODE
  '  MONTHLY-PREMIUM '  AFTER_IMAGE.MONTHLY-PREM TO #NEW-DATA(#I) LEAVING NO
END-SUBROUTINE
*
**************************************
DEFINE SUBROUTINE LOG-MODE-CHANGE-TRAN    /* YK1
**************************************
*
DECIDE FOR FIRST CONDITION
  WHEN BEFORE_IMAGE.LOAN-TYPE = #Fixed OR BEFORE_IMAGE.LOAN-TYPE = #Variable
    IF BEFORE_IMAGE.MODE <> ' ' OR AFTER_IMAGE.MODE <> ' '
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #MODE-CHANGE
      #WORK := FUNCTION-LOAN-PAYMODE(<BEFORE_IMAGE.MODE>)(1)
      COMPRESS BEFORE_IMAGE.POLICY-NUMBER #WORK TO #OLD-DATA(#I)
      #WORK := FUNCTION-LOAN-PAYMODE(<AFTER_IMAGE.MODE>)(1)
      COMPRESS AFTER_IMAGE.POLICY-NUMBER #WORK TO #NEW-DATA(#I)
    END-IF
    IF BEFORE_IMAGE.MONTHLY-PREM <> ' ' OR AFTER_IMAGE.MONTHLY-PREM <> ' '
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #MODE-CHANGE
      COMPRESS BEFORE_IMAGE.POLICY-NUMBER 'Monthly Payment' BEFORE_IMAGE.MONTHLY-PREM TO #OLD-DATA(#I)
      COMPRESS AFTER_IMAGE.POLICY-NUMBER 'Monthly Payment' AFTER_IMAGE.MONTHLY-PREM TO #NEW-DATA(#I)
    END-IF
    IF BEFORE_IMAGE.BILL-CYCLE <> AFTER_IMAGE.BILL-CYCLE
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #BILL-CYCLE-CHANGE
      COMPRESS BEFORE_IMAGE.POLICY-NUMBER 'Bill Cycle ' BEFORE_IMAGE.BILL-CYCLE TO #OLD-DATA(#I)
      COMPRESS AFTER_IMAGE.POLICY-NUMBER 'Bill Cycle ' AFTER_IMAGE.BILL-CYCLE TO #NEW-DATA(#I)
    END-IF
  WHEN BEFORE_IMAGE.LOAN-TYPE = #CAP
    #I := #I + 1
    #FIELD-DESCRIPTION(#I) := #MODE-CHANGE
    #WORK := FUNCTION-LOAN-PAYMODE(<BEFORE_IMAGE.MODE>)(1)
    COMPRESS 'CAP Loan Mode' #WORK TO #OLD-DATA(#I)
    #WORK := FUNCTION-LOAN-PAYMODE(<AFTER_IMAGE.MODE>)(1)
    COMPRESS 'CAP Loan Mode' #WORK TO #NEW-DATA(#I)
    IF BEFORE_IMAGE.BILL-CYCLE <> AFTER_IMAGE.BILL-CYCLE
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #BILL-CYCLE-CHANGE
      COMPRESS 'CAP Loan' 'Bill Cycle ' BEFORE_IMAGE.BILL-CYCLE TO #OLD-DATA(#I)
      COMPRESS 'CAP Loan' 'Bill Cycle ' AFTER_IMAGE.BILL-CYCLE TO #NEW-DATA(#I)
    END-IF
  WHEN NONE
    IF BEFORE_IMAGE.MODE <> AFTER_IMAGE.MODE
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #MODE-CHANGE
      #WORK := FUNCTION-PAYMENT-MODE(<BEFORE_IMAGE.MODE>)(1)
      COMPRESS 'Policy' BEFORE_IMAGE.POLICY-NUMBER #WORK TO #OLD-DATA(#I)
      #WORK := FUNCTION-PAYMENT-MODE(<AFTER_IMAGE.MODE>)(1)
      COMPRESS 'Policy' AFTER_IMAGE.POLICY-NUMBER #WORK TO #NEW-DATA(#I)
    END-IF
    IF BEFORE_IMAGE.BILL-CYCLE <> AFTER_IMAGE.BILL-CYCLE
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := #BILL-CYCLE-CHANGE
      COMPRESS 'Policy' BEFORE_IMAGE.POLICY-NUMBER 'Bill Cycle ' BEFORE_IMAGE.BILL-CYCLE TO #OLD-DATA(#I)
      COMPRESS 'Policy' AFTER_IMAGE.POLICY-NUMBER 'Bill Cycle ' AFTER_IMAGE.BILL-CYCLE TO #NEW-DATA(#I)
    END-IF
END-DECIDE
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE LOG-DISCLAIMER-TRAN     /* YK1
*************************************
*
IF AFTER_IMAGE.CC-DISCLAIMER = 'true'
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DISCLAIMER
  #NEW-DATA(#I) := 'Credit Card disclaimer confirmed'
END-IF
IF AFTER_IMAGE.BA-DISCLAIMER = 'true'
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DISCLAIMER
  #NEW-DATA(#I) := 'Bank Account disclaimer confirmed'
END-IF
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE LOG-PAYER-CHANGE         /* YK2
**********************************
*
IF AFTER_IMAGE.PAYER <> BEFORE_IMAGE.PAYER
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #PAYOR-CHANGE
  COMPRESS 'Policy' BEFORE_IMAGE.POLICY-NUMBER BEFORE_IMAGE.PAYER INTO #OLD-DATA(#I)
  COMPRESS 'Policy' BEFORE_IMAGE.POLICY-NUMBER AFTER_IMAGE.PAYER INTO #NEW-DATA(#I)
END-IF
END-SUBROUTINE
*
END
