* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram GOSTCN03.
/**
/** :author nguentchev
* Synch Status and FIR ADABAS files when change request from AUC
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID    (N8)
1 DateOfDeath    (N8)
LOCAL
1 #ID-NUMBER (N6)
1 ST-V VIEW OF A-STATUS
  2 STATUS-CODE-SP (A1)           (HD='STATUS-CD')
  2 DATE-OF-DIVORCE (N8.0)        (HD='DIVORCE-DT')
  2 FORMER-SPOUSE-STATUS (A1)     (HD='FS-STATUS')
  2 FORMER-SPOUSE-DATE-OF-DIVORCE (N8) (HD='FS-DIVORCE-DT')
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-ID
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID (N8)
  2 ROLE-1-CD (A1)
  2 CONTACT-2-ID (N8)
  2 ROLE-2-CD (A1)
1 FIR-DPN VIEW OF A-FIRS
  2 C*DEPENDENT-TABLE
  2  DEPN-CONTACT-ID                   (N8.0/30)
  2  DEPN-STATUS-CD                    (A1/30)
1 J  (I2)
*
END-DEFINE
*
#ID-NUMBER := F-GET-PARENT-CN (< #CONTACT-ID >)
IF #ID-NUMBER > 0
  FIND (1) CNT WITH ID-NUMBER = #ID-NUMBER
  END-FIND
  FIND CR-V WITH CONTACT-1-ID = CNT.CONTACT-ID AND CONTACT-2-ID = #CONTACT-ID
  END-FIND
  FIND (1) ST-V WITH ID-NUMBER = #ID-NUMBER
  END-FIND
  FIND (1) FIR-DPN WITH ID-NUMBER = #ID-NUMBER
  END-FIND
*
  IF DateOfDeath > 0
    DECIDE ON FIRST VALUE OF ROLE-2-CD
      VALUE 'S'
        GET ST-V *ISN (0047)
        STATUS-CODE-SP := 'D'
        DATE-OF-DIVORCE := DateOfDeath
        UPDATE (0055)
      VALUE 'F'
        GET ST-V *ISN (0047)
        FORMER-SPOUSE-STATUS := 'D'
        FORMER-SPOUSE-DATE-OF-DIVORCE := DateOfDeath
        UPDATE (0060)
      VALUE 'K', 'G'
        GET FIR-DPN *ISN (0049)
        FOR J 1 TO 30
          IF DEPN-CONTACT-ID (J) = #CONTACT-ID
            DEPN-STATUS-CD (J) := 'D'
            ESCAPE BOTTOM
          END-IF
        END-FOR
        UPDATE (0065)
      NONE VALUE IGNORE
    END-DECIDE
  ELSE
    DECIDE ON FIRST VALUE OF ROLE-2-CD
      VALUE 'S'
        GET ST-V *ISN (0047)
        STATUS-CODE-SP := 'S'
        RESET DATE-OF-DIVORCE
        UPDATE (0078)
      VALUE 'F'
        GET ST-V *ISN (0047)
        FORMER-SPOUSE-STATUS := 'F'
        RESET FORMER-SPOUSE-DATE-OF-DIVORCE
        UPDATE (0083)
      VALUE 'K'
        GET FIR-DPN *ISN (0049)
        FOR J 1 TO 30
          IF DEPN-CONTACT-ID (J) = #CONTACT-ID
            RESET DEPN-STATUS-CD (J)
            ESCAPE BOTTOM
          END-IF
        END-FOR
        UPDATE (0088)
      VALUE 'G'
        GET FIR-DPN *ISN (0049)
        FOR J 1 TO 30
          IF DEPN-CONTACT-ID (J) = #CONTACT-ID
            DEPN-STATUS-CD (J) := 'G'
            ESCAPE BOTTOM
          END-IF
        END-FOR
        UPDATE (0097)
      NONE VALUE IGNORE
    END-DECIDE
  END-IF
END-IF
END
