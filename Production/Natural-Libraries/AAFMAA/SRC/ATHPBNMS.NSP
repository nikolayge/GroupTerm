* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPBNMS   BENE name mismatch with mbr spouse
*  THIS PROGRAM MODIFIES THE NT NOTE FILE FILE - USE CAREFULLY
DEFINE DATA
*
LOCAL
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER          (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-1ST    (N1)
  3 ID-NUMBER-LAST-5 (N5)
 2 NOTE-DATE          (N8/60) /* LET P/A = PRIMARY/ALTERNATE DESING
 2 REDEFINE NOTE-DATE         /* let L = linked policies
  3 BENE-DATES-6-POLS (6)
   4 BENE-ISS-DT        (N6/7)   /* (D, L)
   4 BENE-SPLIT-IND     (N1/7)   /* (D, L)
   4 BENE-LST-UPD       (N8)     /* (D)
   4 BENE-STRT-PEND     (N8)     /* (D)
   4 BENE-FOL-UP-IND    (N1)     /* (D)
   4 BENE-OWN-TRNSF-DT  (N8)     /* (D)
   4 BENE-SUC-OWN       (N1)     /* (D)
   4 BENE-NEW-LAYOUT    (N1)     /* (D)
   4 BENE-FIL1          (A4)
 2 NOTE-CLERK-ID      (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-6-POLS  (6)
   4 BENE-STATUS      (A1)     /* (D)
   4 BENE-RESTRICT    (A1)     /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)    /* (D, P/A)
    5 BENE-SHARE-TYP   (A1)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT          (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-6-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC    (5)    /* (D, P/A, TEXT-LINE)
     6 BENE-TEXT        (A60)
     6 REDEFINE BENE-TEXT
      7 BENE-TEXT-1ST-25 (A25)
      7 BENE-TEXT-LST-35 (A35)
     6 REDEFINE BENE-TEXT
      7 BENE-NAME-5      (A5)
      7 BENE-TEXT-LST-55 (A55)
*
1 ST-V VIEW OF A-STATUS
 2 WIDOW-FLAG
 2 NAME
 2 NAME-SP
*
1 #ST-ID             (N6)
1 REDEFINE #ST-ID
 2 #ST-ID-A          (A6)
1 #I1                (I2)
1 #I2                (I2)
1 #I3                (I2)
1 #CNT-2             (P5)
1 #CNT-3             (P5)
1 #LAST-NAME         (A25)
1 #FIRST-NAME        (A25)
1 REDEFINE #FIRST-NAME
  2 #FIRST-NAME-5    (A5)
1 #EXTRA-1           (A25)
1 #EXTRA-2           (A25)
1 #EXTRA-3           (A25)
1 #NUM-BNS           (P5)
1 #NUM-POLS          (P5)
1 #NUM-TYPE-BYPASSED (P5)
1 #NUM-DATE-BYPASSED (P5)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'BENEFICIARY NAME MISMATCHES'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                 'BENEFICIARY NAME MATCHES'
                 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ NT-V BY ID-NUMBER FROM 400000
  IF ID-NUMBER GT 599999
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #NUM-BNS
  FOR #I1 1 TO 6
    IF  (BENE-ISS-DT (#I1, 1) = 0)
      ESCAPE TOP
    END-IF
    ADD 1 TO #NUM-POLS
*
*   DISPLAY ID-NUMBER BENE-LST-UPD (#I1)
    IF (BENE-LST-UPD (#I1) GE 19770101)
      ADD 1 TO #NUM-DATE-BYPASSED
      ESCAPE TOP
    END-IF
*
    IF (BENE-TYPE (#I1, 1) NE 'B')
      ADD 1 TO #NUM-TYPE-BYPASSED
      ESCAPE TOP
    END-IF
*
    COMPUTE #ST-ID = ID-NUMBER - 400000
*
    FIND (1) ST-V WITH MEMBER-KEY = #ST-ID-A
      IF ST-V.WIDOW-FLAG = 'Y'
        ESCAPE TOP
      END-IF
*
      SEPARATE NAME-SP LEFT INTO #LAST-NAME #FIRST-NAME #EXTRA-1
                 #EXTRA-2 #EXTRA-3
*
      IF #FIRST-NAME-5 NE BENE-NAME-5 (#I1, 1, 1)
        ADD 1 TO #CNT-2
        DISPLAY (2) #CNT-2 'CN' #ST-ID
              'MBR-NAME' ST-V.NAME   (EM=XXXXXXXXXXXXXX)
              'LST-UPD' BENE-LST-UPD (#I1)
              'SPS-NAME' NAME-SP    (EM=XXXXXXXXXXXXXXXXXXXX)
              'BENE-NAME' BENE-TEXT (#I1, 1, 1) (EM=XXXXXXXXXXXXXXXXX)
      ELSE
        ADD 1 TO #CNT-3
        DISPLAY (3) #CNT-3 'CN' #ST-ID
              'MBR-NAME' ST-V.NAME   (EM=XXXXXXXXXXXXXX)
              'LST-UPD' BENE-LST-UPD (#I1)
              'SPS-NAME' NAME-SP    (EM=XXXXXXXXXXXXXXXXXXXX)
              'BENE-NAME' BENE-TEXT (#I1, 1, 1) (EM=XXXXXXXXXXXXXXXXX)
      END-IF
    END-FIND
  END-FOR
END-READ
WRITE 'NUM BNS READ = ' #NUM-BNS
WRITE 'NUM BN POLS  = ' #NUM-POLS
WRITE 'NUM POLS BYPASSED DUE TO UPDATE DT' #NUM-DATE-BYPASSED
WRITE 'NUM POLS BYPASSED DUE TO TYPE     ' #NUM-TYPE-BYPASSED
*
END
