* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
*****************************************************************
*                                                               *
* New COPYCODE member: ATHISACV created by ACCORD on: 02/09/05  *
*                                                               *
*****************************************************************
*
*********************************************************
*       IF THIS MACRO IS CHANGED PROBABLY ATHISCNV
*       SHOULD ALSO BE CHANGED
**********************************************************
*
DEFINE SUBROUTINE CALC-CURR-ACV
*
MOVE IS-ISS-AGE TO TG-ISS-X
SUBTRACT 14 FROM TG-ISS-X
MOVE TG-ISS-X TO TG-ATT-X
ADD IS-DURATION TO TG-ATT-X
*
IF TG-ATT-X > 86
  MOVE 86 TO TG-ATT-X
END-IF
*
IF IS-TYPE = 'OL'
  MOVE 82 TO TG-PUP-X
ELSE
  MOVE TG-ISS-X TO TG-PUP-X
  IF IS-TYPE = '30'
    ADD 30 TO TG-PUP-X
  ELSE
    ADD 20 TO TG-PUP-X
  END-IF
END-IF
*
MOVE TG-ISS-X TO TB-ISS-X
MOVE TG-ATT-X TO TB-ATT-X
MOVE TG-PUP-X TO TB-PUP-X
MOVE TG-ISS-X TO TC-ISS-X
MOVE TG-ATT-X TO TC-ATT-X
MOVE TG-PUP-X TO TC-PUP-X
*
IF IS-PAID-UP-SW = 'Y'
  RESET IS-PREM-DEFIC
*
ELSE
*
  COMPUTE ROUNDED IS-NR-AX = (TG-MX(TG-ISS-X) * IS-CALC-1A) /
    (TG-NX(TG-ISS-X) - TG-NX(TG-PUP-X) - (IS-CALC-2 *
    (TG-DX(TG-ISS-X) - TG-DX(TG-PUP-X))))
  COMPUTE ROUNDED IS-NR-BX = TG-NX(TG-ATT-X) - TG-NX(TG-PUP-X) -
    (IS-CALC-2 * (TG-DX(TG-ATT-X) - TG-DX(TG-PUP-X)))
  COMPUTE ROUNDED IS-NR-A = (IS-WORK-DB * ((TG-MX(TG-ATT-X) * IS-CALC-1A) -
    (IS-NR-AX * IS-NR-BX))) / TG-DX(TG-ATT-X)
  COMPUTE ROUNDED IS-NR-B = (IS-CALC-9-EXP-FACT * (TB-NX(TB-ATT-X) -
    (0.6 * TB-NX(TB-PUP-X)) - (IS-CALC-2 *
    (TB-DX(TB-ATT-X) - (0.6 * TB-DX (TB-PUP-X)))))) / TB-DX(TB-ATT-X)
  COMPUTE ROUNDED IS-NR-EX = (TG-NX(TG-ISS-X) - TG-NX(TG-PUP-X) -
    (IS-CALC-2 * (TG-DX(TG-ISS-X) - TG-DX(TG-PUP-X)))) / TG-DX(TG-ISS-X)
  COMPUTE ROUNDED IS-NR-FX = ((IS-CALC-9-EXP-FACT / 10) *
    (IS-CALC-5 ** IS-DURATION) * IS-NR-BX) / TG-DX(TG-ATT-X)
  COMPUTE ROUNDED IS-NR-C = ((10 * (TB-NX(TB-ISS-X) - (0.6 * TB-NX(TB-PUP-X)) -
    (IS-CALC-2 * (TB-DX(TB-ISS-X) - (0.6 * TB-DX(TB-PUP-X)))))
     * IS-NR-FX) / TB-DX(TB-ISS-X))/IS-NR-EX
  COMPUTE ROUNDED IS-NET-RESV = IS-NR-A + IS-NR-B - IS-NR-C
  COMPUTE ROUNDED IS-GR-GX = IS-ANN-PREM / IS-WORK-DB
  COMPUTE ROUNDED IS-GR-A = (IS-WORK-DB * ((TG-MX(TG-ATT-X) * IS-CALC-1A) -
    (IS-GR-GX * IS-NR-BX))) / TG-DX(TG-ATT-X)
  COMPUTE ROUNDED IS-GROS-RESV = IS-GR-A + IS-NR-B
*
  MOVE TG-ATT-X TO TG-ATT-10-X
  ADD 10 TO TG-ATT-10-X
  IF TG-ATT-10-X > 86
    MOVE 86 TO TG-ATT-10-X
  END-IF
*
  COMPUTE ROUNDED IS-PREM-DEFIC = (IS-GROS-RESV - IS-NET-RESV) /(((TG-NX(TG-ATT-X) -
    TG-NX(TG-ATT-10-X)) * 12) /(TG-DX(TG-ATT-X) - TG-DX(TG-ATT-10-X)))
  IF IS-PREM-DEFIC < 0
    RESET IS-PREM-DEFIC
  END-IF
  MOVE IS-PREM-DEFIC TO IS-ORIG-PREM-DEFIC
*
END-IF
*
IF IS-PAID-UP-SW = 'Y'
  COMPUTE ROUNDED IS-BACV-A = (IS-WORK-DB * TG-MX(TG-ATT-X) * IS-CALC-1A) /
    TG-DX(TG-ATT-X)
  COMPUTE ROUNDED IS-BACV-B = (IS-CALC-9-EXP-FACT * 0.4 * (TB-NX(TB-ATT-X) -
    IS-CALC-2 * TB-DX(TB-ATT-X))) / TB-DX(TB-ATT-X)
  COMPUTE ROUNDED IS-BEG-ACV = IS-BACV-A + IS-BACV-B
ELSE
  MOVE IS-GROS-RESV TO IS-BEG-ACV
END-IF
*
END-SUBROUTINE
*
*********************************************************************************
*
