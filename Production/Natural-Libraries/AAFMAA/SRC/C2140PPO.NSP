* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: C2140PPO - Initial Menu For c2150ppo (Possible Options for Ins)
*
***********************************************************************
*                       MODIFICATION LOG                              *
* USER    DATE      TAG     REASON                                    *
*                                                                     *
* PAM   06/02/2004  PM1    Adjusted pgm to use maximum amount sub-    *
*                          program as per Rick Browett.               *
* PAM   02/09/2005  PM2    Increased #LUMP-PMT to 6.2.                *
* DRW   03/05/2005  DW3    Add AL (Annuity Life) Processing           *
* DRW   04/29/2005  DW4    Add DOB processing to GMAXSXX              *
* PAM   12/24/2009  PM5    Added new generational insurance requirements
* PAM   04/06/2010  PM6    Added CONTACT file                         *
* YAK   09/26/2012  YK1    Use function to identify Level Term II plans
* YAK   11/15/2012  YK2    Add No Medical Term plan (EZ)              *
* NGG    20130419   NG1    Stability Project for $1M Policy           *
* TMT   06/11/2014  TT1    Contact Normalization - DOB and Sex        *
* TMT   10/13/2014  TT2    AL Stability Project
***********************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING CONTPDA1
LOCAL
1 #SCRR-SEL    (A1/20)
1 #SCRR-SEL-CV (C/20)
1 #SCRR-ID     (N6/20)
1 #SCRR-SUFFIX (A3/20)
1 #SCRR-TYPE   (A1/20)
1 #SCRR-NAME   (A25/20)
1 #SCRR-DOB-N  (N8/20)
1 #SCRR-DOB    (A10/20)
1 #SCRR-SSN-N  (N9/20)
1 #SCRR-SSN    (A11/20)
1 #SCRR-FACE-TM-N (N6/20)
1 #SCRR-FACE-TM   (A7/20)
1 #SCRR-AVAIL-TM-N(N6/20)
1 #SCRR-AVAIL-TM  (A11/20)
1 #SCRR-FACE-WL-N (N7/20)       /*TT1
1 #SCRR-FACE-WL   (A7/20)
1 #SCRR-AVAIL-WL-N(N7/20)       /*TT1
1 #SCRR-AVAIL-WL  (A12/20)
1 #SCRR-ACT-FLG (A1/20)
1 #SCRR-CONTACT-ID (N8/20)    /* PM6
1 #ADD-GEN-SEL (A1)
1 #ADD-GEN-SEL-CV (C)
*
1 #LINKED-CN (N6/30)
1 REDEFINE #LINKED-CN
 2 #LINKED-CN-A (A6/30)
1 #LINKED-MBR-NAME (A35)
1 #LINKED-MBR-CN   (A9)
1 #CN    (N6)
1 REDEFINE #CN
 2 #CN-A  (A6)
1 #I1   (I2)
1 #I2   (I2)
1 #I3   (I2)
1 #I4   (I2)
1 #LOADED      (L)            /* PM6
1 #STATUS-CODE (A1)           /* PM6
1 #D-I         (N2)           /* PM6
1 #REL-I       (I2)           /* PM6
1 #PARENT-CID       (N8)      /* PM6
1 #REL-CID          (N8)      /* PM6
1 #MODE             (A1)      /* PM6
1 #REL-MSG          (A72)     /* PM6
*
1 #N2   (N2)
1 REDEFINE #N2
 2 #A2   (A2)
1 #SVC-FEE-CV (C)
1 #TEXT      (A78)
1 #DID-DEPN   (L)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 LINKED-CN   (30)
 2 MEMBER-CONTACT-ID          /* PM6
 2 RESTRICT-MBR-SW
 2 PROCESS-IND
 2 SUSPEND-CODE
 2 INTRO-FLAG
 2 SSN
*  2 SEX                      /* TT1   
 2 NAME
 2 NAME-SP
 2 DATE-OF-DEATH
*  2 DATE-OF-BIRTH            /* TT1
*  2 DATE-OF-BIRTH-SP         /* TT1
 2 STATUS-CODE-SP
 2 SSN-SP
 2 STATUS      (9)
 2 FACE-AMOUNT (9)
 2 DEATH-BENEFIT (9)
*
1 LK-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 MEMBER-CONTACT-ID          /* PM6
 2 RESTRICT-MBR-SW
 2 SUSPEND-CODE
 2 INTRO-FLAG
 2 PROCESS-IND
 2 SSN
 2 NAME
 2 DATE-OF-DEATH
*  2 DATE-OF-BIRTH           /* TT1
 2 STATUS      (9)
 2 PLAN        (9)
 2 REDEFINE PLAN
  3 PLAN-OCC  (9)
   4 PLAN-A1   (A1)
   4 PLAN-A2   (A2)
 2 FACE-AMOUNT (9)
 2 PAID-UP-DATE (9)
 2 ISSUE-DATE   (9)
 2 ISSUE-AGE    (9)
 2 SMOKER-FLAG (9)
 2 DEATH-BENEFIT (9)
 2 CASH-VALUE  (9)
 2 BEG-CASH-VALUE (9)
*
1 FR-V VIEW OF A-FIRS
 2 DEPN-CONTACT-ID  (60)        /* PM6
 2 DEPN-NAME     (14)
 2 DEPN-DATE-OF-BIRTH (14)
 2 DEPN-SSN      (14)
 2 DEPN-STATUS-CD   (60)        /* PM6
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER
 2 SSN
 2 NAME
 2 INTRO-FLAG
 2 MEMBER-CN
 2 REDEFINE MEMBER-CN
  3 MEMBER-CN-N   (N6)
 2 ACTION-FLAG
 2 REFERING-ID-NUMBER   (2)
 2 REDEFINE REFERING-ID-NUMBER
  3 REF-ID-A  (A6/2)
 2 DATE-OF-BIRTH
 2 MIL-STATUS
 2 RANK
*
1 IM-V VIEW OF A-INTRO-MEMBER
 2 ID-NUMBER
 2 SSN
 2 NAME
 2 DATE-OF-BIRTH
 2 RANK
*
1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS       /* PM6
 2 CONTACT-1-ID                              /* PM6
 2 ROLE-1-CD                                 /* PM6
 2 CONTACT-2-ID                              /* PM6
 2 ROLE-2-CD                                 /* PM6
*
1 CT-V VIEW OF A-CONTACTS                    /* PM6
 2 ID-NUMBER                                 /* PM6
 2 CONTACT-ID                                /* PM6
 2 FIRST-NAME                                /* PM6
 2 MIDDLE-NAME                               /* PM6
 2 LAST-NAME                                 /* PM6
 2 SUFFIX                                    /* PM6
 2 GENDER-CD                                 /* PM6
 2 DATE-OF-BIRTH                             /* PM6
 2 SSN                                       /* PM6
 2 DELETE-FLAG                               /* PM6
*
1 #DIVIDEND   (N5.4)
1 REDEFINE #DIVIDEND
 2 #DIV-5    (N5)
 2 #REM      (N4)
*
1 #FND-REC       (L)
1 #FOUND-ST      (L)
1 #IS-A-CN       (L)
1 #IS-AN-AT      (L)
1 #IS-AN-IM      (L)
1 #MATCH         (L)
1 #MSG         (A75)
1 #TOT-FACE-WL (N7.2)
1 #TOT-FACE-TM (N7.2)
1 #TOT-FACE    (N7.2)
1 #FORMATED-NAME (A35)
1 #MIL-STATUS    (A1)
1 #FORMAT-CN     (A9)
1 #FIRST/MID (A25)
1 #SUFFIX    (A25)
1 #EXTRA1    (A25)
1 #EXTRA2    (A25)
1 #SMOK-FLAG (A1)
* 1 #LUMP-PMT    (N5.2)                       /* PM2
1 #LUMP-PMT    (N6.2)                         /* PM2
1 #1035-EXCH-AMT (N6.2)
1 #FAC-AMT     (N7.2)                         /* NG1
1 #NUM-YEARS   (N3)
1 #ANN-PREM    (N6.2)
1 #DUES-PMT    (N3.2)
1 #PREM-AGE    (N2)
1 #PAYS-DUES   (L)
1 #FLEX-POLICY (L)
1 #PREM-RATING (A1)
1 #PAY-STAT    (A4)
1 #EFF-DATE    (N8)
1 #PLAN        (A3)
1 REDEFINE #PLAN
 2 #PLAN-A1    (A1)
 2 #PLAN-N2    (N2)
*
1 #WYYYYMMDD     (A8)
1 REDEFINE #WYYYYMMDD
 2 #WCENT    (N2)
 2 #WYY      (N2)
 2 #WMM      (N2)
 2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
 2 #WYYYYMMDD-N (N8)
1 REDEFINE #WYYYYMMDD
 2 #WYYYY-N   (N4)
 2 #WMMDD-N   (N4)
1 REDEFINE #WYYYYMMDD
 2 #WCENT-A   (A2)
 2 #WYY-A     (A2)
 2 #WMM-A     (A2)
 2 #WDD-A     (A2)
1 REDEFINE #WYYYYMMDD
 2 #WYYYY-A   (A4)
 2 #WMMDD-A   (A4)
*
1 #MONTHS (A36)  INIT
   <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
 2 #MON       (A3/12)
*
* #MAX-FACE-WL    (P6.2) INIT <800000>        /* PM1
* #MAX-FACE-TM    (P6.2) INIT <600000>        /* PM1
* #MAX-FACE-WL-DISP (A7) INIT <'800,000'>     /* PM1
* #MAX-FACE-TM-DISP (A7) INIT <'600,000'>     /* PM1
1 #MAX-FACE-WL    (P7.2)                      /* PM1
1 #MAX-FACE-TM    (P7.2)                      /* PM1
1 #MAX-FACE-WL-DISP (A9)                      /* PM1    /*TT1
1 #MAX-FACE-TM-DISP (A9)                      /* PM1    /*TT1
1 #OVERRIDE-MAX-FACE-WL    (P7.2)             /* PM1
1 #OVERRIDE-MAX-FACE-TM    (P7.2)             /* PM1
1 #INTRO-FLAG (A1)                            /* PM1
1 #DOB        (N8)                            /* DW4
1 #DATE-OF-BIRTH       (N8.0)
1 #L-DATE-OF-BIRTH     (N8.0)
*
END-DEFINE
***********************************************************************
*
INCLUDE GONERCXX
*
IF #CGA-ORIG-SUFFIX = 'FLX'
  MOVE TRUE TO #FND-REC
  FIND (1) ST-V WITH MEMBER-KEY = #CGA-ID-G
    IF NO RECORD FOUND
      RESET #FND-REC
      FIND ST-V WITH SSN = #CGA-SCR-ID
        IF ST-V.PROCESS-IND = 'S' OR = 'C'
          ESCAPE TOP
        END-IF
*
        MOVE TRUE TO #FND-REC
        ESCAPE BOTTOM
      END-FIND
    END-NOREC
  END-FIND
*
  IF NOT #FND-REC
    MOVE TRUE TO #PAYS-DUES
  END-IF
*
  RESET #CGA-SCR-COMMAND #CGA-SCR-SUFFIX
*  
  RESET PDA-CONTACT                               /* TT1
  P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID          /* TT1
  CALLNAT 'CONTS001' PDA-CONTACT                  /* TT1
*  
  CALLNAT 'C1602SCO'
    #CGA-SCR-COMMAND #CGA-SCR-ID #CGA-SCR-SUFFIX #CGA-ORIG-COMMAND #CGA-CLERK
    #PAYS-DUES 
    ST-V.NAME 
    P-DATE-OF-BIRTH          /* TT1
    #SMOK-FLAG 
    P-GENDER                 /* TT1
    #LUMP-PMT
    #1035-EXCH-AMT 
    #FAC-AMT            /* CHANGED TO P7.2
    #NUM-YEARS 
    #ANN-PREM 
    #DUES-PMT 
    #PREM-AGE
    #FLEX-POLICY 
    #PREM-RATING  FETCH 'G1000PXX'
END-IF
*
IF #CGA-SCR-ID = 0
  FETCH 'C2150PPO'
END-IF
*
RESET #CGA-ISN #FND-REC
*
IF #CGA-SCR-ID LT 999999
  MOVE #CGA-SCR-ID TO #CGA-ID
END-IF
*
IF #CGA-SCR-ID LT 500000   /* member
  FIND (1) ST-V WITH MEMBER-KEY = #CGA-ID-G
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = #CGA-ID-G
        IF NO RECORDS FOUND
          MOVE 'Member not found' TO #TEXT
          PERFORM ERROR-MSG
        END-NOREC
        MOVE TRUE TO #IS-A-CN
      END-FIND
    END-NOREC
    MOVE TRUE TO #IS-A-CN
  END-FIND
ELSE
  IF #CGA-SCR-ID LT 700000   /* AT record (5xxxxx)
    FIND (1) AT-V WITH ID-NUMBER = #CGA-ID
      IF NO RECORDS FOUND
        MOVE 'AT Record not found' TO #TEXT
        PERFORM ERROR-MSG
      END-NOREC
      MOVE TRUE TO #IS-AN-AT
    END-FIND
  ELSE
    IF #CGA-SCR-ID LT 800000   /* IM record (7XXXXX)
      FIND (1) IM-V WITH ID-NUMBER = #CGA-ID
        IF NO RECORDS FOUND
          MOVE 'IM Record not found' TO #TEXT
          PERFORM ERROR-MSG
        END-NOREC
        MOVE TRUE TO #IS-AN-IM
      END-FIND
    ELSE
      FIND ST-V WITH SSN = #CGA-SCR-ID
        IF ST-V.PROCESS-IND = 'S' OR = 'C'
          ESCAPE TOP
        ELSE
          MOVE ST-V.ID-NUMBER TO #CGA-ID /* get set for find
          MOVE ST-V.ID-NUMBER TO #CGA-SCR-ID
          MOVE TRUE TO #IS-A-CN #FND-REC
          ESCAPE BOTTOM
        END-IF
      END-FIND
*
      IF NOT #FND-REC
        FIND AT-V WITH SSN = #CGA-SCR-ID
          IF AT-V.INTRO-FLAG = 'V'
            ESCAPE TOP
          ELSE
            MOVE TRUE TO #IS-AN-AT #FND-REC
            IF AT-V.ID-NUMBER NE 0
              MOVE AT-V.ID-NUMBER TO #CGA-ID #CGA-SCR-ID
            END-IF
            ESCAPE BOTTOM
          END-IF
        END-FIND
      END-IF
*
      IF NOT #FND-REC
        FIND (1) IM-V WITH SSN = #CGA-SCR-ID
          IF NO RECORDS FOUND
            MOVE 'Record not found' TO #TEXT
            PERFORM ERROR-MSG
          END-NOREC
          MOVE TRUE TO #IS-AN-IM
          MOVE IM-V.ID-NUMBER TO #CGA-ID #CGA-SCR-ID
          ESCAPE BOTTOM
        END-FIND
      END-IF
    END-IF
  END-IF
END-IF
*
DECIDE FOR FIRST CONDITION                  /* PM1
  WHEN #IS-A-CN                             /* PM1
    MOVE ST-V.INTRO-FLAG TO #INTRO-FLAG     /* PM1                                    /* TT1
    MOVE #DATE-OF-BIRTH TO #DOB             /* DW4
  WHEN #IS-AN-AT                            /* PM1
    MOVE AT-V.INTRO-FLAG TO #INTRO-FLAG     /* PM1
    MOVE AT-V.DATE-OF-BIRTH TO #DOB         /* DW4
  WHEN NONE                                 /* PM1
    RESET #INTRO-FLAG #DOB                  /* PM4
END-DECIDE                                  /* PM1
*
PERFORM CALL-GMAXSXX                        /* PM1
*
IF #IS-AN-IM
  ADD 1 TO #I2
  MOVE 'I' TO #SCRR-TYPE (#I2)
*
  IF IM-V.DATE-OF-BIRTH LE (*DATN - 810000)
    MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
    MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
  ELSE
    MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
    MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
  END-IF
*
  MOVE IM-V.NAME          TO #SCRR-NAME(#I2)
  MOVE IM-V.DATE-OF-BIRTH TO #SCRR-DOB-N (#I2)
  MOVE IM-V.SSN           TO #SCRR-SSN-N (#I2)
  MOVE IM-V.ID-NUMBER     TO #SCRR-ID (#I2)
  RESET                      #SCRR-FACE-TM-N (#I2)
  RESET                      #SCRR-FACE-WL-N (#I2)
END-IF
*
IF #IS-A-CN
 AND (ST-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G')
  MOVE LINKED-CN (1) TO #CGA-SCR-ID
  FETCH 'G1000PXX'
END-IF
*
IF #IS-AN-AT
* below logic removed 11/12/2002 KEC
* IF AT-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
*   MOVE AT-V.REFERING-ID-NUMBER (1) TO #CGA-SCR-ID
*   FETCH 'G1000PXX'
* ELSE
*   IF AT-V.MEMBER-CN NE ' '
*     MOVE AT-V.MEMBER-CN-N TO #CGA-SCR-ID
*     FETCH 'G1000PXX'
*   ELSE
      ADD 1 TO #I2
      MOVE 'A' TO #SCRR-TYPE (#I2)
*
      IF AT-V.DATE-OF-BIRTH LE (*DATN - 810000)
        MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
        MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
      ELSE
        MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
        MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
      END-IF
*
      MOVE AT-V.NAME          TO #SCRR-NAME(#I2)
      MOVE AT-V.DATE-OF-BIRTH TO #SCRR-DOB-N (#I2)
      MOVE AT-V.SSN           TO #SCRR-SSN-N (#I2)
      MOVE AT-V.ID-NUMBER     TO #SCRR-ID (#I2)
      RESET                      #SCRR-FACE-TM-N (#I2)
      RESET                      #SCRR-FACE-WL-N (#I2)
*   END-IF
* END-IF
END-IF
*
MOVE (AD=P) TO #SCRR-SEL-CV (*)
*
IF #IS-A-CN
  RESET #I2
* * * * * * * * * * * * * MEMBER
*
  MOVE TRUE TO #FOUND-ST
  FIND (1) ST-V WITH ST-V.ID-NUMBER = #CGA-ID       /* general find  PM6
    IF NO RECORDS FOUND
      RESET #FOUND-ST
    END-NOREC
*
    FIND (1) LK-V WITH LK-V.ID-NUMBER = #CGA-ID     /* for load      PM6
      IF NO RECORDS FOUND
        RESET #FOUND-ST
      END-NOREC
    END-FIND
  END-FIND
*
  IF #FOUND-ST
    MOVE ST-V.LINKED-CN (*) TO #LINKED-CN (*)
    PERFORM LOAD-CN-INFO
  END-IF
*
* * * * * * * * * * * * * LINKED INSURED
*
  FOR #I1 1 TO 30
    IF #LINKED-CN (#I1) NE 0
      IF #LINKED-CN (#I1) LT 500000
        FIND (1) LK-V WITH MEMBER-KEY = #LINKED-CN-A (#I1)
          RESET #MATCH
          FOR #I3 1 TO 20
            IF (SSN = #SCRR-SSN-N (#I3)) AND (#SCRR-SSN-N (#I3) NE 0)
              MOVE TRUE TO #MATCH
              ESCAPE BOTTOM
            END-IF
          END-FOR
*
          IF #MATCH
           OR (LK-V.INTRO-FLAG = 'F') /* no connection
           OR  ((NOT ST-V.INTRO-FLAG = 'S' OR = 'G' OR = 'K')
            AND (NOT LK-V.INTRO-FLAG = 'S' OR = 'G' OR = 'K'))
            IGNORE
          ELSE
            PERFORM LOAD-CN-INFO
          END-IF
        END-FIND
      ELSE
        FIND (1) AT-V WITH ID-NUMBER = #LINKED-CN (#I1)
          MOVE AT-V.INTRO-FLAG TO #INTRO-FLAG                     /* PM1
          MOVE AT-V.DATE-OF-BIRTH TO #DOB     /* DW4
          PERFORM CALL-GMAXSXX                                    /* PM1
*
          ADD 1 TO #I2
*
          IF #I2 GT 20
            MOVE 20 TO #I2
          END-IF
*
          MOVE ACTION-FLAG TO #SCRR-ACT-FLG (#I2)
          IF AT-V.INTRO-FLAG = 'S'
            MOVE 'S' TO #SCRR-TYPE (#I2)
*
            IF AT-V.DATE-OF-BIRTH LE (*DATN - 810000)
              MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
              MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
            ELSE
              MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
              MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
            END-IF
          ELSE
            IF AT-V.DATE-OF-BIRTH LE (*DATN - 240000)
             AND FUNCTION-GEN23-INSURE(<AT-V.MEMBER-CN>) = FALSE  /* PM5
              MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
              MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
            ELSE
              MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
              MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
            END-IF
*
            IF AT-V.INTRO-FLAG = 'G'
              MOVE 'G' TO #SCRR-TYPE (#I2)
            ELSE
              MOVE 'K' TO #SCRR-TYPE (#I2)
            END-IF
          END-IF
*
          MOVE AT-V.NAME          TO #SCRR-NAME(#I2)
          MOVE AT-V.DATE-OF-BIRTH TO #SCRR-DOB-N (#I2)
          MOVE AT-V.SSN           TO #SCRR-SSN-N (#I2)
          MOVE AT-V.ID-NUMBER     TO #SCRR-ID (#I2)
          RESET                      #SCRR-FACE-TM-N (/#I2)
          RESET                      #SCRR-FACE-WL-N (/#I2)
        END-FIND
      END-IF
    END-IF
  END-FOR
*
* * * * * * * * * * * * * Spouse and Gen
  PERFORM LOAD-SPOUSE-GEN                                         /* PM6
*
* * * * * * * * * * * * * at's for existing members
*
  FIND AT-V WITH SSN = ST-V.SSN
    IF AT-V.INTRO-FLAG = 'V'
      ESCAPE TOP
    END-IF
*
    MOVE AT-V.INTRO-FLAG TO #INTRO-FLAG                           /* PM1
    MOVE AT-V.DATE-OF-BIRTH TO #DOB       /* DW4
    PERFORM CALL-GMAXSXX                                          /* PM1
*
    ADD 1 TO #I2
*
    IF #I2 GT 20
      MOVE 20 TO #I2
    END-IF
*
    RESET #SCRR-SEL-CV (#I2)
    MOVE ACTION-FLAG TO #SCRR-ACT-FLG (#I2)
    IF NOT AT-V.INTRO-FLAG = 'K' OR = 'G'
      MOVE 'M' TO #SCRR-TYPE (#I2)
*
      IF AT-V.DATE-OF-BIRTH LE (*DATN - 810000)
        MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
        MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
      ELSE
        MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
        MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
      END-IF
    ELSE
      IF AT-V.DATE-OF-BIRTH LE (*DATN - 240000)
       AND FUNCTION-GEN23-INSURE(<AT-V.MEMBER-CN>) = FALSE        /* PM5
        MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
        MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
      ELSE
        MOVE #MAX-FACE-TM TO #SCRR-AVAIL-TM-N (#I2)
        MOVE #MAX-FACE-WL TO #SCRR-AVAIL-WL-N (#I2)
      END-IF
*
      IF AT-V.INTRO-FLAG = 'G'
        MOVE 'G' TO #SCRR-TYPE (#I2)
      ELSE
        MOVE 'K' TO #SCRR-TYPE (#I2)
      END-IF
    END-IF
*
    MOVE AT-V.NAME          TO #SCRR-NAME(#I2)
    MOVE AT-V.DATE-OF-BIRTH TO #SCRR-DOB-N (#I2)
    MOVE AT-V.SSN           TO #SCRR-SSN-N (#I2)
    MOVE AT-V.ID-NUMBER     TO #SCRR-ID (#I2)
    RESET                      #SCRR-FACE-TM-N (#I2)
    RESET                      #SCRR-FACE-WL-N (#I2)
  END-FIND
END-IF
* * * * * * * * * * * * * FINISH FILLING SCREEN
*
FOR #I1 1 TO 20
  IF #SCRR-NAME (#I1) = ' '
    ESCAPE TOP
  END-IF
*
  IF (#CGA-SCR-SUFFIX = 'SPS') AND (#SCRR-TYPE (#I1) = 'S')
   AND (NOT #SCRR-AVAIL-TM (#I1) = 'Ineligible' OR = 'Deceased'
                        OR = 'Over-age')
    MOVE 'X' TO #SCRR-SEL (#I1)
  END-IF
*
  IF NOT #SCRR-AVAIL-TM (#I1) = 'Ineligible' OR = 'Deceased'
                        OR = 'Over-age'
    RESET #SCRR-SEL-CV (#I1)
  END-IF
*
  IF (#SCRR-AVAIL-TM (#I1) ='Over-age') AND (#SCRR-DOB-N (#I1) = 0)
    MOVE 'Missing DOB' TO #SCRR-AVAIL-TM (#I1)
    MOVE 'Missing DOB' TO #SCRR-AVAIL-WL (#I1)
  END-IF
*
  MOVE EDITED #SCRR-SSN-N (#I1) (EM=999-99-9999) TO #SCRR-SSN (#I1)
  MOVE #SCRR-DOB-N (#I1) TO #WYYYYMMDD-N
  COMPRESS #WMM-A #WDD-A #WYYYY-A INTO #SCRR-DOB (#I1) WITH '/'
  IF #SCRR-FACE-TM (#I1) = ' '
    MOVE EDITED #SCRR-FACE-TM-N(#I1) (EM=ZZZ,ZZ9) TO #SCRR-FACE-TM (#I1)
  END-IF
  IF #SCRR-FACE-WL (#I1) = ' '
    MOVE EDITED #SCRR-FACE-WL-N(#I1) (EM=ZZZ,ZZ9) TO #SCRR-FACE-WL (#I1)
  END-IF
*
  IF #SCRR-AVAIL-TM (#I1) = ' '
    MOVE EDITED #SCRR-AVAIL-TM-N(#I1)(EM=ZZZ,ZZ9) TO #SCRR-AVAIL-TM(#I1)
*
    IF #SCRR-ACT-FLG (#I1) NE ' '
      COMPRESS #SCRR-AVAIL-TM (#I1) '(' #SCRR-ACT-FLG (#I1) ')'
          INTO #SCRR-AVAIL-TM (#I1) LEAVING NO
    END-IF
  END-IF
*
  IF #SCRR-AVAIL-WL (#I1) = ' '
    MOVE EDITED #SCRR-AVAIL-WL-N(#I1)(EM=Z,ZZZ,ZZ9) TO #SCRR-AVAIL-WL(#I1)     /*tt1
*
    IF #SCRR-ACT-FLG (#I1) NE ' '
      COMPRESS #SCRR-AVAIL-WL (#I1) '(' #SCRR-ACT-FLG (#I1) ')'
          INTO #SCRR-AVAIL-WL (#I1) LEAVING NO
    END-IF
  END-IF
END-FOR
*
* * * * * * * * * * * * * SHOW SCREEN
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF NOT #CGA-SCR-SUFFIX = 'OVR' OR = 'NEW' OR = 'OLD'
  RESET #CGA-SCR-SUFFIX
END-IF
*
REPEAT                                 /* PM6
  IF #I2 = 1
    MOVE 'X' TO #SCRR-SEL (1)
  ELSE
    SET KEY PF4                        /* PM6
    INPUT MARK *#SCRR-SEL (1) USING MAP 'C2140MPO'
*
    IF *PF-KEY = 'PF4'                 /* PM6
      PERFORM CHECK-FOR-DETAIL         /* PM6
      ESCAPE TOP                       /* PM6
    END-IF                             /* PM6
  END-IF
*
  SET KEY OFF                          /* PM6
  ESCAPE BOTTOM                        /* PM6
END-REPEAT                             /* PM6
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
FOR #I2 1 TO 20
  IF #SCRR-SEL (#I2) NE ' '
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #ADD-GEN-SEL NE ' '
  MOVE ST-V.ID-NUMBER TO #CGA-SCR-ID
  MOVE 'GEN' TO #CGA-SCR-SUFFIX
ELSE
  IF #I2 LE 20
    MOVE #SCRR-ID (#I2) TO #CGA-SCR-ID
    IF #SCRR-SUFFIX (#I2) NE ' '
      MOVE #SCRR-SUFFIX (#I2) TO #CGA-SCR-SUFFIX
    END-IF
  END-IF
END-IF
*
MOVE 'PO' TO #CGA-SCR-COMMAND
FETCH 'C2150PPO'
*
***********************************************************************
DEFINE SUBROUTINE LOAD-CN-INFO
***********************************************************************
*
MOVE LK-V.INTRO-FLAG TO #INTRO-FLAG                               /* PM1
RESET #L-DATE-OF-BIRTH                                                   /* TT1
#L-DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<LK-V.MEMBER-CONTACT-ID>)          /* TT1

MOVE #L-DATE-OF-BIRTH TO #DOB      /* DW4                                /* TT1
PERFORM CALL-GMAXSXX                                              /* PM1
*
ADD 1 TO #I2
*
IF #I2 GT 20
  MOVE 20 TO #I2
END-IF
*
RESET #TOT-FACE-WL #TOT-FACE-TM #TOT-FACE
MOVE *DATN TO #EFF-DATE
FOR #I3 1 TO 9
  MOVE LK-V.PLAN(#I3) TO #PLAN
  IF ((LK-V.PLAN (#I3) = 'LT')
   AND (#L-DATE-OF-BIRTH LT (#EFF-DATE - 400000)))              /* TT1
   OR ((LK-V.PLAN(#I3) = 'RT')
   AND (LK-V.ISSUE-AGE(#I3) GT '65'))
   OR ((LK-V.PLAN(#I3) = 'EZ')                               /* YK2 
   AND (LK-V.PAID-UP-DATE(#I3) LE #EFF-DATE))                /* YK2
*  OR  (((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))            /* YK1
   OR (F-LT2-TRUE(<#PLAN>)                                   /* YK1
   AND ((LK-V.ISSUE-DATE(#I3) + 40000) LE #EFF-DATE))
   OR ( IS-ANNUITY-LIFE(<LK-V.PLAN (#I3)>))   /* DW3           /* TT2
*
    CALLNAT 'GDROPSTM' USING
      LK-V.PLAN          (#I3)
      LK-V.SMOKER-FLAG   (#I3)
      #L-DATE-OF-BIRTH                                          /* TT1
      LK-V.ISSUE-DATE    (#I3)
      LK-V.PAID-UP-DATE  (#I3)
      #EFF-DATE
      LK-V.ISSUE-AGE     (#I3)
      #PAY-STAT
      LK-V.FACE-AMOUNT   (#I3)
      LK-V.DEATH-BENEFIT (#I3)
      LK-V.CASH-VALUE    (#I3)
      LK-V.BEG-CASH-VALUE(#I3)
  END-IF
*
  IF LK-V.STATUS (#I3) = 'D'
*   IF  (LK-V.PLAN (#I3) = 'RT' OR = 'LT')
*    OR ((LK-V.PLAN-A1 (#I3) GE 'U') AND (LK-V.PLAN-A1 (#I3) LE 'Z'))
    IF FUNCTION-TERM-POLICY(<LK-V.PLAN(#I3)>)                /* YK1
      ADD LK-V.FACE-AMOUNT (#I3) TO #TOT-FACE-TM
    ELSE
      ADD LK-V.FACE-AMOUNT (#I3) TO #TOT-FACE-WL
    END-IF
  END-IF
END-FOR
*
COMPUTE #TOT-FACE = #TOT-FACE-TM + #TOT-FACE-WL
*
IF #TOT-FACE-WL = 1000
  MOVE 'Sv-Only' TO #SCRR-FACE-TM  (#I2)
  MOVE 'Sv-Only' TO #SCRR-FACE-WL  (#I2)
  MOVE #MAX-FACE-WL-DISP TO #SCRR-AVAIL-WL (#I2)
ELSE
  MOVE #TOT-FACE-TM TO #SCRR-FACE-TM-N (#I2)
  MOVE #TOT-FACE-WL TO #SCRR-FACE-WL-N (#I2)
END-IF
*
IF LK-V.INTRO-FLAG = 'S'
  MOVE 'S' TO #SCRR-TYPE (#I2)
*
  IF #L-DATE-OF-BIRTH LE (*DATN - 810000)                                   /* TT1
    MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
    MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
  ELSE
    COMPUTE #SCRR-AVAIL-TM-N (#I2) = #MAX-FACE-TM - #TOT-FACE-TM
    COMPUTE #SCRR-AVAIL-WL-N (#I2) = #MAX-FACE-WL - #TOT-FACE-WL
  END-IF
ELSE
  IF LK-V.INTRO-FLAG = 'K' OR = 'G'
    MOVE LK-V.INTRO-FLAG TO #SCRR-TYPE (#I2)
    IF #L-DATE-OF-BIRTH LE (*DATN - 240000)                                  /* TT1
     AND FUNCTION-GEN23-INSURE(<#CN-A>) = FALSE                 /* PM5
      MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
      MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
    ELSE
      COMPUTE #SCRR-AVAIL-TM-N (#I2) = #MAX-FACE-TM - #TOT-FACE-TM
      COMPUTE #SCRR-AVAIL-WL-N (#I2) = #MAX-FACE-WL - #TOT-FACE-WL
    END-IF
  ELSE
    MOVE 'M' TO #SCRR-TYPE (#I2)
    IF #L-DATE-OF-BIRTH LE (*DATN - 810000)                                 /* TT1
      MOVE 'Over-age' TO #SCRR-AVAIL-TM (#I2)
      MOVE 'Over-age' TO #SCRR-AVAIL-WL (#I2)
    ELSE
      COMPUTE #SCRR-AVAIL-TM-N (#I2) = #MAX-FACE-TM - #TOT-FACE-TM
      COMPUTE #SCRR-AVAIL-WL-N (#I2) = #MAX-FACE-WL - #TOT-FACE-WL
    END-IF
*
    FIND AT-V WITH SSN = ST-V.SSN
      IF AT-V.INTRO-FLAG = 'V'
        ESCAPE TOP
      END-IF
*
      IF AT-V.ACTION-FLAG NE 'A'
        MOVE AT-V.ACTION-FLAG TO #SCRR-ACT-FLG (#I2)
      END-IF
    END-FIND
  END-IF
END-IF
*
IF (#TOT-FACE + #SCRR-AVAIL-TM-N (#I2)) GT #MAX-FACE-WL
* COMPUTE #SCRR-AVAIL-TM-N (#I2) = #MAX-FACE-TM - #TOT-FACE       /* PM1
  COMPUTE #SCRR-AVAIL-TM-N (#I2) = #MAX-FACE-WL - #TOT-FACE       /* PM1
END-IF
*
IF (#TOT-FACE + #SCRR-AVAIL-WL-N (#I2)) GT #MAX-FACE-WL
  COMPUTE #SCRR-AVAIL-WL-N (#I2) = #MAX-FACE-WL - #TOT-FACE
END-IF
*
COMPUTE #DIVIDEND = #SCRR-AVAIL-TM-N (#I2) / 5000
IF #REM NE 0
 COMPUTE #SCRR-AVAIL-TM-N (#I2) = 5000 * (#DIV-5 + 1)
END-IF
*
COMPUTE #DIVIDEND = #SCRR-AVAIL-WL-N (#I2) / 5000
IF #REM NE 0
 COMPUTE #SCRR-AVAIL-WL-N (#I2) = 5000 * (#DIV-5 + 1)
END-IF
*
IF LK-V.DATE-OF-DEATH NE 0
  MOVE 'Deceased' TO #SCRR-AVAIL-TM (#I2)
  MOVE 'Deceased' TO #SCRR-AVAIL-WL (#I2)
ELSE
  IF  (LK-V.SUSPEND-CODE = '5' OR = '6' OR = '9')
   OR (LK-V.RESTRICT-MBR-SW = 'Y')
    MOVE 'Ineligible' TO #SCRR-AVAIL-TM (#I2)
    MOVE 'Ineligible' TO #SCRR-AVAIL-WL (#I2)
  END-IF
END-IF
*
MOVE LK-V.NAME          TO #SCRR-NAME(#I2)
MOVE #L-DATE-OF-BIRTH     TO #SCRR-DOB-N (#I2)            /* TT1
MOVE LK-V.SSN           TO #SCRR-SSN-N (#I2)
MOVE LK-V.ID-NUMBER     TO #SCRR-ID (#I2)
#SCRR-CONTACT-ID (#I2) := LK-V.MEMBER-CONTACT-ID       /* PM6
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE ERROR-MSG
***********************************************************************
*
INPUT TEXT #TEXT MARK *#CGA-SCR-ID USING MAP 'G1000MXX'
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE CALL-GMAXSXX                                    /* PM1
***********************************************************************
*
CALLNAT 'GMAXSXX'
  #CGA-ORIG-COMMAND
  #MAX-FACE-WL
  #MAX-FACE-TM
  #OVERRIDE-MAX-FACE-WL
  #OVERRIDE-MAX-FACE-TM
  #INTRO-FLAG
  #DOB
*
MOVE EDITED #MAX-FACE-WL (EM=Z,ZZZ,ZZ9) TO #MAX-FACE-WL-DISP
MOVE EDITED #MAX-FACE-TM (EM=Z,ZZZ,ZZ9) TO #MAX-FACE-TM-DISP
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE LOAD-SPOUSE-GEN                                /* PM6
***********************************************************************
*
FIND CR-V WITH CR-V.CONTACT-1-ID = ST-V.MEMBER-CONTACT-ID
  IF NOT CR-V.ROLE-2-CD = 'S' OR = 'K' OR = 'G'
    ESCAPE TOP
  END-IF
*
  RESET #MATCH #LOADED
  FIND (1) CT-V WITH CT-V.CONTACT-ID = CR-V.CONTACT-2-ID
    IF CT-V.DELETE-FLAG = 'Y'
      ESCAPE BOTTOM
    END-IF
*
    FOR #I3 1 TO 20
      IF  (CT-V.SSN = #SCRR-SSN-N (#I3) AND #SCRR-SSN-N (#I3) NE 0)
       OR (FUNCTION-DISP-CT-NAME(<CT-V.FIRST-NAME,CT-V.MIDDLE-NAME,
                                  CT-V.LAST-NAME,CT-V.SUFFIX>) = #SCRR-NAME (#I3))
        MOVE TRUE TO #MATCH
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF #MATCH
      ESCAPE BOTTOM
    END-IF
*
    FIND LK-V WITH LK-V.SSN = CT-V.SSN
      IF LK-V.PROCESS-IND = 'S' OR = 'C'
        ESCAPE TOP
      END-IF
      PERFORM LOAD-CN-INFO
      #LOADED := TRUE
    END-FIND
*
    IF #LOADED
      ESCAPE BOTTOM
    END-IF
*
    IF CR-V.ROLE-2-CD = 'S'                  /* SPOUSES
      #INTRO-FLAG  := 'S'                    /* PM1
      #DOB := CT-V.DATE-OF-BIRTH             /* DW4
      PERFORM CALL-GMAXSXX                   /* PM1
*
      ADD 1 TO #I2
      IF #I2 GT 20
        MOVE 20 TO #I2
      END-IF
*
      RESET #SCRR-FACE-TM-N (#I2) #SCRR-FACE-WL-N (#I2)
      #SCRR-NAME(#I2) := FUNCTION-DISP-CT-NAME(<CT-V.FIRST-NAME,CT-V.MIDDLE-NAME,
                                                CT-V.LAST-NAME,CT-V.SUFFIX>)
      #SCRR-DOB-N (#I2)         := CT-V.DATE-OF-BIRTH
      #SCRR-SSN-N (#I2)         := CT-V.SSN
      #SCRR-CONTACT-ID (#I2)    := CT-V.CONTACT-ID
      #SCRR-AVAIL-TM-N (#I2)    := #MAX-FACE-TM
      #SCRR-AVAIL-WL-N (#I2)    := #MAX-FACE-WL
      #SCRR-TYPE (#I2)          := CR-V.ROLE-2-CD
      #SCRR-SUFFIX (#I2)        := 'SPS'
      #SCRR-ID (#I2)            := ST-V.ID-NUMBER
*
      DECIDE FOR FIRST CONDITION
        WHEN ST-V.STATUS-CODE-SP = 'D'
          #SCRR-AVAIL-TM (#I2)  := 'Deceased'
          #SCRR-AVAIL-WL (#I2)  := 'Deceased'
        WHEN ST-V.STATUS-CODE-SP = 'F'
         OR  ST-V.SUSPEND-CODE = '7' OR = '8' OR = '9'
          #SCRR-TYPE (#I2)      := 'F'
          #SCRR-AVAIL-TM (#I2)  := 'Ineligible'
          #SCRR-AVAIL-WL (#I2)  := 'Ineligilbe'
        WHEN CT-V.DATE-OF-BIRTH LE (*DATN - 810000)
          #SCRR-AVAIL-TM (#I2)  := 'Over-age'
          #SCRR-AVAIL-WL (#I2)  := 'Over-age'
        WHEN NONE
          IGNORE
      END-DECIDE
    ELSE
      IF CT-V.DATE-OF-BIRTH LT (*DATN - 240000)
        ESCAPE BOTTOM
      END-IF
*
      RESET #STATUS-CODE
      FIND (1) FR-V WITH FR-V.ID-NUMBER = #CGA-ID
        FOR #I4 1 TO 60
          IF FR-V.DEPN-CONTACT-ID (#I4) = CT-V.CONTACT-ID
            #STATUS-CODE := FR-V.DEPN-STATUS-CD (#I4)
            ESCAPE BOTTOM
          END-IF
        END-FOR
      END-FIND
*
      IF #STATUS-CODE = 'D'
        ESCAPE BOTTOM
      END-IF
*
      #INTRO-FLAG := CR-V.ROLE-2-CD
      #DOB := CT-V.DATE-OF-BIRTH                                  /* DW4
      PERFORM CALL-GMAXSXX                                        /* PM1
*
      ADD 1 TO #I2
*
      IF #I2 GT 20
        MOVE 20 TO #I2
      END-IF
*
      RESET #SCRR-FACE-TM-N (#I2) #SCRR-FACE-WL-N (#I2)
      #SCRR-NAME(#I2) := FUNCTION-DISP-CT-NAME(<CT-V.FIRST-NAME,CT-V.MIDDLE-NAME,
                                                CT-V.LAST-NAME,CT-V.SUFFIX>)
      #SCRR-DOB-N (#I2)         := CT-V.DATE-OF-BIRTH
      #SCRR-SSN-N (#I2)         := CT-V.SSN
      #SCRR-CONTACT-ID (#I2)    := CT-V.CONTACT-ID
      #SCRR-AVAIL-TM-N (#I2)    := #MAX-FACE-TM
      #SCRR-AVAIL-WL-N (#I2)    := #MAX-FACE-WL
      #SCRR-TYPE (#I2)          := CR-V.ROLE-2-CD
      #SCRR-ID (#I2)            := ST-V.ID-NUMBER
*
      IF #I4 GT 0      
       AND #I4 LT 15   
         #N2 := #I4    
      ELSE             
        ADD +1 TO #D-I
        MOVE #D-I TO #N2
      END-IF           
      COMPRESS 'D' #A2 INTO #SCRR-SUFFIX (#I2) LEAVING NO
    END-IF
  END-FIND
END-FIND
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CHECK-FOR-DETAIL                                /* PM6
************************************************************************
*
RESET #REL-CID
#PARENT-CID := ST-V.MEMBER-CONTACT-ID
*
IF *CURS-LINE GT 2
 AND *CURS-LINE LT 23
  #REL-I    := *CURS-LINE - 2
  #REL-CID  := #SCRR-CONTACT-ID (#REL-I)
END-IF
*
IF #REL-CID NE 0
   #MODE    := 'V'
   CALLNAT 'GUPCISXX' #PARENT-CID #REL-CID #CGA-SCR-COMMAND #CGA-ID #CGA-USER-COMPANY #CGA-CLERK #MODE #REL-MSG
END-IF
*
END-SUBROUTINE
*
END
