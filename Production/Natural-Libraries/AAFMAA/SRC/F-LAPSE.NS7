* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-LAPSE                                               *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Determine if policy lapsed during specified timeframe *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    07/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE FUNCTION FUNCTION-LAPSED RETURNS (L) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #CN-A                  (A6)
1 #SEGMENT               (I2)
1 #BEGIN-DATE            (N8)
1 #END-DATE              (N8)
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 ISSUE-DATE            (9)
 2 STATUS                (9)
*
1 MD-V VIEW OF A-MBRSHIP-MTD-DETAIL
 2 ENTRY-DATE
 2 CODE
 2 ISSUE-DATE
 2 ID-NUMBER
*
1 #INVERSE-BEGIN-DATE    (N8)
1 #INVERSE-END-DATE      (N8)
*
END-DEFINE
************************************************************************
*
FUNCTION-LAPSED := FALSE
*
COMPUTE #INVERSE-BEGIN-DATE = 99999999 - #END-DATE
COMPUTE #INVERSE-END-DATE   = 99999999 - #BEGIN-DATE
*
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORDS FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #CN-A
      IF NO RECORDS FOUND
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
IF ST-V.STATUS (#SEGMENT) = 'D'
  ESCAPE ROUTINE
END-IF
*
READ MD-V BY ENTRY-DATE FROM #INVERSE-BEGIN-DATE THRU #INVERSE-END-DATE
  IF MD-V.ID-NUMBER = ST-V.ID-NUMBER
   AND MD-V.ISSUE-DATE = ST-V.ISSUE-DATE (#SEGMENT)
   AND MD-V.CODE = 'L' OR = 'T' OR = 'U' OR = 'V'
    FUNCTION-LAPSED := TRUE
    ESCAPE BOTTOM
  END-IF
END-READ
*
END-FUNCTION
*
END
