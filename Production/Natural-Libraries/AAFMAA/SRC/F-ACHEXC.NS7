* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-ACHEXC                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Determine if policy is reversable for ACH             *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE FUNCTION FUNCTION-ACH-REVERSABLE RETURNS (L) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #SEGMENT               (I2)
1 #CN-A                  (A6)
1 #TRAN-CODE             (A4)
1 #TRAN-AMOUNT           (P7.2)
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ISSUE-DATE            (9)
 2 STATUS                (9)
 2 PAY-STATUS            (9)
 2 EZPAY-CAP-LOAN-AMOUNT
*
END-DEFINE
************************************************************************
*
FUNCTION-ACH-REVERSABLE         := TRUE
*
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORD FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #CN-A
      IF NO RECORD FOUND
        FUNCTION-ACH-REVERSABLE := FALSE
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
  END-NOREC
*
  DECIDE FOR FIRST CONDITION
    WHEN #TRAN-CODE = 'PRER' OR = 'RIDR' OR = 'BADJ' OR = 'FPFR'
     AND ST-V.PAY-STATUS (#SEGMENT) GE '40'
      FUNCTION-ACH-REVERSABLE   := FALSE
    WHEN #TRAN-CODE = 'APFR'
     AND #TRAN-AMOUNT GT ST-V.EZPAY-CAP-LOAN-AMOUNT
      FUNCTION-ACH-REVERSABLE   := FALSE
    WHEN NONE
      IGNORE
  END-DECIDE
END-FIND
*
END-FUNCTION
*
END
