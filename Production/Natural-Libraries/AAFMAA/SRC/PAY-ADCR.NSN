* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**********************************************************************************
* Pgm ID : PAY-ADCR
*
* Function : pay amounts left that are not able to be be used for other purposes.
*
* 10/19/2011 vt Initial creation
* 08/19/2013 vt1 Add PS payment /ticket 2177/
**********************************************************************************
define data parameter
1 #rest-amt           (n7.2)
1 #STATUS-ISN         (P8)
1 CC-PAYMENT
  2 PARM-DATE             (N8.0)
  2 ID-NUMBER             (N6.0)
  2 INSURED-NAME          (A50)
  2 C@PAY-POLICY          (N2.0)
  2 PAY-POLICY     (1:9)
    3 POLICY-NBR          (A16)
    3 segment             (i2)               
    3 cap-policy          (a1)
    3 PLAN-TYPE           (A3)
    3 ISSUE-DATE          (N8.0)
    3 FACE-AMOUNT         (N7.2)
    3 MAX-PREMIUMS        (N4)
    3 MIN-PREMIUMS        (N2)
    3 DEFAULT-PREM        (N2)
    3 C@AVAILABLE-PAYMENT (N2.0)
    3 AVAILABLE-PAYMENT (1:10)
      4 PAYMENT-NOTE      (A) DYNAMIC
      4 CC-PAYABLE        (A1)
      4 VAR-AMOUNT        (A1)
      4 EXCEED-AMT        (A1)
      4 TRAN-TYPE         (A6)               /* vt1  was a4
      4 PAID-TO-DATE      (N8.0)
      4 PAYMENT-MODE      (A1)
      4 DEFAULT-AMOUNT    (N6.2)
      4 DUE-AMOUNT        (N6.2)
1 #timn               (n7)
1 #error-message      (a75)
LOCAL USING C2500AAM
LOCAL
1 #ID-NUMBER    (N6)
1 REDEFINE #ID-NUMBER
  2 #ID-NUMBER-A (A6)
1 #i1                     (i2)
1 #l                      (i2)
1 #m                      (i2)
1 #cc-x                   (i2)
1 #iln1                   (i2)
1 #iln2                   (i2)
1 #lx                     (i2)
1 #pol-nbr                (a16)
* 1 #fpf-amt                (p7.2)
1 #inp-amt                (p7.2)
1 #inp-tr-cd              (a4)
1 #cash-code              (a1)
1 #issue-date             (n8)
1 REDEFINE #ISSUE-DATE
  2 #ISSUE-DATE-A         (A8)
1 #TERM                   (L)
1 #DATE-MMDDYYYY          (N8)
1 REDEFINE #DATE-MMDDYYYY
  2 #MMDD                 (N4)
  2 #YYYY                 (N4)
1 REDEFINE #DATE-MMDDYYYY
  2 #DATE-MMDDYYYY-A      (A8)
1 #update-mode            (l) init<TRUE>
1 #SG-TBL                 (15)
  2 #SG-CAT1              (A2)
  2 #SG-CAT2              (A2)
  2 #SG-AMT               (N6.2)
1 #datn                   (n8)
1 redefine #datn
  2 #datn-yyyy             (n4)
  2 #datn-mm               (n2)
  2 #datn-dd               (n2)
1 #datn-a                  (a10)
1 #amount                  (a10)
* 1 #WMMDDYYYY
*   2 #WMM   (N2)
*   2 #WDD   (N2)
*   2 #WYYYY (N4)
* 1 REDEFINE #WMMDDYYYY
*   2 #WMMDDYYYY-A (A8)
* 1 #WYYYYMMDD
*   2 #WYYYY   (N4)
*   2 #WMM     (N2)
*   2 #WDD     (N2)
* 1 REDEFINE #WYYYYMMDD
*   2 #WYYYYMMDD-N (N8)
*
1 #TT-DATE                 (P8)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER         (N6)
  2 name              (a25)            /* used in prnc pmts
  2 PROCESS-IND       (A1)
  2 STATUS            (A1/9)
  2 PAY-STATUS        (A2/9)
  2 MODE              (A1/9)
  2 PLAN              (A3/9)
  2 ISSUE-DATE        (N8/9)
  2 PAID-TO-DATE      (N8/9)
  2 PAID-UP-DATE      (N8/9)
  2 ANNUAL-PREM       (P5.2/9)
  2 MTD-ACV-PREM-ADJ  (P7.2/9)
  2 X-FIRST-PLACE-FEE (P7.2/9)
  2 alot-amt
  2 alot-sw
  2 stop-date
  2 debit-credit-amt
  2 AUX-DEBIT-CREDIT-AMT
  2 ALT-LP-AMT
  2 PAD-CODE
*
1 AR-V VIEW OF A-ACCOUNTING-REGISTER
  2 id-number         (n6)
  2 name              (a25)
  2 transaction-datn  (p9)
  2 transaction-timn  (p7)
  2 cash-code         (a1)
  2 clerk-id          (a3)
  2 terminal-id       (a8)
  2 PROCESS-IND       (A1)
  2 POLICY-NUMBER     (A16)
  2 AALP-FLAG         (A1)
  2 TXN-CODE          (A4)
  2 TXN-ACCT-NUMBER   (N3)
  2 MESSAGE-CODE      (A1)
  2 credit-amount     (p7.2)
  2 acct-date         (n8)
  2 debit-amount      (p7.2)
  2 description       (a20)
  2 SG-CATEGORY-1     (a2/15)
  2 SG-CATEGORY-2     (a2/15)
  2 SG-AMOUNT         (n6.2/15)
*
1 #out-rec            (a100)
1 #tab                (a1) INIT <H'09'>
1 #tran-code          (a4)
*
end-define
*
GET ST-V #STATUS-ISN
move *datn to #datn
*
#cc-x      := 1
#inp-amt   := #rest-amt
#inp-tr-cd := 'ADCR'
#cash-code := 'I'
perform apply-adcr
perform prepare-ACCT-REG
perform write-report
*
*****************
DEFINE SUBROUTINE APPLY-adcr
*****************
*
#ILN1              := 1
#SCR-ID-SSN (1)    := ST-V.ID-NUMBER
#SCR-EFF-DT (1)    := #DATE-MMDDYYYY-A
#SCR-TRAN-CODE (1) := #INP-TR-CD
#SCR-TRAN-AMT  (1) := #INP-AMT
#SCR-CASH-CODE (1) := #CASH-CODE
*
CALLNAT 'CADCRSAM' #SCR-LINES
  ST-V.STATUS                (1:9)
  ST-V.PAY-STATUS            (1:9)
  ST-V.MODE                  (1:9)
  ST-V.PLAN                  (1:9)
  ST-V.ISSUE-DATE            (1:9)
  ST-V.PAID-TO-DATE          (1:9)
  ST-V.PAID-UP-DATE          (1:9)
  ST-V.ANNUAL-PREM           (1:9)
  ST-V.MTD-ACV-PREM-ADJ      (1:9)
  ST-V.ALOT-AMT
  ST-V.ALOT-SW
  ST-V.STOP-DATE
  ST-V.DEBIT-CREDIT-AMT
  ST-V.AUX-DEBIT-CREDIT-AMT
  ST-V.ALT-LP-AMT
  ST-V.PAD-CODE
  #ILN1
  #ILN2
  #UPDATE-MODE
  #TT-DATE
*
update (0148)
END-SUBROUTINE
*
*****************
define subroutine Prepare-ACCT-REG
*****************
*
write '=' #scr-tran-amt(1) '=' #iln1
RESET AR-V
ar-v.id-number               := ST-V.id-number
ar-v.transaction-datn        := #datn
ar-v.transaction-timn        := #timn
ar-v.acct-date               := #datn
ar-v.cash-code               := 'I'
ar-v.name                    := ST-V.name
ar-v.clerk-id                := 'EDS'
ar-v.terminal-id             := *init-id
AR-V.TXN-CODE                := 'ADCR'
AR-V.PROCESS-IND             := ST-V.PROCESS-IND
AR-V.AALP-FLAG               := #SCR-ALP-CODE  (1)
AR-V.POLICY-NUMBER           := ST-V.id-number
* AR-V.SG-AMOUNT           (1) := #scr-tran-amt(1)    /* vt 03082012
AR-V.credit-amount           := #scr-tran-amt(1)
* AR-V.MESSAGE-CODE            := '1'                 /* vt 03132012
AR-V.TXN-ACCT-NUMBER         := 450
AR-V.DESCRIPTION             := 'INV'

STORE AR-V
*
end-subroutine
*
*****************
define subroutine write-report
*****************
#tran-code := 'ADCR'
compress #datn-mm '/' #datn-dd '/' #datn-yyyy into #datn-a leaving no
move edited #scr-tran-amt(1) (EM=zzzzzz9.99) to #amount
compress st-v.id-number #tab
  #tran-code              #tab
  #amount                 #tab
  #datn-a                 #tab
  into #out-rec leaving no
write work 3 #out-rec
*
end-subroutine
end
