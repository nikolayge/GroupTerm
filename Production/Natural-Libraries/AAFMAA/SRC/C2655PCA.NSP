* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*** <@ NATURAL META INFORMATION BEGIN
***
*** @Mode Structured
***
*** NATURAL META INFORMATION END @>
* ************************************************************
* PGM-ID: C2655PCA
* PURPOSE: TO PROCESS 'CA' COMMAND (CHANGE MISC LOAN INFO)
* ************************************************************
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE      TAG     REASON                                        *
*                                                                      *
* PAM  09102004  PM1     Allow KHB and PAM to remove loan dates for    *
*                        corrections.                                  *
* DRW  12282004  DW2     Increase Var Loan to handle 999,999.99        *
* DRW  05072006  DW3     Remove ALOT-SW logic                          *
* DRW  10202007  DW4     CAP MIP ONLY Bill Processing                  *
* YAK  12052007  YK1     Remove hardcording for interest rates         *
*                          (Read GEN-TABLE through OBJNGENT)           *
* DRW  01282008  DW5     Allow Selected Users to push CAP-BILL-DATE    *
*                        into the future                               *
* YAK  02052008  YK2     Change code to use FUNCTION-INT-RATE          *
*                         to get rate from GEN-TABLE                   *
* PAM 06/04/2010 PM2     Added Lin,Angel and Rachel to CAP BILL DATE   *
*                        restrictions - extended time to 12 months     *
* vxt 02/06/2015 vt1    /Ticket 3335/                                  *
* TMT 07/11/2017 TT1     Multiple Cap Project
* vxt 01/08/2018 vt2    /Ticket 6490/ Add status for lapsed and reinstated CAP loans
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL  USING C2655LCA
LOCAL  USING CAPL0001  /* TT1
LOCAL
* 1 #CURR-VAR-INT-RATE    (P2.3) INIT <8.10>   /* YK1
1 #CURR-VAR-INT-RATE    (P2.3)                 /* YK1
1 #SCR-CV           (C)
1 #SCN-CAP-BILL-DATE    (A8)      /* DW4
1 #LOW-DATE           (N8)
1 #HIGH-DATE          (N8)
1 #MANAGER-HIGH-DATE  (N8)    /* DW5
1 #CAP-X             (I2)
*
1 #INVALID        (L)
1 #ERR-MSG        (A60)
1 #TEXT           (A75)
1 #FIELD-CHANGED  (L)
1 #WRITE-TRLOG    (L)
1 #UPDATE-DATE    (L)
1 #I1                   (I2)
1 #I2                   (I2)
1 #HOLD-LOAN-DATE      (N8/9)
1 #HOLD-LOAN-DATE-2    (N8/9)
1 #HOLD-CAP-AMT        (N5.2/9)
1 #HOLD-CAP-AMT-2      (N5.2/9)
1 #HOLD-NXT-YR-ACC-INT (N5.2/9)
1 #HOLD-NXT-YR-ACC-INT-2 (N5.2/9)
1 #HOLD-INTEREST-RATE  (N2.3/9)
1 #HOLD-APL-CNT        (P3)
1 #HOLD-LPO-PMT-RCVD         (P5.2/9)
1 #HOLD-LPO-PMT-RCVD-2       (P5.2/9)
1 #HOLD-LPO-PRIOR-LOAN-BAL   (P5.2/9)
1 #HOLD-LPO-PRIOR-LOAN-BAL-2 (P6.2/9)   /* DW2
1 #HOLD-LPO-PRIOR-ACC-INT    (P5.2/9)
1 #HOLD-LPO-PRIOR-ACC-INT-2  (P5.2/9)
1 #HOLD-CAP-BILL-DATE         (N8)      /* DW4
*
1 #CAP-NUM (N2/9)               /* TT1 Begin
1 #CAP-ISSUE-DATE (A10/9)
1 #LOAN-AMOUNT (N7.2/9)
1 #INTEREST-RATE (N2.3/9)
1 #PAY-OFF-DATE (A10/9)
1 #CURRENT-LOAN-STATUS (A10/9)
1 #POLICY-NUMBER (A16/9)
1 I (I4)
1 #HOLD-DATE (A8)
1 #DATE (D)                     /* TT1 end
*
1 #WYYYYMMDD
  2 #WYYYY    (N4)
  2 #WMM      (N2)
  2 #WDD      (N2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N (N8)
*
1 #WMMDDYYYY
  2 #WMM      (N2)
  2 #WDD      (N2)
  2 #WYYYY    (N4)
1 REDEFINE #WMMDDYYYY
  2 #WMMDDYYYY-A (A8)
*
1 #TODAY-YYYYMMDD  (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY     (N4)
  2 #TODAY-MM       (N2)
  2 #TODAY-DD       (N2)
*
1 #YYYYMMDD         (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A    (A4)
  2 #MM-A     (A2)
  2 #DD-A     (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM     (N2)
  2 #DD     (N2)

*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER        (N6)
  2 PROCESS-IND      (A1)  /* PM - 06/2000 - SPLIT COMPANIES
  2 NAME             (A25)
  2 DATE-LAST-UPDATE (N8)
  2 TERMINAL-ID      (A8)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (P9)
  2 TRANSACTION-TIMN (P7)
  2 OLD-DATA         (A125/32)
  2 NEW-DATA         (A125/32)
  2 REDEFINE NEW-DATA
    3 NEW-DATA-OCC     (32)
      4 NEW-DATA-CA      (A3)
      4 NEW-DATA-REST    (A22)
*
1 #INDEXT         (I2)
1 #W-OLD-NEW-DATA  (A125)
1 REDEFINE #W-OLD-NEW-DATA
  2 #W-DESCRIPTION   (A10)
  2 #W-DATA          (A115)
*
END-DEFINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX       /* NATURAL ERROR ROUTINE
*
GET CA-V #CGA-ISN
*
MOVE *DATN TO #TODAY-YYYYMMDD    /* DW4
MOVE *DATN TO #YYYYMMDD
MOVE 31 TO #DD
ADD 0 TO #MM                    /* Chged to must be current Month
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
MOVE #YYYYMMDD TO #HIGH-DATE
*
MOVE *DATN TO #YYYYMMDD                     /* DW5
MOVE 31 TO #DD
ADD 12 TO #MM                   /* Push forward CAP BILL START DATE     PM2 - was 3 months
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
MOVE #YYYYMMDD TO #MANAGER-HIGH-DATE    /* DW5

*
MOVE *DATN TO #YYYYMMDD       /* DW4
MOVE 1 TO #DD
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #LOW-DATE
*
MOVE APL-CNT             TO #HOLD-APL-CNT
MOVE X-LOAN-DATE            (*)  TO #HOLD-LOAN-DATE          (*)
MOVE X-LOAN-DATE-2          (*)  TO #HOLD-LOAN-DATE-2        (*)
MOVE X-CAP-AMT              (*)  TO #HOLD-CAP-AMT            (*)
MOVE X-CAP-AMT-2            (*)  TO #HOLD-CAP-AMT-2          (*)
MOVE X-NXT-YR-ACCUM-INT     (*)  TO #HOLD-NXT-YR-ACC-INT     (*)
MOVE X-NXT-YR-ACCUM-INT-2   (*)  TO #HOLD-NXT-YR-ACC-INT-2   (*)
MOVE X-INTEREST-RATE        (*)  TO #HOLD-INTEREST-RATE      (*)
MOVE X-LPO-PMT-RCVD         (*) TO #HOLD-LPO-PMT-RCVD        (*)
MOVE X-LPO-PMT-RCVD-2       (*) TO #HOLD-LPO-PMT-RCVD-2      (*)
MOVE X-LPO-PRIOR-LOAN-BAL   (*) TO #HOLD-LPO-PRIOR-LOAN-BAL  (*)
MOVE X-LPO-PRIOR-LOAN-BAL-2 (*) TO #HOLD-LPO-PRIOR-LOAN-BAL-2(*)
MOVE X-LPO-PRIOR-ACC-INT    (*) TO #HOLD-LPO-PRIOR-ACC-INT   (*)
MOVE X-LPO-PRIOR-ACC-INT-2  (*) TO #HOLD-LPO-PRIOR-ACC-INT-2 (*)
MOVE CAP-BILL-DATE              TO #HOLD-CAP-BILL-DATE                /* DW4
*
FIND LNS-V WITH LNS-V.ID-NUMBER = CA-V.ID-NUMBER SORTED BY LOAN-NUMBER           /* TT1 Begin
*
  ADD 1 TO I
  #LOAN-AMOUNT(I)         := LNS-V.LOAN-INITIAL-AMOUNT
  #INTEREST-RATE(I)       := LNS-V.LOAN-INTEREST-RATE
  #POLICY-NUMBER(I)       := LNS-V.LOAN-POLICY-NUMBER
*
  IF LNS-V.LOAN-ISSUE-DATE > 0
    #HOLD-DATE := LNS-V.LOAN-ISSUE-DATE
    MOVE EDITED #HOLD-DATE TO #DATE (EM=YYYYMMDD)
    MOVE EDITED #DATE (EM=MM/DD/YYYY) TO #CAP-ISSUE-DATE(I)
  ELSE
    #CAP-ISSUE-DATE(I) := 'N/A'
  END-IF
*
  IF LNS-V.LOAN-PAYOFF-DATE > 0
    #HOLD-DATE := LNS-V.LOAN-PAYOFF-DATE
    MOVE EDITED #HOLD-DATE TO #DATE (EM=YYYYMMDD)
    MOVE EDITED #DATE (EM=MM/DD/YYYY) TO #PAY-OFF-DATE(I)
  ELSE
    #PAY-OFF-DATE(I) := 'N/A'
  END-IF
*
  PERFORM CHECK-LOAN-STATUS
  #CAP-NUM(I)        := LNS-V.LOAN-NUMBER
end-find                                           /* TT1 End
*
IF CAP-BILL-DATE NE 0
  MOVE CAP-BILL-DATE TO #YYYYMMDD                                       /* DW4
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SCN-CAP-BILL-DATE LEAVING NO
END-IF
*
#CURR-VAR-INT-RATE := FUNCTION-INT-RATE(<'RATE5AB'>)  /* YK2
*
RESET #I2
FOR #I1 1 TO 9
*
  IF CA-V.CONV-DURATION (#I1) = 999                  /* DW4
    MOVE #I1 TO #CAP-X
  END-IF
*
  ADD 1 TO #I2
  IF ISSUE-DATE (#I1) NE 0
    MOVE #I1 TO #SCR-SEQ-F (#I2)
    MOVE 'F' TO #SCR-F     (#I2)
    COMPRESS ISS-DT-MM (#I1) ISS-DT-DD(#I1) ISS-DT-YYYY (#I1)
      INTO #SCR-ISS-DT-A (#I2) LEAVING NO
*
    IF X-LOAN-DATE (#I1) NE 0
      MOVE X-INTEREST-RATE (#I1) TO #SCR-FIX-RT (#I2)
      COMPRESS X-LOAN-MM (#I1) X-LOAN-DD (#I1) X-LOAN-YYYY (#I1)
        INTO #SCN-LOAN-DATE-A (#I2) LEAVING NO
    END-IF
*
    MOVE #I1 TO #SCR-SEQ-V (#I2)
    MOVE 'V' TO #SCR-V     (#I2)
    COMPRESS ISS-DT-MM (#I1) ISS-DT-DD(#I1) ISS-DT-YYYY (#I1)
      INTO #SCR-ISS-DT-A-2 (#I2) LEAVING NO
*
    IF X-LOAN-DATE-2 (#I1) NE 0
      MOVE #CURR-VAR-INT-RATE TO #SCR-VAR-RT (#I2)
      COMPRESS X-LOAN-MM-2 (#I1) X-LOAN-DD-2 (#I1) X-LOAN-YYYY-2 (#I1)
        INTO #SCN-LOAN-DATE-A-2 (#I2) LEAVING NO
    END-IF
  END-IF
END-FOR
*
MOVE '* ' TO #CGA-SCR-COMMAND
INPUT USING MAP 'C2655MCA'
*
IF #CGA-SCR-COMMAND NE '*'
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
*
PERFORM DO-EDITS
PERFORM UPDATE-RECORD
PERFORM DO-TRLOG
*
IF #FIELD-CHANGED
  UPDATE (0145)
END-IF
*
IF #WRITE-TRLOG
  MOVE 'CA-' TO NEW-DATA-CA (1)
  STORE TRLOG
END-IF
*
MOVE 'CA-' TO #CGA-SCR-COMMAND
*
END TRANSACTION
*
MOVE (AD=P) TO #SCR-CV
*
IF #FIELD-CHANGED
  RESET #TEXT
  COMPRESS 'TRANS COMPLETED' #TEXT INTO #TEXT
  INPUT TEXT #TEXT USING MAP 'C2655MCA'
ELSE
  IF #POLICY-NUMBER(1) = ' '                                     /* TT1
    INPUT TEXT 'NO UPDATES REQUESTED' USING MAP 'C2655MCA'
  ELSE
    INPUT TEXT 'NO UPDATES REQUESTED' USING MAP 'C2656MCA'       /* TT1
  END-IF
END-IF
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE DO-EDITS
* * * * * * * * * * * * * *
*
IF NOT #SCN-CAP-BILL-DATE = '00000000' OR = ' '     /* DW4
  IF #SCN-CAP-BILL-DATE NE MASK(MMDDYYYY)
    REINPUT WITH 'INVALID DATE; FORMAT MUST BE MMDDYYYY'
      MARK *#SCN-CAP-BILL-DATE
  END-IF
*
  IF  (#CAP-X = 0)
      OR (CA-V.X-LOAN-AMOUNT (#CAP-X) = 0)
    REINPUT 'CAP LOAN must exist to use CAP-BILL-DATE'
      MARK *#SCN-CAP-BILL-DATE
  END-IF
*
  MOVE #SCN-CAP-BILL-DATE TO #WMMDDYYYY-A
  MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
  MOVE #WYYYYMMDD-N TO #YYYYMMDD
*
  IF #HOLD-CAP-BILL-DATE NE #YYYYMMDD         /* DW4
    IF #DD NE 1
      REINPUT 'Day must be 01'
        MARK *#SCN-CAP-BILL-DATE
    END-IF
*
    IF #CGA-CLERK = 'KHB' OR = 'SKS' OR = 'ELB' OR = 'ACN'    /* DW5 PM2
        OR = 'LCE' OR = 'RDR' OR = 'VXT'  or = 'OAH'             /* PM2  /* vt1
      IF (#YYYYMMDD LT  #LOW-DATE)
          OR (#YYYYMMDD GT #MANAGER-HIGH-DATE)
        REINPUT 'Must be Current or Within 12 Month"s in future'
          MARK *#SCN-CAP-BILL-DATE
      END-IF
    ELSE
      IF  (#YYYYMMDD LT #LOW-DATE)
          OR (#YYYYMMDD GT #HIGH-DATE)
        REINPUT 'Must be Current or Prior Month'
          MARK *#SCN-CAP-BILL-DATE
      END-IF
    END-IF
  END-IF
END-IF
*
RESET #I2
FOR #I1 1 TO 9
* IF STATUS (#I1) NE 'D'
*   ESCAPE TOP
* END-IF
*
  ADD 1 TO #I2
  IF CA-V.CONV-DURATION (#I1) = 999  /* CAP POLICY
    IF   (#HOLD-LOAN-DATE (#I1) NE 0)
        AND (#SCN-LOAN-DATE-A (#I2) = ' ' OR = '00000000')
        AND NOT (#CGA-CLERK = 'VXT' OR = 'KHB' )           /* PM1 vt 06/19/2013
      REINPUT 'CAP LOAN-DATE/PAYOFF DATE CANNOT BE REMOVED'
        MARK *#SCN-LOAN-DATE (#I2)
    END-IF
*
    IF   (#SCN-LOAN-DATE (#I2) NE 0)
        AND (#SLD-DD (#I2) NE '01')
        AND (X-LOAN-AMOUNT(#I1) NE 0)  /* NON 0 IS OK IF USED FOR PAYOFF DT
      REINPUT 'CAP LOAN DATE MUST DAY 01'
        MARK *#SCN-LOAN-DATE (#I2)
    END-IF
  END-IF
*
  IF   (#SCN-LOAN-DATE (#I2) NE MASK(MMDDYYYY))
      AND (#SCN-LOAN-DATE-A (#I2) NE '00000000')
    REINPUT WITH 'INVALID DATE; FORMAT MUST BE MMDDYYYY'
      MARK *#SCN-LOAN-DATE (#I2)
  END-IF
*
  IF   (#SCN-LOAN-DATE-2 (#I2) NE MASK(MMDDYYYY))
      AND (#SCN-LOAN-DATE-A-2 (#I2) NE '00000000')
    REINPUT WITH 'Invalid date; format must be MMDDYYYY'
      MARK *#SCN-LOAN-DATE-2 (#I2)
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE DO-TRLOG
* * * * * * * * * * * * * *
*
ASSIGN #INDEXT = 1
*
IF #HOLD-CAP-BILL-DATE NE CAP-BILL-DATE         /* DW4
  MOVE TRUE TO #FIELD-CHANGED
  MOVE TRUE TO #WRITE-TRLOG
  MOVE 'CP BIL DT' TO #W-DESCRIPTION
  MOVE #HOLD-CAP-BILL-DATE TO #W-DATA
  MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
  MOVE CAP-BILL-DATE TO #W-DATA
  MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
  ADD 1 TO #INDEXT
END-IF
*
FOR #I1 1 TO 9
* IF STATUS (#I1) NE 'D'
*   ESCAPE TOP
* END-IF
*
  IF #HOLD-CAP-AMT (#I1) NE X-CAP-AMT (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'CAP-AMT' TO #W-DESCRIPTION
    MOVE EDITED #HOLD-CAP-AMT (#I1) (EM=ZZ,ZZ9.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-CAP-AMT (#I1) (EM=ZZ,ZZ9.99)  TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-CAP-AMT-2 (#I1) NE X-CAP-AMT-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'CAP-AM2' TO #W-DESCRIPTION
    MOVE EDITED #HOLD-CAP-AMT-2 (#I1) (EM=ZZ,ZZ9.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-CAP-AMT-2 (#I1) (EM=ZZ,ZZ9.99)  TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LOAN-DATE (#I1) NE X-LOAN-DATE (#I1)
    MOVE '1' TO X-PRINCIPAL-CHG-CODE (#I1) /* SO ACC INT IS USD
*                                    /* for next capitalization
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'LOAN-DT' TO #W-DESCRIPTION
    MOVE #HOLD-LOAN-DATE (#I1) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE X-LOAN-DATE (#I1) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LOAN-DATE-2 (#I1) NE X-LOAN-DATE-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'LN-DAT2' TO #W-DESCRIPTION
    MOVE #HOLD-LOAN-DATE-2 (#I1) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE X-LOAN-DATE-2 (#I1) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-INTEREST-RATE (#I1) NE X-INTEREST-RATE (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'INTR RT' TO #W-DESCRIPTION
    MOVE EDITED #HOLD-INTEREST-RATE (#I1) (EM=Z9.999) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-INTEREST-RATE (#I1) (EM=Z9.999) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-NXT-YR-ACC-INT (#I1) NE X-NXT-YR-ACCUM-INT (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'NXYR-ACIN'   TO #W-DESCRIPTION
    MOVE EDITED #HOLD-NXT-YR-ACC-INT (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA     TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-NXT-YR-ACCUM-INT (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA     TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-NXT-YR-ACC-INT-2 (#I1) NE X-NXT-YR-ACCUM-INT-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    MOVE 'NXYR-ACIN'   TO #W-DESCRIPTION
    MOVE EDITED #HOLD-NXT-YR-ACC-INT-2 (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA     TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-NXT-YR-ACCUM-INT-2 (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA     TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PMT-RCVD (#I1) NE X-LPO-PMT-RCVD (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-PMT' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PMT-RCVD (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PMT-RCVD (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PMT-RCVD-2 (#I1) NE X-LPO-PMT-RCVD-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-PMT-2' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PMT-RCVD-2 (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PMT-RCVD-2 (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PRIOR-LOAN-BAL (#I1) NE X-LPO-PRIOR-LOAN-BAL (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-LON' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PRIOR-LOAN-BAL (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PRIOR-LOAN-BAL (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PRIOR-LOAN-BAL-2 (#I1) NE X-LPO-PRIOR-LOAN-BAL-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-LON-2' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PRIOR-LOAN-BAL-2(#I1)(EM=ZZZ,ZZZ.99)
      TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PRIOR-LOAN-BAL-2 (#I1)(EM=ZZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PRIOR-ACC-INT (#I1) NE X-LPO-PRIOR-ACC-INT (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-INT' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PRIOR-ACC-INT (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PRIOR-ACC-INT (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
*
  IF #HOLD-LPO-PRIOR-ACC-INT-2 (#I1) NE X-LPO-PRIOR-ACC-INT-2 (#I1)
    MOVE TRUE TO #FIELD-CHANGED
    MOVE TRUE TO #WRITE-TRLOG
    COMPRESS 'LPO-INT-2' INTO #W-DESCRIPTION LEAVING NO
    MOVE EDITED #HOLD-LPO-PRIOR-ACC-INT-2(#I1)(EM=ZZ,ZZZ.99) TO #W-DATA
    COMPRESS #W-DATA 'Pol Issued' ISSUE-DATE (#I1) INTO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
    MOVE EDITED X-LPO-PRIOR-ACC-INT-2 (#I1) (EM=ZZ,ZZZ.99) TO #W-DATA
    MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
    ADD 1 TO #INDEXT
  END-IF
END-FOR
*
IF #HOLD-APL-CNT NE APL-CNT
  MOVE TRUE TO #FIELD-CHANGED
  MOVE TRUE TO #WRITE-TRLOG
  MOVE 'APL-CNT' TO #W-DESCRIPTION
  MOVE #HOLD-APL-CNT TO #W-DATA
  MOVE #W-OLD-NEW-DATA TO TRLOG.OLD-DATA (#INDEXT)
  MOVE APL-CNT TO #W-DATA
  MOVE #W-OLD-NEW-DATA TO TRLOG.NEW-DATA (#INDEXT)
  ADD 1 TO #INDEXT
END-IF
*
IF #WRITE-TRLOG
  MOVE CA-V.ID-NUMBER TO TRLOG.ID-NUMBER
  MOVE CA-V.PROCESS-IND TO TRLOG.PROCESS-IND /* PM - 06/2000
  MOVE NAME TO TRLOG.NAME
  MOVE DATE-OTHER-UPDATE TO TRLOG.DATE-LAST-UPDATE
  MOVE #CGA-CLERK TO TRLOG.CLERK-ID
  MOVE *INIT-ID TO TRLOG.TERMINAL-ID
  MOVE *DATN TO TRLOG.TRANSACTION-DATN
  MOVE *TIMN TO TRLOG.TRANSACTION-TIMN
* below removed 7/96 as per lcc (ca should never update this date)
* IF #UPDATE-DATE = TRUE
*   MOVE *DATN TO DATE-OTHER-UPDATE
* END-IF
END-IF
*
END-SUBROUTINE
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-RECORD
* * * * * * * * * * * * * * * *
*
IF #SCN-CAP-BILL-DATE = MASK (MMDDYYYY)                          /* DW4
  MOVE #SCN-CAP-BILL-DATE TO #WMMDDYYYY-A
  MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
  MOVE #WYYYYMMDD-N TO CAP-BILL-DATE
ELSE
  RESET CAP-BILL-DATE
END-IF
*
RESET #I2
FOR #I1 1 TO 9
* IF STATUS (#I1) NE 'D'
*   ESCAPE TOP
* END-IF
*
  ADD 1 TO #I2
  IF #SCR-FIX-RT (#I2) NE 0
    MOVE #SCR-FIX-RT (#I2) TO X-INTEREST-RATE (#I1)
  END-IF
*
  IF #SCN-LOAN-DATE (#I2) NE 0
    MOVE #SCN-LOAN-DATE-A (#I2) TO #WMMDDYYYY-A
    MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
    MOVE #WYYYYMMDD-N TO X-LOAN-DATE (#I1)
  ELSE
    RESET X-LOAN-DATE (#I1)
  END-IF
*
  IF #SCN-LOAN-DATE-2 (#I2) NE 0
    MOVE #SCN-LOAN-DATE-A-2 (#I2) TO #WMMDDYYYY-A
    MOVE BY NAME #WMMDDYYYY TO #WYYYYMMDD
    MOVE #WYYYYMMDD-N TO X-LOAN-DATE-2 (#I1)
  ELSE
    RESET X-LOAN-DATE-2 (#I1)
  END-IF
END-FOR
*
END-SUBROUTINE
*
***********************************
DEFINE SUBROUTINE CHECK-LOAN-STATUS   /* TT1
***********************************
DECIDE ON FIRST VALUE OF LOAN-STATUS
  VALUE 'P'
    #CURRENT-LOAN-STATUS(I) := 'Pending'
  VALUE 'D'
    #CURRENT-LOAN-STATUS(I) := 'Defaulted'
  VALUE 'C'
    #CURRENT-LOAN-STATUS(I) := 'Current'
  VALUE 'Q'
    #CURRENT-LOAN-STATUS(I) := 'Paid off'
  VALUE 'L'                                         /* vt2 \/
    #CURRENT-LOAN-STATUS(I) := 'Lapsed'
    value 'R'
    #current-loan-status(I) := 'Reinstated'         /* vt2 /\
  NONE VALUE
    IGNORE
END-DECIDE
**************
END-SUBROUTINE
**************
















END
