* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1008SAT   ** DISPLAY DUPLICATE SSN's
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* DRW  10/16/2007  DW1     Stop bypassing Contact records              *
*                                                                      *
* PAM  05/26/2011  PM1     Remove contact info from AT - CRM project   *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-ORIG-COMMAND(A2)
1 #AT-CONTACT-ID   (N8)     /* PM1
*
LOCAL USING OBJLCONT        /* PM1
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER
 2 INTRO-FLAG
 2 DATE-RECEIVED
 2 AT-STATUS
 2 PLAN
 2 FACE-AMT-UNITS
 2 FACE-AMT-DOLS
 2 CONTACT-ID               /* PM6
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PROCESS-IND
 2 INTRO-FLAG
 2 MEMBER-CONTACT-ID        /* PM6
 2 ISSUE-DATE (1)
 2 PLAN     (1)
 2 FACE-AMOUNT  (1)
*
1 #I1  (I2)
1 #I2  (I2)
1 #I3  (I2)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY    (A4)
 2 #MM      (A2)
 2 #DD      (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A  (A8)
*
1 #MMDDYYYY   (N8)
1 REDEFINE #MMDDYYYY
 2 #MM-X    (A2)
 2 #DD-X    (A2)
 2 #YYYY-X  (A4)
1 REDEFINE #MMDDYYYY
 2 #MMDDYYYY-A  (A8)
*
1 #NEW-LBL   (A50)
1 #NUM-FNDS  (N3)
1 #SEL-NEW-CV(C)
1 #SEL-NEW   (A1)
1 #SEL       (A1/18)
1 #SEL-CV    (C/18)
1 #CN        (N6/18)
1 #ISN       (P8/18)
1 #AT-STATUS (A2/18)
1 #NAME      (A25/18)
1 #DOB-A     (A8/18)
1 REDEFINE #DOB-A
 2 #DOB      (N8/18)
1 #DATE-RECEIVED-A (A8/18)
1 REDEFINE #DATE-RECEIVED-A
 2 #DATE-RECEIVED (N8/18)
1 #PLAN      (A3/18)
1 #FACE      (N7.2/18)                              /* NG1
*
1 #SSN       (N9)                                   /* PM1
1 #NAME-DISP (A25)                                  /* PM1
1 #CT-DOB    (N8)                                   /* PM1
*
END-DEFINE
*
* IF #AT-SSN LT 700000 /* this must not be an SSN   /* PM1
IF #AT-CONTACT-ID = 0                               /* PM1
  ESCAPE ROUTINE
END-IF
*
FIND (1) CT-V WITH CT-V.CONTACT-ID = #AT-CONTACT-ID                                                   /* PM1
  #NAME-DISP := FUNCTION-DISP-CT-NAME(<CT-V.FIRST-NAME,CT-V.MIDDLE-NAME,CT-V.LAST-NAME,CT-V.SUFFIX>)  /* PM1
  #CT-DOB    := CT-V.DATE-OF-BIRTH                                                                    /* PM1
END-FIND                                                                                              /* PM1
*
RESET #NUM-FNDS
MOVE (AD=P) TO #SEL-CV (*)
* FIND AT-V WITH SSN = #AT-SSN
FIND AT-V WITH AT-V.CONTACT-ID = #AT-CONTACT-ID     /* PM1
  IF  (AT-V.INTRO-FLAG = 'V')
   OR (AT-V.AT-STATUS = 'A')
   OR (#I1 GE 18)
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #I1
  RESET #SEL-CV (#I1)
  MOVE AT-V.ID-NUMBER     TO #CN     (#I1)
  MOVE AT-V.DATE-RECEIVED TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #MMDDYYYY-A LEAVING NO
  MOVE #MMDDYYYY-A TO #DATE-RECEIVED-A (#I1)
  MOVE AT-V.AT-STATUS     TO #AT-STATUS (#I1)
  #NAME (#I1) := #NAME-DISP                 /* PM1
  MOVE #CT-DOB TO #YYYYMMDD                 /* PM1
* MOVE AT-V.NAME          TO #NAME   (#I1)  /* PM1
* MOVE AT-V.DATE-OF-BIRTH TO #YYYYMMDD      /* PM1
  COMPRESS #MM #DD #YYYY INTO #MMDDYYYY-A LEAVING NO
  MOVE #MMDDYYYY-A TO #DOB-A  (#I1)
  MOVE AT-V.PLAN          TO #PLAN   (#I1)
  COMPUTE #FACE (#I1) = (AT-V.FACE-AMT-UNITS * 1000) + FACE-AMT-DOLS
*
  ADD 1 TO #NUM-FNDS
END-FIND
*
* FIND ST-V WITH SSN = #AT-SSN                             /* PM1
FIND ST-V WITH ST-V.MEMBER-CONTACT-ID = #AT-CONTACT-ID     /* PM1
* IF  (ST-V.PROCESS-IND = 'S' OR = 'C')    /* DW1
  IF (#I1 GE 18)
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #I1

  RESET #SEL-CV (#I1)
  MOVE ST-V.ID-NUMBER      TO #CN     (#I1)
  MOVE ST-V.ISSUE-DATE (1) TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #MMDDYYYY-A LEAVING NO
  MOVE #MMDDYYYY-A TO #DATE-RECEIVED-A (#I1)
  MOVE ST-V.INTRO-FLAG     TO #AT-STATUS (#I1)
  #NAME (#I1)  := #NAME-DISP                 /* PM1
  MOVE #CT-DOB TO #YYYYMMDD                  /* PM1
* MOVE ST-V.NAME           TO #NAME   (#I1)  /* PM1
* MOVE ST-V.DATE-OF-BIRTH  TO #YYYYMMDD      /* PM1
  COMPRESS #MM #DD #YYYY INTO #MMDDYYYY-A LEAVING NO
  MOVE #MMDDYYYY-A TO #DOB-A (#I1)
  MOVE ST-V.PLAN (1)       TO #PLAN   (#I1)
  MOVE FACE-AMOUNT (1)     TO #FACE (#I1)
*
  ADD 1 TO #NUM-FNDS
END-FIND
*
IF #NUM-FNDS = 0
  ESCAPE ROUTINE
END-IF
*
INPUT TEXT 'Press ENTER to continue' USING MAP 'C1008MAT'
*
IF #CGA-SCR-COMMAND NE '*'
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
*
END
