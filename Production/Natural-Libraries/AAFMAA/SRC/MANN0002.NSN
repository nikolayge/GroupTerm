* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram MANN0002.
/**
/** :author nguentchev
/*
/*    13th Check Amount = (DRATE * Residual - Expense Charge) * YearFraction
/*    DRATE (aka Dividend Rate) = Crediting Rate - Settlement Rate
/*    Settlement Rate is either 4% or 3.5% (depending on the Issue Date of the policy)
/*    Year-Fraction comes into play only if the annuity begins or ends in the current year
/*    In such cases, the annuitant would receive the 13th check only for the portion of the year the annuity was active
/*    In all cases, the 13th Check should be calculated on the Residual amount at the end of the year
/*    This is to prevent overpayment to the Interest Only annuitant in case he/she has exercised Partial Withdrawals
/*
DEFINE DATA
PARAMETER
1 #RESERVE     (N7.2) BY VALUE
1 #ANNU-DATE   (N8)   BY VALUE
1 REDEFINE #ANNU-DATE
  2 YYYY  (A4)
1 REDEFINE #ANNU-DATE
  2 #ANNU-DATE-A (A8)
1 #ISSUE-DATE  (N8)
1 #13TH-CHECK  (N7.2)
LOCAL
1 #KEY  (A15) INIT  <'RATE7AO'>
1 REDEFINE #KEY
  2 #GEN-KEY     (A4)
  2 #GEN-SUB-KEY (A3)
  2 #YYYY        (N4)
  2 #MMDD        (A4)
1 BLANK (A1) CONST <' '>
1 CURR-RT  (N7.4)
1 SETTL-RT (N7.4)
1 #D1       (D)
1 #D2       (D)
1 #TODAY    (N8)
1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
1 #JDate              (A5) 1 REDEFINE #JDATE
  2 YY       (A2)
  2 #DDD     (N3)
1 #PR        (N1.5)
1 #expense-charge (N7.2)
END-DEFINE
/*
IF #ANNU-DATE.YYYY = BLANK
  RESET #13TH-CHECK
  ESCAPE ROUTINE
END-IF
*
CURR-RT := FUNCTION-INT-RATE(<'RATE1AG'>) 
CURR-RT := CURR-RT / 100
IF #ISSUE-DATE > 20150101
  SETTL-RT := 0.035
ELSE
  SETTL-RT := 0.04
END-IF
*
#TODAY := *DATN
IF #ANNU-DATE = 0
  #ANNU-DATE := 20141201
ELSE
  MOVE EDITED #ANNU-DATE-A TO #D1 (EM=YYYYMMDD)
END-IF
IF #ANNU-DATE.YYYY = #TODAY.#YYYY-A
  #D2  := *DATX
  #DDD := #D2 - #D1
ELSE
*  #JDATE := *DATJ
  #DDD := 365
END-IF
#PR := #DDD / 365
#KEY.#YYYY := #TODAY.#YYYY
#expense-charge := GET-EXPENSE-CHARGE (< #KEY >)
#13TH-CHECK :=
( #RESERVE * ( CURR-RT - SETTL-RT) - GET-EXPENSE-CHARGE (< #KEY >) ) * #PR
*
IF #13TH-CHECK < 0
  RESET #13TH-CHECK
END-IF
END
