* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDETT - DEATH LISTING FOR AUDITORS
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    02252005  PM1     Made adjustments for 2005.                  *
* SAG   12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* DRW 02/16/2008 DW1 2Add Additional Info to outut File
* DRW 03/06/2008 DW2 Add More info to file
* DRW 01/03/2009 DW3 Increase size of #RECS-READ
* TMT 06/10/2014 TT1 CONTACT NORMALIZATION - GENDER AND DOB
************************************************************************
*
DEFINE DATA
LOCAL
1 VA-V VIEW OF A-VA-HISTORY
  2 RECORD-KEY           (N15)
  2 REDEFINE RECORD-KEY
    3 ID-NUMBER           (N6)
    3 REDEFINE ID-NUMBER
      4 ID-NUMBER-A        (A6)
    3 ISS-DATE            (N8)
    3 SPLIT-IND           (N1)
  2 issue-plan            (a3)                  /* DW1
  2 TERMINATION-DATE     (P7)
  2 TERMINATION-CODE     (A3)
  2 DEATH-BENEFIT-M      (P7.2/1)
  2 CASH-VALUE-M         (P7.2/13)                                 /* PM1
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER            (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A        (A6)
  2 NAME
  2 MEMBER-CONTACT-ID
*  2 date-of-birth                                /* TT1
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 DOD-CENT       (N2)
    3 DOD-YY         (N2)
    3 DOD-MM         (N2)
    3 DOD-DD         (N2)
  2 REDEFINE DATE-OF-DEATH
    3 DOD-YYYY       (N4)
    3 DOD-MMDD       (N4)
  2 DATE-DEATH-NOTIFY
  2 issue-date       (9)
  2 plan             (9)
*
1 FR-V VIEW OF A-FIRS
  2 POL-NAME     (A19/45)
  2 POL-AMT-PAID (P7.2/45)
  2 POL-DATE     (N8/45)
  2 POL-AMOUNT     (45)     /* DW1
  2 pol-type       (45)     /* DW1
  2 cause-of-death          /* DW2
*
1 #JCL-PARM       (A17)
1 REDEFINE #JCL-PARM
  2 #JCL-FIL-1        (A13)    /* 'PROCESS YEAR='
  2 #JCL-PROCESS-YYYY (N4)     /* '1988'
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* DW3
  2 #RECS-WRITTEN     (P5)
  2 #LAST-ID          (N6)
  2 #CNT              (P5)
  2 #cnt-3            (n5)
  2 #I1               (I2)
  2 #I2               (I2)
*
1 #TERM-DT               (N6)
1 REDEFINE #TERM-DT
  2 #TERM-DT-YYYY         (N4)
  2 #TERM-DT-MM           (N2)
*
1 #DATE-OF-DEATH         (N8)
1 REDEFINE #DATE-OF-DEATH
  2 #DEATH-DT-YYYY         (N4)
  2 #DEATH-DT-MM           (N2)
  2 #DEATH-DT-DD           (N2)
*
1 #HIST-KEY-YEAR-MONTH-P (P7)
1 #YEAR-MONTH-M          (N6)
1 REDEFINE #YEAR-MONTH-M
  2 #HIST-YYYY            (N4)
  2 #HIST-MM              (N2)
*
1 #START-KEY         (N15) INIT <000000000000000>
1 REDEFINE #START-KEY
  2 #START-ID-NUMBER  (N6)
  2 #START-ISSUE-DT   (N8)
  2 #START-SPLIT-IND  (N1)
*
1 #END-KEY         (N15) INIT <999999999999999>
1 REDEFINE #END-KEY
  2 #END-ID-NUMBER  (N6)
  2 #END-ISSUE-DT   (N8)
  2 #END-SPLIT-IND  (N1)
*
1 #VA-REC-FOUND       (L)
1 #STAT-NOT-FND       (L)
*
1 #DOD-YYYYMMDD-G
  2 #DOD-YYYYMM     (N6)
  2 REDEFINE #DOD-YYYYMM
    3 #DOD-YYYY         (N4)
    3 #DOD-MM           (N2)
  2 #DOD-DD           (N2)
1 REDEFINE #DOD-YYYYMMDD-G
  2 #DOD-YYYYMMDD     (N8)
*
1 #NO-FIRS    (L)
1 #TOT-DB     (P13.2)
*
1 #EXTRACT-RECORD (A180)                                          /* PM1
1 #TAB       (A1) INIT <H'09'>
1 #DISP-AMOUNT-PAID (A15)                                         /* PM1
1 #disp-face-amount (a15)                          /* DW1
1 #CSV              (A15)                                         /* PM1
1 #age-at-death     (n4)       /* DW2
1 #yyyymmdd     (n8)
1 redefine #yyyymmdd
  2 #yyyy     (n4)
  2 #mm       (n2)
  2 #dd       (n2)
1 #pol-num   (a16)
1 DATE-OF-BIRTH        (N8.0)                     /* TT1
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM
write 'JCl-PARM=' #jcl-parm
WRITE 'Year =' #JCL-PROCESS-YYYY
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Death Information for' #JCL-PROCESS-YYYY
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
* COMPRESS 'ID Number' #TAB VA-V.ID-NUMBER NAME                  /* PM1
COMPRESS 'CN Number' #TAB                                        /* DW1
  'Iss Date'          #tab                                       /* DW1
  'Name'              #TAB
  'Date of Death' #TAB
  'Date Pols Termed' #TAB
  'Date Notified' #TAB
  'Death Benefit' #TAB
  'Face Amount'   #tab                       /* DW1
  'Cash Surrender Value' #tab
  'Plan'          #tab                     /* DW2
  'pol-Num'       #tab
  'Cause-of-Death' #tab
  'Age-At-Death'
  INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ VA-V BY RECORD-KEY
* IF #RECS-READ GT 100
*   ESCAPE BOTTOM
* END-IF
*
  ADD +1 TO #RECS-READ
  MOVE VA-V.TERMINATION-DATE TO #TERM-DT
*
  IF  (#TERM-DT-YYYY NE #JCL-PROCESS-YYYY)  /* DIED IN DIFF PROCESS YEAR
*  OR (VA-V.ID-NUMBER = #LAST-ID)      /* 2ND POLICY OF A DEAD MBR
    ESCAPE TOP
  END-IF
*
  IF VA-V.TERMINATION-CODE NE 'DTH'   /* IF DEAD AS OF Y/E
    ESCAPE TOP
  END-IF
*
  MOVE VA-V.ID-NUMBER TO #LAST-ID
  RESET #STAT-NOT-FND
  FIND (1) ST-V WITH WIDOW-KEY = VA-V.ID-NUMBER-A
    IF NO RECORDS FOUND
      WRITE 'STAT REC NOT FOUND FOR ' VA-V.ID-NUMBER
        ' Issue date = ' ISS-DATE
        ' Term code  = ' TERMINATION-CODE
        ' Term date  = ' TERMINATION-DATE
        ' Death Bene = ' DEATH-BENEFIT-M (1)
      MOVE TRUE TO #STAT-NOT-FND
    END-NOREC
  END-FIND
*
  IF #STAT-NOT-FND
    ESCAPE TOP
  END-IF
*
  MOVE ST-V.DATE-OF-DEATH TO #DATE-OF-DEATH                       /* PM1
*  IF (#DEATH-DT-YYYY GE #JCL-PROCESS-YYYY)                        /* PM1
*    ESCAPE TOP                                                    /* PM1
*  END-IF                                                          /* PM1
*
  MOVE EDITED CASH-VALUE-M (13)  (EM=Z,ZZZ,ZZZ.99) TO #CSV        /* PM1
  move left #csv to #csv                        /* DW1
*
  for #i1 1 to 9                                     /* DW2
    if   (st-v.issue-date (#i1) = va-v.iss-date)
        and (st-v.plan (#i1) = va-v.issue-plan)
      CALLNAT 'GPLNMSXX' #pol-num st-v.ID-NUMBER #I1 /* build POL-#
      escape bottom
    end-if
  end-for
*  
  RESET DATE-OF-BIRTH                                                   /* TT1
  DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)          /* TT1
*  
END-ALL
*
SORT BY    ST-V.DATE-OF-DEATH
    USING VA-V.ID-NUMBER
    ST-V.NAME
    VA-V.TERMINATION-DATE
    ST-V.DATE-DEATH-NOTIFY
    VA-V.ISS-DATE
    #CSV
    issue-plan                  /* DW2
    date-of-birth                                                         /* TT1
    cause-of-death
    #pol-num
*
  RESET #NO-FIRS
  FIND (1) FR-V WITH WIDOW-KEY = VA-V.ID-NUMBER-A
    IF NO RECORDS FOUND
      WRITE (3) 'Firs record not found for' VA-V.ID-NUMBER
      MOVE TRUE TO #NO-FIRS
    END-NOREC
*
    IF NOT #NO-FIRS
      FOR #I1 1 TO 9
        IF POL-NAME (#I1) = 'AMAA' OR = 'AAFMAA'
            AND POL-DATE (#I1) = VA-V.ISS-DATE                       /* PM1
            and (pol-type (#i1) = va-v.issue-plan)                  /* DW1
          ADD POL-AMT-PAID (#I1) TO #TOT-DB
          add 1 to #cnt-3
          DISPLAY (3) #cNt-3 'ID-NUMBER' VA-V.ID-NUMBER
            'NAME'  ST-V.NAME
            'DOD' st-v.DATE-OF-DEATH
            'TERM DATE' VA-V.TERMINATION-DATE
            'NOTIFY' ST-V.DATE-DEATH-NOTIFY
            'DEATH/BENEFIT' POL-AMT-PAID (#I1) (EM=Z,ZZZ,ZZZ.99)
            'CSV' #CSV                                    /* PM1
*
          MOVE EDITED POL-AMT-PAID (#I1) (EM=Z,ZZZ,ZZZ.99)
            TO #DISP-AMOUNT-PAID
          move left #DISP-AMOUNT-PAID to #DISP-AMOUNT-PAID
          MOVE EDITED POL-AMount (#I1) (EM=Z,ZZZ,ZZZ)            /* DW1
            TO #DISP-face-amount
          move left #DISP-face-amount to #DISP-face-amount
*
          move st-v.date-of-death to #yyyymmdd
          subtract date-of-birth from #yyyymmdd               /* TT1
          move #yyyy to #age-at-death
          COMPRESS VA-V.ID-NUMBER #TAB
            va-v.iss-date  #tab                        /* DW1
            ST-V.NAME #TAB
            ST-V.DATE-OF-DEATH #TAB
            VA-V.TERMINATION-DATE #TAB
            ST-V.DATE-DEATH-NOTIFY #TAB
            #DISP-AMOUNT-PAID #TAB
            #disp-face-amount #TAB                     /* DW1
            #CSV              #tab                     /* PM1
            pol-type (#i1)    #tab                /* DW2
            #pol-num          #tab                /* DW2
            cause-of-death    #tab                /* DW2
            #age-at-death                         /* DW2
            INTO #EXTRACT-RECORD LEAVING NO
          WRITE WORK 1 #EXTRACT-RECORD
          RESET #EXTRACT-RECORD
        END-IF
      END-FOR
    END-IF
  END-FIND
END-SORT
WRITE (3) 'TOT DEATH BENEFIT = ' #TOT-DB (EM=ZZZ,ZZZ,ZZZ,ZZZ.99)
*
END
