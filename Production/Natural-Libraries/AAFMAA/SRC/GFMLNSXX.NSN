* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GFMLNSXX   Process First Middle Last name
*
DEFINE DATA PARAMETER
1 #NAME      (A25)
1 #FIRST     (A20)
1 #MI        (A1)
1 #LAST      (A25)
1 #ERR-MSG   (A60)
*
LOCAL
*
1 #NAME-TEST  (A50)
1 REDEFINE #NAME-TEST
 2 #NAME-25    (A25)
 2 #NAME-REMAIN  (A25)
*
1 #FIRST-MID    (A25)
1 #LAST-WORK    (A25)
1 #SUFFIX       (A25)
1 #EXTRA1       (A25)
1 #EXTRA2       (A25)
1 #EXTRA3       (A25)
1 #EXTRA4       (A25)
1 #EXTRA6       (A25)
1 #EXTRA7       (A25)
1 #EXTRA8       (A25)
1 #EXTRA9       (A25)
*
1 #MI-TEST      (A25)
1 REDEFINE #MI-TEST
 2 #MI-T1    (A1)
 2 #MI-REST  (A24)
 2 REDEFINE #MI-REST
  3 #MI-PER   (A1)
  3 #MI-PER-REST   (A23)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
RESET #ERR-MSG
*
* DISPLAY #NAME #LAST #FIRST #MI
IF (#LAST = ' ') AND (#FIRST = ' ') AND (#MI = ' ') AND (#NAME = ' ')
  MOVE 'All name fields are blank' TO #ERR-MSG
  ESCAPE ROUTINE
END-IF
*
IF #NAME = ' '
* WRITE 'Build name from first/mi/last'
* WRITE 'F=' #FIRST 'mi=' #MI 'L=' #LAST
  IF (#FIRST NE ' ') OR (#MI NE ' ')
    SEPARATE #LAST LEFT INTO #LAST-WORK #SUFFIX #EXTRA1 #EXTRA2
                         WITH DELIMITER ','
*
    IF #EXTRA2 NE ' '
      COMPRESS #LAST-WORK #SUFFIX #EXTRA1 INTO #NAME-TEST
      MOVE #EXTRA2 TO #SUFFIX
    ELSE
      IF #EXTRA1 NE ' '
        COMPRESS #LAST-WORK #SUFFIX INTO #NAME-TEST
        MOVE #EXTRA1 TO #SUFFIX
      ELSE
        IF #SUFFIX NE ' '
          MOVE #LAST-WORK TO #NAME-TEST
          MOVE #SUFFIX TO #SUFFIX
        ELSE
          MOVE #LAST-WORK TO #NAME-TEST
        END-IF
      END-IF
    END-IF
*
    COMPRESS #NAME-TEST ',' INTO #NAME-TEST LEAVING NO
    COMPRESS #NAME-TEST #FIRST #MI INTO #NAME-TEST
  ELSE
    MOVE #LAST-WORK TO #NAME-TEST
  END-IF
*
  IF #SUFFIX NE ' '
    COMPRESS #NAME-TEST ',' INTO #NAME-TEST LEAVING NO
    COMPRESS #NAME-TEST #SUFFIX INTO #NAME-TEST LEAVING NO
  END-IF
*
  IF #NAME-REMAIN NE ' '
    MOVE 'Name exceeds 25 characters' TO #ERR-MSG
  ELSE
    MOVE #NAME-TEST TO #NAME
  END-IF
* WRITE 'name=' #NAME
ELSE
* WRITE 'Build first/mi/last from name'
* WRITE 'name=' #NAME
  RESET #LAST #FIRST #MI
  SEPARATE #NAME LEFT INTO #LAST #FIRST-MID #SUFFIX
       #EXTRA1 #EXTRA2 #EXTRA3 #EXTRA4    WITH DELIMITER ','
  SEPARATE #FIRST-MID LEFT INTO  #FIRST #MI-TEST
      #EXTRA6 #EXTRA7 #EXTRA8 #EXTRA9      WITH DELIMITER ' '
* WRITE 'after sep' #FIRST
*
  IF (#MI-REST = ' ')
    OR ((#MI-PER = '.') AND (#MI-PER-REST = ' ')
   AND  (#EXTRA6 = ' ') AND (#EXTRA7 = ' ') AND (#EXTRA8 = ' ')
   AND  (#EXTRA9 = ' '))
    MOVE #MI-T1 TO #MI
  ELSE
    COMPRESS #FIRST #MI-TEST #EXTRA6 #EXTRA7 #EXTRA8 #EXTRA9
                INTO #FIRST
  END-IF
*
  IF (#SUFFIX NE ' ') OR (#EXTRA1 NE ' ') OR (#EXTRA2 NE ' ')
                      OR (#EXTRA3 NE ' ')
     COMPRESS #LAST ',' INTO #LAST LEAVING NO
     COMPRESS #LAST #SUFFIX INTO #LAST LEAVING NO  /* added 1/13/2003
     COMPRESS #LAST #EXTRA1 #EXTRA2 #EXTRA3 #EXTRA4
                                 INTO #LAST
  END-IF
* WRITE 'F=' #FIRST 'mi=' #MI 'L=' #LAST
END-IF
*
END
