* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPNTDL - DELETE AGED NOTES
*
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  09062006 DW1  Increase size of #recs-read
* DRW 03/09/2009 DW2 Increase Notes occurance from 60 to 500  
* RSE 10/12/2012 RE1 CN Expansion Project - criterion changed
*******************************************
DEFINE DATA
LOCAL
*
* * * *   N O T E S     F I L E   * * * * * * * * * * * * * * *
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER       (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A    (A6)
 2 C*NOTE-TABLE
 2 NOTE-DATE       (N8/500)    /* DW2
 2 NOTE-CLERK-ID   (A3/500)    /* DW2
 2 NOTE-LINE-NUM   (P1/500)    /* DW2
 2 NOTE-TEXT       (A60/500)   /* DW2
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 NOTE-REC-IND    (A1)
 2 DATE-RETIRED    (N8)
*
1 #CNT-1               (P7)
1 #CNT-2               (P7)
1 #CNT-3               (P7)
1 #CNT-4               (P7)
1 #CNT-5               (P7)
1 #CNT-6               (P7)
1 #ISN                 (P8)
1 #CLEAR               (L)
1 #NO-ST-REC           (L)
1 #FND-FINAL-BENE      (L)
1 #CNT-LINE-NUMS       (I2)   /* PM ADDED 04/2000 TO CLEAR LINE ONLY
1 #CNT-TERMS           (I2)   /* PM ADDED 04/2000 TO COUNT TERMINATIONS
1 #NOTE-TEXT       (A60)
1 REDEFINE #NOTE-TEXT
 2 #NOTE-TEXT-14   (A14)
 2 #NOTE-TEXT-8A   (A8)
1 REDEFINE #NOTE-TEXT
 2 #NOTE-TEXT-17   (A17)
 2 #NOTE-TEXT-8B   (A8)
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* DW1
  2 #NUM-NOTES        (P7)   /* RE1 formerly P5  
  2 #DELETED-NOTES    (P7)   /* RE1 formerly P5 
  2 #DELETED-RECS     (P7)   /* RE1 formerly P5 
  2 #CNT              (P7)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #I4               (I2)
  2 #NT-UPDATE        (L)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT          (A2)
  2 #YY            (A2)
  2 #MM            (A2)
  2 #DD            (A2)
*
1 #DATN-1-YEAR     (N8)
1 #DATN-2-YEAR     (N8)
*
1 #DATN-6-MON      (N8)
1 REDEFINE #DATN-6-MON
 2 #DT-6-MO-YYYY   (N4)
 2 #DT-6-MO-MM     (N2)
 2 #DT-6-MO-DD     (N2)
*
1 #DATN-3-MON      (N8)
1 REDEFINE #DATN-3-MON
 2 #DT-3-MO-YYYY   (N4)
 2 #DT-3-MO-MM     (N2)
 2 #DT-3-MO-DD     (N2)
*
1 #DATN-1-MON      (N8)
1 REDEFINE #DATN-1-MON
 2 #DT-1-MO-YYYY   (N4)
 2 #DT-1-MO-MM     (N2)
 2 #DT-1-MO-DD     (N2)
*
END-DEFINE
*
MOVE *DATN TO #DATN-1-YEAR
SUBTRACT 10000 FROM #DATN-1-YEAR
*
MOVE *DATN TO #DATN-2-YEAR
SUBTRACT 20000 FROM #DATN-2-YEAR
*
MOVE *DATN TO #DATN-6-MON
SUBTRACT 6 FROM #DT-6-MO-MM
IF #DT-6-MO-MM LT 1
  ADD 12 TO #DT-6-MO-MM
  SUBTRACT 1 FROM #DT-6-MO-YYYY
END-IF
*
MOVE *DATN TO #DATN-3-MON
SUBTRACT 3 FROM #DT-3-MO-MM
IF #DT-3-MO-MM LT 1
  ADD 12 TO #DT-3-MO-MM
  SUBTRACT 1 FROM #DT-3-MO-YYYY
END-IF
*
MOVE *DATN TO #DATN-1-MON
SUBTRACT 1 FROM #DT-1-MO-MM
IF #DT-1-MO-MM LT 1
  ADD 12 TO #DT-1-MO-MM
  SUBTRACT 1 FROM #DT-1-MO-YYYY
END-IF
*
FORMAT     LS=133 PS=76
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'NOTES FILE CONTENTS'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'ACCOUNTING NOTES DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'MBR-SVC NOTES DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=133 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'BLANK NOTES DEL W/ LINE NUMBERS'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (6) LS=133 PS=76
WRITE  (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'ALL NOTES DELETED (FICHE)'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (7) LS=133 PS=76
WRITE  (7) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      '1999 TERMINATION NOTES DELETED'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ NT-V BY ID-NUMBER
*  IF ID-NUMBER GE 200000       /* STOP after member notes      /* RE1
  IF ID-NUMBER GE 500000       /* STOP after member notes       /* RE1
    ESCAPE BOTTOM
  END-IF
*
  MOVE C*NOTE-TABLE TO #NUM-NOTES
  ADD 1 TO #RECS-READ
  RESET #NT-UPDATE
*
  RESET #FND-FINAL-BENE
  FOR #I1 1 TO C*NOTE-TABLE
    RESET #CLEAR
*
* PM ADDED 04/2000 TO CLEAR LINE-NUMBERS FORM BLANK NOTES
*
    IF NOTE-TEXT (#I1) = ' '
     AND NOTE-DATE (#I1) = 0
     AND NOTE-CLERK-ID (#I1) = ' '
     AND NOTE-LINE-NUM (#I1) NE 0
       DISPLAY (5) ID-NUMBER NOTE-LINE-NUM (#I1)
       RESET NOTE-LINE-NUM (#I1)
       MOVE TRUE TO #NT-UPDATE
       ADD +1 TO #CNT-LINE-NUMS
       ESCAPE TOP
    END-IF
*
    IF NOTE-TEXT (#I1) = ' '
      MOVE TRUE TO #NT-UPDATE
      ESCAPE TOP
    END-IF
*
    IF   (NOTE-TEXT (#I1) = MASK ('B/C attached'))
     OR  (NOTE-TEXT (#I1) = MASK ('B/C END sent'))
      MOVE TRUE TO #FND-FINAL-BENE /* above will preceed "Sent B/C"
    END-IF
*
* below removed 8/30/2002 as per bmg
*   IF (NOTE-CLERK-ID (#I1) = 'KHB' OR = 'ELB' OR = 'SKS' OR = 'SLL'
*      OR = 'ALB')
*    AND  (NOTE-TEXT (#I1) NE MASK ('*'))
*    AND  (NOTE-TEXT (#I1) NE MASK ('Formerly:'))
*    AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR)       /* OVER 1 YEAR OLD
*     ADD 1 TO #CNT-3
*     DISPLAY (3) #CNT-3 ID-NUMBER NOTE-DATE(#I1) NOTE-CLERK-ID(#I1)
*                                              NOTE-TEXT (#I1)
*     DISPLAY (6) #CNT-3 ID-NUMBER NOTE-DATE(#I1) NOTE-CLERK-ID(#I1)
*                                              NOTE-TEXT (#I1)
*     RESET NOTE-TEXT     (#I1)
*           NOTE-DATE     (#I1)
*           NOTE-CLERK-ID (#I1)
*           NOTE-LINE-NUM (#I1)
*     MOVE TRUE TO #CLEAR
*     MOVE TRUE TO #NT-UPDATE
*   END-IF
*
    IF #CLEAR
      ESCAPE TOP
    END-IF
*
    IF  ((NOTE-TEXT (#I1) = MASK ('ACK'))
                    AND  (NOTE-DATE (#I1) LT #DATN-1-MON))
     OR ((NOTE-TEXT (#I1) = MASK ('GIFT'))
                    AND  (NOTE-DATE (#I1) LT #DATN-1-MON))
*    OR ((NOTE-TEXT (#I1) = MASK ('RFD'))    /* removed 8/30/2002
*                   AND  (NOTE-DATE (#I1) LT #DATN-1-MON))
*    OR ((NOTE-TEXT (#I1) = MASK ('REF'))    /* removed 8/30/2002
*                   AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
*            below are created in athp07p5 by eds at month-end
     OR ((NOTE-TEXT (#I1) = MASK ('FPF UNPAID'))
                    AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('PREMIUM OVERDUE'))
                    AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('ALLOTMENT STOPPED'))
                    AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('CURRENTLY OVERLOANED'))
                    AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('ESTIMATED FUTURE'))
                    AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('ALLOT START NOTICE'))
                    AND  (NOTE-DATE (#I1) LT #DATN-6-MON))
*            below are created in G4000PNU
     OR ((NOTE-TEXT (#I1) = MASK ('* MEMBER ELECTS TO LOWER'))
                    AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
     OR ((NOTE-TEXT (#I1) = MASK ('* MEMBER ELECTS TO INCREASE'))
                    AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
      IF   (NOTE-LINE-NUM(#I1 + 1) = 0 OR = 1) /* BYPASS IF NOT SNGLE NT
       AND (NOTE-LINE-NUM (#I1) = 1)
        ADD 1 TO #CNT-3
        DISPLAY (3) #CNT-3 ID-NUMBER NOTE-DATE(#I1) NOTE-CLERK-ID(#I1)
                                                 NOTE-TEXT (#I1)
        DISPLAY (6) #CNT-3 ID-NUMBER NOTE-DATE(#I1) NOTE-CLERK-ID(#I1)
                                                 NOTE-TEXT (#I1)
        RESET NOTE-TEXT     (#I1)
              NOTE-DATE     (#I1)
              NOTE-CLERK-ID (#I1)
              NOTE-LINE-NUM (#I1)
        MOVE TRUE TO #CLEAR
        MOVE TRUE TO #NT-UPDATE
      END-IF
    END-IF
*
    IF #CLEAR
      ESCAPE TOP
    END-IF
*
    IF ((NOTE-TEXT (#I1) = MASK ('RET SERIES'))
     OR (NOTE-TEXT (#I1) = MASK ('SEP SERIES'))
     OR (NOTE-TEXT (#I1) = MASK ('TML SERIES')))
*
      RESET #NO-ST-REC
      FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
        IF NO RECORD
          FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
            IF NO RECORD
              MOVE TRUE TO #NO-ST-REC
            END-NOREC
*
            MOVE *ISN TO #ISN
            ESCAPE BOTTOM (0266)
          END-FIND
        END-NOREC
*
        MOVE *ISN TO #ISN
      END-FIND
*
      IF NOT #NO-ST-REC
        GET ST-V #ISN
        IF  ((DATE-RETIRED NE 0)
         AND (DATE-RETIRED LT #DATN-6-MON) /* RETIRED OVER 6 MON AGO
         AND (NOTE-LINE-NUM (#I1 + 1) = 0 OR = 1)) /* BYP IF NOT SNG NT
          OR ((ID-NUMBER LE 200)   /* DELETE SAMPLESS
            AND  (NOTE-DATE (#I1) LT #DATN-3-MON))
          ADD 1 TO #CNT-4
*         DISPLAY (4) #CNT-4 ID-NUMBER NOTE-DATE (#I1)
*  removed 6/98 as per KRM        NOTE-CLERK-ID (#I1) NOTE-TEXT (#I1)
          DISPLAY (6) #CNT-4 ID-NUMBER NOTE-DATE (#I1)
                                  NOTE-CLERK-ID (#I1) NOTE-TEXT (#I1)
          RESET NOTE-TEXT     (#I1)
                NOTE-DATE     (#I1)
                NOTE-CLERK-ID (#I1)
                NOTE-LINE-NUM (#I1)
          MOVE TRUE TO #CLEAR
          MOVE TRUE TO #NT-UPDATE
        END-IF
      END-IF
    END-IF
*
    IF #CLEAR
      ESCAPE TOP
    END-IF
*
    MOVE NOTE-TEXT (#I1) TO #NOTE-TEXT
*            below are created in aoap005a from Mr rqsts
    IF   ((NOTE-TEXT (#I1) = MASK ('Interim FAS'))
     AND  (NOTE-DATE (#I1) LT #DATN-3-MON))
*
    OR  (((#NOTE-TEXT = MASK ('MR Sent'))
    OR    (#NOTE-TEXT = MASK ('*MR Sent')))
     AND  (#NOTE-TEXT NE MASK ('MR Sent /BN'))  /* KEEP these forever
     AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
*
* below removed 3/97 as per JMB
*   OR (((NOTE-TEXT (#I1) = MASK ('MR Sent /BNF'))
*   OR   (NOTE-TEXT (#I1) = MASK ('Sent B/C')))
*    AND #FND-FINAL-BENE)
*
    OR   ((NOTE-TEXT (#I1) = MASK ('BASD'))
     AND  (#NOTE-TEXT-8A = 'BASED ON')
     AND  (NOTE-DATE (#I1) LT #DATN-6-MON))
    OR   ((NOTE-TEXT (#I1) = MASK ('1405 DT'))
     AND  (#NOTE-TEXT-8B = 'BASED ON')
     AND  (NOTE-DATE (#I1) LT #DATN-6-MON))
    OR   ((NOTE-TEXT (#I1) = MASK ('PEBD'))
     AND  (#NOTE-TEXT-8A = 'BASED ON')
     AND  (NOTE-DATE (#I1) LT #DATN-6-MON))
*            below are created by Marge Ramage as mbr follow-up msgs
    OR ((NOTE-TEXT (#I1) = MASK ('WELCOME'))
     AND  (NOTE-DATE (#I1) LT #DATN-2-YEAR))
    OR ((NOTE-TEXT (#I1) = MASK ('6 MO'))
     AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
    OR ((NOTE-TEXT (#I1) = MASK ('1YEAR'))
     AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
    OR ((NOTE-TEXT (#I1) = MASK ('1YR'))
     AND  (NOTE-DATE (#I1) LT #DATN-1-YEAR))
*
      ADD 1 TO #CNT-4
*     DISPLAY (4) #CNT-4 ID-NUMBER NOTE-DATE (#I1)
*   removed 6/98 as per KRM       NOTE-CLERK-ID (#I1) NOTE-TEXT (#I1)
      DISPLAY (6) #CNT-4 ID-NUMBER NOTE-DATE (#I1)
                                  NOTE-CLERK-ID (#I1) NOTE-TEXT (#I1)
      RESET NOTE-TEXT     (#I1)
            NOTE-DATE     (#I1)
            NOTE-CLERK-ID (#I1)
            NOTE-LINE-NUM (#I1)
      MOVE TRUE TO #NT-UPDATE
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #CNT-2
    DISPLAY (2) NOTITLE GIVE SYSTEM FUNCTIONS
            #CNT-2
            'ID/NUMBER'      NT-V.ID-NUMBER
            'DATE'           NT-V.NOTE-DATE  (#I1)
            'CLERK'          NT-V.NOTE-CLERK-ID(#I1)
            'LINE/NUM'       NT-V.NOTE-LINE-NUM(#I1)
            'TEXT'           NT-V.NOTE-TEXT  (#I1)
  END-FOR /*(0165)
*
  IF #NT-UPDATE
    PERFORM CLEAR-DELETED-LINES
*
    RESET #NO-ST-REC
    FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
      IF NO RECORD
        FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
          IF NO RECORD
            MOVE TRUE TO #NO-ST-REC
          END-NOREC
*
          MOVE *ISN TO #ISN
          ESCAPE BOTTOM (0368)
        END-FIND
      END-NOREC
*
      MOVE *ISN TO #ISN
    END-FIND
*
    IF #NUM-NOTES = 0
      IF NOT #NO-ST-REC
        GET ST-V #ISN
        RESET NOTE-REC-IND
        UPDATE (0385)
      END-IF
*
      DELETE (0155)
      WRITE 'NOTE REC DELETED - ' NT-V.ID-NUMBER
      ADD 1 TO #DELETED-RECS
    ELSE
      UPDATE (0155)
    END-IF
  END-IF
*
  END TRANSACTION
END-READ
*
WRITE (2) 'TOT-RECS-READ        = ' #RECS-READ
WRITE (2) 'TOT-NOTES-DELETED    = ' #DELETED-NOTES
WRITE (2) 'TOT-RECS-DELETED     = ' #DELETED-RECS
WRITE (2) 'TOT-ACCT-NOTES-DEL   = ' #CNT-3
WRITE (2) 'TOT-MBR-SVR-NOTES-DEL= ' #CNT-4
WRITE (2) 'TOT BLANK LINES      = ' #CNT-LINE-NUMS /* PM ADDED 04/2000
* WRITE (2) 'TOT TERMS            = ' #CNT-TERMS     /* PM REMOVE THIS
*
WRITE (3) 'TOT-RECS-READ        = ' #RECS-READ
WRITE (3) 'TOT-NOTES-DELETED    = ' #DELETED-NOTES
WRITE (3) 'TOT-RECS-DELETED     = ' #DELETED-RECS
WRITE (3) 'TOT-ACCT-NOTES-DEL   = ' #CNT-3
WRITE (3) 'TOT-MBR-SVR-NOTES-DEL= ' #CNT-4
WRITE (3) 'TOT BLANK LINES      = ' #CNT-LINE-NUMS /* PM ADDED 04/2000
* WRITE (3) 'TOT TERMS            = ' #CNT-TERMS     /* PM REMOVE THIS
WRITE (5) 'TOT BLANK LINES      = ' #CNT-LINE-NUMS /* PM ADDED 04/2000
* WRITE (7) 'TOT TERMS            = ' #CNT-TERMS     /* PM REMOVE THIS
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CLEAR-DELETED-LINES
* * * * * * * * * * * * * * * * * * *
*
MOVE 1 TO #I2
MOVE NT-V.C*NOTE-TABLE TO #I3
*
IF #I3 GT 500                          /* DW2
   WRITE 'ERROR ON ROUTINE CLEAR-DELETED; CALL EDS'
   ESCAPE ROUTINE
END-IF
*
FOR #I1 1 TO #I3
  IF NT-V.NOTE-TEXT(#I1) = ' '
    SUBTRACT 1 FROM #NUM-NOTES
    ADD 1 TO #DELETED-NOTES
    ESCAPE TOP
  END-IF
*
  MOVE NT-V.NOTE-TEXT     (#I1) TO NT-V.NOTE-TEXT     (#I2)
  MOVE NT-V.NOTE-CLERK-ID (#I1) TO NT-V.NOTE-CLERK-ID (#I2)
  MOVE NT-V.NOTE-DATE     (#I1) TO NT-V.NOTE-DATE     (#I2)
  MOVE NT-V.NOTE-LINE-NUM (#I1) TO NT-V.NOTE-LINE-NUM (#I2)
  ADD 1 TO #I2
END-FOR
*
IF #I2 LT 501    /* WAS 61                   DW2
  FOR #I1 #I2 500    /* WAS 60               DW2
    RESET NT-V.NOTE-TEXT     (#I1)
    RESET NT-V.NOTE-DATE     (#I1)
    RESET NT-V.NOTE-LINE-NUM (#I1)
    RESET NT-V.NOTE-CLERK-ID (#I1)
  END-FOR
END-IF
*
END-SUBROUTINE
*
END
