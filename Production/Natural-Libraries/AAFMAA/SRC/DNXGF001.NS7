* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID DNXGF001   Role decoration
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20100225  None    Program written.                             *
*                                                                      *
************************************************************************
*
DEFINE FUNCTION FUNCTION-ROLE-DECORATION RETURNS (A) DYNAMIC
DEFINE DATA
PARAMETER
1 #CONTACT-ID     (N8)
LOCAL
1 CountNotes VIEW OF A-CONTACT-NOTES
1 STATUS VIEW OF A-STATUS
1 PHONE VIEW OF A-CONTACT-PHONE
1 EMAIL VIEW OF A-CONTACT-EMAIL
1 ADDRS VIEW OF A-ADDRESS-RELATIONSHIP
1 #BD-LINKS   (N6/100)
1 #MEMBER     (A1) CONST <'*'>
1 #NOTES      (A1) CONST <'+'>
1 #ADRS       (A1) CONST <'@'>
1 #BENE       (A1) CONST <'$'>
1 BLANK       (A1) CONST <' '>
*
END-DEFINE
*
FUNCTION-ROLE-DECORATION := BLANK

F-NBR1.
FIND NUMBER STATUS WITH MEMBER-CONTACT-ID = #CONTACT-ID
IF *NUMBER ( F-NBR1.) > 0
  COMPRESS #MEMBER FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
END-IF
F-NBR2.
FIND NUMBER CountNotes WITH CONTACT-ID = #CONTACT-ID
IF *NUMBER ( F-NBR2.) > 0
  COMPRESS #NOTES FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
END-IF
*
FIND NUMBER ADDRS WITH CONTACT-ID = #CONTACT-ID
IF *NUMBER (0047) > 0
  COMPRESS #ADRS FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
ELSE
  FIND NUMBER PHONE WITH CONTACT-ID = #CONTACT-ID
  IF *NUMBER (0051) > 0
    COMPRESS #ADRS FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
  ELSE
    FIND NUMBER EMAIL WITH CONTACT-ID = #CONTACT-ID
    IF *NUMBER (0055) > 0
      COMPRESS #ADRS FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
      ELSE IGNORE END-IF
  END-IF
END-IF
RESET #BD-LINKS (*)
#BD-LINKS (*) := FUNCTION-BD-LINKS(<#CONTACT-ID>)(*)
IF #BD-LINKS (*) > 0
  COMPRESS #BENE FUNCTION-ROLE-DECORATION INTO FUNCTION-ROLE-DECORATION LEAVING NO
END-IF
END-FUNCTION

END

