* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPMSSN - find members/owner with missing SSN's
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* PAM  04282008 PM2  Combine into 1 file instead of 2 separate ones for*
*                    insured and owner                                 *
* JNT  09182013 JT1  Address normalization                             *
* YAK  05162014 YK1  Email/Phone contact normalization                 *  
* TMT  06102014 TT1 CONTACT NORMALIZATION DOB AND SEX                  *
************************************************************************
DEFINE DATA
 LOCAL USING ADDA0001                                 /* JT1
 LOCAL  USING GONERLXX
 LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 NAME
 2 PROCESS-IND
 2 INTRO-FLAG
 2 DUES-BALANCE
 2 PAD-CODE
 2 SSN
 2 NUMBER-MEMBERSHIPS
 2 TRAD-UP-SENT-DT
 2 STOP-DATE
*  2 DATE-OF-BIRTH                             /* TT1
 2 ISSUE-DATE              (9)
 2 X-RID-BALANCE           (9)
 2 X-FIRST-PLACE-FEE       (9)
 2 X-BILL-ADJUST           (9)
 2 RID-PD-TO               (9)
 2 PAY-STATUS              (9)
 2 RID-PLAN                (9)
 2 MTD-ACV-PREM-ADJ        (P7.2/9)
 2 PLAN                    (9)
 2 REDEFINE PLAN
  3 PLAN-OCC               (9)
   4 PLAN-A1               (A1)
   4 PLAN-A2               (A1)
 2 STATUS                  (9)
 2 ANNUAL-PREM             (9)
 2 MONTH-ALLOC-AMT         (9)
 2 ALOT-AMT
 2 MODE                    (9)
 2 FACE-AMOUNT             (9)
 2 COST-BASIS              (9)
 2 CASH-VALUE              (9)
 2 STM-PREM                (P6.2/9)
 2 TOT-PREMIUMS-PAID       (9)
 2 PAID-TO-DATE            (9)
 2 DEBIT-CREDIT-AMT        (P7.2)
 2 MEMBER-CONTACT-ID                         /* JT1
*
1 ST-N VIEW OF A-STATUS
 2 ID-NUMBER
 2 SSN
 2 NAME
 2 MEMBER-CONTACT-ID                         /* JT1
*
1 #I1                      (I2)
1 #I2                      (I2)
1 #I3                      (I2)
1 #I4                      (I2)
1 #I5                      (I2)
*
1 #DATN                    (N8)
1 REDEFINE #DATN
 2 #DATN-CC                (N2)
 2 #DATN-YY                (N2)
 2 #DATN-MM                (N2)
 2 #DATN-DD                (N2)
*
1 #BLANK-OWNER             (L)
1 #BLANK-MEMBER            (L)
*
1 #TIMN                    (N7)
1 REDEFINE #TIMN
 2 #TIMN-HH                (A2)
 2 REDEFINE #TIMN-HH
  3 #TIMN-HH-N             (N2)
 2 #TIMN-MM                (A2)
 2 #TIMN-SS                (A2)
 2 #TIMN-T                 (A1)
*
* 1 #TAB                   (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB                     (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-300             (A300)             /* PM2
1 #CNT-1                   (P5)
1 #CNT-2                   (P5)
1 #SPLIT                   (A1)
1 #ERR-MSG                 (A60)
1 #STREET                  (A25)
1 #STREET-2                (A25)
1 #CITY                    (A22)
1 #STATE                   (A2)
1 #NAME                    (A25)
1 #FIRST                   (A25)
1 #LAST                    (A25)
1 #FIRST-20                (A20)
1 #MI                      (A1)
1 #OWNER-STREET            (A25)
1 #OWNER-STREET-2          (A25)
1 #OWNER-CITY              (A22)
1 #OWNER-STATE             (A2)
1 #OWNER-FIRST             (A25)
1 #OWNER-LAST              (A25)
1 #OWNER-FIRST-20          (A20)
1 #OWNER-MI                (A1)
1 #DISPLAY-POLICY-NUMBER   (A16)
1 #DISPLAY-FACE-AMOUNT     (A15)
1 #DISPLAY-ISSUE-DATE      (A10)
1 REDEFINE #DISPLAY-ISSUE-DATE
 2 #DISPLAY-ISSUE-MM       (A2)
 2 #DISPLAY-SLASH-1        (A1)
 2 #DISPLAY-ISSUE-DD       (A2)
 2 #DISPLAY-SLASH-2        (A1)
 2 #DISPLAY-ISSUE-YYYY     (A4)
1 #DISPLAY-PLAN            (A3)
1 #WORK-ISSUE-DATE         (N8)
1 REDEFINE #WORK-ISSUE-DATE
 2 #WORK-ISSUE-YYYY        (N4)
 2 #WORK-ISSUE-MM          (N2)
 2 #WORK-ISSUE-DD          (N2)
1 #WORK-ZIP-5              (A5)
1 #WORK-ZIP-4              (A4)
1 #DISPLAY-ZIP             (A10)
1 #DISPLAY-OWNER-ZIP       (A10)
1 #DISPLAY-OWNER-ID        (A6)
* YK1
1 #DISPLAY-PHONE           (A) DYNAMIC
1 #DISPLAY-OWNER-EMAIL     (A) DYNAMIC
1 #DISPLAY-EMAIL           (A) DYNAMIC
* 1 #PHONE                   (N11)
* 1 REDEFINE #PHONE
*  2 #PHONE-1ST              (N1)
*  2 #PHONE-AREA-CODE        (N3)
*  2 #PHONE-PREFIX           (N3)
*  2 #PHONE-REST             (N4)
*
1 #MULTIPLE-OWNER          (L)
1 #NON-INS-OWNER           (L)
1 #OWNER-OCC-9             (9)
 2 #OWNER-FLAG             (A1)
 2 #OWNER-CN               (N6/10)
 2 #OWNER-CN-BY-POL        (N6)
 2 #OWNER-CN-SEG           (I2/9)
 2 #ISS-DATE               (N8/9)
 2 #ISS-DATE-DISP          (A10/9)
*    beneficiary info output
 2 #BENE-TYPE              (A1/2)
 2 #BENE-TEXT              (A60/10)
1 #WORK-OWNER-CN           (N6)
1 REDEFINE #WORK-OWNER-CN
 2 #WORK-OWNER-CN-A        (A6)
*
1 #ZIP5-INSURED            (N5)                           /* JT1 start
1 #ZIP4-INSURED            (N4)
1 #ZIP5-OWNER              (N5)                           
1 #ZIP4-OWNER              (N4)                           /* JT1 end
END-DEFINE
*
************************************************************************
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Members with Missing SSNS'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Owners with Missing SSNS'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATN
*
COMPRESS 'Mem_Id_Num' #TAB                /* 11
         'Pol_Num' #TAB                   /* 8
         'Face' #TAB                      /* 5
         'Iss_Date' #TAB                  /* 9
         'Plan' #TAB                      /* 5
         'Ins_Last' #TAB                  /* 9
         'Ins_First' #TAB                 /* 10
         'Ins_Street1' #TAB               /* 12
         'Ins_Street2' #TAB               /* 12
         'Ins_City' #TAB                  /* 9
         'Ins_State' #TAB                 /* 10
         'Ins_Zip' #TAB                   /* 8
         'Ins_Email' #TAB                 /* 10
         'Contact_Num' #TAB               /* 11
         'Own_ID_Num' #TAB                /* 10
         'Own_Last' #TAB                  /* 9  ===== 148
         'Own_First' #TAB                 /* 10
         'Own_Street1' #TAB               /* 12
         'Own_Street2' #TAB               /* 12
         'Own_City' #TAB                  /* 9
         'Own_State' #TAB                 /* 10
         'Own_Zip' #TAB                   /* 8
         'Own_Email'                      /* 9
                               INTO #EXTRACT-300 LEAVING NO   /* PM2
WRITE WORK 1 #EXTRACT-300
RESET #EXTRACT-300
*
READ ST-V BY MEMBER-KEY FROM '001900'
*
  IF PROCESS-IND = 'C' 
   OR NUMBER-MEMBERSHIPS = 0
    ESCAPE TOP
  END-IF
*
  RESET #BLANK-MEMBER
*
  IF ST-V.SSN NE 0
    CALLNAT 'GOWNRSXX' ST-V.ID-NUMBER
     #NON-INS-OWNER
     #MULTIPLE-OWNER
     #OWNER-OCC-9 (1:9)
*
    IF #NON-INS-OWNER
      FOR #I3 1 TO 9
        IF #OWNER-CN-BY-POL (#I3) EQ 0
          ESCAPE TOP
        END-IF
*
        MOVE  #OWNER-CN-BY-POL (#I3) TO #WORK-OWNER-CN
        FIND (1) ST-N WITH ST-N.MEMBER-KEY = #WORK-OWNER-CN-A
          IF NO RECORDS FOUND
            WRITE (2) 'Owner record not found for ' #WORK-OWNER-CN-A
                      'For member ' ST-V.ID-NUMBER
            ESCAPE TOP
          END-NOREC
*
          IF ST-N.SSN EQ 0
            ADD 1 TO #CNT-2
            MOVE ST-N.ID-NUMBER TO #DISPLAY-OWNER-ID
* YK1 <            
*           MOVE ST-N.TELE-NUMBER TO #PHONE
            #DISPLAY-PHONE       := F-PREF-PHONE(<ST-N.MEMBER-CONTACT-ID>)
*           MOVE ST-N.INTERNET-ADDR TO #DISPLAY-OWNER-EMAIL
            #DISPLAY-OWNER-EMAIL := F-PREF-EMAIL(<ST-N.MEMBER-CONTACT-ID>)
* > YK1            
            MOVE #I3 TO #I1
            PERFORM GET-OWNER-ADDRESS
            PERFORM GET-OWNER-NAME
            PERFORM GET-OWNER-ZIP
            PERFORM CREATE-FILE
          END-IF
        END-FIND
      END-FOR
    ELSE
      ESCAPE TOP
    END-IF
  ELSE
    MOVE TRUE TO #BLANK-MEMBER
    ADD 1 TO #CNT-1
* YK1    
*   MOVE ST-V.TELE-NUMBER TO #PHONE
    #DISPLAY-PHONE := F-PREF-PHONE(<ST-V.MEMBER-CONTACT-ID>)
    #DISPLAY-EMAIL := F-PREF-EMAIL(<ST-V.MEMBER-CONTACT-ID>)
*
    CALLNAT 'GOWNRSXX' ST-V.ID-NUMBER
     #NON-INS-OWNER
     #MULTIPLE-OWNER
     #OWNER-OCC-9 (1:9)
*
    FOR #I1 1 TO 9
      IF ISSUE-DATE (#I1) = 0
        ESCAPE BOTTOM
      END-IF
*
      IF #NON-INS-OWNER
       AND #OWNER-CN-BY-POL (#I1) NE 0
        MOVE  #OWNER-CN-BY-POL (#I1) TO #WORK-OWNER-CN
        FIND ST-N WITH ST-N.MEMBER-KEY = #WORK-OWNER-CN-A
          IF NO RECORDS FOUND
            WRITE 'Owner record not found for ' #WORK-OWNER-CN-A
                  'For member ' ST-V.ID-NUMBER
            ESCAPE TOP
          END-NOREC
*
          MOVE ST-N.ID-NUMBER TO #DISPLAY-OWNER-ID
* YK1     MOVE ST-N.INTERNET-ADDR TO #DISPLAY-OWNER-EMAIL
          #DISPLAY-OWNER-EMAIL := F-PREF-EMAIL(<ST-N.MEMBER-CONTACT-ID>)   /* YK1
          PERFORM GET-OWNER-ADDRESS
          PERFORM GET-OWNER-NAME
          PERFORM GET-OWNER-ZIP
        END-FIND
      ELSE
        RESET #OWNER-STREET #OWNER-STREET-2 #OWNER-CITY #OWNER-STATE
              #OWNER-FIRST-20 #OWNER-MI #OWNER-LAST #DISPLAY-OWNER-ZIP
              #DISPLAY-OWNER-ID #DISPLAY-OWNER-EMAIL
      END-IF
*
      PERFORM CREATE-FILE
*
    END-FOR
  END-IF
END-READ
*
************************************************************************
DEFINE SUBROUTINE GET-INSURED-ADDRESS
************************************************************************
*
  RESET #PDA-ADDRESS                                        /* JT1 start 
  MOVE 'P' TO #ADD-TYPE                                   
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS                        
  MOVE #ADD-LINE-1 TO #STREET 
  MOVE #ADD-LINE-2 TO #STREET-2
  MOVE #ADD-CITY   TO #CITY
  MOVE #ADD-STATE  TO #STATE
  MOVE #ADD-ZIP5   TO #ZIP5-INSURED
  MOVE #ADD-ZIP4   TO #ZIP4-INSURED
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE GET-OWNER-ADDRESS
************************************************************************
*
  RESET #PDA-ADDRESS                                        /* JT1 start 
  MOVE 'P' TO #ADD-TYPE                                   
  #ADD-CONTACT-ID := ST-N.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS                        
  MOVE #ADD-LINE-1 TO #OWNER-STREET 
  MOVE #ADD-LINE-2 TO #OWNER-STREET-2
  MOVE #ADD-CITY   TO #OWNER-CITY
  MOVE #ADD-STATE  TO #OWNER-STATE
  MOVE #ADD-ZIP5   TO #ZIP5-OWNER
  MOVE #ADD-ZIP4   TO #ZIP4-OWNER
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE GET-INSURED-NAME
************************************************************************
*
RESET #NAME #FIRST-20 #MI #LAST #ERR-MSG
MOVE ST-V.NAME TO #NAME
CALLNAT 'GFMLNSXX'
  #NAME
  #FIRST-20
  #MI
  #LAST
  #ERR-MSG
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE GET-OWNER-NAME
************************************************************************
*
RESET #NAME #OWNER-FIRST-20 #OWNER-MI #OWNER-LAST #ERR-MSG
MOVE ST-N.NAME TO #NAME
CALLNAT 'GFMLNSXX'
  #NAME
  #OWNER-FIRST-20
  #OWNER-MI
  #OWNER-LAST
  #ERR-MSG
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE GET-INSURED-ZIP
************************************************************************
*
RESET #WORK-ZIP-5 #WORK-ZIP-4 #DISPLAY-ZIP
* MOVE EDITED ST-V.ZIP-CODE-5 (EM=99999) TO #WORK-ZIP-5           /* JT1 start
* MOVE EDITED ST-V.ZIP-CODE-4 (EM=9999) TO #WORK-ZIP-4
MOVE EDITED #ZIP5-INSURED (EM=99999) TO #WORK-ZIP-5
MOVE EDITED #ZIP4-INSURED (EM=9999) TO #WORK-ZIP-4                /* JT1 end
COMPRESS #WORK-ZIP-5 '-' #WORK-ZIP-4 INTO #DISPLAY-ZIP LEAVING NO
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE GET-OWNER-ZIP
************************************************************************
*
RESET #WORK-ZIP-5 #WORK-ZIP-4 #DISPLAY-OWNER-ZIP
* MOVE EDITED ST-N.ZIP-CODE-5 (EM=99999) TO #WORK-ZIP-5
* MOVE EDITED ST-N.ZIP-CODE-4 (EM=9999) TO #WORK-ZIP-4
MOVE EDITED #ZIP5-OWNER (EM=99999) TO #WORK-ZIP-5                /* JT1
MOVE EDITED #ZIP4-OWNER (EM=9999) TO #WORK-ZIP-4                 /* JT1
COMPRESS #WORK-ZIP-5 '-' #WORK-ZIP-4 INTO #DISPLAY-OWNER-ZIP LEAVING NO
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CREATE-FILE
************************************************************************
*
PERFORM GET-INSURED-NAME
PERFORM GET-INSURED-ADDRESS
PERFORM GET-INSURED-ZIP
*
RESET #DISPLAY-POLICY-NUMBER #DISPLAY-FACE-AMOUNT
      #DISPLAY-ISSUE-DATE #DISPLAY-PLAN #WORK-ISSUE-DATE
*
CALLNAT 'GPLNMSXX' #DISPLAY-POLICY-NUMBER ST-V.ID-NUMBER #I1
*
MOVE ST-V.ISSUE-DATE (#I1)             TO #WORK-ISSUE-DATE
MOVE EDITED #WORK-ISSUE-YYYY (EM=9999) TO #DISPLAY-ISSUE-YYYY
MOVE EDITED #WORK-ISSUE-MM (EM=99)     TO #DISPLAY-ISSUE-MM
MOVE EDITED #WORK-ISSUE-DD (EM=99)     TO #DISPLAY-ISSUE-DD
MOVE '/'                               TO #DISPLAY-SLASH-1
MOVE '/'                               TO #DISPLAY-SLASH-2
*
MOVE EDITED ST-V.FACE-AMOUNT (#I1)(EM=ZZZ,ZZ9) TO #DISPLAY-FACE-AMOUNT
MOVE ST-V.PLAN (#I1)                           TO #DISPLAY-PLAN
*
COMPRESS ST-V.ID-NUMBER #TAB               /* 8
         #DISPLAY-POLICY-NUMBER #TAB       /* 15
         #DISPLAY-FACE-AMOUNT #TAB         /* 15
         #DISPLAY-ISSUE-DATE #TAB          /* 10
         #DISPLAY-PLAN #TAB                /* 3
         #LAST #TAB                        /* 25
         #FIRST-20 #TAB                    /* 20
         #STREET #TAB                      /* 25
         #STREET-2 #TAB                    /* 25 ===== 155
         #CITY #TAB                        /* 22
         #STATE #TAB                       /* 2
         #DISPLAY-ZIP #TAB                 /* 10
*        ST-V.INTERNET-ADDR #TAB           /* 60
         #DISPLAY-EMAIL                    /*        YK1
*        #PHONE #TAB                       /* 11
         #DISPLAY-PHONE                    /*        YK1 
         #DISPLAY-OWNER-ID #TAB            /* 6
         #OWNER-LAST #TAB                  /* 25 ===== 136
         #OWNER-FIRST-20 #TAB              /* 20
         #OWNER-STREET #TAB                /* 25
         #OWNER-STREET-2 #TAB              /* 25
         #OWNER-CITY #TAB                  /* 22
         #OWNER-STATE #TAB                 /* 2
         #DISPLAY-OWNER-ZIP #TAB           /* 10
         #DISPLAY-OWNER-EMAIL #TAB         /* 60
                                INTO #EXTRACT-300 LEAVING NO
*
IF #BLANK-MEMBER
  WRITE WORK 1 #EXTRACT-300
  DISPLAY 'CNT-1' #CNT-1 'CN Number' ST-V.ID-NUMBER
          'Issue Date' ISSUE-DATE (#I1) 'SSN' ST-V.SSN
          'Insured First' #FIRST-20 'Insured Last' #LAST
ELSE
  WRITE WORK 1 #EXTRACT-300                /* PM2
  DISPLAY (2) 'CNT-2' #CNT-2 'CN Number' ST-N.ID-NUMBER
          'Issue Date' ISSUE-DATE (#I1) 'SSN' ST-N.SSN
          'Owner First' #OWNER-FIRST-20 'Owner Last' #OWNER-LAST
          'Mem CN' ST-V.ID-NUMBER
END-IF
*
RESET #EXTRACT-300
*
END-SUBROUTINE
*
END
