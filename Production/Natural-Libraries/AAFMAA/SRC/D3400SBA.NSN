* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: D3400SBA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  03052005 DW1  Add AL (Annuity Life) PROCESSING
* PAM  03112005 PM2  Reset display fields for GRANKSXX
* DRW  04082005 DW3  Show map even of only former is eligible
* DRW  04252005 DW4  Separate SGLI from other Ins for Special Contracts
* DRW  06072005 DW5  Make 'C' indicate Combat related
* DRW  06132005 DW6  ADD SERV-CONN-IND TO E9010SDX LINKAGE
* DRW  06142005 DW7  Use DOD instead of DATN if dead for ins tot calcs
* DRW  06152005 DW8  Set Spc Code if SVC-CONN = "D'
* DRW  07062005 DW9  Add displ 14 to list of lowest rank displ
* DRW  07082005 DW10 Prev Enlisted Rank Processing
* DRW  08242005 DW11 Check Rank when setting 1405 date defaults
* DRW  08242005 DW12 Bypass AAFMAA Pols if mwmber is dead
* DRW 10/12/2005 DW13 2005 Open Enroll Processing
* DRW 02/15/2006 DW14 SBS Processing
* DRW 07/31/2006 DW15 always populate DIEMS date if after 9/7/1980
* DRW 09/11/2006 DW16 If SBS loaded PREV-Rank-displ do not recalculate
* DRW 11/03/2006 DW17 Fix AL Death Benefit Calc using AVL-TDIV
* DRW 02/20/2007 DW18 Modify DEATH BENEFIT Calc
* DRW 02/20/2008 DW19 Bypass RETIRED TYPE Choice Map if CAS Assume DEAD
* DRW 10/31/2008 DW20 SET SBP OPT ON A RECALLED TO ACT DUTY W NO SBP OPT
* DRW 11/13/2008 DW21 Chg Mil-Disab TEST to check for less than 30%
* DRW 11/13/2008 DW22 Do not overlay RET INd if in SBW processing
* DRW 12/27/2008 DW23 Replace WAASSRT1 with SAGSRT9 in 2 places
* TDM 01/29/2009 TM1  CHGD MAX AGE TO 26 TO COVER OLD KIDS FOR DEA BENES
* DRW 010/01/2009 DW24 Return to using AAFMAA Death Benefit (Back out DW18)
* NGG 06/29/2010 NG1 Changes to allow up to 60 dependants
************************************************************************
* AFSC Changes (RSE: add suffix 'a' to distinguish from the above)
* DRW 02/04/2010 DW23a Remove reference to GDROPSTM and AAFM logic
* DRW 02/15/2010 DW24a Change reference to #CGA-ORIG-SUFFIX  (SBS/SBW)
* DRW 02/18/2010 DW25 Bypass INPUT if coming from Longevity Calc (LON)
*                                             or Disability Calc (CR*)
* DRW 03/29/2010 DW26 RESV IDT Processing
* DRW 04/10/2010 DW27 Remove reference to AAFM Fields
* DRW 04/19/2010 DW28 Chg DEP DOB to 0301 if it is 0229
* bz  04/12/2011 bz1  Change Calc. for 'AL' Plans less than 2 Years old
* bz  04/24/2011 bz2 Gift of membership
* TMT 06/16/2014 TT1 Contact Normalization
*******************************************
DEFINE DATA
PARAMETER USING BMASTABA
PARAMETER USING BINF1ABA
PARAMETER USING BDEPTABA
PARAMETER USING BWAGEABA
PARAMETER USING BINF2ABA
*
LOCAL  USING GONERLXX
* LOCAL  USING D3400LBA
LOCAL USING CVALULXX                /* PM5
LOCAL  USING GWAGELXX
LOCAL
*
1 FR-V VIEW OF A-FIRS
  2 POL-CODE (A1/45)
  2 POL-NAME (A19/45)
  2 POL-TYPE (A4/45)
  2 POL-AMOUNT (P7.0/45)
  2 POL-DATE (N8.0/45)
  2 POL-REMARKS (A21/45)
  2 POL-AMT-PAID (P7.2/45)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 ONLINE-ACTION-GN (A1) /*
  2 MILITARY-STATUS (A1)
  2 OTHER-COMPANY-ID (N6.0)
  2 PROCESS-IND (A1)
  2 DATA-SHARED-IND (A1)
  2 APPL-SOURCE (A7)
  2 STATUS (A1/9)
*   2 DATE-OF-BIRTH (N8.0)               /* TT1
  2 FIRS-LOCATION-CODE (A1/9)
  2 DEATH-SERVICE-CON (A2)
  2 ONLINE-ACTION (A1/9)
  2 PLAN (A3/9)
  2 CONVERSION-FLAG (A1/9)
  2 ISSUE-DATE (N8.0/9)
  2 REDEFINE ISSUE-DATE /* REDEF. BEGIN : ISSUE-DATE
    3 ISSUE-DATE-GR (9)
      4 ISS-DT-CENT (N2)
      4 ISS-DT-YY (N2)
      4 ISS-DT-MM (N2)
      4 ISS-DT-DD (N2)
  2 FACE-AMOUNT (P7.2/9)
  2 ISSUE-AGE (A2/9)
  2 PAID-UP-DATE (N8.0/9)
  2 REDEFINE PAID-UP-DATE
    3 PAID-UP-DATE-CENT (N2) /* REDEF. FROM PREDICT
    3 PAID-UP-DATE-YYMMDD (N6) /* REDEF. FROM PREDICT
  2 LAST-ANN-CASH-VALUE (P7.2/9)
  2 SMOKER-FLAG (A1/9)
  2 DEATH-BENEFIT (P7.2/9)
  2 CASH-VALUE (P7.2/9)
  2 NXT-MON-DEATH-BENEFIT (P7.2/9)
  2 MONTH-ALLOC-AMT (P7.2/9)                /*TT1
  2 BEG-CASH-VALUE (P7.2/9)
*
1 #EMERGENCY-CN (N6)      /* NG2
1 #ORIGINAL-CN  (N6)      /* NG2
*
1 #DISPL        (P2)      /* DW16
1 #NEW-RANK     (A3)
1 #PREV-ENL-RANK   (A3)   /* DW10
1 #PREV-ENL-DOP    (N8)
1 #PREV-PREV-ENL-DOP (N8)
1 #HOLD-SUFF   (A3)
*
1 FI-V VIEW OF A-INVESTMENTS
  2 C*INV-GEN-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL-2
*
1 #PASSED-FD-TYPE   (A2) INIT <'BA'>
1 #PASSED-FIN-ISN   (P8)
1 #PASSED-TOT-INV   (N9)
1 #PASSED-INV       (N9/7)
1 #PASSED-WAR       (N2.2)
1 REDEFINE #PASSED-WAR
  2 #PASSED-WAR-N    (N4)
1 #PASSED-MTG-DEBT  (N7)
1 #PASSED-RET-INV   (N9)
*
1 #SCR-CUR-SPS-IND      (A1)
1 #SCR-FMR-SPS-IND      (A1)
*
1 #SCR-RET-IND            (A1)
1 #SCR-DEAD-IND           (A1)
1 #SCR-DISAB-IND          (A1)   /* DW18
*
1 #NP                 (I4) CONST <60>               /* NG1
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2) INIT <60>                /* NG1
  2 #SRT-ENTRY-LENGTH (I2) INIT <52>
  2 #SRT-KEY-LENGTH   (I2) INIT <8>
  2 #SRT-KEY-POSITION (I2) INIT <0>  /* DOB ONLY
  2 #SRT-SORT-TYPE    (A1) INIT <'A'>
*
1 #FIND-LINK       (A6)
1 REDEFINE #FIND-LINK
  2 #FIND-LINK-N    (N6)
1 #OVER-AGE        (L)
1 #HANDI-KIDS      (L)
1 #INVALID         (L)
1 #RANK-LIT        (A23)
1 #RANK-DISPLAY    (A5)
1 #I1              (I2)
1 #I2              (I2)
1 #HOLD-STATUS     (A1)
1 #PAY-STAT        (A4)
1 #EFF-DATE        (N8)
1 #PLAN    (A3)
1 REDEFINE #PLAN
  2 #PLAN-A1   (A1)
  2 #PLAN-N2   (N2)
*
1 #CURRENT-KIDS-EXIST         (L)
1 #PRIOR-KIDS-EXIST           (L)
1 #FMR-SPS-ELIGIBLE           (L)
1 #CURR-TOT-INS                (P7.2)
1 #AVL-TDIV                    (P7.2)
*
1 #DT-PROM-TO-1LT             (N8)
1 REDEFINE #DT-PROM-TO-1LT
  2 #DT-PROM-TO-1LT-YYYY       (N4)
  2 #DT-PROM-TO-1LT-MM         (N2)
  2 #DT-PROM-TO-1LT-DD         (N2)
*
*  INFO FOR CALL TO E9010SDX AND E9010SDX
*
1 #P-AGE            (P3)
1 #P-PARMS
  2 #P-DATE-OF-BIRTH (N8)
  2 #P-DATE-RETIRED  (N8)
  2 #P-MILITARY-STAT (A1)
  2 #P-MILITARY-SERV (A3)
  2 #P-RANK          (A3)
  2 #P-ACTIVE-DUTY-BASE-DATE (N8)
1 #P-NAME           (A19)
1 #P-TYPE           (A4)
1 #P-AMOUNT         (P7)
1 #P-DATE           (N8)
1 #P-REMARKS        (A21)
1 #P-ERROR-FLAG     (L)
1 #P-CADET-FLAG     (A1)
*
1 #INS-CHG-MSG      (A76)
1 #INS-AMTS         (P7.2/6)
1 #INS-CHANGED      (L/6)
1 #CURR-INS-CALCS   (L)
1 #INS-TYPES-TBL    (A24) INIT<'VGLIAFBAMBA AFA OBA USBA'>
1 REDEFINE #INS-TYPES-TBL
  2 #INS-TYPE        (A4/6)
*
1 #CURR-DATE        (N8)
1 REDEFINE #CURR-DATE
  2 #CURR-DATE-YYYY (N4)
  2 #CURR-DATE-MMDD (N4)
  2 REDEFINE #CURR-DATE-MMDD
    3 #CURR-DATE-MM  (N2)
    3 #CURR-DATE-DD  (N2)
*
1 #YYYYMMDD         (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY           (N4)
  2 #MMDD           (N4)
  2 REDEFINE #MMDD
    3 #MM            (N2)
    3 #DD            (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A        (A4)
  2 #MM-A          (A2)
  2 #DD-A          (A2)
*
1 #SECOND-CN (N6)                        /* bz1
1 tst_CYC  (N2)
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
FOR #I1 1 TO 10                                   /* Chg DEP DOB from 0229 to 0301    DW28
  MOVE #CGA-DEP-DOB (#I1) TO #YYYYMMDD
  IF #MMDD = 0229
    MOVE 0301 TO #MMDD
    MOVE #YYYYMMDD TO #CGA-DEP-DOB (#I1)
  END-IF
END-FOR                                            /* END DW28
*
IF   (#CGA-SERV-CONN = 'D')                        /* DW8
    AND (#CGA-MBR-SPECIAL-CODE = ' ')
  MOVE 'N' TO #CGA-MBR-SPECIAL-CODE                /* TO ind LOD No
END-IF
*
IF #CGA-DOD = 0
  MOVE #CGA-AS-OF-DT TO #CGA-DOD
END-IF
*
*
* * * * * * GET CURRENT TOTAL INSURANCE AMTS * * * * * * * * *
IF   (#CGA-TOT-INS = 0)
    AND (#CGA-ID-G NE '000000')
  FIND (1) FR-V WITH MEMBER-KEY = #CGA-ID-G
    IF NO RECORDS
      FIND (1) FR-V WITH WIDOW-KEY = #CGA-ID-G
        IF NO RECORDS
          ESCAPE BOTTOM
        END-NOREC
      END-FIND
    END-NOREC
  END-FIND
*
  GET ST-V #CGA-ISN
*
  IF *DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX'       /*  bz2_2
    IF #CGA-DOD LT *DATN
      MOVE #CGA-DOD TO #CURR-DATE  
    ELSE
      MOVE *DATN TO #CURR-DATE                      /* GET INS AMTS
    END-IF
*
    MOVE ST-V.MILITARY-STATUS TO #P-MILITARY-STAT   /* USING CURRENT INFO
    MOVE TRUE TO #CURR-INS-CALCS
    PERFORM CALC-TOTAL-INS
    MOVE #CGA-TOT-INS TO #CURR-TOT-INS
    RESET #CGA-TOT-INS
    GET ST-V #CGA-ISN                 /* to reset face-amount if LT
  END-IF
*
  IF #CGA-DOD LT #CGA-AS-OF-DT
    MOVE #CGA-DOD TO #CURR-DATE   /* DW7
  ELSE
    MOVE #CGA-AS-OF-DT TO #CURR-DATE         /* USE OVERRIDE ASSUMPTIONS
  END-IF
*
  MOVE #CGA-MIL-STAT TO #P-MILITARY-STAT
  RESET #CURR-INS-CALCS
  PERFORM CALC-TOTAL-INS
*
  IF   (*DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX')         /* bz2_2
      AND (#CURR-TOT-INS NE #CGA-TOT-INS)
      AND (#CGA-ORIG-COMMAND NE 'BS')
      AND (#CGA-ORIG-SUFFIX NE 'RET')
    FOR #I1 1 TO 6
      IF #INS-CHANGED (#I1)
        IF #INS-CHG-MSG = ' '
          MOVE #INS-TYPE (#I1) TO #INS-CHG-MSG
        ELSE
          COMPRESS #INS-CHG-MSG '/' #INS-TYPE (#I1)
            INTO #INS-CHG-MSG LEAVING NO
        END-IF
      END-IF
    END-FOR
*
    COMPRESS #INS-CHG-MSG 'INS AMTS CHANGE DUE TO ASSUMPTIONS'
      INTO #CGA-BA-ERR-MSG
  END-IF
END-IF
* * * * * * * * * * * * * * * * *
*
IF #CGA-SCR-SUFFIX = 'EDU'                          /* similar logic in d3160pba/340pba/3660prp
    OR = 'EDF'                                      /* edu within ad fir series
    OR = 'EDM'                                      /* edu within MBA process
  MOVE 'A' TO #SRT-SORT-TYPE                        /* shift sequence of kids
  MOVE #CGA-SCN-NUM-DEP TO #SRT-NUM-ENTRIES
  CALLNAT 'SAGSRT9' #CGA-DEP-TABLE(*) #SRT-PARM     /* SAG RH1 11252005 DW23
*
  IF #CGA-PORTFOLIO-AMT NE 0                        /* for BAG/SPN/CYC processing
    MOVE #CGA-PORTFOLIO-AMT TO #CGA-TOT-INV
  ELSE
    RESET #CGA-WAGE-YR (*) #CGA-WAGE-AMT (*)
*
    FIND (1) FI-V WITH ID-NUMBER = #CGA-ID
      MOVE *ISN TO #PASSED-FIN-ISN
    END-FIND
*
    IF  (FI-V.C*INV-GEN-TBL NE 0)
        OR (FI-V.C*INV-MUTUAL-STOCKS-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL NE 0)
        OR (FI-V.C*INV-E-BONDS-TBL-2 NE 0)
      CALLNAT 'D3281SFD'
        #CGA-SCR-SUFFIX
        #PASSED-FD-TYPE
        #PASSED-FIN-ISN
        #CGA-ISN
        #CGA-ID
        #PASSED-TOT-INV
        #PASSED-INV (1:7)
        #PASSED-WAR
        #PASSED-MTG-DEBT
        #PASSED-RET-INV
      MOVE #PASSED-TOT-INV TO #CGA-TOT-INV
*
      MOVE #PASSED-WAR-N    TO #CGA-WAGE-YR  (60)
    END-IF
  END-IF
*
  MOVE 'D3185PBA' TO #CGA-NEXT-PROGRAM
  ESCAPE ROUTINE
END-IF
*
IF  NOT #CGA-ORIG-COMMAND = 'SS' OR = 'SO'
  RESET #CGA-SS-PIA-WAS-ENTERED
  IF #CGA-SS-PIA NE 0
    MOVE TRUE TO #CGA-SS-PIA-WAS-ENTERED
  END-IF
*
  RESET #CGA-OFFSET-PIA-WAS-ENTERED
  IF #CGA-OFFSET-PIA NE 0
    MOVE TRUE TO #CGA-OFFSET-PIA-WAS-ENTERED
  END-IF
END-IF
*
IF #CGA-SBP-OPTION = 'O' OR = 'X'   /* RESET OPEN & DISENROLL SBP OPT
  MOVE 'N' TO #CGA-SBP-OPTION
END-IF
*
IF #CGA-O-E-SBP-OPT = 'O' OR = 'X' /* RESET OPEN & DISENROLL SBP OPT
  MOVE 'N' TO #CGA-O-E-SBP-OPT
END-IF
*
IF #CGA-FMR-HUSBAND-SBP-OPTION = 'O' /* RESET OPEN SBP OPT
  MOVE 'N' TO #CGA-FMR-HUSBAND-SBP-OPTION
END-IF
*
IF  (#CGA-AS-OF-DT LT 19930101)
    OR (#CGA-DOD LT 19930101)
  IF   (#CGA-MBR-SPECIAL-CODE = 'Y')      /* INDICATES A CHIEF-OF-STAFF
      AND (#CGA-RANK = 'GEN')
      AND (#CGA-SERV-CONN = 'Y' OR = 'C')  /* DW5
    MOVE #VA-DIC (1) TO #CGA-ENTERED-VA-DIC-AMT /* FORCE HIGHEST VADIC
  END-IF
*
  IF   (#CGA-RANK = 'SMA')            /* SGT MAJOR OF ARMY
      AND (#CGA-SERV-CONN = 'Y' OR = 'C')  /* DW5
    MOVE #E10/SMA TO #CGA-ENTERED-VA-DIC-AMT   /* FORCE SMA
  END-IF
END-IF
*
* * * * * * * CHECK FOR NATL GUARD/RESERVE * * * * * * * * *
*
IF   (#CGA-MIL-SERV = 'AR ' OR = 'AG ' OR = 'AGA' OR = 'ARA'
    OR = 'FR ' OR = 'FG ' OR = 'FGA' OR = 'FRA'
    OR = 'MR ' OR = 'NR ' OR = 'CGR'
    OR = 'MRA' OR = 'NRA' OR = 'CGA')
  MOVE TRUE TO #CGA-GUARD-RESV
ELSE
  RESET #CGA-GUARD-RESV
END-IF
*
IF   (#CGA-MIL-SERV = 'AGA' OR = 'ARA'
    OR = 'FGA' OR = 'FRA'
    OR = 'MRA' OR = 'NRA' OR = 'CGA')
    AND (((#CGA-AS-OF-DT - #CGA-BASD) GE 200000)
    OR  ((#CGA-AS-OF-DT - #CGA-BASD) GE 150000)
    AND (#CGA-MBR-SPECIAL-CODE = 'U'))
 AND (#CGA-MF-SCR-COMMAND NE '7') /* RESV IDT Case    DW26
  RESET #CGA-GUARD-RESV
  RESET #CGA-SSBP-PCT
END-IF
*
* * * * MOVE CURR F SPOUSE TO FORMER SPOUSE  * * * * * * * *
*
IF   (#CGA-CUR-SPS-STAT = 'F')
    AND (#CGA-FMR-SPS-DOB  = 0)
  MOVE #CGA-CUR-SPS-STAT             TO #CGA-FMR-SPS-STAT
  MOVE #CGA-CUR-SPS-DOB              TO #CGA-FMR-SPS-DOB
  MOVE #CGA-CUR-SPS-NAME             TO #CGA-FMR-SPS-NAME
  MOVE #CGA-CUR-SPS-SSA-OPTION       TO #CGA-FMR-SPS-SSA-OPTION
  MOVE #CGA-CUR-SPS-DATE-OF-MARRIAGE TO #CGA-FMR-SPS-DATE-OF-MARRIAGE
  MOVE #CGA-CUR-SPS-DATE-OF-DIVORCE  TO #CGA-FMR-SPS-DATE-OF-DIVORCE
  RESET #CGA-CUR-SPS-STAT
  RESET #CGA-CUR-SPS-DOB
  RESET #CGA-CUR-SPS-NAME
  RESET #CGA-CUR-SPS-SSA-OPTION
  RESET #CGA-CUR-SPS-DATE-OF-MARRIAGE
  RESET #CGA-CUR-SPS-DATE-OF-DIVORCE
END-IF
*
* BELOW LOGIC FOR PRIOR-KIDS AND FMR SPS IS ALSO IN D3150PBA/D3152/D3154
*
FOR #I1 1 TO #NP                                          /* NG1
  IF   (#CGA-DEP-STAT (#I1) = 'P' OR = 'Y')
    IF  (#CGA-DEP-DOB (#I1) + 260000 GT #CGA-AS-OF-DT)    /* WAS 22 TM1
        OR (#CGA-DEP-STAT (#I1) = 'Y')
      MOVE TRUE TO #PRIOR-KIDS-EXIST
    END-IF
  ELSE
    IF  (#CGA-DEP-DOB (#I1) + 260000 GT #CGA-AS-OF-DT)    /* WAS 22 TM1
        OR (#CGA-DEP-STAT (#I1) = 'I' OR = 'J' OR = 'K' OR = 'L')
      MOVE TRUE TO #CURRENT-KIDS-EXIST
    END-IF
  END-IF
END-FOR
*
IF   (#CGA-FMR-SPS-DOB NE 0)                        /* FORMER SPS
    AND (NOT #CGA-FMR-SPS-STAT = 'D' OR = ' ')      /* NOT DEAD
  IF   (#CGA-SBP-OPTION = 'E' OR = 'F')             /* FMR SPS SBP OPTION
      OR  (#CGA-FMR-SPS-DATE-OF-MARRIAGE = 0)       /* MISSING FMR DOM
      OR  (#CGA-FMR-SPS-DATE-OF-DIVORCE  = 0)       /* MISSING FMR DODV
      OR  ((#CGA-FMR-SPS-DATE-OF-MARRIAGE NE 0)     /* NOT MARRIED 10 YRS
      AND (#CGA-FMR-SPS-DATE-OF-DIVORCE NE 0)       /* AND NO PRIOR KIDS
      AND (#CGA-FMR-SPS-DATE-OF-DIVORCE - #CGA-FMR-SPS-DATE-OF-MARRIAGE
      GE 100000))
    MOVE TRUE TO #FMR-SPS-ELIGIBLE
  END-IF
END-IF
*
* ABOVE LOGIC FOR PRIOR-KIDS AND FMR SPS IS ALSO IN D3150PBA/D3152/D3154
*
* DISPLAY #CGA-CUR-SPS-DOB #PRIOR-KIDS-EXIST
IF  (NOT (#CGA-ORIG-COMMAND = 'RP' OR = 'SI' OR = 'SU' OR = 'BS'))
    AND ((NOT #CGA-CUR-SPS-PROCESS) AND (NOT #CGA-FMR-SPS-PROCESS))
    AND (*DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX' )    /* IF ONLINE AND 1ST TIME     /* bz2_2
    AND (NOT #CGA-SCR-SUFFIX = 'DC' OR = 'PAY')
    AND (#CGA-ORIG-SUFFIX NE 'SBW')                       /* cur/fmr already loaded    /* DW24a
    AND (((#CGA-CUR-SPS-DOB NE 0)                      /* AND BOTH CURRENT AND FORMER SPOUSES
    AND (#CGA-FMR-SPS-DOB NE 0))
    AND (#FMR-SPS-ELIGIBLE)                            /* FORMER SPS ELIGIBLE
    OR ((#CGA-CUR-SPS-DOB NE 0) AND #PRIOR-KIDS-EXIST) /* COMBINATION
    OR (#FMR-SPS-ELIGIBLE AND #CURRENT-KIDS-EXIST)     /* COMBINATION
    OR (#FMR-SPS-ELIGIBLE)                             /* Only Former /* DW3
    OR (#PRIOR-KIDS-EXIST AND #CURRENT-KIDS-EXIST))    /* COMBINATION
  MOVE '*' TO #CGA-SCR-COMMAND
  IF #CGA-CLERK = 'BZX' OR #CGA-CLERK = 'BZP'               /* bz2
     DECIDE ON FIRST VALUE #CGA-SBP-OPTION                  /* bz2 
       VALUE 'E' , 'F'  #CGA-FMR-SPS-PROCESS := TRUE        /* bz2
       VALUE 'A' , 'B'  #CGA-CUR-SPS-PROCESS := TRUE        /* bz2
       NONE IGNORE                                          /* bz2
     END-DECIDE                                             /* bz2
   ELSE
       INPUT TEXT #CGA-BA-ERR-MSG
         MARK *#SCR-CUR-SPS-IND USING MAP 'D3400MBA'
*
      IF #CGA-SCR-COMMAND NE  '*'
        IF #CGA-SCR-COMMAND = 'RF'
          MOVE 'D3000PBA' TO #CGA-NEXT-PROGRAM
          ESCAPE ROUTINE
        ELSE
          FETCH 'G1000PXX'
        END-IF
      END-IF
*
      IF (#SCR-CUR-SPS-IND NE ' ') AND (#SCR-FMR-SPS-IND = ' ')
        MOVE TRUE TO #CGA-CUR-SPS-PROCESS
      ELSE
        IF (#SCR-FMR-SPS-IND NE ' ') AND (#SCR-CUR-SPS-IND = ' ')
          MOVE TRUE TO #CGA-FMR-SPS-PROCESS
        ELSE
          REINPUT 'SELECT EITHER CURRENT OR FORMER SPOUSE/DEPENDENTS'
            MARK *#SCR-CUR-SPS-IND
        END-IF
      END-IF

   END-IF                                                        /* bz2
    
ELSE
  IF *DEVICE = 'BATCH' AND #CGA-CLERK <> 'BZX'                            /* IF BATCH   /*  bz2_2
*   IF #PRIOR-KIDS-EXIST OR #FMR-SPS-ELIGIBLE   /* TO COUNT THESE IN TAS
*     IF #CGA-SECURITY-CODE LT 40               /* ONLY THE 1ST TIME
*       ADD 40 TO #CGA-SECURITY-CODE /* removed 2/93 replaced by invests
*     END-IF
*   END-IF
*
    IF #CGA-FMR-SPS-PROCESS              /* IF 2ND TIME AROUND IN BATCH
      IGNORE
    ELSE
*     IF   (#CGA-CUR-SPS-DOB = 0)        /* NO CURRENT SPS
*      AND (NOT #CURRENT-KIDS-EXIST)     /* NO CURRENT KIDS
*      AND (#FMR-SPS-ELIGIBLE OR #PRIOR-KIDS-EXIST) /* FMR OR PRIOR
*       MOVE TRUE TO #CGA-FMR-SPS-PROCESS      /* DO FMR SPS
*     ELSE
      MOVE TRUE TO #CGA-CUR-SPS-PROCESS /* ALWAYS DO CURRENT IN BATCH
*     END-IF
    END-IF
  ELSE
    IF  (#CGA-ORIG-COMMAND = 'BS')   /* IF DOING SBP CALCS
        OR ((#CGA-ORIG-COMMAND = 'RP')
        AND (#CGA-ORIG-SUFFIX = 'DC' OR = 'SBU'))
      IF #CGA-SBP-OPTION = 'E' OR = 'F'
        MOVE TRUE TO #CGA-FMR-SPS-PROCESS
      ELSE
        MOVE TRUE TO #CGA-CUR-SPS-PROCESS
      END-IF
    ELSE
      IF (NOT #CGA-ORIG-SUFFIX = 'RET' OR = 'TML') /* NOT DOING SERIES
          AND (#CGA-ORIG-SUFFIX NE 'SBW') /* cur/fmr already loaded  DW24a
          AND (#FMR-SPS-ELIGIBLE               /* IF FORMER SPOUSE
          OR #PRIOR-KIDS-EXIST)               /* IF PRIOR KIDS
        MOVE TRUE TO #CGA-FMR-SPS-PROCESS
      ELSE
        IF #CGA-FMR-SPS-PROCESS     /* IF 2ND TIME AROUND
          IGNORE
        ELSE
          MOVE TRUE TO #CGA-CUR-SPS-PROCESS
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
* * * * * * * SETUP SPOUSE TO PROCESS * * * * *
*
IF #CGA-CUR-SPS-PROCESS
  MOVE #CGA-CUR-SPS-NAME             TO #CGA-SPS-NAME
  MOVE #CGA-CUR-SPS-DOB              TO #CGA-SPS-DOB
  MOVE #CGA-CUR-SPS-STAT             TO #CGA-SPS-STAT
  MOVE #CGA-CUR-SPS-SSA-OPTION       TO #CGA-SSA-OPTION
  MOVE #CGA-CUR-SPS-DATE-OF-MARRIAGE TO #CGA-DATE-OF-MARRIAGE
  MOVE #CGA-CUR-SPS-DATE-OF-DIVORCE  TO #CGA-DATE-OF-DIVORCE
ELSE
  MOVE #CGA-FMR-SPS-NAME             TO #CGA-SPS-NAME
  MOVE #CGA-FMR-SPS-DOB              TO #CGA-SPS-DOB
  MOVE #CGA-FMR-SPS-STAT             TO #CGA-SPS-STAT
  MOVE #CGA-FMR-SPS-SSA-OPTION       TO #CGA-SSA-OPTION
  MOVE #CGA-FMR-SPS-DATE-OF-MARRIAGE TO #CGA-DATE-OF-MARRIAGE
  MOVE #CGA-FMR-SPS-DATE-OF-DIVORCE  TO #CGA-DATE-OF-DIVORCE
  RESET #CGA-RSFPP
  RESET #CGA-CIVILIAN-SBP
  RESET #CGA-CS-SBP
  RESET #CGA-ENTERED-WID-BENEFIT
  RESET #CGA-FMR-HUSBAND-NAME
  RESET #CGA-FMR-HUSBAND-RANK
  RESET #CGA-FMR-HUSBAND-SERV-CONN
  RESET #CGA-FMR-HUSBAND-SBP-AMT
  RESET #CGA-FMR-HUSBAND-SS-PIA
  RESET #CGA-FMR-HUSBAND-OFFSET-PIA
  RESET #CGA-FMR-HUSBAND-DOD
END-IF
*
MOVE #CGA-CUR-SPS-SBP-AMT TO #CGA-SBP-AMT  /* SET HARD-CODED SBP-AMT
IF  (#CGA-ORIG-COMMAND = 'BS')  /* orig amt needed for 1992 added
    OR ((#CGA-ORIG-COMMAND = 'RP')  /* cost calcs
    AND (#CGA-ORIG-SUFFIX = 'DC' OR = 'SBU'))
    OR (#CGA-ORIG-SUFFIX = 'RET')       /* added 4/95 as per bob
  IGNORE            /* let original sbp be processed
ELSE
  IF   (#CGA-O-E-DATE NE 0)  /* IF 2 YRS PAST 92/99 OPEN-ENROLL DATE
      AND (#CGA-AS-OF-DT GE #CGA-O-E-DATE + 20000)
      AND (#CGA-DOD      GE #CGA-O-E-DATE + 20000)
    IF #CGA-O-E-SBP-OPT NE ' '
      MOVE #CGA-O-E-SBP-OPT TO #CGA-SBP-OPTION
    END-IF
*
    IF #CGA-O-E-TYPE-ANN NE ' '
      MOVE #CGA-O-E-TYPE-ANN TO #CGA-RSV-NG-TYPE-ANNUITY
    END-IF
*
    IF #CGA-O-E-SBP-AMT NE 0
      MOVE #CGA-O-E-SBP-AMT  TO #CGA-SBP-AMT
    END-IF
  END-IF
*
  IF   (#CGA-99-O-E-DATE NE 0) /* IF 2 YRS PAST 2005 OPEN-ENR DATE
      AND (#CGA-AS-OF-DT GE #CGA-99-O-E-DATE + 20000)
      AND (#CGA-DOD      GE #CGA-99-O-E-DATE + 20000)
    IF #CGA-99-O-E-SBP-OPT NE ' '       /* calc
      MOVE #CGA-99-O-E-SBP-OPT TO #CGA-SBP-OPTION
    END-IF
*
    IF #CGA-99-SSBP-PCT NE 0
      MOVE #CGA-99-SSBP-PCT TO #CGA-SSBP-PCT
    END-IF
*
    IF #CGA-99-TYPE-ANN NE ' '
      MOVE #CGA-99-TYPE-ANN     TO #CGA-RSV-NG-TYPE-ANNUITY
    END-IF
*
    MOVE #CGA-99-O-E-SBP-AMT  TO #CGA-SBP-AMT
  END-IF
END-IF
*
IF   (#CGA-ORIG-COMMAND = 'RP' OR = 'SU' OR = 'SI')
    AND (#CGA-BA-ERR-MSG NE MASK ('HARD-CODED'))   /* ONLY ALLOWED MSG
    AND (#CGA-BA-ERR-MSG NE MASK ('YTD ACTIVE'))   /* ONLY ALLOWED MSG
  RESET #CGA-BA-ERR-MSG
END-IF
*
* * * * * * * DISPLAY WIDOW WORKING MESSAGE * * * * * * * * * * * *
*
IF (#CGA-CUR-SPS-STAT = 'W') /* IF STILL WORKING - ONLY WIDOWS
    AND (#CGA-CUR-SPS-DOB + 720000 GT #CGA-AS-OF-DT)  /* AND UNDER 72
  IF #CGA-ORIG-COMMAND NE 'BS'
    MOVE 'SPOUSE WORKING - NOT ELIGIBLE FOR SOC. SEC.'
      TO #CGA-BA-ERR-MSG
  END-IF
  MOVE 1 TO #CGA-ENTERED-WID-BENEFIT    /* INDICATE USE ZERO
END-IF
*
* * * * * * * GET CURRENT AND FORMER RANK DISPLACEMENT INDEX * * *
RESET #CGA-MF-ORIG-PARM-2                                     /* DW10
RESET #CGA-RANK-DISPL #RANK-LIT #RANK-DISPLAY                     /* PM1
CALLNAT 'GRANKSXX' #CGA-ORIG-COMMAND #CGA-RANK #CGA-MIL-SERV
  #INVALID #CGA-RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
*     setup default PEBD/BASD/1405
IF #CGA-PEBD = 0
  MOVE #CGA-BASD TO #CGA-PEBD
END-IF
*
IF #CGA-BASD = 0
  MOVE #CGA-PEBD TO #CGA-BASD
END-IF
*
IF #CGA-1405-DT = 0
  IF   (#CGA-PEBD LE 19580531)
      AND (#CGA-BASD LE 19580531)
      AND (#CGA-RANK-DISPL LE 11)           /* DW11
      AND (#CGA-RANK-DISPL GE 1)            /* DW11
    MOVE #CGA-PEBD TO #CGA-1405-DT
      #CGA-1405-DT-2-TIER
  ELSE
    MOVE #CGA-BASD TO #CGA-1405-DT
      #CGA-1405-DT-2-TIER
  END-IF
END-IF
*
IF (#CGA-DIEMS = 0) AND (#CGA-PEBD GE 19800908)  /* DW15
  MOVE #CGA-PEBD TO #CGA-DIEMS
END-IF
*
IF #INVALID OR (#CGA-RANK-DISPL = 99)
  RESET #CGA-RANK-DISPL
ELSE
*
  IF   (#CGA-MBR-SPECIAL-CODE = 'F' OR = 'C')   /* INDICATES FROCKED
      AND (NOT (#CGA-RANK-DISPL = 11 OR = 19 OR = 28 OR = 29 OR = 30
      OR = 14))                        /* DW9
    ADD 1 TO #CGA-RANK-DISPL         /* shift displ to next rank
  END-IF
*
  IF   (#CGA-DT-PROM-TO-CUR-RANK LE 19810914) /* dopma date  see d3070
      AND (#CGA-DT-PROM-TO-CUR-RANK NE 0)    /* this logic also in d3660
      AND (#CGA-DT-PROM-TO-CUR-RANK = #CGA-SEP-RET-DT) /* posthumous promot
      AND (NOT #CGA-GUARD-RESV)
      AND (NOT (#CGA-RANK-DISPL = 11 OR = 19 OR = 28 OR = 29 OR = 30
      OR = 14))                        /* DW9
    ADD 1 TO #CGA-RANK-DISPL         /* shift displ to next rank
  END-IF
*
  IF   (#CGA-ORIG-SUFFIX = 'SBW')       /* DW16
      AND (#CGA-PRIOR-RANK-DISPL NE 0)  /* DISPL Entered in ATHPSBSW
    MOVE #CGA-PRIOR-RANK-DISPL TO #DISPL  /* Get prior rank
    RESET #NEW-RANK
    CALLNAT 'GRANKSXX' #CGA-ORIG-COMMAND #NEW-RANK #CGA-MIL-SERV
      #INVALID #DISPL #RANK-LIT #RANK-DISPLAY
    IF #INVALID
      MOVE 'UNK' TO #NEW-RANK
    END-IF
    MOVE #NEW-RANK TO #CGA-MF-ORIG-PARM-2 /* Needed in D3079SBA/D3660SRP
  ELSE
    IF #CGA-RANK-DISPL = 11 OR = 14 OR = 19                   /* DW10
      MOVE #CGA-SCR-SUFFIX TO #HOLD-SUFF
      MOVE '340' TO #CGA-SCR-SUFFIX
      CALLNAT 'DENLSSXX'     /* get prev enlisted rank info   /* DW10
        #CGA-SCR-COMMAND
        #CGA-SCR-ID
        #CGA-SCR-SUFFIX
        #CGA-RANK-DISPL
        #CGA-DOB
        #CGA-RANK
        #CGA-DT-PROM-TO-CUR-RANK
        #CGA-DT-PROM-TO-PRV-RANK
        #CGA-AS-OF-DT
        #CGA-BASD
        #CGA-MIL-SERV
        #PREV-ENL-RANK
        #CGA-PRIOR-RANK-DISPL
        #PREV-ENL-DOP
        #PREV-PREV-ENL-DOP
      MOVE #HOLD-SUFF TO #CGA-SCR-SUFFIX
      MOVE #PREV-ENL-RANK TO #CGA-MF-ORIG-PARM-2
    ELSE
*                         /* above logic is also in D3079SBA
      IF NOT #CGA-RANK-DISPL = 11 OR = 14 OR = 19 OR = 28 OR = 29 OR = 30
        COMPUTE #CGA-PRIOR-RANK-DISPL = #CGA-RANK-DISPL + 1
      END-IF
*
      IF #CGA-RANK-DISPL = 30      /* e10
        MOVE 20 TO #CGA-PRIOR-RANK-DISPL  /*e9
      END-IF
    END-IF
  END-IF
END-IF
*
* DISPLAY RET PAY CALC CHOICE SCREEN IF APPLICABLE
*
IF  ((#CGA-MIL-STAT = 'A')                                   /* IF ACTIVE
    OR ((#CGA-MIL-STAT= 'R') AND (#CGA-AS-OF-DT GT *DATN)
    AND (#CGA-AS-OF-DT = #CGA-SEP-RET-DT)))
    AND (#CGA-DOD GE #CGA-AS-OF-DT)                          /* AND ALIVE
    AND (*DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX')           /* AND IN ONLINE  /* bz2_2
    AND (#CGA-ORIG-COMMAND = 'RP')
    AND (#CGA-MIL-DISAB-PCT LT 30)    /* DW21
    AND (#CGA-SCR-SUFFIX NE MASK ('ES'))             /* early retirement
    AND (#CGA-SCR-SUFFIX NE MASK ('15'))             /* 15 yr ret
    AND (NOT #CGA-SCR-SUFFIX = 'SVT' OR = 'SVO')     /* sell versus term leave
*    AND (#CGA-SCR-SUFFIX NE 'SBW')                  /* RP CALC IND already loaded     
    AND (#CGA-ORIG-SUFFIX NE 'SBW')                  /* RP CALC IND already loaded  DW24a
    AND (#CGA-ORIG-SUFFIX NE 'DI*')                  /* RP CALC IND already loaded  DW25
    AND (#CGA-SCR-SUFFIX NE 'CAS')                   /* Assume Dead         /* DW19
    AND (NOT #CGA-SCR-SUFFIX = 'LON'                 /* Longevity Calc      /* DW25
                       OR = 'CR*')                   /* Disability Calc     /* DW25
  INPUT MARK *#SCR-RET-IND USING MAP 'D3401MBA'
*
  IF #CGA-SCR-COMMAND = '*' OR = ' '
    IGNORE
  ELSE
    IF #CGA-SCR-COMMAND = 'RF'
      MOVE 'D3000PBA' TO #CGA-NEXT-PROGRAM
      ESCAPE ROUTINE
    ELSE
      FETCH 'G1000PXX'
    END-IF
  END-IF
*
  IF (#SCR-RET-IND NE ' ') AND (#SCR-DEAD-IND = ' ')
      AND (#SCR-DISAB-IND = ' ')                       /* DW18
    MOVE 1 TO #CGA-RETIRED-PAY-CALC-IND                /* INDICATES RETIRED
  ELSE
    IF (#SCR-DEAD-IND NE ' ') AND (#SCR-RET-IND = ' ')
        AND (#SCR-DISAB-IND = ' ')    /* DW18
      MOVE 2 TO #CGA-RETIRED-PAY-CALC-IND              /* INDICATES DEAD
    ELSE
      IF (#SCR-DISAB-IND NE ' ') AND (#SCR-RET-IND = ' ')  /* DW18
          AND (#SCR-DEAD-IND = ' ')
        MOVE 3 TO #CGA-RETIRED-PAY-CALC-IND            /* INDICATES DEAD
      ELSE
        REINPUT 'Select either RETIRED or DEATH or DISABLED'
          MARK *#SCR-RET-IND
      END-IF
    END-IF
  END-IF
ELSE
  IF   (#CGA-RANK-DISPL = 5)                    /* (BG) - DISABLED TOMBSTONE GENERAL
      AND (#CGA-SEP-RET-DT NE 0)
      AND (#CGA-DT-PROM-TO-CUR-RANK = #CGA-SEP-RET-DT)
      AND (#CGA-ORIG-SUFFIX NE 'SBW')           /* #RP-CALC-IND already loaded DW22
      AND #CGA-ORIG-SUFFIX NE 'DI*'             /* #RP-CALC-IND alrdy loaded DW25
    MOVE 1 TO #CGA-RETIRED-PAY-CALC-IND         /* TO REQUIRE 3 YRS AS BG
  ELSE
    IF   (#CGA-MIL-DISAB-PCT NE 0)              /* chg 8/95 as per jud (was and)
        AND (#CGA-ORIG-SUFFIX NE 'SBW')         /* #RP-CALC-IND already loaded DW22
        AND #CGA-ORIG-SUFFIX NE 'DI*'           /* #RP-CALC-IND alrdy loaded DW25
      MOVE 3 TO #CGA-RETIRED-PAY-CALC-IND       /* ASSUME DISABILITY
    ELSE
      IF ((#CGA-MIL-STAT = 'R')
          OR (#CGA-SCR-SUFFIX =  MASK ('ES'))   /* early retirement
          OR (#CGA-SCR-SUFFIX =  MASK ('15'))   /* 15 yr retirement
          OR (#CGA-SCR-SUFFIX =  'SVT'))        /* SEL VERSUS TERMINAL LEAVE
          AND (#CGA-ORIG-SUFFIX NE 'NAV')       /* NAVY ALWAYS ASSUMED DEATH
          AND (NOT #CGA-ORIG-SUFFIX = 'SBS' OR = 'SBW')
          AND #CGA-ORIG-SUFFIX NE 'DI*'         /* #RP-CALC-IND alrdy loded DW25
        MOVE 1 TO #CGA-RETIRED-PAY-CALC-IND      /* ASSUME RETIRED
      ELSE
        IF #CGA-ORIG-SUFFIX NE 'SBW'             /* #RP-CALC-IND already loaded
         AND #CGA-ORIG-SUFFIX NE 'DI*'           /* #RP-CALC-IND alrdy loaded DW25
          MOVE 2 TO #CGA-RETIRED-PAY-CALC-IND    /* OTHERWISE ASSUME DEAD
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF   (#CGA-MIL-STAT = 'A')  /* BELOW ADDED 8/2003 KRM
    AND (#CGA-GUARD-RESV)
    AND (#CGA-RETIRED-PAY-CALC-IND NE '1')    /* added 1/2004 krm
    AND (#CGA-MBR-SPECIAL-CODE NE 'N')        /* added 1/2004 krm
    AND (#CGA-MF-SCR-COMMAND NE '7')          /* RESV IDT Case    DW26
  RESET #CGA-GUARD-RESV
  RESET #CGA-SSBP-PCT
*
  IF   (#CGA-SERV-CONN = 'Y' OR = 'C')   /* DW5
      AND (#CGA-SBP-OPTION = ' ')
    MOVE 'B' TO #CGA-SBP-OPTION
  END-IF
END-IF
*
* * * * * * * * * * INIT CRITICAL DATES * * * * * * * *
*
* * * * * * * ADVANCE RANK TO 1LT IF PAST 2ND ANNIVERSARY OF BASD *
*
IF (#CGA-RANK = '2LT' OR = 'O1 ')
    AND (NOT #CGA-ORIG-COMMAND = 'SU' OR = 'SI')
    AND (NOT #CGA-GUARD-RESV)
    AND (#CGA-DOD GE #CGA-AS-OF-DT)    /*  ALIVE
*
  IF #CGA-DT-PROM-TO-CUR-RANK NE 0
    MOVE #CGA-DT-PROM-TO-CUR-RANK TO #DT-PROM-TO-1LT
  ELSE
    MOVE #CGA-BASD TO #DT-PROM-TO-1LT
  END-IF
*
  ADD 20000 TO #DT-PROM-TO-1LT
*
  IF #DT-PROM-TO-1LT-DD NE 01
    ADD 1 TO #DT-PROM-TO-1LT-MM
    MOVE 1 TO #DT-PROM-TO-1LT-DD
    IF #DT-PROM-TO-1LT-MM GT 12
      ADD 1 TO #DT-PROM-TO-1LT-YYYY
      MOVE 1 TO #DT-PROM-TO-1LT-MM
    END-IF
  END-IF
*
  IF #CGA-AS-OF-DT GE #DT-PROM-TO-1LT
    MOVE #DT-PROM-TO-1LT TO #CGA-DT-PROM-TO-CUR-RANK
    MOVE '1LT' TO #CGA-RANK
    MOVE 11 TO #CGA-PRIOR-RANK-DISPL
    MOVE 10 TO #CGA-RANK-DISPL
*
    IF #CGA-ORIG-COMMAND NE 'BS'
      MOVE 'RANK HAS BEEN ADVANCED TO 1LT FOR THIS SUMMARY'
        TO #CGA-BA-ERR-MSG
    END-IF
  END-IF
END-IF
*
IF #CGA-MIL-STAT = 'R'
  MOVE TRUE TO #CGA-BA-EXPL (3)           /* EXPL 03   RETIRED
ELSE
  IF #CGA-MIL-STAT = 'C'
    MOVE TRUE TO #CGA-BA-EXPL (4)         /* EXPL 04   CIVILIAN
  ELSE
    IF #CGA-BASD + 200000 LE #CGA-AS-OF-DT
      MOVE TRUE TO #CGA-BA-EXPL (2)       /* EXPL 02 - OVER 20 YRS
    ELSE
      MOVE TRUE TO #CGA-BA-EXPL (1)       /* EXPL 01  ACTIVE
      MOVE TRUE TO #CGA-BA-EXPL (19)      /* EXPL 19 - UNDER 20 YRS
    END-IF
  END-IF
END-IF
*
IF #CGA-SBP-OPTION = 'N'
  IF #CGA-MIL-STAT = 'R' OR = 'C'
    MOVE TRUE TO #CGA-BA-EXPL (20)    /* EXPL 20 - NON PARTICIPATING
  END-IF
ELSE
  IF NOT (#CGA-MIL-STAT = 'C' OR = 'R')      /* IF ACTIVE
      AND (#CGA-BASD + 200000 LE #CGA-AS-OF-DT) /* AND OVER 20 YRS SVC
    IF #CGA-SBP-OPTION = ' '
      MOVE TRUE TO #CGA-BA-EXPL (23)    /* EXPL 23 - SBP OVER 20
    END-IF
  ELSE
    IF (#CGA-MIL-STAT = 'R') AND (#CGA-SBP-OPTION = ' ')
        AND (#CGA-SBP-AMT = 0)
        AND (#CGA-MBR-SPECIAL-CODE NE 'X') /* Recalled to Act duty   DW20
      MOVE TRUE TO #CGA-RET-NO-SBP-ELECTION
      MOVE TRUE TO #CGA-BA-EXPL (21)    /* EXPL 21 - RET NO SBP ELCT
    END-IF
  END-IF
END-IF
*
IF #CGA-SERV-CONN = 'Y' OR = 'C'   /* DW5
  MOVE TRUE TO #CGA-BA-EXPL (36)        /* EXPL 36 - SERV CONN
END-IF
*
IF #CGA-SERV-CONN = 'N' OR = 'D'
  MOVE TRUE TO #CGA-BA-EXPL (36)     /* EXPL 35 - NON SERV CONN
END-IF
*
RESET #HANDI-KIDS
FOR #I1 1 TO #NP                     /* NG1
  IF (#CGA-DEP-STAT (#I1) = 'I' OR = 'J' OR = 'K' OR = 'L' OR = 'Y')
    MOVE TRUE TO #HANDI-KIDS
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF  (#CGA-SPS-STAT = 'D')
    OR (#CGA-SPS-NAME = 'NONE' OR = ' ')
  RESET     #CGA-CS-SBP
    #CGA-CIVILIAN-SBP
*
  IF NOT #HANDI-KIDS
    IF #CGA-ORIG-COMMAND NE 'BS'
      RESET #CGA-RSFPP
    END-IF
  END-IF
END-IF
*
COMPUTE ROUNDED #CGA-INT-PER-MONTH = (#CGA-TOT-INS * .08) / 12
*
IF   (#CGA-ORIG-COMMAND = 'WO' OR = 'WB')
    AND (#CGA-SPS-STAT = 'R' OR = 'W')        /* WIDOW REMARRIED
  IF   (#CGA-SPS-DOB NE 0) AND (#CGA-WID-REMARRY-DATE NE 0)
    IF #CGA-SPS-DOB + 600000 GT #CGA-WID-REMARRY-DATE /* remarried befor
      MOVE 0 TO #CGA-RSFPP                             /* she was 60
    END-IF
    IF #CGA-WID-REMARRY-DATE GE 19861114
      IF #CGA-SPS-DOB + 550000 LE #CGA-WID-REMARRY-DATE
        MOVE TRUE TO #CGA-WID-REMAR-AFTER-55
      END-IF
    ELSE
      IF #CGA-SPS-DOB + 600000 LE #CGA-WID-REMARRY-DATE
        MOVE TRUE TO #CGA-WID-REMAR-AFTER-55
      END-IF
    END-IF
  ELSE
    MOVE 0 TO #CGA-RSFPP
  END-IF
END-IF
*
IF   (NOT #CGA-WID-REMAR-AFTER-55)
    AND (#CGA-SPS-STAT = 'R' OR = 'W')
  RESET #CGA-CS-SBP
END-IF
*
IF #CGA-RET-PAY = 0
  IF #CGA-SEP-RET-DT GT #CGA-AS-OF-DT
    IF #CGA-AS-OF-DT LT 19400101
      MOVE TRUE TO #CGA-BYP-PT-RET-DT-BEFORE-1940
    ELSE
      IGNORE
    END-IF
  ELSE
    IF #CGA-SEP-RET-DT NE 0
        AND #CGA-SEP-RET-DT LT 19400101
      MOVE TRUE TO #CGA-BYP-PT-RET-DT-BEFORE-1940
    END-IF
  END-IF
END-IF
*
* * * * * * * * * * SCRUNCH DEAD DEPENDANTS * * * * * * *
MOVE 0 TO #I2
FOR #I1 1 TO #NP                       /* NG1
  IF   (#CGA-DEP-NAME (#I1) NE ' ')
      AND (#CGA-DEP-STAT (#I1) NE 'D')
    ADD 1 TO #I2
    MOVE #CGA-DEP-NAME (#I1) TO #CGA-DEP-NAME (#I2)
    MOVE #CGA-DEP-STAT (#I1) TO #CGA-DEP-STAT (#I2)
    MOVE #CGA-DEP-DOB  (#I1) TO #CGA-DEP-DOB  (#I2)
  END-IF
END-FOR /*(0972)
MOVE #I2 TO #CGA-SCN-NUM-DEP
* * * * * * * * * * CLEAR OUT REMAINING DEPENDENTS
*                                         NG1  \/\/
IF #I2 LT #NP
  RESET #CGA-DEP-NAME (#I2 + 1:#NP)
  RESET #CGA-DEP-STAT (#I2 + 1:#NP)
  RESET #CGA-DEP-DOB  (#I2 + 1:#NP)
END-IF
*                                         NG1  /\/\
* * * * * * * * * * CLEAR OUT REMAINING DEPENDENTS
*
* SORT THE 10 DEPENDENTS BY DOB FOR D3070PBA SO YOUNGEST DEPENDANT IS
* LAST FOR AGE 16 LOGIC
*
MOVE #CGA-SCN-NUM-DEP TO #SRT-NUM-ENTRIES
* CALL 'WAASSRT1' #CGA-DEP-DOB (1) #SRT-PARM    /* DW23
CALLNAT 'SAGSRT9' #CGA-DEP-TABLE(*) #SRT-PARM     /* SAG RH1 11252005 DW23
*
IF #CGA-ORIG-COMMAND = 'ED'
  MOVE 'D3185PBA' TO #CGA-NEXT-PROGRAM
  ESCAPE ROUTINE
END-IF
*
MOVE 'D3070PBA' TO #CGA-NEXT-PROGRAM
ESCAPE ROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-TOTAL-INS
* * * * * * * * * * * * * * * * *
* IF #CGA-CLERK <> 'BZX' PRINT NOTITLE 'Keith"s test New calculation' // 'Press Enter' END-IF   /*   --db

RESET #CGA-PORTFOLIO-AMT #SECOND-CN  #PDA-CVALUE
IF #CGA-DOD LT *DATN
  MOVE #CGA-DOD TO #CURR-DATE   /* DW7
ELSE
  MOVE *DATN TO #CURR-DATE  /* use system date for AAFMAA ins
END-IF
*
MOVE OTHER-COMPANY-ID TO #FIND-LINK-N
IF   (ST-V.PROCESS-IND = 'S')
    AND (ST-V.OTHER-COMPANY-ID NE 0)
    AND ST-V.DATA-SHARED-IND NE 'N'
    AND (#FIND-LINK-N GT 1900)
  FIND (1) ST-V WITH MEMBER-KEY = #FIND-LINK
  END-FIND
END-IF
*
*
*  #AS-OF-YYYYMMDD := #CGA-AS-OF-DT
  #AS-OF-YYYYMMDD := *DATN
  #SUFFIX := 'NTP'
  #CN := #CGA-ID
  tst_CYC := 0

*
TWO_CN.
REPEAT  /* UNTIL #SECOND-CN = 0         /*   bz1
    tst_CYC := tst_CYC + 1
  IF #SECOND-CN <> 0
    RESET  #PDA-CVALUE
    #CN := ST-V.ID-NUMBER
  END-IF
CALLNAT 'CVALUSXX'     #PDA-CVALUE
* PRINT NOTITLE                                                    /*  --db
*       'CN:' #CN  '- - - - - - - - -' 'For Cycle:' tst_CYC        /*  --db
*       / #FACE-AMT (*) / #DB (*)                                  /*  --db

FOR #I1 1 TO 9
  IF  (STATUS (#I1) NE 'D')
      OR (FIRS-LOCATION-CODE (#I1) = 'X' OR = '&' OR = '1' OR = '3'
      OR = '5' OR = '6')
      OR (PLAN (#I1) = 'SO')
      OR ((#CGA-ID = 59841) AND (ISSUE-DATE (#I1) = 19970903))
      OR ((#CGA-ID = 59841) AND (ISSUE-DATE (#I1) = 20000301))
      OR (#CGA-ORIG-COMMAND = 'WB' OR = 'WO')         /* DW12
    ESCAPE TOP
  ELSE
*  IF #CGA-CLERK <> 'BZX' PRINT 'Accum:' #CGA-TOT-INS  ' +  DB:' #DB(#I1) 'Plan:' PLAN (#I1) END-IF  /*  --db
      #CGA-TOT-INS := #CGA-TOT-INS +  #DB (#I1)
      
  END-IF    
END-FOR
   IF #SECOND-CN <> 0 ESCAPE BOTTOM (TWO_CN.) END-IF
   #SECOND-CN := FUNCTION-SECONDARY-CN (< #CGA-ID >) (1)     /*  bz1
*   PRINT NOTITLE  #CGA-ID '=>'   #SECOND-CN                 /*   --db
   IF #SECOND-CN = 0                                         /*  bz1
      ESCAPE BOTTOM (TWO_CN.)                                /*  bz1
   ELSE                                                      /*  bz1
      FIND (1) ST-V WITH ID-NUMBER = #SECOND-CN  END-FIND    /*  bz1
   END-IF                                                    /*  bz1
END-REPEAT                                                   /*  bz1
IF #SECOND-CN <> 0                                           /*  bz1
      GET ST-V #CGA-ISN                                      /*  bz1
END-IF                                                       /*  bz1
*   EJECT                                                    /*   --db*
IF #CGA-DOD LT #CGA-AS-OF-DT
  MOVE #CGA-DOD TO #CURR-DATE   /* DW7
ELSE
  MOVE #CGA-AS-OF-DT TO #CURR-DATE    /* USE OVERRIDE ASSUMPTIONS
END-IF
MOVE #CGA-DOB TO #YYYYMMDD
*
IF #MMDD GT #CURR-DATE-MMDD
  ADD 1 TO #YYYY
END-IF
*
COMPUTE #P-AGE = #CURR-DATE-YYYY - #YYYY
MOVE #CGA-DOB         TO #P-DATE-OF-BIRTH
MOVE #CGA-SEP-RET-DT  TO #P-DATE-RETIRED
MOVE #CGA-MIL-SERV    TO #P-MILITARY-SERV
MOVE #CGA-RANK        TO #P-RANK
*
IF #CGA-RANK = 'CDT'
  MOVE 'Y'            TO #P-CADET-FLAG
ELSE
  MOVE 'N'            TO #P-CADET-FLAG
END-IF
*
RESET #CGA-MF-SCR-PARM-1-N
FOR #I1 1 TO 45
  IF  (FR-V.POL-NAME (#I1) = ' ')
      OR ((FR-V.POL-NAME (#I1) = 'SGLI' OR = 'VGLI')
      AND (#CGA-MBR-SPECIAL-CODE = 'A'))  /* AWAITING ACT-DUTY
    ESCAPE TOP
  END-IF
*
  MOVE FR-V.POL-NAME    (#I1) TO #P-NAME
  MOVE FR-V.POL-TYPE    (#I1) TO #P-TYPE
  MOVE FR-V.POL-AMOUNT  (#I1) TO #P-AMOUNT
  MOVE FR-V.POL-DATE    (#I1) TO #P-DATE
  MOVE FR-V.POL-REMARKS (#I1) TO #P-REMARKS
*
  IF FR-V.POL-NAME (#I1) = 'NSLI'
    RESET #OVER-AGE
*
    IF   (FR-V.POL-TYPE (#I1) = 'ML65')
        AND ((#CGA-DOB + 650000) LE #CURR-DATE)
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    IF   (FR-V.POL-TYPE (#I1) = 'ML70')
        AND ((#CGA-DOB + 700000) LE #CURR-DATE)
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    IF #OVER-AGE
      DIVIDE 2 INTO #P-AMOUNT
    END-IF
  END-IF
*
  IF #P-NAME = 'AFRBA'  OR = 'AFBA' OR = 'SGLI' OR = 'VGLI'
      OR = 'AFRABA' OR = 'MBA'
    MOVE #P-MILITARY-STAT TO #HOLD-STATUS
*
    IF    (#P-NAME = 'SGLI' OR = 'VGLI')
        AND (#CGA-MBR-SPECIAL-CODE = 'X')     /* RECALLED TO ACTIVE DUTY
      MOVE 'A' TO #P-MILITARY-STAT
    END-IF
*
    CALLNAT 'E9010SDX' #CGA-ORIG-COMMAND #P-AGE #P-PARMS #P-NAME
      #P-TYPE #P-AMOUNT #P-DATE #P-REMARKS #P-ERROR-FLAG
      #CGA-MIL-DISAB-PCT ST-V.DEATH-SERVICE-CON     /* DW6
    MOVE #HOLD-STATUS TO #P-MILITARY-STAT
  END-IF
*
  IF #P-NAME = 'OBA' OR = 'USBA' OR = 'AUSA' OR = 'AFA'
    CALLNAT 'E9020SDX' #CGA-ORIG-COMMAND #P-AGE #P-PARMS #P-NAME
      #P-TYPE #P-AMOUNT #P-DATE #P-REMARKS #P-ERROR-FLAG
      #P-CADET-FLAG
  END-IF
*
  IF   #CURR-INS-CALCS
      AND (*DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX')     /* bz2_2
    DECIDE ON FIRST #P-NAME
      VALUE 'SGLI'   MOVE #P-AMOUNT TO #INS-AMTS (1)
      VALUE 'VGLI'   MOVE #P-AMOUNT TO #INS-AMTS (1)
      VALUE 'AFBA'   MOVE #P-AMOUNT TO #INS-AMTS (2)
      VALUE 'AFRBA'  MOVE #P-AMOUNT TO #INS-AMTS (2)
      VALUE 'AFRABA' MOVE #P-AMOUNT TO #INS-AMTS (2)
      VALUE 'MBA'    MOVE #P-AMOUNT TO #INS-AMTS (3)
      VALUE 'AFA'    MOVE #P-AMOUNT TO #INS-AMTS (4)
      VALUE 'OBA'    MOVE #P-AMOUNT TO #INS-AMTS (5)
      VALUE 'USBA'   MOVE #P-AMOUNT TO #INS-AMTS (6)
      NONE IGNORE
    END-DECIDE
  END-IF
*
  IF   NOT #CURR-INS-CALCS
      AND (*DEVICE NE 'BATCH' OR #CGA-CLERK = 'BZX'  )    /* bz2_2
    DECIDE ON FIRST #P-NAME
      VALUE 'SGLI'   IF #P-AMOUNT NE #INS-AMTS (1)
          MOVE TRUE TO #INS-CHANGED (1)
        END-IF
      VALUE 'VGLI'   IF #P-AMOUNT NE #INS-AMTS (1)
          MOVE TRUE TO #INS-CHANGED (1)
        END-IF
      VALUE 'AFBA'   IF #P-AMOUNT NE #INS-AMTS (2)
          MOVE TRUE TO #INS-CHANGED (2)
        END-IF
      VALUE 'AFRBA'  IF #P-AMOUNT NE #INS-AMTS (2)
          MOVE TRUE TO #INS-CHANGED (2)
        END-IF
      VALUE 'AFARBA' IF #P-AMOUNT NE #INS-AMTS (2)
          MOVE TRUE TO #INS-CHANGED (2)
        END-IF
      VALUE 'MBA'    IF #P-AMOUNT NE #INS-AMTS (3)
          MOVE TRUE TO #INS-CHANGED (3)
        END-IF
      VALUE 'AFA'    IF #P-AMOUNT NE #INS-AMTS (4)
          MOVE TRUE TO #INS-CHANGED (4)
        END-IF
      VALUE 'OBA'    IF #P-AMOUNT NE #INS-AMTS (5)
          MOVE TRUE TO #INS-CHANGED (5)
        END-IF
      VALUE 'USBA'   IF #P-AMOUNT NE #INS-AMTS (6)
          MOVE TRUE TO #INS-CHANGED (6)
        END-IF
      NONE IGNORE
    END-DECIDE
  END-IF
*
  IF  (FR-V.POL-CODE (#I1) = 'X' OR = '&' OR = '1' OR = '3'
      OR = '5' OR = '6' OR = '7' OR = '8')
      OR (#P-TYPE = 'ACCD' OR = 'TRAV' OR = 'TRVL')
    IGNORE
  ELSE
    IF   (FR-V.POL-NAME (#I1) = 'AMAA' OR = 'AAFMAA')
        AND (#P-TYPE NE 'SO')
      ADD FR-V.POL-AMT-PAID (#I1) TO #CGA-TOT-INS
    ELSE
      IF #CGA-SCR-SUFFIX = 'PRO'    /* profile
        IF #P-TYPE = 'OL'
          ADD #P-AMOUNT TO #CGA-TOT-INS
        ELSE
          ADD #P-AMOUNT TO #CGA-MF-SCR-PARM-1-N
        END-IF
      ELSE
        ADD #P-AMOUNT TO #CGA-TOT-INS
      END-IF
    END-IF
  END-IF
END-FOR
* END-IF
*
END-SUBROUTINE
*
END
