* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GDATESXX -  Date utility to calculate the difference between
*                  -  2 dates
************************************************************************
*                              REQUEST TYPES                           *
* A = number of days difference                                        *
*     #TO-DATE     = most recent date in YYYYMMDD format               *
*     #FROM-DATE   = oldest date in YYYYMMDD format                    *
*     #RETURN-CALC = Number of days difference                         *
*                                                                      *
* B = calculate month end date from month beginning date               *
*     #TO-DATE     = end of month date (calculated) in YYYYMMDD format *
*     #FROM-DATE   = beginning of month date in YYYYMMDD format        *
*     #RETURN-CALC = not used                                          *
*                                                                      *
* C = flip date from YYYYMMDD format to MMDDYYYY format                *
*     #TO-DATE     = date to be flipped in YYYYMMDD format             *
*     #FROM-DATE   = not used                                          *
*     #RETURN-CALC = new date in MMDDYYYY format                       *
*                                                                      *
* D = flip date from MMDDYYYY format to YYYYMMDD format                *
*     #TO-DATE     = date to be flipped in MMDDYYYY format             *
*     #FROM-DATE   = not used                                          *
*     #RETURN-CALC = new date in YYYYMMDD format                       *
*                                                                      *
* E = Subtract specified time (Years/Months/Days) from a supplied date *
*     #TO-DATE     = starting date in YYYYMMDD format                  *
*     #FROM-DATE   = time to subtract from date in YYYYMMDD format     *
*                  = YYYY - Number of years to subtract                *
*                  = MM   - Number of months to subtract               *
*                  = DD   - Number of days to subtract                 *
*     #RETURN-CALC = new date in YYYYMMDD format                       *
*                                                                      *
* F = Determine Day of Week (1=S,2=M,3=T,4=W,5=T,6=F,7=S)              *
*     #TO-DATE     = date to determine day of week in YYYYMMDD format *
*     #FROM-DATE   = not used                                          *
*     #RETURN-CALC = Day of Week (1,2,3,4,5,6,7)                       *
*                                                                      *
* G = unassigned                                                       *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    04282004          Date Utility Written                        *
*                                                                      *
* PAM    06252004          Added routine 'B'                           *
*                                                                      *
* PAM    07202004          Added routines 'C' and 'D'                  *
*                                                                      *
* PAM    11172004          Added routine 'E'                           *
*                                                                      *
* PAM    08092005          Added routine 'F'                           *
*                                                                      *
* PAM    01/28/2010 PM1    Created PDA and updated calendar table      *               
************************************************************************
*
DEFINE DATA
PARAMETER USING GDATEAXX                                        /* PM1
*
LOCAL
*
1 #NUMBER-OF-DAYS-IN-MONTH-TABLE
 2 #JANUARY                 (A5)  INIT <'01 31'>
 2 #FEBRUARY                (A5)  INIT <'02 28'>
 2 #MARCH                   (A5)  INIT <'03 31'>
 2 #APRIL                   (A5)  INIT <'04 30'>
 2 #MAY                     (A5)  INIT <'05 31'>
 2 #JUNE                    (A5)  INIT <'06 30'>
 2 #JULY                    (A5)  INIT <'07 31'>
 2 #AUGUST                  (A5)  INIT <'08 31'>
 2 #SEPTEMBER               (A5)  INIT <'09 30'>
 2 #OCTOBER                 (A5)  INIT <'10 31'>
 2 #NOVEMBER                (A5)  INIT <'11 30'>
 2 #DECEMBER                (A5)  INIT <'12 31'>
*
1 REDEFINE #NUMBER-OF-DAYS-IN-MONTH-TABLE
 2 #MONTHS-OCC              (12)
  3 #MONTH                  (N2)
  3 #SPACE                  (A1)
  3 #DAYS                   (N2)
*
1 #WYYYYMMDD                (N8)
1 REDEFINE #WYYYYMMDD
 2 #YYYY                    (N4)
 2 #MM                      (N2)
 2 #DD                      (N2)
*
1 #IS-A-LEAP-YEAR           (L)
1 #I1                       (I2)
1 #I2                       (I2)
1 #WORK-CALC                (P5)
1 #REMAINDER                (P5.2)
*
*** Below is used for date flipping ***
1 #YYYYMMDD
 2 #YYYY-F                  (N4)
 2 #MM-F                    (N2)
 2 #DD-F                    (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-N              (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A              (A8)
*
1 #MMDDYYYY
 2 #MM-F                    (N2)
 2 #DD-F                    (N2)
 2 #YYYY-F                  (N4)
1 REDEFINE #MMDDYYYY
 2 #MMDDYYYY-N              (N8)
1 REDEFINE #MMDDYYYY
 2 #MMDDYYYY-A              (A8)
*** End of date flipping **************
*
1 #START-YYYYMMDD           (N8)
1 REDEFINE #START-YYYYMMDD
 2 #START-YYYY              (N4)
 2 REDEFINE #START-YYYY
  3 #START-CC               (N2)
  3 #START-YY               (N2)
 2 #START-MM                (N2)
 2 #START-DD                (N2)
*
1 #MINUS-YYYYMMDD           (N8)
1 REDEFINE #MINUS-YYYYMMDD
 2 #MINUS-YYYY              (N4)
 2 #MINUS-MM                (N2)
 2 #MINUS-DD                (N2)
*
1 #DAY-OF-WEEK-TABLE
 2 #2001                    (A24) INIT <'MOTHTHSUTUFRSUWESAMOTHSA'>
 2 #2002                    (A24) INIT <'TUFRFRMOWESAMOTHSUTUFRSU'>
 2 #2003                    (A24) INIT <'WESASATUTHSUTUFRMOWESAMO'>
 2 #2004                    (A24) INIT <'THSUMOTHSATUTHSUWEFRMOWE'>
 2 #2005                    (A24) INIT <'SATUTUFRSUWEFRMOTHSATUTH'>
 2 #2006                    (A24) INIT <'SUWEWESAMOTHSATUFRSUWEFR'>
 2 #2007                    (A24) INIT <'MOTHTHSUTUFRSUWESAMOTHSA'>
 2 #2008                    (A24) INIT <'TUFRSATUTHSUTUFRMOWESAMO'>
 2 #2009                    (A24) INIT <'THSUSUWEFRMOWESATUTHSUTU'>
 2 #2010                    (A24) INIT <'FRMOMOTHSATUTHSUWEFRMOWE'>
 2 #2011                    (A24) INIT <'SATUTUFRSUWEFRMOTHSATUTH'>   /* PM1
 2 #2012                    (A24) INIT <'SUWETHSUTUFRSUWESAMOTHSA'>   /* PM1
 2 #2013                    (A24) INIT <'TUFRFRMOWESAMOTHSUTUFRSU'>   /* PM1
 2 #2014                    (A24) INIT <'WESASATUTHSUTUFRMOWESAMO'>   /* PM1
 2 #2015                    (A24) INIT <'THSUSUWEFRMOWESATUTHSUTU'>   /* PM1
*
1 REDEFINE #DAY-OF-WEEK-TABLE
 2 #YEAR-OCCS               (15)
  3 #MONTH-OCCS             (12)
   4 #DAY                   (A2)
*
1 #RETURN-DAY-TABLE         (N7)
1 REDEFINE #RETURN-DAY-TABLE
 2 #RETURN-DAY-OCCS         (7)
  3 #RETURN-DAY             (N1)
*
END-DEFINE
*
************************************************************************
*
RESET #RETURN-CALC
*
DECIDE ON FIRST VALUE OF #REQUEST
   VALUE 'A'
     PERFORM CALCULATE-DAYS-DIFFERENCE
   VALUE 'B'
     PERFORM CALCULATE-MONTH-END-DATE
   VALUE 'C'
     PERFORM FLIP-DATE-TO-MMDDYYYY
   VALUE 'D'
     PERFORM FLIP-DATE-TO-YYYYMMDD
   VALUE 'E'
     PERFORM SUBTRACT-FROM-DATE
   VALUE 'F'
     PERFORM DETERMINE-DAY-OF-WEEK
   NONE VALUE
     ESCAPE ROUTINE
END-DECIDE
*
************************************************************************
DEFINE SUBROUTINE CALCULATE-DAYS-DIFFERENCE
************************************************************************
*
IF #FROM-DATE NE MASK (YYYYMMDD)
 OR #TO-DATE NE MASK (YYYYMMDD)
 OR #TO-DATE LT #FROM-DATE
   MOVE 9999999 TO #RETURN-CALC
   ESCAPE ROUTINE
END-IF
*
MOVE #TO-DATE TO #WYYYYMMDD
*
REPEAT
  IF #WYYYYMMDD LE #FROM-DATE
    ESCAPE BOTTOM
  END-IF
*
  SUBTRACT 1 FROM #DD
  ADD +1 TO #RETURN-CALC
*
  IF #DD = 0
    SUBTRACT 1 FROM #MM
    IF #MM = 0
      MOVE 12 TO #MM
      SUBTRACT 1 FROM #YYYY
    END-IF
*
    FOR #I1 1 TO 12
      IF #MONTH (#I1) = #MM
        MOVE #DAYS (#I1) TO #DD
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF #MM = 2
      PERFORM CHECK-FOR-LEAP-YEAR
      IF #IS-A-LEAP-YEAR
        MOVE 29 TO #DD
      END-IF
    END-IF
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CALCULATE-MONTH-END-DATE
************************************************************************
*
MOVE #FROM-DATE TO #WYYYYMMDD
*
FOR #I1 1 TO 12
  IF #MONTH (#I1) = #MM
    MOVE #DAYS (#I1) TO #DD
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #MM = 2
  PERFORM CHECK-FOR-LEAP-YEAR
  IF #IS-A-LEAP-YEAR
    ADD +1 TO #DD
  END-IF
END-IF
*
MOVE #WYYYYMMDD TO #TO-DATE
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE FLIP-DATE-TO-MMDDYYYY
************************************************************************
*
MOVE #TO-DATE TO #YYYYMMDD-N
RESET #MMDDYYYY-A
IF #YYYYMMDD-N NE 0
  MOVE BY NAME #YYYYMMDD TO #MMDDYYYY
END-IF
MOVE #MMDDYYYY-A TO #RETURN-CALC-A
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE FLIP-DATE-TO-YYYYMMDD
************************************************************************
*
MOVE #TO-DATE TO #MMDDYYYY-N
RESET #YYYYMMDD-A
IF #MMDDYYYY-N NE 0
  MOVE BY NAME #MMDDYYYY TO #YYYYMMDD
END-IF
MOVE #YYYYMMDD-A TO #RETURN-CALC-A
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE SUBTRACT-FROM-DATE
************************************************************************
*
IF #TO-DATE NE MASK (YYYYMMDD)
 OR #FROM-DATE LT 0
   MOVE 9999999 TO #RETURN-CALC
   ESCAPE ROUTINE
END-IF
*
MOVE #TO-DATE TO #START-YYYYMMDD
MOVE #FROM-DATE TO #MINUS-YYYYMMDD
*
IF #MINUS-YYYY NE 0
  REPEAT
    SUBTRACT 1 FROM #START-YYYY
    IF #START-YYYY LT 0
      MOVE 99999999 TO #RETURN-CALC
      ESCAPE ROUTINE
    END-IF
*
    SUBTRACT 1 FROM #MINUS-YYYY
    IF #MINUS-YYYY = 0
      ESCAPE BOTTOM
    END-IF
  END-REPEAT
END-IF
*
IF #MINUS-MM NE 0
  REPEAT
    SUBTRACT 1 FROM #START-MM
    IF #START-MM EQ 0
      MOVE 12 TO #START-MM
      SUBTRACT 1 FROM #START-YYYY
      IF #START-YYYY LT 0
        MOVE 99999999 TO #RETURN-CALC
        ESCAPE ROUTINE
      END-IF
    END-IF
*
    SUBTRACT 1 FROM #MINUS-MM
    IF #MINUS-MM = 0
      ESCAPE BOTTOM
    END-IF
  END-REPEAT
END-IF
*
IF #MINUS-DD NE 0
  REPEAT
    SUBTRACT 1 FROM #START-DD
    IF #START-DD = 0
      SUBTRACT 1 FROM #START-MM
      IF #START-MM EQ 0
        MOVE 12 TO #START-MM
        SUBTRACT 1 FROM #START-YYYY
        IF #START-YYYY LT 0
          MOVE 99999999 TO #RETURN-CALC
          ESCAPE ROUTINE
        END-IF
      END-IF
*
      FOR #I1 1 TO 12
        IF #MONTH (#I1) = #START-MM
          MOVE #DAYS (#I1) TO #START-DD
          ESCAPE BOTTOM
        END-IF
      END-FOR
*
      IF #START-MM = 2
        MOVE #START-YYYYMMDD TO #WYYYYMMDD
        PERFORM CHECK-FOR-LEAP-YEAR
        IF #IS-A-LEAP-YEAR
          MOVE 29 TO #START-DD
        END-IF
      END-IF
    END-IF
*
    SUBTRACT 1 FROM #MINUS-DD
    IF #MINUS-DD = 0
      ESCAPE BOTTOM
    END-IF
  END-REPEAT
END-IF
*
MOVE #START-YYYYMMDD TO #RETURN-CALC
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-DAY-OF-WEEK
************************************************************************
*
MOVE #TO-DATE TO #START-YYYYMMDD
*
IF #START-CC NE 20
 OR #START-YY LT 01
 OR #START-YY GT 15  /* PM1
 OR #START-MM LT 01
 OR #START-MM GT 12
 OR #START-DD LT 01
 OR #START-DD GT 31
  MOVE 99999999 TO #RETURN-CALC
  ESCAPE ROUTINE
END-IF
*
MOVE #START-YY TO #I1
MOVE #START-MM TO #I2
*
PERFORM LOAD-RETURN-DAY-TABLE
*
MOVE #START-DD TO #WORK-CALC
REPEAT
  IF #WORK-CALC LT 8
    ESCAPE BOTTOM
  END-IF
*
  SUBTRACT 7 FROM #WORK-CALC
END-REPEAT
*
IF #WORK-CALC LT 1
 OR #WORK-CALC GT 7
   MOVE 99999999 TO #RETURN-CALC
   ESCAPE ROUTINE
END-IF
*
MOVE #WORK-CALC TO #I1
MOVE #RETURN-DAY (#I1) TO #RETURN-CALC
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE LOAD-RETURN-DAY-TABLE
************************************************************************
*
DECIDE FOR FIRST CONDITION
  WHEN #DAY (#I1,#I2) = 'SU'
    MOVE 1 TO #RETURN-DAY (1)
    MOVE 2 TO #RETURN-DAY (2)
    MOVE 3 TO #RETURN-DAY (3)
    MOVE 4 TO #RETURN-DAY (4)
    MOVE 5 TO #RETURN-DAY (5)
    MOVE 6 TO #RETURN-DAY (6)
    MOVE 7 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'MO'
    MOVE 2 TO #RETURN-DAY (1)
    MOVE 3 TO #RETURN-DAY (2)
    MOVE 4 TO #RETURN-DAY (3)
    MOVE 5 TO #RETURN-DAY (4)
    MOVE 6 TO #RETURN-DAY (5)
    MOVE 7 TO #RETURN-DAY (6)
    MOVE 1 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'TU'
    MOVE 3 TO #RETURN-DAY (1)
    MOVE 4 TO #RETURN-DAY (2)
    MOVE 5 TO #RETURN-DAY (3)
    MOVE 6 TO #RETURN-DAY (4)
    MOVE 7 TO #RETURN-DAY (5)
    MOVE 1 TO #RETURN-DAY (6)
    MOVE 2 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'WE'
    MOVE 4 TO #RETURN-DAY (1)
    MOVE 5 TO #RETURN-DAY (2)
    MOVE 6 TO #RETURN-DAY (3)
    MOVE 7 TO #RETURN-DAY (4)
    MOVE 1 TO #RETURN-DAY (5)
    MOVE 2 TO #RETURN-DAY (6)
    MOVE 3 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'TH'
    MOVE 5 TO #RETURN-DAY (1)
    MOVE 6 TO #RETURN-DAY (2)
    MOVE 7 TO #RETURN-DAY (3)
    MOVE 1 TO #RETURN-DAY (4)
    MOVE 2 TO #RETURN-DAY (5)
    MOVE 3 TO #RETURN-DAY (6)
    MOVE 4 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'FR'
    MOVE 6 TO #RETURN-DAY (1)
    MOVE 7 TO #RETURN-DAY (2)
    MOVE 1 TO #RETURN-DAY (3)
    MOVE 2 TO #RETURN-DAY (4)
    MOVE 3 TO #RETURN-DAY (5)
    MOVE 4 TO #RETURN-DAY (6)
    MOVE 5 TO #RETURN-DAY (7)
  WHEN #DAY (#I1,#I2) = 'SA'
    MOVE 7 TO #RETURN-DAY (1)
    MOVE 1 TO #RETURN-DAY (2)
    MOVE 2 TO #RETURN-DAY (3)
    MOVE 3 TO #RETURN-DAY (4)
    MOVE 4 TO #RETURN-DAY (5)
    MOVE 5 TO #RETURN-DAY (6)
    MOVE 6 TO #RETURN-DAY (7)
  WHEN NONE
    MOVE 99999999 TO #RETURN-CALC
    ESCAPE ROUTINE
END-DECIDE
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CHECK-FOR-LEAP-YEAR
************************************************************************
*
RESET #IS-A-LEAP-YEAR #WORK-CALC #REMAINDER
DIVIDE 4 INTO #YYYY GIVING #WORK-CALC REMAINDER #REMAINDER
IF #REMAINDER = 0  /* 1rst test - evenly divisible by 4
  RESET #WORK-CALC #REMAINDER
  DIVIDE 100 INTO #YYYY GIVING #WORK-CALC REMAINDER #REMAINDER
  IF #REMAINDER NE 0 /* 2nd test - not evenly divisible by 100
    MOVE TRUE TO #IS-A-LEAP-YEAR
  ELSE
    RESET #WORK-CALC #REMAINDER
    DIVIDE 400 INTO #YYYY GIVING #WORK-CALC REMAINDER #REMAINDER
    IF #REMAINDER = 0 /* 3rd test - not div by 100 but evenly div by 400
      MOVE TRUE TO #IS-A-LEAP-YEAR
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
END
