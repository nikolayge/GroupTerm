* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* logic for batch projections (only used if *device = 'batch')
*
**         MODIFICATIONS
** SAG 10282005 RH1 AB Replace CALL to Cobol with CALLNAT to Natural
* TMT 06/16/2014 TT1  Contact Normalization - DOB and Sex
***********************************************************************
MOVE #MAX-PAG-LINES TO #PAGE-LENGTH
REPEAT
  READ WORK 1 ONCE RECORD #RECORD-1   /* 1ST THIRD OF INFO
    AT END OF FILE
      MOVE '******' TO #ID-NUMBER-1-A
    END-ENDFILE
*
  IF #ID-NUMBER-1-A = '******'    /* ALWAYS LAST RECORD
    ESCAPE BOTTOM
  END-IF
*
  IF #ID-NUMBER-1 = 999999
    READ WORK 1 ONCE RECORD #RECORD-2  /* 2ND THIRD OF INFO
*
    READ WORK 1 ONCE RECORD #RECORD-3  /* 3RD THIRD OF INFO
    RESET                  #PROJECTION-VIEW
    MOVE #ID-NUMBER-1   TO #PROJECTION-VIEW.ID-NUMBER
    MOVE #DOB           TO #DATE-OF-BIRTH         /* TT1
    MOVE #LOAN-AMT      TO #PROJECTION-VIEW.LOAN-AMOUNT
    MOVE #LOAN-AMT-2    TO #PROJECTION-VIEW.LOAN-AMOUNT-2
    MOVE #LOAN-INT-RATE TO #PROJECTION-VIEW.INTEREST-RATE-5
    MOVE 'N'            TO #PROJECTION-VIEW.RESTRICT-MBR-SW
    MOVE #UNITS         TO #PROJECTION-VIEW.NUMBER-MEMBERSHIPS
    MOVE 'D'            TO #PROJECTION-VIEW.STATUS       (1)
    MOVE #PLAN          TO #PROJECTION-VIEW.PLAN         (1)
    MOVE #MODE          TO #PROJECTION-VIEW.MODE         (1)
*
    IF #SMOKER-FLAG = 'C'
      MOVE 'C'          TO #PROJECTION-VIEW.CONVERSION-FLAG (1)
    ELSE
      MOVE #SMOKER-FLAG TO #PROJECTION-VIEW.CONVERSION-FLAG (1)
    END-IF
*
    MOVE #SMOKER-FLAG   TO #PROJECTION-VIEW.SMOKER-FLAG     (1)
    MOVE #ISS-AGE       TO #PROJECTION-VIEW.ISSUE-AGE       (1)
    MOVE #PAY-STATUS    TO #PROJECTION-VIEW.PAY-STATUS      (1)
    MOVE #ISS-DATE      TO #PROJECTION-VIEW.ISSUE-DATE      (1)
    MOVE #FACE-AMT      TO #PROJECTION-VIEW.FACE-AMOUNT     (1)
    MOVE #ANN-PREM      TO #PROJECTION-VIEW.ANNUAL-PREM     (1)
    MOVE #LAST-DURATION TO #PROJECTION-VIEW.LAST-DURATION   (1)
    MOVE #CV            TO #PROJECTION-VIEW.CASH-VALUE      (1)
    MOVE #DB            TO #PROJECTION-VIEW.DEATH-BENEFIT   (1)
    MOVE #UNITS         TO #PROJECTION-VIEW.NUM-MBRSHIPS    (1)
    COMPUTE #PROJECTION-VIEW.DURATION (1) = #LAST-DURATION + 1
    MOVE #MONTH-IND     TO #PROJECTION-VIEW.MONTH-IND       (1)
    MOVE #MIN-CV        TO #PROJECTION-VIEW.MIN-CASH-VALUE  (1)
    MOVE #NXT-YR-MIN-CV TO #PROJECTION-VIEW.NXT-YR-MIN-CASH-VALUE (1)
    MOVE #CV            TO #PROJECTION-VIEW.NXT-MON-CASH-VALUE    (1)
    MOVE #DB            TO #PROJECTION-VIEW.NXT-MON-DEATH-BENEFIT (1)
*   MOVE #LAST-ANN-CV   TO #PROJECTION-VIEW.LAST-ANN-CASH-VALUE   (1)
    MOVE #ACV           TO #PROJECTION-VIEW.ACC-CASH-VALUE        (1)
    MOVE #CONV-DUR      TO #PROJECTION-VIEW.CONV-DURATION         (1)
    MOVE #MONTHLY-ALLOC-CRED TO #PROJECTION-VIEW.MONTH-ALLOC-AMT  (1)
    MOVE #MONTHLY-PREM-DEFIC TO #PROJECTION-VIEW.PREM-DEFICIENCY  (1)
    MOVE #NET-PREM-RATIO     TO #PROJECTION-VIEW.NET-PR-RATIO     (1)
    RESET VP-S01
    MOVE #NAME          TO VP-S01.NAME
*     MOVE #SEX           TO VP-S01.SEX                 /* TT1
    MOVE 1              TO VP-S01.C*ACTIVE-SEGMENTS
    MOVE 19920901       TO VP-S01.PAID-TO-DATE (1)
  ELSE
    RESET #REC-FOUND
    FIND (1) VP-S01 WITH MEMBER-KEY = #ID-NUMBER-1-A
      MOVE TRUE TO #REC-FOUND
      MOVE *ISN TO #CGA-ISN
      GET #PROJECTION-VIEW #CGA-ISN
    END-FIND
  END-IF
*
  MOVE #ID-NUMBER-1 TO #CGA-ORIG-ID
  MOVE VP-S01.NAME  TO #SCR-NAME
  SEPARATE VP-S01.NAME LEFT INTO #LAST-NAME #FIRST-NAME
         #OVFLW-NAME WITH DELIMITER #COMMA
*
  COMPRESS #FIRST-NAME #LAST-NAME INTO #L-SWITCHED-NAME
*
  IF #OVFLW-NAME > '   '
    COMPRESS #L-SWITCHED-NAME ',' INTO #HOLD-NAME LEAVING NO SPACE
    COMPRESS #HOLD-NAME #OVFLW-NAME INTO #L-SWITCHED-NAME
  END-IF
*
  RESET #SCR-SELECTION (*)
  PERFORM FORMAT-MENU
*
  FOR #IDX 1 TO 4
    IF #SCR-SELECTION (#IDX) = 'X'
      MOVE #IDX TO #ISC
      PERFORM SETUP-PROJECTION
      PERFORM BUILD-PAGE-1
      NEWPAGE (2)
      WRITE (2) NOTITLE USING FORM 'C2V13FVP'
    END-IF
  END-FOR
END-REPEAT
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SETUP-PROJECTION
*
MOVE *DATU                      TO #PARM-DATE
MOVE #IDX                       TO #L-SEGNO
MOVE VP-S01.C*ACTIVE-SEGMENTS   TO #L-ACTIVE-SEG
MOVE PAID-TO-YYMMDD (#IDX)      TO #L-PAID-THRU
*
FOR #SCR-X 1 TO VP-S01.C*ACTIVE-SEGMENTS
  IF STATUS (#SCR-X) = 'D'
    MOVE ISSUE-DATE-YYMMDD (#SCR-X) TO #L-ISS-DT (#SCR-X)
  END-IF
END-FOR
*
MOVE #SCR-OVR-GROWTH-RATE TO #L-GROWTH-RATE
*
IF #SCR-OVR-MTHLY-VAL = 'Y'
  MOVE 'M'              TO #L-MONTHLY-SW
ELSE
  MOVE 'N'              TO #L-MONTHLY-SW
END-IF
*
MOVE #SCR-OVR-START-YEAR  TO #L-MNTHLY-START-YR
MOVE 'PROJ'               TO #L-REIN-CODE
MOVE #SCR-OVR-NUM-YRS     TO #L-NUM-PROJ-YRS
MOVE #SCR-OVR-DISPLAY-CALCS TO #L-DISPLAY-CALCS
MOVE #SCR-OVR-SHOW-LOAN-REPAID TO #L-SHOW-LOAN-REPAID
*
IF #CGA-ORIG-COMMAND = 'AV'
  MOVE 'AV' TO #L-REIN-CODE
  MOVE 25   TO #L-NUM-PROJ-YRS
  MOVE 'N'  TO #L-SHOW-LOAN-REPAID
END-IF
*
IF #CGA-SCR-SUFFIX = MASK ('IP')
  MOVE 'N' TO #L-SHOW-LOAN-REPAID
  IF #CGA-SCR-SUFFIX = 'IPA'
    MOVE 'IPRA' TO #L-REIN-CODE
  ELSE
    IF #CGA-SCR-SUFFIX = 'IPB'
      MOVE 'IPRB' TO #L-REIN-CODE
    ELSE
      IF #CGA-SCR-SUFFIX = 'IPC'
        MOVE 'IPRC' TO #L-REIN-CODE
      ELSE
        IF #CGA-SCR-SUFFIX = 'IPD'
          MOVE 'IPRD' TO #L-REIN-CODE
        ELSE
          IF #CGA-SCR-SUFFIX = 'IPE'
            MOVE 'IPRE' TO #L-REIN-CODE
            MOVE 'Y' TO #L-SHOW-LOAN-REPAID
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF #SCR-OVR-CAP-INT = 'Y'
  MOVE 'CAP' TO #L-REIN-CODE
END-IF
*
IF #CGA-SCR-SUFFIX = MASK ('LN')
* MOVE 'N' TO #L-SHOW-LOAN-REPAID
  IF #CGA-SCR-SUFFIX = 'LN4'
    MOVE 'LN40' TO #L-REIN-CODE
  ELSE
    IF #CGA-SCR-SUFFIX = 'LN5'
      MOVE 'LN50' TO #L-REIN-CODE
    END-IF
  END-IF
END-IF
*
MOVE 0                    TO #L-NUM-DTLS
*
* * * *  *  *  *  *
*
IF *DEVICE = 'BATCH'
** SAG RH1 0282005 AB START
** CALL 'AOCX2V5B' #PROJECTION-VIEW.ID-NUMBER
**                 #L-ACTIVE-SEG
**                 #L-NUM-DTLS
   CALLNAT 'AOCX2V5B'  #PROJECTION-VIEW  #L-MISC-INPUT  #L-OUTPUT-INFO
** SAG RH1 10282005 AB END
ELSE
** SAG RH1 10282005 AB START
**CALL 'AOCX2V5A' ID-NUMBER           /* DATABASE FIELDS - DYNAMIC CALL
**    #L-ACTIVE-SEG        /* INPUT PARMS
**    #L-NUM-DTLS          /* OUTPUT FIELDS
  CALLNAT 'AOCX2V5B'  #PROJECTION-VIEW  #L-MISC-INPUT  #L-OUTPUT-INFO 
** SAG RH1 10282005 AB END
END-IF
*
END-SUBROUTINE   /* (0109)
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE SHIFT-DOB
* * * * * * * * * * * * * *
MOVE #OVR-DOB-MM TO #SCR-OVR-DOB-MM
MOVE #OVR-DOB-DD TO #SCR-OVR-DOB-DD
MOVE #OVR-DOB-YY TO #SCR-OVR-DOB-YY
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DATE-REFORMAT
* * * * * * * * * * * * * * * * *
*
IF (#WORK-DATE-MM GT 00) AND (#WORK-DATE-MM LT 13)
  MOVE #WORK-DATE-MM TO #I1
  MOVE EDITED #WORK-DATE-DD (EM=Z9) TO #WORK-DATE-DD-A
  COMPRESS #WORK-DATE-DD #MON (#I1) #WORK-DATE-YYYY-A
                                   INTO #W-DATE-EDITED WITH '-'
ELSE
  MOVE '    ?    ' TO #W-DATE-EDITED
END-IF
*
END-SUBROUTINE
*
