* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GZRTLTRP
****************************
* LT to RT conversion report
****************************
* TMT  05/12/2014  TT1  Contact Normalization Project
*****************************
DEFINE DATA
*
LOCAL USING ATHLTMRF
LOCAL
*
1 #PROCESS-DATE   (N6)   /* DW5
1 #ORIG-REFUND   (P5.2)
1 #AMT    (N5.2)
1 #I3     (I2)
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-CLERK-ID (A3/60)
 2 NOTE-LINE-NUM (N1/60)
 2 NOTE-DATE     (N8/60)
 2 REDEFINE NOTE-DATE
  3 NOTE-DATE-OCC  (60)
   4 NOTE-DATE-YYYY  (N4)
   4 NOTE-DATE-MMDD  (N4)
 2 NOTE-TEXT     (A60/60)
 2 REDEFINE NOTE-TEXT
  3 NOTE-TEXT-OCC   (60)
   4 NOTE-TEXT-60  (A60)
   4 REDEFINE NOTE-TEXT-60
    5 NOTE-TEXT-1-20  (A20)
    5 REDEFINE NOTE-TEXT-1-20
     6 NOTE-TEXT-11-20    (A10)
     6 NOTE-TEXT-ISS-MM   (A2)
     6 NOTE-TEXT-ISS-/1   (A1)
     6 NOTE-TEXT-ISS-DD   (A2)
     6 NOTE-TEXT-ISS-/2   (A1)
     6 NOTE-TEXT-ISS-YYYY (A4)
    5 NOTE-TEXT-21-22 (A2)     /* '-2' FOR POLICY #
    5 REDEFINE NOTE-TEXT-21-22
     6 NOTE-TEXT-21   (A1)
     6 NOTE-TEXT-22   (N1)    /* POLICY #
    5 NOTE-TEXT-23-60 (A38)
    5 REDEFINE NOTE-TEXT-23-60
     6 NOTE-TEXT-23-31    (A09) /* ' RT TO LT' OR ' LT TO RT'
     6 REDEFINE NOTE-TEXT-23-31
      7 NOTE-TEXT-23           (A1)
      7 NOTE-TEXT-PRIOR-PLAN   (A2)
     6 NOTE-TEXT-34-60    (A29)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER      (N6)
 2 REDEFINE ID-NUMBER
   3 ID-NBR       (A6)
 2 DTS-DATN
 2 DTS-TIMN
 2 DATE-ACCT-UPDATE
 2 WIDOW-FLAG     (A1)
 2 PROCESS-IND    (A1)
* 2 INTERNET-ADDR
 2 NAME
*  2 SEX                     /* tt1
 2 INTRO-FLAG
 2 X-LOAN-AMOUNT   (9)
 2 X-LOAN-AMOUNT-2 (9)
 2 X-LOAN-DATE     (9)
 2 DEBIT-CREDIT-AMT
 2 X-BILL-ADJUST   (9)
 2 LINKED-CN       (1)
 2 REDEFINE LINKED-CN
  3 LINKED-CN-A   (A6/1)
*  2 DATE-OF-BIRTH           /* tt1
 2 NUMBER-MEMBERSHIPS
 2 C*ACTIVE-SEGMENTS
 2 ISSUE-DATE          (N8/9)
 2 REDEFINE ISSUE-DATE
  3 ISS-DT-OCC (9)
   4 ISS-DT-YYYY    (N4)
   4 ISS-DT-MM      (N2)
   4 ISS-DT-DD      (N2)
 2 ONLINE-ACTION       (9)
 2 SMOKER-FLAG         (9)
 2 STATUS              (9)
 2 PLAN                (9)
 2 PAY-STATUS          (/9)
 2 FACE-AMOUNT         (9)
 2 ANNUAL-PREM         (9)
 2 ISSUE-AGE           (9)
 2 REDEFINE ISSUE-AGE
  3 ISSUE-AGE-N        (N2/9)
 2 FIRS-LOCATION-CODE   (9)
 2 MODE                (9)
 2 PAID-TO-DATE        (9)
 2 REDEFINE PAID-TO-DATE
  3 PAID-TO-DATE-OCC (9)
   4 PAID-TO-DATE-YYYY (N4)
   4 PAID-TO-DATE-MM   (N2)
   4 PAID-TO-DATE-DD   (N2)
 2 NUM-MBRSHIPS        (P1/9)
 2 TOT-PREMIUMS-PAID   (9)
 2 MONTH-ALLOC-AMT     (9)
 2 EXPIRY-DAY          (9)
 2 PREM-RATING         (9)
*
1 TR-V VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER
  2 TRANSACTION-DATN
  2 C*CHANGED-DATA-TABLE
  2 CHANGED-DATA-TABLE   (1:20)
     3 OLD-DATA
     3 NEW-DATA
*
1 #NEW-DATA
 2 #TR-REG-CONV-NEW-LBL-1         (A12) INIT <' AFTER CONV '>
 2 #TR-REG-CONV-NEW-LBL-2         (A11) INIT <'ISSUE-DATE '>
 2 #TR-REG-CONV-NEW-ISSUE-DATE    (N08)
 2 #TR-REG-CONV-NEW-LBL-3         (A07) INIT <'  PLAN '>
 2 #TR-REG-CONV-NEW-PLAN          (A03)
 2 #TR-REG-CONV-NEW-LBL-4         (A11) INIT <'  FACE-AMT '>
 2 #TR-REG-CONV-NEW-FAC-AMT       (A09)
 2 #TR-REG-CONV-NEW-LBL-5         (A07) INIT <'  MODE '>
 2 #TR-REG-CONV-NEW-MODE          (A01)
 2 #TR-REG-CONV-NEW-LBL-6         (A10) INIT <'  ANN-PRM '>
 2 #TR-REG-CONV-NEW-ANN-PRM       (A08)
 2 #TR-REG-CONV-NEW-LBL-7         (A06) INIT <'  ACV '>
 2 #TR-REG-CONV-NEW-ACV           (A10)
 2 #TR-REG-CONV-NEW-LBL-8         (A12) INIT <'  CST-BASIS '>
 2 #TR-REG-CONV-NEW-COST-BASIS    (A10)
1 REDEFINE #NEW-DATA
  2 #NEW-DATA-LINE                  (A120)
*
1 #OLD-DATA
 2 #TR-REG-CONV-OLD-LBL-1         (A08) INIT <'CI-CONV '>
 2 #TR-REG-CONV-OLD-LBL-2         (A11) INIT <'ISSUE-DATE '>
 2 #TR-REG-CONV-OLD-ISSUE-DATE    (N08)
 2 #TR-REG-CONV-OLD-LBL-3         (A07) INIT <'  PLAN '>
 2 #TR-REG-CONV-OLD-PLAN          (A03)
 2 #TR-REG-CONV-OLD-LBL-4         (A11) INIT <'  FACE-AMT '>
 2 #TR-REG-CONV-OLD-FAC-AMT       (A09)
 2 #TR-REG-CONV-OLD-LBL-5         (A07) INIT <'  MODE '>
 2 #TR-REG-CONV-OLD-MODE          (A01)
 2 #TR-REG-CONV-OLD-LBL-6         (A10) INIT <'  ANN-PRM '>
 2 #TR-REG-CONV-OLD-ANN-PRM       (A08)
 2 #TR-REG-CONV-OLD-LBL-7         (A06) INIT <'  ACV '>
 2 #TR-REG-CONV-OLD-ACV           (A10)
 2 #TR-REG-CONV-OLD-LBL-8         (A12) INIT <'  CST-BASIS '>
 2 #TR-REG-CONV-OLD-COST-BASIS    (A10)
1 REDEFINE #OLD-DATA
  2 #OLD-DATA-LINE                  (A120)
*
1 #REPORT-LINE (a250)
1 #ANN-PREM      (A9)
1 #PREV-ANN-PREM (A9)
1 #LT-ISSUE-DATE (N8)
1 #LT-RT-COUNTER (N7)
1 #I1 (I2)
1 #I2 (I2)
1 #TAB (A1) init <H'09'>
END-DEFINE
* * * * * * * * * * * * * * * * *
*
DEFINE WORK FILE 1
 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/LT_RT_REPORT.TXT'
compress 'CN' #TAB 'CURR/ISS-DATE' #TAB 'CONV/Date' #TAB 'PLAN' #TAB
'CURR/ANN-PREM' #TAB 'PRIOR/ANN-PREM' #TAB 'LT ISS-DATE' #TAB
INTO #REPORT-LINE
WRITE WORK FILE 1 #REPORT-LINE
READ ST-V BY MEMBER-KEY FROM '001900'   /* 001900
  FOR #I1 1 TO C*ACTIVE-SEGMENTS
    IF  (NOT PLAN (#I1) = 'RT')
      ESCAPE TOP
    END-IF
*
    FIND (1) NT-V WITH ID-NUMBER = ST-V.ID-NUMBER
      FOR #I2 1 TO 60
        IF   (NOTE-TEXT (#I2) = MASK ('* POL ISS'))  /* LT to RT Conv
             AND  (NOTE-TEXT-23-31 (#I2) = ' LT TO RT')
             ADD 1 TO #LT-RT-COUNTER
             reset #ann-prem #prev-ann-prem #LT-ISSUE-DATE
             PERFORM GET-TR-DATA
*
           MOVE EDITED ST-V.ANNUAL-PREM (#I1) (EM=ZZ,ZZZ.99) TO #ANN-PREM
           COMPRESS ST-V.ID-NUMBER #TAB ST-V.ISSUE-DATE (#I1) #TAB NOTE-DATE (#I2)
           #TAB PLAN (#I1) #TAB #ANN-PREM #TAB #PREV-ANN-PREM #TAB
           #LT-ISSUE-DATE #TAB INTO #REPORT-LINE leaving no
           WRITE work file 1 #REPORT-LINE
*
        END-IF
      END-FOR
    END-FIND
  END-FOR
END-READ
      compress 'Records found' #lt-rt-counter #tab into #report-line
      WRITE WORK FILE 1 #report-line
*
DEFINE SUBROUTINE GET-TR-DATA
  FIND TR-V WITH TR-V.ID-NUMBER = ST-V.ID-NUMBER
*     ACCEPT IF TR-V.TRANSACTION-DATN = NOTE-DATE (#I2) OR
*               TR-V.TRANSACTION-DATN = ST-V.ISSUE-DATE (#I1)
     FOR #I3 = 1 TO C*CHANGED-DATA-TABLE
      RESET #OLD-DATA-LINE #NEW-DATA-LINE
      #OLD-DATA-LINE := TR-V.OLD-DATA (#I3)
      #NEW-DATA-LINE := TR-V.NEW-DATA (#I3)
      IF #TR-REG-CONV-OLD-LBL-1 = 'CI-CONV' AND
         #TR-REG-CONV-OLD-PLAN = 'LT' AND
         #TR-REG-CONV-NEW-PLAN = 'RT'
           MOVE #TR-REG-CONV-OLD-ANN-PRM TO #PREV-ANN-PREM
           MOVE #TR-REG-CONV-OLD-ISSUE-DATE TO #LT-ISSUE-DATE
           ESCAPE ROUTINE
     END-IF
    END-FOR
  END-FIND
END-SUBROUTINE
*
END
