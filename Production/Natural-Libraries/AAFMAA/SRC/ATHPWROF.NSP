* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPWROF - Write-off Accounts
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    20070531  None    Program Written. Adapted from ATHPRFND.     *
*                                                                      *
* PAM    20070730  PM1     Expanded to allow CREDIT write-offs         * 
* JNT    20130923  JT1     Address normalization                       *
************************************************************************
*
DEFINE DATA
LOCAL
*
1 #INPUT   (A50)
1 REDEFINE #INPUT
 2 #INPUT-X  (A1/50)
*
1 #TAB       (A1) INIT <H'09'>     
1 #FIELD    (A6)
1 REDEFINE #FIELD
 2 #FIELD-N  (N6)
1 REDEFINE #FIELD
 2 #FIELD-X  (A1/6)
*
1 #ST-X   (I2)
1 #ST-ISN   (P8)
1 #ST-NOT-FOUND   (L)
*
1 #INP-CN      (N6)
1 REDEFINE #INP-CN
 2 #INP-CN-A   (A6)
1 #INP-DB-CR   (N6.2)
*
1 #AMT    (N6.2)
1 REDEFINE #AMT
 2 #AMT-DOLS     (N6)
 2 #AMT-CENTS    (N2)
1 REDEFINE #AMT
 2 #AMT-X        (A1/8)
1 REDEFINE #AMT
 2 #AMT-A        (A8)
*
1 #TOT-REFUND    (P7.2)
1 #TOT-DB-CR     (P7.2)
1 #TOT-MISMATCH  (P7.2)
1 #TOT-BYPASSED  (P7.2)
1 #DB-CR-REFD    (P7.2)
1 #DB-CR-REFD-DISP (A10)
1 #NUM-DB-CR     (N5)
1 #MISMATCH-CNT (N5)
1 #BYPASSED-CNT   (N5)
1 #UPDATE    (L)
1 #HOLD-CUR-ACCT-UPDT (N8)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER      (N6)
 2 REDEFINE ID-NUMBER
   3 ID-NBR       (A6)
 2 DTS-DATN
 2 DTS-TIMN
 2 DATE-ACCT-UPDATE
 2 WIDOW-FLAG     (A1)
 2 ALOT-AMT
 2 PROCESS-IND    (A1)
 2 NAME
 2 DATE-OF-DEATH                                                
 2 PAD-CODE
 2 INTRO-FLAG
 2 DEBIT-CREDIT-AMT
 2 X-BILL-ADJUST    (9)
 2 ISSUE-DATE          (N8/9)
 2 REDEFINE ISSUE-DATE
  3 ISS-DT-OCC (9)
   4 ISS-DT-YYYY    (N4)
   4 ISS-DT-MM      (N2)
   4 ISS-DT-DD      (N2)
 2 STATUS              (A1/9)
 2 PLAN                (A3/9)
 2 ANNUAL-PREM         (9)
  2 MODE                (9)
*  2 ADDRESS-CHG-CODE                                        /* JT1 
*  2 ADDRESS-1                                               /* JT1 
*
1 ACCT-REG-V VIEW OF A-ACCOUNTING-REGISTER
 2 ID-NUMBER             (N6)
 2 PROCESS-IND           (A1)
 2 TRANSACTION-DATN      (P9)
 2 TRANSACTION-TIMN      (P7)
 2 TXN-CODE              (A4)
 2 SEQUENCE-NUM          (N1)
 2 NAME                  (A25)
 2 TXN-ACCT-NUMBER       (N3)
 2 ACCT-DATE             (N8)
 2 DATE-LAST-ACCT-UPDATE (N8)
 2 DEBIT-AMOUNT          (P7.2)
 2 CREDIT-AMOUNT         (P7.2)
 2 DESCRIPTION           (A20)
 2 MESSAGE-CODE          (A1)
 2 AALP-FLAG             (A1)
 2 CLERK-ID              (A3)
 2 TERMINAL-ID           (A8)
 2 CASH-CODE             (A1)
 2 SG-CATEGORY-1         (A2/15)
 2 SG-CATEGORY-2         (A2/15)
 2 SG-AMOUNT             (N6.2/15)
*
1 #COUNTERS
 2 #RECS-READ        (P5)
 2 #RECS-SELECTED    (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #CNT-5            (P5)
 2 #CNT-6            (P5)
 2 #CNT-7            (P5)
 2 #CNT-8            (P5)
 2 #CNT-9            (P5)
 2 #CNT-10           (P5)
 2 #CNT-11           (P5)
 2 #CNT-12           (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
 2 #I4               (I2)
 2 #I5               (I2)
1 #NUM-RECS          (N5)
1 #NUM-POLS          (P5)
1 #INT               (I2)
1 #NEG               (L)
*
1 #TODAY-MMDDYYYY (N8)
1 REDEFINE #TODAY-MMDDYYYY
 2 #TODAY-MMDDYYYY-A (A8)
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A    (A8)
1 REDEFINE #YYYYMMDD
 2 #YYYY          (N4)
 2 #MM            (N2)
 2 #DD            (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A        (A4)
 2 #MM-A          (A2)
 2 #DD-A          (A2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
       'PROGRAM EXCEPTIONS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (1) LS=133 PS=76
WRITE  (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
       'PROGRAM EXCEPTIONS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Accounts Written Off'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #TODAY-MMDDYYYY-A LEAVING NO
*
READ WORK 1 RECORD #INPUT
  IF #INPUT = MASK ('CN')
    ESCAPE TOP
  END-IF
*
  MOVE 1 TO #I3                /* beginning of field
  RESET #I5 #NEG               /* field #               /* PM1
  FOR #I1 1 TO 50
    IF #INPUT-X (#I1) = #TAB OR = ' ' OR = '.' OR = ','
      ADD 1 TO #I5             /* field #
      MOVE 6 TO #I4
      COMPUTE #I2 = #I1 - 1
*
      IF   (#I5 = 3)           /* cents
       AND (#I2 = #I3)         /* only leading diget entered (is 12.3)
        SUBTRACT 1 FROM #I4    /* start 1 pos to the left
      END-IF
*
      FOR #I2 #I2 TO #I3 STEP -1
        IF #INPUT-X (#I2) = '-'                        /* PM1
          MOVE TRUE TO #NEG                            /* PM1
          ESCAPE TOP                                   /* PM1
        END-IF                                         /* PM1
        
        MOVE #INPUT-X (#I2) TO #FIELD-X (#I4)
        SUBTRACT 1 FROM #I4
      END-FOR
      COMPUTE #I3 = #I1 + 1   /* beginning of next field
*
      IF #I5 = 1 MOVE #FIELD-N TO #INP-CN   END-IF
      IF #I5 = 2 MOVE #FIELD-N TO #AMT-DOLS END-IF
      IF #I5 = 3 MOVE #FIELD-N TO #AMT-CENTS
                 MOVE #AMT     TO #INP-DB-CR END-IF
      RESET #FIELD
      IF #I5 = 3
        ESCAPE BOTTOM
      END-IF
    END-IF
  END-FOR
*
  RESET #ST-NOT-FOUND
  RESET #DB-CR-REFD
  FIND (1) ST-V WITH MEMBER-KEY = #INP-CN-A
    IF NO RECORD FOUND
      MOVE TRUE TO #ST-NOT-FOUND
      ESCAPE BOTTOM
    END-NOREC
    MOVE *ISN TO #ST-ISN
  END-FIND
*
  IF #ST-NOT-FOUND
    ADD +1 TO #BYPASSED-CNT
    ADD #INP-DB-CR TO #TOT-BYPASSED
    WRITE (1) #INP-CN 'Bypassed - CN not found'
    WRITE (2) #INP-CN 'Bypassed - CN not found'
    ESCAPE TOP
  END-IF
*
  IF #NEG                                                 /* PM1
    COMPUTE #INP-DB-CR = #INP-DB-CR * -1                  /* PM1
  END-IF                                                  /* PM1
*
  IF #INP-DB-CR NE ST-V.DEBIT-CREDIT-AMT
    WRITE (1) ST-V.ID-NUMBER 'Bypassed - DB/CR Mismatch - File DB/CR='
        #AMT 'ST DB/CR =' DEBIT-CREDIT-AMT
    WRITE (2) ST-V.ID-NUMBER 'Bypassed - DB/CR Mismatch - File DB/CR='
        #AMT 'ST DB/CR =' DEBIT-CREDIT-AMT
    ADD +1 TO #MISMATCH-CNT
    ADD #INP-DB-CR TO #TOT-MISMATCH
    ESCAPE TOP
  END-IF
*
  GET ST-V #ST-ISN
*
  COMPUTE #DB-CR-REFD = DEBIT-CREDIT-AMT
  IF #NEG                                                 /* PM1
    COMPUTE #DB-CR-REFD = #DB-CR-REFD * -1                /* PM1
  END-IF                                                  /* PM1
*  
  ADD #DB-CR-REFD TO #TOT-REFUND
  ADD #DB-CR-REFD TO #TOT-DB-CR
  MOVE EDITED #DB-CR-REFD (EM=ZZ,ZZZ.99) TO #DB-CR-REFD-DISP
*
  MOVE ST-V.DATE-ACCT-UPDATE TO #HOLD-CUR-ACCT-UPDT
  MOVE *DATN TO ST-V.DATE-ACCT-UPDATE
  MOVE *DATN TO ST-V.DTS-DATN
  MOVE *TIMN TO ST-V.DTS-TIMN
*
  ADD 1 TO #NUM-DB-CR
  IF #NEG                                                 /* PM1
    ADD #DB-CR-REFD TO ST-V.DEBIT-CREDIT-AMT              /* PM1
  ELSE                                                    /* PM1
    SUBTRACT #DB-CR-REFD FROM ST-V.DEBIT-CREDIT-AMT     /* <***************
  END-IF                                                  /* PM1
  PERFORM ACCT-REG-AND-NOTE
*
  ADD 1 TO #CNT-2
  DISPLAY (2)      #CNT-2
    'CN'           ST-V.ID-NUMBER
    'DB-CR/Adjust' #DB-CR-REFD  (EM=-ZZ,ZZZ.99)
    'New/DB-CR'    ST-V.DEBIT-CREDIT-AMT (EM=-ZZZ,ZZZ.99)
*
  UPDATE (0247)
  END TRANSACTION
END-WORK
*
NEWPAGE (1)    
WRITE (1) ///                            
WRITE (1) #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE (1) #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE (1) #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT REFUND'
WRITE (1) ' '
WRITE (1) #MISMATCH-CNT (EM=Z,ZZ9)      '     Num Mismatched'
WRITE (1) #TOT-MISMATCH (EM=ZZZ,ZZZ.99) 'Total mismatch amt'
WRITE (1) ' '
WRITE (1) #BYPASSED-CNT (EM=Z,ZZ9)      '     Num Bypassed - other'
WRITE (1) #TOT-BYPASSED (EM=ZZZ,ZZZ.99) 'Total bypassed - other'
*
WRITE (2) ///
WRITE (2) #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE (2) #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE (2) #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT REFUND'
WRITE (2) ' '
WRITE (2) #MISMATCH-CNT (EM=Z,ZZ9)      '     Num Mismatched'
WRITE (2) #TOT-MISMATCH (EM=ZZZ,ZZZ.99) 'Total mismatch amt'
WRITE (2) ' '
WRITE (2) #BYPASSED-CNT (EM=Z,ZZ9)      '     Num Bypassed - other'
WRITE (2) #TOT-BYPASSED (EM=ZZZ,ZZZ.99) 'Total bypassed - other'
*
******************************************************************************
DEFINE SUBROUTINE ACCT-REG-AND-NOTE
******************************************************************************
*
RESET ACCT-REG-V
MOVE ST-V.ID-NUMBER        TO ACCT-REG-V.ID-NUMBER
MOVE ST-V.PROCESS-IND      TO ACCT-REG-V.PROCESS-IND 
MOVE *DATN                 TO ACCT-REG-V.TRANSACTION-DATN
MOVE *TIMN                 TO ACCT-REG-V.TRANSACTION-TIMN
MOVE *INIT-ID              TO ACCT-REG-V.TERMINAL-ID
MOVE 'EDS'                 TO ACCT-REG-V.CLERK-ID
MOVE ST-V.NAME             TO ACCT-REG-V.NAME
MOVE 'A'                   TO ACCT-REG-V.CASH-CODE
RESET                         ACCT-REG-V.AALP-FLAG
MOVE *DATN                 TO ACCT-REG-V.ACCT-DATE
*
IF #NEG                                                  /* PM1
  MOVE 'RDCR' TO ACCT-REG-V.TXN-CODE                     /* PM1
  MOVE #DB-CR-REFD TO ACCT-REG-V.DEBIT-AMOUNT            /* PM1
ELSE                                                     /* PM1
  MOVE 'ADCR'                TO ACCT-REG-V.TXN-CODE
  MOVE #DB-CR-REFD TO ACCT-REG-V.CREDIT-AMOUNT
END-IF                                                   /* PM1
*
IF #HOLD-CUR-ACCT-UPDT = 0
  MOVE *DATN               TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
ELSE
  MOVE #HOLD-CUR-ACCT-UPDT TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
END-IF
*
MOVE 450 TO ACCT-REG-V.TXN-ACCT-NUMBER
*
IF ST-V.INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q'
  MOVE 'CAP MBR' TO ACCT-REG-V.DESCRIPTION
END-IF
COMPRESS ACCT-REG-V.DESCRIPTION 'WRITE-OFF' INTO DESCRIPTION
*
STORE ACCT-REG-V
*
END-SUBROUTINE
*
END
