* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2009SNM - Turn MEC-Ind on if necessary
* Callnat'd by C2000PNM/C2100PAI for new pols & C2300PCI on PUAPS
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG    REASON                                      *
*                                                                      *
* PAM    02102005   PM1    Increased #LUMP-PMT from 5.2 to 6.2.        *
* PAM    10/26/2011 PM2    Changed MAP from C2009MNM to C2008MNM       *
* TMT    06/18/2014 TT1    Contact Normalization DOB and SEX
************************************************************************
*
DEFINE DATA
PARAMETER
1 #SEG     (I2)
1 #CN      (N6)
1 REDEFINE #CN
  2 #CN-A   (A6)
1 #CLERK     (A3)
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID           /* TT1
*  2 DATE-OF-BIRTH              /* TT1
  2 INTRO-FLAG
  2 X-FIRST-PLACE-FEE (9)
  2 X-BILL-ADJUST     (9)
  2 MEC-IND      (9)
  2 PLAN         (9)
  2 ISSUE-DATE   (9)
  2 PAID-TO-DATE (9)
  2 PAID-UP-DATE (9)
  2 ISSUE-AGE    (9)
  2 ANNUAL-PREM  (9)
  2 SMOKER-FLAG  (9)
  2 FACE-AMOUNT  (9)
  2 DURATION     (9)
  2 PAY-STATUS   (9)
  2 TOT-PREMIUMS-PAID (9)
  2 MODE         (9)
  2 COST-BASIS   (9)
  2 MONTH-ALLOC-AMT (9)
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL
  2 ID-NUMBER        (N6)
  2 ENTRY-DATE       (N8)
  2 REC-ISN          (N8)
  2 CODE             (A1)
  2 ISSUE-DATE       (N8)
  2 NUM-MBRSHIPS     (N3)
  2 USER             (A3)
*
1 #Y-N         (A1)
1 #INT         (I2)
1 #I1          (I2)
1 #I2          (I2)
1 #I3          (I2)
1 #SUFFIX      (A3)
1 #MAX-7-PAY   (P6.2)
1 #MEC-PREMS-PAID-TO-DATE (P7.2)         /*TT1 increased from 6.2 to 7.2
1 #EXCEEDS-MEC (P7.2)                    /*TT1 Increased from 6.2 to 7.2
1 #1035-NX     (A60)
1 #1035-EXCH-AMT  (P6.2)
1 #LUMP-PMT  (P6.2)                                               /* PM1
1 #SCR-TEXT-78    (A78/18)
1 #NOTE-TEXT (A60/10)
1 #NT-ISN    (N8)
1 #ST-ISN    (N8)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY  (N4)
    3 REDEFINE #YYYY
      4 #YYYY-A (A4)
    3 #MM    (N2)
    3 REDEFINE #MM
      4 #MM-A   (A2)
  2 #DD-A     (A2)
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)  /* NG99
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID     (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
* ABOVE for CALC-PREMS-PAID
1 DATE-OF-BIRTH       (N8.0)       /* TT1
*
END-DEFINE
* * * * * * * * * * * * * *
*
* WRITE 'top of C2009SNM cn=' #CN 'seg=' #SEG
*
IF (#SEG = 0) OR (#SEG GT 9)   /* must be NM ltr
*  IF #CLERK = 'DRW'
*    WRITE 'SEG=' #SEG
*  END-IF
  ESCAPE ROUTINE
END-IF
*
MOVE #SEG TO #I1
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  MOVE *ISN TO #ST-ISN
  IF MEC-IND (#I1) = 'Y'   /* already a MEC
    ESCAPE ROUTINE
  END-IF
*
  RESET DATE-OF-BIRTH                                                  /* TT1
  DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)         /* TT1
*
  RESET #PREMS-PAID
  MOVE DATE-OF-BIRTH           TO #PP-DATE-OF-BIRTH
  MOVE ANNUAL-PREM (#I1)       TO #PP-ANNUAL-PREM
  MOVE PAY-STATUS (#I1)        TO #PP-PAY-STATUS
  MOVE TOT-PREMIUMS-PAID (#I1) TO #PP-TOT-PREMIUMS-PAID
  MOVE PAID-TO-DATE (#I1)      TO #PP-PAID-TO-DATE
  MOVE ISSUE-DATE (#I1)        TO #PP-ISSUE-DATE
  MOVE MODE (#I1)              TO #PP-MODE
  MOVE PLAN (#I1)              TO #PP-PLAN
  MOVE COST-BASIS (#I1)        TO #PP-COST-BASIS
  MOVE MONTH-ALLOC-AMT (#I1)   TO #PP-MONTH-ALLOC-AMT
  MOVE INTRO-FLAG              TO #PP-INTRO-FLAG
  MOVE X-FIRST-PLACE-FEE (#I1) TO #PP-FIRST-PLACE-FEE
  MOVE X-BILL-ADJUST     (#I1) TO #PP-BADJ
*
  CALLNAT 'GPRPDSCO' /* get #prems-paid
    #PP-DATE-OF-BIRTH
    #PP-ANNUAL-PREM
    #PP-PAY-STATUS
    #PP-TOT-PREMIUMS-PAID
    #PP-PAID-TO-DATE
    #PP-ISSUE-DATE
    #PP-MODE
    #PP-PLAN
    #PP-COST-BASIS
    #PP-MONTH-ALLOC-AMT
    #PP-INTRO-FLAG
    #PP-FIRST-PLACE-FEE
    #PREMS-PAID
    #SUB-PREMS-PAID
    #PP-BADJ
  ADD #PP-FIRST-PLACE-FEE TO #PREMS-PAID /* assume mbr will pay fpf
* ADD #PP-BADJ   TO #PREMS-PAID /* and bill adjust /* REmoved 12/7/2004
*
  RESET #SUFFIX
  CALLNAT 'GMECCSXX' ST-V.ID-NUMBER
    #SUFFIX
    PLAN        (#I1)
    ISSUE-DATE  (#I1)
    PAID-TO-DATE(#I1)
    PAID-UP-DATE(#I1)
    ISSUE-AGE   (#I1)
    ANNUAL-PREM (#I1)
    SMOKER-FLAG (#I1)
    FACE-AMOUNT (#I1)
    DURATION    (#I1)
    PAY-STATUS  (#I1)
    TOT-PREMIUMS-PAID (#I1)
    #PREMS-PAID  /* calc tpp
*
    #MAX-7-PAY
    #MEC-PREMS-PAID-TO-DATE
    #EXCEEDS-MEC
    #1035-NX
    #1035-EXCH-AMT
    #LUMP-PMT
*
  IF #1035-EXCH-AMT NE 0
    INPUT MARK *#Y-N USING MAP 'C2008MNM'    /* PM2
*
    IF NOT #Y-N = 'Y' OR = 'N'
      REINPUT 'Must be (Y)es or (N)o'
        MARK *#Y-N
    END-IF
  END-IF
*
  IF  (#EXCEEDS-MEC NE 0)   /* Did not pass MEC test
      OR (#Y-N = 'Y') /* Prior 1035 pol was a MEC
    IF #Y-N = 'Y'
*     IF #Y-N = 'U' /* Did not know about MEC condition / removed 6/7/04
*       MOVE *DATN TO #YYYYMMDD  /* get 30 days date
*       ADD 1 TO #MM
*       IF #MM GT 12
*         SUBTRACT 12 FROM #MM
*         ADD 1 TO #YYYY
*       END-IF
*       COMPRESS #MM-A #DD-A #YYYY-A INTO #NOTE-TEXT (1) WITH '/'
*
*       COMPRESS '*@' #NOTE-TEXT (1)
*        'Check MEC condition on recent 1035 policy' INTO #NOTE-TEXT (1)
*
*       RESET MBRSHIP-MTD-DET-V   /* create 'pd' entry
*       MOVE #YYYYMMDD     TO ENTRY-DATE
*       MOVE #NT-ISN       TO REC-ISN
*       MOVE 'S'           TO CODE
*       MOVE #ST-ISN       TO MBRSHIP-MTD-DET-V.ISSUE-DATE
*       MOVE #CN           TO MBRSHIP-MTD-DET-V.ID-NUMBER
*       MOVE #CLERK        TO USER
*       STORE MBRSHIP-MTD-DET-V
*     ELSE   /* must be 'Y'
      MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #NOTE-TEXT (1) WITH '/'
      COMPRESS '* POL ISS' #NOTE-TEXT (1)
        'is a MEC since the orig pol was a MEC'
        INTO #NOTE-TEXT (1)
*     END-IF
*
      MOVE 1 TO #INT
      CALLNAT 'E4205SDM' #CN #INT #CLERK #NOTE-TEXT (1:10)
    END-IF
  END-IF
*
  IF  (#EXCEEDS-MEC NE 0)   /* Did not pass MEC test
      OR (#Y-N = 'Y') /* Prior 1035 pol was a MEC
    MOVE 'Y' TO MEC-IND (#I1)
    UPDATE (0120)
    END TRANSACTION
    WRITE /// 'CN' ST-V.ID-NUMBER
      /// 'The policy issued' ISSUE-DATE (#I1) 'exceeds'
      /   'the Mec test or is a 1035 and the prior policy'
      /   'was a MEC and has been marked accordingly.'
      //  'Press ENTER to view MEC description.'
*
    RESET #I1   /* below is also in C2060PNC and C2460PLI
    ADD 1 TO #I1
    COMPRESS 'This policy is identified as a Modified'
      'Endowment Contract (MEC)' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'and comes under the provisions of the Technical and'
      'Miscellaneous Revenue ACT' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS '(TAMRA) of 1988. Unless you are disabled, TAMRA may impose'
      'ordinary income tax' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'implications on MEC policy distributions, i.e. loans,'
      'withdrawals or cash' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'surrenders, regardless of your age. Also, if you are'
      'under age 59 1/2 a' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS '10% penalty tax may apply to distributions. The tax'
      'and penalty apply only' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'to distribution of interest earnings. The IRS sequence'
      'for distributions is' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'interest earnings first and then principal. No income'
      'tax or penalty is' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'applicable to principal. Principal is the cost basis'
      'of the policy. Cost' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'Basis is premiums paid plus any taxable distributions'
      'made from the policy.' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'For the purpose of determining any gain includable'
      'in gross income upon cash' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'surrender, all MEC"s issued by the same company to the'
      'same policyholder' INTO #SCR-TEXT-78(#I1)
    ADD 1 TO #I1
*
    COMPRESS 'within any calendar year are treated as one MEC contract.'
      TO #SCR-TEXT-78(#I1)
*
    ADD 1 TO #I1
    MOVE 'You may want to seek professional advice.' TO #SCR-TEXT-78(#I1)
*
    INPUT USING MAP 'C206AMNC'
  END-IF
END-FIND
*
END
