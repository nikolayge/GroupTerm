* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*** <@ NATURAL META INFORMATION BEGIN
***
*** @Mode REPORT
***
*** NATURAL META INFORMATION END @>
************************************************************************
* PROGRAM-ID: C1099SXX - select 1099 type
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  01042006 None Program Written                                   *
* DRW 11/21/2007 DW2 Add Type W (1099-C)
************************************************************************
*
DEFINE DATA
PARAMETER
1 #COMMAND                   (A2)
1 #ID                        (N9)
1 #SUFFIX                    (A3)
1 #INT-1099-KEY              (A10)
1 REDEFINE #INT-1099-KEY
  2 #INT-1099-ID-NUMBER       (N6)
  2 #INT-1099-YYYY            (N4)
  2 REDEFINE #INT-1099-YYYY
    3 #INT-1099-CENT           (N2)
    3 #INT-1099-YY             (A2)
1 #YEAR-INT-TYPE-ID-KEY      (A11)
1 REDEFINE #YEAR-INT-TYPE-ID-KEY
  2 #YEAR-INT-YEAR            (N4)
  2 #YEAR-INT-TYPE            (A1)
  2 #YEAR-INT-ID-NUMBER       (N6)
1 #ISN                        (P8)
*
LOCAL USING C2910LTB
LOCAL
1 #I1                        (I2)
1 #I2                        (I2)
1 #SELECTION-FOUND           (L)
1 #OK                        (L)
1 #ADD-A-NEW-INT-TYPE        (A1)
1 #TYPE                      (A8)
1 #TEXT                      (A70)
1 #SELECT                    (A1/15)
1 #SELECT-CV                 (C/15)
*
1 #INT-TABLE                 (15)
  2 #INT-TYPE                 (A1)
  2 #INT-AMOUNT               (N7.2)
  2 #INT-DESCRIPTION          (A40)
  2 #INT-ISN                  (P8)
*
1 #TB-TABLE
  2 #D                        (A1)   INIT <'D'>
  2 #A                        (A1)   INIT <'A'>
  2 #I                        (A1)   INIT <'I'>
  2 #E                        (A1)   INIT <'E'>
*
1 REDEFINE #TB-TABLE
  2 #TB-OCC                   (4)
    3 #TB-ENTRY                (A1)
1 #NUM-TB-ENTRIES            (P3)   INIT <4>
*
1 #TC-TABLE
  2 #2                        (A1)   INIT <'2'>
  2 #C                        (A1)   INIT <'C'>
  2 #M                        (A1)   INIT <'M'>
  2 #N                        (A1)   INIT <'N'>
  2 #P                        (A1)   INIT <'P'>
  2 #L                        (A1)   INIT <'L'>
1 REDEFINE #TC-TABLE
  2 #TC-OCC                   (6)
    3 #TC-ENTRY                (A1)
1 #NUM-TC-ENTRIES            (P3)   INIT <6>

*
1 #TW-TABLE                                        /* DW2
  2 #W                     (A1)    INIT <'W'>
1 REDEFINE #TW-TABLE
  2 #TW-OCC                  (1)
    3 #TW-ENTRY                   (A1)
1 #NUM-TW-ENTRIES          (P3) INIT <1>
*
END-DEFINE
*
************************************************************************
*
DEFINE WINDOW LIST-1099
  SIZE 24 * 75
  BASE TOP LEFT
  FRAMED ON POSITION SYMBOL AUTO
*
MOVE (AD=P) TO #SELECT-CV (*)
RESET #INT-TABLE (*) #I1
FIND INT-1099-V WITH ID-NUMBER-YEAR-KEY = #INT-1099-KEY
  IF NO RECORDS FOUND
    IGNORE
  END-NOREC
*
  RESET #OK #TYPE
  IF #COMMAND = 'TB'
    MOVE '1099-INT' TO #TYPE
    FOR #I2 1 TO #NUM-TB-ENTRIES
      IF INT-1099-V.INT-TYPE = #TB-ENTRY (#I2)
        MOVE TRUE TO #OK
        ESCAPE BOTTOM
      ELSE
        ESCAPE TOP
      END-IF
    END-FOR
  ELSE
    IF #COMMAND = 'TC'
      MOVE '1099-R' TO  #TYPE
      FOR #I2 1 TO #NUM-TC-ENTRIES
        IF INT-1099-V.INT-TYPE = #TC-ENTRY (#I2)
          MOVE TRUE TO #OK
          ESCAPE BOTTOM
        ELSE
          ESCAPE TOP
        END-IF
      END-FOR
    else           /* Must be a "W" (TW)
      MOVE '1099-C' TO  #TYPE                       /* DW2
      FOR #I2 1 TO #NUM-TW-ENTRIES
        IF INT-1099-V.INT-TYPE = #TW-ENTRY (#I2)
          MOVE TRUE TO #OK
          ESCAPE BOTTOM
        ELSE
          ESCAPE TOP
        END-IF
      END-FOR
    END-IF
  end-if
*
  IF #OK
    ADD +1 TO #I1
    MOVE (AD=NV) TO #SELECT-CV (#I1)
    MOVE INT-1099-V.INT-TYPE TO #INT-TYPE (#I1)
    MOVE INT-1099-V.AMOUNT   TO #INT-AMOUNT (#I1)
    #INT-ISN (#I1) := *ISN (0099)
    DECIDE FOR FIRST CONDITION
      WHEN #INT-TYPE (#I1) = 'L'
        MOVE 'Loans Treated as a Distribution' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = '2'
        MOVE 'Cash Value - 1035 Exchange     ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'C'
        MOVE 'Cash Value Surrender           ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'M'
        MOVE 'MEC Interest                   ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'N'
        MOVE 'Annuitant Interest             ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'P'
        MOVE 'Pension                        ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'D'
        MOVE 'Annuitant Interest             ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'A'
        MOVE 'Death Benefit Interest         ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'I'
        MOVE 'Interest                       ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'E'
        MOVE 'Escrow Interest                ' TO #INT-DESCRIPTION (#I1)
      WHEN #INT-TYPE (#I1) = 'W'                                           /* DW2
        MOVE 'CAP Loan Write-Off             ' TO #INT-DESCRIPTION (#I1)
      WHEN NONE
        MOVE 'Unknown                        ' TO #INT-DESCRIPTION (#I1)
    END-DECIDE
  END-IF
END-FIND
*
MOVE '*' TO #COMMAND
SET WINDOW 'LIST-1099'
RESET #TEXT
IF #I1 = 0
  MOVE 'X' TO #ADD-A-NEW-INT-TYPE
  COMPRESS 'No' #TYPE 'entries found for' #INT-1099-YYYY INTO #TEXT
  INPUT TEXT #TEXT MARK *#ADD-A-NEW-INT-TYPE USING MAP 'C1099MXX'
ELSE
  COMPRESS 'Please select an interest type to modify'
    'or add a new interest type' INTO #TEXT
  INPUT TEXT #TEXT MARK *#SELECT (1) USING MAP 'C1099MXX'
END-IF
*
IF #COMMAND = '*'
  RESET #SELECTION-FOUND
  FOR #I1 1 TO 15
    IF #SELECT (#I1) NE ' '
      MOVE #INT-1099-ID-NUMBER TO #YEAR-INT-ID-NUMBER
      MOVE #INT-1099-YYYY TO #YEAR-INT-YEAR
      MOVE #INT-TYPE (#I1) TO #YEAR-INT-TYPE
      MOVE #INT-ISN  (#I1) TO #ISN
      MOVE TRUE TO #SELECTION-FOUND
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF NOT #SELECTION-FOUND
      AND #ADD-A-NEW-INT-TYPE = ' '
    MOVE 'Please make a selection' TO #TEXT
    REINPUT WITH TEXT #TEXT
  END-IF
END-IF
*
SET WINDOW OFF
*
END
