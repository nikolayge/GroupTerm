* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* bz 07/01/2017
* Convert Name String to 'Proper format'
* Convert Address String to 'Proper format'  (HD-7407)
* Exclusion !Do not apply to STATE field!
* DEAR GENERAL de'beaar M O'LEARY, JR, RET  =>
* Dear General De'Beaar M O'Leary, Jr, Ret  =>
* Applicable to parts of Name or to composed name 
* Usage :
* 1)   1 p_Name   (A)   Dynamic
*      p_Name := EDIT_NAME_LOWCASE(<p_Name>)
*
* 2)   1 p_Name   (A)   Dynamic
*      1 V_Target (A45) 
*      v_Target := EDIT_NAME_LOWCASE(<p_Name>)
* bz-Name exclude special treatment for suffix
*
DEFINE FUNCTION EDIT_NAME_LOWCASE RETURNS (A) DYNAMIC BY VALUE
DEFINE DATA
PARAMETER
1 p_Name (A) DYNAMIC BY VALUE
LOCAL
1 A225  (A225)
1 REDEFINE A225
  2 A01      (A1/1:225)
1 #i        (I2)
1 v_L       (I2)
1 l_Conv    (L) INIT <FALSE>
*                                  
END-DEFINE
* ON ERROR                                                        /* --db
*   WRITE "Err:" *ERROR-NR "Err-ln:" *ERROR-LINE *PROGRAM /       /* --db   
* END-ERROR                                                       /* --db  
*
 IF p_Name = ' ' ESCAPE ROUTINE END-IF
 p_Name := *TRIM(p_Name)
 v_L := *LENGTH(p_Name)
 IF v_L = 1  EDIT_NAME_LOWCASE := *TRANSLATE (p_Name, UPPER ) ESCAPE ROUTINE END-IF
 A225 := p_Name
 A01(1) := *TRANSLATE (A01(1), UPPER )
 FOR #i = 1 TO v_L 
    IF    A01(#i) <> MASK(A) 
      AND A01(#i) <> MASK(N)  /* HD-7407
      l_Conv := FALSE
    ELSE
      IF l_Conv
        A01(#i) := *TRANSLATE (A01(#i), LOWER )
      ELSE
        A01(#i) := *TRANSLATE (A01(#i), UPPER )
      END-IF
      l_Conv := TRUE
    END-IF
  END-FOR

 EDIT_NAME_LOWCASE := *TRIM(A225)

END-FUNCTION
END
