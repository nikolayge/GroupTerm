* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram POSN0001.
/**
/** :author nguentchev
/* TODO Enter your code here
*
*  Policy OWNERSHIP maintenance
*
DEFINE DATA
PARAMETER
1 #REQUEST      (I4)
1 #MSG          (A) DYNAMIC
PARAMETER USING POSALOAD
LOCAL USING ERROR_L
LOCAL USING POSL0002
LOCAL
1 BLANK           (A1) CONST <' '>
1 #REL-EXIST      (L)
1 #MESSAGE-LOG    (I4) CONST <2>
1 #ERROR-LOG      (I4) CONST <3>
1 #GET            (I4) CONST <0>
1 #ADD            (I4) CONST <1>
1 #UPDATE         (I4) CONST <2>
1 #DELETE         (I4) CONST <3>
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
**  VALUE 'REPLACE' PERFORM REPLACE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  VALUE #GET    PERFORM GET-RECORD
  NONE VALUE
    IGNORE
END-DECIDE
/*
DEFINE CHECK-EXIST
/*     -----------
RESET #REL-EXIST         /* 'No record exist'
FIND PC-V WITH CONTACT-ID = #POWN.CONTACT-ID
    AND POLICY-NUMBER = #POWN.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PC-V.RELATION = #POWN.RELATION
  #REL-EXIST := TRUE
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
/*
DEFINE GET-RECORD
/*     -----------
#MSG := #NOT-EXIST      /* 'No record exist'
FIND PC-V WITH CONTACT-ID = #POWN.CONTACT-ID
    AND POLICY-NUMBER = #POWN.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PC-V.RELATION = #POWN.RELATION
  MOVE BY NAME PC-V TO #POWN
  RESET #MSG
END-FIND
*
END-SUBROUTINE
/*
/**
DEFINE REPLACE-RECORD
/*     -------------
#MSG := #NOTHING-UPDATE          /* 'No record exist'
FIND PC-V WITH CONTACT-ID = #POWN.CONTACT-ID
    AND POLICY-NUMBER = #POWN.POLICY-NUMBER
  IF NO RECORDS FOUND
    PERFORM ADD-RECORD
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #POWN TO PC-V
  IF PC-V.ADD-USER = BLANK
    PC-V.ADD-USER      := *USER
  END-IF
  IF PC-V.ADD-DATE = 0
    PC-V.ADD-DATE      := *DATN
  END-IF
  IF PC-V.ADD-TIME = 0
    PC-V.ADD-TIME      := *TIMN
  END-IF
  PC-V.LAST-UPD-USER := *USER
  PC-V.LAST-UPD-DATE := *DATN
  PC-V.LAST-UPD-TIME := *TIMN
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/*
DEFINE ADD-RECORD
/*     ----------
PERFORM CHECK-EXIST
IF #REL-EXIST
/* 'No duplicate key allowed - record exist'
  #MSG := #DUPLICATE-KEY
ELSE
  MOVE BY NAME #POWN TO PC-V
  IF PC-V.ADD-USER = BLANK
    PC-V.ADD-USER      := *USER
  END-IF
  IF PC-V.ADD-DATE = 0
    PC-V.ADD-DATE      := *DATN
  END-IF
  IF PC-V.ADD-TIME = 0
    PC-V.ADD-TIME      := *TIMN
  END-IF
  IF PC-V.LAST-UPD-USER = BLANK
    PC-V.LAST-UPD-USER := *USER
  END-IF
  IF PC-V.LAST-UPD-DATE = 0
    PC-V.LAST-UPD-DATE := *DATN
  END-IF
  IF PC-V.LAST-UPD-TIME = 0
    PC-V.LAST-UPD-TIME := *TIMN
  END-IF
  STORE PC-V
  MOVE BY NAME PC-V TO #POWN
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
/*     -------------
#MSG := #NOTHING-UPDATE          /* 'No record exist'
FIND PC-V WITH CONTACT-ID = #POWN.CONTACT-ID
    AND POLICY-NUMBER = #POWN.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PC-V.RELATION = #POWN.RELATION
  MOVE BY NAME #POWN TO PC-V
  IF PC-V.LAST-UPD-USER = BLANK
    PC-V.LAST-UPD-USER := *USER
  END-IF
  IF PC-V.LAST-UPD-DATE = 0
    PC-V.LAST-UPD-DATE := *DATN
  END-IF
  IF PC-V.LAST-UPD-TIME = 0
    PC-V.LAST-UPD-TIME := *TIMN
  END-IF
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/*
DEFINE DELETE-RECORD
/*     -------------
#MSG := #NOTHING-DELETE      /* 'No record to delete'
FIND PC-V WITH CONTACT-ID = #POWN.CONTACT-ID
    AND POLICY-NUMBER = #POWN.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PC-V.RELATION = #POWN.RELATION
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
*
ON ERROR
  COMPRESS #INTERNAL-ERROR
    #POWN.REFERENCE-NUMBER
    INTO #MSG
  CALLNAT 'POSLOG' #ERROR-LOG #MSG
  ESCAPE ROUTINE
END-ERROR
END
