* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM ID DNXVN006   Notified By Data                                                  *
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  08/16/2010  None    Program created                             *
* RSE  01/05/2011  RE1     Display Notified Date from DATE-DEATH-NOTIFY*
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL USING GONERLXX
LOCAL
1 BLANK  (A1)  CONST <' '>
1 #NOTIFIED-DATE (A15) CONST <'Notified Date:'>
1 #RELATIONSHIP  (A14) CONST <'Relationship:'>
1 #PHONE         (A8)  CONST <'<Phone:>'>
1 #NODATA  (A1)  CONST <'?'>
*
1 STATUS VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 NOK-CONTACT-ID
  2 DATE-DEATH-NOTIFY 2 REDEFINE DATE-DEATH-NOTIFY
    3 DDN-YEAR  (A4)
    3 DDN-MONTH (A2)
    3 DDN-DAY   (A2)

1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 DEATH-NOTIFIED-BY-CONTACT-ID
  2 ID-NUMBER
  2 GENDER-CD
  2 DATE-OF-DEATH-REPORTED 2 REDEFINE DATE-OF-DEATH-REPORTED
    3 DOD-YEAR  (A4)
    3 DOD-MONTH (A2)
    3 DOD-DAY   (A2)
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DOB-YEAR  (A4)
    3 DOB-MONTH (A2)
    3 DOB-DAY   (A2)
  2 SSN
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
*
1 #DEATH-NOTIFIED-BY-CONTACT-ID (N8)
1 #SSN-A              (A10) 1 REDEFINE #SSN-A
  2 #SSN-3            (A3)
  2 #SSN-2            (A2)
  2 #SSN-4            (A4)
1 #PHONE-NUM          (A) DYNAMIC
END-DEFINE
*
RESET #TEXT (*)
IF #ISN = 0
  #TEXT (1) := 'No data'
  ESCAPE ROUTINE
END-IF
*
GET CONTACT #ISN
#CONTACT-ID := CONTACT.CONTACT-ID
*
FIND STATUS WITH ID-NUMBER = CONTACT.ID-NUMBER
END-FIND
*
IF DEATH-NOTIFIED-BY-CONTACT-ID = 0
  #TEXT (1) := 'Name: ?'
*  COMPRESS #TEXT (1) #NOTIFIED-DATE #NODATA INTO #TEXT (1)   /* RE1 START
  IF DATE-DEATH-NOTIFY = 0
    COMPRESS #TEXT (1) #NOTIFIED-DATE #NODATA INTO #TEXT (1)
  ELSE
    COMPRESS DDN-MONTH DDN-DAY DDN-YEAR INTO #SSN-A WITH DELIMITER '/'
    COMPRESS #TEXT (1) #NOTIFIED-DATE #SSN-A INTO #TEXT (1)
  END-IF                                                      /* RE1 END
  COMPRESS #RELATIONSHIP #NODATA #PHONE #NODATA
    INTO #TEXT (2)
ELSE
  #DEATH-NOTIFIED-BY-CONTACT-ID := DEATH-NOTIFIED-BY-CONTACT-ID
  IF DATE-DEATH-NOTIFY = 0
    COMPRESS #TEXT (1) #NOTIFIED-DATE #NODATA INTO #TEXT (1)
  ELSE
    COMPRESS DDN-MONTH DDN-DAY DDN-YEAR INTO #SSN-A WITH DELIMITER '/'
    COMPRESS #TEXT (1) #NOTIFIED-DATE #SSN-A INTO #TEXT (1)
  END-IF
  #PHONE-NUM := FUNCTION-DEFAULT-PHONE (< #DEATH-NOTIFIED-BY-CONTACT-ID >)
  FIND CONTACT WITH CONTACT-ID = #DEATH-NOTIFIED-BY-CONTACT-ID
    #TEXT (2) := FUNCTION-CONTACT-NAME-2 (< CONTACT.CONTACT-ID >)
    COMPRESS 'Name:' #TEXT (2) #TEXT (1) INTO #TEXT (1)
    FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID
        AND CONTACT-2-ID = #DEATH-NOTIFIED-BY-CONTACT-ID
      #SSN-A := FUNCTION-DISP-RL-DESC (<ROLE-2-CD>)
    END-FIND
    COMPRESS #RELATIONSHIP #SSN-A #PHONE
      #PHONE-NUM
      INTO #TEXT (2)
  END-FIND
END-IF
*
ON ERROR
  COMPRESS 'Application error' *ERROR-NR *ERROR-LINE *PROGRAM
    INTO #TEXT  (1)
  ESCAPE ROUTINE
END-ERROR
*
END
