* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBROUTINE-ID: GFWAGRXX
*
*  APPLIES FREE WAGE CREDITS TO WAGE HISTORY
*
*  1. IF RETIRED PRIOR TO 1957 RESET ALL OFFSET WAGES
*  2. IF RETIRED WITH NO RETIRED DATE OR IF RETIRED DATE IS
*     PRIOR TO 01/02/57 BYPASS ALL FREE WAGES
*  3. IF WAGE FLAG = 'E' OR 'F' BYPASS PROCESSING THIS YEAR
*  4. BYPASS IF NOT ON ACTIVE DUTY DURING THIS YEAR
*  5. IF ON ACTIVE DUTY THE WHOLE YEAR APPLY FULL YEARS FREE
*     WAGES
*  6. IF STARTED ON ACTIVE DUTY DURING THIS YEAR OR RETIRED OR BECAME
*     AGE 65 DURING THIS YEAR APPLY A PARTIAL YEARS FREE WAGES
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.BENEFIT-ANALYSIS-BLOCK
LOCAL  USING GWAGELXX
LOCAL
1 #I1         (I2)
1 #FREE-WAGES-APPLIED (L)
1 #HOLD-RET-DATE   (N8)
1 #USE-RET-RSV-DATE (L)
*
1 ST-V VIEW OF A-STATUS
 2 RET-RSV-DATE   (N8)
END-DEFINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE APPLY-FREE-WAGE-CREDITS
* * * * * * * * * * * * * * * * * * *
*
RESET #USE-RET-RSV-DATE
IF #CGA-MIL-STAT NE 'A'
  IF #CGA-SEP-RET-DT = 0
    IF #CGA-GUARD-RESV     /* added 7/1997 as per RRK
      GET ST-V #CGA-ISN
*
      IF RET-RSV-DATE = 0
        ESCAPE ROUTINE
      END-IF
*
      MOVE TRUE TO #USE-RET-RSV-DATE
      MOVE RET-RSV-DATE TO #CGA-SEP-RET-DT
    ELSE
      ESCAPE ROUTINE
    END-IF
  ELSE
    IF #CGA-SEP-RET-DT LT 19570102
      FOR #I1 1 TO #CGA-NUM-WAGE-HIST-YRS
        RESET #CGA-WAGE-AMT-FW (#I1, 2)
      END-FOR
*
      ESCAPE ROUTINE
*
    END-IF
  END-IF
END-IF
*
IF #CGA-PEBD GT #CGA-BASD
  MOVE #CGA-PEBD TO #WH-LOW-DATE-N
ELSE
  MOVE #CGA-BASD TO #WH-LOW-DATE-N
END-IF
*
MOVE #CGA-SEP-RET-DT TO #WH-HIGH-DATE-N
* * * * * * * * * * * * * * * * * * * * * * * * * *
FOR #I1 1 TO #CGA-NUM-WAGE-HIST-YRS
*           #CGA-WAGE-AMT-FW (#I1, *)
IF   (#CGA-WAGE-FLAG (#I1) = 'F')
 AND (#CGA-ORIG-COMMAND NE 'SU')  /* DO NOT DISTURB ORIGINAL FLAG IF SU
  MOVE ' ' TO #CGA-WAGE-FLAG (#I1)
  ESCAPE TOP
END-IF
*
* IF   (#CGA-WAGE-FLAG (#I1) = 'E')  /*<<<<<<<<<<<<<<<<<<<<<<<
IF ((#CGA-SEP-RET-DT NE 0) AND (#CGA-WAGE-YR (#I1) GT #WH-HIGH-YYYY))
  OR (#CGA-WAGE-YR (#I1) LT #WH-LOW-YYYY)
  ESCAPE TOP
END-IF
*
IF ((#WH-HIGH-YYYY = #CGA-WAGE-YR (#I1)) AND (#WH-HIGH-MMDD = 0101))
  MOVE #CGA-WAGE-HIST-RET-AMT TO #CGA-WAGE-AMT-FW (#I1,2)  /* FOR OFFSET
  ESCAPE TOP
END-IF
*
IF #CGA-WAGE-YR (#I1) = #WH-HIGH-YYYY
  IF #WH-HIGH-DD = 01
    COMPUTE #WH-FREE-WAGE-MONTHS = #WH-HIGH-MM - 1
  ELSE
    MOVE #WH-HIGH-MM TO #WH-FREE-WAGE-MONTHS
  END-IF
ELSE
  IF #CGA-WAGE-YR (#I1) = #WH-LOW-YYYY
    COMPUTE #WH-FREE-WAGE-MONTHS = 13 - #WH-LOW-MM
  ELSE
    MOVE 12 TO #WH-FREE-WAGE-MONTHS
  END-IF
END-IF
*
IF   (#CGA-WAGE-YR (#I1) = #WH-HIGH-YYYY)
 AND (#CGA-WAGE-HIST-RET-AMT = 0)
 AND (NOT #USE-RET-RSV-DATE)     /* NOT FOR GREY-AREA RETIREES
  COMPUTE ROUNDED #CGA-WAGE-HIST-RET-AMT =   /* USE ACTIVE PAY THAT YEAR
      (#CGA-ACT-PAY * #WH-FREE-WAGE-MONTHS) / 10
  IF #CGA-WAGE-HIST-RET-AMT GT #CGA-WAGE-MAX (#I1)
    MOVE #CGA-WAGE-MAX (#I1) TO #CGA-WAGE-HIST-RET-AMT
  END-IF
END-IF
*
IF #CGA-WAGE-YR (#I1) LE 1956
  MOVE 160 TO #WH-FREE-WAGE-UNITS
ELSE
  IF #CGA-WAGE-YR (#I1) LE 1977
    MOVE 300 TO #WH-FREE-WAGE-UNITS
    IF #WH-FREE-WAGE-MONTHS LT 4
      MOVE 1 TO #WH-FREE-WAGE-MONTHS
    ELSE
      IF #WH-FREE-WAGE-MONTHS LT 7
        MOVE 2 TO #WH-FREE-WAGE-MONTHS
      ELSE
        IF #WH-FREE-WAGE-MONTHS LT 10
          MOVE 3 TO #WH-FREE-WAGE-MONTHS
        ELSE
          MOVE 4 TO #WH-FREE-WAGE-MONTHS
        END-IF
      END-IF
    END-IF
  ELSE
    MOVE 100 TO #WH-FREE-WAGE-UNITS
    IF #CGA-WAGE-YR (#I1) = #WH-HIGH-YYYY
      COMPUTE #WH-AFTER-1977-AMT = #CGA-WAGE-HIST-RET-AMT / 300
      IF #WH-AFTER-1977-AMT GT 12
        MOVE 12 TO #WH-FREE-WAGE-MONTHS
      ELSE
        MOVE #WH-AFTER-1977-AMT TO #WH-FREE-WAGE-MONTHS
      END-IF
    ELSE
      COMPUTE #WH-AFTER-1977-AMT = #CGA-WAGE-AMT (#I1) / 300
      IF #WH-AFTER-1977-AMT GT 12
        MOVE 12 TO #WH-FREE-WAGE-MONTHS
      ELSE
        MOVE #WH-AFTER-1977-AMT TO #WH-FREE-WAGE-MONTHS
      END-IF
    END-IF
  END-IF
END-IF
*
RESET #FREE-WAGES-APPLIED
*
IF (#CGA-MIL-STAT = 'R') AND (#CGA-WAGE-YR (#I1) = #WH-HIGH-YYYY)
* IF #CGA-SS-STAT = 'Z'     /*  REMOVED 01/92 AS PER JUD WALTON
*   RESET #WH-FREE-WAGE-MONTHS
* END-IF
*
  MOVE #CGA-WAGE-HIST-RET-AMT TO #WH-AMT-WITH-FREE-WAGE
  IF #CGA-WAGE-HIST-RET-AMT GE #CGA-WAGE-MAX (#I1)
    MOVE #CGA-WAGE-MAX (#I1) TO #WH-AMT-WITH-FREE-WAGE
    MOVE #WH-AMT-WITH-FREE-WAGE TO #CGA-WAGE-AMT-FW (#I1, 2) /* OFFSET
  ELSE
    MOVE #CGA-WAGE-HIST-RET-AMT TO #CGA-WAGE-AMT-FW (#I1, 2) /* OFFSET
*
    IF #CGA-ORIG-COMMAND NE 'SO'
      MOVE TRUE TO #FREE-WAGES-APPLIED
      COMPUTE #WH-AMT-WITH-FREE-WAGE = #CGA-WAGE-HIST-RET-AMT +
            (#WH-FREE-WAGE-UNITS * #WH-FREE-WAGE-MONTHS)
      IF #WH-AMT-WITH-FREE-WAGE GE #CGA-WAGE-MAX (#I1)
        MOVE #CGA-WAGE-MAX (#I1) TO #WH-AMT-WITH-FREE-WAGE
      END-IF
    END-IF
  END-IF
*
  IF #CGA-ORIG-COMMAND NE 'SU'
    MOVE #WH-AMT-WITH-FREE-WAGE TO #CGA-WAGE-HIST-RET-AMT
  END-IF
*
* IF #CGA-SS-STAT = 'Z'
*   MOVE #WH-AMT-WITH-FREE-WAGE TO #CGA-WAGE-AMT-FW (#I1, 1) /* SS
*
*   IF (#FREE-WAGES-APPLIED) AND (#CGA-ORIG-COMMAND NE 'SU')
*    AND (#CGA-WAGE-FLAG (#I1) NE 'E')
*     MOVE 'F' TO #CGA-WAGE-FLAG (#I1)
*   END-IF
* ELSE
    RESET #FREE-WAGES-APPLIED
    IF  (#CGA-WAGE-YR (#I1) NE #CGA-TODAY-YYYY-N)
     OR ((#CGA-WAGE-YR (#I1) = #CGA-TODAY-YYYY-N)
               AND (#CGA-WAGE-AMT-FW (#I1, 1) NE 0))
*
      IF #CGA-WAGE-AMT-FW (#I1, 1) GE #CGA-WAGE-MAX (#I1)
        MOVE #CGA-WAGE-MAX (#I1) TO #CGA-WAGE-AMT-FW (#I1, 1)
      ELSE
        MOVE TRUE TO #FREE-WAGES-APPLIED
*
        IF   (#CGA-ORIG-COMMAND NE 'SU')
         AND (#CGA-WAGE-FLAG (#I1) NE 'E')
          MOVE 'F' TO #CGA-WAGE-FLAG (#I1)
        END-IF
*
        COMPUTE #CGA-WAGE-AMT-FW (#I1, 1) = #CGA-WAGE-AMT-FW (#I1, 1) +
              (#WH-FREE-WAGE-UNITS * #WH-FREE-WAGE-MONTHS)
        IF #CGA-WAGE-AMT-FW (#I1, 1) GE #CGA-WAGE-MAX (#I1)
          MOVE #CGA-WAGE-MAX (#I1) TO #CGA-WAGE-AMT-FW (#I1, 1)
        END-IF
      END-IF
*
    END-IF
* END-IF
ELSE
  IF #CGA-WAGE-AMT-FW (#I1, 1) GE #CGA-WAGE-MAX (#I1)
    MOVE #CGA-WAGE-MAX (#I1) TO #CGA-WAGE-AMT-FW (#I1, 1)
  ELSE
    MOVE TRUE TO #FREE-WAGES-APPLIED
*
    IF   (#CGA-ORIG-COMMAND NE 'SU')
     AND (#CGA-WAGE-FLAG (#I1) NE 'E')
      MOVE 'F' TO #CGA-WAGE-FLAG (#I1)
    END-IF
*
    COMPUTE #CGA-WAGE-AMT-FW (#I1, 1) = #CGA-WAGE-AMT-FW (#I1, 1) +
          (#WH-FREE-WAGE-UNITS * #WH-FREE-WAGE-MONTHS)
    IF #CGA-WAGE-AMT-FW (#I1, 1) GE #CGA-WAGE-MAX (#I1)
      MOVE #CGA-WAGE-MAX (#I1) TO #CGA-WAGE-AMT-FW (#I1, 1)
    END-IF
  END-IF
*
  MOVE #CGA-WAGE-AMT-FW (#I1, 1) TO #CGA-WAGE-AMT-FW (#I1, 2)
END-IF
*
IF #CGA-WAGE-YR (#I1) LE 1956
  MOVE 0 TO #CGA-WAGE-AMT-FW (#I1, 2)
END-IF
*
END-FOR /*(0072)
*
IF #USE-RET-RSV-DATE
  RESET #CGA-SEP-RET-DT
END-IF
*
END-SUBROUTINE
*
END
