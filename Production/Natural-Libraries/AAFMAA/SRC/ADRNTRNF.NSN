* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ADRNTRNF
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    01/31/2011  None    Program written                           *
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID         (N8)
1 #ADDR-POOL-ID       (N8)
1 #UPDATE-REQUEST     (I4)
1 #TR-ISN             (P8)
1 #CLERK-ID           (A3)
LOCAL
1 #HEADER
  2 #TR-ID-NUMBER     (N6)
  2 #TR-NAME          (A) DYNAMIC
  2 #TR-DATE-LAST-UPDATE (N8)
  2 #TR-CLERK-ID      (A3)
  2 #LOG-DESCRIPTION   (A) DYNAMIC
1 #OLD-DATA-TABLE   (32)
  2 #FIELD-DESCRIPTION (A10)
  2 #OLD-DATA          (A115)
1 #NEW-DATA-TABLE   (32)
  2 #NEW-DATA-TAG      (A3)
  2 #NEW-DATA          (A122)
*
1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER          (N6)
*
1 A-ADD-POOL VIEW OF A-ADDRESS-POOL
*
1 #POOL-ISN            (P8)
1 #TEXT                (A/3) DYNAMIC
*
1 #PRIMARY-DELETE   (I4) CONST <1>
1 #PRIMARY-UPDATE   (I4) CONST <2>
1 #SECONDARY-DELETE (I4) CONST <3>
1 #SECONDARY-UPDATE (I4) CONST <4>
*
END-DEFINE
*
RESET
  #HEADER
  #OLD-DATA-TABLE (*)
  #NEW-DATA-TABLE (*)
#TR-CLERK-ID := #CLERK-ID
FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    PRINT *PROGRAM 'No Contact :' #CONTACT-ID
    ESCAPE ROUTINE
  END-NOREC
  #LOG-DESCRIPTION       := 'CG-ADD-AD'
  #TR-DATE-LAST-UPDATE   := *DATN
  #TR-NAME               := FUNCTION-CONTACT-NAME-2 (< #CONTACT-ID >)
  #TR-ID-NUMBER          := CONTACT.ID-NUMBER
  IF #UPDATE-REQUEST = #PRIMARY-DELETE OR= #PRIMARY-UPDATE
    #FIELD-DESCRIPTION(1)  := 'ADDRESS 1'
    #FIELD-DESCRIPTION(2)  := 'ADDRESS 2'
    #FIELD-DESCRIPTION(3)  := 'ADDRESS 3'
  ELSE
    #FIELD-DESCRIPTION(1)  := 'S-ADDRS 1'
    #FIELD-DESCRIPTION(2)  := 'S-ADDRS 2'
    #FIELD-DESCRIPTION(3)  := 'S-ADDRS 3'
  END-IF
END-FIND
*
FIND A-ADD-POOL WITH ADDRESS-POOL-ID = #ADDR-POOL-ID
  IF NO RECORDS FOUND
    PRINT *PROGRAM 'No Address :' #ADDR-POOL-ID
    ESCAPE ROUTINE
  END-NOREC
  #POOL-ISN := *ISN (0074)
  CALLNAT 'ADRNTRVW' #CONTACT-ID
    #POOL-ISN
    #TEXT (*)
  #NEW-DATA (1) := #TEXT (1)
  #NEW-DATA (2) := #TEXT (2)
  #NEW-DATA (3) := #TEXT (3)
END-FIND
*
CALLNAT 'GPRN0007'
  #HEADER
  #OLD-DATA-TABLE (*)
  #NEW-DATA-TABLE (*)
  #TR-ISN
*
END
