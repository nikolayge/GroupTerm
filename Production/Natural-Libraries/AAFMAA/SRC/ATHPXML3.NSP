* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPXML3 - modify print file & add XML Info
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  03102005 xxx  Created
* DRW  04142005 DW1  Counted Documents by CN
* DRW  05092005 DW2  Use Policy Number & Use '~~' to Indicate M/E Notics
* DRW  05302005 DW3  Get Doc Type from 1st rec & put on all recs
* DRW  06022005 DW4  Replace '&<>' with XML OK chars &amp; &lt; &gt;
*******************************************
*
DEFINE DATA
LOCAL
*
1 #JCL-PARM       (A10)
1 REDEFINE #JCL-PARM   /* DATE PARM 03/01/1999
 2 #JCL-MM    (A2)
 2 #JCL-SL1   (A1)
 2 #JCL-DD    (A2)
 2 #JCL-SL2   (A1)
 2 #JCL-YYYY  (A4)
1 REDEFINE #JCL-PARM
 2 #JCL-DOC-TYPE      (A8)  /* TERM/LOANPMT/NOTICE
*
1 #PARM-YYYYMMDD      (A8)
1 #PARM-MMDDYYYY      (A10)
1 #DOC-TYPE    (A8)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY  (A4)
 2 #MM-A  (A2)
 2 #DD-A  (A2)
*
1 #I1        (I2)
1 #I2        (I2)
1 #PAGE-TOP  (A1) INIT <H'0C'>
1 #ME-IND      (A2)  INIT <'~~'> /* Inds M/E notices from ATHP07P0 DW2
1 #POL-NUM     (A16)     /* DW2
1 #CN     (N6)
1 REDEFINE #CN
 2 #CN-A   (A6)
1 #LAST-CN       (N6)   /* DW2
1 #LAST-POL-NUM  (A16)   /* DW2
1 #LINES-SKIPPED (N3)
1 #NUM-DOCS      (N7)
1 #PAGE-NUM      (N7)
1 #NUM-PAGES     (N7)
1 #M-E-NOTC          (L)
1 #M-E-NOTC-1ST-LINE (L)
1 #M-E-NOTC-ASTS     (L)
1 #M-E-NOTC-TOTAL-PAGE (L)
1 INPUT-FILE   (A81)
1 REDEFINE INPUT-FILE
 2 INPUT-FILE-X   (A1/81)   /* DW4
1 REDEFINE INPUT-FILE
 2 INP-POS-1      (A1)
 2 INP-POS-2-81  (A80)
 2 REDEFINE INP-POS-2-81
  3 INP-POS-2   (A1)
  3 INP-CMND    (A2)
  3 INP-FIL1    (A1)
  3 INP-ID      (A6)
  3 REDEFINE  INP-ID
   4 INP-CN-6-A (A6)
  3 REDEFINE  INP-ID
   4 INP-CN-5-A (A5)
  3 REDEFINE  INP-ID
   4 INP-CN-4-A (A4)
  3 REDEFINE  INP-ID
   4 INP-CN-3-A (A3)
  3 REDEFINE  INP-ID
   4 INP-CN-6   (N6)
  3 REDEFINE  INP-ID
   4 INP-CN-5   (N5)
  3 REDEFINE  INP-ID
   4 INP-CN-4   (N4)
  3 REDEFINE  INP-ID
   4 INP-CN-3   (N3)
  3 INP-FIL2    (A3)
  3 INP-FIL3    (A1)
  3 INP-SUFFIX  (A3)
  3 INP-17-20   (A4)
  3 INP-REF     (A3)
  3 INP-FIL4    (A34)
  3 INP-POL-NUM (A16) /* Policy Number                  /* DW2
  3 INP-FIL5    (A6)
 2 REDEFINE INP-POS-2-81   /* M/E Notice layout
  3 INP-NOTC-FIL1   (A44)
  3 REDEFINE INP-NOTC-FIL1
   4 INP-NOTC-FIL2  (A8)
   4 INP-NOTC-ASTS  (A14)
   4 INP-NOTC-FIL3  (A22)
  3 INP-NOTC-CN     (N6) /* Mailed to CN (Owner of pol) Usually Insured
  3 INP-NOTC-FIL4   (A1)
  3 INP-NOTC-FOR-CN (A6)    /* Insured CN if mailed to Owner
  3 INP-NOTC-FIL5   (A20)
  3 INP-NOTC-ME-IND (A2)   /* '~~' to indicate M/E Notices   /* DW2
*
1 OUTPUT-FILE  (A100)
1 #PARSED-LINE (A100)     /* DW4
1 REDEFINE #PARSED-LINE
 2 #PARSED-LINE-X (A1/100)   /* DW4
*
1 #1ST-REC     (L)
1 #1ST-TIME    (L)
1 #FOUND-REF   (L)
1 #LAST-ID     (A6)
1 #RECS-IN    (P7)
1 #RECS-OUT   (P7)
1 #PAGES-OUT   (P7)
1 #RECS-CLEARED (P7)
*
1 #LINES-ON-A-PAGE   (N3)
*
END-DEFINE
*
READ WORK 1 ONCE RECORD #JCL-PARM
MOVE #JCL-PARM TO #PARM-MMDDYYYY
COMPRESS #JCL-YYYY #JCL-MM #JCL-DD INTO #PARM-YYYYMMDD LEAVING NO
WRITE 'PARM DATE = ' #PARM-YYYYMMDD
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT      LS=145 PS=76
FORMAT (1)  LS=145 PS=76
FORMAT (2)  LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
               '(2) AAFMAA Print lines deleted'
               5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
*
MOVE TRUE TO #1ST-REC
READ WORK 2 RECORD INPUT-FILE    /* Count documents prior to processing
  ADD 1 TO #RECS-IN
  IF #1ST-REC
    RESET #1ST-REC
    IF NOT INP-POS-1 = '1' OR = #PAGE-TOP
      MOVE '1' TO INP-POS-1
    END-IF
  END-IF
*
  IF  ((INP-POS-1 = '1')   /* top of page
   OR  (INP-POS-1 = #PAGE-TOP))
   AND (INP-POS-2-81 NE MASK (' TOTAL COUNTS')) /* M/E Notice total page
   AND (INP-NOTC-ASTS NE '**************')     /*Must be M/E Setup Page
    IF INP-POS-2 NE ' '   /* Must be top of M/E notice (ZIP Bar-Code)
      READ WORK 2 ONCE RECORD INPUT-FILE  /* GET NEXT REC for TOP OF PAG
        IF INP-NOTC-ASTS = '**************'
          ESCAPE TOP
        END-IF
    END-IF
*
    RESET #CN                      /* DW1
    MOVE INP-POL-NUM TO #POL-NUM   /* DW2
    IF   (INP-NOTC-CN = MASK (NNNNNN)) /* Must be M/E Notc 1st Addr Lin
     AND (INP-NOTC-ME-IND = #ME-IND)     /* DW2
      IF INP-NOTC-FOR-CN = MASK (NNNNNN)  /* Notice sent to Owner
        MOVE INP-NOTC-FOR-CN TO #CN-A     /* Index with Insured
      ELSE                                /* Notices sent to Insured
        MOVE INP-NOTC-CN TO #CN           /* Index with Insured
      END-IF
      MOVE TRUE TO #M-E-NOTC
    ELSE
      RESET #M-E-NOTC
      IF INP-CN-6-A = MASK (NNNNNN)
        MOVE INP-CN-6 TO #CN
      ELSE
        IF INP-CN-5-A = MASK (NNNNN)
          MOVE INP-CN-5 TO #CN
        ELSE
          IF INP-CN-4-A = MASK (NNNN)
            MOVE INP-CN-4 TO #CN
          ELSE
            IF INP-CN-3-A = MASK (NNN)
              MOVE INP-CN-3 TO #CN
            ELSE
              WRITE 'CN format not found on' INP-ID 'in Count Phase'
                  'on doc#' #NUM-DOCS 'on rec#' #RECS-IN
              WRITE 'INT-NOTC-AST=' INP-NOTC-ASTS
              WRITE INPUT-FILE
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
*    DISPLAY #POL-NUM #LAST-POL-NUM #CN #LAST-CN
    IF   (#POL-NUM NE #LAST-POL-NUM)    /* DW2
     AND (#CN      NE #LAST-CN)         /* DW2
      ADD 1 TO #NUM-DOCS
    END-IF
*
    MOVE #POL-NUM TO #LAST-POL-NUM
    MOVE #CN      TO #LAST-CN
  END-IF
END-WORK
RESET #CN #POL-NUM #LAST-POL-NUM #LAST-CN #RECS-IN
*
WRITE 'num-docs read in count phase =' #NUM-DOCS
*
MOVE '<ns0:DALTransfer xmlns:ns0="http://DALSchema.Request">'
   TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
COMPRESS '<TRANSFERDATE>' *DAT4U  '</TRANSFERDATE>'
    INTO OUTPUT-FILE LEAVING NO
PERFORM WRITE-PRINT-LINE
*
COMPRESS '<TOTALDOCUMENTS>' #NUM-DOCS '</TOTALDOCUMENTS>'
      INTO OUTPUT-FILE LEAVING NO
PERFORM WRITE-PRINT-LINE
*
COMPRESS '<BATCHID>' '??' '</BATCHID>' INTO OUTPUT-FILE LEAVING NO
PERFORM WRITE-PRINT-LINE
*
MOVE '<DOCUMENTS>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
MOVE TRUE TO #1ST-TIME #1ST-REC
READ WORK 2 RECORD INPUT-FILE
  ADD +1 TO #RECS-IN
*
  IF #1ST-REC
    RESET #1ST-REC
    IF NOT INP-POS-1 = '1' OR = #PAGE-TOP
      MOVE '1' TO INP-POS-1
    END-IF
  END-IF
*
  IF INP-POS-2 NE ' '
*   WRITE (2) INPUT-FILE
    RESET INP-POS-2-81
    IF INP-POS-1 = '1' /* Must be M/E Notice since this is bar code line
      IGNORE           /* Page top will be developed on next line
    ELSE
      IF INP-POS-1 = '0'  /* skip 2 line
        ADD 2 TO #LINES-SKIPPED
      ELSE
        IF INP-POS-1 = '-'  /* skip 3 line
          ADD 3 TO #LINES-SKIPPED
        ELSE
          ADD 1 TO #LINES-SKIPPED
        END-IF
      END-IF
    END-IF
*
    ADD 1 TO #RECS-CLEARED
    ESCAPE TOP
  END-IF
*
  RESET #M-E-NOTC-1ST-LINE
  IF   (INP-NOTC-CN = MASK (NNNNNN)) /* Must be M/E Notice 1st Addr Line
   AND (INP-NOTC-ME-IND = #ME-IND)   /* DW2
    MOVE '1' TO INP-POS-1
    MOVE TRUE TO #M-E-NOTC-1ST-LINE
*   WRITE 'Found M/E 1st line' INPUT-FILE
  END-IF
*
  IF   (INP-NOTC-ASTS = '**************') /*Must be M/E Setup Page
   AND (INP-NOTC-ME-IND = #ME-IND)   /* DW2
    MOVE '1' TO INP-POS-1
*   WRITE 'Found M/E **** 1st line A' INPUT-FILE
  END-IF
*
  IF  (INP-POS-1 = '1')   /* top of page
   OR (INP-POS-1 = #PAGE-TOP)
    ADD 1 TO #NUM-PAGES
    RESET #POL-NUM      /* DW2
*   IF #NUM-PAGES GT 200
*     ESCAPE BOTTOM
*   END-IF
*
    IF INP-REF = 'REF'   /* TOP OF PAGE OF AD REF   ?????
      MOVE TRUE TO #FOUND-REF
      MOVE INP-ID TO #LAST-ID
    ELSE
      IF (#FOUND-REF) AND (INP-ID = #LAST-ID)
        RESET #FOUND-REF
        ESCAPE TOP
      END-IF
    END-IF
*
    RESET #M-E-NOTC-TOTAL-PAGE
    IF INP-POS-2-81 = MASK (' TOTAL COUNTS') /* M/E Notice total page
      MOVE TRUE TO #M-E-NOTC-TOTAL-PAGE
*     WRITE 'Found Notc Total Page'
      ESCAPE TOP
    END-IF
*
    IF   (INP-NOTC-ASTS = '**************') /* Must be M/E Setup Page
     AND (INP-NOTC-ME-IND = #ME-IND)   /* DW2
      MOVE TRUE TO #M-E-NOTC-ASTS
*     WRITE 'Found M/E **** 1st line B' INPUT-FILE
      ESCAPE TOP
    ELSE
      RESET #M-E-NOTC-ASTS
    END-IF
*
    RESET #CN
    MOVE INP-POL-NUM TO #POL-NUM   /* DW2
    IF #M-E-NOTC-1ST-LINE
*     DISPLAY INP-NOTC-CN INP-NOTC-FOR-CN
      IF INP-NOTC-FOR-CN = MASK (NNNNNN)  /* Notice sent to Owner
        MOVE INP-NOTC-FOR-CN TO #CN-A     /* Index with Insured
      ELSE                                /* Notices sent to Insured
        MOVE INP-NOTC-CN TO #CN           /* Index with Insured
      END-IF
*
      MOVE TRUE TO #M-E-NOTC
    ELSE
      RESET #M-E-NOTC
      IF INP-CN-6-A = MASK (NNNNNN)
        MOVE INP-CN-6 TO #CN
      ELSE
        IF INP-CN-5-A = MASK (NNNNN)
          MOVE INP-CN-5 TO #CN
        ELSE
          IF INP-CN-4-A = MASK (NNNN)
            MOVE INP-CN-4 TO #CN
          ELSE
            IF INP-CN-3-A = MASK (NNN)
              MOVE INP-CN-3 TO #CN
            ELSE
              WRITE 'CN format not found on' INP-ID 'Process Phase'
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
    PERFORM DEVELOPE-DOC-TYPE
*
*    WRITE 'curr-cn=' #CN 'last-cn=' #LAST-CN
*          'curr-pol-num=' #POL-NUM 'last-pol-num=' #LAST-POL-NUM
    IF   ((#POL-NUM = #LAST-POL-NUM) AND (#LAST-POL-NUM NE ' '))
     AND ((#CN      = #LAST-CN)      AND (#LAST-CN      NE 0))
      PERFORM CLOSE-PAGE
    ELSE
      IF  (#LAST-POL-NUM NE ' ')
       OR (#LAST-CN      NE 0)
        PERFORM CLOSE-PAGE
        PERFORM CLOSE-DOCUMENT
        RESET #PAGE-NUM
      END-IF
*
      MOVE '<DOCUMENT>' TO OUTPUT-FILE
      PERFORM WRITE-PRINT-LINE
*
      COMPRESS '<DOCTYPE>' #DOC-TYPE '</DOCTYPE>'
        INTO OUTPUT-FILE LEAVING NO
      PERFORM WRITE-PRINT-LINE
*
      COMPRESS '<CN>' #CN '</CN>' INTO OUTPUT-FILE LEAVING NO
      PERFORM WRITE-PRINT-LINE
*
      IF #POL-NUM NE ' '                                    /* DW2
        COMPRESS '<POL-NUM>' #POL-NUM '</POL-NUM>'          /* DW2
             INTO OUTPUT-FILE LEAVING NO
        PERFORM WRITE-PRINT-LINE
      END-IF
*
      COMPRESS '<PAGES numPages="' '??' '">' INTO OUTPUT-FILE LEAVING NO
      PERFORM WRITE-PRINT-LINE
    END-IF
*
    MOVE #POL-NUM TO #LAST-POL-NUM
    MOVE #CN      TO #LAST-CN
    ADD 1 TO #PAGE-NUM
*
    COMPRESS '<PAGE number="' #PAGE-NUM '">' INTO OUTPUT-FILE LEAVING NO
    PERFORM WRITE-PRINT-LINE
*
    MOVE '<BODY>' TO OUTPUT-FILE
    PERFORM WRITE-PRINT-LINE
*
    ADD 1 TO #PAGES-OUT
    RESET #LINES-ON-A-PAGE
    RESET INP-POS-1
*
    IF #1ST-TIME   /* no page top on 1st record
      RESET #1ST-TIME
    ELSE
      RESET INP-POS-1   /* aafm top of page
    END-IF
  ELSE
    IF  #M-E-NOTC-ASTS       /* SKIP all M/E Setup pages
     OR #M-E-NOTC-TOTAL-PAGE /* SKIP all M/E Total pages
      ESCAPE TOP
    END-IF
*
    IF   (INP-POS-1 = ' ')
     AND (INP-POS-2-81 = ' ')    /* Skip 1 line
      ADD 1 TO #LINES-SKIPPED
      ESCAPE TOP
    END-IF
*
    IF   (INP-POS-1 = '0')  /* skip 2 line
     AND (INP-POS-2-81 = ' ')
      ADD 2 TO #LINES-SKIPPED
      ESCAPE TOP
    END-IF
*
    IF   (INP-POS-1 = '-')  /* skip 3 line
     AND (INP-POS-2-81 = ' ')
      ADD 3 TO #LINES-SKIPPED
      ESCAPE TOP
    END-IF
*
    IF   (INP-POS-1 = '0')  /* skip 2 lines with current data
     AND (INP-POS-2-81 NE ' ')
      ADD 1 TO #LINES-SKIPPED
*     PERFORM LINES-SKIPPED-TEST
      RESET INP-POS-1
    END-IF
*
    IF   (INP-POS-1 = '-')  /* skip 3 lines with current data
     AND (INP-POS-2-81 NE ' ')
      ADD 2 TO #LINES-SKIPPED
*     PERFORM LINES-SKIPPED-TEST
      RESET INP-POS-1
    END-IF
*
    PERFORM LINES-SKIPPED-TEST
  END-IF
*
  PERFORM PARSE-INPUT-FILE     /* DW4
  COMPRESS '<LINE>' #PARSED-LINE '</LINE>' INTO OUTPUT-FILE LEAVING NO
  PERFORM WRITE-PRINT-LINE
END-WORK
*
IF #RECS-IN GT 0   /* Close out prior document
  PERFORM CLOSE-PAGE
  PERFORM CLOSE-DOCUMENT
END-IF
*
MOVE '</DOCUMENTS>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
MOVE '</ns0:DALTransfer>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
WRITE  'TOTAL RECS read    =' #RECS-IN
WRITE  'TOTAL RECS written =' #RECS-OUT
WRITE  'TOTAL pages out    =' #PAGES-OUT
WRITE  'TOTAL pages written=' #NUM-PAGES
WRITE  'TOTAL Num Documents=' #NUM-DOCS
WRITE  'TOTAL RECS cleared =' #RECS-CLEARED
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE LINES-SKIPPED-TEST
* * * * * * * * * * * * * * * * * *
*
IF #LINES-SKIPPED NE 0
  COMPRESS '<LINE break="' #LINES-SKIPPED '"/>'
       INTO OUTPUT-FILE LEAVING NO
  PERFORM WRITE-PRINT-LINE
  RESET #LINES-SKIPPED
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE CLOSE-PAGE
* * * * * * * * * * * * * *
*
MOVE '</BODY>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
MOVE '</PAGE>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CLOSE-DOCUMENT
* * * * * * * * * * * * * * * * *
*
MOVE '</PAGES>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
MOVE '</DOCUMENT>' TO OUTPUT-FILE
PERFORM WRITE-PRINT-LINE
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DEVELOPE-DOC-TYPE
* * * * * * * * * * * * * * * * * *
*
IF #M-E-NOTC               /* M/E Prem/Int/Paid-up Etc notice
  IF INP-SUFFIX = 'PDU'        /* Paid-up-notices
    MOVE 'COMM' TO #DOC-TYPE   /* Communications
  ELSE
    MOVE 'NOTC' TO #DOC-TYPE
  END-IF
ELSE
  IF   (INP-CMND = 'ST' OR = 'AD' OR = 'CI')  /* Status/AD/CI Screen
   AND (INP-SUFFIX = 'LAP') /* from ATHPLAPS    /*DW3
    MOVE 'TERM' TO #DOC-TYPE
  ELSE
    IF   (INP-CMND = 'AD')   /* Loan Pmt letter
     AND (INP-SUFFIX = MASK ('LP'))  /* LPF/LPV  /*DW3
      MOVE 'LOAN' TO #DOC-TYPE
    ELSE
      IF INP-SUFFIX = 'CCE'    /* Cred Card Expireation   /* DW3
        MOVE 'NOTC' TO #DOC-TYPE
      ELSE
        IF INP-SUFFIX = 'PNC' /* Cr Crd Conv to mode Q Ltr  /* DW3
          MOVE 'NOTC' TO #DOC-TYPE
        ELSE
          IF INP-SUFFIX = 'PDS'    /* PAD Start Letter        /* DW3
            MOVE 'EZP' TO #DOC-TYPE
          ELSE
            IF INP-SUFFIX = 'L2R'    /* LT to RT CONV Letter    /* DW3
              MOVE 'COMM' TO #DOC-TYPE
            ELSE
              WRITE #CN 'No Doc Type - Used previous of' #DOC-TYPE
                'Cmnd=' INP-CMND 'Suffix=' INP-SUFFIX
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-PRINT-LINE
* * * * * * * * * * * * * * * * *
*
WRITE WORK 3 OUTPUT-FILE
ADD +1 TO #RECS-OUT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PARSE-INPUT-FILE     /* DW4
* * * * * * * * * * * * * * * * *
*
RESET #PARSED-LINE #I2
*
FOR #I1 1 TO 81
  IF INPUT-FILE-X (#I1) = '&' OR = '<' OR = '>'
    ADD 1 TO #I2
    MOVE '&' TO #PARSED-LINE-X (#I2)
    IF INPUT-FILE-X (#I1) = '&'
      MOVE 'a' TO #PARSED-LINE-X (#I2 + 1)
      MOVE 'm' TO #PARSED-LINE-X (#I2 + 2)
      MOVE 'p' TO #PARSED-LINE-X (#I2 + 3)
      ADD 3 TO #I2
    END-IF
*
    IF INPUT-FILE-X (#I1) = '<'
      MOVE 'l' TO #PARSED-LINE-X (#I2 + 1)
      MOVE 't' TO #PARSED-LINE-X (#I2 + 2)
      ADD 2 TO #I2
    END-IF
*
    IF INPUT-FILE-X (#I1) = '>'
      MOVE 'g' TO #PARSED-LINE-X (#I2 + 1)
      MOVE 't' TO #PARSED-LINE-X (#I2 + 2)
      ADD 2 TO #I2
    END-IF
*
    ADD 1 TO #I2
    MOVE ';' TO #PARSED-LINE-X (#I2)
    WRITE 'Parse=' INPUT-FILE #PARSED-LINE
  ELSE
    ADD 1 TO #I2
    MOVE INPUT-FILE-X (#I1) TO #PARSED-LINE-X (#I2)
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
