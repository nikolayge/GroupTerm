* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AP1099TB PRODUCES A SORTED TRIAL BALANCE
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    01132005  PM1     Added minimum amount for print.             *
*                                                                      *
* PAM    01192005  PM2     Do not reset tax amount for type '2' and    *
*                          type 'N' 1099Rs.                            *
*                                                                      *
* PAM    01242005  PM3     Annuitants (Type D or N) should always print*
*                          regardless of taxable amount. Also include  *
*                          Type '2'.                                   *
*                                                                      *
* PAM    01252005  PM4     Reset taxable amount for type '2' 1099R's.  *
*                          Continue to include gross and prms paid.    *
*                                                                      *
* PAM    10042005  PM5     Added new distribution code 'L'             *
*                                                                      *
* PAM    12232005  PM6     Added new minimum amount for Type A of $600 *
*                                                                      *
* PAM    12192006  PM7     Added second NAME line for INT forms        *
* YAK    01232007  YAK     Fixed #INT-TYPE assignment                  *
* PAM    01242007  PM8     Added new minimum amount for Type D of $600 *
* bz   06/09/2013          Rebranding (Search for AAFMAA)              *
* JNT  09/06/2013  JT1     Address normalization                       *
* bz-Name-C 07/07/2017     Fine Names. Keep all capital    
************************************************************************
*
DEFINE DATA
LOCAL USING ADDA0001                                 /* JT1
LOCAL
1 CAL-YEAR  (N4) INIT <2007>
*
1  INT-1099-V VIEW OF A-INT-1099
 2 YEAR-INT-TYPE-KEY
 2 SSN            (N9)                               /* JT1 start
 2 REDEFINE SSN 
  3 SSN-A         (A9)
 2 SSN-TAX-ID-IND     (A1)
 2 CONTACT-ID     (N8)                               
*  2 ADDR-1    (A25)
*  2 ADDR-1-A  (A25)
*  2 ADDR-2    (A25)
*  2 REDEFINE ADDR-2
*   3 #A2   (A1/1:25)
*  2 ZIP-CODE   (N9)
*  2 REDEFINE ZIP-CODE
*   3 ZIP-1    (N5)
*   3 ZIP-2    (N4)                                  /* JT1 end
 2 AMOUNT    (P9.2)
 2 GROSS-AMT   (P9.2)
 2 RELATED-ID-NUMBER    (N6)
* 2 NAME         (A25)                               /* JT1
 2 NAME-2       (A25)                                             /* PM6
 2 INSURED      (A25)
 2 INT-TYPE     (A1)
*
1 #PREM-PAID  (P9.2)
1 #ZIP      (N9)
1 REDEFINE #ZIP
  2 #ZIP-1    (N5)
  2 #ZIP-2    (A4)
1 REDEFINE #ZIP
  2 #ZIP-A   (A9)
1 #NUM-PAYEES    (N6)
1 #IN    (I2)
1 #STATE   (A2)
1 REDEFINE #STATE
  2 #ST1    (A1)
  2 #ST2    (A1)
1 #TYPE-RPT  (A7) INIT<'1099R'>
*
1 #I1       (I2)
1 #H-ZIP    (A10)
1 #HOLD-ID  (A11)
1 #LAST        (A30)
1 #FIRST       (A30)
1 #SUFFIX      (A30)
1 #EXTRA       (A30)
1 #HOLD-INTER  (A30)
1 #DEL         (A1)  INIT <','>
1 #INT-TYPE    (A1)
1 #MIN-AMT     (P3) INIT <10>                                     /* PM1
1 #MIN-AMT-A   (P3) INIT <600>                                    /* PM6
*
1 #NAME        (A25)                                              /* JT1
1 #ADD-LINE1   (A25)                                              /* JT1
1 #ADD-LINE2   (A25)                                              /* JT1
1 #ADD-LINE3   (A25)                                              /* JT1   
1 #LETTER            (A7) CONST <'!LETTER'>                       /* JT1 /* bz-Name
1 Letter             (A) DYNAMIC                                  /* JT1
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
** LIMIT 15
FORMAT (1) PS=62
          LS=133
WRITE (1) TITLE LEFT  5T 'AP1099TB'
                     27T 'AAFMAA'
    / 32T'2007 SORTED IRS REPORT (BY NAME)' /
             / 4T 'SSN' 21T 'NAME' 50T 'ADDRESS' 76T 'GROSS'
              92T 'TAX' 108T 'PREM' 120T 'ID' 125T 'TP'  /
*
READ INT-1099-V IN LOGICAL BY YEAR-INT-TYPE-KEY STARTING FROM '2007 '
  IF YEAR-INT-TYPE-KEY GE '2008 '
    ESCAPE BOTTOM
  END-IF
*
  IF AMOUNT LT #MIN-AMT                                           /* PM1
   AND NOT INT-TYPE = 'N' OR = 'D' OR = '2'                       /* PM3
    ESCAPE TOP                                                    /* PM1
  END-IF                                                          /* PM1
*
  IF AMOUNT LT #MIN-AMT-A                                         /* PM6
   AND INT-TYPE = 'A' OR = 'D'                                    /* PM8
*  AND INT-TYPE = 'A'                                             /* PM6
    ESCAPE TOP                                                    /* PM6
  END-IF                                                          /* PM6
*
END-ALL
*
SORT BY INT-TYPE #NAME                                            /* JT1
  USING SSN                                                       /* JT1
   SSN-TAX-ID-IND
   #ADD-LINE1                                                     /* JT1
   #ADD-LINE2                                                     /* JT1
   #ADD-LINE3                                                     /* JT1
   #ADD-ZIP-CODE                                                  /* JT1
*    ADDR-1
*    ADDR-1-A
*    ADDR-2
*    ZIP-CODE
   AMOUNT
   GROSS-AMT
   RELATED-ID-NUMBER
   INSURED
   NAME-2                                                         /* PM7
*
  PERFORM COMPUTE-1099
  PERFORM PRINT-1099-TB
*
  AT BREAK OF INT-TYPE
    PERFORM PROCESS-1099-BREAK
    MOVE INT-TYPE TO #INT-TYPE
*
  END-BREAK
END-SORT
*
*******************************
DEFINE SUBROUTINE COMPUTE-1099
*******************************
*
RESET #PREM-PAID
MOVE INT-TYPE TO #INT-TYPE                                        /* YAK
*
IF #INT-TYPE = '2' OR = 'C' OR = 'M' OR = 'L'                     /* PM5
  COMPUTE #PREM-PAID = GROSS-AMT - AMOUNT
END-IF
*
IF #INT-TYPE = '2'  /* OR = 'N'                                   /* PM4
  RESET AMOUNT                                                    /* PM4
END-IF
*
******************
END-SUBROUTINE
******************
*
*****************************
DEFINE SUBROUTINE PRINT-1099-TB
*****************************
*
RESET #PDA-ADDRESS                                           /* JT1 start                      
  MOVE 'P' TO #ADD-TYPE                                         
  #ADD-CONTACT-ID := INT-1099-V.CONTACT-ID                       
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  Letter := #LETTER
  CALLNAT 'GPRS0CNT' #ADD-CONTACT-ID Letter
  #NAME := Letter                                               
*
 MOVE #ADD-LINE-1 TO #ADD-LINE1                                
 MOVE #ADD-LINE-2 TO #ADD-LINE2                                
 MOVE #ADD-LINE-3 TO #ADD-LINE3                                  
 MOVE #ADD-STATE TO #STATE   
 MOVE EDITED #ADD-ZIP-CODE (EM=99999-9999) TO #H-ZIP                                       
*  
* MOVE INT-TYPE TO #INT-TYPE                  /* YAK
* MOVE '  ' TO #STATE
* *
* IF ZIP-1 = 0
*   RESET #STATE
* ELSE
*   FOR #IN 25 TO 1 STEP -1
*     IF #A2 (#IN) NE ' '
*       MOVE  #A2 (#IN) TO #ST2
*       MOVE  #A2 (#IN - 1) TO #ST1
*       RESET #A2 (#IN - 1)
*       RESET #A2 (#IN)
*       ESCAPE BOTTOM
*     END-IF
*   END-FOR
* END-IF
* *
* IF ZIP-2 = 0
*   MOVE ZIP-1 TO #ZIP-1
*   MOVE '    ' TO #ZIP-2
*   MOVE #ZIP-A TO #H-ZIP
* ELSE
*   MOVE ZIP-CODE TO #ZIP
*   MOVE EDITED ZIP-CODE (EM=99999-9999) TO #H-ZIP
* END-IF                                                     /* JT1 end
*
ADD 1 TO #NUM-PAYEES
  WRITE (1) 2T SSN(EM=999-99-9999) 15T #NAME 42T #ADD-LINE1      /* JT1
         70T GROSS-AMT(EM=-ZZZ,ZZZ,ZZ9.99)
         86T AMOUNT(EM=-ZZZ,ZZZ,ZZ9.99)
         102T #PREM-PAID(EM=-ZZZ,ZZZ,ZZ9.99)
         118T RELATED-ID-NUMBER (EM=999999) INT-TYPE /
         15T NAME-2  42T #ADD-LINE2 /                           /* PM6      /* JT1
         15T INSURED 42T #ADD-LINE3 / 42T #STATE 48T #H-ZIP     /* PM6      /* JT1
*
END-SUBROUTINE
*
*************************************
DEFINE SUBROUTINE PROCESS-1099-BREAK
*************************************
*
WRITE (1) 30T 'NUMBER OF PAYEES -' 50T #NUM-PAYEES ' TYPE = ' #INT-TYPE
RESET #NUM-PAYEES
NEWPAGE (1)
*
END-SUBROUTINE
*
END
