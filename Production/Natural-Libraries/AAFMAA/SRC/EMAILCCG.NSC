* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* email address editor INCLUDED FROM C2200PCG/C2250PUP
* * * * *
DEFINE SUBROUTINE EMAIL-EDIT
* * * * *
*
RESET #HAS-@ #HAS-PER #FOUND-BLANK
FOR #E1 1 TO 60
  IF #FOUND-BLANK
    IF #INTR-ADDR-X (#E1) NE ' '
      MOVE 'Imbedded blanks invalid' TO #EMAIL-ERR
    ELSE
      ESCAPE TOP
    END-IF
  END-IF
*
  IF #INTR-ADDR-X (#E1) = ' '
    MOVE TRUE TO #FOUND-BLANK
*
    MOVE #E1 TO #E3   /* find beg of domain type
    FOR #E2 #E1 TO 1 STEP -1
      IF #INTR-ADDR-X (#E2) = '.'
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    FOR #E3 1 TO 4
      ADD 1 TO #E2
      MOVE #INTR-ADDR-X (#E2) TO #DOMAIN-TYPE-X (#E3)
    END-FOR
*
    ESCAPE TOP
  END-IF
*
  IF  (#INTR-ADDR-X (#E1) = MASK (N))
   OR (#INTR-ADDR-X (#E1) = MASK (A))
   OR (#INTR-ADDR-X (#E1) = '@' OR = '.' OR = '-' OR = '_'
      OR = '%' OR = '!' OR = '&' OR = '"'
      OR = '/'
      OR = '\')
    IGNORE
  ELSE
    MOVE 'Only letters numbers @ . - _ % ! / \ &  are valid'
     TO #EMAIL-ERR
  END-IF
*
  IF (#INTR-ADDR-X (#E1) = '@') AND #HAS-@
    MOVE 'Only 1 "@" is allowed' TO #EMAIL-ERR
  END-IF
*
  IF #HAS-@  /* at right of @
    IF #INTR-ADDR-X (#E1) = '.'
      MOVE TRUE TO #HAS-PER
    END-IF
*
    IF #INTR-ADDR-X (#E1) = '_' OR = '%' OR = '!' OR = '&'
         OR = '/' OR = '\'
      MOVE 'Invalid character to right of "@"' TO #EMAIL-ERR
    END-IF
  END-IF
*
  IF #INTR-ADDR-X (#E1) = '@'
    MOVE TRUE TO #HAS-@
  END-IF
END-FOR
*
IF NOT #HAS-@
  MOVE '1 "@" is required' TO #EMAIL-ERR
END-IF
*
IF NOT #HAS-PER
  MOVE 'At least 1 "." required after "@"' TO #EMAIL-ERR
END-IF
*
IF NOT #DOMAIN-TYPE = 'COM' OR = 'NET' OR = 'ORG' OR = 'EDU'
  OR = 'MIL' OR = 'GOV' OR = 'KR' OR = 'DE' OR = 'FR' OR = 'US'
  OR = 'UK' OR = 'IT' OR = 'CA' OR = 'MX' OR = 'VA' OR = 'JP'
  OR = 'SE' OR = 'LT' OR = 'CR' OR = 'GR' OR = 'BE'
*
  OR = 'com' OR = 'net' OR = 'org' OR = 'edu'
  OR = 'mil' OR = 'gov' OR = 'kr' OR = 'de' OR = 'fr' OR = 'us'
  OR = 'uk' OR = 'it' OR = 'ca' OR = 'mx' OR = 'va' OR = 'jp'
  OR = 'se' OR = 'lt' OR = 'cr' OR = 'gr' OR = 'be'
  MOVE '* Non-std Email Domain*' TO #TEXT
END-IF
*
END-SUBROUTINE
* END OF COPYCODE
