* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE FUNCTION FUNCTION-WEB-USER RETURNS (A/2) DYNAMIC BY VALUE
  DEFINE DATA
  PARAMETER
  1 #ACTIVE-DIRECTORY-NAME (A) DYNAMIC
  LOCAL USING OBJLGENT
  LOCAL USING OBJLUSER
  LOCAL
  1 #GEN-KEY     (A4)
  1 #DEFAULT-CLERK (A3) CONST <'DFT'>
  1 #DEFAULT-USER  (A8) CONST <'DEFAULT'>
  END-DEFINE
*
#GEN-KEY := 'WEBU'
READ GEN-TABLE-V WITH GEN-KEY = #GEN-KEY
  IF GEN-KEY NE #GEN-KEY
    ESCAPE BOTTOM
  END-IF
  ACCEPT IF GEN-ALPHA-VALUE = #ACTIVE-DIRECTORY-NAME
    OR *TRANSLATE(GEN-ALPHA-VALUE, UPPER) = #ACTIVE-DIRECTORY-NAME
  FUNCTION-WEB-USER(1) := GEN-SUB-KEY
  FIND(1) USER-V WITH CLERK-ID = GEN-SUB-KEY
    FUNCTION-WEB-USER(2) := USER-V.TERMINAL-ID
  END-FIND
  ESCAPE ROUTINE
END-READ
FUNCTION-WEB-USER(1) := #DEFAULT-CLERK
FUNCTION-WEB-USER(2) := #DEFAULT-USER
*
END-FUNCTION
END
