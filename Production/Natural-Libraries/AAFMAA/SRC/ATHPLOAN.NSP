* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPLOAN - letter to members with loans
*
* TMT  06/10/2014  TT1  CONTACT NORMALIZATION - REMOVED SEX
*
DEFINE DATA
LOCAL
1 #LON-CN     (P6/6000)
1 #LON-ISS-DT (P8/6000)
1 #LON-AMT    (P6.2/6000)
*
1 #LOAN-FILE-OUT      /* (a21)
 2 #CN     (N6)
 2 #ISS-DT (N8)
 2 #LN-AMT (N5.2)
*
1 #LOAN-FILE-IN      /* (a21)
 2 #CN-IN     (N6)
 2 #ISS-DT-IN (N8)
 2 #LN-AMT-IN (N5.2)
*
1 #PRIOR-CN      (N6) INIT <000000>   /* use 999999 if no input
1 REDEFINE #PRIOR-CN
 2 #PRIOR-CN-A   (A6)
1 #PRIOR-ISS-DT  (N8)
1 #PRIOR-LN-AMT  (N6.2)
1 #LN-AMT-TO-USE (N6.2)
1 #SKIP-READ     (L)
1 #ST-FND        (A1)
1 #DEAD          (A1)
1 #NUM-DEAD      (P5)
1 #DOD           (N8)
1 #INACT         (A1)
1 #NUM-INACTIVE  (P5)
1 #NUM-ACTIVE    (P5)
1 #NUM-PURGED    (P5)
1 #TOT-INT       (P9.2)
1 #TOT-LOAN      (P9.2)
1 #INT-DIFX      (N5.7)
1 #INT-DIFF      (N5.7)
1 #TOT-INT-DIFF  (N9.5)
1 #NUM-18        (N5)
1 #NUM-AFT-7/1/04 (N5)
1 #NUM-ADDL-LOANS (N5)
1 #NUM-MBRS      (N5)
1 #VAR-LN        (L)
1 #FILE-IN       (P5)
1 #MONS          (N3)
1 #CNT-DISP      (A6)
1 #CNT-2         (P5)
1 #CNT-3         (P5)
1 #CNT-4         (P5)
1 #CNT-5         (P5)
1 #CNT-6         (P5)
1 #CNT-7         (P5)
1 #CNT-8         (P5)
*
1 #I1            (I2)
1 #VAR-LOAN-DT   (N8)
1 REDEFINE #VAR-LOAN-DT
 2 #VAR-LOAN-DT-YYYYMM  (N6)
 2 REDEFINE #VAR-LOAN-DT-YYYYMM
  3 #VAR-LOAN-DT-YYYY   (N4)
  3 #VAR-LOAN-DT-MM     (N2)
 2 #VAR-LOAN-DT-DD      (N2)
1 #YYYYMMDD    (N8)
*
1 REDEFINE #YYYYMMDD
 2 #YYYYMM  (N6)
 2 REDEFINE #YYYYMM
  3 #YYYY    (N4)
  3 #MM      (N2)
 2 #DD      (N2)
*
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 WIDOW-FLAG
*  2 SEX                        /* TT1
  2 RANK
  2 INTRO-FLAG      (A1)
*
  2 STM-P-I               (9)
  2 STM-P-I-LAST          (9)
  2 X-LOAN-AMOUNT-2       (9)
  2 X-LOAN-DATE-2         (9)
  2 REDEFINE X-LOAN-DATE-2
   3 X-LOAN-DATE-2-OCC   (9)
    4 X-LOAN-DATE-2-YYYYMM (N6)
    4 REDEFINE X-LOAN-DATE-2-YYYYMM
     5 X-LOAN-DATE-2-YYYY   (N4)
     5 X-LOAN-DATE-2-MM     (N2)
    4 X-LOAN-DATE-2-DD     (N2)
  2 X-ACCUM-INTEREST-2    (9)
  2 X-NXT-YR-ACCUM-INT-2  (9)
  2 ISSUE-DATE            (9)
*
1 ST2-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 WIDOW-FLAG
 2 DATE-OF-DEATH
 2 NUMBER-MEMBERSHIPS
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=66
FORMAT (2) LS=133 PS=66
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'VARIABLE LOANS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=66
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'VARIABLE LOANS from 1/1/2003 paid off'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=66
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'New VARIABLE LOANS since 1/1/2003'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
PERFORM ACCUM-MONTHS
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-MONTHS
* * * * * * * * * * * * * * * *
*
READ WORK 4 RECORD #LOAN-FILE-IN
  FOR #I1 1 TO 6000
    IF ((#LON-CN (#I1) = #CN-IN) AND (#LON-ISS-DT (#I1) = #ISS-DT-IN))
     OR (#LON-CN (#I1) = 0)
      MOVE #CN-IN     TO #LON-CN     (#I1)
      MOVE #ISS-DT-IN TO #LON-ISS-DT (#I1)
      ADD  #LN-AMT-IN TO #LON-AMT    (#I1)
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-WORK
*
FOR #I1 1 TO 100
  DISPLAY #I1 #LON-CN (#I1) #LON-ISS-DT (#I1) #LON-AMT (#I1)
END-FOR
*
END-SUBROUTINE
*
READ RECORDS ST-V BY MEMBER-KEY FROM '001900'
* IF #CNT-2 GE 10
*   ESCAPE BOTTOM
* END-IF
*
* IF #CNT-2 = 2400  /* to make 2400 sections
*   RESET #CNT-2
* END-IF
*
  RESET #VAR-LN
  FOR #I1 1 TO 9    /* <*********************
    IF X-LOAN-AMOUNT-2 (#I1) = 0
      ESCAPE TOP
    END-IF
*
    REPEAT
      IF #SKIP-READ
        RESET #SKIP-READ
        MOVE #PRIOR-LN-AMT TO #LN-AMT-TO-USE
      ELSE
        PERFORM READ-LOAN-IN
      END-IF
*  WRITE 'just read' #PRIOR-CN #PRIOR-ISS-DT #LN-AMT-to-use
*
      IF #PRIOR-CN LT ST-V.ID-NUMBER  /* loan must have paid off
        PERFORM VAR-LOAN-PAID-OFF
*       WRITE 'escape top cn'
        ESCAPE TOP
      ELSE
        IF #PRIOR-CN GT ST-V.ID-NUMBER   /* new loan
          PERFORM NEW-VAR-LN
          RESET #LN-AMT-TO-USE
          MOVE TRUE TO #SKIP-READ
*          WRITE 'new loan by CN' ST-V.ID-NUMBER ISSUE-DATE (#I1)
        ELSE   /* must ne matching cn's
          IF #PRIOR-ISS-DT LT ST-V.ISSUE-DATE (#I1) /*must have paid of
            PERFORM VAR-LOAN-PAID-OFF
*        WRITE 'escape top iss-dt'
            ESCAPE TOP
          ELSE
            IF #PRIOR-ISS-DT GT ST-V.ISSUE-DATE (#I1) /* new var loan
              PERFORM NEW-VAR-LN
              RESET #LN-AMT-TO-USE
              MOVE TRUE TO #SKIP-READ
*             WRITE 'new loan by iss-dt' ST-V.ID-NUMBER ISSUE-DATE (#I1)
*           ELSE    /* MUST BE MATCHING CN'S AND ISS-DT
*              WRITE 'matched var loan' ST-V.ID-NUMBER ISSUE-DATE (#I1)
*                     X-LOAN-AMOUNT-2 (#I1) #LN-AMT-IN #LN-AMT-to-use
             END-IF
          END-IF
        END-IF
      END-IF
      ESCAPE BOTTOM
    END-REPEAT
*
    RESET #INT-DIFF
    MOVE TRUE TO #VAR-LN
    MOVE 20040701 TO #YYYYMMDD
    IF X-LOAN-DATE-2-YYYYMM (#I1) LT 200301
      MOVE 20030101 TO #VAR-LOAN-DT
    ELSE
      MOVE X-LOAN-DATE-2 (#I1) TO #VAR-LOAN-DT
    END-IF
*
    IF #MM LT #VAR-LOAN-DT-MM
      ADD 12 TO #MM
      SUBTRACT 1 FROM #YYYY
    END-IF
*
    SUBTRACT #VAR-LOAN-DT-YYYYMM FROM #YYYYMM
    COMPUTE #MONS = (#YYYY * 12) + #MM
*
    IF (#LN-AMT-TO-USE NE 0) AND (#VAR-LOAN-DT GT 20030101)
      ADD 1 TO #NUM-ADDL-LOANS
    END-IF
*
    IF #VAR-LOAN-DT GE 20040701
      ADD 1 TO #NUM-AFT-7/1/04
      RESET #MONS
    END-IF
*
    COMPUTE ROUNDED #INT-DIFF =
*           X-LOAN-AMOUNT-2 (#I1) * .007 / 12 * #MONS
           X-LOAN-AMOUNT-2 (#I1) * (.054199 / 100) * #MONS
*
*   1.087 (1/12) = 1.00697602   1.080 (1/12) = 1.00643403
*   1.00697602 - 1.00643403 = .00054199
*                .007 / 12 =  .00058333
    RESET #INT-DIFX
    IF #LN-AMT-TO-USE NE 0
      COMPUTE ROUNDED #INT-DIFX =
*       (#ln-amt-to-use + X-LOAN-AMOUNT-2 (#I1) / 2) * .007 / 12 * 18
    ((#LN-AMT-TO-USE + X-LOAN-AMOUNT-2(#I1)) / 2) * (.054199 / 100) * 18
    END-IF
*
    IF #MONS = 18
      ADD 1 TO #NUM-18
    END-IF
*
    ADD X-LOAN-AMOUNT-2    (#I1) TO #TOT-LOAN
    ADD X-ACCUM-INTEREST-2 (#I1) TO #TOT-INT
    ADD #INT-DIFF                TO #TOT-INT-DIFF
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 'CN' ST-V.ID-NUMBER
         'ISS/DT'    ISSUE-DATE (#I1)
         'Loan/date' X-LOAN-DATE-2 (#I1)
         'Loan/Amt'  X-LOAN-AMOUNT-2 (#I1)   (EM=ZZZ,ZZZ.99)
         'Prior/Loan-Amt' #LN-AMT-TO-USE     (EM=ZZZ,ZZZ.99)
         'Mons'      #MONS
         'Int/Diff'  #INT-DIFF             (EM=ZZZ,ZZZ.99)
         'Int/DifX'    #INT-DIFX             (EM=ZZZ,ZZZ.99)
         'Acc/int'   X-ACCUM-INTEREST-2 (#I1) (EM=ZZZ,ZZZ.99)
         '2002-YE/P&I'  STM-P-I-LAST (#I1) (EM=ZZ,ZZZ.99)
         '2003-YE/P&I'  STM-P-I      (#I1) (EM=ZZ,ZZZ.99)
*
    MOVE ST-V.ID-NUMBER TO #CN
    MOVE ISSUE-DATE (#I1) TO #ISS-DT
    MOVE X-LOAN-AMOUNT-2 (#I1) TO #LN-AMT
*   WRITE WORK 2 #LOAN-FILE-OUT
  END-FOR
*
  IF #VAR-LN
    ADD 1 TO #NUM-MBRS
  END-IF
END-READ
*
WRITE (2) 'tot-var prin     ' #TOT-LOAN (EM=ZZZ,ZZZ,ZZZ.99)
WRITE (2) 'tot-var int      ' #TOT-INT  (EM=ZZ,ZZZ,ZZZ.99)
WRITE (2) 'tot-int-diff     ' #TOT-INT-DIFF   (EM=ZZ,ZZZ,ZZZ.99)
WRITE (2) 'Num 18 mons      ' #NUM-18         (EM=Z,ZZ9)
WRITE (2) 'Num Var ln mbrs  ' #NUM-MBRS       (EM=Z,ZZ9)
WRITE (2) 'Num prior loans  ' #FILE-IN        (EM=Z,ZZ9)
WRITE (2) 'LN"S after 7/1/04' #NUM-AFT-7/1/04 (EM=Z,ZZ9)
WRITE (2) 'Addl loans       ' #NUM-ADDL-LOANS (EM=Z,ZZ9)
*
WRITE (3) 'NUM purged       ' #NUM-PURGED  (EM=Z,ZZ9)
WRITE (3) 'NUM DEAD         ' #NUM-DEAD    (EM=Z,ZZ9)
WRITE (3) 'NUM INACTIVE     ' #NUM-INACTIVE(EM=Z,ZZ9)
WRITE (3) 'NUM active       ' #NUM-ACTIVE  (EM=Z,ZZ9)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-LOAN-IN
* * * * * * * * * * * * * * * *
*
IF #PRIOR-CN = 999999
  ESCAPE ROUTINE
END-IF
*
READ WORK 3 ONCE RECORD #LOAN-FILE-IN
AT END
  MOVE 999999 TO #PRIOR-CN
  ESCAPE ROUTINE
END-ENDFILE
*
ADD 1 TO #FILE-IN
MOVE #CN-IN      TO #PRIOR-CN
MOVE #ISS-DT-IN  TO #PRIOR-ISS-DT
MOVE #LN-AMT-IN  TO #PRIOR-LN-AMT
MOVE #LN-AMT-IN  TO #LN-AMT-TO-USE
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE VAR-LOAN-PAID-OFF
* * * * * * * * * * * * * * * *
*
MOVE 'Y' TO #ST-FND
RESET #DEAD #DOD #INACT
FIND (1) ST2-V WITH MEMBER-KEY = #PRIOR-CN-A
  IF NO RECORDS FOUND
    RESET #ST-FND
    ESCAPE BOTTOM
  END-NOREC
*
  IF ST2-V.NUMBER-MEMBERSHIPS = 0
    MOVE 'Y' TO #INACT
  END-IF
*
  IF ST2-V.WIDOW-FLAG = 'Y'
    MOVE 'Y' TO #DEAD
    MOVE ST2-V.DATE-OF-DEATH TO #DOD
  END-IF
END-FIND
*
IF #ST-FND NE 'Y'
  ADD 1 TO #NUM-PURGED
ELSE
  IF #DEAD = 'Y'
    ADD 1 TO #NUM-DEAD
  ELSE
    IF #INACT = 'Y'
      ADD 1 TO #NUM-INACTIVE
    ELSE
      ADD 1 TO #NUM-ACTIVE
    END-IF
  END-IF
END-IF
*
ADD 1 TO #CNT-3
DISPLAY (3) #CNT-3
  'CN' #PRIOR-CN
  'Iss/Date' #PRIOR-ISS-DT
  'Prior/Loan/Amt' #LN-AMT-TO-USE
  'Stat/fnd' #ST-FND
  'Inact'  #INACT
  'DEAD'   #DEAD
  'DOD'    #DOD
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE NEW-VAR-LN
* * * * * * * * * * * * * * * *
*
ADD 1 TO #CNT-4
DISPLAY (4) #CNT-4 ST-V.ID-NUMBER
  ST-V.ISSUE-DATE      (#I1)
  ST-V.X-LOAN-DATE-2   (#I1)
  ST-V.X-LOAN-AMOUNT-2 (#I1)
*
END-SUBROUTINE
*
END
