* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPANNM - List Annual Mtg Attenders on nx & Paper
* FILL IN BETWEEN * ############## LINES ON THIS SCREEN AND MODIFY VIEW
* AND LOGIC AFTER 'SELECT-PROCESS' AND 'DETAIL-PROCESS'
* *** ATHPANNR will reset status File date-book * reset count on NX
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  Reset display fields for GRANKSXX                 *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)
*                    EBCDIC to HEX code '09' ASCII
* DRW  09062006 DW2  Increase size of #recs-read
* YAK  20140514 YK1  Email/Phone contact normalization
* TMT  20140613 TT1  Contact Normalization - DOB and Sex               *
* vxt  11/21/2016 vt1 /Ticket 1924/                                    *
************************************************************************
DEFINE DATA
LOCAL USING G4090LNX
LOCAL
1 #PERSON-FOR    (A3) INIT <'TEH'>     /* INITIALS THIS REPORT IS FOR
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT   (A75)
1 #TITLE (A50) INIT<'ANNUAL MEETING ATTENDERS                   **'>
1 #NUM-UP        (P3) INIT <02>  /* MUST MATCH NUM OCC OF #FULL-LINE-TAB
1 #FULL-LINE     (A78)           /* KEEP AT (A78)
1 REDEFINE #FULL-LINE
  2 #FULL-LINE-TAB  (2)           /* CANNOT EXCEED 78 TOTAL
    3 #L-ID-NUMBER       (N06)     /* FORMAT LINE HERE AND BELOW
    3 #L-FILL-1          (A01)
    3 #L-RANK            (A03)
    3 #L-FILL-2          (A01)
    3 #L-NAME            (A12)
    3 #L-FILL-3          (A01)
    3 #L-MIL-STAT        (A01)
    3 #L-FILL-4          (A01)
    3 #L-MIL-ID          (A03)
    3 #L-FILL-5          (A01)
    3 #L-DOB             (N8)
    3 #L-FILL-6          (A1)
*
1 #NOTE-NUM      (N6)
1 #NUM-NOTES-STORED  (P3)
1 #MAX-NOTES     (P3) INIT <20>  /* MAXIMUM NUMBER OF NOTE RECORDS
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-FLAG    (A1)
  2 MEMBER-CONTACT-ID
*  2 DATE-OF-BIRTH                    /* TT1
  2 INTRO-FLAG
  2 WIDOW-FLAG     (A1)
  2 RANK                (A3)
  2 MILITARY-STATUS     (A1)
  2 MILITARY-SERVICE    (A3)
  2 DATE-BOOK
  2 NAME                (A25)
  2 NUMBER-MEMBERSHIPS
*
1 NT-800107-V VIEW OF A-NOTES
  2 ID-NUMBER
  2 NOTE-DATE (N8/1)
  2 NOTE-TEXT (A60/1)
  2 REDEFINE NOTE-TEXT
    3 ANN-MTG-CNT   (N5)
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* Dw2
  2 #RECS-DISPLAYED   (P5)
  2 #RECS-SELECTED    (P5)
  2 #TOTAL-SELECTED-DISP (A7)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #I1               (I2)
  2 #I5               (I2)
  2 #I2               (I2)
  2 #ISC              (I2)  /* SCREEN LINE INDEX
  2 #IUP              (I2)  /* COLUMN-UP INDEX
  2 #ITB              (I2)  /* TABLE INDEX IF DETAIL STORED
*
  2 #IND              (A1/120)
  2 #ID               (N5/120)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT          (A2)
  2 #YY            (A2)
  2 #MM            (A2)
  2 #DD            (A2)
*
1 #DATN         (N8)
1 REDEFINE #DATN
  2 #DATN-CC     (A2)
  2 #DATN-YY     (A2)
  2 #DATN-MM     (A2)
  2 #DATN-DD     (A2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
  2 #TIMN-HH     (A2)
  2 REDEFINE #TIMN-HH
    3 #TIMN-HH-N  (N2)
  2 #TIMN-MM     (A2)
  2 #TIMN-SS     (A2)
  2 #TIMN-T      (A1)
*
1 #SCR-LINES      (A78/22)
1 #SPLIT-LINE     (A78)
1 REDEFINE #SPLIT-LINE
  2 #FST-60        (A60)
  2 #LST-18        (A18)
1 #UPDATE         (L)
1 #LAST      (A25)
1 #FIRST-MID (A25)
1 #EXTRA-1   (A25)
1 #EXTRA-2   (A25)
1 #EXTRA-3   (A25)
1 #EXTRA-4   (A25)
1 #COMMAND    (A2) INIT <'BA'>
1 #INVALID    (L)
1 #RANK-DISPL (P2)
1 #RANK-LIT   (A23)
1 #RANK-DISPLAY (A5)
1 #NT-NOT-FND   (L)
1 #ARMY     (P3)
1 #AIRF     (P3)
1 #OTHER    (P3)
1 #PENTAGON-EMAIL  (P5)
1 #ANN-MTG-PENT    (P5)
1 #ANN-MTG-PENT-EMAIL (P5)
* YK1
1 #WORK-PHONE  (A20)
1 #HOME-PHONE  (A20)
1 #LOWEST-CN   (N6)
*
1 DATE-OF-BIRTH        (N8.0)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
COMPRESS 'CN'
  #TAB 'DOB'
  #TAB 'RANK'
  #TAB 'NAME'
  #TAB 'STATUS'
  #TAB 'MIL-id'
  INTO #EXTRACT
WRITE WORK FILE 1 #EXTRACT
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X #TITLE
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'Generals attending'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATN
MOVE *TIMN TO #TIMN
IF #TIMN-HH-N GT 12
  SUBTRACT 12 FROM #TIMN-HH-N
END-IF
*
MOVE #TITLE TO #SCR-LINES (1)
MOVE 2  TO #ISC      /* INDEX OF 22 POSSIBLE LINES ON A SCREEN
RESET #IUP
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
READ ST-V BY MEMBER-KEY FROM '001900'
* READ ST-V BY WIDOW-KEY        /* ONLY WIDOWS BY ID-NUM
* READ ST-V PHYSICAL            /* ALL RECORDS
  IF DATE-BOOK NE ' '
    WRITE ST-V.ID-NUMBER 'dt-cd=' DATE-BOOK NUMBER-MEMBERSHIPS
  END-IF
  IF  ( WIDOW-FLAG = 'Y')   /* bypass deceased members
      OR (NUMBER-MEMBERSHIPS = 0)  /* bypass inactive members
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #RECS-READ
* BELOW IS TO SEARCH FOR PENTAGON WORKERS BY PHONE #
* YK1
  #WORK-PHONE := F-FORMATTED-PHONE(<ST-V.MEMBER-CONTACT-ID,'B'>)
  #HOME-PHONE := F-FORMATTED-PHONE(<ST-V.MEMBER-CONTACT-ID,'H'>)
*  MOVE NXT-OF-KIN-TELE-NUMBER TO #PHONE
*
* IF   (#PHONE-AREA-CODE = 703)
*  AND (#PHONE-PREFIX    = 697 OR = 695 OR = 614)
*  AND (NOT INTRO-FLAG = 'S' OR = 'K' OR = 'G')
*  AND (INTERNET-ADDR NE ' ')
*  AND (INTERNET-ADDR NE MASK ('999')) /* indicates do not show on di
*  AND (INTERNET-ADDR NE MASK ('?'))   /* invalid address
*   ADD 1 TO #PENTAGON-EMAIL
* END-IF
*
* ####################################################################
  IF DATE-BOOK =  ' '
    ESCAPE TOP
  END-IF
* ####################################################################
*
* IF   (#PHONE-AREA-CODE = 703)
*  AND (#PHONE-PREFIX    = 697 OR = 695 OR = 614)
*  AND (NOT INTRO-FLAG = 'S' OR = 'K' OR = 'G')
*   ADD 1 TO #ANN-MTG-PENT
*
*   IF (INTERNET-ADDR NE ' ')
*    AND (INTERNET-ADDR NE MASK ('999'))
*    AND (INTERNET-ADDR NE MASK ('?'))   /* invalid address
*     ADD 1 TO #ANN-MTG-PENT-EMAIL
*   END-IF
* END-IF
* * * * * * * GET CURRENT AND FORMER RANK DISPLACEMENT INDEX * * *
*
  RESET #RANK-DISPL #RANK-LIT #RANK-DISPLAY                       /* PM1
  CALLNAT 'GRANKSXX' #COMMAND RANK MILITARY-SERVICE
    #INVALID #RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
  IF #INVALID
    MOVE 99 TO #RANK-DISPL
  END-IF
  RESET DATE-OF-BIRTH
  DATE-OF-BIRTH           := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)        /* TT1
END-ALL
*
* sORT BY ID-NUMBER
* SORT BY #RANK-DISPL RANK NAME
SORT BY DATE-OF-BIRTH
    USING MILITARY-STATUS
    MILITARY-SERVICE
    ID-NUMBER
    #HOME-PHONE                       /* YK1
    #WORK-PHONE                       /* YK1
    RANK
    NAME
  IF (ID-NUMBER LT #LOWEST-CN) OR (#LOWEST-CN = 0)
    MOVE ID-NUMBER TO #LOWEST-CN  /* to find longest member
  END-IF
*
*
* IF RANK = 'GA' OR = 'GEN' OR = 'LTG' OR = 'BG' OR = 'MG'
  PERFORM DETAIL-PROCESS
  ADD 1 TO #RECS-DISPLAYED
* END-IF
*
  ADD 1 TO #RECS-SELECTED
  ADD 1 TO #CNT-3
  DISPLAY (3) #CNT-3 ID-NUMBER RANK NAME
    'MIL-SERV'   MILITARY-SERVICE
    'Home/Phone' #HOME-PHONE                      /* YK1
    'Work/Phone' #WORK-PHONE                      /* YK1
*
  COMPRESS ST-V.ID-NUMBER
    #TAB DATE-OF-BIRTH
    #TAB RANK
    #TAB NAME
    #TAB MILITARY-STATUS
    #TAB MILITARY-SERVICE
    INTO #EXTRACT
  WRITE WORK FILE 1 #EXTRACT
END-SORT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
WRITE (2) #FULL-LINE                  /* PRINT THE LAST LINE
MOVE #FULL-LINE TO #SCR-LINES (#ISC)  /* PROCESS LAST NOTE LINE
*
IF #ISC NE 22
  MOVE EDITED #RECS-DISPLAYED (EM=ZZZ,ZZ9) TO #TOTAL-SELECTED-DISP
  COMPRESS '** END OF LISTING **'
    'ARMY =' #ARMY 'AIRF = '#AIRF 'OTHER =' #OTHER
    'TOTAL =' #TOTAL-SELECTED-DISP INTO #SCR-LINES (22)
END-IF
*
PERFORM BUILD-NOTE-RECORD
PERFORM BUILD-NOTIFY-NOTE-RECORD
*
FIND (1) TRMS-V WITH CLERK-ID = #PERSON-FOR
  MOVE 'Y' TO MSG-JUST-SENT
  UPDATE (0286)
  END TRANSACTION
END-FIND
*
FIND (1) NT-800107-V WITH ID-NUMBER = 800107
  IF NO RECORD FOUND
    MOVE 800107 TO NT-V.ID-NUMBER
    MOVE *DATN TO NOTE-DATE (1)
    RESET NOTE-TEXT (*)
    RESET ANN-MTG-CNT
    MOVE TRUE TO #NT-NOT-FND
  END-NOREC
*
  MOVE #RECS-SELECTED TO ANN-MTG-CNT
*
  IF #NT-NOT-FND
    STORE NT-800107-V
  ELSE
    UPDATE
  END-IF
  END TRANSACTION
END-FIND
*
WRITE (2) 'TOT-RECS-READ        = ' #RECS-READ
WRITE (2) 'TOT-SELECTED         = ' #RECS-SELECTED
WRITE (2) 'TOT-army             = ' #ARMY
WRITE (2) 'TOT-airf             = ' #AIRF
WRITE (2) 'TOT-other            = ' #OTHER
WRITE (2) #PENTAGON-EMAIL 'Works at Pentagon'
WRITE (2) #ANN-MTG-PENT   'Works at Pentagon going to Ann Mtg'
WRITE (2) #ANN-MTG-PENT-EMAIL 'Works at Pent going to Ann Mtg has e-ml'
WRITE (2) #LOWEST-CN          'Longest as member'
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-NOTE-RECORD
* * * * * * * * * * * * * * * * * *
*
MOVE 950000 TO #NOTE-NUM                /* GET NEXT AVAILABLE NX RECORD
* READ (1) NT-V BY ID-NUMBER FROM #NOTE-NUM                     /* vt1 11/21/2016 \/
*   MOVE ID-NUMBER TO #NOTE-NUM
* END-READ
* *
* IF  (#NOTE-NUM LE 950000)
*     OR (#NOTE-NUM GT 980000)
*   MOVE 980000 TO #NOTE-NUM
* END-IF
* *
* SUBTRACT 1 FROM #NOTE-NUM
*
READ NT-V BY ID-NUMBER FROM #NOTE-NUM
  IF #NOTE-NUM < NT-V.ID-NUMBER
    ESCAPE BOTTOM
  END-IF
  IF NT-V.ID-NUMBER GE 980000
    ESCAPE BOTTOM
  END-IF
  ADD 1 TO #NOTE-NUM
END-READ                                                          /* vt1 11/21/2016 /\
*
IF #NUM-NOTES-STORED GT #MAX-NOTES
  ESCAPE ROUTINE
END-IF
*
IF #NUM-NOTES-STORED = #MAX-NOTES
  MOVE
    '* EXCEEDED MAX SCREENS - SEE PRINTED REPORT FOR ADDITIONAL INFO *'
    TO #SCR-LINES (22)
END-IF
ADD 1 TO #NUM-NOTES-STORED
*
RESET NT-V
MOVE #NOTE-NUM     TO NT-V.ID-NUMBER
MOVE #DATN         TO NT-V.DTS-DATN
MOVE #TIMN         TO NT-V.DTS-TIMN
MOVE #DATN         TO NT-V.NOTE-DATE (1)
MOVE 'EDS'         TO NT-V.NOTE-CLERK-ID (1)
MOVE 'LIS'         TO NT-V.NOTE-CLERK-ID (2)
MOVE 'UNV'         TO NT-V.NOTE-CLERK-ID (3)
MOVE #PERSON-FOR   TO NT-V.NOTE-CLERK-ID (4)
*
MOVE 31 TO #I1       /* INDEX OF 18 CHAR NOTE
MOVE 1  TO #I2       /* INDEX OF SEGMENT WITHIN 18 CHAR NOTE
*
FOR #ISC 1 TO 22
  IF #I2 GT 3
    ADD 1 TO #I1
    MOVE 1 TO #I2
  END-IF
*
  MOVE #SCR-LINES (#ISC) TO #SPLIT-LINE
  MOVE #FST-60 TO NT-V.NOTE-TEXT (#ISC)
  MOVE #LST-18 TO NT-V.NOTE-18   (#I1, #I2)
*
  ADD 1 TO #I2
END-FOR
*
STORE NT-V
END TRANSACTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-NOTIFY-NOTE-RECORD
* * * * * * * * * * * * * * * * * * * * * *
*
MOVE 950000 TO #NOTE-NUM                /* GET NEXT AVAILABLE NX RECORD
* READ (1) NT-V BY ID-NUMBER FROM #NOTE-NUM                             /* vt1 11/21/2016 \/
*   MOVE ID-NUMBER TO #NOTE-NUM
* END-READ
* *
* IF  (#NOTE-NUM LE 950000)
*     OR (#NOTE-NUM GT 980000)
*   MOVE 980000 TO #NOTE-NUM
* END-IF
* *
* RESET NT-V
* SUBTRACT 1 FROM #NOTE-NUM
*
READ NT-V BY ID-NUMBER FROM #NOTE-NUM
  IF #NOTE-NUM < NT-V.ID-NUMBER
    ESCAPE BOTTOM
  END-IF
  IF NT-V.ID-NUMBER GE 980000
    ESCAPE BOTTOM
  END-IF
  ADD 1 TO #NOTE-NUM
END-READ
RESET NT-V                                                                 /* vt1 11/21/2016 /\
*
MOVE #NOTE-NUM     TO NT-V.ID-NUMBER
MOVE #DATN         TO NT-V.DTS-DATN
MOVE #TIMN         TO NT-V.DTS-TIMN
MOVE #DATN         TO NT-V.NOTE-DATE (1)
MOVE 'EDS'         TO NT-V.NOTE-CLERK-ID (1)
MOVE #PERSON-FOR   TO NT-V.NOTE-CLERK-ID (2)
MOVE 'UNV'         TO NT-V.NOTE-CLERK-ID (3)
*
COMPRESS 'LIST OF' #TITLE INTO NT-V.NOTE-TEXT (2)
MOVE 'IS AVAILABLE ON NX SCREEN' TO NT-V.NOTE-TEXT (3)
*
STORE NT-V
END TRANSACTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DETAIL-PROCESS
* * * * * * * * * * * * * * * * *
*
ADD 1 TO #IUP
*
IF #IUP GT #NUM-UP
  WRITE (2) #FULL-LINE                  /* PRINT A LINE
  MOVE #FULL-LINE TO #SCR-LINES (#ISC)
  RESET #FULL-LINE
  MOVE 1 TO #IUP
  ADD 1 TO #ISC
*
  IF #ISC GT 22
    PERFORM BUILD-NOTE-RECORD
    RESET #SCR-LINES (2:22)
    MOVE 2 TO #ISC              /* TO LET HEADER REMAIN
    MOVE 1 TO #IUP
  END-IF
END-IF
*
* ####################################################################
MOVE ST-V.ID-NUMBER              TO #L-ID-NUMBER (#IUP)
MOVE ST-V.NAME                   TO #L-NAME       (#IUP)
MOVE ST-V.RANK    TO #L-RANK (#IUP)
MOVE ST-V.MILITARY-SERVICE       TO #L-MIL-ID     (#IUP)
MOVE ST-V.MILITARY-STATUS        TO #L-MIL-STAT   (#IUP)
MOVE DATE-OF-BIRTH               TO #L-DOB        (#IUP)              /* TT1
*
IF ST-V.MILITARY-SERVICE = MASK ('A')
  ADD 1 TO #ARMY
ELSE
  IF ST-V.MILITARY-SERVICE = MASK ('F')
    ADD 1 TO #AIRF
  ELSE
    ADD 1 TO #OTHER
  END-IF
END-IF
* ####################################################################
*
END-SUBROUTINE
*
END
