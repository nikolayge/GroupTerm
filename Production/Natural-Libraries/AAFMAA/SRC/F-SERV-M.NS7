* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************************************
* NAME         : F-SERVICE (F-SERV-M)
* DESCRIPTION  : Get/Update Service Mode
********************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG   REASON
* YAK    11/29/2016        Created for EB&P II
********************************************************************************
DEFINE FUNCTION F-SERVICE RETURNS (L)
  DEFINE DATA
  PARAMETER
  1 #ACTION    (A) DYNAMIC
  1 #CN        (N6)
  1 #PLAN      (A3)
  1 #MODE      (A1)
  1 #STATUS    (A1) OPTIONAL
  1 #USER      (A) DYNAMIC OPTIONAL
  LOCAL
  1 SVC-MEMBER-V VIEW OF A-SVC-MEMBER
    2 LAST-UPD-USER (A8)
    2 LAST-UPD-DATE (N8)
    2 LAST-UPD-TIME (N7)
    2 SVC-STATUS (A1)
    2 SVC-MODE (A1)
    2 SVC-ID-NUMBER (N6)
    2 SVC-PLAN (A3)
  END-DEFINE
*
F1.
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = #CN
  ACCEPT IF SVC-MEMBER-V.SVC-PLAN = #PLAN
  DECIDE FOR FIRST CONDITION
    WHEN #ACTION = 'GET'
      IF (SVC-STATUS = 'A'
          OR (SVC-PLAN = 'SAS' AND SVC-STATUS = 'P' OR= 'F'))
        #MODE := SVC-MEMBER-V.SVC-MODE
        #STATUS := SVC-MEMBER-V.SVC-STATUS
        F-SERVICE := TRUE
      END-IF
    WHEN #ACTION = 'UPDATE'
      SVC-MEMBER-V.SVC-MODE := #MODE
      SVC-MEMBER-V.SVC-STATUS := #STATUS
      LAST-UPD-USER := #USER
      LAST-UPD-DATE := *DATN
      LAST-UPD-TIME := *TIMN
      UPDATE (F1.)
    WHEN NONE
      IGNORE
  END-DECIDE
END-FIND
END-FUNCTION
END
