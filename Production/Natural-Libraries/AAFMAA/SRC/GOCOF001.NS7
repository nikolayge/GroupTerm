* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID DNXGF002   Defaul Phone
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20101225  None    Program written.                             *
*                                                                      *
************************************************************************
*
DEFINE FUNCTION FUNCTION-SPOUSE-CI RETURNS (N8)
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID         (N8)
  LOCAL
  1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
    2 CONTACT-1-ID (N8)
    2 ROLE-1-CD (A1)
    2 CONTACT-2-ID (N8)
    2 ROLE-2-CD (A1)
    2 LAST-USER-UPD (A8)
    2 ACCESS-AUTH-SVC (A1)
    2 ACCESS-AUTH-INS (A1)
    2 LAST-DATE-UPD (N8)
    2 LAST-TIME-UPD (N7)
*
  END-DEFINE
RESET FUNCTION-SPOUSE-CI
*
FIND CNT-REL WITH CONTACT-2-ID = #CONTACT-ID /* LEFT SPOUSE
  IF NO RECORDS
    PERFORM GET-RIGHT-SPOUSE
    ESCAPE BOTTOM
  END-NOREC
  IF ROLE-1-CD = 'S' /* SPOUSE
    FUNCTION-SPOUSE-CI := CONTACT-1-ID
    ESCAPE BOTTOM
  END-IF
END-FIND
IF FUNCTION-SPOUSE-CI = 0
  PERFORM GET-RIGHT-SPOUSE
END-IF
*
DEFINE GET-RIGHT-SPOUSE
/*     ================
FIND CNT-REL WITH CONTACT-1-ID = #CONTACT-ID /* RIGHT SPOUSE
  IF NO RECORDS
    ESCAPE BOTTOM
  END-NOREC
  IF ROLE-1-CD = 'S' /* SPOUSE
    FUNCTION-SPOUSE-CI := CONTACT-2-ID
    ESCAPE BOTTOM
  END-IF
END-FIND
END-SUBROUTINE
*
END-FUNCTION
END
