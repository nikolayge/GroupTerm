* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNADG1.
/**
/** :author nguentchev
/* TODO: Returns address collection for given Contact-ID
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID          (N8)
1 #XML-OUTPUT          (A) DYNAMIC
LOCAL USING AUCAAD01
LOCAL USING AUCAAR01
LOCAL
1 #XML-ADDR            (A) DYNAMIC
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID (N8)
  2 ADDRESS-POOL-ID (N8)
  2 STATUS (A1)
1 #GET           (I4) CONST <0>
1 #NATURAL-ERROR (I4) CONST <-3>
*
END-DEFINE
*
#XML-OUTPUT := ' '
COMPRESS FULL #XML-OUTPUT  '<AddressRelations'
  '>' INTO #XML-OUTPUT LEAVING NO
*
FIND ADDR-REL-V WITH CONTACT-ID = #CONTACT-ID
  ACCEPT IF ADDR-REL-V.STATUS NE 'D'
*
  COMPRESS #XML-OUTPUT '<AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
  RESET AddressRelation
  AddressRelation.AddressPoolID := ADDR-REL-V.ADDRESS-POOL-ID
  AddressRelation.ContactID     := #CONTACT-ID
  AddressRelation.RelationID    := *ISN (0032)
  AddressRelation.ActionType    := #GET
  CALLNAT 'AUCNARM1' AddressRelation
  CALLNAT 'AUCNARS1' #XML-ADDR AddressRelation
  COMPRESS #XML-OUTPUT #XML-ADDR INTO #XML-OUTPUT
*
  RESET Address
  Address.AddressPoolID := ADDR-REL-V.ADDRESS-POOL-ID
  Address.ContactID     := #CONTACT-ID
  Address.ActionType    := #GET
  CALLNAT 'AUCNADM1' Address
  CALLNAT 'AUCNADS1' #XML-ADDR Address
  COMPRESS #XML-OUTPUT #XML-ADDR INTO #XML-OUTPUT
*
  COMPRESS #XML-OUTPUT  '</AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
END-FIND
COMPRESS FULL #XML-OUTPUT  '</AddressRelations'
  '>' INTO #XML-OUTPUT LEAVING NO
*
END
