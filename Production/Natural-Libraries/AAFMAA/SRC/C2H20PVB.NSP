* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* * * * * * * * * * * * * * * * * *
* PROGRAM-ID: C2H20PVB            *
* VB  -  DEATH BENEFIT HISTORY    *
* * * * * * * * * * * * * * * * * *
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  09252005 DW1  Only use 1st 13 occs of Monthly-History
*******************************************
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2H20LVB
LOCAL
*
1 #SCR-MESSAGE    (A35)
1 #SCR-NAME       (A24)
1 #SCR-ID         (A7)
1 #SCR-ISS-DATE1  (A10)
1 #SCR-ISS-DATE2  (A10)
1 #SCR-ISS-DATE3  (A10)
1 #SCR-ISS-DATE4  (A10)
1 #HIST-A         (13)
  2 #YR-MON-Y-A   (A4)
  2 #YR-MON-M-A   (A2)
  2 #DTH-BEN1-A   (A11)
  2 #DTH-BEN2-A   (A11)
  2 #DTH-BEN3-A   (A11)
  2 #DTH-BEN4-A   (A11)
  2 #WDTH-BEN1-A  (N7.2)
  2 #WDTH-BEN2-A  (N7.2)
  2 #WDTH-BEN3-A  (N7.2)
  2 #WDTH-BEN4-A  (N7.2)
  2 #TOT-BEN-A    (A12)
  2 #TOT-LOAN-A   (A9)
  2 #WLOAN-BAL1-A (N5.2)
  2 #WLOAN-BAL2-A (N5.2)
  2 #WLOAN-BAL3-A (N5.2)
  2 #WLOAN-BAL4-A (N5.2)
1 #WNUM-9         (N9.2)
1 #WNUM-7         (N7.2)
1 #WNUM-5         (N5.2)
1 #SCR-DATE-N (N8)
1 REDEFINE #SCR-DATE-N
  2 #SCR-YYYYMM     (N6)
  2 #SCR-DD         (N2)
********************************************************************
*                    WORK AREA
********************************************************************
1 #TOT-BEN        (N8.2)
1 #TOT-LOAN       (N6.2)
1 #SEG-NO         (N1)
1 #ACTIVE-NO      (N1)
1 #HOLD-SCR-ID    (N9)
1 #HOLD-SCR-COMM  (A2)
1 #WYYYYMM        (N6)
1 #W-YEAR-MO      (N6)
1 REDEFINE #W-YEAR-MO
  2 #W-YEAR       (N4)
  2 #W-MO         (N2)
*
1 #LAST-NAME      (A25)
1 #WNUM-3         (N3)
1 REDEFINE #WNUM-3
  2 #W-NUM-1      (A1)
  2 #W-NUM-2      (A2)
*
1 #SCN-NAME       (A25)
1 REDEFINE #SCN-NAME
  2 #SCN-NAME-1   (A1)
  2 #SCN-NAME-24  (A24)
1 #FIRST/MIDDLE   (A25)
1 #SUFFIX         (A25)
1 #TEMP-NAME      (A25)
1 #DEL            (A1)   INIT <','>
*
1 #W-X           (I2)
1 #ST-X          (I2)
1 #H-X           (I2)
1 #A-X           (I2)
1 #N-X           (I2)
1 #I-CNT         (I2)
*
1 #W-MON-IND     (N3)
1 REDEFINE #W-MON-IND
  2 #W-MI        (A1)
  2 #W-MI-A      (A2)
*
1 #WYYYYMMDD     (N8)
1 REDEFINE #WYYYYMMDD
  2 #WYYYY       (A4)
  2 #WMM         (A2)
  2 #WDD         (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N  (N8)
*
1 #WMMDDYYYY     (A10)
*
1 #W-SCR-DATE    (N6)
1 REDEFINE #W-SCR-DATE
  2 #W-SCR-YEAR  (A4)
  2 #W-SCR-MON   (A2)
*
1 #START-KEY        (N15)
1 REDEFINE #START-KEY
  2 #START-ID       (N6)
  2 #START-ISSUE    (N8)
  2 #START-IND      (N1)
*
1 #END-KEY          (N15)
1 REDEFINE #END-KEY
  2 #END-ID         (N6)
  2 #END-ISSUE      (N8)
  2 #END-IND        (N1)
*
1 #REC-KEY
  2 #R-ID-NUM       (N6)
  2 #R-ISS-DATE     (N8)
  2 #R-SPLIT-IND    (N1)
*
1 #HOLD-REC-KEY     (N15)
1 REDEFINE #HOLD-REC-KEY
  2 #HOLD-ID        (N6)
  2 #HOLD-ISS-DATE  (N8)
  2 #HOLD-SPLIT-IND (N1)
*
1 #W-MPREM          (P3.2)
*
1 #REC-COUNTER      (N1)
1 #FOUND-STATUS     (L)
*
END-DEFINE
***********************************************************************
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
GET STATUS-V #CGA-ISN
RESET #CGA-SCR-SUFFIX
MOVE 0           TO #REC-COUNTER
MOVE #CGA-SCR-ID TO #START-ID
MOVE 00000000    TO #START-ISSUE
MOVE 0           TO #START-IND
MOVE #CGA-SCR-ID TO #END-ID
MOVE 99999999    TO #END-ISSUE
MOVE 9           TO #END-IND
*
READ VA-HIST LOGICAL WITH RECORD-KEY FROM #START-KEY THRU #END-KEY
  ADD 1 TO #REC-COUNTER
* IF #REC-COUNTER GT 1
*   RESET             #CGA-SCR-ID
*   MOVE '* '      TO #CGA-SCR-COMMAND
*   INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H20MVB'
*   IF #CGA-SCR-COMMAND NE '* '
*         FETCH 'G1000PXX'
*   END-IF
* END-IF
*
  RESET #HOLD-REC-KEY
  MOVE VA-HIST.RECORD-KEY TO #HOLD-REC-KEY
  PERFORM HEADER-INFO
  MOVE 0 TO #ACTIVE-NO
*
  FOR #ST-X 1 TO 9
    MOVE #ST-X TO #SEG-NO
    IF   (STATUS-V.STATUS(#ST-X)        EQ 'D')
     AND (STATUS-V.ISSUE-DATE(#ST-X)  EQ #HOLD-ISS-DATE)
     AND (STATUS-V.SPLIT-IND(#ST-X) EQ #HOLD-SPLIT-IND)
      ADD 1 TO #ACTIVE-NO
      PERFORM MOVE-STATUS-INFO
      PERFORM CALC-TOTALS
      RESET #TOT-LOAN
      ESCAPE BOTTOM
    ELSE
      ESCAPE TOP
    END-IF
  END-FOR
*
  IF #ST-X GT 9
    MOVE 0 TO #SEG-NO
    PERFORM USE-HISTORY
    PERFORM CALC-TOTALS
  END-IF
END-READ
*
IF #REC-COUNTER EQ 0
  MOVE 'NO HISTORY RECORDS FOUND' TO #SCR-MESSAGE
  PERFORM HEADER-INFO
  INPUT WITH TEXT #SCR-MESSAGE MARK
          *#CGA-SCR-COMMAND USING MAP 'C2H20MVB'
  IF  (#CGA-SCR-COMMAND NE #CGA-ORIG-COMMAND)
   OR (#CGA-SCR-ID NE #CGA-ORIG-ID)
    FETCH 'G1000PXX'
  END-IF
ELSE
  MOVE 'VB'      TO #CGA-SCR-COMMAND
  MOVE ID-NUMBER TO #CGA-SCR-ID
  INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H20MVB'
END-IF
*
FETCH 'G1000PXX'
*
*******************************
DEFINE SUBROUTINE HEADER-INFO
*******************************
*
MOVE *DATN    TO #SCR-DATE-N
MOVE #SCR-YYYYMM TO #W-YEAR-MO
PERFORM ADJUST-DATE
SEPARATE NAME INTO #LAST-NAME #FIRST/MIDDLE #SUFFIX
   WITH DELIMITER #DEL
*
IF #SUFFIX = '    '
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #SCN-NAME
ELSE
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #TEMP-NAME
  COMPRESS #TEMP-NAME ',' #SUFFIX INTO #SCN-NAME LEAVING NO SPACE
END-IF
*
MOVE #SCN-NAME-24 TO #SCR-NAME
COMPRESS 'CN' #START-ID INTO #SCR-ID LEAVING NO SPACE
*
END-SUBROUTINE
*
***********************************
DEFINE SUBROUTINE MOVE-STATUS-INFO
***********************************
*
MOVE STATUS-V.ISSUE-DATE(#ST-X)       TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY     INTO #WMMDDYYYY LEAVING NO SPACE
IF #SEG-NO = 1
  MOVE #WMMDDYYYY TO #SCR-ISS-DATE1
END-IF
*
IF #SEG-NO = 2
  MOVE #WMMDDYYYY TO #SCR-ISS-DATE2
END-IF
*
IF #SEG-NO = 3
  MOVE #WMMDDYYYY TO #SCR-ISS-DATE3
END-IF
*
IF #SEG-NO = 4
  MOVE #WMMDDYYYY TO #SCR-ISS-DATE4
END-IF
*
RESET #WYYYYMMDD
PERFORM HISTORY-LOOP
*
END-SUBROUTINE
*
*******************************
DEFINE SUBROUTINE USE-HISTORY
*******************************
*
ADD 1 TO #SEG-NO
MOVE #HOLD-ISS-DATE TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY INTO #WMMDDYYYY LEAVING NO SPACE
MOVE #WMMDDYYYY TO #SCR-ISS-DATE1
RESET #WMMDDYYYY
PERFORM HISTORY-LOOP
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE HISTORY-LOOP
******************************
*
MOVE 12  TO #I-CNT
MOVE 1   TO #H-X
MOVE 0   TO #A-X
MOVE 1   TO #N-X
*
FOR #H-X 1 TO 13
    MOVE *DATN    TO #SCR-DATE-N
    MOVE #SCR-YYYYMM TO #W-YEAR-MO
*
    IF #A-X = 14
      ESCAPE BOTTOM
    END-IF
*
    MOVE VA-HIST.C*MONTHLY-HIST TO #N-X
*
    IF #N-X GT 13              /* DW1
      MOVE 13 TO #N-X
    END-IF
*
    FOR #A-X 1 TO 13
     PERFORM ADJUST-DATE
     COMPUTE #H-X = #H-X + #I-CNT
     RESET #WYYYYMM
     IF VA-HIST.YEAR-MONTH-M(#N-X) = #W-YEAR-MO
      MOVE VA-HIST.YEAR-MONTH-M(#N-X)       TO #W-SCR-DATE
      MOVE #W-SCR-YEAR                      TO #YR-MON-Y-A(#A-X)
      MOVE #W-SCR-MON                       TO #YR-MON-M-A(#A-X)
      RESET #W-SCR-DATE
*
      IF #SEG-NO = 1
        MOVE VA-HIST.DEATH-BENEFIT-M(#N-X) TO #WNUM-7
        MOVE EDITED #WNUM-7 (EM=ZZZZ,ZZZ.99) TO #DTH-BEN1-A(#A-X)
        MOVE #WNUM-7 TO #WDTH-BEN1-A(#A-X)
        RESET #WNUM-7
        MOVE VA-HIST.PRORAT-LOAN-BAL-M(#N-X) TO #WNUM-5
        MOVE #WNUM-5 TO #WLOAN-BAL1-A(#A-X)
        RESET #WNUM-5
      END-IF
*
      IF #SEG-NO = 2
        MOVE VA-HIST.DEATH-BENEFIT-M(#N-X) TO #WNUM-7
        MOVE EDITED #WNUM-7 (EM=ZZZZ,ZZZ.99) TO #DTH-BEN2-A(#A-X)
        MOVE #WNUM-7 TO #WDTH-BEN2-A(#A-X)
        RESET #WNUM-7
        MOVE VA-HIST.PRORAT-LOAN-BAL-M(#N-X) TO #WNUM-5
        MOVE #WNUM-5 TO #WLOAN-BAL2-A(#A-X)
        RESET #WNUM-5
      END-IF
*
      IF #SEG-NO = 3
        MOVE VA-HIST.DEATH-BENEFIT-M(#N-X) TO #WNUM-7
        MOVE EDITED #WNUM-7 (EM=ZZZZ,ZZZ.99) TO #DTH-BEN3-A(#A-X)
        MOVE #WNUM-7 TO #WDTH-BEN3-A(#A-X)
        RESET #WNUM-7
        MOVE VA-HIST.PRORAT-LOAN-BAL-M(#N-X) TO #WNUM-5
        MOVE #WNUM-5 TO #WLOAN-BAL3-A(#A-X)
        RESET #WNUM-5
      END-IF
*
      IF #SEG-NO = 4
        MOVE VA-HIST.DEATH-BENEFIT-M(#N-X) TO #WNUM-7
        MOVE EDITED #WNUM-7 (EM=ZZZZ,ZZZ.99) TO #DTH-BEN4-A(#A-X)
        MOVE #WNUM-7 TO #WDTH-BEN4-A(#A-X)
        RESET #WNUM-7
        MOVE VA-HIST.PRORAT-LOAN-BAL-M(#N-X) TO #WNUM-5
        MOVE #WNUM-5 TO #WLOAN-BAL4-A(#A-X)
        RESET #WNUM-5
      END-IF
*
      IF #ACTIVE-NO GE 5
        MOVE 'MORE THAN 4 POLICIES' TO #SCR-MESSAGE
        ESCAPE BOTTOM
      END-IF
*
      COMPUTE #TOT-LOAN = #TOT-LOAN + VA-HIST.PRORAT-LOAN-BAL-M(#H-X)
      MOVE EDITED #TOT-LOAN (EM=ZZ,ZZZ.99) TO #TOT-LOAN-A(#A-X)
      COMPUTE #N-X = #N-X - 1
*     PERFORM ADJUST-DATE
      PERFORM BUMP-HIST-SEGMENT
     ELSE
      MOVE #W-YEAR-MO                       TO #W-SCR-DATE
      MOVE #W-SCR-YEAR                      TO #YR-MON-Y-A(#A-X)
      MOVE #W-SCR-MON                       TO #YR-MON-M-A(#A-X)
      RESET #W-SCR-DATE
      RESET #DTH-BEN1-A(#A-X)
      RESET #DTH-BEN2-A(#A-X)
      RESET #DTH-BEN3-A(#A-X)
      RESET #DTH-BEN4-A(#A-X)
      MOVE '         .00' TO #TOT-BEN-A(#A-X)
      MOVE '      .00'    TO #TOT-LOAN-A(#A-X)
      COMPUTE #H-X = #H-X - #I-CNT
      COMPUTE #I-CNT = #I-CNT - 2
      COMPUTE #H-X = #H-X + 1
*     ADD 1 TO #A-X
*     PERFORM ADJUST-DATE
     END-IF
    END-FOR
  END-FOR
*
END-SUBROUTINE
*
*******************
DEFINE ADJUST-DATE
*******************
*
COMPUTE #W-YEAR-MO = #W-YEAR-MO - 1
IF #W-MO = 0
  MOVE 12 TO #W-MO
  COMPUTE #W-YEAR = #W-YEAR - 1
END-IF
END-SUBROUTINE
*
*********************
DEFINE CALC-TOTALS
*********************
*
MOVE 1 TO #A-X
FOR #A-X 1 TO 13
  COMPUTE #TOT-BEN = (#WDTH-BEN1-A(#A-X) + #WDTH-BEN2-A(#A-X)) +
                     (#WDTH-BEN3-A(#A-X) + #WDTH-BEN4-A(#A-X))
  MOVE EDITED #TOT-BEN (EM=ZZZZZ,ZZZ.99) TO #TOT-BEN-A(#A-X)
  RESET #TOT-BEN
  COMPUTE #TOT-LOAN = (#WLOAN-BAL1-A(#A-X) + #WLOAN-BAL2-A(#A-X)) +
                      (#WLOAN-BAL3-A(#A-X) + #WLOAN-BAL4-A(#A-X))
  MOVE EDITED #TOT-LOAN (EM=ZZ,ZZZ.99) TO #TOT-LOAN-A(#A-X)
  RESET #TOT-LOAN
END-FOR
*
END-SUBROUTINE
****************************
DEFINE BUMP-HIST-SEGMENT
****************************
*
COMPUTE #H-X = #H-X - #I-CNT
COMPUTE #I-CNT = #I-CNT - 2
COMPUTE #H-X = #H-X + 1
*
END-SUBROUTINE
*
END
