* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: DNXUN006
* DESCRIPTION:   Notified by data
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NGG  12/09/2010  None Creation
* RSE  07/13/2011  RE1  Change 'CB' to 'DN'; reset #MSG
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #ISN              (P8)   /* STATUS RECORD ISN
1 #CLERK-ID         (A3)
1 #MODE             (A1)
1 #MSG                       (A72) 1 redefine #MSG
  2 #GLOBAL-USER-COMPANY     (A1)
  2 #GLOBAL-SECURITY-CODE    (N2)
LOCAL
1 #CGA-USER-COMPANY  (A1)
1 #ORIG-COMMAND      (A2)  INIT <'DN'>     /* RE1

1 NOTIFIED VIEW OF A-CONTACTS
1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER                     (N6)
  2 CONTACT-ID                    (N8)
  2 DEATH-NOTIFIED-BY-CONTACT-ID  (N8)
  2 DATE-OF-DEATH-REPORTED        (N8)
1 #DEATH-NOTIFIED-BY-CONTACT-ID   (N8)
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
1 STATUS VIEW OF A-STATUS
  2 DATE-DEATH-NOTIFY
1 P_PARMS
  2 P_ERROR_NUM (I4)  /* ERROR NUMBER
  2 P_ERROR_LINE (I4) /* ERROR LINE
  2 P_ERROR_PROGRAM (A32)
  2 P_ERROR_LIB (A32)
  2 P_ERROR_DBID (I4)
  2 P_ERROR_FNR (I4)
  2 P_ERROR_TIME (T)
  2 P_ERROR_PGM_TYPE (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG (I4)
  2 P_ERROR_LEVEL (I4)
  2 P_ERROR_CLASS (I1)
  2 P_ERROR_TYPE (I1)

END-DEFINE

DEFINE WINDOW Appl-error
  SIZE 5 * 77
  BASE 11/3
  FRAMED ON POSITION SYMBOL AUTO

SET KEY PF3 NAMED 'Exit'
SET KEY PF7 NAMED 'Prev'
SET KEY PF8 NAMED 'Next'

#CGA-USER-COMPANY := #GLOBAL-USER-COMPANY
RESET #MSG
  #DEATH-NOTIFIED-BY-CONTACT-ID
GET CONTACT #ISN

IF DEATH-NOTIFIED-BY-CONTACT-ID = 0
  CALLNAT 'DNXUN09A' CONTACT-ID #DEATH-NOTIFIED-BY-CONTACT-ID
    #GLOBAL-USER-COMPANY
    #CLERK-ID
    #MSG
  IF #DEATH-NOTIFIED-BY-CONTACT-ID > 0
    GET CONTACT #ISN
    DEATH-NOTIFIED-BY-CONTACT-ID := #DEATH-NOTIFIED-BY-CONTACT-ID
    UPDATE (0080)
    FIND STATUS WITH ID-NUMBER = CONTACT.ID-NUMBER
      IF DATE-DEATH-NOTIFY = 0
        DATE-DEATH-NOTIFY := *DATN 
      END-IF
      UPDATE (0083)
    END-FIND
    END TRANSACTION
    IF #CLERK-ID = 'NG'
      PRINT CONTACT
        /  #ORIG-COMMAND
        /  ID-NUMBER
        /  #CGA-USER-COMPANY
        /  #CLERK-ID
        /  #MODE
    END-IF
  END-IF
ELSE
  #DEATH-NOTIFIED-BY-CONTACT-ID := DEATH-NOTIFIED-BY-CONTACT-ID
  FIND CONTACT-REL WITH CONTACT-1-ID = CONTACT.CONTACT-ID
      AND CONTACT-2-ID = #DEATH-NOTIFIED-BY-CONTACT-ID
    IF CONTACT-REL.ROLE-2-CD = 'W'
        AND CONTACT-REL.ROLE-1-CD = 'M'
      CALLNAT 'DNXUN07A'
        CONTACT.CONTACT-ID
        #DEATH-NOTIFIED-BY-CONTACT-ID
        #CGA-USER-COMPANY
        #CLERK-ID
        #MSG
      ESCAPE ROUTINE
    END-IF
  END-FIND
  #MODE := 'U'
  IF #CLERK-ID = 'NG'
    PRINT
      /  CONTACT-ID
      /  DEATH-NOTIFIED-BY-CONTACT-ID
      /  #ORIG-COMMAND
      /  ID-NUMBER
      /  #CGA-USER-COMPANY
      /  #CLERK-ID
      /  #MODE
  END-IF
  CALLNAT 'GUPCISXX'
    CONTACT-ID                 /* Members CONTACT-ID
    DEATH-NOTIFIED-BY-CONTACT-ID        /* Other CONTACT-ID
    #ORIG-COMMAND
    ID-NUMBER                  /* CN or ID-NUMBER
    #CGA-USER-COMPANY
    #CLERK-ID
    #MODE
    #MSG
  RESET #MSG         /* RE1
  FIND NOTIFIED WITH CONTACT-ID = DEATH-NOTIFIED-BY-CONTACT-ID
    IF NO RECORDS FOUND
      RESET CONTACT.DEATH-NOTIFIED-BY-CONTACT-ID
      UPDATE (0072)
    END-NOREC
  END-FIND
END-IF

ON ERROR
  SET WINDOW 'Appl-error'
  CALLNAT 'USR2001N' P_PARMS  /* Get the actual error line details
  COMPRESS 'Application error' P_ERROR_NUM 'In program' P_ERROR_PROGRAM
    'Error line' P_ERROR_LINE
    INTO #MSG
  INPUT WINDOW='Appl-error'
    #MSG (AD=IO)
    / 'Please call production support'
  #MSG := 'PF3'
  SET WINDOW OFF
  ESCAPE ROUTINE
END-ERROR

END
