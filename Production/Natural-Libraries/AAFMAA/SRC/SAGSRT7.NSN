* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID  SAGSRT7
************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11242005  RH1   SS - Created for implementing sorting algorithm *
************************************************************************
DEFINE DATA PARAMETER
**
1 #SRT-TABLE           (500)
  2 #SRT-NAME           (A25)
  2 #SRT-ID-NUMBER      (N9)
**
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES   (I2)
  2 #SRT-ENTRY-LENGTH  (I2)
  2 #SRT-KEY-LENGTH    (I2)
  2 #SRT-KEY-POSITION  (I2)
  2 #SRT-SORT-TYPE     (A1)
**
LOCAL
**
1 #SRT-WORK            (500)
  2 #SRT-NAME           (A25)
  2 REDEFINE #SRT-NAME
    3 #SRT-ST-DATE       (N8)
    3 #SRT-ST-STATUS     (A2)
    3 #SRT-ST-USER       (A3)
    3 #SRT-AT-NAME       (A12)
  2 #SRT-ID-NUMBER      (N9)
  2 REDEFINE #SRT-ID-NUMBER
    3 #SRT-FILL-1        (A3)
    3 #SRT-ID-NUMBER-A   (A6)
  2 REDEFINE #SRT-ID-NUMBER
    3 #SRT-FILL-2        (A3)
    3 #SRT-ID-NUMBER-6   (N6)
1 REDEFINE #SRT-WORK
  2 #SRT-ROW           (A34/1:500)
1 REDEFINE #SRT-WORK
  2 #SRT-REC           (500)
    3 #SRT-KEY         (A34)
**
1 #I                   (P7)
1 #ROWS                (P7)
1 #DONE                (L)  INIT <FALSE>
1 #TEMP-ROW            (A34)
**
END-DEFINE
**
**  first eliminate any empty rows
FOR #I EQ 1 TO 500
  IF      #SRT-TABLE.#SRT-NAME (#I)      EQ ' '
      AND #SRT-TABLE.#SRT-ID-NUMBER (#I) EQ 0
    ESCAPE TOP
  ELSE
    ADD 1 TO #ROWS
    MOVE BY NAME #SRT-TABLE (#I) TO #SRT-WORK (#ROWS)
  END-IF
END-FOR
IF #ROWS EQ 0 ESCAPE ROUTINE END-IF
**

DECIDE ON FIRST VALUE OF #SRT-SORT-TYPE
**
  VALUE 'A'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
        IF #SRT-KEY (#I) LT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  VALUE 'D'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
        IF #SRT-KEY (#I) GT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  NONE VALUE
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT RECOGNIZED - EXECUTION TERMINATED'
    STOP
**
END-DECIDE
**
RESET #SRT-TABLE (*)
FOR #I EQ 1 TO #ROWS
  MOVE BY NAME #SRT-WORK (#I) TO #SRT-TABLE (#I)
END-FOR
#SRT-NUM-ENTRIES := #ROWS  /* SUBJECT TO CONFIRMATION
**
END
