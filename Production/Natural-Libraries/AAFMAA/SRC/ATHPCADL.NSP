* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPCADL    ** DELETE ALL CA LPO INFO    **
* THIS IS RUN monthly
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* RSE  09/21/2009 RE1  Change 600K logic for Unification
************************************************************************
*
DEFINE DATA
LOCAL  USING GONERLXX
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER     (N6)
 2 X-LPO-PMT-RCVD         (9)
 2 X-LPO-PMT-RCVD-2       (9)
 2 X-LPO-PRIOR-LOAN-BAL   (9)
 2 X-LPO-PRIOR-LOAN-BAL-2 (9)
 2 X-LPO-PRIOR-ACC-INT    (9)
 2 X-LPO-PRIOR-ACC-INT-2  (9)
 2 X-LOAN-DATE            (9)
 2 X-LOAN-DATE-2          (9)
 2 ISSUE-DATE           (9)
*
1 #I1  (I2)
1 #I2  (I2)
1 #CNT-2  (P5)
1 #UPDATE (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                  'LPO Info deleted'
                   5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ  ST-V WITH MEMBER-KEY = '001900'
*  IF ID-NUMBER GE 600000                      /* RE1
*    END TRANSACTION
*    ESCAPE BOTTOM
*  END-IF
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)        /* RE1   
    ESCAPE TOP
  END-IF
*
  RESET #UPDATE
  FOR #I1 1 TO 9
    IF  (X-LPO-PMT-RCVD         (#I1) NE 0)
     OR (X-LPO-PMT-RCVD-2       (#I1) NE 0)
     OR (X-LPO-PRIOR-LOAN-BAL   (#I1) NE 0)
     OR (X-LPO-PRIOR-LOAN-BAL-2 (#I1) NE 0)
     OR (X-LPO-PRIOR-ACC-INT    (#I1) NE 0)
     OR (X-LPO-PRIOR-ACC-INT-2  (#I1) NE 0)
      MOVE TRUE TO #UPDATE
      ADD 1 TO #CNT-2
      DISPLAY (2) #CNT-2 ST-V.ID-NUMBER ISSUE-DATE(#I1)
          'Fix/loan/date' X-LOAN-DATE            (#I1)
          'Fix/pmt/rcvd'  X-LPO-PMT-RCVD         (#I1)
          'Fix/prior/bal' X-LPO-PRIOR-LOAN-BAL   (#I1)
          'Fix/prior/int' X-LPO-PRIOR-ACC-INT    (#I1)
          'Var/loan-date' X-LOAN-DATE-2          (#I1)
          'Var/pmt/rcvd'  X-LPO-PMT-RCVD-2       (#I1)
          'Var/prior/bal' X-LPO-PRIOR-LOAN-BAL-2 (#I1)
          'Var/prior/int' X-LPO-PRIOR-ACC-INT-2  (#I1)
      RESET
         X-LPO-PMT-RCVD         (#I1)
         X-LPO-PMT-RCVD-2       (#I1)
         X-LPO-PRIOR-LOAN-BAL   (#I1)
         X-LPO-PRIOR-LOAN-BAL-2 (#I1)
         X-LPO-PRIOR-ACC-INT    (#I1)
         X-LPO-PRIOR-ACC-INT-2  (#I1)
    END-IF
  END-FOR
*
  IF #UPDATE
    UPDATE (0043)
  END-IF
*
  END TRANSACTION
END-READ
*
END
