* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPSTST - Statistics by State       *****************
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* RSE  09302009 RE1  Change PROCESS-IND/600K logic for Unification     *
* NGG  20130730 NG1  Address normalization
* TMT  06102014 TT1  CONTACT NORMALIZATION - DOB AND SEX
************************************************************************
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 PROCESS-IND
  2 INTRO-FLAG
  2 DUES-BALANCE
  2 PAD-CODE
  2 TRAD-UP-SENT-DT
  2 STOP-DATE
  2 WIDOW-FLAG
*   2 DATE-OF-BIRTH                         /* TT1
  2 NUMBER-MEMBERSHIPS
  2 MEMBER-CONTACT-ID        /* NG1
*  2 ADDRESS-1
*  2 ADDRESS-2
*  2 ADDRESS-3
  2 X-BILL-DOLLAR(9)
  2 ISSUE-DATE (9)
  2 X-RID-BALANCE (9)
  2 CONV-DURATION (9)
  2 X-FIRST-PLACE-FEE (9)
  2 X-BILL-ADJUST (9)
  2 RID-PD-TO (9)
  2 PAY-STATUS (9)
  2 FACE-AMOUNT (9)
  2 RID-PLAN (9)
  2 PAID-UP-DATE (9)
  2 REDEFINE PAID-UP-DATE
    3 PAID-UP-OCC (9)
      4 PAID-UP-YYYY (N4)
      4 PAID-UP-MM   (N2)
      4 PAID-UP-DD   (N2)
  2 PAID-TO-DATE (9)
  2 REDEFINE PAID-TO-DATE
    3 PAID-TO-OCC (9)
      4 PAID-TO-YYYY (N4)
      4 PAID-TO-MM   (N2)
      4 PAID-TO-DD   (N2)
  2 BILL-TO-DATE (9)
  2 MTD-ACV-PREM-ADJ (P7.2/9)
  2 PLAN (9)
  2 REDEFINE PLAN
    3 PLAN-OCC (9)
      4 PLAN-A1 (A1)
      4 PLAN-A2 (A1)
  2 STATUS (9)
  2 ANNUAL-PREM (9)
*  2 MONTH-ALLOC-AMT (9)
  2 ALOT-AMT
  2 MODE (9)
  2 COST-BASIS (9)
  2 CASH-VALUE (9)
  2 STM-PREM (P6.2/9)
  2 TOT-PREMIUMS-PAID (9)
  2 DEBIT-CREDIT-AMT (P7.2)
  2 OWNERSHIP-FLAG (9)
*
*
1 #I1            (I2)
1 #I2            (I2)
1 #I3            (I2)
1 #I4            (I2)
1 #I5            (I2)
1 #CNT-1         (P5)
*
1 #DATN         (N8)
1 REDEFINE #DATN
  2 #DATN-CC     (N2)
  2 #DATN-YY     (N2)
  2 #DATN-MM     (N2)
  2 #DATN-DD     (N2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
  2 #TIMN-HH     (A2)
  2 REDEFINE #TIMN-HH
    3 #TIMN-HH-N  (N2)
  2 #TIMN-MM     (A2)
  2 #TIMN-SS     (A2)
  2 #TIMN-T      (A1)
1 #UPDATE  (L)
1 #HOLD-ID    (N6)
1 #CNT-2      (P3)
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-RECORD (A125)
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
* #PP-MONTH-ALLOC-AMT (N7.2)       /*TT1
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID         (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
* ABOVE for CALC-PREMS-PAID
*
* 1 #SPLIT       (A1)
* 1 #STREET      (A25)
* 1 #STREET-2    (A25)
* 1 #CITY        (A22)
1 #STATE       (A2)
* 1 #ERR-MSG     (A60)
*
1 #STATE-TABLE
  2 #ALABAMA              (A21) INIT<'AL Alabama           '>
  2 #ALASKA               (A21) INIT<'AK Alaska            '>
  2 #ARIZONA              (A21) INIT<'AZ Arizona           '>
  2 #ARKANSAS             (A21) INIT<'AR Arkansas          '>
  2 #CALIFORNIA           (A21) INIT<'CA California        '>
  2 #COLORADO             (A21) INIT<'CO Colorado          '>
  2 #CONNECTICUT          (A21) INIT<'CT Connecticut       '>
  2 #DELAWARE             (A21) INIT<'DE Delaware          '>
  2 #DISTRICT-OF-COLUMBIA (A21) INIT<'DC Dist of Columbia  '>
  2 #FLORIDA              (A21) INIT<'FL Florida           '>
  2 #GEORGIA              (A21) INIT<'GA Georgia           '>
  2 #HAWAII               (A21) INIT<'HI Hawaii            '>
  2 #IDAHO                (A21) INIT<'ID Idaho             '>
  2 #ILLINOIS             (A21) INIT<'IL Illinois          '>
  2 #INDIANA              (A21) INIT<'IN Indiana           '>
  2 #IOWA                 (A21) INIT<'IA Iowa              '>
  2 #KANSAS               (A21) INIT<'KS Kansas            '>
  2 #KENTUCKY             (A21) INIT<'KY Kentucky          '>
  2 #LOUISIANA            (A21) INIT<'LA Louisiana         '>
  2 #MAINE                (A21) INIT<'ME Maine             '>
  2 #MARYLAND             (A21) INIT<'MD Maryland          '>
  2 #MASSACHUSETTS        (A21) INIT<'MA Massachusetts     '>
  2 #MICHIGAN             (A21) INIT<'MI Michigan          '>
  2 #MINNESOTA            (A21) INIT<'MN Minnesota         '>
  2 #MISSISSIPPI          (A21) INIT<'MS Mississippi       '>
  2 #MISSOURI             (A21) INIT<'MO Missouri          '>
  2 #MONTANA              (A21) INIT<'MT Montana           '>
  2 #NEBRASKA             (A21) INIT<'NE Nebraska          '>
  2 #NEVADA               (A21) INIT<'NV Nevada            '>
  2 #NEW-HAMPSHIRE        (A21) INIT<'NH New Hampshire     '>
  2 #NEW-JERSEY           (A21) INIT<'NJ New Jersey        '>
  2 #NEW-MEXICO           (A21) INIT<'NM New Mexico        '>
  2 #NEW-YORK             (A21) INIT<'NY New York          '>
  2 #NORTH-CAROLINA       (A21) INIT<'NC North Carolina    '>
  2 #NORTH-DAKOTA         (A21) INIT<'ND North Dakota      '>
  2 #OHIO                 (A21) INIT<'OH Ohio              '>
  2 #OKLAHOMA             (A21) INIT<'OK Oklahoma          '>
  2 #OREGON               (A21) INIT<'OR Oregon            '>
  2 #PENNSYLVANIA         (A21) INIT<'PA Pennsylvania      '>
  2 #RHODE-ISLAND         (A21) INIT<'RI Rhode Island      '>
  2 #SOUTH-CAROLINA       (A21) INIT<'SC South Carolina    '>
  2 #SOUTH-DAKOTA         (A21) INIT<'SD South Dakota      '>
  2 #TENNESSEE            (A21) INIT<'TN Tennessee         '>
  2 #TEXAS                (A21) INIT<'TX Texas             '>
  2 #UTAH                 (A21) INIT<'UT Utah              '>
  2 #VERMONT              (A21) INIT<'VT Vermont           '>
  2 #VIRGINIA             (A21) INIT<'VA Virginia          '>
  2 #WASHINGTON           (A21) INIT<'WA Washington        '>
  2 #WEST-VIRGINIA        (A21) INIT<'WV West Virginia     '>
  2 #WISCONSIN            (A21) INIT<'WI Wisconsin         '>
  2 #WYOMING              (A21) INIT<'WY Wyoming           '>
  2 #APO-AP               (A21) INIT<'AP APO Pacifics      '>
  2 #APO-AE               (A21) INIT<'AE APO Europe        '>
  2 #APO-AA               (A21) INIT<'AA APO Americas      '>
  2 #OTHER                (A21) INIT<'XX Foreign           '>
*
1 REDEFINE #STATE-TABLE
  2 #TABLE-OCC                      (55)
    3 #STATE-FR-TABLE                 (A2)
    3 #SPACE-1                        (A1)
    3 #STATE-NAME                     (A18)
*
1 #TOTALS-TABLE
  2 #TTL-TABLES-OCC                 (55)
    3 #TOTAL-MEMBERS                 (N6)    /* ZZZ,ZZ9
    3 #TOTAL-POLICIES                (N6)    /* ZZZ,ZZ9
    3 #TOTAL-FACE-AMOUNT             (N12.2) /* ZZZ,ZZZ,ZZZ,ZZ9.99
    3 #TOTAL-DEATH-BENEFIT           (N12.2) /* ZZZ,ZZZ,ZZZ,ZZ9.99
*
1 #TOTAL-MEMS                      (N6)
1 #TOTAL-POLS                      (N6)
1 #TOTAL-DB                        (N12.2)
1 #TOTAL-FACE                      (N12.2)
*
1 #DISPLAY-MEMBERS        (A7)
1 #DISPLAY-POLICIES       (A7)
1 #DISPLAY-FACE-AMOUNT    (A18)
1 #DISPLAY-DEATH-BENEFIT  (A18)
*
**** Below for CVALUSXX ******
1 #SUFFIX     (A3)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREM   (N7.2/9)
1 #FACE-AMT    (N7.2/9)
1 #TPP         (N7.2/9)
***** Above for CVALUSXX ******
*
END-DEFINE
* * * * * * * * * *
*
MOVE *DATN TO #DATN
*
FORMAT  (1) LS=133 PS=76
WRITE   (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Members/Policies by State'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS 'St' #TAB
  'St Name' #TAB
  'Members' #TAB
  'Policies' #TAB
  'Face Amount' #TAB
  'Death Benefit' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ ST-V BY MEMBER-KEY FROM '001900'
*
*  IF ID-NUMBER GE 600000                      RE1 START
*    ESCAPE BOTTOM
*  END-IF
*
*  IF PROCESS-IND = 'S' OR = 'C'
  IF PROCESS-IND = 'C'
      OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1 END
    ESCAPE TOP
  END-IF
*
  IF WIDOW-FLAG = 'Y'
      OR NUMBER-MEMBERSHIPS = 0
    ESCAPE TOP
  END-IF
*
*   MOVE 'S' TO #SPLIT
*   RESET #STREET #STREET-2 #CITY #STATE
*   CALLNAT 'GCTSTSXX'  /* SPLIT CITY/STATE / DO NOT REMOVE IN CASE CITY
*    #SPLIT
*    ST-V.ADDRESS-1     /* AND STATE ARE NEEDED FOR EXTRACT FILE
*    ST-V.ADDRESS-2
*    ST-V.ADDRESS-3
*    #STREET
*    #STREET-2
*    #CITY
*    #STATE
*    #ERR-MSG
*
  #STATE := GET-STATE-CODE (< MEMBER-CONTACT-ID >)
  FOR #I1 1 TO 55
    IF #STATE = #STATE-FR-TABLE (#I1)
      ADD +1 TO #TOTAL-MEMBERS (#I1)
      ESCAPE BOTTOM
    END-IF
*
    IF #I1 = 55
      ADD +1 TO #TOTAL-MEMBERS (#I1)
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  MOVE FACE-AMOUNT (*) TO #FACE-AMT (*)
  CALLNAT 'CVALUSXX'
    ST-V.ID-NUMBER
    #SUFFIX
    #DATN
    #LOAN-BAL     (1:9)
    #LOAN-BAL-2   (1:9)
    #ACC-INT      (1:9)
    #ACC-INT-2    (1:9)
    #CSV          (1:9)
    #CVALUE       (1:9)
    #LVALUE       (1:9)
    #MCSV         (1:9)
    #DB           (1:9)
    #ANN-PREM     (1:9)
    #FACE-AMT   (1:9)
    #TPP        (1:9)
*
  FOR #I2 1 TO 9
    IF ISSUE-DATE (#I2) = 0
      ESCAPE BOTTOM
    END-IF
*
    IF NOT STATUS (#I2) = 'D'
      ESCAPE TOP
    END-IF
*
    ADD +1 TO #TOTAL-POLICIES (#I1)
    ADD #DB (#I2) TO #TOTAL-DEATH-BENEFIT (#I1)
    ADD ST-V.FACE-AMOUNT (#I2) TO #TOTAL-FACE-AMOUNT (#I1)
  END-FOR
*
END-READ
*
FOR #I1 1 TO 55
*
  ADD #TOTAL-MEMBERS (#I1)       TO #TOTAL-MEMS
  ADD #TOTAL-POLICIES (#I1)      TO #TOTAL-POLS
  ADD #TOTAL-DEATH-BENEFIT (#I1) TO #TOTAL-DB
  ADD #TOTAL-FACE-AMOUNT (#I1)   TO #TOTAL-FACE
*
  MOVE EDITED #TOTAL-MEMBERS (#I1) (EM=ZZZ,ZZ9)  TO #DISPLAY-MEMBERS
  MOVE EDITED #TOTAL-POLICIES (#I1) (EM=ZZZ,ZZ9) TO #DISPLAY-POLICIES
  MOVE EDITED #TOTAL-FACE-AMOUNT (#I1) (EM=ZZZ,ZZZ,ZZZ,ZZZ.99)
    TO #DISPLAY-FACE-AMOUNT
  MOVE EDITED #TOTAL-DEATH-BENEFIT (#I1) (EM=ZZZ,ZZZ,ZZZ,ZZZ.99)
    TO #DISPLAY-DEATH-BENEFIT
  DISPLAY (1) 'St' #STATE-FR-TABLE (#I1)
    'State' #STATE-NAME (#I1)
    'Members' #DISPLAY-MEMBERS
    'Policies' #DISPLAY-POLICIES
    'Face' #DISPLAY-FACE-AMOUNT
    'Death Ben' #DISPLAY-DEATH-BENEFIT
*
  COMPRESS #STATE-FR-TABLE (#I1) #TAB
    #STATE-NAME (#I1) #TAB
    #DISPLAY-MEMBERS #TAB
    #DISPLAY-POLICIES #TAB
    #DISPLAY-FACE-AMOUNT #TAB
    #DISPLAY-DEATH-BENEFIT INTO #EXTRACT-RECORD LEAVING NO
  WRITE WORK 1 #EXTRACT-RECORD
  RESET #EXTRACT-RECORD
*
END-FOR
*
MOVE EDITED #TOTAL-MEMS (EM=ZZZ,ZZ9)  TO #DISPLAY-MEMBERS
MOVE EDITED #TOTAL-POLS (EM=ZZZ,ZZ9) TO #DISPLAY-POLICIES
MOVE EDITED #TOTAL-FACE (EM=ZZZ,ZZZ,ZZZ,ZZZ.99) TO #DISPLAY-FACE-AMOUNT
MOVE EDITED #TOTAL-DB (EM=ZZZ,ZZZ,ZZZ,ZZZ.99) TO #DISPLAY-DEATH-BENEFIT
*
WRITE (1) 3X 'Totals' 13X #DISPLAY-MEMBERS 1X #DISPLAY-POLICIES
  2X #DISPLAY-FACE-AMOUNT 1X #DISPLAY-DEATH-BENEFIT
*
COMPRESS ' ' #TAB
  'Totals'#TAB
  #DISPLAY-MEMBERS #TAB
  #DISPLAY-POLICIES #TAB
  #DISPLAY-FACE-AMOUNT #TAB
  #DISPLAY-DEATH-BENEFIT INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
END
