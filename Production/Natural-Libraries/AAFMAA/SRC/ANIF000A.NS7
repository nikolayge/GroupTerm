* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Object ID: ANIF000A
*
DEFINE FUNCTION ADDRESS-CHANGE-CODE RETURNS (A1)
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID    (N8)
  1 #TYPE          (A1) OPTIONAL
  LOCAL
  1 AR-V VIEW OF A-ADDRESS-RELATIONSHIP
    2 CONTACT-ID        (N8)
    2 ADDRESS-POOL-ID   (N8)
    2 ADDR-TYPE-CD      (A1)
    2 PREVENT-OVER-IND  (A1)
    2 STATUS            (A1)
    2 RETURNED-MAIL-IND (A1)
    2 ADDR-LOCATION     (A1)
    2 ADDRESS-SOURCE    (A1)
  1 AP-V VIEW OF A-ADDRESS-POOL
    2 ADDR-VALID-IND
*
  1 #USE-TYPE    (A1)
  1 #PRIMARY     (A1) CONST <'P'>
  END-DEFINE
*
RESET ADDRESS-CHANGE-CODE
IF #TYPE SPECIFIED
  #USE-TYPE := #TYPE
ELSE
  #USE-TYPE := #PRIMARY
END-IF
*
FIND AR-V WITH CONTACT-ID = #CONTACT-ID
  ACCEPT IF AR-V.STATUS NE 'D'
    AND AR-V.ADDR-TYPE-CD = #USE-TYPE
  FIND (1) AP-V WITH ADDRESS-POOL-ID = AR-V.ADDRESS-POOL-ID
    DECIDE FOR FIRST CONDITION
      WHEN PREVENT-OVER-IND = 'Y'
        ADDRESS-CHANGE-CODE := 'N'
      WHEN PREVENT-OVER-IND = 'S'
        ADDRESS-CHANGE-CODE := 'X'
      WHEN RETURNED-MAIL-IND = 'Y'
        ADDRESS-CHANGE-CODE := 'U'
      WHEN ADDR-VALID-IND = 'F'
        ADDRESS-CHANGE-CODE := 'W'
      WHEN NONE
        ADDRESS-CHANGE-CODE := ADDRESS-SOURCE
    END-DECIDE
  END-FIND
  ESCAPE BOTTOM
END-FIND
*
END-FUNCTION
*
END
