* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* bz 07/03/2017
* Convert Suffix to 'Proper format'
*
DEFINE FUNCTION EDIT_SFX_LOWCASE RETURNS (A) DYNAMIC BY VALUE
DEFINE DATA
PARAMETER
1 p_Name (A) DYNAMIC BY VALUE
LOCAL
1 v_A100  (A100)
1 REDEFINE v_A100
  2 v_A1      (A1/1:100)
1 #i        (N2)
1 v_L       (N2)
1 l_Conv    (L) INIT <TRUE>
1 v_Exc     (A/2,7)  DYNAMIC INIT (1,V) <'Iii','Ii','Iv','Viii','Vii','Vi','Dds'>
                                  (2,V) <'III','II','IV','VIII','VII','VI','DDS'>
*                                  
END-DEFINE
*
p_Name := *TRIM(p_Name)
v_L := *LENGTH(p_Name)
v_A100 := p_Name
IF v_L > 1     /* One Letter Name
  v_A1(1) := *TRANSLATE (v_A1(1), UPPER )
  FOR #i = 2 TO v_L
    IF v_A1(#i) <> MASK(A)
      l_Conv := FALSE
    ELSE
      IF l_Conv
        v_A1(#i) := *TRANSLATE (v_A1(#i), LOWER )
      ELSE
        v_A1(#i) := *TRANSLATE (v_A1(#i), UPPER )
      END-IF
      l_Conv := TRUE
    END-IF
  END-FOR
ELSE
  v_A1(1) := *TRANSLATE (v_A1(1), UPPER )
END-IF

EDIT_SFX_LOWCASE := *TRIM(v_A100)
IF *LENGTH( EDIT_SFX_LOWCASE ) <= 4
  EXAMINE  EDIT_SFX_LOWCASE FOR v_Exc(1,*) REPLACE FIRST v_Exc(2,*)
END-IF
END-FUNCTION
END
