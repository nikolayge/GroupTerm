* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVYE5 - VALUE-ADDED YEAR-END PRODESSING - STEP 5
*                        FORMAT REPORTS AND diskette for auditors
*
* 12/12/2012 vxt extend the field size
* 12/30/2013 vt2 increased the size of face-amt field.
DEFINE DATA
LOCAL
*
1 #JCL-PARM       (A17)
1 REDEFINE #JCL-PARM
 2 #JCL-FIL-1        (A13)    /* 'PROCESS YEAR='
 2 #JCL-PROCESS-YYYY (N4)     /* '1988'
*
1 #EXTR-RECORD
 2 #ID-NUMBER            (N6)     /* from v/a history rec
 2 REDEFINE #ID-NUMBER
  3 #ID-NUMBER-A         (A6)
 2 REDEFINE #ID-NUMBER
  3 #ID-NUMBER-1ST-4     (N4)
  3 #ID-NUMBER-LST-2     (N2)
 2 REDEFINE #ID-NUMBER
  3 #ID-NUMBER-1ST-5     (N5)
  3 #ID-NUMBER-LST-1     (N1)
 2 REDEFINE #ID-NUMBER
  3 #ID-FILL             (N4)
  3 #ID-NUMBER-5         (N1)
  3 #ID-NUMBER-6         (N1)
 2 #ISSUE-DATE           (N8)
 2 REDEFINE #ISSUE-DATE
  3 #ISSUE-DATE-YYYY     (N4)
  3 #ISSUE-DATE-MM       (N2)
  3 #ISSUE-DATE-DD       (N2)
 2 #PLAN-HIST            (A3)
 2 #ISSUE-AGE            (A2)
 2 #DB-HIST              (N7.2)
 2 #ACV-HIST             (N7.2)
 2 #CV-HIST              (N7.2)
 2 #YTD-PREMIUMS-PAID    (N7.2)
 2 #YTD-INTEREST-PAID    (N5.2)
 2 #YTD-COI-PAID         (N5.2)
 2 #YTD-ACQ-PAID         (N4.2)
 2 #YTD-MNT-PAID         (N4.2)
 2 #TERM-CODE            (A3)
 2 #TERM-DATE            (N6)
 2 REDEFINE #TERM-DATE
  3 #TERM-DATE-YYYY      (N4)
  3 #TERM-DATE-MM        (N2)
 2 #PROR-LOAN-AMT        (N5.2)
 2 #SMOKER-FLAG-HIST     (A1)
 2 #USES-1996-PREMS      (A1)  /* Y if month-alloc-amt is 999.99
 2 #CAP-POLICY           (A1)  /* Y if conv-dur is 999
 2 #BEG-OF-YR-ACV        (N7.2)
 2 #BEG-OF-YR-DB         (N7.2)
************     above = 118 chars
 2 #PAY-STATUS           (A2)       /* from status record
 2 #NUM-SHIPS            (N1)
 2 #FACE-AMT             (N7.2)             /* vt2 was n6.2
 2 #ANN-PREM             (N5.2)
 2 #MIN-CV               (N7.2)             /* vt1 was n6.2 08082012
 2 #NXT-YR-MIN-CV        (N7.2)             /* vt1 was n6.2 08082012
 2 #INT-RATE             (N2.3)
 2 #MODE                 (A1)
*       below added 1997 y/e
 2 #INTRO-FLAG           (A1)
 2 #PLAN                 (A3)
 2 #DOB                  (N8)
 2 #ACV                  (N7.2)
 2 #CV                   (N7.2)
 2 #CONT-FUND            (N7.2)
 2 #CURR-ACQ             (N4.2)
 2 #MON-AMORT-ACQ        (N3.2)
 2 #DB                   (N7.2)
 2 #FIXED-LOAN           (N7.2)
 2 #FIXED-ACC-INT        (N5.2)
 2 #VAR-LOAN             (N7.2)
 2 #VAR-ACC-INT          (N5.2)
 2 #DB-CR                (N7.2)
 2 #PAID-TO-DT           (N8)
 2 #PAID-UP-DT           (N8)
 2 #5-5-DEATH-BENEFIT    (N7.2)
* below added 12/1998
 2 #PRORATED-LOAN        (N7.2)
 2 #CAP-POL-FLAG         (A1)
 2 #SMOKER-FLAG          (A1)
 2 #USES-1996-FLAG       (A1)
 2 #DUES-FLAG            (A1)
 2 #NXT-MON-CASH-VALUE   (N7.2)
 2 #NXT-MON-DEATH-BENEFIT (N7.2)
 2 #YTD-DUES-PAID        (N5.2)
*
1 #VALUATION-TAPE-DISKETTE
 2 #VTD-ID-NUMBER        (N6)
 2 #VTD-COMMA-1          (A1) INIT <','>
 2 #VTD-PLAN             (A3)
 2 #VTD-COMMA-2          (A1) INIT <','>
 2 #VTD-ISSUE-DATE       (N8)
 2 #VTD-COMMA-3          (A1) INIT <','>
 2 #VTD-ISSUE-AGE        (A2)
 2 #VTD-COMMA-4          (A1) INIT <','>
 2 #VTD-NUM-SHIPS        (N1)
 2 #VTD-COMMA-5          (A1) INIT <','>
 2 #VTD-ANN-PREM         (N5.2)
 2 #VTD-COMMA-6          (A1) INIT <','>
 2 #VTD-MODE             (A1)
 2 #VTD-COMMA-7          (A1) INIT <','>
 2 #VTD-SMOKER-CODE      (A1)
 2 #VTD-COMMA-8          (A1) INIT <','>
 2 #VTD-DB               (N7.2)
 2 #VTD-COMMA-9          (A1) INIT <','>
 2 #VTD-ACV              (N7.2)
 2 #VTD-COMMA-10         (A1) INIT <','>
 2 #VTD-CV               (N7.2)
 2 #VTD-COMMA-11         (A1) INIT <','>
 2 #VTD-PREM-DEFIC       (N3.2)
 2 #VTD-COMMA-12         (A1) INIT <','>
 2 #VTD-ALLOC-CRED       (N3.2)
 2 #VTD-COMMA-13         (A1) INIT <','>
 2 #VTD-PROR-LOAN-AMT    (N5.2)
 2 #VTD-COMMA-14         (A1) INIT <','>
 2 #VTD-INT-RATE         (N2.3)
*
1 #COUNTERS
 2 #RECS-READ        (P7)
 2 #RECS-WRITTEN     (P7)
 2 #VAL-TP-DISK-OUT  (P7)
 2 #CNT              (P7)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #TOT-FACE         (P11.2)
 2 #TOT-ACV-BOY      (P11.2)
 2 #TOT-ACV          (P11.2)
 2 #TOT-CV           (P11.2)
 2 #TOT-DB           (P11.2)
 2 #TOT-DB-BOY       (P11.2)
 2 #TOT-MON-PRMS     (P11.2)
 2 #TOT-FX-LN        (P11.2)
 2 #TOT-FX-INT       (P11.2)
 2 #TOT-VR-LN        (P11.2)
 2 #TOT-VR-INT       (P11.2)
 2 #TOT-DB-CR        (P11.2)
 2 #TOT-CONT-FUND    (P11.2)
 2 #TOT-CURR-ACQ     (P11.2)
 2 #TOT-YTD-PRMS     (P11.2)
 2 #TOT-YTD-INT      (P11.2)
 2 #TOT-YTD-COI      (P11.2)
 2 #TOT-YTD-ACQ      (P11.2)
 2 #TOT-YTD-MNT      (P11.2)
 2 #TOT-YTD-PRMS-TM  (P11.2)
 2 #TOT-YTD-INT-TM   (P11.2)
 2 #TOT-YTD-COI-TM   (P11.2)
 2 #TOT-YTD-ACQ-TM   (P11.2)
 2 #TOT-YTD-MNT-TM   (P11.2)
 2 #TOT-YTD-PREM-SUB   (P11.2)
 2 #TOT-YTD-PREM-GRAND (P11.2)
*
1 #CNT-2    (P7)
1 #CNT-3    (P7)
1 #CNT-4    (P7)
1 #CNT-5    (P7)
1 #CNT-6    (P7)
*
1 #TERM-DT               (N6)
1 REDEFINE #TERM-DT
 2 #TERM-DT-YYYY         (N4)
 2 #TERM-DT-MM           (N2)
*
1 #MONTHS (A36) INIT <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MONTHS
 2 #MONTH    (A3/12)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (N4)
 2 #MM     (A2)
 2 #DD     (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM (A6)
 2 #DD-X   (A2)
*
1 #ISS-DATE-DISP  (A10)
1 #DOB-DISP       (A10)
1 #PAID-THRU-DISP (A10)
1 #PAID-UP-DISP   (A10)
1 #TERM-DT-DISP   (A7)
1 #MON-PREM       (P5.2)
1 #CUR-AGE        (P4)
1 #DO-AGE-SORT    (L)
1 #LAST-CN        (N6)
1 #STATUS-DISP    (A4)
1 #INVALID        (L)
1 #ERR-MSG        (A60)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM:' *PROGRAM 2X
   'Auditors Rpt FOR YEAR-END V/A PROCESSING FOR' #JCL-PROCESS-YYYY
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE:' *PAGE-NUMBER /
FORMAT (2)  LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM 2X
   'Auditors Rpt (Part 1) FOR IN-FORCE POLS FOR' #JCL-PROCESS-YYYY
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE:' *PAGE-NUMBER (2) /
FORMAT (3)  LS=160 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM:' *PROGRAM 2X
   'Auditors Rpt (Part 2) FOR IN-FORCE POLS FOR' #JCL-PROCESS-YYYY
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE:' *PAGE-NUMBER (3) /
FORMAT (4)  LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM:' *PROGRAM 2X
   'Auditors Rpt FOR IN-FORCE POLS by Current Age FOR' #JCL-PROCESS-YYYY
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE:' *PAGE-NUMBER (4) /
FORMAT (5)  LS=133 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM:' *PROGRAM 2X
   'Auditors Rpt FOR TERMINATED POLS FOR' #JCL-PROCESS-YYYY
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 2X 'PAGE:' *PAGE-NUMBER (5) /
*
READ WORK 1 RECORD #EXTR-RECORD
  ADD +1 TO #RECS-READ
*
  IF #ISSUE-DATE-YYYY GT #JCL-PROCESS-YYYY
    ESCAPE TOP
  END-IF
*
  IF #PLAN = 'RT' OR = 'LT'
    PERFORM CALC-PREM-PD-RT-LT
  END-IF
*
  CALLNAT 'GPYSTSXX' #PAY-STATUS #STATUS-DISP #INVALID #ERR-MSG
*
  MOVE #ISSUE-DATE TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #ISS-DATE-DISP WITH '/'
  MOVE #DOB TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #DOB-DISP WITH '/'
  MOVE #PAID-TO-DT TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #PAID-THRU-DISP WITH '/'
  MOVE #PAID-UP-DT TO #YYYYMMDD
  COMPRESS #MM #DD #YYYY INTO #PAID-UP-DISP WITH '/'
  MOVE #TERM-DATE TO #YYYYMM
  COMPRESS #MM #YYYY INTO #TERM-DT-DISP WITH '/'
*
  COMPUTE #MON-PREM = #ANN-PREM / 12
*
  RESET #DO-AGE-SORT
  IF #TERM-CODE = ' ' OR = 'REI'
    PERFORM IN-FORCE-POLICY
*
    IF #DOB NE 0
      MOVE *DATN TO #YYYYMMDD
      SUBTRACT #DOB FROM #YYYYMMDD
      MOVE #YYYY TO #CUR-AGE
      MOVE TRUE TO #DO-AGE-SORT
    END-IF
  ELSE
    PERFORM TERMINATED-POLICY
  END-IF
  MOVE #ID-NUMBER TO #LAST-CN
*
  IF NOT #DO-AGE-SORT
    ESCAPE TOP
  END-IF
END-ALL
*
SORT BY #CUR-AGE DESCENDING #ISSUE-DATE
  USING #ID-NUMBER
        #MON-PREM
        #YTD-PREMIUMS-PAID
        #PLAN
        #PAID-UP-DISP
        #PAID-THRU-DISP
        #DOB-DISP
        #ISS-DATE-DISP
        #MODE
        #STATUS-DISP
*
  AT BREAK #CUR-AGE
    WRITE (4) '*** TOTALS ***' 51X #TOT-YTD-PREM-SUB
                                     (EM=Z,ZZZ,ZZZ,ZZZ.99) /
    RESET #TOT-YTD-PREM-SUB
  END-BREAK
*
  ADD 1 TO #CNT-4
*
* IF #ID-NUMBER-LST-2 = 0  /* only print a subset  (xxxx00)
*  OR (#CUR-AGE GT 90)     /* only print a subset  (very old guys)
*  OR (#CUR-AGE LT 22)     /* only print a subset  (very young guys)
    DISPLAY (4)
      'CNT'            #CNT-4 (EM=ZZZ,ZZ9)
      'CN'             #ID-NUMBER
      'Cur-Age'        #CUR-AGE
      'Iss-Date'       #ISS-DATE-DISP
      'DOB'            #DOB-DISP
      'Plan'           #PLAN
      'Paid-Thru'      #PAID-THRU-DISP
      'Mon/Prem'       #MON-PREM   (EM=Z,ZZZ.99)
      'YTD/Prems-paid' #YTD-PREMIUMS-PAID  (EM=ZZZ,ZZZ.99)
      'Mode'           #MODE
      'Paid-up'        #PAID-UP-DISP
      'Stat'           #STATUS-DISP
* END-IF
*
  ADD #YTD-PREMIUMS-PAID TO #TOT-YTD-PREM-SUB
  ADD #YTD-PREMIUMS-PAID TO #TOT-YTD-PREM-GRAND
END-SORT
*
WRITE (4) '*** GRAND TOTALS ***' 45X #TOT-YTD-PREM-GRAND
                                     (EM=Z,ZZZ,ZZZ,ZZZ.99)
*
WRITE (2)  / '*** TOTALS ***'
 /  46X #TOT-FACE     (EM=Z,ZZZ,ZZZ,ZZZ.99)
    29X #TOT-DB-BOY   (EM=Z,ZZZ,ZZZ,ZZZ.99)
     7X #TOT-DB-CR    (EM=ZZZ,ZZZ,ZZZ.99)
 /  61X #TOT-MON-PRMS (EM=ZZZ,ZZZ.99)
    31X #TOT-DB       (EM=Z,ZZZ,ZZZ,ZZZ.99)
*
WRITE (3) / '*** TOTALS ***'
 /  23X #TOT-CURR-ACQ   (EM=ZZZ,ZZZ.99)
     9X #TOT-FX-INT     (EM=ZZZ,ZZZ.99)
    10X #TOT-VR-INT     (EM=ZZZ,ZZZ.99)
     7X #TOT-YTD-PRMS   (EM=Z,ZZZ,ZZZ,ZZZ.99)
     4X #TOT-YTD-COI    (EM=ZZZ,ZZZ,ZZZ.99)
     2X #TOT-YTD-MNT    (EM=Z,ZZZ,ZZZ.99)
     7X #TOT-CV         (EM=Z,ZZZ,ZZZ,ZZZ.99)
 /  13X #TOT-CONT-FUND  (EM=Z,ZZZ,ZZZ.99)
     4X #TOT-FX-LN      (EM=ZZZ,ZZZ,ZZZ.99)
     6X #TOT-VR-LN      (EM=ZZZ,ZZZ,ZZZ.99)
     5X #TOT-ACV-BOY    (EM=Z,ZZZ,ZZZ,ZZZ.99)
     7X #TOT-YTD-INT    (EM=ZZ,ZZZ,ZZZ.99)
     3X #TOT-YTD-ACQ    (EM=ZZ,ZZZ,ZZZ.99)
     2X #TOT-ACV        (EM=Z,ZZZ,ZZZ,ZZZ.99)
*
WRITE (5) / '*** TOTALS ***'
 /  68X #TOT-YTD-INT-TM  (EM=ZZ,ZZZ,ZZZ.99)
     6X #TOT-YTD-ACQ-TM  (EM=ZZZ,ZZZ.99)
 /  59X #TOT-YTD-PRMS-TM (EM=ZZ,ZZZ,ZZZ.99)
     5X #TOT-YTD-COI-TM  (EM=ZZ,ZZZ,ZZZ.99)
     6X #TOT-YTD-MNT-TM  (EM=ZZZ,ZZZ.99)
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE IN-FORCE-POLICY
* * * * * * * * * * * * * * * * * *
*
ADD #MON-PREM       TO #TOT-MON-PRMS
ADD #FACE-AMT       TO #TOT-FACE
ADD #ACV            TO #TOT-ACV
ADD #BEG-OF-YR-ACV  TO #TOT-ACV-BOY
ADD #CV             TO #TOT-CV
ADD #DB             TO #TOT-DB
ADD #BEG-OF-YR-DB   TO #TOT-DB-BOY
ADD #CONT-FUND      TO #TOT-CONT-FUND
ADD #CURR-ACQ       TO #TOT-CURR-ACQ
ADD #YTD-PREMIUMS-PAID TO #TOT-YTD-PRMS
ADD #YTD-INTEREST-PAID TO #TOT-YTD-INT
ADD #YTD-COI-PAID      TO #TOT-YTD-COI
ADD #YTD-ACQ-PAID      TO #TOT-YTD-ACQ
ADD #YTD-MNT-PAID      TO #TOT-YTD-MNT
*
IF #ID-NUMBER = #LAST-CN
  RESET #FIXED-LOAN
  RESET #FIXED-ACC-INT
  RESET #VAR-LOAN
  RESET #VAR-ACC-INT
  RESET #DB-CR
END-IF
*
ADD #FIXED-LOAN     TO #TOT-FX-LN
ADD #FIXED-ACC-INT  TO #TOT-FX-INT
ADD #VAR-LOAN       TO #TOT-VR-LN
ADD #VAR-ACC-INT    TO #TOT-VR-INT
ADD #DB-CR          TO #TOT-DB-CR
*
ADD 1 TO #CNT-2
IF   (#ID-NUMBER-6 = 0)  /* only print a subset
 AND (#ID-NUMBER-5 = 0 OR = 1 OR = 2)
  DISPLAY (2)
    'CNT'            #CNT-2 (EM=ZZZ,ZZ9)
    'CN'             #ID-NUMBER
    'Iss-Date'       #ISS-DATE-DISP
    'DOB'            #DOB-DISP
    'Iss/Age'        #ISSUE-AGE
    'Plan'           #PLAN
    'PAY/STAT'       #STATUS-DISP
    'Face/Amt'       #FACE-AMT   (EM=ZZZ,ZZZ.99)
    'Mon/Prem'       #MON-PREM   (EM=Z,ZZZ.99)
    'MD'             #MODE
    'Paid-thru/Dt'   #PAID-THRU-DISP
    'Paid-Up/Dt'     #PAID-UP-DISP
    'Beg-of/yr-DB'   #BEG-OF-YR-DB (EM=ZZZ,ZZZ.99)
    'Death/Bene'     #DB           (EM=ZZZ,ZZZ.99)
    'DB-CR/AMT'      #DB-CR        (EM=ZZ,ZZZ.99-)
  DISPLAY (3)
    'CNT'            #CNT-2              (EM=ZZZ,ZZ9)
    'CN'             #ID-NUMBER
    'Cont/Fund'      #CONT-FUND          (EM=ZZ,ZZZ.99)
    'Curr/Acq-Fnd'   #CURR-ACQ           (EM=ZZZZ.99)
    'Fixed/Ln-Bal'   #FIXED-LOAN         (EM=ZZ,ZZZ.99)
    'FIXED/ACC-INT'  #FIXED-ACC-INT      (EM=Z,ZZZ.99-)
    'Var/Loan-Bal'   #VAR-LOAN           (EM=ZZ,ZZZ.99)
    'VAR/ACC-INT'    #VAR-ACC-INT        (EM=Z,ZZZ.99-)
    'Beg-of/Yr-ACV'  #BEG-OF-YR-ACV      (EM=ZZZ,ZZZ.99-)
    'YTD/PRMS-PD'    #YTD-PREMIUMS-PAID  (EM=ZZ,ZZZ.99)
    'YTD/INT-PAID'   #YTD-INTEREST-PAID  (EM=Z,ZZZ.99)
    'YTD/COI'        #YTD-COI-PAID       (EM=Z,ZZZ.99)
    'YTD/ACQ'        #YTD-ACQ-PAID       (EM=ZZZ.99)
    'YTD/MNT'        #YTD-MNT-PAID       (EM=ZZZ.99)
    'ACV'            #ACV                (EM=ZZZ,ZZZ.99-)
    'CASH/VALUE'     #CV                 (EM=ZZZ,ZZZ.99-)
  END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE TERMINATED-POLICY
* * * * * * * * * * * * * * * * * *
*
ADD #YTD-PREMIUMS-PAID TO #TOT-YTD-PRMS-TM
ADD #YTD-INTEREST-PAID TO #TOT-YTD-INT-TM
ADD #YTD-COI-PAID      TO #TOT-YTD-COI-TM
ADD #YTD-ACQ-PAID      TO #TOT-YTD-ACQ-TM
ADD #YTD-MNT-PAID      TO #TOT-YTD-MNT-TM
*
ADD 1 TO #CNT-5
DISPLAY (5)
    'CNT'            #CNT-5 (EM=Z,ZZ9)
    'CN'             #ID-NUMBER
    'Iss Date'       #ISS-DATE-DISP
    'Why/Terminated' #TERM-CODE
    'Date/Terminated'#TERM-DT-DISP
    'Plan'           #PLAN
    'Iss Date'       #ISS-DATE-DISP
    'YTD/PRMS-PD'    #YTD-PREMIUMS-PAID  (EM=ZZ,ZZZ.99)
    'YTD/INT-PAID'   #YTD-INTEREST-PAID  (EM=Z,ZZZ.99)
    'YTD/COI'        #YTD-COI-PAID       (EM=Z,ZZZ.99)
    'YTD/ACQ'        #YTD-ACQ-PAID       (EM=ZZZ.99)
    'YTD/MNT'        #YTD-MNT-PAID       (EM=ZZZ.99)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-PREM-PD-RT-LT
* * * * * * * * * * * * * * * *
*
IF #ISSUE-DATE-YYYY GT #JCL-PROCESS-YYYY
  RESET #YTD-PREMIUMS-PAID
ELSE
  IF #ISSUE-DATE-YYYY LT #JCL-PROCESS-YYYY
    MOVE #ANN-PREM TO #YTD-PREMIUMS-PAID
  ELSE
   COMPUTE #YTD-PREMIUMS-PAID = (12 - #ISSUE-DATE-MM) * (#ANN-PREM / 12)
  END-IF
END-IF
*
END-SUBROUTINE
*
END
