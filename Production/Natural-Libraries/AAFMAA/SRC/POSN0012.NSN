* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSN0012.
/**
/** :author nguentchev
/*
/* PENDING DESIGNATION BENEFICIARIES
/*
DEFINE DATA
PARAMETER
1 #REQUEST      (I4)
1 #MSG          (A) DYNAMIC
PARAMETER USING POSAPBCI
LOCAL USING ERROR_L
LOCAL USING POSL0004
LOCAL
1 #GET            (I4) CONST <0>
1 #ADD            (I4) CONST <1>
1 #UPDATE         (I4) CONST <2>
1 #DELETE         (I4) CONST <3>
1 BLANK           (A1) CONST <' '>
1 #REL-EXIST      (L)
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  VALUE #GET    PERFORM GET-RECORD
  NONE VALUE
    IGNORE
END-DECIDE
/*
DEFINE CHECK-EXIST
/*     -----------
RESET #REL-EXIST         /* 'No record exist'
FIND PB-V WITH CONTACT-ID = #PBENE.CONTACT-ID
    AND POLICY-NUMBER = #PBENE.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PB-V.RELATION = #PBENE.RELATION
  #REL-EXIST := TRUE
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
/*
DEFINE GET-RECORD
/*     -----------
#MSG := #NOT-EXIST      /* 'No record exist'
FIND PB-V WITH CONTACT-ID = #PBENE.CONTACT-ID
    AND POLICY-NUMBER = #PBENE.POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF PB-V.RELATION = #PBENE.RELATION
  MOVE BY NAME PB-V TO #PBENE
  RESET #MSG
END-FIND
*
END-SUBROUTINE
/*
DEFINE ADD-RECORD
/*     ----------
PERFORM CHECK-EXIST
IF #REL-EXIST
/* 'No duplicate key allowed - record exist'
  #MSG := #DUPLICATE-KEY
ELSE
  MOVE BY NAME #PBENE TO PB-V
  IF PB-V.ADD-USER = BLANK
    PB-V.ADD-USER      := *USER
  END-IF
  IF PB-V.ADD-DATE = 0
    PB-V.ADD-DATE      := *DATN
  END-IF
  IF PB-V.ADD-TIME = 0
    PB-V.ADD-TIME      := *TIMN
  END-IF
  IF PB-V.LAST-UPD-USER = BLANK
    PB-V.LAST-UPD-USER := *USER
  END-IF
  IF PB-V.LAST-UPD-DATE = 0
    PB-V.LAST-UPD-DATE := *DATN
  END-IF
  IF PB-V.LAST-UPD-TIME = 0
    PB-V.LAST-UPD-TIME := *TIMN
  END-IF
  STORE PB-V
  RESET #MSG
  MOVE BY NAME PB-V TO #PBENE
END-IF
END-SUBROUTINE
/**
END
