* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: GEMALSXX
* DESCRIPTION: Add/Update/Del of Contact Email
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  08/27/2010  None Creation
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID       (N8)
1 #EMAIL-ID         (N8)
1 #CLERK-ID         (A3)
1 #MODE             (A1)
1 #MSG              (A70)
LOCAL USING A-CTDATA
LOCAL
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-TYPE
  2 EMAIL-ADDRESS
  2 EMAIL-PROTECTION-IND
  2 EMAIL-UNDELIV
  2 EMAIL-PREFERRED-IND
  2 START-DATE
  2 REDEFINE START-DATE
    3 #ESD-YY                 (N4)
    3 #ESD-MM                 (N2)
    3 #ESD-DD                 (N2)
  2 REDEFINE START-DATE
    3 #START-DATE-A           (A8)
  2 END-DATE
  2 REDEFINE END-DATE
    3 #EED-YY                 (N4)
    3 #EED-MM                 (N2)
    3 #EED-DD                 (N2)
  2 REDEFINE END-DATE
    3 #END-DATE-A             (A8)
  2 LAST-DATE-UPD             (N8)
  2 REDEFINE LAST-DATE-UPD
    3 #L-YY                   (A4)
    3 #L-MM                   (A2)
    3 #L-DD                   (A2)
  2 LAST-TIME-UPD             (N7)
  2 LAST-USER-UPD             (A8)
*
1 TERM-V VIEW OF A-TERMINALS
  2 COMMENTS
  2 REDEFINE COMMENTS
    3 #TERM-EMAIL-ID          (N8)
*
1 #MAP-CV                      (C)
1 #EMAIL-TYPE                  (A1)
1 #EMAIL-ADDRESS               (A62)
1 #EMAIL-PROTECTION-IND        (A1)
1 #EMAIL-UNDELIV               (A1)
1 #EMAIL-PREFERRED-IND         (A1)
1 #START-DATE                  (A8)
1 REDEFINE #START-DATE
  2 #START-DATE-N              (N8)
1 REDEFINE #START-DATE
  2 #S-MM                      (N2)
  2 #S-DD                      (N2)
  2 #S-YY                      (N4)
1 #END-DATE                    (A8)
1 REDEFINE #END-DATE
  2 #END-DATE-N                (N8)
1 REDEFINE #END-DATE
  2 #E-MM                      (N2)
  2 #E-DD                      (N2)
  2 #E-YY                      (N4)
1 #LAST-DATE-UPD               (A10)
1 #LAST-USER-UPD               (A31)
1 #LAST-UPD-TEXT               (A10)
1 #BY-TEXT                     (A2)
1 #CN-TEXT                     (A2)
1 #CI-TEXT                     (A2)
1 #DEL-TEXT                    (A3)
1 #CONFIRM-DELETE              (A1)
END-DEFINE
*
DEFINE WINDOW Confirm-Delete
  SIZE 8 * 65
  BASE 13/2
  FRAMED ON POSITION SYMBOL AUTO
*
IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>)
  IGNORE
END-IF
*
SET KEY PF3 PF11 PF12
IF #EMAIL-ID > 0
  FIND EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-ID = #EMAIL-ID
    MOVE EMAIL-ADDRESS TO #EMAIL-ADDRESS
    MOVE EMAIL-TYPE TO #EMAIL-TYPE
    MOVE EMAIL-PROTECTION-IND TO #EMAIL-PROTECTION-IND
    MOVE EMAIL-UNDELIV TO #EMAIL-UNDELIV
    MOVE EMAIL-PREFERRED-IND TO #EMAIL-PREFERRED-IND
    IF START-DATE > 0
      MOVE #ESD-MM TO #S-MM
      MOVE #ESD-DD TO #S-DD
      MOVE #ESD-YY TO #S-YY
    END-IF
    IF END-DATE > 0
      MOVE #EED-MM TO #E-MM
      MOVE #EED-DD TO #E-DD
      MOVE #EED-YY TO #E-YY
    END-IF
    MOVE 'Updated on' TO #LAST-UPD-TEXT
    COMPRESS #L-MM '-' #L-DD '-' #L-YY INTO #LAST-DATE-UPD LEAVING NO
    MOVE 'by' TO #BY-TEXT
    FIND (1) TERM-V WITH CLERK-ID = LAST-USER-UPD
      MOVE COMMENTS TO #LAST-USER-UPD
    END-FIND
    IF #MODE = 'V'
      MOVE (AD=P) TO #MAP-CV
      SET KEY PF11=OFF
    ELSE
      MOVE (AD=U) TO #MAP-CV
      MOVE 'Del' TO #DEL-TEXT
    END-IF
  END-FIND
ELSE
  SET KEY PF11=OFF
  MOVE (AD=U) TO #MAP-CV
END-IF
IF #CONT-ID-NUMBER > 0
  MOVE 'CN' TO #CN-TEXT
END-IF
MOVE 'CI' TO #CI-TEXT

*
REPEAT
  INPUT WITH TEXT #MSG USING MAP 'GEMALMXX'
*
  IF *PF-KEY = 'PF3'
    SET WINDOW OFF
    ESCAPE ROUTINE
  END-IF
*
  IF *PF-KEY = 'PF11' AND #MODE = 'U'
    PERFORM DELETE-RECORD
    ESCAPE ROUTINE
  END-IF
*
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #CONTACT-ID
    ESCAPE TOP
  END-IF
*
  IF #MAP-CV NOT MODIFIED
    REINPUT 'No changes made. Press PF3 to exit.'
  ELSE
    PERFORM VALIDATE-FIELDS
    PERFORM ADD/UPDATE-RECORD
    ESCAPE ROUTINE
  END-IF
END-REPEAT
*********************************
DEFINE SUBROUTINE VALIDATE-FIELDS
*********************************
IF #EMAIL-ADDRESS NE SCAN('@')
  REINPUT 'Invalid Email Address format'
    MARK 1
END-IF
*
IF NOT (#EMAIL-TYPE = 'P' OR = 'B' OR = 'O')
  REINPUT 'Invalid Email Type'
    MARK *#EMAIL-TYPE
END-IF
*
IF NOT (#EMAIL-PREFERRED-IND = 'P' OR = 'S' OR = 'O')
  REINPUT 'Invalid Preference'
    MARK *#EMAIL-PREFERRED-IND
END-IF
*
IF NOT (#EMAIL-PROTECTION-IND = 'Y' OR = 'N')
  REINPUT 'Invalid Protect Email'
    MARK *#EMAIL-PROTECTION-IND
END-IF
*
IF NOT (#EMAIL-UNDELIV = 'Y' OR = 'N')
  REINPUT 'Invalid Undeliverable'
    MARK *#EMAIL-UNDELIV
END-IF
*
IF #START-DATE > ' ' AND #START-DATE-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#START-DATE
END-IF
*
IF #END-DATE > ' ' AND #END-DATE-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#END-DATE
END-IF
*
IF #START-DATE > #END-DATE
  REINPUT 'Start Date must precede End Date'
    MARK *#START-DATE
END-IF
*
END-SUBROUTINE
***********************************
DEFINE SUBROUTINE ADD/UPDATE-RECORD
**********************************
IF #EMAIL-ID = 0
  FIND (1) TERM-V WITH TERMINAL-ID = 'EMAILID'
     MOVE #TERM-EMAIL-ID TO EMAIL-ID 
     ADD 1 TO #TERM-EMAIL-ID
     UPDATE
  END-FIND
  MOVE #CONTACT-ID TO EMAIL-V.CONTACT-ID
  MOVE #EMAIL-ADDRESS TO EMAIL-ADDRESS
  MOVE #EMAIL-TYPE TO EMAIL-TYPE
  MOVE #EMAIL-PROTECTION-IND TO EMAIL-PROTECTION-IND
  MOVE #EMAIL-UNDELIV TO EMAIL-UNDELIV
  MOVE #EMAIL-PREFERRED-IND TO EMAIL-PREFERRED-IND
  MOVE #S-MM TO #ESD-MM
  MOVE #S-DD TO #ESD-DD
  MOVE #S-YY TO #ESD-YY
  MOVE #E-MM TO #EED-MM
  MOVE #E-DD TO #EED-DD
  MOVE #E-YY TO #EED-YY
  MOVE *DATN TO LAST-DATE-UPD
  MOVE *TIMN TO LAST-TIME-UPD
  MOVE #CLERK-ID TO LAST-USER-UPD
  STORE EMAIL-V
  MOVE 'Record successfully added.' TO #MSG
ELSE
  FIND EMAIL-V WITH CONTACT-ID = #CONTACT-ID
    ACCEPT IF EMAIL-ID = #EMAIL-ID
    MOVE #EMAIL-ADDRESS TO EMAIL-ADDRESS
    MOVE #EMAIL-TYPE TO EMAIL-TYPE
    MOVE #EMAIL-PROTECTION-IND TO EMAIL-PROTECTION-IND
    MOVE #EMAIL-UNDELIV TO EMAIL-UNDELIV
    MOVE #EMAIL-PREFERRED-IND TO EMAIL-PREFERRED-IND
    MOVE #S-MM TO #ESD-MM
    MOVE #S-DD TO #ESD-DD
    MOVE #S-YY TO #ESD-YY
    MOVE #E-MM TO #EED-MM
    MOVE #E-DD TO #EED-DD
    MOVE #E-YY TO #EED-YY
    MOVE *DATN TO LAST-DATE-UPD
    MOVE *TIMN TO LAST-TIME-UPD
    MOVE #CLERK-ID TO LAST-USER-UPD
    UPDATE
  END-FIND
  MOVE 'Record successfully updated.' TO #MSG
END-IF
*
END TRANSACTION
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE DELETE-RECORD
*******************************
RESET #CONFIRM-DELETE 
SET WINDOW 'Confirm-Delete'
REPEAT
  INPUT WINDOW='Confirm-Delete' USING MAP 'GEML1MXX'
  IF *PF-KEY = 'PF3'
    ESCAPE BOTTOM
  END-IF
  IF #CONFIRM-DELETE = 'Y'
    FIND (1) EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-ID = #EMAIL-ID
      DELETE 
      END TRANSACTION
      MOVE 'Record successfully deleted.' TO #MSG
    END-FIND
    ESCAPE BOTTOM
  END-IF
END-REPEAT
SET WINDOW OFF
*
END-SUBROUTINE
*
END

