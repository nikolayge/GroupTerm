* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDETH - DEATH LISTING FOR ANNUAL REPORT
*    **** SEE ATHPDETT FOR AUDITORS DEATH REPORT ******* <<<<<<<<
*
* on 4/21/1998 logic was added to add cn46122 (jack Vaughn) to this
*  list from last year. a va rec must be added (046122195001010)
* (iss dt doesn't matter) after the run this va rec was deleted
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* SAG  12012005 RH1  SS - Replaced Hex constant '05' with HEX '09' ASCII
* DRW  08162006 DW1  Add list of selected policies
* DRW 01/03/2009 DW2 Increase size of #RECS-READ
*******************************************
*
DEFINE DATA
LOCAL
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #DETH-EXT     (A50)
1 VA-V VIEW OF A-VA-HISTORY
 2 RECORD-KEY           (N15)
 2 REDEFINE RECORD-KEY
  3 ID-NUMBER           (N6)
  3 REDEFINE ID-NUMBER
   4 ID-NUMBER-A        (A6)
  3 ISS-DATE            (N8)
  3 SPLIT-IND           (N1)
 2 TERMINATION-DATE     (P7)
 2 TERMINATION-CODE     (A3)
 2 DEATH-BENEFIT-M      (P7.2/1)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER            (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A        (A6)
 2 NAME
 2 MILITARY-SERVICE
 2 MILITARY-STATUS
 2 RANK
 2 DATE-OF-DEATH
 2 REDEFINE DATE-OF-DEATH
  3 DOD-CENT       (N2)
  3 DOD-YY         (N2)
  3 DOD-MM         (N2)
  3 DOD-DD         (N2)
 2 REDEFINE DATE-OF-DEATH
  3 DOD-YYYY       (N4)
  3 DOD-MMDD       (N4)
* 2 STATUS          (9)
* 2 ISSUE-DATE      (9)
* 2 PLAN            (9)
*
1 #JCL-PARM       (A17)
1 REDEFINE #JCL-PARM
 2 #JCL-FIL-1        (A13)    /* 'PROCESS YEAR='
 2 #JCL-PROCESS-YYYY (N4)     /* '1988'
*
1 #COUNTERS
  2 #RECS-READ        (P7)    /* DW2
  2 #RECS-WRITTEN     (P5)
  2 #LAST-ID          (N6)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I2               (I2)
*
 1 #TERM-DT               (N6)
 1 REDEFINE #TERM-DT
  2 #TERM-DT-YYYY         (N4)
  2 #TERM-DT-MM           (N2)
*
 1 #HIST-KEY-YEAR-MONTH-P (P7)
 1 #YEAR-MONTH-M          (N6)
 1 REDEFINE #YEAR-MONTH-M
  2 #HIST-YYYY            (N4)
  2 #HIST-MM              (N2)
*
1 #START-KEY         (N15) INIT <000000000000000>
1 REDEFINE #START-KEY
 2 #START-ID-NUMBER  (N6)
 2 #START-ISSUE-DT   (N8)
 2 #START-SPLIT-IND  (N1)
*
1 #END-KEY         (N15) INIT <999999999999999>
1 REDEFINE #END-KEY
 2 #END-ID-NUMBER  (N6)
 2 #END-ISSUE-DT   (N8)
 2 #END-SPLIT-IND  (N1)
*
1 #IM    (I1)
1 #ID    (I1)
*
1 #TOT-NET-DB        (P13.2)
1 #NET-DB            (P7.2)
1 #GROSS-DB          (P7.2)
1 #FOUND-DB-POL       (L)
1 #VA-REC-FOUND       (L)
1 #STAT-NOT-FND       (L)
*
1 #DOD-YYYYMMDD-G
 2 #DOD-YYYYMM     (N6)
 2 REDEFINE #DOD-YYYYMM
3 #DOD-YYYY         (N4)
  3 #DOD-MM           (N2)
 2 #DOD-DD           (N2)
1 REDEFINE #DOD-YYYYMMDD-G
 2 #DOD-YYYYMMDD     (N8)
*
1 #SERVICE (A5) INIT <'AF   '>
1 #STATUS  (A8) INIT <'RETIRED '>
1 #DOTS-1      (A25)  INIT <'.........................'>
1 #DOTS-2      (A25)  INIT <'.........................'>
1 #DOTS-8      (A8)   INIT <'........'>
*
1 #RANK               (A3)
1 #NAME      (A25)
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #FIRST-MID  (A25)
1 #MI        (A1)
1 #ERR-MSG      (A60)
*
1 #MONTHS (A36) INIT <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MONTHS
 2 #MONTH    (A3/12)
*
1 #CNT-3          (N5)
1 #LINE           (A80)
1 #AST            (A1)
1 #DOD-MMMYYYY    (A8)
1 #DOD-MMM        (A3)
1 #MIL-SERV       (A1)
1 #SERV           (A9) INIT <'     ARMY'>
1 #NO-FIRS    (L)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /*
          'Deaths with pols issued in 2004/2005/2006'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
INPUT #JCL-PARM
*
COMPRESS 'AAFMAA Member Deaths In' #JCL-PROCESS-YYYY INTO #DETH-EXT
WRITE WORK FILE 2 #DETH-EXT
ADD 1 TO #CNT
*
RESET #DETH-EXT
WRITE WORK FILE 2 #DETH-EXT
ADD 1 TO #CNT
*
COMPRESS 'Last Name'
  #TAB 'First-Middle'
  #TAB 'Rank'
  #TAB 'Service'
  #TAB 'Status'
  #TAB 'DOD'
  #TAB
      INTO #DETH-EXT LEAVING NO
*
WRITE WORK FILE 2 #DETH-EXT
ADD 1 TO #CNT
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT (2) LS=133 PS=61
WRITE  (2) TITLE LEFT 25T #SERV 35T 'DEATH LOSSES FOR'
               #JCL-PROCESS-YYYY
         //    51T 'SERVICE' 59T 'STATUS' 77T 'DATE OF'
         /     77T ' DEATH'
         /
*
READ VA-V BY RECORD-KEY
* IF #RECS-READ GT 100
*   ESCAPE BOTTOM
* END-IF
*
  ADD +1 TO #RECS-READ
  MOVE VA-V.TERMINATION-DATE TO #TERM-DT
*
  IF  (#TERM-DT-YYYY NE #JCL-PROCESS-YYYY)  /* DIED NOT IN PROCESS YEAR
   OR (VA-V.ID-NUMBER = #LAST-ID)      /* 2ND POLICY OF A DEAD MBR
    ESCAPE TOP
  END-IF
*
* RESET #STAT-NOT-FND    /* BELOW TO FIND VA BUT NO STATUS *******
*    or to find selected policies
* FIND (1) ST-V WITH MEMBER-KEY = VA-V.ID-NUMBER-A
*   IF NO RECORDS FOUND
*     FIND (1) ST-V WITH WIDOW-KEY = VA-V.ID-NUMBER-A
*       IF NO RECORDS FOUND
*         DISPLAY 'ID' VA-V.ID-NUMBER
*               ' ISSUE DATE = ' ISS-DATE
*               ' TERM CODE  = ' TERMINATION-CODE
*               ' TERM DATE  = ' TERMINATION-DATE
*               ' DEATH BENE = ' DEATH-BENEFIT-M (1)
*         MOVE TRUE TO #STAT-NOT-FND
*       END-NOREC
*     END-FIND
*   END-NOREC
* END-FIND
*
* IF #STAT-NOT-FND
*   ESCAPE TOP
* END-IF
*                                           /******************
*
  IF VA-V.TERMINATION-CODE NE 'DTH'   /* IF DEAD AS OF Y/E
    ESCAPE TOP
  END-IF
*
  IF ISS-DATE GE 20040101
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-3 VA-V.ID-NUMBER ISS-DATE
  END-IF
*
  MOVE VA-V.ID-NUMBER TO #LAST-ID
  RESET #STAT-NOT-FND
  FIND (1) ST-V WITH WIDOW-KEY = VA-V.ID-NUMBER-A
    IF NO RECORDS FOUND
      WRITE 'STAT REC NOT FOUND FOR ' VA-V.ID-NUMBER
            ' Issue date = ' ISS-DATE
            ' Term code  = ' TERMINATION-CODE
            ' Term date  = ' TERMINATION-DATE
            ' Death Bene = ' DEATH-BENEFIT-M (1)
      MOVE TRUE TO #STAT-NOT-FND
    END-NOREC
  END-FIND
*
  IF #STAT-NOT-FND
*   IF VA-V.ID-NUMBER = 46122 /* to force a death after building the va
*     MOVE 'VAUGHN, JACK E.' TO NAME
*     MOVE 'A' TO MILITARY-SERVICE
*     MOVE 'R' TO MILITARY-STATUS
*     MOVE 'LTC' TO RANK
*     MOVE 19960106 TO DATE-OF-DEATH
*   ELSE
      ESCAPE TOP
*   END-IF
  END-IF
*
  IF MILITARY-SERVICE = MASK ('F')
    MOVE 'F' TO #MIL-SERV
  ELSE
    MOVE 'A' TO #MIL-SERV
  END-IF
END-ALL
*
* * * * * * SORT INPUT RECORDS * * * * * *
*
* SORT BY #MIL-SERV ST-V.NAME
SORT BY ST-V.NAME
  USING VA-V.ID-NUMBER
        VA-V.TERMINATION-DATE
        ST-V.RANK
        ST-V.DATE-OF-DEATH
        ST-V.MILITARY-STATUS
        ST-V.MILITARY-SERVICE
        #MIL-SERV
*
  AT BREAK #MIL-SERV
    WRITE (2) '     * - DEATH REPORTED IN' #JCL-PROCESS-YYYY
              'BUT OCCURED IN A PRIOR YEAR'
    MOVE 'AIR FORCE' TO #SERV
    NEWPAGE (2)
  END-BREAK
*
  MOVE VA-V.TERMINATION-DATE TO #TERM-DT
*
  RESET #SERVICE #STATUS
*
  IF ST-V.RANK = 'CDT'
    MOVE 'CADET' TO #SERVICE
  ELSE
    IF ST-V.MILITARY-SERVICE = 'AR' OR = 'AG' OR = 'AGA'
      MOVE 'USAR' TO #SERVICE
    ELSE
      IF ST-V.MILITARY-SERVICE = 'FR' OR = 'FG' OR = 'FGA' OR = 'FRA'
        MOVE 'AFRES' TO #SERVICE
      ELSE
        IF ST-V.MILITARY-SERVICE = MASK ('A')
          MOVE 'USA' TO #SERVICE
        ELSE
          IF ST-V.MILITARY-SERVICE = MASK ('F')
            MOVE 'USAF' TO #SERVICE
          ELSE
            IF ST-V.MILITARY-SERVICE = MASK ('N')
              MOVE 'USN' TO #SERVICE
            ELSE
              IF ST-V.MILITARY-SERVICE = MASK ('M')
                MOVE 'USMC' TO #SERVICE
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
    IF ST-V.MILITARY-STATUS = 'C'
      MOVE 'CIVILIAN' TO #STATUS
    ELSE
      IF ST-V.MILITARY-STATUS = 'R'
        MOVE 'RETIRED' TO #STATUS
      ELSE
        MOVE 'ACTIVE' TO #STATUS
      END-IF
    END-IF
  END-IF
*
  MOVE ST-V.NAME TO #NAME
  RESET #LAST #MI #FIRST-20
  CALLNAT 'GFMLNSXX' /* split name
   #NAME
   #FIRST-20
   #MI
   #LAST
   #ERR-MSG
*
  COMPRESS #FIRST-20 #MI INTO #FIRST-MID
  MOVE ST-V.DOD-MM TO #I1
*
  IF (#I1 = 0) OR (#I1 GT 12)
    MOVE ' ? ' TO #DOD-MMM
  ELSE
    MOVE #MONTH (#I1) TO #DOD-MMM
  END-IF
*
  COMPRESS #DOD-MMM ST-V.DOD-YYYY INTO #DOD-MMMYYYY
*
  IF ST-V.DOD-YYYY NE #JCL-PROCESS-YYYY
    MOVE '*' TO #AST
  ELSE
    RESET #AST
  END-IF
*
  WRITE (2) 04T ST-V.RANK
            08T #LAST
            34T #FIRST-MID
            60T #SERVICE
            66T #STATUS
*           75T #DOTS-8
            77T #DOD-MMMYYYY
            86T #AST
*
  ADD 1 TO #CNT
  COMPRESS ST-V.RANK #DOTS-1 INTO #RANK LEAVING NO
*
  COMPRESS #LAST
    #TAB #FIRST-MID
    #TAB #RANK
    #TAB #SERVICE
    #TAB #STATUS
    #TAB #DOD-MMMYYYY
    #TAB #AST
        INTO #DETH-EXT LEAVING NO
*
    WRITE WORK FILE 2 #DETH-EXT
    ADD 1 TO #CNT
END-SORT
*
WRITE 'num-recs = ' #CNT
*
END
