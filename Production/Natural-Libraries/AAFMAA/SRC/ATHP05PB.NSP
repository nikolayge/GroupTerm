* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PROGRAM-ID: ATHP05PB    - MEMBERSHIPS STATISTICS PROCESSING
*
*  THIS PROGRAM RUNS IN BATCH MODE ONLY
*
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  02222005 DW1  Add AL (annuity Life) Processing
* DRW  03152005 DW2  Fix GDROPSTM CALL using #EFF-DATE not #EFFECT-DATE
* DRW  05182005 DW3  Add Online-action of 'T' NEW/ADDL & CSV
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
* DRW 08/21/2008 DW4 Gen Plus Processing
* RSE 09/17/2009 RE1 Change PROCESS-IND logic
* vxt 11/01/2010 vt1 Replace name and rank  of Status file with the name and rank from the new files.
* YAK 10/09/2012 YK1 Use function to identify Level Term II plans
* YAK 01/15/2012 YK2 Add EZ term plan (EZ)
* YAK 01/15/2012 YK3 Add GP2 plan (Generation Plus 2)
* NGG 06/23/2014 NG1 CONTACT NORMALIZATION
* NGG 20141023   NG2 Annuity life products  
* vxt 06/02/2017 vt2 Group Term Project  
*******************************************
*
DEFINE DATA
GLOBAL USING ATHG05P0
LOCAL  USING ATHL05PB
LOCAL USING GONERLXX                        /* SAG RH1 11292005 SS
LOCAL
1 OBSOLETE-STATUS-FIELDS                         /* NG1
  2 DATE-OF-BIRTH     (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 DOB-YYYY (N4.0)
    3 REDEFINE DOB-YYYY
      4 DOB-CENT (N2.0)
      4 DOB-YY (N2.0)
    3 DOB-MMDD (N4.0)
    3 REDEFINE DOB-MMDD
      4 DOB-MM (N2.0)
      4 DOB-DD (N2.0)
*
1 #REPT-EXTRACT          (A119)                        /* vt1 was 74
1 REDEFINE #REPT-EXTRACT
  2 #RE-SECTION         (A1)
  2 #RE-SUBSECTION      (A1)
  2 #RE-NAME-ID
    3 #RE-NAME-1         (A70)                           /* vt1 was 25
    3 #RE-FILL-1         (A1)
    3 #RE-ID-NUMBER-2    (N6)
  2 REDEFINE #RE-NAME-ID
    3 #RE-ID-NUMBER-1    (N6)
    3 #RE-FILL-2         (A1)
    3 #RE-NAME-2         (A70)                           /* vt1 was 25
  2 #RE-UNITS-GAIN-LOSS (N2)
  2 #RE-CURR-UNITS      (N2)
  2 #RE-ISS-AGE         (A2)
  2 #RE-ISS-DATE        (A8)
  2 #RE-TYPE            (A4)
  2 #RE-TERM-AGE        (N3)
  2 #RE-TERM-DATE       (A8)
  2 #RE-PLAN            (A3)
  2 #RE-RANK            (A3)
  2 #RE-STAT            (A1)
  2 #RE-SERV            (A3)
  2 #RE-MODE            (A1)
*
1 #TOT-LINES       (P3)
1 #TOT-FACE        (P7.2)
1 #FACE-X          (I2)
1 #UNITS           (P2)
1 #PLAN      (A3)
1 REDEFINE #PLAN
  2 #PLAN-A1   (A1)
  2 #PLAN-N2   (N2)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #I4              (I2)
1 #I5              (I2)
1 #IP              (I2)  /* plan index
1 #IAGE            (I2)
1 #IRANK           (I2)
1 #ISERV           (I2)
1 #ISTAT           (I2)
1 #IPLAN           (I2)
1 #ITYPE           (I2)
1 #IGRADE          (I2)
1 #ITRANS          (I2)
1 #ILOSS           (I2)
1 #HOLD-ILOSS      (I2)
1 #CALC-AGE        (P5)
1 #LOSSES          (L)    /* TRUE IF LOSSES
1 #LOSS-ON-LINE     (L)    /* TRUE IF CUR LINE HAS LOSS
1 #GAIN-ON-LINE     (L)    /* TRUE IF CUR LINE HAS GAIN
1 #INTRO-COMP-GRADE (L)    /* TRUE IF MBR IS INTRO LESS THAN 5 YRS
1 #CSV-OR-LAPS      (L)
1 #MATCH-ISS        (L)
1 #PAY-STAT        (A4)
1 #EFFECT-DATE        (N8)
*
1 #WORK-YYYYMMDD    (N8)
1 REDEFINE #WORK-YYYYMMDD
  2 #WORK-CENT       (A2)
  2 #WORK-YY         (A2)
  2 #WORK-MM         (A2)
  2 #WORK-DD         (A2)
*
1 #ACC-ME-TOT-MEMBERS   (P7)
1 #ACC-ME-TOT-MEMBERSHIPS (P7)
1 #AT-LEAST-1-SEGMENT   (L)
1 #POLICY-EXPIRED       (L)
1 #STATS-REC-FOUND      (L)
1 #TOT-MBRS             (P7)
1 #TOT-SHIPS            (P7)
*
1 #STATS-ISN          (P8)
1 #MBR-STATS-KEY      (N6)
1 REDEFINE #MBR-STATS-KEY
  2 #MBR-STATS-KEY-YYYY (N4)
  2 #MBR-STATS-KEY-MM   (N2)
*
1 #LAST-MON-MBR-STATS-KEY      (N6)
1 REDEFINE #LAST-MON-MBR-STATS-KEY
  2 #LAST-MON-MBR-STATS-KEY-YYYY (N4)
  2 #LAST-MON-MBR-STATS-KEY-MM   (N2)
*
1 #ABEND-CODE       (I4)
1 #FACE-AMT (P7.2/9)
1 #DEATH-BN (P7.2/9)
*
1 #contact-id   (n8)                        /* vt1 \/
1 #name         (a70)
1 #last-name    (a25)
1 #first-name   (a25)
1 #middle-name  (a25)
1 #suffix       (a10)
1 #name-f       (a70)
*
1 #pda-rank
  2 #PASS-COMMAND       (A2)
  2 #RANK-PASS          (A3)
  2 #MIL-ID-PASS        (A3)
  2 #INVALID            (L)
  2 #RANK-PAY-TBL-DISPL (P2)
  2 #RANK-LITERAL-PASS  (A23)
  2 #RANK-DISPLAY       (A5)
*
  2 #CONTACT-ID-r       (N8)
  2 #RANK-ABBR          (A) DYNAMIC
  2 #LETTER-ENV         (A) DYNAMIC
  2 #SALUTATION         (A) DYNAMIC
  2 #SALUTATION-WITH-NAME (A) DYNAMIC
*                                           /* vt1 /\
END-DEFINE
* * * * * * * * * * * * * * * * * *
*
ON ERROR
*    WRITE   / '*E R R O R - N R    ' *ERROR-NR            /* SAG RH1 11292005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     MOVE +4095 TO #ABEND-CODE
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
  INCLUDE GABNDCX3
  print '=' STATUS-VIEW.ID-NUMBER
  TERMINATE 2                                                       /* SAG RH1 11292005 SS - END
END-ERROR
*
FORMAT PS=72
WRITE TITLE LEFT 'PROGRAM: ATHP05P0'              /* USE DRIVER NAME
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
DATE-OF-BIRTH := GET-DATE-OF-BIRTH (< STATUS-VIEW.MEMBER-CONTACT-ID >)  /* NG1
IF PROCESS-SWITCH = 'P'                 /* PROCESSING MODE
  PERFORM ACCUM-STATS
  ESCAPE ROUTINE
END-IF
*
IF PROCESS-SWITCH = 'F'                   /* FINALIZE MODE
  FETCH RETURN 'ATHP05PZ'
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-STATS
* * * * * * * * * * * * * * * *
*
* * * * * * ACCUM FOR MANNING STATS * * *
*
IF   (STATUS-VIEW.NUMBER-MEMBERSHIPS NE 0)  /* ACTIVE MEMBERSHIPS
    AND (NOT STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G')  /* MBRS
  IF STATUS-VIEW.MILITARY-STATUS = 'A'     /* ACTIVE DUTY
    IF STATUS-VIEW.MILITARY-SERVICE = MASK ('A')   /* ARMY
      DECIDE ON FIRST VALUE OF STATUS-VIEW.RANK
        VALUE 'GA'   ADD 1 TO #MBC-MANNING-ARMY (1)
        VALUE 'GEN'  ADD 1 TO #MBC-MANNING-ARMY (2)
        VALUE 'LTG'  ADD 1 TO #MBC-MANNING-ARMY (3)
        VALUE 'MG'   ADD 1 TO #MBC-MANNING-ARMY (4)
        VALUE 'BG'   ADD 1 TO #MBC-MANNING-ARMY (5)
        VALUE 'COL'  ADD 1 TO #MBC-MANNING-ARMY (6)
        VALUE 'LTC'  ADD 1 TO #MBC-MANNING-ARMY (7)
        VALUE 'MAJ'  ADD 1 TO #MBC-MANNING-ARMY (8)
        VALUE 'CPT'  ADD 1 TO #MBC-MANNING-ARMY (9)
        VALUE 'LT'   ADD 1 TO #MBC-MANNING-ARMY (10)
        VALUE '1LT'  ADD 1 TO #MBC-MANNING-ARMY (10)
        VALUE '2LT'  ADD 1 TO #MBC-MANNING-ARMY (10)
        VALUE 'O11'  ADD 1 TO #MBC-MANNING-ARMY (1)
        VALUE 'O10'  ADD 1 TO #MBC-MANNING-ARMY (2)
        VALUE 'O9'   ADD 1 TO #MBC-MANNING-ARMY (3)
        VALUE 'O8'   ADD 1 TO #MBC-MANNING-ARMY (4)
        VALUE 'O7'   ADD 1 TO #MBC-MANNING-ARMY (5)
        VALUE 'O6'   ADD 1 TO #MBC-MANNING-ARMY (6)
        VALUE 'O5'   ADD 1 TO #MBC-MANNING-ARMY (7)
        VALUE 'O4'   ADD 1 TO #MBC-MANNING-ARMY (8)
        VALUE 'O3'   ADD 1 TO #MBC-MANNING-ARMY (9)
        VALUE 'O2'   ADD 1 TO #MBC-MANNING-ARMY (10)
        VALUE 'O1'   ADD 1 TO #MBC-MANNING-ARMY (10)
        VALUE 'CW4'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'WO4'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'CW3'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'CWO'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'WO3'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'CW2'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'WO2'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'CW1'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'WO1'  ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'WO'   ADD 1 TO #MBC-MANNING-ARMY (11)
        VALUE 'CSM'  ADD 1 TO #MBC-MANNING-ARMY (12) /* e9
        VALUE 'SGM'  ADD 1 TO #MBC-MANNING-ARMY (12) /* e9
        VALUE '1SG'  ADD 1 TO #MBC-MANNING-ARMY (13) /* e8
        VALUE 'MSG'  ADD 1 TO #MBC-MANNING-ARMY (13) /* e8
        VALUE 'PSG'  ADD 1 TO #MBC-MANNING-ARMY (14) /* e7
        VALUE 'SFC'  ADD 1 TO #MBC-MANNING-ARMY (14) /* e7
        VALUE 'SSG'  ADD 1 TO #MBC-MANNING-ARMY (15) /* e6
        VALUE 'SGT'  ADD 1 TO #MBC-MANNING-ARMY (16) /* e5
        VALUE 'CPL'  ADD 1 TO #MBC-MANNING-ARMY (17) /* e4
        VALUE 'SPC'  ADD 1 TO #MBC-MANNING-ARMY (17) /* e4
        VALUE 'E9'  ADD 1 TO #MBC-MANNING-ARMY (12) /* E9
        VALUE 'E8'  ADD 1 TO #MBC-MANNING-ARMY (13) /* E8
        VALUE 'E7'  ADD 1 TO #MBC-MANNING-ARMY (14) /* E7
        VALUE 'E6'  ADD 1 TO #MBC-MANNING-ARMY (15) /* E6
        VALUE 'E5'  ADD 1 TO #MBC-MANNING-ARMY (16) /* E5
        VALUE 'E4'  ADD 1 TO #MBC-MANNING-ARMY (17) /* E4
        NONE         IGNORE
      END-DECIDE
    END-IF /* (0199)
*
    IF STATUS-VIEW.MILITARY-SERVICE = MASK ('F')   /* AIR-FORCE
      DECIDE ON FIRST VALUE OF STATUS-VIEW.RANK
        VALUE 'GA'   ADD 1 TO #MBC-MANNING-AIRF (1)
        VALUE 'GEN'  ADD 1 TO #MBC-MANNING-AIRF (2)
        VALUE 'LTG'  ADD 1 TO #MBC-MANNING-AIRF (3)
        VALUE 'MG'   ADD 1 TO #MBC-MANNING-AIRF (4)
        VALUE 'BG'   ADD 1 TO #MBC-MANNING-AIRF (5)
        VALUE 'COL'  ADD 1 TO #MBC-MANNING-AIRF (6)
        VALUE 'LTC'  ADD 1 TO #MBC-MANNING-AIRF (7)
        VALUE 'MAJ'  ADD 1 TO #MBC-MANNING-AIRF (8)
        VALUE 'CPT'  ADD 1 TO #MBC-MANNING-AIRF (9)
        VALUE 'LT'   ADD 1 TO #MBC-MANNING-AIRF (10)
        VALUE '1LT'  ADD 1 TO #MBC-MANNING-AIRF (10)
        VALUE '2LT'  ADD 1 TO #MBC-MANNING-AIRF (10)
        VALUE 'O11'  ADD 1 TO #MBC-MANNING-AIRF (1)
        VALUE 'O10'  ADD 1 TO #MBC-MANNING-AIRF (2)
        VALUE 'O9'   ADD 1 TO #MBC-MANNING-AIRF (3)
        VALUE 'O8'   ADD 1 TO #MBC-MANNING-AIRF (4)
        VALUE 'O7'   ADD 1 TO #MBC-MANNING-AIRF (5)
        VALUE 'O6'   ADD 1 TO #MBC-MANNING-AIRF (6)
        VALUE 'O5'   ADD 1 TO #MBC-MANNING-AIRF (7)
        VALUE 'O4'   ADD 1 TO #MBC-MANNING-AIRF (8)
        VALUE 'O3'   ADD 1 TO #MBC-MANNING-AIRF (9)
        VALUE 'O2'   ADD 1 TO #MBC-MANNING-AIRF (10)
        VALUE 'O1'   ADD 1 TO #MBC-MANNING-AIRF (10)
        VALUE 'CW4'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'WO4'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'CW3'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'CWO'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'WO3'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'CW2'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'WO2'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'CW1'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'WO1'  ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'WO'   ADD 1 TO #MBC-MANNING-AIRF (11)
        VALUE 'CMS'  ADD 1 TO #MBC-MANNING-AIRF (12) /* e9
        VALUE 'SMS'  ADD 1 TO #MBC-MANNING-AIRF (13) /* e8
        VALUE 'MSG'  ADD 1 TO #MBC-MANNING-AIRF (14) /* e7
        VALUE 'TSG'  ADD 1 TO #MBC-MANNING-AIRF (15) /* e6
        VALUE 'SSG'  ADD 1 TO #MBC-MANNING-AIRF (16) /* e5
        VALUE 'E9'  ADD 1 TO #MBC-MANNING-AIRF (12) /* E9
        VALUE 'E8'  ADD 1 TO #MBC-MANNING-AIRF (13) /* E8
        VALUE 'E7'  ADD 1 TO #MBC-MANNING-AIRF (14) /* E7
        VALUE 'E6'  ADD 1 TO #MBC-MANNING-AIRF (15) /* E6
        VALUE 'E5'  ADD 1 TO #MBC-MANNING-AIRF (16) /* E5
        NONE         IGNORE
      END-DECIDE
    END-IF /* (0254)
  END-IF /* (0198)
END-IF /* (0196)
*
**IF PROCESS-IND = 'S' OR = 'C'
IF PROCESS-IND = 'C'                                /* RE1
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * ACCUM FOR EOM TOTAL MEMBERSHIPS AND CHECK FOR GAINS
*
RESET #GAIN-ON-LINE
RESET #AT-LEAST-1-SEGMENT
RESET #POLICY-EXPIRED
RESET #TOT-FACE
RESET #TOT-LINES
RESET #FACE-AMT (*)
*
FOR #I1 1 TO 9
  IF STATUS (#I1) NE 'D'
    ESCAPE TOP
  END-IF
*
  MOVE FACE-AMOUNT   (#I1) TO #FACE-AMT (#I1)
  MOVE DEATH-BENEFIT (#I1) TO #DEATH-BN (#I1)
*
  MOVE PLAN(#I1) TO #PLAN
  MOVE #C-RUN-DT-YYYYMMDD TO #EFFECT-DATE   /* was #eff-date  /* DW2
  IF   ((PLAN (#I1) = 'LT')
      AND (DATE-OF-BIRTH LE (#EFFECT-DATE - 400000)))
      OR  ((PLAN (#I1) = 'RT')
      AND (ISSUE-AGE (#I1) GT '65'))
      OR  ((PLAN (#I1) = 'EZ')                                /* YK2
      AND (PAID-UP-DATE(#I1) LE #EFFECT-DATE))                /* YK2
*     OR  (((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))          /* YK1
      OR  (F-LT2-TRUE(<PLAN(#I1)>)                            /* YK1
      AND ((ISSUE-DATE(#I1) + 40000) LE #EFFECT-DATE))
*     OR (PLAN (#I1) = 'AL')             /* DW1
      OR IS-ANNUITY-LIFE (< PLAN (#I1) >)  /* NG2
*
    CALLNAT 'GDROPSTM' USING
      PLAN          (#I1)
      SMOKER-FLAG   (#I1)
      DATE-OF-BIRTH
      ISSUE-DATE    (#I1)
      PAID-UP-DATE  (#I1)
      #EFFECT-DATE
      ISSUE-AGE     (#I1)
      #PAY-STAT
      #FACE-AMT     (#I1)
      #DEATH-BN     (#I1)
      CASH-VALUE    (#I1)
      BEG-CASH-VALUE (#I1)
  END-IF
*
  IF STATUS-VIEW.STATUS (#I1) = 'D'
    ADD #FACE-AMT (#I1) TO #TOT-FACE
  END-IF
END-FOR
*
PERFORM CALC-FACE-X
MOVE #FACE-X TO #I3
*
FOR #I1 1 TO 9
  IF STATUS-VIEW.ONLINE-ACTION (#I1) = '1' OR = 'G'  /* CHECK FOR GAINS
      OR = 'B' OR = 'H' OR = 'Q' OR = 'T'     /* DW3
    MOVE TRUE TO #GAIN-ON-LINE
  END-IF
*
  IF   (STATUS-VIEW.STATUS (#I1) = 'E')
      AND (STATUS-VIEW.ONLINE-ACTION (#I1) = '8' OR = 'F') /* ACCUM
    MOVE TRUE TO #POLICY-EXPIRED                        /* EXPIRATIONS
    ADD 1 TO #EXPIRE-LINES
  END-IF
END-FOR
*
IF   (#POLICY-EXPIRED)
    AND (STATUS-VIEW.NUMBER-MEMBERSHIPS = 0)    /* ACCUM EXPIRATION
  ADD 1 TO #EXPIRE-MBRS                      /* MEMBERS
END-IF
*
FOR #I1 1 TO 9
  IF STATUS-VIEW.STATUS (#I1) NE 'D'
    ESCAPE TOP
  END-IF
*
  MOVE TRUE TO #AT-LEAST-1-SEGMENT
  PERFORM FIND-PLAN-X
  ADD 1 TO #ME-LINES-BY-PLAN  (#IP)
  ADD 1 TO #ME-LINES-BY-UNITS (#I3)
  ADD 1 TO #TOT-LINES
*
  IF STATUS-VIEW.PLAN (#I1) NE 'FSO'
    ADD 1 TO #ME-TOT-LINES    /* for crooks
*
    IF STATUS-VIEW.PAY-STATUS (#I1) GT '40'
      ADD 1 TO #TOT-PUP-LINES
    END-IF
  END-IF
*
  IF STATUS-VIEW.PLAN (#I1) NE 'FSO'
    ADD #DEATH-BN (#I1) TO #ME-INS-IN-FORCE
  END-IF
*
  IF STATUS-VIEW.PAY-STATUS (#I1) = '44'
    ADD 1 TO #ME-ETI-LINES
    ADD #DEATH-BN (#I1) TO #ME-ETI-DB
  ELSE
    IF STATUS-VIEW.PAY-STATUS (#I1) GT '40'
      ADD 1 TO #ME-PAID-UP-LINES
      ADD #DEATH-BN (#I1) TO #ME-PAID-UP-DB
    END-IF
  END-IF
END-FOR
*
IF #AT-LEAST-1-SEGMENT
  ADD 1 TO #ME-TOT-MEMBERS
  MOVE #FACE-X TO #I2
  ADD 1 TO #ME-MBR-BY-UNITS (#I2)
END-IF
*
* * * * * * * * * * * ACCUM FOR NEW BUSINESS REPORT SUMMARY * * * *
*
RESET #REPT-EXTRACT        /* INITIALIZE RECORD
*
IF STATUS-VIEW.ONLINE-ACTION-GN = '2'          /* NEW MEMBER
  MOVE TRUE TO #GAIN-ON-LINE
END-IF
*
IF #GAIN-ON-LINE                        /* SET 2 PLACES ABOVE
  DECIDE ON FIRST VALUE OF STATUS-VIEW.RANK
    VALUE 'CDT' MOVE 1 TO #IRANK
    VALUE 'WO1' MOVE 2 TO #IRANK
    VALUE 'CW2' MOVE 2 TO #IRANK
    VALUE 'CW3' MOVE 2 TO #IRANK
    VALUE 'CW4' MOVE 2 TO #IRANK
    VALUE 'MW4' MOVE 2 TO #IRANK
    VALUE 'CW5' MOVE 2 TO #IRANK
    VALUE '2LT' MOVE 3 TO #IRANK
    VALUE '1LT' MOVE 4 TO #IRANK
    VALUE 'CPT' MOVE 5 TO #IRANK
    VALUE 'MAJ' MOVE 6 TO #IRANK
    VALUE 'LTC' MOVE 7 TO #IRANK
    VALUE 'COL' MOVE 8 TO #IRANK
    VALUE 'GEN' MOVE 9 TO #IRANK
    VALUE 'GA ' MOVE 9 TO #IRANK
    VALUE 'LTG' MOVE 9 TO #IRANK
    VALUE 'MG ' MOVE 9 TO #IRANK
    VALUE 'BG ' MOVE 9 TO #IRANK
    NONE        MOVE 10 TO #IRANK
  END-DECIDE
*
  IF   (#IRANK = 10)                          /* NON-MILITARY RANK
      AND (STATUS-VIEW.MILITARY-STATUS = 'C')    /* CIVILIAN
    MOVE 11 TO #IRANK
  END-IF
*
  IF STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    MOVE 11 TO #IRANK
  END-IF
*
  IF STATUS-VIEW.RESTRICT-MBR-SW = 'Y'
    IF STATUS-VIEW.MILITARY-SERVICE = MASK ('A')
      MOVE 5 TO #ISERV
    ELSE
      MOVE 12 TO #ISERV
    END-IF
  ELSE
    DECIDE ON FIRST VALUE OF STATUS-VIEW.MILITARY-SERVICE
      VALUE 'A  ' MOVE 1  TO #ISERV
      VALUE 'AG ' MOVE 3  TO #ISERV
      VALUE 'AGA' MOVE 3  TO #ISERV
      VALUE 'AR ' MOVE 4  TO #ISERV
      VALUE 'AC ' MOVE 6  TO #ISERV
      VALUE 'ACR' MOVE 7  TO #ISERV
      VALUE 'F  ' MOVE 8  TO #ISERV
      VALUE 'FG ' MOVE 10 TO #ISERV
      VALUE 'FGA' MOVE 10 TO #ISERV
      VALUE 'FRA' MOVE 10 TO #ISERV
      VALUE 'FR ' MOVE 11 TO #ISERV
      VALUE 'FC ' MOVE 13 TO #ISERV
      VALUE 'FCR' MOVE 14 TO #ISERV
      NONE        MOVE 15 TO #ISERV
    END-DECIDE
*
    IF   (STATUS-VIEW.MILITARY-STATUS = 'R' OR = 'C')
        AND (#ISERV = 1 OR = 8)
      ADD 1 TO #ISERV
    END-IF
*
    IF STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      MOVE 15 TO #ISERV
    END-IF
  END-IF
*
  DECIDE ON FIRST VALUE OF STATUS-VIEW.MILITARY-STATUS
    VALUE 'A' MOVE 1 TO #ISTAT
    VALUE 'R' MOVE 2 TO #ISTAT
    NONE      MOVE 3 TO #ISTAT
  END-DECIDE
*
  IF STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    MOVE 3 TO #ISTAT
  END-IF
*
  FOR #I1 1 TO 9
    IF   (STATUS-VIEW.PAY-STATUS (#I1) = '47')  /* Includes AL
      MOVE 4 TO #IPLAN
    ELSE
      DECIDE ON FIRST VALUE OF STATUS-VIEW.PLAN (#I1)
        VALUE 'OL ' MOVE 1 TO #IPLAN
        VALUE '20P' MOVE 2 TO #IPLAN
        VALUE '30P' MOVE 3 TO #IPLAN
        VALUE '7P ' MOVE 5 TO #IPLAN
        VALUE 'RT ' MOVE 6 TO #IPLAN
        VALUE 'LT ' MOVE 7 TO #IPLAN
        VALUE 'SO ' MOVE 8 TO #IPLAN
        VALUE 'FSO' MOVE 8 TO #IPLAN
        NONE        MOVE 8 TO #IPLAN /* LUMP FLEX AND VAR TERM WITH SO
*                                    /* Also plan AL and GPL    DW1 DW4   add GP2 YK3
      END-DECIDE
    END-IF
*
    DECIDE FOR FIRST CONDITION
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '21' MOVE 1 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '26' MOVE 2 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '31' MOVE 3 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '36' MOVE 4 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '41' MOVE 5 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '46' MOVE 6 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '51' MOVE 7 TO #IAGE
      WHEN STATUS-VIEW.ISSUE-AGE (#I1) LT '56' MOVE 8 TO #IAGE
      WHEN NONE                                MOVE 9 TO #IAGE
    END-DECIDE
*
    IF  ((STATUS-VIEW.ONLINE-ACTION-GN = '2') AND (#I1 = 1))
        OR (STATUS-VIEW.ONLINE-ACTION (#I1) = '1' OR = 'G'
        OR = 'B' OR = 'H' OR = 'Q' OR = 'T') /* DW3
      IF STATUS-VIEW.PLAN (#I1) NE 'FSO'
        ADD 1 TO #GAIN-LINES
        ADD #DEATH-BN (#I1) TO #GAIN-DB
      END-IF
    END-IF
*
    IF   (STATUS-VIEW.ONLINE-ACTION-GN = '2')         /* NEW MEMBER
        AND (#I1 = 1)                                  /* 1ST LINE
      MOVE '1' TO #RE-SECTION            /* INDICATE NEW BUS REGISTER
      MOVE '1' TO #RE-SUBSECTION         /* INDICATE NEW MEMBER
      MOVE 'NEW ' TO #RE-TYPE
      MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
      PERFORM FORMAT-EXTRACT-RECORD
      PERFORM FIND-PLAN-X
*
      ADD 1    TO #MBC-NU-POLS-TOTAL-NEW    (1)
      ADD STATUS-VIEW.ISSUE-AGE-N (#I1)
        TO #MBC-NU-POLS-TOTAL-AGE-NEW(1)
      ADD 1    TO #MBC-NU-POLS-BY-AGE-NEW   (1, #IAGE)
      ADD 1    TO #MBC-NU-POLS-BY-RANK-NEW  (1, #IRANK)
      ADD 1    TO #MBC-NU-POLS-BY-MIL-ID-NEW(1, #ISERV)
      ADD 1    TO #MBC-NU-POLS-BY-SDTQ-NEW  (1, #IP)
      ADD #FACE-AMT (#I1) TO #MBC-NU-FACE-BY-SDTQ-NEW (1, #IP)
      ADD 1    TO #MBC-NU-MBRS-ADDL-BY-PLAN (1, #IPLAN)
    ELSE
      IF STATUS-VIEW.ONLINE-ACTION (#I1) = '1' OR = 'G' /* ADDL POLICY
          OR = 'Q' OR = 'T'                            /* DW3
        MOVE '1' TO #RE-SECTION            /* INDICATE NEW BUS REGISTER
        MOVE '2' TO #RE-SUBSECTION         /* INDICATE ADDL MEMBER
        MOVE 'ADDL' TO #RE-TYPE
        MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
        PERFORM FORMAT-EXTRACT-RECORD
        PERFORM FIND-PLAN-X
*
        ADD 1   TO #MBC-NU-POLS-TOTAL-ADDL   (1)
        ADD STATUS-VIEW.ISSUE-AGE-N (#I1)
          TO #MBC-NU-POLS-TOTAL-AGE-ADDL(1)
        ADD 1   TO #MBC-NU-POLS-BY-AGE-ADDL  (1, #IAGE)
        ADD 1   TO #MBC-NU-POLS-BY-RANK-ADDL (1, #IRANK)
        ADD 1   TO #MBC-NU-POLS-BY-STAT-ADDL (1, #ISTAT)
        ADD 1   TO #MBC-NU-POLS-BY-SDTQ-ADDL (1, #IP)
        ADD #FACE-AMT (#I1) TO #MBC-NU-FACE-BY-SDTQ-ADDL (1, #IP)
        ADD 1   TO #MBC-NU-MBRS-ADDL-BY-PLAN (1, #IPLAN)
      END-IF
    END-IF
*
    IF  STATUS-VIEW.ONLINE-ACTION (#I1) = 'B' OR = 'H'   /* REINSTATE
      MOVE '1' TO #RE-SECTION            /* INDICATE NEW BUS REGISTER
      MOVE '3' TO #RE-SUBSECTION         /* INDICATE REINSTATEMENT
      MOVE 'REIN' TO #RE-TYPE
      MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
      PERFORM FORMAT-EXTRACT-RECORD
    END-IF
  END-FOR
END-IF
*
* * * * * * * * * ACCUM FOR TERM/TRANS REGISTER AND LOSS SCREEN
RESET #LOSSES
RESET #CSV-OR-LAPS
*
FOR #I1 1 TO 9
  RESET #REPT-EXTRACT        /* INITIALIZE RECORD
  RESET #ITRANS                      /* ACCUM TRANSFERS
  IF STATUS-VIEW.ONLINE-ACTION (#I1) = '7'
    MOVE 1   TO #ITRANS
    MOVE '2' TO #RE-SECTION            /* INDICATE TERM/TRANS REG
    MOVE '6' TO #RE-SUBSECTION         /* INDICATE PAID-UP
    MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
*
    IF STATUS-VIEW.PAY-STATUS (#I1) = '47'
      MOVE 'PUSP' TO #RE-TYPE
    ELSE
      IF STATUS-VIEW.PAY-STATUS (#I1) = '48'
        MOVE 'PUAP' TO #RE-TYPE
      ELSE
        MOVE 'PDUP' TO #RE-TYPE
      END-IF
    END-IF
*
    PERFORM FORMAT-EXTRACT-RECORD
  END-IF
*
  IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'A' OR = '9'
    MOVE 2   TO #ITRANS
    MOVE '2' TO #RE-SECTION            /* INDICATE TERM/TRANS REG
    MOVE '7' TO #RE-SUBSECTION         /* INDICATE RPU/ETI
    MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
*
    IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'A'
      MOVE 'RPU' TO #RE-TYPE
    ELSE
      MOVE 'ETI' TO #RE-TYPE
    END-IF
*
    PERFORM FORMAT-EXTRACT-RECORD
  END-IF
*
  IF STATUS-VIEW.ONLINE-ACTION (#I1) = '2' OR = '3' OR = '4'
    MOVE 4 TO #ITRANS
*
    MOVE '2' TO #RE-SECTION            /* INDICATE TERM/TRANS REG
    MOVE '5' TO #RE-SUBSECTION         /* INDICATE CONVERSION
    MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN     /* NEW PLAN
*               CONV NOT ALLOWED ON PLAN AL    /* DW1
*               CONV NOT ALLOWED ON PLAN EZ    /* YK2
    IF STATUS-VIEW.ONLINE-ACTION (#I1) = '2'
      MOVE 'OL' TO #RE-TYPE                     /* OLD PLAN
    ELSE
      IF STATUS-VIEW.ONLINE-ACTION (#I1) = '3'
        MOVE '20P' TO #RE-TYPE
      ELSE
        IF STATUS-VIEW.ONLINE-ACTION (#I1) = '4'
          MOVE '30P' TO #RE-TYPE
        ELSE
          IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'N'
            MOVE '7P' TO #RE-TYPE
          ELSE
            IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'O'
              MOVE 'RT' TO #RE-TYPE
            ELSE
              IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'P'
                MOVE 'LT' TO #RE-TYPE
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
    PERFORM FORMAT-EXTRACT-RECORD
  END-IF
*
  RESET #LOSS-ON-LINE
  RESET #ILOSS
*
  IF STATUS-VIEW.STATUS (#I1) = 'E'
    RESET #MATCH-ISS
    FOR #I2 1 TO 9          /* TO CATCH SPLIT CONDITIONS
      IF   (#I1 NE #I2)
          AND (STATUS-VIEW.ISSUE-DATE (#I2) = STATUS-VIEW.ISSUE-DATE(#I1))
          AND (STATUS-VIEW.STATUS (#I2) = 'D')
        MOVE TRUE TO #MATCH-ISS
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF NOT #MATCH-ISS
        AND (STATUS-VIEW.PLAN (#I1) NE 'FSO')
      ADD 1 TO #LOSS-LINES-NON-DEATH
      ADD #DEATH-BN (#I1) TO #LOSS-DB-NON-DEATH
    END-IF
*
    IF STATUS-VIEW.ONLINE-ACTION (#I1) = '5' OR = 'T' /* CSV    DW3
      MOVE 1 TO #ILOSS
      MOVE TRUE TO #LOSS-ON-LINE
      MOVE 1 TO #I2
      MOVE 2 TO #ITYPE
    ELSE
      IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'J'     /* CSV-EX
        MOVE 2 TO #ILOSS
        MOVE TRUE TO #LOSS-ON-LINE
        MOVE 2 TO #I2
        MOVE 2 TO #ITYPE
      ELSE
        IF STATUS-VIEW.ONLINE-ACTION (#I1) = '6'     /* LAPSE
          MOVE 3 TO #ILOSS
          MOVE TRUE TO #LOSS-ON-LINE
          MOVE 3 TO #I2
          MOVE 3 TO #ITYPE
        ELSE
          IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'K' OR = 'Q' /*RESIGN
            MOVE 4 TO #ILOSS
            MOVE TRUE TO #LOSS-ON-LINE
            MOVE 4 TO #I2
            MOVE 3 TO #ITYPE
          ELSE
            IF STATUS-VIEW.ONLINE-ACTION (#I1) = '8' OR = 'F'
              MOVE TRUE TO #LOSS-ON-LINE                   /* EXPIRE
              MOVE 5 TO #I2
              MOVE 4 TO #ITYPE
              MOVE 3 TO #ITRANS
            ELSE
              IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'E' OR = 'G' OR = 'H'
                IF STATUS-VIEW.PLAN (#I1) NE 'FSO'
                  ADD 1 TO #LOSS-LINES-DEATH
                  SUBTRACT 1 FROM #LOSS-LINES-NON-DEATH
                  ADD #DEATH-BN (#I1) TO #LOSS-DB-DEATH
                  SUBTRACT #DEATH-BN (#I1) FROM #LOSS-DB-NON-DEATH
                END-IF
*
                MOVE TRUE TO #LOSS-ON-LINE
                MOVE 6 TO #I2                               /* DEATH
                MOVE 1 TO #ITYPE
              ELSE
                IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'L' OR = 'S'
                  MOVE 5 TO #ILOSS
                  MOVE TRUE TO #LOSS-ON-LINE
                  MOVE 7 TO #I2                               /* LAPN
                  MOVE 3 TO #ITYPE
                ELSE
                  IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'M'
                    MOVE 6 TO #ILOSS
                    MOVE TRUE TO #LOSS-ON-LINE
                    MOVE 8 TO #I2                               /* LAPO
                    MOVE 3 TO #ITYPE
                  END-IF
                END-IF
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF #ITRANS NE 0
    ADD 1 TO #MBC-LOST-POLS-BY-TRANSFER (1, #ITRANS)
    ADD 1 TO #MBC-LOST-POLS-BY-TRANSFER (2, #ITRANS)
  END-IF
*
  IF #LOSS-ON-LINE
    MOVE TRUE TO #LOSSES
    COMPUTE #CALC-AGE = #C-RUN-DT-YYYY - OBSOLETE-STATUS-FIELDS.DOB-YYYY
*
    IF #C-RUN-DT-MM LE OBSOLETE-STATUS-FIELDS.DOB-MM
      SUBTRACT 1 FROM #CALC-AGE
    END-IF
*
    MOVE '2'       TO #RE-SECTION            /* INDICATE TERM/TRANS REG
    MOVE #CALC-AGE TO #RE-TERM-AGE           /* CALCULATED AGE
    MOVE STATUS-VIEW.PLAN (#I1) TO #RE-PLAN
*
    IF #ITYPE = 1                    /* DEATH
      MOVE '1'   TO #RE-SUBSECTION         /* INDICATE DEATH
      MOVE STATUS-VIEW.DATE-OF-DEATH TO #WORK-YYYYMMDD
      COMPRESS #WORK-MM '/' #WORK-DD '/' #WORK-YY
        INTO #RE-TERM-DATE LEAVING NO
      MOVE 'DETH' TO #RE-TYPE
      PERFORM FORMAT-EXTRACT-RECORD
    ELSE
      IF #ITYPE = 2                    /* CASH-VALUE
        MOVE '2'   TO #RE-SUBSECTION         /* INDICATE CASH-VALUE
        MOVE STATUS-VIEW.ONLINE-DATE (#I1) TO #WORK-YYYYMMDD
        COMPRESS #WORK-MM '/' #WORK-DD '/' #WORK-YY
          INTO #RE-TERM-DATE LEAVING NO
*
        IF STATUS-VIEW.ONLINE-ACTION (#I1) = '5' OR = 'T'   /* DW3
          MOVE 'CSV' TO #RE-TYPE
        ELSE
          IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'J'
            MOVE 'CVEX' TO #RE-TYPE
          END-IF
        END-IF
*
        PERFORM FORMAT-EXTRACT-RECORD
      ELSE
        IF #ITYPE = 3                    /* LAPSE/RESIGN
          MOVE '3'   TO #RE-SUBSECTION         /* INDICATE CASH-VALUE
          MOVE STATUS-VIEW.ONLINE-DATE (#I1) TO #WORK-YYYYMMDD
          COMPRESS #WORK-MM '/' #WORK-DD '/' #WORK-YY
            INTO #RE-TERM-DATE LEAVING NO
*
          IF STATUS-VIEW.ONLINE-ACTION (#I1) = '6'
            MOVE 'LAPS' TO #RE-TYPE
          ELSE
            IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'K' OR = 'Q'
              MOVE 'RSGN' TO #RE-TYPE
            ELSE
              IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'L'
                MOVE 'LAPN' TO #RE-TYPE
              ELSE
                IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'M'
                  MOVE 'LAPO' TO #RE-TYPE
                ELSE
                  IF STATUS-VIEW.ONLINE-ACTION (#I1) = 'S'
                    MOVE 'LAPC' TO #RE-TYPE
                  END-IF
                END-IF
              END-IF
            END-IF
          END-IF
*
          PERFORM FORMAT-EXTRACT-RECORD
        ELSE
          IF #ITYPE = 4                    /* EXPIRATION
            MOVE '4'   TO #RE-SUBSECTION         /* INDICATE EXPIRATION
            MOVE STATUS-VIEW.ONLINE-DATE (#I1) TO #WORK-YYYYMMDD
            COMPRESS #WORK-MM '/' #WORK-DD '/' #WORK-YY
              INTO #RE-TERM-DATE LEAVING NO
            MOVE 'EXPR' TO #RE-TYPE
            PERFORM FORMAT-EXTRACT-RECORD
          END-IF
        END-IF
      END-IF
    END-IF
*
    IF STATUS-VIEW.MILITARY-STATUS = 'A'
      MOVE 1 TO #I3
    ELSE
      IF STATUS-VIEW.MILITARY-STATUS = 'R'
        MOVE 2 TO #I3
      ELSE
        MOVE 3 TO #I3
      END-IF
    END-IF
*
    IF STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      MOVE 3 TO #I3
    END-IF
*
    IF STATUS-VIEW.MILITARY-STATUS = 'A' OR = 'R'
      IF STATUS-VIEW.RANK = 'GEN' OR = 'LTG' OR = 'MG' OR = 'BG'
          OR = 'COL' OR = 'LTC' OR = 'MAJ'
        MOVE 1 TO #IGRADE
      ELSE
        MOVE 2 TO #IGRADE
      END-IF
    ELSE
      MOVE 1 TO #IGRADE
    END-IF
*
    IF   (STATUS-VIEW.INTRO-FLAG = 'I' OR = 'B' OR = 'Q')
        AND (STATUS-VIEW.ISSUE-DATE(#I1) GE #C-RUN-DT-YYYYMMDD - 50000)
      MOVE TRUE TO #INTRO-COMP-GRADE
    ELSE
      RESET #INTRO-COMP-GRADE
    END-IF
*   below only used in ATH05F4
    DECIDE ON FIRST VALUE OF STATUS-VIEW.PLAN (#I1)
      VALUE 'OL ' MOVE 1 TO #IPLAN
      VALUE '20P' MOVE 2 TO #IPLAN
      VALUE '30P' MOVE 3 TO #IPLAN
      VALUE '7P'  MOVE 4 TO #IPLAN
      VALUE 'RT'  MOVE 5 TO #IPLAN
      VALUE 'LT'  MOVE 6 TO #IPLAN
      VALUE 'SO'  MOVE 7 TO #IPLAN
      VALUE 'FSO' MOVE 7 TO #IPLAN
      NONE        MOVE 7 TO #IPLAN /* lump FLEX & var term with SO
*                                  /* Also plan AL     DW1
*                                  /* Also plan EZ and var term AR plans   YK2
    END-DECIDE
*
    ADD 1 TO #MBC-LOSSES-UNITS (#I2, #I3)
    ADD 1 TO #MBC-LOST-POLS-BY-PLAN (2, #IPLAN)
    ADD 1 TO #MBC-LOST-POLS-BY-TYPE (2, #ITYPE)
    ADD 1 TO #MBC-LOST-POLS-TYPE-TOTAL (2)
    ADD 1 TO #MBC-LOST-POLS-TOTAL (2, #ITYPE)
*
    COMPUTE #MBC-LOST-POLS-TOTAL-AGE (2, #ITYPE) =
      #MBC-LOST-POLS-TOTAL-AGE (2, #ITYPE)
      + (#CALC-AGE * 1)
*
    IF #ILOSS NE 0
      MOVE TRUE TO #CSV-OR-LAPS
      MOVE #ILOSS TO #HOLD-ILOSS
*
      IF NOT STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
        IF STATUS-VIEW.MILITARY-SERVICE = MASK ('F')
          IF #IGRADE = 1
            ADD 1 TO #AIRF-COMP-GRADE-UNITS (#ILOSS, #I3)
          ELSE
            ADD 1 TO #AIRF-FIELD-GRADE-UNITS (#ILOSS, #I3)
          END-IF
*
          IF #INTRO-COMP-GRADE
            ADD 1 TO #AIRF-INTRO-UNITS (#ILOSS, #I3)
          END-IF
        ELSE
          IF #IGRADE = 1
            ADD 1 TO #ARMY-COMP-GRADE-UNITS (#ILOSS, #I3)
          ELSE
            ADD 1 TO #ARMY-FIELD-GRADE-UNITS (#ILOSS, #I3)
          END-IF
*
          IF #INTRO-COMP-GRADE
            ADD 1 TO #ARMY-INTRO-UNITS (#ILOSS, #I3)
          END-IF
        END-IF
      END-IF
    END-IF
*
    IF STATUS-VIEW.NUMBER-MEMBERSHIPS NE 0
      ADD 1 TO #MBC-LOSSES-PARTIAL-UNITS(#I2)
    END-IF
  END-IF
END-FOR
*
IF #LOSSES AND (STATUS-VIEW.NUMBER-MEMBERSHIPS = 0)
  ADD 1 TO #MBC-LOSSES-MBRS (#I2, #I3)
  ADD 1 TO #MBC-LOST-POLS-BY-PLAN (1, #IPLAN)
  ADD 1 TO #MBC-LOST-POLS-BY-TYPE (1, #ITYPE)
  ADD 1 TO #MBC-LOST-POLS-TOTAL   (1, #ITYPE)
  ADD 1 TO #MBC-LOST-POLS-TYPE-TOTAL (1)
  COMPUTE #MBC-LOST-POLS-TOTAL-AGE (1, #ITYPE) =
    #MBC-LOST-POLS-TOTAL-AGE (1, #ITYPE) + #CALC-AGE
*
  IF #CSV-OR-LAPS
    MOVE #HOLD-ILOSS TO #ILOSS
    IF NOT STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      IF STATUS-VIEW.MILITARY-SERVICE = MASK ('F')
        IF #IGRADE = 1
          ADD 1 TO #AIRF-COMP-GRADE-MBRS (#ILOSS, #I3)
        ELSE
          ADD 1 TO #AIRF-FIELD-GRADE-MBRS (#ILOSS, #I3)
        END-IF
*
        IF #INTRO-COMP-GRADE
          ADD 1 TO #AIRF-INTRO-MBRS (#ILOSS, #I3)
        END-IF
      ELSE
        IF #IGRADE = 1
          ADD 1 TO #ARMY-COMP-GRADE-MBRS (#ILOSS, #I3)
        ELSE
          ADD 1 TO #ARMY-FIELD-GRADE-MBRS (#ILOSS, #I3)
        END-IF
*
        IF #INTRO-COMP-GRADE
          ADD 1 TO #ARMY-INTRO-MBRS (#ILOSS, #I3)
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE  /* (0191)
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-EXTRACT-RECORD
* * * * * * * * * * * * * * * * * * * *
*
IF ((STATUS-VIEW.ONLINE-ACTION-GN = '2') AND (#I1 = 1))
    OR (STATUS-VIEW.ONLINE-ACTION (#I1) = '1' OR = 'G'
    OR = 'Q' OR = 'T')    /* DW3
  MOVE STATUS-VIEW.ID-NUMBER            TO #RE-ID-NUMBER-1
*
  move status-view.member-contact-id to #contact-id #contact-id-r                           /* vt1 \/
  #name-f := FUNCTION-FRM-NAME(<#CONTACT-ID,#FIRST-NAME,#MIDDLE-NAME,#LAST-NAME,#SUFFIX>)
  compress #first-name #middle-name #last-name to #name
  move #name                            to #re-name-2
* MOVE STATUS-VIEW.NAME                 TO #RE-NAME-2                                       /* vt1 /\
*
  move #name                            to #re-name-1
*  MOVE STATUS-VIEW.NAME                 TO #RE-NAME-1                                      /* vt1 /\
*
  MOVE STATUS-VIEW.ID-NUMBER            TO #RE-ID-NUMBER-2
END-IF
*
callnat 'RANKN001' #pda-rank                                                        /* vt1 \/
move #rank-abbr                         to #re-rank
* MOVE STATUS-VIEW.RANK                 TO #RE-RANK                                 /* vt1 /\
MOVE STATUS-VIEW.MILITARY-STATUS      TO #RE-STAT
MOVE STATUS-VIEW.MILITARY-SERVICE     TO #RE-SERV
MOVE STATUS-VIEW.MODE (#I1)           TO #RE-MODE
MOVE 1                                TO #RE-UNITS-GAIN-LOSS
MOVE #TOT-LINES                       TO #RE-CURR-UNITS
MOVE STATUS-VIEW.ISSUE-AGE    (#I1)   TO #RE-ISS-AGE
MOVE STATUS-VIEW.ISSUE-DATE   (#I1)   TO #WORK-YYYYMMDD
COMPRESS #WORK-MM '/' #WORK-DD '/' #WORK-YY INTO #RE-ISS-DATE LEAVING NO
*
WRITE WORK 5 #REPT-EXTRACT
*
END-SUBROUTINE /* (0965)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-FACE-X
* * * * * * * * * * * * * * * *
IF #TOT-FACE LE  25000 MOVE 1  TO #FACE-X ELSE
  IF #TOT-FACE LE  50000 MOVE 2  TO #FACE-X ELSE
    IF #TOT-FACE LE  75000 MOVE 3  TO #FACE-X ELSE
      IF #TOT-FACE LE 100000 MOVE 4  TO #FACE-X ELSE
        IF #TOT-FACE LE 125000 MOVE 5  TO #FACE-X ELSE
          IF #TOT-FACE LE 150000 MOVE 6  TO #FACE-X ELSE
            IF #TOT-FACE LE 175000 MOVE 7  TO #FACE-X ELSE
              IF #TOT-FACE LE 200000 MOVE 8  TO #FACE-X ELSE
                IF #TOT-FACE LE 225000 MOVE 9  TO #FACE-X ELSE
                  MOVE 10 TO #FACE-X
                END-IF END-IF END-IF END-IF END-IF END-IF END-IF END-IF END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-PLAN-X
* * * * * * * * * * * * * * *
MOVE PLAN (#I1) TO #PLAN
* IF #PLAN = 'OL'  MOVE 1 TO #IP END-IF
* IF #PLAN = '20P' MOVE 2 TO #IP END-IF
* IF #PLAN = '30P' MOVE 3 TO #IP END-IF
* IF #PLAN = '7P'  MOVE 4 TO #IP END-IF
* IF #PLAN = 'RT'  MOVE 5 TO #IP END-IF
* IF #PLAN = 'LT'  MOVE 6 TO #IP END-IF
* IF #PLAN = 'SO'  MOVE 7 TO #IP END-IF
* IF #PLAN = 'FSO' MOVE 7 TO #IP END-IF
* IF #PLAN = MASK (..'F') MOVE 7 TO #IP END-IF /* LUMP FLEX WITH SO
* IF #PLAN = MASK (.'F'.) MOVE 7 TO #IP END-IF /* LUMP FLEX WITH SO
* IF (#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z')
*   MOVE 7 TO #IP END-IF /* VAR-TERM WITH SO
* IF #PLAN = 'AL'  MOVE 7 TO #IP END-IF   /* AL with SO   DW1
* IF #PLAN = 'GPL'  MOVE 7 TO #IP END-IF /* Combined w AL but I think not used DW4
* YK1 Start
DECIDE FOR FIRST CONDITION
  WHEN #PLAN = 'OL'  #IP := 1
  WHEN #PLAN = '20P' #IP := 2
  WHEN #PLAN = '30P' #IP := 3
  WHEN #PLAN = '7P'  #IP := 4
  WHEN #PLAN = 'RT'  #IP := 5
  WHEN #PLAN = 'LT'  #IP := 6                                              
  WHEN #PLAN = 'SO' OR = 'FSO' OR = MASK (..'F') OR = MASK (.'F'.) #IP := 7
  WHEN #PLAN = 'EZ'  #IP := 8                           /* YK2
  WHEN (F-LT2PLUS-TRUE(<#PLAN>))   #IP := 9             /* YK1
  WHEN ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))  #IP := 7             /* YK1
* WHEN #PLAN = 'AL'  #IP := 7
  WHEN IS-ANNUITY-LIFE (< #PLAN >) #IP := 7   /* NG2
  WHEN #PLAN = 'GPL' #IP := 7
  WHEN #PLAN = 'GP2' #IP := 7                           /* YK3
  when #plan = 'GT1' #ip := 7                /* vt2 
  WHEN NONE IGNORE
END-DECIDE
* YK1 End
END-SUBROUTINE
*
END
