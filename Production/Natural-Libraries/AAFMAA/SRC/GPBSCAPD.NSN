* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*********************************************************************
* NAME         : GPBSCAPD                                           *
* AUTHOR       : Rolly Estabillo                                    *
* DESCRIPTION  : Get CAP Loan Details                               *
*********************************************************************
*                       MODIFICATION LOG                            *
* USER   DATE      TAG   REASON                                     *
* RSE   01242012   None  Program Written                            *
*********************************************************************
DEFINE DATA 
PARAMETER USING GPBACAPD
LOCAL
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 INTRO-FLAG
  2 CAP-BILL-DATE
  2 STATUS(9)
  2 CONV-DURATION(1:9)
  2 ISSUE-DATE(1:9)
  2 REDEFINE ISSUE-DATE /* BEGIN REDEFINE: ISSUE-DATE
    3 ISS-DT-RED-2 (1:9)
      4 ISS-DT-YYYY (N4.0)
      4 ISS-DT-MM (N2.0)
      4 ISS-DT-DD-2 (N2.0)
  2 REDEFINE ISSUE-DATE /* REDEF. BEGIN : ISSUE-DATE
    3 ISS-DT-RED (1:9)
      4 ISS-DT-YYYYMM (N6.0)
      4 ISS-DT-DD (N2.0)
  2 PAID-TO-DATE(1:9)
  2 FACE-AMOUNT(1:9)
  2 MONTH-ALLOC-AMT(1:9)
  2 MODE(1:9)
  2 ANNUAL-PREM(1:9)
  2 CAP-ENTRY-DATE
  2 REFUND-TO-CAP
  2 X-INTEREST-RECEIVED(1:9)
  2 X-ALT-LP-AMT(1:9)
  2 X-ALT-LP-IND(1:9)
  2 X-LOAN-AMOUNT(1:9)
  2 X-LOAN-DATE(1:9)
  2 X-INTEREST-RATE(1:9)
*
1 NT-V VIEW OF A-NOTES
  2 NOTE-TEXT(60)
*
1 #I          (I2)
1 #I1         (I2)
1 #SUFFIX     (A3)
1 REDEFINE #SUFFIX
  2 #SUFFIX-2   (A2)
  2 #SUFFIX-X   (N1)
1 #AS-OF-DT   (N8)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #MODAL-PREM (N7.2)
1 #FACE-AMT    (N7.2/9)   
1 #TPP         (N7.2/9)
1 #DAILY-INTEREST(N3.7)                         
1 #WYYYYMMDD
  2 #WYYYY-AA  (A4)
  2 #WMM-AA    (A2)
  2 #WDD-AA    (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N  (N8.0)
1 #WMMDDYYYY
  2 #WMM   (N2)
  2 #WDD   (N2)
  2 #WYYYY (N4)
1 REDEFINE #WMMDDYYYY
  2 #WMMDDYYYY-N (N8)
1 REDEFINE #WMMDDYYYY
  2 #WMM-A   (A2)
  2 #WDD-A   (A2)
  2 #WYYYY-A (A4)
1 #YYYYMMDD (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY  (A4)
    3 REDEFINE #YYYY
      4 #YYYY-N(N4)
    3 #MM    (A2)
    3 REDEFINE #MM
      4 #MM-N(N2)
  2 #DD      (A2)
  2 REDEFINE #DD
    3 #DD-N  (N2)
1 #POL-NUMBER (A16)
*
END-DEFINE
*
FIND ST-V WITH ID-NUMBER = #ID-NUMBER
  FOR #I1 1 TO 9
    IF (STATUS(#I1) = 'D')
      AND (CONV-DURATION (#I1) = 999)
*      AND (X-LOAN-AMOUNT (#I1) NE 0)
*
      CALLNAT 'GPLNMSXX' #POL-NUMBER ST-V.ID-NUMBER #I1 /* build POL-#
      COMPRESS 'Policy Number       :' #POL-NUMBER INTO #CAP-INFO(1)
*
      MOVE ISSUE-DATE(#I1) TO #WYYYYMMDD-N
      COMPRESS #WMM-AA '/' #WDD-AA '/' #WYYYY-AA INTO #CAP-INFO(2) LEAVING NO
      COMPRESS 'Issue Date          :' #CAP-INFO(2) INTO #CAP-INFO(2)         
*
      PERFORM GET-PAID-THRU
*
      MOVE EDITED FACE-AMOUNT(#I1) (EM=Z,ZZZ,ZZ9.99) TO #CAP-INFO(4)
      MOVE LEFT #CAP-INFO(4) TO #CAP-INFO(4)
      COMPRESS 'Face Amount         :' #CAP-INFO(4) INTO #CAP-INFO(4)
*
      MOVE EDITED ANNUAL-PREM(#I1)  (EM=ZZ,ZZ9.99) TO #CAP-INFO(5)
      MOVE LEFT #CAP-INFO(5) TO #CAP-INFO(5)
      COMPRESS 'Annual Premium      :' #CAP-INFO(5) INTO #CAP-INFO(5)
*
      PERFORM GET-MODAL-PREM
      MOVE LEFT #CAP-INFO(6) TO #CAP-INFO(6)
      COMPRESS 'Modal Premium       :' MODE(#I1) #CAP-INFO(6) INTO #CAP-INFO(6)
*
      PERFORM GET-NOTE
      COMPRESS 'Note                :' #CAP-INFO(7) INTO #CAP-INFO(7)
*
      MOVE EDITED X-INTEREST-RATE(#I1)  (EM=Z9.999) TO #CAP-INFO(8)
      MOVE LEFT #CAP-INFO(8) TO #CAP-INFO(8)
      COMPRESS 'CAP Interest Rate   :' #CAP-INFO(8) INTO #CAP-INFO(8)
*
      COMPRESS 'Refund to CAP       :' REFUND-TO-CAP INTO #CAP-INFO(9)
*
*      MOVE ISSUE-DATE(#I1) TO #WYYYYMMDD-N
*      COMPRESS #WMM-AA '/' #WDD-AA '/' #WYYYY-AA INTO #CAP-INFO(10) LEAVING NO
*      COMPRESS 'CAP Issue Date      :' #CAP-INFO(10) INTO #CAP-INFO(10)
*
      MOVE X-LOAN-DATE(#I1) TO #WYYYYMMDD-N
      COMPRESS #WMM-AA '/' #WDD-AA '/' #WYYYY-AA INTO #CAP-INFO(10) LEAVING NO
      COMPRESS 'Loan Date           :' #CAP-INFO(10) INTO #CAP-INFO(10)
*
      MOVE CAP-BILL-DATE TO #WYYYYMMDD-N
      COMPRESS #WMM-AA '/' #WDD-AA '/' #WYYYY-AA INTO #CAP-INFO(11) LEAVING NO
      COMPRESS 'CAP Bill Date       :' #CAP-INFO(11) INTO #CAP-INFO(11)
*
      MOVE EDITED X-LOAN-AMOUNT(#I1) (EM=ZZ,ZZ9.99) TO #CAP-INFO(12)
      MOVE LEFT #CAP-INFO(12) TO #CAP-INFO(12)
      COMPRESS 'Loan Amount         :' #CAP-INFO(12) INTO #CAP-INFO(12)
*
      PERFORM GET-ACC-INT
      MOVE LEFT #CAP-INFO(13) TO #CAP-INFO(13)
      COMPRESS 'Accumulated Interest:' #CAP-INFO(13) INTO #CAP-INFO(13)
*
      COMPUTE ROUNDED #DAILY-INTEREST =
        X-LOAN-AMOUNT (#I1) * (X-INTEREST-RATE (#I1) / 365) / 100  
      MOVE EDITED #DAILY-INTEREST (EM=ZZ9.9999) TO #CAP-INFO(14)
      MOVE LEFT #CAP-INFO(14) TO #CAP-INFO(14)
      COMPRESS 'Daily Interest      :' #CAP-INFO(14) INTO #CAP-INFO(14)
*
      MOVE EDITED X-INTEREST-RECEIVED(#I1) (EM=ZZ,ZZ9.99) TO #CAP-INFO(15)
      MOVE LEFT #CAP-INFO(15) TO #CAP-INFO(15)
      COMPRESS 'Interest Received   :' #CAP-INFO(15) INTO #CAP-INFO(15)
*
      PERFORM GET-AUTO-LOAN-IND  
      COMPRESS 'Auto Loan Indicator :' #CAP-INFO(16) INTO #CAP-INFO(16)
*
      MOVE EDITED X-ALT-LP-AMT(#I1) (EM=ZZ,ZZ9.99) TO #CAP-INFO(17)
      MOVE LEFT #CAP-INFO(17) TO #CAP-INFO(17)
      COMPRESS 'Auto Loan Amount    :' #CAP-INFO(17) INTO #CAP-INFO(17)
*
    END-IF
  END-FOR
END-FIND
*******************************
DEFINE SUBROUTINE GET-PAID-THRU
*******************************
MOVE PAID-TO-DATE(#I1) TO #YYYYMMDD
IF  (ISSUE-DATE (#I1) LT 19970101)    /* old or new
    OR (MONTH-ALLOC-AMT (#I1) = 999.99)  /*   processing
    OR (ISS-DT-DD (#I1) = 1)    /* NXT PROCESSING WITH ISS DAY = 1
  IF #MM = '02'
    MOVE 28 TO #DD
  ELSE
    IF #MM = '04' OR = '06' OR = '09' OR = '11'
      MOVE 30 TO #DD
    ELSE
      MOVE 31 TO #DD
    END-IF
  END-IF
ELSE
  IF ((ISS-DT-MM (#I1) = 2)
      AND (ISS-DT-DD (#I1) = 28 OR = 29))  /* LAST DAY OF MONTH
      OR ((ISS-DT-MM (#I1) = 4 OR = 6 OR = 9 OR = 11)
      AND (ISS-DT-DD (#I1) = 30))
      OR ((ISS-DT-MM (#I1) = 1 OR = 3 OR = 5 OR = 7 OR = 8 OR = 10
      OR = 12) AND (ISS-DT-DD (#I1) = 31))
    IF #MM = '02'
      MOVE 27 TO #DD /* nxt to last day of month
    ELSE
      IF #MM = '04' OR = '06' OR = '09' OR = '11'
        MOVE 29 TO #DD
      ELSE
        MOVE 30 TO #DD
      END-IF
    END-IF
  ELSE
    COMPUTE #DD = ISS-DT-DD (#I1) - 1
*
    IF (#MM = '02') AND (#DD-N GE 27)
      MOVE 26 TO #DD /* nxt to nxt to last day of month
    END-IF
  END-IF
END-IF
*
COMPRESS #MM '/' #DD '/' #YYYY INTO #CAP-INFO(3) LEAVING NO
COMPRESS 'Paid Thru           :' #CAP-INFO(3) INTO #CAP-INFO(3)
*
END-SUBROUTINE
********************************
DEFINE SUBROUTINE GET-MODAL-PREM
********************************
IF MODE (#I1) = 'A' OR = '1'  
  MOVE ANNUAL-PREM (#I1) TO #MODAL-PREM
ELSE
  IF MODE (#I1) = 'S' OR = '2'  
    COMPUTE #MODAL-PREM = ANNUAL-PREM (#I1) / 2
  ELSE
    IF MODE (#I1) = 'Q' OR  = '4'  
      COMPUTE #MODAL-PREM = ANNUAL-PREM (#I1) / 4
    ELSE
      COMPUTE #MODAL-PREM = ANNUAL-PREM (#I1) / 12
    END-IF
  END-IF
END-IF
MOVE EDITED #MODAL-PREM (EM=ZZ,ZZ9.99) TO #CAP-INFO(6)
*
END-SUBROUTINE
**************************
DEFINE SUBROUTINE GET-NOTE
**************************
IF ST-V.INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q'
  MOVE CAP-ENTRY-DATE TO #YYYYMM
*
  IF #YYYYMM GT 199612 /* nm was done in previous mon after 12/1996
    ADD 1 TO #MM-N
*
    IF #MM-N GT 12
      SUBTRACT 12 FROM #MM-N
      ADD 1 TO #YYYY-N
    END-IF
  END-IF
*
  COMPRESS #MM '/' #YYYY INTO #CAP-INFO(7) LEAVING NO
  COMPRESS '*C.A.P. Mbr as of' #CAP-INFO(7) INTO #CAP-INFO(7)
END-IF
*
END-SUBROUTINE
*****************************
DEFINE SUBROUTINE GET-ACC-INT
*****************************
MOVE ST-V.FACE-AMOUNT (*) TO #FACE-AMT (*)  
MOVE *DATN TO #AS-OF-DT
MOVE 'NTP' TO #SUFFIX  
CALLNAT 'CVALUSXX'
  ST-V.ID-NUMBER
  #SUFFIX
  #AS-OF-DT
  #LOAN-BAL   (1:9)
  #LOAN-BAL-2 (1:9)
  #ACC-INT  (1:9)
  #ACC-INT-2(1:9)
  #CSV        (1:9)
  #CVALUE     (1:9)
  #LVALUE     (1:9)
  #MCSV       (1:9)
  #DB         (1:9)
  #ANN-PREMIUM  (1:9)
  #FACE-AMT   (1:9)    
  #TPP        (1:9) 
MOVE EDITED #ACC-INT(#I1)  (EM=ZZ,ZZ9.99) TO #CAP-INFO(13)
*
END-SUBROUTINE
************** 
***********************************
DEFINE SUBROUTINE GET-AUTO-LOAN-IND
***********************************
DECIDE ON FIRST VALUE X-ALT-LP-IND   (#I1)
  VALUE '1' MOVE 'ALP' TO #CAP-INFO(16)
  VALUE '2' MOVE 'AIP' TO #CAP-INFO(16)
  VALUE '3' MOVE 'ELP' TO #CAP-INFO(16)
  VALUE '4' MOVE 'EIP' TO #CAP-INFO(16)
  VALUE '6' MOVE 'MIP' TO #CAP-INFO(16)
  VALUE '8' MOVE 'CIP' TO #CAP-INFO(16)  
  VALUE '9' MOVE 'AIW' TO #CAP-INFO(16)   
  VALUE 'L'
    MOVE 'AIL' TO #CAP-INFO(16)  /* CAP Pending Loan  
  VALUE '*'                                              
    MOVE 'AI*' TO #CAP-INFO(16)  /* CAP Pending Loan after ATHPIALA
  VALUE 'E' 
    MOVE 'EIL' TO #CAP-INFO(16)
  VALUE 'C'                                              
    MOVE 'CIL' TO #CAP-INFO(16)                 
  VALUE '!'                                              
    MOVE 'CI*' TO #CAP-INFO(16)                 
  NONE IGNORE
END-DECIDE
*
END-SUBROUTINE
*
END
