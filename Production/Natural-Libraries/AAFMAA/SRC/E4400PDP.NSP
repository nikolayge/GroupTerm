* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* ***********************************************************
* PGM-ID: E4400PDP
*     'DP' COMMAND PROCESSING
*
*  MODIFICATIONS:
*
*  0589  ADD EDIT FOR AFBA INSURANCE ISSUE DATE  PER DAIL   JAK
*  1089  ADD DISABILITY PCT FOR SGLI EDIT        PER WALTON JAK
*  1189  GIVE SGLI TO ANY ACTIVE MEMBER WITHOUT IT   WALTON DRW
*  0590  FIX INDEX ON TRLOG PROBLEM                         JAK
*  0690  ADD NSLI PROCESSING                                DRW
*  0990  ADD POL ENTRIES UP TO 45                           DRW
* 0999  ADD EDITS FOR POL TYPE FOR ALL OTHER POLICIES       JAK
* 082004 FIX AFSC WIDOW CLIENTS 1ST FIRS REC BUILT BY DP    DRW
* 012005 MAKE CSR BEGIN AT CMD LINE PER JMJ                 TDM
* 012005 MAKE CSR BEGIN AT CMD LINE PER JMJ                 TDM
*
* DRW 05102005 DW1 Call CVALUSXX to get amounts
* DRW 06132005 DW2 ADD SERV-CONN-IND TO E9010SDX LINKAGE
* DRW 06142005 DW3 Use DOD instead of DATN if dead for ins calcs
* TDM 06152005 TM1 ADDED DOCS TEXT MSG FOR AFSC ONLY APPL SRCE 'TU'
* DRW 04142006 DW4 ADD AFSC/AAFMAA SYNC PROCESSING for POL CODE
* DRW 10022006 DW5 Allow up to 400,000 SGLI
* DRW 05302007 DW6 Only Display AAFMAA policies
* DRW 03/13/2009 DW7 SAS Unification
* DRW 09/17/2009 DW8 Process-Ind Changes
* RSE 01/04/2010 RE1 Allow IAS users to see non-AAFMAA ins policies;
*                    Prevent SAS users from updating AAFMAA policy code
* PAM 04/20/2010 PM9 Adjust to stop extra STORE process
* RSE 05/25/2010 RE2 Display DM, DC, DD, and SU dates for SAS user.
* RSE 07/22/2010     No tag. Add PERS-STAT field on map E4400MDP.
* NGG 12/03/2010 NG2 Security for selected users
* RSE 03/12/2011 RE3 Add logic to process add'l CNs of members with > 9 policies
* PAM 03/15/2011 PM10 Added location-code update for CN's of members with > 9 policies
* RSE 03/22/2011 RE4 Fix bug that prevents SAS users from entering on page 2
* YAK 07/25/2011 YK1 Add Dropdown Box/Help for External Company names
*                    Populate PolicyID when saving
* bz  09/23/2011 bz1 Add member CI to FIR
* vxt 04/12/2012 vt1 Remove protection for update of non-AAFMAA policies
*                    for degrandfathered members/ticket# 1060/.
* bz2 9/21/2012      (OI-960)
* TMT 06/12/2014 TT1 Contact Normalization - Gender and DOB
* bz  6/17/2014      (OI-2864)
* *********************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING E4400LDP
LOCAL
*
1 STAT-V VIEW OF A-STATUS       /* RE3 Used by the add'l CN's of members with  9 policies
  2 ID-NUMBER
  2 ALOT-SW                                   /* PM10
  2 ACTIVE-SEGMENTS (1:9) /* PERIODIC GROUP
    3 STATUS (A1)
    3 PLAN (A3)
    3 PAY-STATUS (A2)
    3 FIRS-LOCATION-CODE (A1)
    3 CONVERSION-FLAG (A1) /*
    3 CONV-DURATION (P3.0)
    3 ONLINE-ACTION (A1) /*
    3 SPLIT-IND (P1.0)
    3 ISSUE-DATE (N8.0)
    3 FACE-AMOUNT (P7.2)
    3 DEATH-BENEFIT (P7.2) /*
    3 NXT-MON-DEATH-BENEFIT (P7.2) /*
    3 LAST-ANN-CASH-VALUE (P7.2)
    3 MONTH-ALLOC-AMT (P7.2)             /*TT1
    3 BEG-CASH-VALUE (P7.2)
  2 C*ACTIVE-SEGMENTS
  2 RESTRICT-MBR-SW
*
1 SEC-FIRS-V VIEW OF A-FIRS
  2 ID-NUMBER (N6.0)
  2 MEMBER-FLAG (A1)
  2 WIDOW-FLAG (A1)
  2 DTS-DATN (P9.0)
  2 DTS-TIMN (P7.0)
  2 INS-LAST-FM-DATE (N8.0)
  2 DP-LAST-FM-DATE (N8.0)
  2 DOCU-LAST-FM-DATE (N8.0)
  2 POL-CODE (A1/1:45)
  2 POL-NAME (A19/1:45)
  2 POL-NUMBER (A14/1:45)
  2 POL-TYPE (A4/1:45)
  2 POL-REMARKS (A21/1:45)
  2 POL-AMOUNT (P7.0/1:45)
  2 POL-DATE (N8.0/1:45)
*
1 #SFX        (I2)           /* DW4
1 #TR-POL-CODE-AREA  (A125)  /* DW4
1 REDEFINE #TR-POL-CODE-AREA
  2 #TR-POL-CODE         (A1)
  2 #TR-POL-CODE-FILL1   (A1)
  2 #TR-POL-CODE-ISS-DT  (N8)
  2 #TR-POL-CODE-FILL2   (A1)
  2 #TR-POL-CODE-SPL-IND (N1)
  2 #TR-POL-CODE-FILL3   (A1)
  2 #TR-POL-CODE-PLAN    (A3)
*
1 #SUFFIX     (A3)   /* DW1
1 REDEFINE #SUFFIX
  2 #SUFFIX-2   (A2)
  2 #SUFFIX-X   (N1)
1 #AS-OF-DT   (N8)
1 #LOAN-BAL   (N7.2/27)  /* RE3 START from 9 to 27
1 #LOAN-BAL-2 (N7.2/27)
1 #ACC-INT    (N7.2/27)
1 #ACC-INT-2  (N7.2/27)
1 #CSV        (N7.2/27)
1 #CVALUE     (N7.2/27)
1 #LVALUE     (N7.2/27)
1 #MCSV       (N7.2/27)
1 #DB         (N7.2/27)
1 #ANN-PREMIUM (N7.2/27)
1 #FACE-AMT    (N7.2/27)
1 #TPP         (N7.2/27)
1 #CSV-P      (P7.2/27)  /* RE3 END
*
1 #TEXT-40   (A40)
1 #SCR-N-MSG (A35) /*   'code n means member not the owner'
1 #MBR-LBL   (A6)
1 #RANK      (A3)
1 #NAME      (A25)
1 #SSN       (N9)
* 1 #DP-ON-AFSC   (L)    /* ACCESSING AAFMAA INS IN AN AFSC DI   /* DW8
1 #DP-ON-AFSC-ISN (P8)   /* ISN OF AAFMAA RECORD
1 #ISN            (P8)
1 #TOT-LOAN  (P6.2)
1 #INVALID             (L)
1 #ASSUMED-SGLI        (L)
1 #Y-N                 (A1)
1 #HLD-POL-CODE        (A1/15)
1 #HLD-POL-NAME        (A19/15)
1 #HLD-POL-NUMBER      (A14/15)
1 #HLD-POL-TYPE        (A4/15)
1 #HLD-POL-REMARKS     (A21/15)
1 #HLD-POL-AMOUNT      (N7/15)
1 #HLD-POL-DATE        (A5/15)
1 #TEXT                (A75)
1 #ERR-MSG             (A60)
1 #FIELD-CHANGED (L)
1 #POL-CODE-CHANGED (L) INIT<FALSE>
1 #POL-CODE-UPDATED (L) INIT<FALSE>
1 #OVER-AGE      (L)
1 #NO-SGLI       (L)
1 #I1            (I2)
1 #NO-FIRS       (L)
1 #NO-SEG        (L)
1 #ERROR-FLAG    (L)
1 #AMAA-CHANGED  (L)
1 #CADET-FLAG    (A1)
1 #AGE      (P3)
1 #HOLD-TOTAL-AMOUNT (P7)
1 #FULL-NSLI-AMT     (P7)
1 #NSLI-REMAINDER    (P5)
1 #HAI-LIMIT  (N2)
1 #HOLD-DATN  (N8)
1 #HOLD-TIMN  (N7)
1 #HOLD-POL-PASS-AMOUNT (P7)
1 #HOLD-AMAA-INFO
  2 #HAI-FIRS-CODE     (A1/27)      /* RE3 start from 9 to 27
  2 #HAI-SPLIT-IND     (P1/27)
  2 #HAI-ISSUE-DATE    (P8/27)
  2 #HAI-NEW-FIRS-CODE (A1/27)
  2 #HAI-REAL-I2       (I2/27)  /* DW4 RE3 end
  2 #HAI-ALOT-SW       (A1)    /* DW4
1 #INDEXHAI            (I2)
1 #INSUR-TABLE
  2 #HOLD-POL-TYPE    (A4/1:40)
  2 #HOLD-POL-AMOUNT  (N7/1:40)
  2 #HOLD-POL-REMARKS (A21/1:40)
  2 #HOLD-POL-NAME    (A19/1:40)
  2 #HOLD-POL-NUMBER  (A14/1:40)
  2 #HOLD-POL-DATE    (N8/1:40)
  2 #HOLD-POL-CODE    (A1/1:40)
1 #REFORMAT-DATE (A5)
1 REDEFINE #REFORMAT-DATE
  2 #RD-MM        (A2)
  2 #RD-YY        (A2)
  2 #RD-SP        (A1)
1 #HOLD-SCN-POL-DATE (A5)
1 REDEFINE #HOLD-SCN-POL-DATE  /*FORMAT MM/YY
  2 #HSPD-MM           (A2)
  2 REDEFINE #HSPD-MM
    3 #HSPD-MM-N          (N2)
  2 #HSPD-SLASH         (A1)
  2 #HSPD-YY            (A2)
  2 REDEFINE #HSPD-YY
    3 #HSPD-YY-N          (N2)
1 #HOLD-CONVERT-POL-DATE (N8)
1 REDEFINE #HOLD-CONVERT-POL-DATE
  2 #HCPD-CC               (A2)
  2 #HCPD-YY               (A2)
  2 #HCPD-MM               (A2)
  2 #HCPD-DD               (A2)
1 #SUP-MESSAGE (A70)
1 #HOLD-TEMP-AMT (A10)
1 #WORK-TEMP-AMT (N6.2)
*
1 #P-DX-PARMS
  2 #PDX-DATE-OF-BIRTH   (N8)
  2 #PDX-DATE-RETIRED    (N8)
  2 #PDX-MILITARY-STATUS (A1)
  2 #PDX-MILITARY-SERVICE (A3)
  2 #PDX-MILITARY-RANK   (A3)
  2 #PDX-ACTIVE-DUTY-BASE-DATE    (N8)
1 #PDX-DISABILITY-PCT    (P1.2)
1 #W-CURRENT-DATE (N8)
1 REDEFINE #W-CURRENT-DATE
  2 #WCD-YYYY       (N4)
  2 #WCD-MMDD       (N4)
1 REDEFINE #W-CURRENT-DATE
  2 #WCD-CENT (A2)
  2 #WCD-YY   (A2)
  2 #WCD-MM   (A2)
  2 #WCD-DD   (A2)
1 #INDEXT (I2)
1 #INDEXP (I2)
1 #INDEXR (I2)
1 #INDEXS (I2)
1 #INDEXH (I2)
1 #HOLD-INDEXH (I2)
1 #INDEXV (I2)
1 #NUM-OF-LINES (N2) INIT<0>
1 #HOLD-FIR-ISN (P8)
1 #HOLD-STATUS  (A1)
*
1 #WYYYYMMDD
  2 #WYYYY    (N4)
  2 REDEFINE #WYYYY
    3 #WYYYY-A (A4)
  2 #WMM      (N2)
  2 #WDD      (N2)
  2 REDEFINE #WDD
    3 #WDD-A   (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N (N8)
*
1 #W-DATE-EDITED (A11)
1 #MONTHS (A36)  INIT
  <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
*
1 #FIND-LINK    (N6)
1 REDEFINE #FIND-LINK
  2 #FIND-LINK-A  (A6)
*
1 #LINKED-CN (N6/10)
1 REDEFINE #LINKED-CN
  2 #LINKED-CN-A (A6/10)
*
1 #ID            (N6)
1 REDEFINE #ID
  2 #ID-A      (A6)
*
1 #DM-TEXT (A2)
1 #DD-TEXT (A2)
1 #DC-TEXT (A2)
1 #DP-TEXT (A2)
1 #SU-TEXT (A2)
*
1 #REGULAR-USER   (L)        /* NG1
1 #SECONDARY-CN (N6/25)      /* RE3  START
1 #ID-NUMBER    (N6)
1 #J       (I2)
1 #K       (I2)
1 #L       (I2)
1 #M       (I2)
1 #START   (I2) INIT<1>
1 #END     (I2) INIT<9>
1 #SAVE-POL-CODE (A1/15)
1 #SAVE-POL-NAME (A19/15)
1 #SAVE-POL-NUMBER (A14/15)
1 #SAVE-POL-TYPE (A4/15)
1 #SAVE-POL-REMARKS (A21/15)
1 #SAVE-POL-AMOUNT (N7/15)
1 REDEFINE #SAVE-POL-AMOUNT /* REDEF. BEGIN : #SAVE-POL-AMOUN
  2 #SAVE-POL-AMOUNT-A (A7/15)
1 #SAVE-POL-DATE (A5/15)
1 #PLAN    (A3)
1 #DONE    (L)
1 #FIRST-TIME(L) INIT<TRUE>
1 #WITH-SGLI (L)
1 #3SR       (L)
1 #POLICY-NUMBER  (A16)      /* RE3 END
1 DATE-OF-BIRTH   (N8.0)     /* TT1
END-DEFINE
* - - - - - - - - -
INCLUDE GONERCXX
* - - - - - - - - -
*
SET KEY PF1 = HELP                             /* YK1
* WRITE 'top of E4400PDP'
GET STATUS-VIEW #CGA-ISN
MOVE STATUS-VIEW.ALOT-SW TO #HAI-ALOT-SW               /* DW4
*
IF  STATUS-VIEW.WIDOW-FLAG = 'Y'
  FETCH 'W5300PWP'
END-IF
*
IF #CGA-CLERK = 'ACN'       /* NG1
  RESET #REGULAR-USER
ELSE
  #REGULAR-USER := TRUE
END-IF
*
RESET DATE-OF-BIRTH                                                          /* TT1
DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<STATUS-VIEW.MEMBER-CONTACT-ID>)          /* TT1
*
MOVE 'MEMBER' TO #MBR-LBL
MOVE RANK TO #RANK
MOVE NAME TO #NAME
MOVE DATE-OF-BIRTH TO #WYYYYMMDD-N
PERFORM DATE-REFORMAT
MOVE #W-DATE-EDITED TO #SCN-DOB
MOVE DATE-RETIRED TO #WYYYYMMDD-N
PERFORM DATE-REFORMAT
MOVE #W-DATE-EDITED TO #SCN-RETD
MOVE SSN            TO #SSN
*
FOR #I1 1 TO 10
  IF LINKED-CN (#I1) NE 0
    COMPRESS #TEXT LINKED-CN (#I1) INTO #TEXT
  END-IF
END-FOR
*
IF #TEXT NE ' '
  COMPRESS '*Linked to CN' #TEXT INTO #TEXT
END-IF
*
*  TM1 - START
* IF STATUS-VIEW.PROCESS-IND = 'S' AND STATUS-VIEW.APPL-SOURCE = 'TU'  /* DW8
IF STATUS-VIEW.APPL-SOURCE = 'TU'                                /* DW8
  COMPRESS #TEXT ' *Docs Stored in AAFMAA Vault ' INTO #TEXT
END-IF
*  TM1 - END
*
CALLNAT 'GGRANSXX' USING STATUS-VIEW.ID-NUMBER STATUS-VIEW.PROCESS-IND
  STATUS-VIEW.INTRO-FLAG STATUS-VIEW.MILITARY-SERVICE
  STATUS-VIEW.DATE-OF-DEATH STATUS-VIEW.NUMBER-MEMBERSHIPS
  STATUS-VIEW.PLAN (1) #TEXT-40
COMPRESS #TEXT #TEXT-40 INTO #TEXT
IF FUNCTION-SERVICE-PLAN(<STATUS-VIEW.ID-NUMBER>) = '3SR'                    /* RE3
  MOVE TRUE TO #3SR
  COMPRESS #TEXT ' + Services Member' INTO #TEXT LEAVING NO        /* RE3
END-IF                                                            /* RE3
*
MOVE '* ' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX
*
IF DATE-OF-DEATH NE 0    /* DW3
  MOVE DATE-OF-DEATH TO #W-CURRENT-DATE
ELSE
  ASSIGN #W-CURRENT-DATE = *DATN
END-IF
*
MOVE STATUS-VIEW.DTS-DATN TO #HOLD-DATN
MOVE STATUS-VIEW.DTS-TIMN TO #HOLD-TIMN
MOVE DATE-RETIRED TO #HOLD-CONVERT-DATE
* * * * * * * * * * *
FIND (1) NEW-FIRS-VIEW WITH MEMBER-KEY = #CGA-ID-G
  IF NO RECORDS FOUND
    MOVE TRUE TO #NO-FIRS
    MOVE STATUS-VIEW.ID-NUMBER TO NEW-FIRS-VIEW.ID-NUMBER
    MOVE 'Y' TO NEW-FIRS-VIEW.MEMBER-FLAG
  END-NOREC
*
*  IF #CGA-USER-COMPANY NE 'S'                        /* DW7   RE1
*    MOVE TRUE TO #NO-FIRS                            /* DW6   RE1
*  END-IF                                                      RE1
*
  MOVE 1 TO #INDEXS              /* RE3
  MOVE 1 TO #INDEXH              /* INDEX FOR TABLE RECD BUILD
  MOVE 1 TO #INDEXT              /* INDEX FOR TRLOG RECD BUILD
  RESET #INDEXHAI                /* INDEX FOR AMAA HOLD BUILD
  PERFORM FORMAT-AMAA
  MOVE STATUS-VIEW.ID-NUMBER TO #ID-NUMBER                    /* RE START
  #SECONDARY-CN(*) := FUNCTION-SECONDARY-CN(<#ID-NUMBER>)(*)
  FOR #J 1 25
    IF #SECONDARY-CN(#J) = 0
      ESCAPE BOTTOM
    END-IF
    ADD 9 TO #START
    ADD 9 TO #END
    FIND STAT-V WITH ID-NUMBER = #SECONDARY-CN(#J)
    END-FIND
    PERFORM FORMAT-AMAA-2
    PERFORM READ-SEC-FIRS
  END-FOR                                                                 /* RE3 END
*
  IF ((STATUS-VIEW.INTRO-FLAG = 'S' OR = 'K' OR = 'G')) /* added 9/19/2001 lcb  DW6/DW7
    FOR #I1 #M TO 15                             /* RE3
      MOVE (AD=P) TO #SCR-POL-CD-CV (#I1)
      MOVE (AD=P) TO #SCR-CV        (#I1)
      MOVE (AD=P) TO #SCR-AMT-CV    (#I1)
    END-FOR
  END-IF
*
  IF NEW-FIRS-VIEW.POL-NAME(1) = ' '
    MOVE TRUE TO #NO-SEG
  END-IF
*
  IF #WITH-SGLI               /* RE3
    MOVE FALSE TO #NO-SGLI    /* RE3
  ELSE                        /* RE3
    MOVE TRUE TO #NO-SGLI        /* DETERMINE IF SGLI EXISTS
    FOR #I1 1 TO 45
      IF #CGA-USER-COMPANY NE 'S'                    /* DW7
        ESCAPE BOTTOM               /* DW6
      END-IF
*
      IF NEW-FIRS-VIEW.POL-NAME (#I1) = 'SGLI' OR = 'VGLI' OR = 'S.G.L.I.'
        MOVE FALSE TO #NO-SGLI
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-IF
*
  IF #CGA-USER-COMPANY = 'S'                       /* RE2
    MOVE STATUS-VIEW.LAST-FM-DATE TO #WYYYYMMDD-N
    PERFORM DATE-REFORMAT
    MOVE #W-DATE-EDITED TO #SCN-LST-FM-DM
    MOVE 'DM' TO #DM-TEXT                          /* RE2
*
    MOVE NEW-FIRS-VIEW.DP-LAST-FM-DATE TO #WYYYYMMDD-N
    PERFORM DATE-REFORMAT
    MOVE #W-DATE-EDITED TO #SCN-LST-FM-DC
    MOVE 'DC' TO #DC-TEXT                          /* RE2
*
    MOVE NEW-FIRS-VIEW.DOCU-LAST-FM-DATE TO #WYYYYMMDD-N
    PERFORM DATE-REFORMAT
    MOVE #W-DATE-EDITED TO #SCN-LST-FM-DD
    MOVE 'DD' TO #DD-TEXT                          /* RE2
*
    MOVE STATUS-VIEW.LAST-FM-DATE-WH TO #WYYYYMMDD-N
    PERFORM DATE-REFORMAT
    MOVE #W-DATE-EDITED TO #SCN-LST-FM-SU
    MOVE 'SU' TO #SU-TEXT                          /* RE2
  END-IF
*
  MOVE NEW-FIRS-VIEW.INS-LAST-FM-DATE TO #WYYYYMMDD-N
  PERFORM DATE-REFORMAT
  MOVE #W-DATE-EDITED TO #SCN-LST-FM-DP
*
  RESET #DONE
  IF (#NO-FIRS OR #NO-SEG) AND
      SEC-FIRS-V.POL-NAME(1) = ' '                                  /*  RE3 START
    ASSIGN #INDEXR = 1          /*INDEX FOR INSURANCE POLICIES
    ASSIGN #INDEXV = 1         /*INDEX FOR SAVE START OF INSUR POL
    MOVE 1 TO #K
    REPEAT UNTIL #DONE
      PERFORM MOVE-TO-SCREEN                                      /* RE3 END
      PERFORM SEND-SCREEN
    END-REPEAT                                                    /* RE3
  ELSE
    ASSIGN #INDEXR = 1          /*INDEX FOR INSURANCE POLICIES
    ASSIGN #INDEXV = 1         /*INDEX FOR SAVE START OF INSUR POL
    MOVE 1 TO #K
    REPEAT UNTIL NEW-FIRS-VIEW.POL-NAME(#INDEXR) = '    ' AND
        #SCN-POL-NAME(15) = '   '  AND
        SEC-FIRS-V.POL-NAME(#INDEXR) = ' '                  /* RE3
      RESET #SCN-POL-CODE(1:15)    /* RE3 START
      RESET #SCN-POL-REMARKS(1:15)
      RESET #SCN-POL-NAME(1:15)
      RESET #SCN-POL-NUMBER(1:15)
      RESET #SCN-POL-DATE(1:15)
      RESET #SCN-POL-AMOUNT(1:15)
      RESET #SCN-POL-TYPE(1:15)
      IF NOT #DONE
        PERFORM MOVE-TO-SCREEN
      END-IF
      ADD 9 TO #K               /* RE3 END
      PERFORM FILL-SCREEN
      PERFORM SEND-SCREEN
*
      IF NOT #CGA-USER-COMPANY = 'I'
        IF #POL-CODE-UPDATED
          FOR #INDEXS 1 TO 15
            IF #SCN-POL-CODE(#INDEXS) NE #HLD-POL-CODE(#INDEXS)
                AND (#HLD-POL-CODE(#INDEXS) = '3' OR = '4' OR = '*'
                OR = '5' OR = '&' OR = '7')
                AND (NOT #SCN-POL-CODE(#INDEXS) = '3' OR = '4' OR = '*'
                OR = '5' OR = '&' OR = '7')
              IGNORE
            ELSE
              MOVE #HLD-POL-CODE(#INDEXS) TO #SCN-POL-CODE(#INDEXS)
            END-IF
          END-FOR
        END-IF
      END-IF
*
      MOVE (AD=D) TO #SCR-POL-CD-CV (*)
      MOVE (AD=D) TO #SCR-CV        (*)
      MOVE (AD=D) TO #SCR-AMT-CV    (*)
      ASSIGN #INDEXS = 1
      ASSIGN #INDEXV = #INDEXR
    END-REPEAT
  END-IF
*
  IF #AMAA-CHANGED OR #FIELD-CHANGED
    PERFORM TRLOG-WRITE
    MOVE 'TRANS COMPLETED' TO #SUP-MESSAGE
  ELSE
    MOVE 'NO UPDATES REQUESTED' TO #SUP-MESSAGE
  END-IF
*


  IF #FIELD-CHANGED OR #AMAA-CHANGED
    IF CONTACT-ID-NUMBER = 0                                          /* bz1
      CONTACT-ID-NUMBER := STATUS-VIEW.MEMBER-CONTACT-ID           /* bz1
    END-IF                                                            /* bz1

    IF #NO-FIRS
      IF #FIELD-CHANGED
        PERFORM UPDATE-RECORD
      END-IF
*
      MOVE *DATN TO NEW-FIRS-VIEW.INS-LAST-FM-DATE
      MOVE *DATN TO NEW-FIRS-VIEW.DTS-DATN
      MOVE *TIMN TO NEW-FIRS-VIEW.DTS-TIMN
      STORE NEW-FIRS-VIEW
    ELSE
      IF #FIELD-CHANGED
        PERFORM UPDATE-RECORD
      END-IF
*
      MOVE *DATN TO NEW-FIRS-VIEW.INS-LAST-FM-DATE
      MOVE *DATN TO NEW-FIRS-VIEW.DTS-DATN
      MOVE *TIMN TO NEW-FIRS-VIEW.DTS-TIMN
      UPDATE (0368)
    END-IF
  END-IF
*
  END TRANSACTION
END-FIND /* (0368)
*
IF #AMAA-CHANGED
  PERFORM AMAA-FILE-WRITE
END-IF
*
IF #FIELD-CHANGED
  PERFORM STATUS-INS-UPDATE
END-IF
*
MOVE 'DP-' TO #CGA-SCR-COMMAND
MOVE (AD=P) TO #SCR-CV (*)
MOVE (AD=P) TO #SCR-POL-CD-CV (*)
MOVE (AD=P) TO #SCR-AMT-CV (*)
INPUT TEXT #SUP-MESSAGE USING MAP 'E4400MDP'
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MOVE-TO-SCREEN  /* RE3
* * * * * * * * * * * * * * * * *
**IF #INDEXS = 1
RESET #SCN-POL-CODE(1:15)
RESET #SCN-POL-REMARKS(1:15)
RESET #SCN-POL-NAME(1:15)
RESET #SCN-POL-NUMBER(1:15)
RESET #SCN-POL-DATE(1:15)
RESET #SCN-POL-AMOUNT(1:15)
RESET #SCN-POL-TYPE(1:15)
**END-IF
RESET #L
FOR #J #K 27               /* RE3 START
  IF #J > 15                                               /*  OI-2864
    ESCAPE BOTTOM                                       /*  OI-2864
  END-IF                                                   /*  OI-2864
  IF #SAVE-POL-NAME(#J) NE ' '
    ADD 1 TO #L
    IF #L = 16
      ESCAPE BOTTOM
    END-IF
    MOVE #SAVE-POL-CODE(#J) TO #SCN-POL-CODE(#L)
    MOVE #SAVE-POL-NAME(#J) TO #SCN-POL-NAME(#L)
    MOVE #SAVE-POL-NUMBER(#J) TO #SCN-POL-NUMBER(#L)
    MOVE #SAVE-POL-TYPE(#J) TO #SCN-POL-TYPE(#L)
    MOVE #SAVE-POL-REMARKS(#J) TO #SCN-POL-REMARKS(#L)
    MOVE #SAVE-POL-AMOUNT(#J) TO #SCN-POL-AMOUNT(#L)
    MOVE #SAVE-POL-DATE(#J) TO #SCN-POL-DATE(#L)
*    IF #CGA-USER-COMPANY = 'S' AND #REGULAR-USER    /* PM3
*      MOVE (AD=P) TO #SCR-POL-CD-CV (#L)            /* PM3
*    END-IF                                          /* PM3
    MOVE (AD=P) TO #SCR-CV (#L)
    MOVE (AD=P) TO #SCR-AMT-CV (#L)
  ELSE
    IF #CGA-USER-COMPANY = 'S' AND #REGULAR-USER AND #3SR   /* PM10
      MOVE (AD=D) TO #SCR-POL-CD-CV (#L+1:15)
      MOVE (AD=D) TO #SCR-CV (#L+1:15)
      MOVE (AD=D) TO #SCR-AMT-CV (#L+1:15)
    END-IF
    MOVE TRUE TO #DONE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-AMAA
* * * * * * * * * * * * * * * *
*
MOVE 1 TO #INDEXS
MOVE STATUS-VIEW.FACE-AMOUNT (*) TO #FACE-AMT (1:9)
MOVE *DATN TO #AS-OF-DT
RESET #SUFFIX
CALLNAT 'CVALUSXX'
  STATUS-VIEW.ID-NUMBER
  #SUFFIX
  #AS-OF-DT
  #LOAN-BAL   (1:9)
  #LOAN-BAL-2 (1:9)
  #ACC-INT    (1:9)
  #ACC-INT-2  (1:9)
  #CSV        (1:9)
  #CVALUE     (1:9)
  #LVALUE     (1:9)
  #MCSV       (1:9)
  #DB         (1:9)
  #ANN-PREMIUM  (1:9)
  #FACE-AMT   (1:9)
  #TPP        (1:9)
*
FOR #INDEXP = 1 TO C*ACTIVE-SEGMENTS
  IF STATUS(#INDEXP) NE 'D'
    ESCAPE TOP
  END-IF
*
*  IF #CGA-USER-COMPANY = 'S' AND #REGULAR-USER       /* RE1 NG1 RE3
*    MOVE (AD=P) TO #SCR-POL-CD-CV (#INDEXS)          /* RE1     RE3
*  END-IF                                             /* RE1     RE3
  MOVE (AD=P) TO #SCR-CV (#INDEXS)
  MOVE (AD=P) TO #SCR-AMT-CV (#INDEXS)
  ADD 1 TO #INDEXHAI
  MOVE #INDEXP TO #HAI-REAL-I2 (#INDEXHAI)            /* DW4
  MOVE SPLIT-IND          (#INDEXP) TO #HAI-SPLIT-IND (#INDEXHAI)
  MOVE ISSUE-DATE         (#INDEXP) TO #HAI-ISSUE-DATE (#INDEXHAI)
  MOVE FIRS-LOCATION-CODE (#INDEXP) TO #HAI-FIRS-CODE  (#INDEXHAI)
*
  IF FIRS-LOCATION-CODE (#INDEXP) = '1' OR = '2' OR = '3' OR = '4'
      OR = '7' OR = '8'
    MOVE 'Code 1/2/3/4/7/8 means mbr not owner' TO #SCR-N-MSG
  END-IF
*
  IF   (#INDEXHAI = 1)
      AND (RESTRICT-MBR-SW = 'Y')
    MOVE 'RESTRICTED MBR' TO #SAVE-POL-REMARKS (#INDEXS)  /* RE3
  ELSE
    IF ((#LOAN-BAL   (#INDEXP)    GT 0)
        OR (#LOAN-BAL-2 (#INDEXP) GT 0))
      COMPUTE #TOT-LOAN = #LOAN-BAL   (#INDEXP)
        + #LOAN-BAL-2 (#INDEXP)
      MOVE EDITED #TOT-LOAN (EM=ZZZ,ZZ9) TO #HOLD-TEMP-AMT
      COMPRESS '$' #HOLD-TEMP-AMT INTO #SAVE-POL-REMARKS (#INDEXS)  /* RE3
    END-IF
  END-IF
*
  IF   (STATUS-VIEW.ID-NUMBER = 40486)
      AND (CONV-DURATION (#INDEXP) = 888)
    MOVE 'AAFMAA Owner' TO #SAVE-POL-REMARKS (#INDEXS)  /* RE3
  END-IF
  CALLNAT 'GPLNMSXX' #POLICY-NUMBER STATUS-VIEW.ID-NUMBER #INDEXP
  ASSIGN #SAVE-POL-NUMBER(#INDEXS) = #POLICY-NUMBER
  MOVE FIRS-LOCATION-CODE(#INDEXP) TO #SAVE-POL-CODE(#INDEXS)
  MOVE ISSUE-DATE(#INDEXP) TO #HOLD-CONVERT-DATE
  COMPRESS #HCD-MM '/' #HCD-YY INTO #SAVE-POL-DATE(#INDEXS) LEAVING
    NO SPACE                                                            /* RE3 END
*
  IF STATUS-VIEW.ISSUE-DATE (#INDEXP) LT 19870414
    ASSIGN #SAVE-POL-NAME(#INDEXS) = 'AMAA'
  ELSE
    ASSIGN #SAVE-POL-NAME(#INDEXS) = 'AAFMAA'
  END-IF
*
  DECIDE FOR FIRST CONDITION
    WHEN STATUS-VIEW.PLAN (#INDEXP) = 'SO'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'ACCD'               /* RE3
      ASSIGN #SAVE-POL-REMARKS(#INDEXS) = 'Services Only'   /* RE3
    WHEN STATUS-VIEW.PAY-STATUS(#INDEXP) = '42'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PDUP'    /* RE3
    WHEN STATUS-VIEW.PAY-STATUS(#INDEXP) = '44'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'ETI'     /* RE3
    WHEN STATUS-VIEW.PAY-STATUS(#INDEXP) = '45'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'RPU'     /* RE3
    WHEN STATUS-VIEW.PAY-STATUS(#INDEXP) = '47'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PUSP'    /* RE3
    WHEN STATUS-VIEW.PAY-STATUS(#INDEXP) = '48'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PUSP'    /* RE3
    WHEN NONE
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = PLAN(#INDEXP) /* RE3
  END-DECIDE
*
  MOVE EDITED #DB (#INDEXP) (EM=9999999) TO #SAVE-POL-AMOUNT-A(#INDEXS)  /* RE3
  COMPUTE #INDEXS = #INDEXS + 1
*
END-FOR
MOVE #INDEXS TO #M     /* RE3
*
* IF #DP-ON-AFSC                          /* DW8
*   GET STATUS-VIEW #CGA-ISN
* END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-AMAA-2        /* RE3
* * * * * * * * * * * * * * * *
SUBTRACT 1 FROM #START
FOR #K 1 9
  MOVE STAT-V.FACE-AMOUNT (#K) TO #FACE-AMT (#START+#K)
END-FOR
ADD 1 TO #START
MOVE *DATN TO #AS-OF-DT
RESET #SUFFIX
*
CALLNAT 'CVALUSXX'
  STAT-V.ID-NUMBER
  #SUFFIX
  #AS-OF-DT
  #LOAN-BAL   (#START:#END)
  #LOAN-BAL-2 (#START:#END)
  #ACC-INT    (#START:#END)
  #ACC-INT-2  (#START:#END)
  #CSV        (#START:#END)
  #CVALUE     (#START:#END)
  #LVALUE     (#START:#END)
  #MCSV       (#START:#END)
  #DB         (#START:#END)
  #ANN-PREMIUM  (#START:#END)
  #FACE-AMT   (#START:#END)
  #TPP        (#START:#END)
*
SUBTRACT 1 FROM #START
FOR #INDEXP = 1 TO C*STAT-V.ACTIVE-SEGMENTS
  IF STAT-V.STATUS(#INDEXP) NE 'D'
    ESCAPE TOP
  END-IF
*
  IF  #INDEXS > 15                                                 /*  OI-2864
    ESCAPE BOTTOM                                                  /*  OI-2864
  END-IF                                                           /*  OI-2864
  MOVE (AD=P) TO #SCR-CV (#INDEXS)
  MOVE (AD=P) TO #SCR-AMT-CV (#INDEXS)
  ADD 1 TO #INDEXHAI
  MOVE #INDEXP TO #HAI-REAL-I2 (#INDEXHAI)            /* DW4
  MOVE STAT-V.SPLIT-IND          (#INDEXP) TO #HAI-SPLIT-IND (#INDEXHAI)
  MOVE STAT-V.ISSUE-DATE         (#INDEXP) TO #HAI-ISSUE-DATE (#INDEXHAI)
  MOVE STAT-V.FIRS-LOCATION-CODE (#INDEXP) TO #HAI-FIRS-CODE  (#INDEXHAI)
*
  IF STAT-V.FIRS-LOCATION-CODE (#INDEXP) = '1' OR = '2' OR = '3' OR = '4'
      OR = '7' OR = '8'
    MOVE 'Code 1/2/3/4/7/8 means mbr not owner' TO #SCR-N-MSG
  END-IF
*
  IF   (#INDEXHAI = #START)
      AND (STAT-V.RESTRICT-MBR-SW = 'Y')
    MOVE 'RESTRICTED MBR' TO #SAVE-POL-REMARKS (#INDEXS)
  ELSE
    IF ((#LOAN-BAL   (#INDEXP+#START)    GT 0)
        OR (#LOAN-BAL-2 (#INDEXP+#START) GT 0))
      COMPUTE #TOT-LOAN = #LOAN-BAL   (#INDEXP+#START)
        + #LOAN-BAL-2 (#INDEXP+#START)
      MOVE EDITED #TOT-LOAN (EM=ZZZ,ZZ9) TO #HOLD-TEMP-AMT
      COMPRESS '$' #HOLD-TEMP-AMT INTO #SAVE-POL-REMARKS (#INDEXS)
    END-IF
  END-IF
*
  CALLNAT 'GPLNMSXX' #POLICY-NUMBER STAT-V.ID-NUMBER #INDEXP
  ASSIGN #SAVE-POL-NUMBER(#INDEXS) = #POLICY-NUMBER
  MOVE STAT-V.FIRS-LOCATION-CODE(#INDEXP) TO #SAVE-POL-CODE(#INDEXS)
  MOVE STAT-V.ISSUE-DATE(#INDEXP) TO #HOLD-CONVERT-DATE
  COMPRESS #HCD-MM '/' #HCD-YY INTO #SAVE-POL-DATE(#INDEXS) LEAVING
    NO SPACE
*
  IF STAT-V.ISSUE-DATE (#INDEXP) LT 19870414
    ASSIGN #SAVE-POL-NAME(#INDEXS) = 'AMAA'
  ELSE
    ASSIGN #SAVE-POL-NAME(#INDEXS) = 'AAFMAA'
  END-IF
*
  DECIDE FOR FIRST CONDITION
    WHEN STAT-V.PLAN (#INDEXP) = 'SO'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'ACCD'
      ASSIGN #SAVE-POL-REMARKS(#INDEXS) = 'Services Only'
    WHEN STAT-V.PAY-STATUS(#INDEXP) = '42'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PDUP'
    WHEN STAT-V.PAY-STATUS(#INDEXP) = '44'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'ETI'
    WHEN STAT-V.PAY-STATUS(#INDEXP) = '45'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'RPU'
    WHEN STAT-V.PAY-STATUS(#INDEXP) = '47'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PUSP'
    WHEN STAT-V.PAY-STATUS(#INDEXP) = '48'
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = 'PUSP'
    WHEN NONE
      ASSIGN #SAVE-POL-TYPE(#INDEXS) = PLAN(#INDEXP)
  END-DECIDE
*
  MOVE EDITED #DB (#INDEXP+#START) (EM=9999999) TO #SAVE-POL-AMOUNT-A(#INDEXS)
  COMPUTE #INDEXS = #INDEXS + 1
  ADD 1 TO #M
*
END-FOR
ADD 1 TO #START
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SEND-SCREEN
* * * * * * * * * * * * * * * *
*
*
MOVE #SCN-POL-CODE(*) TO #HLD-POL-CODE(*)
MOVE #SCN-POL-NAME(*) TO #HLD-POL-NAME(*)
MOVE #SCN-POL-TYPE(*) TO #HLD-POL-TYPE(*)
MOVE #SCN-POL-DATE(*) TO #HLD-POL-DATE(*)
MOVE #SCN-POL-AMOUNT(*) TO #HLD-POL-AMOUNT(*)
MOVE #SCN-POL-NUMBER(*) TO #HLD-POL-NUMBER(*)
MOVE #SCN-POL-REMARKS(*) TO #HLD-POL-REMARKS(*)
INPUT TEXT #TEXT USING MAP 'E4400MDP'
IF #CGA-SCR-COMMAND = '*'
  IGNORE
ELSE
  IF #CGA-SCR-COMMAND NE #CGA-ORIG-COMMAND OR
      #CGA-SCR-ID NE #CGA-ORIG-ID
    END TRANSACTION
    FETCH 'G1000PXX'
  END-IF
END-IF
*
MOVE #INDEXH TO #HOLD-INDEXH
PERFORM EDIT-SCREEN
RESET #TEXT
*
RESET #SFX                        /* DW4
FOR #INDEXS = 1 TO 15
  IF   (#SCN-POL-NAME(#INDEXS) = 'AAFMAA' OR = 'AMAA')
    PERFORM PROCESS-AMAA-TRLOG
  ELSE
*
    IF (#SCN-POL-NAME(#INDEXS) = 'SGLI') AND #NO-SGLI  /* INITIAL SGLI
      PERFORM PROCESS-SGLI-TRLOG
    ELSE
      PERFORM PROCESS-TRLOG
    END-IF
  END-IF
*
  IF  #SCN-POL-NAME(#INDEXS) = ' ' OR = 'AAFMAA' OR = 'AMAA'
    IGNORE
  ELSE
    PERFORM MOVE-SCREEN
  END-IF
*
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FILL-SCREEN
* * * * * * * * * * * * * * * *
*

**FOR #INDEXS #INDEXS TO 15           /* RE3
FOR #INDEXS #J TO 15                  /* RE3
*
  IF NEW-FIRS-VIEW.POL-NAME(#INDEXR) NE '   '
    MOVE NEW-FIRS-VIEW.POL-CODE (#INDEXR) TO #SCN-POL-CODE(#INDEXS)
    MOVE NEW-FIRS-VIEW.POL-NAME (#INDEXR) TO #SCN-POL-NAME(#INDEXS)
    MOVE NEW-FIRS-VIEW.POL-TYPE(#INDEXR) TO #SCN-POL-TYPE(#INDEXS)
    MOVE NEW-FIRS-VIEW.POL-REMARKS (#INDEXR) TO
      #SCN-POL-REMARKS(#INDEXS)
    MOVE NEW-FIRS-VIEW.POL-NUMBER (#INDEXR) TO
      #SCN-POL-NUMBER(#INDEXS)
*
    IF NEW-FIRS-VIEW.POL-NAME (#INDEXR) = 'NSLI'
      RESET #OVER-AGE
*
      IF   (NEW-FIRS-VIEW.POL-TYPE (#INDEXR) = 'ML65')
          AND ((DATE-OF-BIRTH + 650000) LE *DATN)                   /* TT1
        MOVE TRUE TO #OVER-AGE
      END-IF
*
      IF   (NEW-FIRS-VIEW.POL-TYPE (#INDEXR) = 'ML70')
          AND ((DATE-OF-BIRTH + 700000) LE *DATN)                   /* TT1
        MOVE TRUE TO #OVER-AGE
      END-IF
*
      IF #OVER-AGE
        DIVIDE 2 INTO NEW-FIRS-VIEW.POL-AMOUNT (#INDEXR)
      END-IF
    END-IF
*
    MOVE NEW-FIRS-VIEW.POL-AMOUNT (#INDEXR)
      TO #SCN-POL-AMOUNT(#INDEXS)
*
    IF NEW-FIRS-VIEW.POL-NAME(#INDEXR) = 'SGLI'
      IF NEW-FIRS-VIEW.POL-DATE(#INDEXR) > 0
        MOVE NEW-FIRS-VIEW.POL-DATE(#INDEXR) TO #HOLD-CONVERT-DATE
      ELSE
        MOVE STATUS-VIEW.ACTIVE-DUTY-BASE-DATE TO #HOLD-CONVERT-DATE
      END-IF
    ELSE
      MOVE NEW-FIRS-VIEW.POL-DATE(#INDEXR) TO #HOLD-CONVERT-DATE
    END-IF
    COMPRESS #HCD-MM '/' #HCD-YY INTO #SCN-POL-DATE(#INDEXS)
      LEAVING NO SPACE
    COMPUTE #INDEXR = #INDEXR + 1
    MOVE 1 TO #J
  ELSE
*    ESCAPE ROUTINE                       /* RE3 START
    IF SEC-FIRS-V.POL-NAME (#INDEXR) NE ' '
      PERFORM MOVE-SEC-FIRS-TO-SCREEN
      COMPUTE #INDEXR = #INDEXR + 1
    ELSE
      ESCAPE ROUTINE
    END-IF                                /* RE3 END
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-SEC-FIRS   /* RE3
* * * * * * * * * * * * * * * *
FIND (1) SEC-FIRS-V WITH ID-NUMBER = #SECONDARY-CN(#J)
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
*
  IF SEC-FIRS-V.POL-NAME(1) = ' '
    ESCAPE ROUTINE
  END-IF
*
  FOR #I1 1 TO 45
    IF #CGA-USER-COMPANY NE 'S'                    /* DW7
      ESCAPE BOTTOM               /* DW6
    END-IF
*
    IF SEC-FIRS-V.POL-NAME (#I1) = 'SGLI' OR = 'VGLI' OR = 'S.G.L.I.'
      MOVE TRUE TO #WITH-SGLI
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
END-FIND
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MOVE-SEC-FIRS-TO-SCREEN   /* RE3
* * * * * * * * * * * * * * * * * * * * *
IF #CGA-USER-COMPANY NE 'S' AND #REGULAR-USER     /* RE1 NG1
  MOVE (AD=P) TO #SCR-POL-CD-CV(#INDEXS)          /* RE1
  MOVE (AD=P) TO #SCR-CV(#INDEXS)                 /* RE1
  MOVE (AD=P) TO #SCR-AMT-CV(#INDEXS)             /* RE1
END-IF                                            /* RE1
MOVE SEC-FIRS-V.POL-CODE (#INDEXR) TO #SCN-POL-CODE(#INDEXS)
MOVE SEC-FIRS-V.POL-NAME (#INDEXR) TO #SCN-POL-NAME(#INDEXS)
MOVE SEC-FIRS-V.POL-TYPE(#INDEXR) TO #SCN-POL-TYPE(#INDEXS)
MOVE SEC-FIRS-V.POL-REMARKS (#INDEXR) TO
  #SCN-POL-REMARKS(#INDEXS)
MOVE SEC-FIRS-V.POL-NUMBER (#INDEXR) TO
  #SCN-POL-NUMBER(#INDEXS)
*
IF SEC-FIRS-V.POL-NAME (#INDEXR) = 'NSLI'
  RESET #OVER-AGE
*
  IF   (SEC-FIRS-V.POL-TYPE (#INDEXR) = 'ML65')
      AND ((DATE-OF-BIRTH + 650000) LE *DATN)                         /* TT1
    MOVE TRUE TO #OVER-AGE
  END-IF
*
  IF   (SEC-FIRS-V.POL-TYPE (#INDEXR) = 'ML70')                     /* TT1
      AND ((DATE-OF-BIRTH + 700000) LE *DATN)
    MOVE TRUE TO #OVER-AGE
  END-IF
*
  IF #OVER-AGE
    DIVIDE 2 INTO SEC-FIRS-V.POL-AMOUNT (#INDEXR)
  END-IF
END-IF
*
MOVE SEC-FIRS-V.POL-AMOUNT (#INDEXR)
  TO #SCN-POL-AMOUNT(#INDEXS)
*
IF SEC-FIRS-V.POL-NAME(#INDEXR) = 'SGLI'
  IF SEC-FIRS-V.POL-DATE(#INDEXR) > 0
    MOVE SEC-FIRS-V.POL-DATE(#INDEXR) TO #HOLD-CONVERT-DATE
  ELSE
    MOVE STATUS-VIEW.ACTIVE-DUTY-BASE-DATE TO #HOLD-CONVERT-DATE
  END-IF
ELSE
  MOVE SEC-FIRS-V.POL-DATE(#INDEXR) TO #HOLD-CONVERT-DATE
END-IF
COMPRESS #HCD-MM '/' #HCD-YY INTO #SCN-POL-DATE(#INDEXS)
  LEAVING NO SPACE
*
END-SUBROUTINE
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-TRLOG
* * * * * * * * * * * * * * * * *
*
MOVE #SCN-POL-DATE(#INDEXS) TO #HOLD-SCN-POL-DATE
IF #HSPD-MM-N > 0
  MOVE '01' TO #HCPD-DD
  IF #HSPD-YY-N GT 20  /* bz2
    MOVE '19' TO #HCPD-CC
  ELSE
    MOVE '20' TO #HCPD-CC
  END-IF
*
  MOVE #HSPD-MM TO #HCPD-MM
  MOVE #HSPD-YY TO #HCPD-YY
ELSE
  MOVE 0 TO #HOLD-CONVERT-POL-DATE
END-IF
IF (#CGA-USER-COMPANY NE 'S' AND #REGULAR-USER AND NOT #3SR)  /* RE1 NG1  RE3 Added 3SR
    OR #NO-FIRS AND #SCN-POL-NAME(#INDEXS) = '    '
    OR #NO-SEG AND #SCN-POL-NAME(#INDEXS) = '    '
  IGNORE
ELSE
  PERFORM ADD-TO-AMOUNT
  IF #NO-FIRS OR #NO-SEG
      OR #CGA-USER-COMPANY NE 'S' AND #REGULAR-USER         /* RE1 NG1
    PERFORM NEW-MOVE
  ELSE
    IF #INDEXV > 40
      IF #SCN-POL-CODE(#INDEXS) > ' '
        PERFORM NEW-MOVE
      ELSE
        IGNORE
      END-IF
    ELSE
      IF #SCN-POL-CODE(#INDEXS) NE NEW-FIRS-VIEW.POL-CODE(#INDEXV)
          OR #SCN-POL-NAME(#INDEXS) NE NEW-FIRS-VIEW.POL-NAME(#INDEXV)
          OR #SCN-POL-AMOUNT(#INDEXS) NE NEW-FIRS-VIEW.POL-AMOUNT(#INDEXV)
          OR #SCN-POL-NUMBER(#INDEXS) NE NEW-FIRS-VIEW.POL-NUMBER(#INDEXV)
          OR #SCN-POL-REMARKS(#INDEXS)
          NE NEW-FIRS-VIEW.POL-REMARKS(#INDEXV)
          OR #HOLD-CONVERT-POL-DATE NE NEW-FIRS-VIEW.POL-DATE(#INDEXV)
          OR #SCN-POL-TYPE(#INDEXS) NE NEW-FIRS-VIEW.POL-TYPE(#INDEXV)
        PERFORM OLD-MOVE
        PERFORM NEW-MOVE
      END-IF
    END-IF
  END-IF
END-IF
*
ADD 1 TO #INDEXV
*
END-SUBROUTINE /* (0817)
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-AMAA-TRLOG
* * * * * * * * * * * * * * * * * * *
*
IF #HAI-FIRS-CODE (#INDEXS) NE #SCN-POL-CODE(#INDEXS)
  IF #HAI-ALOT-SW NE 'A'
    MOVE 'I' TO #HAI-ALOT-SW  /* Only needed for AAFMAA    /* DW4
  END-IF
*
  ADD 1 TO #SFX
  MOVE #HAI-REAL-I2 (#INDEXS) TO #I1
  COMPUTE SYNC-FLD-NUM (#SFX) = 43 + #I1
  MOVE #HAI-FIRS-CODE (#INDEXS) TO #TR-POL-CODE
  MOVE ISSUE-DATE (#I1)         TO #TR-POL-CODE-ISS-DT
  MOVE SPLIT-IND  (#I1)         TO #TR-POL-CODE-SPL-IND
  MOVE PLAN       (#I1)         TO #TR-POL-CODE-PLAN
  MOVE #TR-POL-CODE-AREA TO SYNC-FLD-OLD-DATA (#SFX)
  MOVE #SCN-POL-CODE (#INDEXS) TO #TR-POL-CODE
  MOVE #TR-POL-CODE-AREA TO SYNC-FLD-NEW-DATA (#SFX)    /* DW4
*
  MOVE #SCN-POL-DATE(#INDEXS) TO #HOLD-SCN-POL-DATE
  IF #AMAA-CHANGED
    IGNORE
  ELSE
    FOR #INDEXP 1 TO #INDEXHAI
      MOVE #SCN-POL-CODE(#INDEXP) TO #HAI-NEW-FIRS-CODE(#INDEXP)
    END-FOR
  END-IF
*
  ASSIGN #AMAA-CHANGED = TRUE
  MOVE #SCN-POL-CODE(#INDEXS)    TO NEW-CODE(#INDEXT)
  MOVE #SCN-POL-TYPE(#INDEXS)    TO NEW-TYPE(#INDEXT)
  MOVE #HSPD-MM                  TO NEW-DATE-MM(#INDEXT)
  MOVE #HSPD-YY                  TO NEW-DATE-YY(#INDEXT)
  MOVE #SCN-POL-AMOUNT(#INDEXS)  TO NEW-AMOUNT(#INDEXT)
  MOVE #SCN-POL-NUMBER(#INDEXS)  TO NEW-NUMBER(#INDEXT)
  MOVE #SCN-POL-REMARKS(#INDEXS) TO NEW-REMARKS(#INDEXT)
  MOVE #SCN-POL-NAME(#INDEXS)    TO NEW-NAME(#INDEXT)
  MOVE #HAI-FIRS-CODE(#INDEXS)   TO OLD-CODE(#INDEXT)
  MOVE #SCN-POL-TYPE(#INDEXS)    TO OLD-TYPE(#INDEXT)
  MOVE #HSPD-MM                  TO OLD-DATE-MM(#INDEXT)
  MOVE #HSPD-YY                  TO OLD-DATE-YY(#INDEXT)
  MOVE #SCN-POL-AMOUNT(#INDEXS)  TO OLD-AMOUNT(#INDEXT)
  MOVE #SCN-POL-NUMBER(#INDEXS)  TO OLD-NUMBER(#INDEXT)
  MOVE #SCN-POL-REMARKS(#INDEXS) TO OLD-REMARKS(#INDEXT)
  MOVE #SCN-POL-NAME(#INDEXS)    TO OLD-NAME(#INDEXT)
  ADD 1 TO #INDEXT
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-SGLI-TRLOG
* * * * * * * * * * * * * * * * * * *
*
MOVE TRUE TO #FIELD-CHANGED
MOVE #SCN-POL-CODE(#INDEXS)    TO NEW-CODE(#INDEXT)
MOVE #SCN-POL-TYPE(#INDEXS)    TO NEW-TYPE(#INDEXT)
MOVE #HSPD-MM                  TO NEW-DATE-MM(#INDEXT)
MOVE #HSPD-YY                  TO NEW-DATE-YY(#INDEXT)
MOVE #SCN-POL-AMOUNT(#INDEXS)  TO NEW-AMOUNT(#INDEXT)
MOVE #SCN-POL-NUMBER(#INDEXS)  TO NEW-NUMBER(#INDEXT)
MOVE #SCN-POL-REMARKS(#INDEXS) TO NEW-REMARKS(#INDEXT)
MOVE #SCN-POL-NAME(#INDEXS)    TO NEW-NAME(#INDEXT)
ADD 1 TO #INDEXT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE EDIT-SCREEN
* * * * * * * * * * * * * * * *
*
RESET #POL-CODE-UPDATED
FOR #INDEXS 1 TO 15
  IF   (NOT #SCN-POL-NAME (#INDEXS) = 'AAFMAA' OR = 'AMAA')
      AND (#SCN-POL-CODE (#INDEXS) = '-')
    RESET #SCN-POL-CODE    (#INDEXS)
    RESET #SCN-POL-TYPE    (#INDEXS)
    RESET #SCN-POL-DATE    (#INDEXS)
    RESET #SCN-POL-AMOUNT  (#INDEXS)
    RESET #SCN-POL-NUMBER  (#INDEXS)
    RESET #SCN-POL-REMARKS (#INDEXS)
    RESET #SCN-POL-NAME    (#INDEXS)
  END-IF
*
  IF #SCN-POL-NAME(#INDEXS) = ' '
    IF #SCN-POL-AMOUNT(#INDEXS) NE 0
        OR  #SCN-POL-TYPE(#INDEXS) NE ' '
      REINPUT 'NO COMPANY NAME, BUT AMOUNT OR TYPE PRESENT'
        MARK *#SCN-POL-NAME(#INDEXS)
    END-IF
  END-IF
*
  IF #SCN-POL-NAME(#INDEXS) > ' '
      AND #SCN-POL-TYPE(#INDEXS) = ' '
    REINPUT WITH 'COMPANY NAME PRESENT AND NO TYPE'
      MARK *#SCN-POL-TYPE(#INDEXS)
  END-IF
*
  IF #CGA-USER-COMPANY NE 'S'                         /* RE3
    IF #SCN-POL-NAME (#INDEXS) = 'AAFMAA' OR = 'AMAA' /* RE3
      CALLNAT 'GPLCDSXX' #SCN-POL-CODE (#INDEXS) #INVALID #ERR-MSG
      IF #INVALID
*  OR ((#SCN-POL-CODE (#INDEXS) = '1' OR = '2' OR = '3' OR = '4')
*     AND (NOT #SCN-POL-NAME (#INDEXS) = 'AAFMAA' OR = 'AMAA'))
*  above removed 5/95 as per LCE
          OR   ((STATUS-VIEW.INTRO-FLAG ='S' OR = 'K' OR = 'G')
          AND (#SCN-POL-NAME(#INDEXS) NE ' ')
          AND (NOT #SCN-POL-CODE (#INDEXS) = '&' OR = 'X' OR = '7' OR = '8'))
        REINPUT USING HELP
          MARK *#SCN-POL-CODE (#INDEXS)
      END-IF
    END-IF
  END-IF
*
  IF #3SR OR (NOT #CGA-USER-COMPANY = 'I')        /* RE3
    IF ((#SCN-POL-CODE(#INDEXS) NE #HLD-POL-CODE(#INDEXS))
        AND (#HLD-POL-CODE(#INDEXS) = '3' OR = '4' OR = '*'
        OR = '5' OR = '&' OR = '7')
        AND (NOT #SCN-POL-CODE(#INDEXS) = '3' OR = '4' OR = '*' OR = '5'
        OR = '&' OR = '7'))
      IF #POL-CODE-UPDATED
        IGNORE
      ELSE
        IF #Y-N = ' '
          INPUT USING MAP 'E4401MDP'
        END-IF
*
        IF #Y-N = 'Y'
          MOVE TRUE TO #POL-CODE-UPDATED
          MOVE TRUE TO #FIELD-CHANGED
        ELSE
          IF #SCN-POL-CODE(#INDEXS) = '-'
              OR (NOT #SCN-POL-CODE(#INDEXS) = ' '
              AND NOT #SCN-POL-NAME(#INDEXS) = ' ')
            MOVE #HLD-POL-CODE(#INDEXS) TO #SCN-POL-CODE(#INDEXS)
            MOVE #HLD-POL-NAME(#INDEXS) TO #SCN-POL-NAME(#INDEXS)
            MOVE #HLD-POL-TYPE(#INDEXS) TO #SCN-POL-TYPE(#INDEXS)
            MOVE #HLD-POL-DATE(#INDEXS) TO #SCN-POL-DATE(#INDEXS)
            MOVE #HLD-POL-AMOUNT(#INDEXS) TO #SCN-POL-AMOUNT(#INDEXS)
            MOVE #HLD-POL-NUMBER(#INDEXS) TO #SCN-POL-NUMBER(#INDEXS)
            MOVE #HLD-POL-REMARKS(#INDEXS) TO #SCN-POL-REMARKS(#INDEXS)
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF #SCN-POL-NAME (#INDEXS) = 'NSLI'
    RESET #OVER-AGE
*
    IF   (#SCN-POL-TYPE (#INDEXS) = 'ML65')
        AND ((DATE-OF-BIRTH + 650000) LE *DATN)                  /* TT1
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    IF   (#SCN-POL-TYPE (#INDEXS) = 'ML70')
        AND ((DATE-OF-BIRTH + 700000) LE *DATN)                  /* TT1
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    MOVE #SCN-POL-AMOUNT (#INDEXS) TO #FULL-NSLI-AMT
    IF #OVER-AGE
      MULTIPLY #FULL-NSLI-AMT BY 2
    END-IF
*
    IF #SCN-POL-TYPE (#INDEXS) NE 'PUA'
      IF #FULL-NSLI-AMT GT 10000
        REINPUT 'ORIGINAL NSLI AMOUNT CANNOT EXCEED $10,000'
          MARK *#SCN-POL-AMOUNT (#INDEXS)
      END-IF
    END-IF
  END-IF
*
  IF #SCN-POL-NAME(#INDEXS) = 'AFRBA' OR = 'AFRABA' OR = 'AFBA'
    IF #SCN-POL-TYPE(#INDEXS) = 'BASC' OR = 'SUP1' OR = 'SUP2'
        OR = 'SUP3' OR = 'SUP4' OR = 'SUP5' OR = 'OL'
        OR = 'UL' OR = 'TERM' OR = 'GPTM'
      IGNORE
    ELSE
      REINPUT WITH 'INVALID TYPE FOR COMPANY NAME'
        MARK *#SCN-POL-TYPE(#INDEXS)
    END-IF
  END-IF
*
*  CODE FOR WHEN THEY ARE READY TO STANDARDIZE TYPES.
*
* IF #SCN-POL-NAME(#INDEXS) = 'AFRBA' OR = 'AFRABA' OR = 'AFBA'
*    OR = 'VGLI' OR = 'SGLI' OR = 'NSLI' OR = 'AMAA' OR = 'AAFMAA'
*    OR = 'MBA' OR = 'OBA' OR = 'USBA' OR = 'AUSA' OR = 'AFA'
*          IGNORE
* ELSE
*    IF #SCN-POL-TYPE(#INDEXS) = 'OL' OR = 'TERM'
*          OR = 'PUA' OR = 'ACCD' OR = 'TRVL' OR = 'GPTM' OR = 'ML'
*          OR = 'DT' OR = 'AL' OR = 'UL' OR = 'VL' OR = 'END'
*          OR = 'UNKN' OR = 'ANN' OR = ' ' OR = 'LT' OR = 'ART'
*          OR = 'TRAV'
*             IGNORE
*    ELSE
*          REINPUT 'INVALID PLAN TYPE'
*          MARK *#SCN-POL-TYPE(#INDEXS)
*    END-IF
* END-IF
*
  IF #SCN-POL-NAME(#INDEXS) = 'SGLI' OR = 'VGLI'
    MOVE #SCN-POL-AMOUNT (#INDEXS) TO #FULL-NSLI-AMT
    DIVIDE 10000 INTO #FULL-NSLI-AMT REMAINDER #NSLI-REMAINDER
*
    IF #NSLI-REMAINDER NE 0
      REINPUT 'AMOUNT MUST BE A MULTIPLE OF $10,000'
        MARK *#SCN-POL-AMOUNT (#INDEXS)
    END-IF
*
    IF  (DATE-RETIRED GE 20050901)   /* DW5
        OR (CHIEF-OF-STAFF-IND = 'X') /* RECALLED TO ACTIVE DUtY
      IF #SCN-POL-AMOUNT (#INDEXS) GT 400000
        REINPUT 'AMOUNT CANNOT EXCEED $400,000'
          MARK *#SCN-POL-AMOUNT (#INDEXS)
      END-IF
    ELSE
      IF  (DATE-RETIRED GE 20010401)
          OR (CHIEF-OF-STAFF-IND = 'X') /* RECALLED TO ACTIVE DUtY
        IF #SCN-POL-AMOUNT (#INDEXS) GT 250000
          REINPUT 'AMOUNT CANNOT EXCEED $250,000'
            MARK *#SCN-POL-AMOUNT (#INDEXS)
        END-IF
      ELSE
        IF DATE-RETIRED GE 19921201
          IF #SCN-POL-AMOUNT (#INDEXS) GT 200000
            REINPUT 'AMOUNT CANNOT EXCEED $200,000'
              MARK *#SCN-POL-AMOUNT (#INDEXS)
          END-IF
        ELSE
          IF DATE-RETIRED GE 19910405
            IF #SCN-POL-AMOUNT (#INDEXS) GT 100000
              REINPUT 'AMOUNT CANNOT EXCEED $100,000'
                MARK *#SCN-POL-AMOUNT (#INDEXS)
            END-IF
          ELSE
            IF DATE-RETIRED NE 0
              IF #SCN-POL-AMOUNT (#INDEXS) GT 50000
                REINPUT 'AMOUNT CANNOT EXCEED $50,000'
                  MARK *#SCN-POL-AMOUNT (#INDEXS)
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  MOVE #SCN-POL-DATE(#INDEXS) TO #HOLD-SCN-POL-DATE
  IF #HSPD-MM = '  '
    MOVE '00' TO #HSPD-MM
    MOVE '/' TO #HSPD-SLASH
    MOVE '00' TO #HSPD-YY
  END-IF
*
  IF #HSPD-SLASH = '/'
    IGNORE
  ELSE
    MOVE #SCN-POL-DATE(#INDEXS) TO #REFORMAT-DATE
    MOVE #RD-MM TO #HSPD-MM
    MOVE #RD-YY TO #HSPD-YY
    MOVE '/' TO #HSPD-SLASH
    MOVE #HOLD-SCN-POL-DATE TO #SCN-POL-DATE(#INDEXS)
  END-IF
*
  IF #HSPD-SLASH = '/'
    IF #HSPD-MM-N >= 0 AND #HSPD-MM-N < 13
      IF #HSPD-YY-N >= 0 AND #HSPD-YY-N <= 99
        IGNORE
      ELSE
        REINPUT WITH 'INVALID DATE FORMAT'
          MARK *#SCN-POL-DATE(#INDEXS)
      END-IF
    ELSE
      REINPUT WITH 'INVALID DATE FORMAT'
        MARK *#SCN-POL-DATE(#INDEXS)
    END-IF
  ELSE
    REINPUT WITH 'INVALID DATE FORMAT- MM/YY'
      MARK *#SCN-POL-DATE(#INDEXS)
  END-IF
*
  IF #HSPD-MM-N = 0
    MOVE 0 TO #HOLD-CONVERT-POL-DATE
  ELSE
    MOVE '01' TO #HCPD-DD
    IF #HSPD-YY-N GT 20  /* bz2
      MOVE '19' TO #HCPD-CC
    ELSE
      MOVE '20' TO #HCPD-CC
    END-IF
*
    MOVE #HSPD-MM TO #HCPD-MM
    MOVE #HSPD-YY TO #HCPD-YY
  END-IF
*
  IF #HOLD-CONVERT-POL-DATE NE 0
    IF   (#SCN-POL-NAME(#INDEXS ) = 'AFBA')
        AND (#HOLD-CONVERT-POL-DATE LT 19890601)
      REINPUT WITH 'MUST HAVE ISSUE DATE > 06/89 OR USE AFRBA'
        MARK *#SCN-POL-DATE(#INDEXS)
    END-IF
*
    IF   (#SCN-POL-NAME(#INDEXS) = 'AFRBA' OR = 'AFRABA')
        AND (#HOLD-CONVERT-POL-DATE GE 19890601)
      REINPUT WITH 'MUST HAVE ISSUE DATE < 06/89 OR USE AFBA'
        MARK *#SCN-POL-DATE(#INDEXS)
    END-IF
  END-IF
*
  IF   (#SCN-POL-NAME(#INDEXS) = 'AMAA ' OR = 'AAFMAA')
    IF #INDEXS > #INDEXHAI
      REINPUT WITH 'CANNOT ADD AMAA OR AAFMAA POLICIES'
        MARK *#SCN-POL-NAME(#INDEXS)
    END-IF
  END-IF
*
  MOVE DATE-OF-BIRTH TO #HOLD-CONVERT-DATE
  IF #HCD-MMDD GT #WCD-MMDD
    ADD 1 TO #HCD-YYYY
  END-IF
*
  COMPUTE #AGE = #WCD-YYYY - #HCD-YYYY
  MOVE #SCN-POL-AMOUNT(#INDEXS) TO #HOLD-POL-PASS-AMOUNT
  ASSIGN #PDX-MILITARY-STATUS = MILITARY-STATUS
  ASSIGN #PDX-DATE-OF-BIRTH = DATE-OF-BIRTH
  ASSIGN #PDX-MILITARY-RANK = RANK
  ASSIGN #PDX-DISABILITY-PCT = DISABILITY-PCT
  ASSIGN #ERROR-FLAG = FALSE
*
  IF RANK = 'CDT'
    MOVE 'Y' TO #CADET-FLAG
  ELSE
    MOVE 'N' TO #CADET-FLAG
  END-IF
*
  ASSIGN #PDX-MILITARY-SERVICE = MILITARY-SERVICE
  ASSIGN #PDX-DATE-RETIRED = DATE-RETIRED
*
  IF ACTIVE-DUTY-BASE-DATE = 0
    ASSIGN #PDX-ACTIVE-DUTY-BASE-DATE = BASE-PAY-EF-DATE
  ELSE
    ASSIGN #PDX-ACTIVE-DUTY-BASE-DATE = ACTIVE-DUTY-BASE-DATE
  END-IF
*
* IF SUSPEND-CODE NE '3'    /* DW3
  IF #SCN-POL-NAME(#INDEXS) = 'AFRBA' OR = 'VGLI' OR = 'SGLI'
      OR = 'AFRABA' OR = 'MBA' OR = 'AFBA'
    MOVE #PDX-MILITARY-STATUS TO #HOLD-STATUS
*
    IF   (#SCN-POL-NAME (#INDEXS) = 'SGLI' OR = 'VGLI')
        AND (CHIEF-OF-STAFF-IND = 'X')        /* RECALLED TO ACT DUTY
      MOVE 'A' TO #PDX-MILITARY-STATUS
    END-IF
*
    CALLNAT 'E9010SDX' #CGA-ORIG-COMMAND #AGE #P-DX-PARMS
      #SCN-POL-NAME(#INDEXS) #SCN-POL-TYPE(#INDEXS)
      #HOLD-POL-PASS-AMOUNT #HOLD-CONVERT-POL-DATE
      #SCN-POL-REMARKS(#INDEXS) #ERROR-FLAG #PDX-DISABILITY-PCT
      DEATH-SERVICE-CON                          /* DW2
    MOVE #HOLD-STATUS TO #PDX-MILITARY-STATUS
    MOVE #HOLD-POL-PASS-AMOUNT TO #SCN-POL-AMOUNT(#INDEXS)
    IF #SCN-POL-NAME(#INDEXS) = 'SGLI' OR = 'VGLI'
      COMPRESS #HCPD-MM '/' #HCPD-YY INTO #SCN-POL-DATE(#INDEXS)
        LEAVING NO SPACE
    END-IF
*
    IF #ERROR-FLAG
      REINPUT WITH 'INVALID PLAN OR AMOUNT FOR COMPANY NAME'
        MARK *#SCN-POL-NAME(#INDEXS)
    END-IF
  ELSE
    IF #SCN-POL-NAME(#INDEXS) = 'OBA' OR = 'USBA' OR = 'AUSA'
        OR = 'AFA'
      CALLNAT 'E9020SDX' #CGA-ORIG-COMMAND #AGE #P-DX-PARMS
        #SCN-POL-NAME(#INDEXS) #SCN-POL-TYPE(#INDEXS)
        #HOLD-POL-PASS-AMOUNT #HOLD-CONVERT-POL-DATE
        #SCN-POL-REMARKS(#INDEXS) #ERROR-FLAG #CADET-FLAG
      MOVE #HOLD-POL-PASS-AMOUNT TO #SCN-POL-AMOUNT(#INDEXS)
      IF #ERROR-FLAG
        REINPUT WITH 'INVALID PLAN OR AMOUNT FOR COMPANY NAME'
          MARK *#SCN-POL-NAME(#INDEXS)
      END-IF
    END-IF
  END-IF
* END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MOVE-SCREEN
* * * * * * * * * * * * * * * *
*
IF #INDEXH GT 40
  MOVE #HOLD-INDEXH TO #INDEXH
  REINPUT WITH 'LIMIT OF 40 POLICIES - REMOVE EXCESS POLICIES'
    MARK *#SCN-POL-NAME (#INDEXS)
END-IF
*
MOVE #SCN-POL-CODE(#INDEXS)    TO #HOLD-POL-CODE(#INDEXH)
MOVE #SCN-POL-TYPE(#INDEXS)    TO #HOLD-POL-TYPE(#INDEXH)
MOVE #SCN-POL-NAME(#INDEXS)    TO #HOLD-POL-NAME(#INDEXH)
MOVE #SCN-POL-REMARKS(#INDEXS) TO #HOLD-POL-REMARKS(#INDEXH)
MOVE #SCN-POL-AMOUNT(#INDEXS)  TO #HOLD-POL-AMOUNT(#INDEXH)
MOVE #SCN-POL-NUMBER(#INDEXS)  TO #HOLD-POL-NUMBER(#INDEXH)
MOVE #HOLD-CONVERT-POL-DATE    TO #HOLD-POL-DATE(#INDEXH)
ADD 1 TO #INDEXH
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE TRLOG-WRITE
* * * * * * * * * * * * * * * *
*
MOVE STATUS-VIEW.NAME               TO TRLOG-VIEW.NAME
MOVE NEW-FIRS-VIEW.ID-NUMBER        TO TRLOG-VIEW.ID-NUMBER
MOVE NEW-FIRS-VIEW.INS-LAST-FM-DATE TO TRLOG-VIEW.DATE-LAST-UPDATE
MOVE #CGA-CLERK    TO TRLOG-VIEW.CLERK-ID
MOVE *INIT-ID      TO TRLOG-VIEW.TERMINAL-ID
MOVE *DATN         TO TRLOG-VIEW.TRANSACTION-DATN
MOVE *TIMN         TO TRLOG-VIEW.TRANSACTION-TIMN
MOVE 'DP-'          TO TRLOG-VIEW.NEW-DP (1)
STORE TRLOG-VIEW
*
END-SUBROUTINE /* (1476)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-RECORD
* * * * * * * * * * * * * * * * *
*
RESET POL-ID(*)
FOR #INDEXH 1 TO 40
*  IF #HOLD-POL-NAME(#INDEXH) = " " ESCAPE TOP END-IF
*  PRINT *PROGRAM *LINE #HOLD-POL-DATE(#INDEXH) '-'  #HOLD-POL-NAME(#INDEXH)
  MOVE #HOLD-POL-CODE(#INDEXH) TO NEW-FIRS-VIEW.POL-CODE (#INDEXH)
  MOVE #HOLD-POL-NAME(#INDEXH) TO NEW-FIRS-VIEW.POL-NAME (#INDEXH)
  MOVE #HOLD-POL-TYPE(#INDEXH) TO NEW-FIRS-VIEW.POL-TYPE(#INDEXH)
  MOVE #HOLD-POL-REMARKS(#INDEXH) TO
    NEW-FIRS-VIEW.POL-REMARKS (#INDEXH)
  MOVE #HOLD-POL-NUMBER(#INDEXH) TO NEW-FIRS-VIEW.POL-NUMBER (#INDEXH)
  MOVE #HOLD-POL-AMOUNT(#INDEXH) TO NEW-FIRS-VIEW.POL-AMOUNT (#INDEXH)
* Assign POL-ID - added for Member Center
  IF NEW-FIRS-VIEW.POL-NAME (#INDEXH) <> ' ' OR NEW-FIRS-VIEW.POL-AMOUNT (#INDEXH) <> 0
      OR NEW-FIRS-VIEW.POL-TYPE(#INDEXH) <> ' '             /* YK1
    NEW-FIRS-VIEW.POL-ID (#INDEXH) := #INDEXH             /* YK1
  END-IF                                                   /* YK1
*
  IF NEW-FIRS-VIEW.POL-NAME (#INDEXH) = 'NSLI'
    RESET #OVER-AGE
*
    IF   (NEW-FIRS-VIEW.POL-TYPE (#INDEXH) = 'ML65')
        AND ((DATE-OF-BIRTH + 650000) LE *DATN)               /* TT1
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    IF   (NEW-FIRS-VIEW.POL-TYPE (#INDEXH) = 'ML70')
        AND ((DATE-OF-BIRTH + 700000) LE *DATN)                /* TT1
      MOVE TRUE TO #OVER-AGE
    END-IF
*
    IF #OVER-AGE
      MULTIPLY NEW-FIRS-VIEW.POL-AMOUNT (#INDEXH) BY 2
    END-IF
  END-IF
*
  MOVE #HOLD-POL-DATE(#INDEXH)   TO NEW-FIRS-VIEW.POL-DATE (#INDEXH)
*
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE NEW-MOVE
* * * * * * * * * * * * * *
*
MOVE TRUE TO #FIELD-CHANGED
MOVE #SCN-POL-CODE(#INDEXS)    TO NEW-CODE(#INDEXT)
MOVE #SCN-POL-TYPE(#INDEXS)    TO NEW-TYPE(#INDEXT)
MOVE #HSPD-MM                  TO NEW-DATE-MM(#INDEXT)
MOVE #HSPD-YY                  TO NEW-DATE-YY(#INDEXT)
MOVE #SCN-POL-AMOUNT(#INDEXS)  TO NEW-AMOUNT(#INDEXT)
MOVE #SCN-POL-NUMBER(#INDEXS)  TO NEW-NUMBER(#INDEXT)
MOVE #SCN-POL-REMARKS(#INDEXS) TO NEW-REMARKS(#INDEXT)
MOVE #SCN-POL-NAME(#INDEXS)    TO NEW-NAME(#INDEXT)
ADD 1 TO #INDEXT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE OLD-MOVE
* * * * * * * * * * * * * * *
*
MOVE NEW-FIRS-VIEW.POL-CODE(#INDEXV)    TO OLD-CODE(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-TYPE(#INDEXV)    TO OLD-TYPE(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-DATE(#INDEXV)    TO #HOLD-CONVERT-DATE
MOVE #HCD-MM                            TO OLD-DATE-MM(#INDEXT)
MOVE #HCD-YY                            TO OLD-DATE-YY(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-AMOUNT(#INDEXV)  TO OLD-AMOUNT(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-NUMBER(#INDEXV)  TO OLD-NUMBER(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-REMARKS(#INDEXV) TO OLD-REMARKS(#INDEXT)
MOVE NEW-FIRS-VIEW.POL-NAME(#INDEXV)    TO OLD-NAME(#INDEXT)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE AMAA-FILE-WRITE
* * * * * * * * * * * * * * * * *
MOVE #CGA-ISN TO #ISN
GET DP-S01-STATUS #ISN
ASSIGN #ERROR-FLAG = FALSE
*
IF   (#HOLD-DATN NE DP-S01-STATUS.DTS-DATN)
    AND (#HOLD-TIMN NE DP-S01-STATUS.DTS-TIMN)
  MOVE 'AMAA POLICY CODE NOT UPDATED, REDO' TO #SUP-MESSAGE
ELSE
  ASSIGN #HAI-LIMIT = #INDEXHAI
  FOR #INDEXHAI = 1 TO #HAI-LIMIT
    PERFORM AMAA-UPDATE
  END-FOR
  IF #ERROR-FLAG
    IGNORE
  ELSE
    MOVE *DATN TO DTS-DATN
    MOVE *TIMN TO DTS-TIMN
    UPDATE (1573) END TRANSACTION
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE AMAA-UPDATE
* * * * * * * * * * * * * * *
*
FOR #INDEXP = 1 TO 9
  IF   (DP-S01-STATUS.ISSUE-DATE(#INDEXP) = #HAI-ISSUE-DATE (#INDEXHAI))
      AND (DP-S01-STATUS.SPLIT-IND(#INDEXP)  = #HAI-SPLIT-IND  (#INDEXHAI))
    MOVE #HAI-ALOT-SW TO DP-S01-STATUS.ALOT-SW        /* DW4
    MOVE #HAI-NEW-FIRS-CODE(#INDEXHAI)
      TO DP-S01-STATUS.FIRS-LOCATION-CODE(#INDEXP)
    ESCAPE ROUTINE
  END-IF
END-FOR
*
#SECONDARY-CN(*) := FUNCTION-SECONDARY-CN(<#ID-NUMBER>)(*)                    /* PM10
FOR #J 1 TO 25                                                                /* PM10
  IF #SECONDARY-CN (#J) NE 0                                                  /* PM10
    S01. FIND (1) STAT-V WITH STAT-V.ID-NUMBER = #SECONDARY-CN (#J)           /* PM10
      FOR #INDEXP = 1 TO 9                                                    /* PM10
        IF   (STAT-V.ISSUE-DATE(#INDEXP) = #HAI-ISSUE-DATE (#INDEXHAI))       /* PM10
            AND (STAT-V.SPLIT-IND(#INDEXP)  = #HAI-SPLIT-IND  (#INDEXHAI))       /* PM10
          MOVE #HAI-ALOT-SW TO STAT-V.ALOT-SW                                 /* PM10
          MOVE #HAI-NEW-FIRS-CODE(#INDEXHAI)                                  /* PM10
            TO STAT-V.FIRS-LOCATION-CODE(#INDEXP)                            /* PM10
          UPDATE (S01.)                                                       /* PM10
          ESCAPE ROUTINE                                                      /* PM10
        END-IF                                                                /* PM10
      END-FOR                                                                 /* PM10
    END-FIND                                                                  /* PM10
  END-IF                                                                      /* PM10
END-FOR                                                                       /* PM10
*
MOVE 'ERROR IN AMAA MATCH' TO #SUP-MESSAGE
ASSIGN #ERROR-FLAG = TRUE
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ADD-TO-AMOUNT
* * * * * * * * * * * * * * * *
*
IF #SCN-POL-CODE(#INDEXS) = '*' OR = ' '
  IF #SCN-POL-TYPE(#INDEXS) = 'TRAV' OR = 'ACCD' OR = 'TRVL'
    IGNORE
  ELSE
    COMPUTE #HOLD-TOTAL-AMOUNT = #HOLD-TOTAL-AMOUNT +
      #SCN-POL-AMOUNT(#INDEXS)
  END-IF
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE STATUS-INS-UPDATE
* * * * * * * * * * * * * * * * * *
*
GET DP-STATUS #CGA-ISN
ASSIGN DTS-DATN = *DATN
ASSIGN DTS-TIMN = *TIMN
ASSIGN TOTAL-INS-AMT = #HOLD-TOTAL-AMOUNT
UPDATE (1650) END TRANSACTION
*
END-SUBROUTINE
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DATE-REFORMAT
* * * * * * * * * * * * * * * *
*
IF (#WYYYYMMDD.#WMM GT 00) AND (#WYYYYMMDD.#WMM LT 13)
  MOVE #WYYYYMMDD.#WMM TO #I1
  MOVE EDITED #WYYYYMMDD.#WDD (EM=Z9) TO #WYYYYMMDD.#WDD-A
  COMPRESS #WYYYYMMDD.#WDD #MON (#I1) #WYYYYMMDD.#WYYYY-A
    INTO #W-DATE-EDITED LEAVING NO
ELSE
  MOVE '    ?    ' TO #W-DATE-EDITED
END-IF
*
END-SUBROUTINE
*
END
