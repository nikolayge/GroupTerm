* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2340PCI   *** FAM RIDER PROCESSOR
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    09282004  DW1     add CREDIT CARD processing                  *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
*
* below is linkage to GRIDCSXX
1 #ID         (N6)
1 REDEFINE #ID
 2 #ID-A      (A6)
* THE FOLLOWING INPUT IS PASSED ONLY ON AN "AT" FOR A NEW RIDER
1 #AT-RID-ATTACH-FAC-AMT (N6.2) /* Only if attached to the new "AT"
1 #AT-RID-ATTACH-PLAN    (A3)   /*    policy not yet created
1 #AT-RID-PLAN           (A2/5) /* New rider plan
1 #AT-RID-ATTACH-ISS-DT  (N8/5) /* Only if attached to an existing pol
* output below  (ex = existing)
1 #AT-TOT-FBR-PREM        (N3.2)
1 #EX-FBR-PREM-BY-LINE    (N3.2/9)
1 #EX-SPS-DB-BY-LINE      (N6/9)
1 #EX-DEP-DB-BY-LINE      (N6/9)
1 #EX-EACH-DEP-DB-BY-LINE (N6/9)
1 #NUM-ELIG-CHILDREN      (N2)
1 #NUM-ELIG-CHILDREN-UNDER-6-MON (N2)
1 #TOT-CHILD-DB      (N6)
1 #RID-TEXT          (A78)
* above is linkage to GRIDCSXX
*
1 ST-V VIEW OF A-STATUS
 2 DTS-DATN
 2 DTS-TIMN
 2 ID-NUMBER      (N6)
 2 PROCESS-IND    (A1)  /* PM - 06/2000 - SPLIT COMPANIES
 2 DATE-ACCT-UPDATE (N8)
 2 STATUS         (A1/9)
 2 ISSUE-DATE     (N8/9)
 2 MODE           (A1/9)
 2 PLAN           (A3/9)
 2 FACE-AMOUNT    (P7.2/9)
*
 2 RID-PLAN     (A2/9)
 2 REDEFINE RID-PLAN
  3 RID-PLAN-OCC  (9)
   4 RID-PLAN-1   (A1)
   4 RID-PLAN-2   (N1)
 2 RID-EFF-DT     (N8/9)
 2 RID-PD-TO      (N8/9)
 2 REDEFINE RID-PD-TO
  3 RID-PD-TO-OCC  (9)
   4 RID-PD-TO-YYYY (N4)
   4 RID-PD-TO-MM   (N2)
   4 RID-PD-TO-DD   (N2)
* 2 X-RID-FPF        (P3.2/9)
 2 X-RID-BALANCE    (P5.2/9)
 2 RID-TOT-PAID   (P5.2)
*
1 #FILE-MATCHING-INDX (I2/9)    /* MATCHING INDEX
1 #SCR-TRAN         (A4/9)
1 #SCR-POL-EFF-DT   (A8/9)
1 #SCR-POL-FACE-AMT (A10/9)
1 #SCR-POL-PLAN     (A3/9)
1 #SCR-RID-EFF-DT   (A8/9)
1 #SCR-RID-PD-TO    (A6/9)
1 REDEFINE #SCR-RID-PD-TO
 2 #SCR-RID-PD-TO-OCC  (9)
  3 #SCR-RID-PD-TO-MM    (N2)
  3 #SCR-RID-PD-TO-YYYY  (N4)
1 #SCR-MODE         (A1/9)
1 #SCR-RID-PLAN     (A2/9)
1 REDEFINE #SCR-RID-PLAN
 2 #SCR-RID-PLAN-OCC  (9)
  3 #SCR-RID-PLAN-1  (A1)
  3 #SCR-RID-PLAN-2  (N1)
1 #SCR-RID-EFF-DT-YYYYMMDD (N8/9)
1 #SCR-RID-PD-TO-YYYYMMDD  (N8/9)
1 #SCR-MODAL-PRM (N5.2/9)
1 #OLD-RID-PLAN   (A2/9)
1 #OLD-RID-PD-TO  (A8/9)
1 #OLD-RID-EFF-DT (A8/9)
*
1 #YYYYMMDD         (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM     (N6)
 2 REDEFINE #YYYYMM
   3 #YYYY     (N4)
   3 #MM       (N2)
 2 #DD         (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A     (A4)
 2 #MM-A       (A2)
 2 #DD-A       (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A  (A8)
*
1 #MMDDYYYY-A       (A8)
1 REDEFINE #MMDDYYYY-A
 2 #MMDDYYYY-MMDD-A (A4)
 2 #MMDDYYYY-YYYY-A (A4)
*
1 #MMYYYY-A       (A6)
1 REDEFINE #MMYYYY-A
 2 #MMYYYY-MM-A   (A2)
 2 #MMYYYY-YYYY-A (A4)
*
1 #I1      (I2)
1 #I2      (I2)
1 #ISC     (I2)
1 #IFL     (I2)
1 #ITR1    (I2)
1 #SCR-CV  (C)
1 #REC-CHGD (L)
1 #TEXT     (A75)
1 #DATE-DISP  (A10)
1 #HOLD-TIME (P7)
1 #WORK-MONTHS  (P3)
1 #WORK-AMT     (P5.2)
1 #WORK-FREQ    (N2)
1 #NEW-MON-PREM (P5.2)
1 #OLD-MON-PREM (P5.2)
1 #DIFF-MON-PREM (P5.2)
1 #WRITE-NOTE-RECORD  (L)
1 #NOTE-TEXT     (A60/10)
1 #INT           (I2)
*
1 TR-REG-V VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER    (N6)
 2 PROCESS-IND    (A1)  /* PM - 06/2000 - SPLIT COMPANIES
 2 NAME         (A25)
 2 DATE-LAST-UPDATE  (N8)
 2 TERMINAL-ID       (A8)
 2 CLERK-ID          (A3)
 2 TRANSACTION-DATN  (P9)
 2 TRANSACTION-TIMN  (P7)
 2 OLD-DATA          (A125/20)
 2 NEW-DATA          (A125/20)
*
1 #TR-REG-MODE-GR
 2 #TR-REG-MODE-LBL-1         (A25) INIT <'CI-RID-PDTO-CHG ISSUE-DT '>
 2 #TR-REG-MODE-ISSUE-DATE    (N08)
 2 #TR-REG-MODE-LBL-2         (A10) INIT <'  PAID-TO '>
 2 #TR-REG-MODE-PAID-TO       (N08)
1 REDEFINE #TR-REG-MODE-GR
 2 #TR-REG-MODE               (A51)
*
1 #TR-REG-CORR-GR
 2 #TR-REG-CORR-LBL-1         (A21) INIT <'CI-RID-CORR ISSUE-DT '>
 2 #TR-REG-CORR-ISSUE-DATE    (N08)
 2 #TR-REG-CORR-LBL-2         (A15) INIT <'  RIDER-EFF-DT '>
 2 #TR-REG-CORR-EFF-DT        (N08)
 2 #TR-REG-CORR-LBL-3         (A07) INIT <'  PLAN '>
 2 #TR-REG-CORR-PLAN          (A02)
1 REDEFINE #TR-REG-CORR-GR
 2 #TR-REG-CORR               (A61)
*
1 #TR-REG-RSGN-GR
 2 #TR-REG-RSGN-LBL-1         (A21) INIT <'CI-RID-RSGN ISSUE-DT '>
 2 #TR-REG-RSGN-ISSUE-DATE    (N08)
 2 #TR-REG-RSGN-LBL-2         (A09) INIT <'  EFF-DT '>
 2 #TR-REG-RSGN-EFF-DT        (N08)
 2 #TR-REG-RSGN-LBL-3         (A07) INIT <'  PLAN '>
 2 #TR-REG-RSGN-PLAN          (A02)
 2 #TR-REG-RSGN-LBL-4         (A08) INIT <'  PD-TO '>
 2 #TR-REG-RSGN-PD-TO         (N08)
1 REDEFINE #TR-REG-RSGN-GR
 2 #TR-REG-RSGN               (A71)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX           /* NATURAL ERROR ROUTINE
*
GET ST-V #CGA-ISN
*
MOVE ST-V.ID-NUMBER TO #ID
CALLNAT 'GRIDCSXX' USING   /* TO GET BEFORE UPDATE VALUES
  #ID
  #AT-RID-ATTACH-FAC-AMT
  #AT-RID-ATTACH-PLAN
  #AT-RID-PLAN            (1:5)
  #AT-RID-ATTACH-ISS-DT   (1:5)
  #AT-TOT-FBR-PREM
  #EX-FBR-PREM-BY-LINE    (1:9)
  #EX-SPS-DB-BY-LINE      (1:9)
  #EX-DEP-DB-BY-LINE      (1:9)
  #EX-EACH-DEP-DB-BY-LINE (1:9)
  #NUM-ELIG-CHILDREN
  #NUM-ELIG-CHILDREN-UNDER-6-MON
  #TOT-CHILD-DB
  #RID-TEXT
*
IF #RID-TEXT NE ' '
  WRITE 'FBR Error Messege' #RID-TEXT
END-IF
*
RESET #I2
FOR #I1 1 TO 9
  IF STATUS (#I1) NE 'D'
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #I2
  MOVE #I1 TO #FILE-MATCHING-INDX (#I2)   /* MATCHING INDEX
  MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-POL-EFF-DT (#I2) LEAVING NO
*
  IF RID-PLAN (#I1) NE '  '
  MOVE RID-PLAN (#I1) TO #SCR-RID-PLAN (#I2)
    MOVE RID-EFF-DT (#I1) TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-RID-EFF-DT (#I2) LEAVING NO
*
    MOVE RID-PD-TO  (#I1) TO #YYYYMMDD
    COMPRESS #MM-A #YYYY-A INTO #SCR-RID-PD-TO (#I2) LEAVING NO
  END-IF
*
  MOVE EDITED FACE-AMOUNT(#I1) (EM=ZZZ,ZZZ.99) TO #SCR-POL-FACE-AMT(#I2)
  MOVE PLAN     (#I1) TO #SCR-POL-PLAN (#I2)
  MOVE MODE     (#I1) TO #SCR-MODE     (#I2)
*
  IF #SCR-MODE (#I2) = 'A' OR = '1'   /* DW1
    MOVE  12 TO #WORK-FREQ
  ELSE
    IF #SCR-MODE (#I2) = 'S' OR = '2'  /* DW1
      MOVE 6 TO #WORK-FREQ
    ELSE
      IF #SCR-MODE (#I2) = 'Q' OR = '4'  /* DW1
        MOVE 3 TO #WORK-FREQ
      ELSE
        MOVE 1 TO #WORK-FREQ
      END-IF
    END-IF
  END-IF
*
  COMPUTE #SCR-MODAL-PRM (#I2)
       = #EX-FBR-PREM-BY-LINE (#I1) * #WORK-FREQ
*
  MOVE #SCR-RID-PLAN  (#I2) TO #OLD-RID-PLAN   (#I2)
  MOVE #SCR-RID-PD-TO (#I2) TO #OLD-RID-PD-TO  (#I2)
  MOVE #SCR-RID-EFF-DT(#I2) TO #OLD-RID-EFF-DT (#I2)
END-FOR
*
MOVE '*'              TO #CGA-SCR-COMMAND
MOVE #CGA-ORIG-ID     TO #CGA-SCR-ID
MOVE #CGA-ORIG-SUFFIX TO #CGA-SCR-SUFFIX
INPUT TEXT #TEXT MARK *#SCR-TRAN (1) USING MAP 'C2340MCI'
*
IF #CGA-SCR-COMMAND NE '*'
  END TRANSACTION
  FETCH 'G1000PXX'
END-IF
*
* * * * * * * * * * * * * EDIT SCREEN * * * * * * * * * * * * * *
*
RESET #TEXT #REC-CHGD
*
FOR #ISC 1 TO 9
  IF #SCR-TRAN (#ISC) = ' '
      ESCAPE TOP
  END-IF
*
  MOVE #FILE-MATCHING-INDX (#ISC) TO #IFL    /* RESTORE MATCHING INDEX
*
  IF  (#SCR-TRAN (#ISC) NE ' ')
   AND (ST-V.RID-PLAN (#IFL) = ' ')
    REINPUT 'TRAN invalid without RIDER Ins'
    MARK *#SCR-TRAN (#ISC)
  END-IF
*
  IF   (#SCR-RID-EFF-DT (#ISC) NE ' ')
   AND (#SCR-RID-EFF-DT (#ISC) NE MASK (MMDDYYYY))
    REINPUT 'Date must be format "MMDDYYYY"'
    MARK *#SCR-RID-EFF-DT (#ISC)
  END-IF
*
  IF   (#SCR-RID-PD-TO (#ISC) NE ' ')
   AND (#SCR-RID-PD-TO (#ISC) NE MASK (MMYYYY))
    REINPUT 'Date must be format "MMYYYY"'
    MARK *#SCR-RID-PD-TO (#ISC)
  END-IF
*
  MOVE #SCR-RID-EFF-DT (#ISC) TO #MMDDYYYY-A
  COMPRESS #MMDDYYYY-YYYY-A #MMDDYYYY-MMDD-A
       INTO #YYYYMMDD-A  LEAVING NO
  MOVE #YYYYMMDD TO #SCR-RID-EFF-DT-YYYYMMDD (#ISC)
*
  MOVE #SCR-RID-PD-TO (#ISC) TO #MMYYYY-A
  MOVE #MMYYYY-YYYY-A        TO #YYYY-A
  MOVE #MMYYYY-MM-A          TO #MM-A
  MOVE '15'                  TO #DD-A
  MOVE #YYYYMMDD TO #SCR-RID-PD-TO-YYYYMMDD (#ISC)
END-FOR /*(0264)
*
* * * * * * * * * UPDATE VIEW  * * * * * * * * * * * * * * * * *
*
MOVE *TIMN TO #HOLD-TIME
MOVE 1 TO #ITR1
RESET TR-REG-V                          /* INITIALIZE TRANS-REGISTER
MOVE ID-NUMBER        TO TR-REG-V.ID-NUMBER
MOVE PROCESS-IND      TO TR-REG-V.PROCESS-IND /* PM -06/2000 - SPLIT COM
MOVE NAME             TO TR-REG-V.NAME
MOVE DATE-ACCT-UPDATE TO TR-REG-V.DATE-LAST-UPDATE
MOVE *INIT-ID     TO TR-REG-V.TERMINAL-ID
MOVE #CGA-CLERK   TO TR-REG-V.CLERK-ID
MOVE *DATN        TO TR-REG-V.TRANSACTION-DATN
MOVE #HOLD-TIME   TO TR-REG-V.TRANSACTION-TIMN
*
FOR #ISC 1 TO 9
  IF #SCR-TRAN (#ISC) = ' '
      ESCAPE TOP
  END-IF
*
  MOVE #FILE-MATCHING-INDX (#ISC) TO #IFL    /* RESTORE MATCHING INDEX
*
  DECIDE ON FIRST VALUE OF #SCR-TRAN (#ISC)
    VALUE 'CORR' PERFORM RIDER-CORR  /* TO CHG ISSUE-DATE
    VALUE 'MODE' PERFORM RIDER-MODE  /* TO CHG PAID-THRU DATE
    VALUE 'RSGN' PERFORM RIDER-RSGN  /* TO CANCEL
    VALUE 'LAPS' PERFORM RIDER-RSGN  /* TO CANCEL
    NONE REINPUT 'Enter a valid TRAN Code (CORR/MODE/RSGN/LAPS)'
         MARK *#SCR-TRAN (#ISC)
  END-DECIDE
END-FOR /*(0315)
*
MOVE (AD=P) TO #SCR-CV
COMPRESS #TEXT  'Check it out - Press ENTER if OK' INTO #TEXT
INPUT TEXT #TEXT USING MAP 'C2340MCI'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
* * * * * * * * * * UPDATE RECORDS * * * * * * * * * *
*
MOVE *DATN      TO DATE-ACCT-UPDATE
MOVE *DATN      TO DTS-DATN
MOVE #HOLD-TIME TO DTS-TIMN
*
IF   #REC-CHGD
  UPDATE RECORD (0181)
  STORE TR-REG-V
  IF #WRITE-NOTE-RECORD
    CALLNAT 'E4205SDM' ST-V.ID-NUMBER
                      #INT
                      #CGA-CLERK
                      #NOTE-TEXT      (1:10)
 END-IF
*
  MOVE 'Trans completed' TO #TEXT
ELSE
  MOVE 'No update processed' TO #TEXT
END-IF
*
END TRANSACTION                 /* ACTUALLY UPDATE RECORD (S)
*
MOVE (AD=P) TO #SCR-CV
INPUT TEXT #TEXT USING MAP 'C2340MCI'
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE RIDER-MODE   /*  TO UPDATE PAID-THRU-DATE
* * * * * * * * * * * * * * *
*
IF #SCR-RID-PD-TO  (#ISC) = #OLD-RID-PD-TO (#ISC)
  REINPUT 'Paid-Thru-Date not changed'
  MARK *#SCR-RID-PD-TO (#ISC)
END-IF
*
MOVE ST-V.ISSUE-DATE(#IFL) TO #TR-REG-MODE-ISSUE-DATE /* TRANS-REGISTER
MOVE ST-V.RID-PD-TO (#IFL) TO #TR-REG-MODE-PAID-TO
MOVE #TR-REG-MODE           TO TR-REG-V.OLD-DATA (#ITR1)
MOVE #SCR-RID-PD-TO-YYYYMMDD (#ISC) TO #TR-REG-MODE-PAID-TO
MOVE #TR-REG-MODE TO TR-REG-V.NEW-DATA (#ITR1)
ADD 1 TO #ITR1
*
MOVE #SCR-RID-PD-TO-MM (#ISC) TO #WORK-MONTHS
*
IF ST-V.RID-PD-TO-YYYY (#IFL) GT #SCR-RID-PD-TO-YYYY (#ISC)
  COMPUTE #WORK-MONTHS = #WORK-MONTHS -
   ((ST-V.RID-PD-TO-YYYY (#IFL) - #SCR-RID-PD-TO-YYYY (#ISC)) * 12)
ELSE
  IF #SCR-RID-PD-TO-YYYY (#ISC) GT ST-V.RID-PD-TO-YYYY (#IFL)
    COMPUTE #WORK-MONTHS = #WORK-MONTHS +
   ((#SCR-RID-PD-TO-YYYY (#ISC) - RID-PD-TO-YYYY (#IFL)) * 12)
  END-IF
END-IF
*
SUBTRACT RID-PD-TO-MM (#IFL) FROM #WORK-MONTHS
COMPUTE #WORK-AMT = #EX-FBR-PREM-BY-LINE (#IFL) * #WORK-MONTHS
ADD #WORK-AMT TO ST-V.X-RID-BALANCE (#IFL)
MOVE #SCR-RID-PD-TO-YYYYMMDD (#ISC) TO ST-V.RID-PD-TO (#IFL)
MOVE TRUE TO #REC-CHGD
*
END-SUBROUTINE /*(0368)
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE RIDER-CORR   /*  TO UPDATE PLAN & EFF-DATE
* * * * * * * * * * * * * * *
*
IF   (#SCR-RID-EFF-DT  (#ISC) = #OLD-RID-EFF-DT (#ISC))
 AND (#SCR-RID-PLAN    (#ISC) = #OLD-RID-PLAN   (#ISC))
  REINPUT 'Eff-Date or Plan have not changed'
  MARK *#SCR-RID-PLAN (#ISC)
END-IF
*
IF #SCR-RID-PLAN (#ISC) NE RID-PLAN (#IFL)
  IF NOT #SCR-RID-PLAN-1 (#ISC) = 'F' OR = 'T' OR = 'C'
    REINPUT 'PLAN-1 must be F/T/C'
    MARK *#SCR-RID-PLAN (#ISC)
  END-IF
*
  IF NOT #SCR-RID-PLAN-2 (#ISC) = 1 OR = 2 OR = 3 OR = 4 OR = 5
     REINPUT 'PLAN-2 must be 1/2/3/4/5'
     MARK *#SCR-RID-PLAN (#ISC)
  END-IF
*
  MOVE ST-V.ISSUE-DATE (#IFL) TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-DISP WITH '/'
  MOVE TRUE TO #WRITE-NOTE-RECORD
  ADD 1 TO #INT
  COMPRESS '*Rider Plan changed from' RID-PLAN (#IFL) 'to'
    #SCR-RID-PLAN (#ISC) 'on pol issued' #DATE-DISP
     INTO #NOTE-TEXT (#INT)
*
  MOVE ST-V.RID-PD-TO-MM (#IFL) TO #WORK-MONTHS
  MOVE *DATN TO #YYYYMMDD
*
  IF ST-V.RID-PD-TO-YYYY (#IFL) GT #YYYY
    COMPUTE #WORK-MONTHS = #WORK-MONTHS +
        ((ST-V.RID-PD-TO-YYYY (#IFL) - #YYYY) * 12)
  ELSE
    IF #YYYY GT ST-V.RID-PD-TO-YYYY (#IFL)
      COMPUTE #WORK-MONTHS = #WORK-MONTHS -
          ((#YYYY - RID-PD-TO-YYYY (#IFL)) * 12)
    END-IF
  END-IF
*
  SUBTRACT #MM FROM #WORK-MONTHS /* DIFF BETWEEN TODAY & PD-TO MONTH
*
  IF #SCR-RID-PLAN-2 (#ISC) = MASK (N)
    IF #SCR-RID-PLAN-1 (#ISC) = 'F'
      COMPUTE #NEW-MON-PREM = #SCR-RID-PLAN-2 (#ISC) * 1.50
    ELSE
      IF #SCR-RID-PLAN-1 (#ISC) = 'T'
        COMPUTE #NEW-MON-PREM = #SCR-RID-PLAN-2 (#ISC) * 2.00
      ELSE
        COMPUTE #NEW-MON-PREM = #SCR-RID-PLAN-2 (#ISC) * 1.00
      END-IF
    END-IF
  END-IF
*
  IF RID-PLAN-1 (#IFL) = 'F'
    COMPUTE #OLD-MON-PREM = RID-PLAN-2 (#IFL) * 1.50
  ELSE
    IF RID-PLAN-1 (#IFL) = 'T'
      COMPUTE #OLD-MON-PREM = RID-PLAN-2 (#IFL) * 2.00
    ELSE
      COMPUTE #OLD-MON-PREM = RID-PLAN-2 (#IFL) * 1.00
    END-IF
  END-IF
END-IF
*
COMPUTE #DIFF-MON-PREM = #NEW-MON-PREM - #OLD-MON-PREM
COMPUTE #WORK-AMT = #DIFF-MON-PREM * #WORK-MONTHS
*
MOVE ST-V.ISSUE-DATE (#IFL) TO #TR-REG-CORR-ISSUE-DATE /* TRANS-REGISTER
MOVE ST-V.RID-EFF-DT (#IFL) TO #TR-REG-CORR-EFF-DT
MOVE ST-V.RID-PLAN   (#IFL) TO #TR-REG-CORR-PLAN
MOVE #TR-REG-CORR           TO TR-REG-V.OLD-DATA (#ITR1)
MOVE #SCR-RID-EFF-DT-YYYYMMDD (#ISC) TO #TR-REG-CORR-EFF-DT
MOVE #SCR-RID-PLAN            (#ISC) TO #TR-REG-CORR-PLAN
MOVE #TR-REG-CORR TO TR-REG-V.NEW-DATA (#ITR1)
ADD 1 TO #ITR1
*
ADD #WORK-AMT TO ST-V.X-RID-BALANCE (#IFL)
MOVE #SCR-RID-PLAN            (#ISC) TO ST-V.RID-PLAN   (#IFL)
MOVE #SCR-RID-EFF-DT-YYYYMMDD (#ISC) TO ST-V.RID-EFF-DT (#IFL)
*
MOVE TRUE TO #REC-CHGD
*
END-SUBROUTINE /*(0368)
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE RIDER-RSGN   /*  TO cancel rider
* * * * * * * * * * * * * * *
*
MOVE ST-V.ISSUE-DATE (#IFL) TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-DISP WITH '/'
MOVE TRUE TO #WRITE-NOTE-RECORD
ADD 1 TO #INT
*
IF #SCR-TRAN (#ISC) = 'LAPS'
  MOVE 'CI-RID-LAPS ISSUE-DT ' TO  #TR-REG-RSGN-LBL-1
  COMPRESS '*RIDER PLAN RESIGNED on pol issued' #DATE-DISP
     INTO #NOTE-TEXT (#INT)
ELSE
  MOVE 'CI-RID-RSGN ISSUE-DT ' TO  #TR-REG-RSGN-LBL-1
  COMPRESS '*RIDER PLAN RESIGNED on pol issued' #DATE-DISP
     INTO #NOTE-TEXT (#INT)
END-IF
*
MOVE ST-V.ISSUE-DATE (#IFL) TO #TR-REG-RSGN-ISSUE-DATE /* TRANS-REGISTER
MOVE ST-V.RID-EFF-DT (#IFL) TO #TR-REG-RSGN-EFF-DT
MOVE ST-V.RID-PLAN   (#IFL) TO #TR-REG-RSGN-PLAN
MOVE ST-V.RID-PD-TO  (#IFL) TO #TR-REG-RSGN-PD-TO
MOVE #TR-REG-RSGN           TO TR-REG-V.OLD-DATA (#ITR1)
RESET TR-REG-V.NEW-DATA (#ITR1)
ADD 1 TO #ITR1
*
* COMPUTE #WORK-AMT = ST-V.x-RID-BALANCE (#i1)
*       + ST-V.X-RID-FPF (#I1) /* BALANCE DUE
*
RESET ST-V.RID-PLAN   (#IFL)
RESET ST-V.RID-EFF-DT (#IFL)
RESET ST-V.RID-PD-TO  (#IFL)
*
MOVE TRUE TO #REC-CHGD
*
END-SUBROUTINE /*(0368)
*
END
