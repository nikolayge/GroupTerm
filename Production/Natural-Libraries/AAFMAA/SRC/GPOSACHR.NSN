* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPOSACHR                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Load EZPay ACH rejection transactions                 *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE DATA
PARAMETER USING GPOAACHR
PARAMETER USING GPOAFILE
LOCAL USING GPOLACHR
*
LOCAL
1 #I1                                  (I2)
1 #I2                                  (I2)
1 #ERROR                               (L)
*
END-DEFINE
************************************************************************
*
DEFINE WORK FILE 1 #UNIX-FILE-FOLDER
*
READ WORK 1 #ACH-RECORD
  DECIDE FOR FIRST CONDITION
    WHEN #ACH-RECORD-TYPE = '5'
      PERFORM LOAD-DATE-INDEX
    WHEN #ACH-RECORD-TYPE = '6'
      PERFORM LOAD-MEMBER-INFORMATION
    WHEN #ACH-RECORD-TYPE = '7'
      PERFORM LOAD-REJECTION-INFORMATION
    WHEN NONE
      IGNORE
  END-DECIDE
*
  IF #ERROR
     #ACH-ERROR-CODE := 99
     ESCAPE ROUTINE
  END-IF
END-WORK
*
************************************************************************
DEFINE SUBROUTINE LOAD-DATE-INDEX
************************************************************************
*
FOR #I1 1 TO 12
  IF #ACHR-DATE (#I1) = #ACH-HEADER-AFB-TRAN-DATE
    ESCAPE BOTTOM
  END-IF
*
  IF #ACHR-DATE (#I1) = 0
    #ACHR-DATE (#I1) := #ACH-HEADER-AFB-TRAN-DATE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #I1 LT 1 OR #I1 GT 12
  #ERROR := TRUE
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE LOAD-MEMBER-INFORMATION
************************************************************************
*
FOR #I2 1 TO 500
  IF #ACHR-MEMBER-NUMBER (#I1,#I2) = ' '
    #ACHR-MEMBER-NUMBER (#I1,#I2)    := #ACH-DETAIL-IDENTIFICATION-NUM
    #ACHR-REJECTION-AMOUNT (#I1,#I2) := #ACH-DETAIL-TRAN-AMOUNT / 100
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #I2 LT 1 OR #I2 GT 500
  #ERROR := TRUE
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE LOAD-REJECTION-INFORMATION
************************************************************************
*
FOR #I2 1 TO 500
  IF #ACHR-MEMBER-NUMBER (#I1,#I2) NE ' '
   AND #ACHR-REJECTION-CODE (#I1,#I2) = ' '
    #ACHR-REJECTION-CODE (#I1,#I2) := #ACH-ADDENDA-CHANGE-CODE
    #ACHR-CHANGE-DATA (#I1,#I2)    := #ACH-ADDENDA-DATA
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #I2 LT 1 OR #I2 GT 500
  #ERROR := TRUE
END-IF
*
END-SUBROUTINE
*
END
