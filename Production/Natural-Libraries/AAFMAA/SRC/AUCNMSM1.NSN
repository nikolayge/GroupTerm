* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNMSM1.
/**
/** :author nguentchev
/*
DEFINE DATA
PARAMETER USING AUCAMS01
LOCAL USING OBJADBTS
LOCAL USING GPRABINF
LOCAL USING C2233ACB
LOCAL USING C2234ACB
LOCAL
1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
1 MEMBERSHIP-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 ACCESS-AUTH-CODE
  2 DATE-BOOK
  2 ANNUAL-RPT-FLAG
  2 APPL-SOURCE
  2 BRIDGER-IND
  2 INTRO-FLAG
  2 INS-MAIL-CODE
  2 NEWSLETTER-CODE
  2 PROCESS-IND
  2 PROXY
  2 CHIEF-OF-STAFF-IND
  2 SUSPEND-CODE
1 SRC-APPL-V VIEW OF A-SOURCE-OF-APPL
  2 SOURCE-CODE-GROUP
  2 SOURCE-CODE
  2 SOURCE-DESCRIPTION
1 #GET    (I4) CONST <0>
1 #ADD    (I4) CONST <1>
1 #UPDATE (I4) CONST <2>
1 #DELETE (I4) CONST <3>
1 BLANK   (A1) CONST <' '>
1 #SYNCHRONIZE (L)
1 #ERROR-CODE-A(A3)
1 REDEFINE #ERROR-CODE-A
  2 #ERROR-CODE-N(N3)
*
1 #UNSUPPORTED (A) DYNAMIC CONST <'UNSUPPORTED REQUEST'>
1 #REQUIRED    (A) DYNAMIC CONST <'CONTACT ID REQUIRED'>
1 #MEMBERONLY     (A) DYNAMIC CONST <'MEMBERSHIP REQUIRED'>
1 #UPDATECONFLICT (A) DYNAMIC CONST <'UPDATE CONFLICT'>
END-DEFINE
*
RESET ErrorCode
  ErrorDescription
*
EXPAND ARRAY #ERROR-CODE    TO (*:8)
EXPAND ARRAY #VALIDATE-FLAG TO (*:8)
*
#DB.#FILE-NAME := 'A-STATUS'
*
IF ActionType = #GET OR = #UPDATE
  FIND CNT-V WITH CONTACT-ID = MembershipInfo.ContactID
    IF NO RECORDS FOUND
      ErrorCode := 1
      COMPRESS *PROGRAM #REQUIRED INTO ErrorDescription
      ESCAPE ROUTINE
    END-NOREC
    MembershipInfo.CN := CNT-V.ID-NUMBER
  END-FIND
  FIND MEMBERSHIP-V WITH ID-NUMBER = MembershipInfo.CN
    IF NO RECORDS FOUND
      ErrorCode := 1
      COMPRESS *PROGRAM #REQUIRED INTO ErrorDescription
      ESCAPE ROUTINE
    END-NOREC
    MOVE *ISN TO #DB.#FILE-REC-ISN
    #DB.#REQUEST   := #GET
    CALLNAT 'OBJNDBTS' #DB  /* Get last update date/time
  END-FIND
END-IF
*
DECIDE ON FIRST VALUE OF ActionType
  VALUE #GET    PERFORM GET-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  ANY VALUE
    IF ErrorCode = 0 AND #SYNCHRONIZE
      PERFORM WRITE-TRANS-REG
    END-IF
  NONE VALUE
    ErrorCode := 2
    ErrorDescription := #UNSUPPORTED
END-DECIDE
*
DEFINE GET-RECORD
/*     ==========
GET MEMBERSHIP-V #DB.#FILE-REC-ISN
LastUpdatedDate := #DB.LAST-DATE-UPD
LastUpdatedTime := #DB.LAST-TIME-UPD
LastUpdatedUser := #DB.LUU-NAME
MembershipInfo.CN            := MEMBERSHIP-V.ID-NUMBER
MembershipInfo.ContactID     := MEMBERSHIP-V.MEMBER-CONTACT-ID
MembershipInfo.AAFMAAAccess  := MEMBERSHIP-V.ACCESS-AUTH-CODE
MembershipInfo.AnnualMeeting := MEMBERSHIP-V.DATE-BOOK
MembershipInfo.AnnualReport  := MEMBERSHIP-V.ANNUAL-RPT-FLAG
MembershipInfo.BridgerVerify := MEMBERSHIP-V.BRIDGER-IND
MembershipInfo.IntroFlag     := MEMBERSHIP-V.INTRO-FLAG
MembershipInfo.MailSpecial   := MEMBERSHIP-V.INS-MAIL-CODE
MembershipInfo.Newsletter    := MEMBERSHIP-V.NEWSLETTER-CODE
MembershipInfo.ProcessInd    := MEMBERSHIP-V.PROCESS-IND
MembershipInfo.SpecialCode   := MEMBERSHIP-V.CHIEF-OF-STAFF-IND
MembershipInfo.Underwriting  := MEMBERSHIP-V.SUSPEND-CODE
FIND SRC-APPL-V WITH SOURCE-CODE = MEMBERSHIP-V.APPL-SOURCE
  ACCEPT IF SOURCE-CODE-GROUP = MASK (A)
  MembershipInfo.ApplSource  := SOURCE-DESCRIPTION
END-FIND   
*
END-SUBROUTINE
*
DEFINE UPDATE-RECORD
/*     =============
IF #DB.LAST-DATE-UPD = LastUpdatedDate    
    AND #DB.LAST-TIME-UPD = LastUpdatedTime
  UPDATE-ST.
  GET MEMBERSHIP-V #DB.#FILE-REC-ISN
  IF MembershipInfo.AAFMAAAccess   = MEMBERSHIP-V.ACCESS-AUTH-CODE AND
      MembershipInfo.AnnualMeeting = MEMBERSHIP-V.DATE-BOOK AND
      MembershipInfo.AnnualReport  = MEMBERSHIP-V.ANNUAL-RPT-FLAG AND
      MembershipInfo.BridgerVerify = MEMBERSHIP-V.BRIDGER-IND AND
      MembershipInfo.MailSpecial   = MEMBERSHIP-V.INS-MAIL-CODE AND
      MembershipInfo.Newsletter    = MEMBERSHIP-V.NEWSLETTER-CODE AND
      MembershipInfo.SpecialCode   = MEMBERSHIP-V.CHIEF-OF-STAFF-IND AND
      MembershipInfo.Underwriting  = MEMBERSHIP-V.SUSPEND-CODE
    ESCAPE ROUTINE
  END-IF
  PERFORM MOVE-OLD-VALUES-FOR-TR
  PERFORM VALIDATE-FIELDS
  IF ErrorCode > 0
    ESCAPE ROUTINE
  END-IF
  MOVE BY NAME #MEMBER-INFO TO MEMBERSHIP-V
  UPDATE (UPDATE-ST.)
  #DB.#REQUEST   := #UPDATE
  #DB.LAST-USER-UPD := User
  CALLNAT 'OBJNDBTS' #DB
  LastUpdatedDate := #DB.LAST-DATE-UPD
  LastUpdatedTime := #DB.LAST-TIME-UPD
  LastUpdatedUser := #DB.LUU-NAME
  MOVE TRUE TO #SYNCHRONIZE
ELSE                                       
  ErrorCode := 99
  COMPRESS *PROGRAM #UPDATECONFLICT INTO ErrorDescription
END-IF
*
END-SUBROUTINE
*
DEFINE VALIDATE-FIELDS
/*     ===============
#SYSTEM := 'UNIX'
#MEMBER-INFO.MEMBER-CONTACT-ID  := MEMBERSHIP-V.MEMBER-CONTACT-ID
#MEMBER-INFO.ACCESS-AUTH-CODE   := MembershipInfo.AAFMAAAccess
#MEMBER-INFO.DATE-BOOK          := MembershipInfo.AnnualMeeting
#MEMBER-INFO.ANNUAL-RPT-FLAG    := MembershipInfo.AnnualReport
#MEMBER-INFO.BRIDGER-IND        := MembershipInfo.BridgerVerify
#MEMBER-INFO.INS-MAIL-CODE      := MembershipInfo.MailSpecial
#MEMBER-INFO.NEWSLETTER-CODE    := MembershipInfo.Newsletter
#MEMBER-INFO.CHIEF-OF-STAFF-IND := MembershipInfo.SpecialCode
#MEMBER-INFO.SUSPEND-CODE       := MembershipInfo.Underwriting
*
MOVE 'Y' TO #VALIDATE-FLAG(1)                    /* ACCESS-AUTH-CODE
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(2)                    /* CHIEF-OF-STAFF-IND
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(3)                    /* INS-MAIL-CODE
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(4)                    /* DATE-BOOK
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(5)                    /* ANNUAL-RPT-FLAG
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(6)                    /* BRIDGER-IND
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(7)                    /* NEWSLETTER-CODE
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
MOVE 'Y' TO #VALIDATE-FLAG(8)                    /* SUSPEND-CODE
PERFORM CALL-VALIDATION-ROUTINE
IF ErrorDescription NE ' '
  ESCAPE ROUTINE
END-IF
*
END-SUBROUTINE
*
DEFINE CALL-VALIDATION-ROUTINE
/*     =======================
CALLNAT 'GPRNBINF' #MEMBER-INFO #MEMBER-OTHERS
IF #ERROR-CODE(1) NE ' '
  ErrorDescription := F-ERROR-DESCRIPTION(<#ERROR-CODE(1)>)
  #ERROR-CODE-A    := #ERROR-CODE(1)
  ErrorCode        := #ERROR-CODE-N
END-IF
*
RESET  #VALIDATE-FLAG(*)
*
END-SUBROUTINE
*
DEFINE MOVE-OLD-VALUES-FOR-TR
/*     ======================
#OLD-ACCESS-AUTH-CODE   := MEMBERSHIP-V.ACCESS-AUTH-CODE
#OLD-DATE-BOOK          := MEMBERSHIP-V.DATE-BOOK
#OLD-ANNUAL-RPT-FLAG    := MEMBERSHIP-V.ANNUAL-RPT-FLAG
IF MEMBERSHIP-V.BRIDGER-IND = ' '
  #OLD-BRIDGER-IND      := 'N'
ELSE
  #OLD-BRIDGER-IND        := MEMBERSHIP-V.BRIDGER-IND
END-IF 
#OLD-INS-MAIL-CODE      := MEMBERSHIP-V.INS-MAIL-CODE 
#OLD-NEWSLETTER-CODE    := MEMBERSHIP-V.NEWSLETTER-CODE
#OLD-CHIEF-OF-STAFF-IND := MEMBERSHIP-V.CHIEF-OF-STAFF-IND
#OLD-SUSPEND-CODE       := MEMBERSHIP-V.SUSPEND-CODE
*
END-SUBROUTINE
*
DEFINE MOVE-NEW-VALUES-FOR-TR
/*     ======================
#NEW-ACCESS-AUTH-CODE   := MembershipInfo.AAFMAAAccess
#NEW-DATE-BOOK          := MembershipInfo.AnnualMeeting
#NEW-ANNUAL-RPT-FLAG    := MembershipInfo.AnnualReport
IF MembershipInfo.BridgerVerify = ' '
  #NEW-BRIDGER-IND      := 'N'
ELSE
  #NEW-BRIDGER-IND        := MembershipInfo.BridgerVerify
END-IF
#NEW-INS-MAIL-CODE      := MembershipInfo.MailSpecial
#NEW-NEWSLETTER-CODE    := MembershipInfo.Newsletter
#NEW-CHIEF-OF-STAFF-IND := MembershipInfo.SpecialCode
#NEW-SUSPEND-CODE       := MembershipInfo.Underwriting
*
END-SUBROUTINE
*
DEFINE WRITE-TRANS-REG
/*     ===============
IF ActionType = #UPDATE
  #ID-NUMBER        := CNT-V.ID-NUMBER
  #ORIG-COMMAND     := 'CG'
  #CLERK-ID         := FUNCTION-CLERK(<#DB.LAST-USER-UPD>)
  #ST-NAME          := FUNCTION-DISP-CT-NAME(<CNT-V.FIRST-NAME,CNT-V.MIDDLE-NAME,CNT-V.LAST-NAME,CNT-V.SUFFIX>)
  #LAST-FM-DATE     := *DATN
  PERFORM MOVE-NEW-VALUES-FOR-TR
  CALLNAT 'C2233SCB' #OLD-VALUES #NEW-VALUES
END-IF
*
END-SUBROUTINE
*
END
