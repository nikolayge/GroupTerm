* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-POLPD                                               *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Determine if policy is paid up                        *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    07/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE FUNCTION FUNCTION-POLICY-PAID-UP RETURNS (A1) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #CN-A                  (A6)
1 #SEGMENT               (I2)                                    
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 PAID-TO-DATE          (9)
 2 PAID-UP-DATE          (9)
 2 PAY-STATUS            (9)
*
1 #WORK-PAID-TO-DATE     (N8)
1 REDEFINE #WORK-PAID-TO-DATE 
 2 #PAID-TO-YYYY         (N4)
 2 #PAID-TO-MM           (N2)
 2 #PAID-TO-DD           (N2)
*
1 #WORK-PAID-UP-DATE     (N8)
1 REDEFINE #WORK-PAID-UP-DATE 
 2 #PAID-UP-YYYY         (N4)
 2 #PAID-UP-MM           (N2)
 2 #PAID-UP-DD           (N2)
*
END-DEFINE
************************************************************************
*
FUNCTION-POLICY-PAID-UP   := 'N'
*
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORD FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #CN-A
      IF NO RECORD FOUND
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
#WORK-PAID-TO-DATE := ST-V.PAID-TO-DATE (#SEGMENT)
#WORK-PAID-UP-DATE := ST-V.PAID-UP-DATE (#SEGMENT)
*
IF ST-V.PAY-STATUS (#SEGMENT) GE '40'
 OR (#PAID-TO-YYYY GE #PAID-UP-YYYY AND #PAID-TO-MM GE #PAID-UP-MM)
  FUNCTION-POLICY-PAID-UP := 'Y'
END-IF
*
END-FUNCTION
*
END
