* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GPRNNMT3    *** Easy (No Medical) Terms Rates ***
/** Selects rates to display on NC screen, that applicable to the member based on parameters passed
/** :author nguentchev
/*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* NGG    20130325  NG1  EZ Term rates change                           *
* vxt    20130602  vt1  fix the 1316 error message                                                                     *

DEFINE DATA
PARAMETER
1 #DATE-OF-BIRTH     (N8)
1 #FACE-AMOUNT       (P7.2)
1 #GENDER            (A1)
1 #FACE-AMOUNTS      (A8/7)                      /* vt1  was a8/6
1 #AGE-RANGES        (A7/7)
1 #APREM             (A6/7)
LOCAL USING GRTL0003                   /* NG1
LOCAL
1 RATE-NMT    (N3.2/6,5)
1 #AGE    (N8)
1 REDEFINE #AGE
  2 #AGE-YYYY  (N4)
1 #YYYYMMDD    (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM      (N2)
  2 #DD      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A    (A4)
  2 #MM-A      (A2)
  2 #DD-A      (A2)
1 #DATN    (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY  (N4)
  2 REDEFINE #DATN-YYYY
    3 #DATN-YYYY-A (A4)
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
1 LTII-V VIEW OF A-PRODUCT-RATES
  2 VAR-TERM-RATE (N3.2/10)
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 LTII-TYPE (A2)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 RIDER-OPTION (A1)
  2 LTII-KEY     (A14)
*
1 J   (I2)
1 K   (I2)
1 L   (I2)
*
END-DEFINE
*
RESET #FACE-AMOUNTS (*) #AGE-RANGES (*) #APREM (*)
FOR J 1 7                                           /* NG1
  IF FA-RANGE (J) = #FACE-AMOUNT
    ESCAPE BOTTOM
  END-IF
END-FOR
MOVE EDITED #FACE-AMOUNT (EM=Z99,999) TO #FACE-AMOUNTS(1)
COMPRESS '$' #FACE-AMOUNTS(1) INTO #FACE-AMOUNTS(1) LEAVING NO
*
#AGE := *DATN - #DATE-OF-BIRTH
##AGE := #AGE-YYYY
IF #AGE-YYYY < AGE-RANGE (1,1)
    OR #AGE-YYYY > AGE-RANGE (6,2)
  RESET K
ELSE
  FOR K 1 TO 7
    IF #AGE-YYYY <= AGE-RANGE (K,2 )
        AND #AGE-YYYY >= AGE-RANGE (K,1)
      ESCAPE BOTTOM
    END-IF
  END-FOR
  IF K <= 7
    ##AGE := AGE-RANGE(K,1)
  END-IF
END-IF
##GENDER    := #GENDER
##LTII-TYPE := 'EZ'   /* CURRENT KEY=XCRX99
##NICOTINE  := 'X'
RESET ##EFFECTIVE-DATE
MOVE EDITED *DATN (EM=99999999) TO #YYYYMMDD
READ LTII-V BY LTII-KEY STARTING FROM #LTII-KEY
  IF LTII-TYPE NE ##LTII-TYPE
      OR GENDER NE ##GENDER
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > #YYYYMMDD
    ESCAPE TOP
  END-IF
  ADD 1 TO L  /* Next screen line
  MOVE EDITED VAR-TERM-RATE (J) (EM=ZZ9.99) TO #APREM (L)
  COMPRESS AGE-RANGE (K,1) '-' AGE-RANGE (K,2) INTO #AGE-RANGES (L)
  #FACE-AMOUNTS(L) := #FACE-AMOUNTS(1)
  ADD 1 TO K  /* Next age range
END-READ
*
END
