* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM: PNCAM01S CALLED FROM CNCMR01D ON PC PLATFORM NC PROCESS
* Purpose: To read records and return an array of data for a listbox
*           and a second array containing corresponding ISN's
*       DBM 7/14/97
****************************************************************
DEFINE DATA
PARAMETER
1 #KEY                 (A12)    /* sort order key ID or NAME
1 #NEXT-KEY            (A25)    /* next ID or NAME
1 REDEFINE #NEXT-KEY
  2 #NEXT-ID-KEY        (A6)
1 #LB-ARRAY            (A50/50) /* array to fill listbox
1 #CLIENT-ARRAY        (A50/50) /* array of correspondng ISN's to be put in client-value
1 #CNT                 (I4)     /* counter for read loop and number of listbox items
LOCAL
1 #TEMP-N6              (N6)
1 #LB-ITEM             (A50)
1 REDEFINE #LB-ITEM
  2 ID-NUMBER-A         (A6)
  2 FILLER               2X
  2 NAME               (A25)
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER           (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A       (A6)
  2 NAME               (A25)
  2 C*ACTIVE-SEGMENTS
END-DEFINE
RESET #CNT #LB-ITEM #LB-ARRAY(*) #CLIENT-ARRAY(*) /* reset array all elements
IF #KEY EQ 'ID'
  COMPUTE #TEMP-N6 = VAL(#NEXT-ID-KEY)
  MOVE EDITED #TEMP-N6 (EM=9(6)) TO #NEXT-ID-KEY  /*to get leading 0's in key
  READ (51) ST-V BY MEMBER-KEY FROM #NEXT-ID-KEY
                            /* thru #id-end-a /* member-key must be in view format buff
                               WHERE ST-V.C*ACTIVE-SEGMENTS GT 0
    #CNT := #CNT + 1
    IF #CNT LE 50
      MOVE ST-V.ID-NUMBER-A TO #LB-ITEM.ID-NUMBER-A
      MOVE ST-V.NAME        TO #LB-ITEM.NAME
      MOVE #LB-ITEM TO #LB-ARRAY(#CNT)
      MOVE EDITED *ISN (EM=9(8)) TO #CLIENT-ARRAY(#CNT)
    ELSE
      IF #KEY EQ 'ID'
        MOVE EDITED ST-V.ID-NUMBER (EM=9(6)) TO #NEXT-KEY
      ELSE
        MOVE ST-V.NAME TO #NEXT-KEY
      END-IF
      #CNT := #CNT - 1
    END-IF
  END-READ
ELSE
  IF #KEY EQ 'NAME'
    READ (51) ST-V BY NAME-KEY FROM #NEXT-KEY /* thru #id-end-a /* member-key must be in
      #CNT := #CNT + 1
      IF #CNT LE 50
        MOVE ST-V.ID-NUMBER-A TO #LB-ITEM.ID-NUMBER-A
        MOVE ST-V.NAME        TO #LB-ITEM.NAME
        MOVE #LB-ITEM TO #LB-ARRAY(#CNT)
        MOVE EDITED *ISN (EM=9(8)) TO #CLIENT-ARRAY(#CNT)
      ELSE
        IF #KEY EQ 'ID'
          MOVE EDITED ST-V.ID-NUMBER (EM=9(6)) TO #NEXT-KEY
        ELSE
          MOVE ST-V.NAME TO #NEXT-KEY
        END-IF
        #CNT := #CNT - 1
      END-IF
    END-READ
  ELSE
    IGNORE  /* ERROR
  END-IF
END-IF

END
