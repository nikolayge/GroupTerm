* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* * * * * * * * * * * * * * * * * *
* PROGRAM-ID: C2H40PVY            *
* VY  -  VALUE ADDED ANNIVERSARY  *
* * * * * * * * * * * * * * * * * *
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    09282004  DW1     add CREDIT CARD processing                  *
* DRW    09222005  DW2     Increase Yearly Occs from 14 to 17
* DRW    11242006  DW3     Fix missing Century
************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2H40LVY
LOCAL
*
1 #SCR-MESSAGE    (A35)
1 #SCR-NAME       (A24)
1 #SCR-ISS-DATE   (A10)
1 #SCR-PLAN       (A3)
1 #SCR-ACV        (A12)
1 #SCR-PT-DATE    (A10)
1 #SCR-AGE        (A2)
1 #SCR-CV         (A12)
1 #SCR-PU-DATE    (A10)
1 #SCR-LST-DUR    (A2)
1 #SCR-NMCV       (A12)
1 #SCR-FACE-AMT   (A10)
1 #SCR-VA-DUR     (A2)
1 #SCR-MPREM      (A9)
1 #SCR-CON-DUR    (A2)
1 #SCR-DTH-BEN    (A12)
1 #SCR-MODE       (A1)
1 #SCR-MON-IND    (A2)
1 #SCR-NMDB       (A12)
1 #SCR-CV-FG      (A1)
1 #SCR-PAY-ST     (A2)
1 #SCR-MCV        (A9)
1 #SCR-SMOKE      (A1)
1 #SCR-NYMCV      (A9)
1 #SCR-PR-DEF     (A6)
1 #SCR-ALLOC-AMT  (A6)
1 #SCR-BCV        (A12)
1 #SCR-TRM-YEAR   (A4)
1 #SCR-TRM-MM     (A2)
1 #SCR-NET-PR-RT  (A8)
1 #SCR-ETI-YEAR   (A4)
1 #SCR-ETI-MM     (A2)
1 #SCR-ETI-ACV    (A9)
1 #SCR-ETI-CV     (A9)
1 #SCR-ETI-DB     (A9)
1 #SCR-ETI-LV     (A9)
1 #SCR-PREV-YEAR  (A4)
1 #SCR-PREV-MM    (A2)
1 #SCR-PACV       (A9)
1 #SCR-PCV        (A9)
1 #SCR-PDB        (A9)
1 #SCR-PPLB       (A7)
1 #SCR-PPLAI      (A7)
1 #SCR-TERM-CODE  (A10)
1 #SCR-DTH-MSG2   (A3)
1 #SCR-DOD        (A8)
1 #SCR-DB-MSG     (A3)
1 #SCR-DB-MOD     (A12)
*
1 #SCR-VA-HISTORY-GR
  2 #SCR-17-YEARS             (17)
   3 #YR-MON-Y-A   (A4)
   3 #FILL-1       (A1)
   3 #YR-MON-M-A   (A2)
   3 #FILL-2       (A3)
*  3 #MI-A         (A2)
   3 #FILL-3       (A1)
   3 #ACV-A        (A10)
*  3 #FILL-4       (A1)
   3 #CV-A         (A10)
*  3 #FILL-5       (A1)
   3 #DTH-BEN-A    (A9)
   3 #FILL-6       (A1)
*  3 #FILL-6a      (A5)
   3 #PREM-A       (A5)
   3 #FILL-7       (A1)
*  3 #FILL-7a      (A5)
   3 #COI-A        (A5)
   3 #FILL-8       (A1)
*  3 #FILL-8a      (A7)
   3 #INT-A        (A7)
*  3 #FILL-9       (A1)
   3 #FILL-9A      (A1)
   3 #SM-FG-A      (A1)
   3 #FILL-10      (A1)
   3 #PR-LN-A      (A7)
   3 #FILL-11      (A1)
   3 #PR-INT-A     (A7)
 1 REDEFINE #SCR-VA-HISTORY-GR
  2 #SCR-VA-HISTORY      (A78/17)
*
1 #INFO-SHOWN (L/9)
1 #POLS-UNSHOWN (P3)
1 #POLS-CNT     (P3)
1 #SCR-MSG-CV     (C)
1 #PAY-ST-CV      (C)
1 #DB-MOD-CV      (C)
1 #DOD-CV         (C)
1 #SCR-ETI-YEAR-CV (C)
1 #SCR-ETI-MM-CV  (C)
1 #SCR-ETI-ACV-CV (C)
1 #SCR-ETI-CV-CV  (C)
1 #SCR-ETI-DB-CV  (C)
1 #SCR-ETI-LV-CV  (C)
1 #TRM-YEAR-CV    (C)
1 #TRM-MM-CV      (C)
1 #TRM-CODE-CV    (C)
1 #WNUM-9         (N9)
1 #WNUM-7         (N7)
1 #WNUM-5         (N5)
********************************************************************
*                    WORK AREA
********************************************************************
1 #HOLD-SCR-ID    (N9)
1 #HOLD-SCR-COMM  (A2)
*
1 #LAST-NAME      (A25)
1 #WNUM-3         (N3)
1 REDEFINE #WNUM-3
  2 #W-NUM-1      (A1)
  2 #W-NUM-2      (A2)
*
1 #SCN-NAME       (A25)
1 REDEFINE #SCN-NAME
  2 #SCN-NAME-1   (A1)
  2 #SCN-NAME-24  (A24)
1 #FIRST/MIDDLE   (A25)
1 #SUFFIX         (A25)
1 #TEMP-NAME      (A25)
1 #DEL            (A1)   INIT <','>
*
1 #W-X           (I2)
1 #ST-X          (I2)
1 #H-X           (I2)
1 #A-X           (I2)
1 #I-CNT         (I2)
*
1 #W-MON-IND     (N3)
1 REDEFINE #W-MON-IND
  2 #W-MI        (A1)
  2 #W-MI-A      (A2)
*
1 #WYYYYMMDD     (N8)
1 REDEFINE #WYYYYMMDD
  2 #WYYYY       (A4)
  2 #WMM         (A2)
  2 #WDD         (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N  (N8)
*
1 #WMMDDYY-8     (A8)
*
1 #W-SCR-DATE    (N6)
1 REDEFINE #W-SCR-DATE
  2 #W-SCR-YEAR  (A4)
  2 REDEFINE #W-SCR-YEAR
   3 #W-SCR-YEAR-N  (N4)    /* DW3
  2 #W-SCR-MON   (A2)
*
1 #START-KEY        (N15)
1 REDEFINE #START-KEY
  2 #START-ID       (N6)
  2 #START-ISSUE    (N8)
  2 #START-IND      (N1)
*
1 #END-KEY          (N15)
1 REDEFINE #END-KEY
  2 #END-ID         (N6)
  2 #END-ISSUE      (N8)
  2 #END-IND        (N1)
*
1 #REC-KEY
  2 #R-ID-NUM       (N6)
  2 #R-ISS-DATE     (N8)
  2 #R-SPLIT-IND    (N1)
*
1 #HOLD-REC-KEY     (N15)
1 REDEFINE #HOLD-REC-KEY
  2 #HOLD-ID        (N6)
  2 #HOLD-ISS-DATE  (N8)
  2 #HOLD-SPLIT-IND (N1)
*
1 #W-MPREM          (P5.2)
*
1 #REC-COUNTER      (N1)
1 #FOUND-STATUS     (L)
*
END-DEFINE
***********************************************************************
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
GET STATUS-V #CGA-ISN
*
MOVE 0           TO #REC-COUNTER
MOVE #CGA-SCR-ID TO #START-ID
MOVE 00000000    TO #START-ISSUE
MOVE 0           TO #START-IND
MOVE #CGA-SCR-ID TO #END-ID
MOVE 99999999    TO #END-ISSUE
MOVE 9           TO #END-IND
READ VA-HIST LOGICAL WITH RECORD-KEY FROM #START-KEY THRU #END-KEY
  ADD 1 TO #REC-COUNTER
  IF #REC-COUNTER GT 1
    MOVE '* '      TO #CGA-SCR-COMMAND
*
    IF #CGA-ORIG-SUFFIX = '14Y' OR = '14P' OR = 'MAX'  /* DW2
      INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H41MVY'
    ELSE
      INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H40MVY'
    END-IF
*
    IF #CGA-SCR-COMMAND NE '* '
          FETCH 'G1000PXX'
    END-IF
*
    RESET             #SCR-MESSAGE
    MOVE (AD=N) TO    #SCR-ETI-YEAR-CV
    MOVE (AD=N) TO    #SCR-ETI-MM-CV
    MOVE (AD=N) TO    #SCR-ETI-ACV-CV
    MOVE (AD=N) TO    #SCR-ETI-CV-CV
    MOVE (AD=N) TO    #SCR-ETI-DB-CV
    MOVE (AD=N) TO    #SCR-ETI-LV-CV
    MOVE (AD=N) TO    #TRM-CODE-CV
    MOVE (AD=N) TO    #TRM-YEAR-CV
    MOVE (AD=N) TO    #TRM-MM-CV
    MOVE (AD=N) TO    #SCR-MSG-CV
    MOVE (AD=N) TO    #PAY-ST-CV
    MOVE (AD=N) TO    #DB-MOD-CV
    MOVE (AD=N) TO    #DOD-CV
  END-IF
*
  RESET #HOLD-REC-KEY
  MOVE VA-HIST.RECORD-KEY TO #HOLD-REC-KEY
  PERFORM HEADER-INFO
*
  FOR #ST-X 1 TO 9
    IF  (STATUS-V.STATUS(#ST-X)  = 'D')
     OR ((STATUS-V.STATUS(#ST-X) = 'E' OR = 'F' OR = 'G')
           AND (#CGA-ORIG-SUFFIX = '14P' OR = 'PUR' OR = 'MAX')) /* DW2
      IF STATUS-V.ISSUE-DATE(#ST-X)  EQ #HOLD-ISS-DATE
        IF STATUS-V.SPLIT-IND(#ST-X) EQ #HOLD-SPLIT-IND
          PERFORM MOVE-STATUS-INFO
          ESCAPE BOTTOM
        ELSE
          ESCAPE TOP
        END-IF
      ELSE
        ESCAPE TOP
      END-IF
    ELSE
      ESCAPE TOP
    END-IF
  END-FOR
*
  IF #ST-X GT 9
    PERFORM USE-HIST-INFO
  END-IF
END-READ
*
FOR #ST-X 1 TO 9
  IF  (STATUS-V.STATUS (#ST-X) = 'D')
   OR ((STATUS-V.STATUS (#ST-X) = 'E' OR = 'F' OR = 'G')
                   AND (#CGA-ORIG-SUFFIX = '14P' OR = 'PUR'))
    IF NOT #INFO-SHOWN (#ST-X)
      ADD 1 TO #POLS-UNSHOWN
    END-IF
  END-IF
END-FOR
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF #REC-COUNTER EQ 0
  IF #POLS-UNSHOWN = 0
    MOVE (AD=I)                     TO #SCR-MSG-CV
    MOVE 'NO HISTORY RECORDS FOUND' TO #SCR-MESSAGE
    PERFORM HEADER-INFO
    INPUT WITH TEXT #SCR-MESSAGE MARK
          *#CGA-SCR-COMMAND USING MAP 'C2H40MVY'
    IF #CGA-SCR-COMMAND NE #CGA-ORIG-COMMAND OR
      #CGA-SCR-ID NE #CGA-ORIG-ID
        FETCH 'G1000PXX'
    END-IF
  END-IF
ELSE
  IF #POLS-UNSHOWN = 0
    MOVE 'VY'      TO #CGA-SCR-COMMAND
  END-IF
  MOVE ID-NUMBER TO #CGA-SCR-ID
*
  IF #CGA-ORIG-SUFFIX = '14Y' OR = '14P' OR = 'MAX'  /* DW2'
    INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H41MVY'
  ELSE
    INPUT WITH TEXT #SCR-MESSAGE USING MAP 'C2H40MVY'
  END-IF
*
  IF #CGA-SCR-COMMAND NE '* '
        FETCH 'G1000PXX'
  END-IF
END-IF
*
IF #POLS-UNSHOWN NE 0
  PERFORM HEADER-INFO
  RESET #SCR-VA-HISTORY (*)
  RESET #SCR-PREV-YEAR #SCR-PACV #SCR-PCV #SCR-PDB #SCR-PPLB
  RESET #SCR-PPLAI #SCR-PREV-MM
  MOVE 'HISTORY RECORD NOT FOUND' TO #SCR-MESSAGE
*
  FOR #ST-X 1 TO 9
    IF  (STATUS-V.STATUS (#ST-X) = 'D')
     OR ((STATUS-V.STATUS (#ST-X) = 'E' OR = 'F' OR ='G')
*        AND (#CGA-ORIG-SUFFIX = '14P' OR = 'PUR'))
         AND (#CGA-ORIG-SUFFIX = 'MAX' OR = 'PUR'))   /* DW2
      IF NOT #INFO-SHOWN (#ST-X)
        PERFORM MOVE-STATUS-INFO
        ADD 1 TO #POLS-CNT
*
        IF #POLS-CNT = #POLS-UNSHOWN
          MOVE 'VW' TO #CGA-SCR-COMMAND
        ELSE
          MOVE '*' TO #CGA-SCR-COMMAND
        END-IF
      END-IF
*
      INPUT WITH TEXT #SCR-MESSAGE MARK
             *#CGA-SCR-COMMAND USING MAP 'C2H40MVY'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
    END-IF
  END-FOR
END-IF
*
FETCH 'G1000PXX'
*
***********************************************************************
DEFINE SUBROUTINE HEADER-INFO
***********************************************************************
*
SEPARATE NAME INTO #LAST-NAME #FIRST/MIDDLE #SUFFIX
   WITH DELIMITER #DEL
IF #SUFFIX = '    '
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #SCN-NAME
ELSE
  COMPRESS #FIRST/MIDDLE #LAST-NAME INTO #TEMP-NAME
  COMPRESS #TEMP-NAME ',' #SUFFIX INTO #SCN-NAME LEAVING NO SPACE
END-IF
MOVE #SCN-NAME-24 TO #SCR-NAME
END-SUBROUTINE
***********************************************************************
*
***********************************************************************
DEFINE SUBROUTINE MOVE-STATUS-INFO
***********************************************************************
*
MOVE STATUS-V.ISSUE-DATE(#ST-X)       TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY   INTO #SCR-ISS-DATE LEAVING NO SPACE
RESET #WYYYYMMDD
MOVE STATUS-V.PLAN(#ST-X)             TO #SCR-PLAN
MOVE STATUS-V.PAY-STATUS(#ST-X)       TO #SCR-PAY-ST
IF #SCR-PAY-ST EQ '22'
    IGNORE
ELSE
    MOVE (AD=I) TO #PAY-ST-CV
END-IF
MOVE STATUS-V.CONVERSION-FLAG(#ST-X)       TO #SCR-CV-FG
MOVE EDITED STATUS-V.ACC-CASH-VALUE(#ST-X) (EM=Z,ZZZ,ZZZ.99)
     TO #SCR-ACV
MOVE EDITED STATUS-V.DEATH-BENEFIT(#ST-X)  (EM=Z,ZZZ,ZZ9.99)
     TO #SCR-DTH-BEN
MOVE STATUS-V.PAID-TO-DATE(#ST-X)          TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY INTO #SCR-PT-DATE LEAVING NO SPACE
RESET #WYYYYMMDD
MOVE STATUS-V.ISSUE-AGE(#ST-X)             TO #SCR-AGE
MOVE STATUS-V.SMOKER-FLAG(#ST-X)           TO #SCR-SMOKE
MOVE EDITED STATUS-V.CASH-VALUE(#ST-X)     (EM=Z,ZZZ,ZZZ.99) TO #SCR-CV
MOVE EDITED STATUS-V.NXT-MON-DEATH-BENEFIT(#ST-X) (EM=Z,ZZZ,ZZZ.99)
     TO #SCR-NMDB
MOVE STATUS-V.PAID-UP-DATE(#ST-X)          TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY INTO #SCR-PU-DATE LEAVING NO SPACE
RESET #WYYYYMMDD
MOVE STATUS-V.LAST-DURATION(#ST-X)         TO #WNUM-3
MOVE #W-NUM-2                              TO #SCR-LST-DUR
RESET #WNUM-3
MOVE STATUS-V.DURATION(#ST-X)              TO #WNUM-3
MOVE #W-NUM-2                              TO #SCR-VA-DUR
RESET #WNUM-3
MOVE EDITED STATUS-V.NXT-MON-CASH-VALUE(#ST-X) (EM=Z,ZZZ,ZZZ.99)
     TO #SCR-NMCV
MOVE EDITED STATUS-V.MIN-CASH-VALUE(#ST-X) (EM=ZZ,ZZZ.99)
     TO #SCR-MCV
MOVE EDITED STATUS-V.FACE-AMOUNT(#ST-X)    (EM=ZZZ,ZZ9.99)
     TO #SCR-FACE-AMT
MOVE STATUS-V.MODE(#ST-X)                  TO #SCR-MODE
MOVE STATUS-V.CONV-DURATION(#ST-X)         TO #WNUM-3
MOVE #W-NUM-2                              TO #SCR-CON-DUR
RESET #WNUM-3
MOVE EDITED STATUS-V.NXT-YR-MIN-CASH-VALUE(#ST-X) (EM=ZZ,ZZ9.99)
     TO #SCR-NYMCV
IF STATUS-V.MODE(#ST-X) = 'A' OR = '1'    /* DW1
  COMPUTE #W-MPREM = STATUS-V.ANNUAL-PREM(#ST-X) / 1
ELSE
  IF STATUS-V.MODE(#ST-X) = 'S' OR = '2'   /* DW1
    COMPUTE #W-MPREM = STATUS-V.ANNUAL-PREM(#ST-X) / 2
  ELSE
    IF STATUS-V.MODE(#ST-X) = 'Q' OR = '4'  /* DW1
      COMPUTE #W-MPREM = STATUS-V.ANNUAL-PREM(#ST-X) / 4
    ELSE
      COMPUTE #W-MPREM = STATUS-V.ANNUAL-PREM(#ST-X) / 12
    END-IF
  END-IF
END-IF
MOVE EDITED #W-MPREM (EM=ZZ,ZZ9.99)        TO #SCR-MPREM
MOVE STATUS-V.MONTH-IND(#ST-X)             TO #WNUM-3
MOVE #W-NUM-2                              TO #SCR-MON-IND
RESET #WNUM-3
MOVE EDITED STATUS-V.BEG-CASH-VALUE(#ST-X)  (EM=Z,ZZZ,ZZ9.99)
     TO #SCR-BCV
MOVE EDITED STATUS-V.NET-PR-RATIO(#ST-X)    (EM=ZZ.99999)
     TO #SCR-NET-PR-RT
MOVE EDITED STATUS-V.PREM-DEFICIENCY(#ST-X) (EM=ZZZ.99) TO #SCR-PR-DEF
MOVE EDITED STATUS-V.MONTH-ALLOC-AMT(#ST-X) (EM=ZZZ.99)
     TO #SCR-ALLOC-AMT
PERFORM MOVE-ETI-INFO
PERFORM HISTORY-LOOP
*
END-SUBROUTINE
***********************************************************************
*
***********************************************************************
DEFINE SUBROUTINE USE-HIST-INFO
***********************************************************************
*
MOVE (AD=I)                                       TO #SCR-MSG-CV
MOVE 'NO INSUR MASTER'                            TO #SCR-MESSAGE
RESET #SCR-ISS-DATE #SCR-PLAN #SCR-PAY-ST #SCR-CV-FG
RESET #SCR-ACV #SCR-DTH-BEN #SCR-PT-DATE #SCR-AGE #SCR-SMOKE
RESET #SCR-NMDB #SCR-PU-DATE #SCR-LST-DUR #SCR-VA-DUR #SCR-CV
RESET #SCR-NMCV #SCR-MCV #SCR-FACE-AMT #SCR-MODE #SCR-CON-DUR
RESET #SCR-NYMCV #SCR-MPREM #SCR-MON-IND #SCR-BCV
RESET #SCR-NET-PR-RT #SCR-PR-DEF #SCR-ALLOC-AMT
MOVE #HOLD-ISS-DATE                               TO #WYYYYMMDD
COMPRESS #WMM '/' #WDD '/' #WYYYY INTO #SCR-ISS-DATE LEAVING NO SPACE
RESET #WYYYYMMDD
MOVE EDITED VA-HIST.NET-PREM-RATIO (EM=ZZ.99999)  TO #SCR-NET-PR-RT
MOVE EDITED VA-HIST.PREMIUM-DEFIC (EM=ZZZ.99)     TO #SCR-PR-DEF
MOVE EDITED VA-HIST.MONTHLY-ALLOC-AMT (EM=ZZZ.99) TO #SCR-ALLOC-AMT
PERFORM MOVE-ETI-INFO
PERFORM HISTORY-LOOP
*
END-SUBROUTINE
***********************************************************************
*
***********************************************************************
DEFINE SUBROUTINE MOVE-ETI-INFO
***********************************************************************
*
IF VA-HIST.ETI-DATE NE 0
  MOVE (AD=I)                                       TO #SCR-ETI-YEAR-CV
  MOVE (AD=I)                                       TO #SCR-ETI-MM-CV
  MOVE (AD=I)                                       TO #SCR-ETI-ACV-CV
  MOVE (AD=I)                                       TO #SCR-ETI-CV-CV
  MOVE (AD=I)                                       TO #SCR-ETI-DB-CV
  MOVE (AD=I)                                       TO #SCR-ETI-LV-CV
  MOVE VA-HIST.ETI-DATE                             TO #W-SCR-DATE
  MOVE #W-SCR-YEAR                                  TO #SCR-ETI-YEAR
  MOVE #W-SCR-MON                                   TO #SCR-ETI-MM
  COMPUTE #WNUM-9 = VA-HIST.ETI-ACCUM-CASH-VALUE * 100
  MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-ETI-ACV
  RESET #WNUM-9
  COMPUTE #WNUM-9 = VA-HIST.ETI-CASH-VALUE * 100
  MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-ETI-CV
  RESET #WNUM-9
  COMPUTE #WNUM-9 = VA-HIST.ETI-DEATH-BENE * 100
  MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-ETI-DB
  RESET #WNUM-9
  COMPUTE #WNUM-9 = VA-HIST.ETI-LOAN-VALUE * 100
  MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-ETI-LV
  RESET #WNUM-9
END-IF
PERFORM LAST-ANN-INFO
*
END-SUBROUTINE
***********************************************************************
*
***********************************************************************
DEFINE SUBROUTINE LAST-ANN-INFO
***********************************************************************
*
MOVE VA-HIST.PREV-ANN-DATE                  TO #W-SCR-DATE
MOVE #W-SCR-YEAR                            TO #SCR-PREV-YEAR
MOVE #W-SCR-MON                             TO #SCR-PREV-MM
RESET #W-SCR-DATE
COMPUTE #WNUM-9 = VA-HIST.PREV-ACCUM-CASH-VALUE * 100
MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-PACV
RESET #WNUM-9
COMPUTE #WNUM-9 = VA-HIST.PREV-CASH-VALUE * 100
MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-PCV
RESET #WNUM-9
COMPUTE #WNUM-9 = VA-HIST.PREV-DEATH-BENE * 100
MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #SCR-PDB
RESET #WNUM-9
COMPUTE #WNUM-7 = VA-HIST.PREV-PRORAT-LOAN-BAL * 100
MOVE EDITED #WNUM-7 (EM=ZZZZZ99)   TO #SCR-PPLB
RESET #WNUM-7
COMPUTE #WNUM-7 = VA-HIST.PREV-PRORAT-LN-ACC-INT * 100
MOVE EDITED #WNUM-7 (EM=ZZZZZ99)   TO #SCR-PPLAI
RESET #WNUM-7
IF VA-HIST.TERMINATION-DATE NE 0
  MOVE VA-HIST.TERMINATION-DATE    TO #W-SCR-DATE
  MOVE #W-SCR-YEAR                 TO #SCR-TRM-YEAR
  MOVE #W-SCR-MON                  TO #SCR-TRM-MM
  RESET #W-SCR-DATE
  MOVE (AD=I) TO #TRM-YEAR-CV
  MOVE (AD=I) TO #TRM-MM-CV
  MOVE (AD=I) TO #TRM-CODE-CV
  IF VA-HIST.TERMINATION-CODE EQ 'DTH'
    MOVE 'DEATH     ' TO #SCR-TERM-CODE
  ELSE
    IF VA-HIST.TERMINATION-CODE EQ 'CSV'
      MOVE 'CASH VALUE' TO #SCR-TERM-CODE
    ELSE
      IF VA-HIST.TERMINATION-CODE EQ 'LAP'
        MOVE 'LAPSE     ' TO #SCR-TERM-CODE
      ELSE
        IF VA-HIST.TERMINATION-CODE EQ 'EXP'
          MOVE 'EXPIRATION' TO #SCR-TERM-CODE
        ELSE
          IF VA-HIST.TERMINATION-CODE EQ 'REI'
            MOVE 'REINSTATED' TO #SCR-TERM-CODE
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
************************************
DEFINE SUBROUTINE HISTORY-LOOP
***********************************
*
RESET #SCR-VA-HISTORY (*)   /* DW2
MOVE 13  TO #I-CNT
MOVE 1   TO #H-X
MOVE 0   TO #A-X
*
FOR #H-X 1 TO 14
  FOR #A-X 1 TO 14
   COMPUTE #H-X = #H-X + #I-CNT
   IF VA-HIST.YEAR-MONTH-Y(#H-X) NE 0
    MOVE VA-HIST.YEAR-MONTH-Y(#H-X) TO #W-SCR-DATE
*
    IF #W-SCR-YEAR-N LT 1000  /* missing Century     /* DW3
      IF #W-SCR-YEAR-N LT 50
        ADD 2000 TO #W-SCR-YEAR-N
     ELSE
        ADD 1900 TO #W-SCR-YEAR-N
      END-IF
    END-IF
*
    MOVE #W-SCR-YEAR                      TO #YR-MON-Y-A(#A-X)
    MOVE #W-SCR-MON                       TO #YR-MON-M-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.ACCUM-CASH-VALUE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99-) TO #ACV-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.CASH-VALUE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99-) TO #CV-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.DEATH-BENE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #DTH-BEN-A(#A-X)
    PERFORM BUMP-HIST-SEGMENT
   ELSE
    RESET #YR-MON-Y-A(#A-X)
    RESET #YR-MON-M-A(#A-X)
    RESET #ACV-A(#A-X)
    RESET #CV-A(#A-X)
    RESET #DTH-BEN-A(#A-X)
    RESET #PR-LN-A(#A-X)
    RESET #PR-INT-A(#A-X)
    COMPUTE #H-X = #H-X - #I-CNT
    COMPUTE #I-CNT = #I-CNT - 2
    COMPUTE #H-X = #H-X + 1
   END-IF
  END-FOR
END-FOR
SUBTRACT 1 FROM #A-X                   /* DW2
*
FOR #H-X 15 TO 17                      /* DW2
* DISPLAY #H-X #A-X YEAR-MONTH-Y (#H-X) ACCUM-CASH-VALUE-Y (#H-X)
* IF VA-HIST.SMOKER-FLAG-Y(#H-X) = 'C' OR = 'S' OR = 'N'
  IF VA-HIST.YEAR-MONTH-Y (#H-X) NE 0
    ADD 1 TO #A-X
    MOVE VA-HIST.YEAR-MONTH-Y(#H-X)       TO #W-SCR-DATE
    MOVE #W-SCR-YEAR                      TO #YR-MON-Y-A(#A-X)
    MOVE #W-SCR-MON                       TO #YR-MON-M-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.ACCUM-CASH-VALUE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99-) TO #ACV-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.CASH-VALUE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99-) TO #CV-A(#A-X)
    COMPUTE #WNUM-9 = VA-HIST.DEATH-BENE-Y(#H-X) * 100
    MOVE EDITED #WNUM-9 (EM=ZZZZZZZ99) TO #DTH-BEN-A(#A-X)
  END-IF
END-FOR
*
END-SUBROUTINE
***********************************************************************
*
***********************************************************************
DEFINE SUBROUTINE BUMP-HIST-SEGMENT
***********************************************************************
*
COMPUTE #H-X = #H-X - #I-CNT
COMPUTE #I-CNT = #I-CNT - 2
COMPUTE #H-X = #H-X + 1
IF VA-HIST.TERMINATION-CODE EQ 'DTH'
  MOVE STATUS-V.DATE-OF-DEATH                        TO #WYYYYMMDD
  COMPRESS #WMM #WDD #WYYYY INTO #WMMDDYY-8 LEAVING NO SPACE
  MOVE #WMMDDYY-8 TO #SCR-DOD
  MOVE (AD=I) TO #DOD-CV
  MOVE 'DOD'      TO #SCR-DTH-MSG2
  MOVE'DB='       TO #SCR-DB-MSG
  FIND (1) FIRS-V WITH WIDOW-KEY = #CGA-ID-G
    IF NO RECORDS FOUND
      MOVE (AD=I)                   TO #SCR-MSG-CV
      MOVE 'NO FIRS RECORD ON FILE' TO #SCR-MESSAGE
      ESCAPE BOTTOM
    END-NOREC
  END-FIND
  FOR #W-X 1 TO 32
    IF FIRS-V.POL-NAME(#W-X) EQ 'AMAA               '
      IF FIRS-V.POL-DATE(#W-X) EQ #HOLD-ISS-DATE
        MOVE (AD=I) TO #DB-MOD-CV
        MOVE EDITED FIRS-V.POL-AMT-PAID(#W-X) (EM=Z,ZZZ,ZZZ.99)
                    TO #SCR-DB-MOD
      ELSE
        ADD 1 TO #W-X
      END-IF
    ELSE
      IF FIRS-V.POL-NAME(#W-X) EQ 'AAFMAA             '
        IF FIRS-V.POL-DATE(#W-X) EQ #HOLD-ISS-DATE
          MOVE (AD=I) TO #DB-MOD-CV
          MOVE EDITED FIRS-V.POL-AMT-PAID(#W-X) (EM=Z,ZZZ,ZZZ.99)
                      TO #SCR-DB-MOD
        ELSE
          ADD 1 TO #W-X
        END-IF
      ELSE
        ADD 1 TO #W-X
      END-IF
    END-IF
  END-FOR
END-IF
END-SUBROUTINE
***********************************************************************
END
