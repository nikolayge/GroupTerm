* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : ATHPMELT                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Month-End Letter Downloads                            *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    03302008  None    Program Written                             *
* PAM    03062009  PM1     Remove leading zeros on CN numbers          *
* vxt    01012014  vt1     fix 0936 error                              *
* YAK    05162014  YK1     Email/Phone contact normalization           *
* TMT    06102014  TT1     CONTACT NORMALIZATION - DOB AND SEX         *
************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL USING CONTPDA1         /* TT1
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 NAME
  2 PROCESS-IND
  2 INTRO-FLAG
  2 NUMBER-MEMBERSHIPS
  2 INS-MAIL-CODE
  2 SUSPEND-CODE
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
* 2 INTERNET-ADDR
*  2 SEX                                    /* TT1
  2 RANK
* 2 TELE-NUMBER
* 2 NXT-OF-KIN-TELE-NUMBER
*  2 ZIP-CODE              (N9)      /* vt1 \/
*  2 REDEFINE ZIP-CODE
*   3 ZIP-CODE-5           (N5)
*   3 ZIP-CODE-4N          (N4)
*   3 REDEFINE ZIP-CODE-4N
*    4 ZIP-CODE-4          (A4)      /* vt1 /\
*   2 DATE-OF-BIRTH         (N8)               /* TT1 BEGIN
*  2 REDEFINE DATE-OF-BIRTH
*   3 DATE-OF-BIRTH-YYYY   (N4)
*   3 DATE-OF-BIRTH-MM     (N2)
*   3 DATE-OF-BIRTH-DD     (N2)
*  2 REDEFINE DATE-OF-BIRTH
*   3 DATE-OF-BIRTH-YYYY-A (N4)
*   3 DATE-OF-BIRTH-MM-A   (N2)
*   3 DATE-OF-BIRTH-DD-A   (N2)                /* TT1 END
  2 ISSUE-DATE            (9)
  2 PLAN                  (9)
  2 STATUS                (9)
  2 FACE-AMOUNT           (9)
  2 SMOKER-FLAG           (9)
  2 LINKED-CN             (N6/40)
  2 REDEFINE LINKED-CN
    3 LINKED-CN-OCC        (40)
      4 LINKED-CN-A         (A6)
*
1 SP-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
*
1 #TAB                   (A1) INIT <H'09'>
1 #EXTRACT-RECORD        (A1000)
*
1 #JCL-PARM              (A32)
1 REDEFINE #JCL-PARM
  2 #PARM-DATE            (N8)
  2 REDEFINE #PARM-DATE
    3 #PARM-MM            (N2)
    3 #PARM-DD            (N2)
    3 #PARM-YYYY          (N4)
*
1 #WORK-FIELDS
  2 #AGE                  (N8)
  2 REDEFINE #AGE
    3 #AGE-YYYY            (N4)
    3 #AGE-REST            (N4)
  2 #WORK-PARM-DATE       (N8)
  2 REDEFINE #WORK-PARM-DATE
    3 #WORK-PARM-YYYY      (N4)
    3 #WORK-PARM-MM        (N2)
    3 #WORK-PARM-DD        (N2)
  2 #I1                   (I2)
  2 #TOTAL-INS            (P9)
  2 #AVAILABLE-INS        (P9)
  2 #MPREM                (N7.2)
  2 #FACE-CALC            (N5)
  2 #MILITARY-STATUS      (A1)
  2 #MILITARY-SERVICE     (A3)
  2 #EXTRA-1              (A25)
  2 #EXTRA-2              (A25)
  2 #EXTRA-3              (A25)
  2 #EXTRA-4              (A25)
  2 #EXTRA-5              (A25)
  2 #EXTRA-6              (A25)
*
1 #DISPLAY-FIELDS
  2 #DISP-CN-NUMBER       (A6)
  2 #DISP-MEMBER          (A1)
  2 #DISP-SPOUSE          (A1)
  2 #DISP-ARMY            (A1)
  2 #DISP-AIR-FORCE       (A1)
  2 #DISP-ACTIVE          (A1)
  2 #DISP-RANK            (A3)
  2 #DISP-INSURED-NAME    (A25)
  2 #DISP-FORMATTED-NAME  (A25)
  2 #DISP-MALE            (A1)
  2 #DISP-FEMALE          (A1)
  2 #DISP-BIRTH-DATE      (A10)
  2 #DISP-HOME-PHONE      (A12)
  2 #DISP-WORK-PHONE      (A12)
  2 #DISP-ADDRESS-1       (A50)
  2 #DISP-ADDRESS-2       (A50)
  2 #DISP-ADDRESS-3       (A50)
  2 #DISP-EMAIL           (A60)
  2 #DISP-CURRENT-FACE    (A15)
  2 #DISP-TARGET-FACE     (A15)
  2 #DISP-MONTHLY-PREM    (A15)
  2 #DISP-MEMBER-NAME     (A25)
  2 #DISP-MEMBER-CN       (A6)
*
1 #GPREMSXX-FIELDS
  2 #GPREM-PLAN           (A3)
  2 #GPREM-AGE            (N2)
  2 #GPREM-APREM          (N7.2)    /* vt1 was n6.2
  2 #GPREM-EFF-YYYYMMDD   (N8)
  2 REDEFINE #GPREM-EFF-YYYYMMDD
    3 #GPREM-EFF-YYYY      (N4)
    3 #GPREM-EFF-MM        (N2)
    3 #GPREM-EFF-DD        (N2)
  2 #GPREM-SMOKER         (A1)
  2 #GPREM-FACE-AMT       (N7.2)
  2 #GPREM-SEX            (A1)
*
1 #DATE-OF-BIRTH         (N8)               /* TT1 BEGIN
1 REDEFINE #DATE-OF-BIRTH
  2 #DATE-OF-BIRTH-YYYY   (N4)
  2 #DATE-OF-BIRTH-MM     (N2)
  2 #DATE-OF-BIRTH-DD     (N2)
1 REDEFINE #DATE-OF-BIRTH
  2 #DATE-OF-BIRTH-YYYY-A (N4)
  2 #DATE-OF-BIRTH-MM-A   (N2)
  2 #DATE-OF-BIRTH-DD-A   (N2)              /* TT1 END
END-DEFINE
*
************************************************************************
*
PERFORM LOAD-PARM-DATE
*
COMPRESS 'CN NUMBER'           #TAB
  'MEMBER-CN'           #TAB
  'MEMBER'              #TAB
  'SPOUSE'              #TAB
  'ARMY'                #TAB
  'AIR FORCE'           #TAB
  'ACTIVE'              #TAB
  'MALE'                #TAB
  'FEMALE'              #TAB
  'RANK'                #TAB
  'INSURED NAME'        #TAB
  'FORMATTED NAME'      #TAB
  'MEMBER NAME'         #TAB
  'BIRTH DATE'          #TAB
  'HOME PHONE'          #TAB
  'WORK PHONE'          #TAB
  'ADDRESS 1'           #TAB
  'ADDRESS 2'           #TAB
  'ADDRESS 3'           #TAB
  'EMAIL ADDRESS'       #TAB
  'CURRENT FACE AMOUNT' #TAB
  'TARGET FACE AMOUNT'  #TAB
  'PREMIUM' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 2 #EXTRACT-RECORD
WRITE WORK 3 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ ST-V BY ST-V.MEMBER-KEY FROM '001900'
*  
  RESET PDA-CONTACT                         /* TT1
  P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID    /* TT1
  CALLNAT 'CONTS001' PDA-CONTACT            /* TT1
  #DATE-OF-BIRTH := P-DATE-OF-BIRTH         /* TT1
*  
  PERFORM COMPUTE-AGE
  IF ST-V.PROCESS-IND = 'C'
      OR ST-V.NUMBER-MEMBERSHIPS = 0
      OR #DATE-OF-BIRTH-MM NE #PARM-MM      /* TT1
      OR #AGE-YYYY LT 25
      OR #AGE-YYYY GT 39
      OR ST-V.INS-MAIL-CODE = 'B' OR = 'S' OR = 'F'
      OR ST-V.SUSPEND-CODE = '5' OR = '6' OR = '9'
      OR ST-V.INTRO-FLAG = 'K' OR = 'G'
    ESCAPE TOP
  END-IF
*
  RESET #MILITARY-STATUS
  IF ST-V.INTRO-FLAG = 'S'
    FIND (1) SP-V WITH SP-V.MEMBER-KEY = ST-V.LINKED-CN-A (1)
      #MILITARY-STATUS  := SP-V.MILITARY-STATUS
      #MILITARY-SERVICE := SP-V.MILITARY-SERVICE
    END-FIND
  ELSE
    #MILITARY-STATUS    := ST-V.MILITARY-STATUS
    #MILITARY-SERVICE   := ST-V.MILITARY-SERVICE
  END-IF
*
  IF #MILITARY-STATUS NE 'A'
      OR NOT #MILITARY-SERVICE = 'A' OR = 'F'
    ESCAPE TOP
  END-IF
*
  RESET #TOTAL-INS
  FOR #I1 1 TO 9
    IF ST-V.ISSUE-DATE (#I1) = 0
      IF #I1 GT 1
        COMPUTE #I1 = #I1 - 1
      ELSE
        #I1 := 1
      END-IF
      ESCAPE BOTTOM
    END-IF
*
    IF ST-V.STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    ADD ST-V.FACE-AMOUNT (#I1) TO #TOTAL-INS
*  END-FOR                                               /* VT 10012013
*

    IF ST-V.SMOKER-FLAG (#I1) = 'S'
      ESCAPE TOP
    END-IF
*
  END-FOR                                                 /* VT 10012013
  PERFORM DETERMINE-INSURANCE-AMOUNTS
  IF #AVAILABLE-INS LT 50000
    ESCAPE TOP
  END-IF
*
  PERFORM CALL-G3500SAD
*
  IF #UNDEL-ADDR
    ESCAPE TOP
  END-IF
*
  PERFORM DETERMINE-PREMIUM
*
  RESET #DISPLAY-FIELDS
*
  PERFORM DETERMINE-ACTIVE-ARMY-GUARD-OR-RESERVE
*
* MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO #DISP-CN-NUMBER  /* PM1
  #DISP-CN-NUMBER      := ST-V.ID-NUMBER                     /* PM1
  #DISP-RANK           := ST-V.RANK
  #DISP-INSURED-NAME   := ST-V.NAME
*
  IF ST-V.INTRO-FLAG = 'S'
    #DISP-SPOUSE := 'X'
*
    SEPARATE SP-V.NAME LEFT INTO #DISP-MEMBER-NAME #EXTRA-1 #EXTRA-2
      #EXTRA-3 #EXTRA-4 #EXTRA-5 #EXTRA-6 WITH DELIMITER ','
    MOVE EDITED SP-V.ID-NUMBER (EM=999999) TO #DISP-MEMBER-CN
*
    COMPRESS #RANK #FIRST-NAME #LAST-NAME INTO #DISP-FORMATTED-NAME
  ELSE
    #DISP-FORMATTED-NAME := #FORMATED-NAME
    #DISP-MEMBER := 'X'
  END-IF
*
  IF P-GENDER = 'F'                            /* TT1
    #DISP-FEMALE       := 'X'
  ELSE
    #DISP-MALE         := 'X'
  END-IF
*
  COMPRESS #DATE-OF-BIRTH-MM-A #DATE-OF-BIRTH-DD-A            /* TT1
    #DATE-OF-BIRTH-YYYY-A INTO #DISP-BIRTH-DATE WITH '/'      /* TT1
* YK1
*   IF ST-V.TELE-NUMBER NE 0
*     MOVE EDITED TELE-NUMBER (EM=999-999-9999) TO #DISP-HOME-PHONE
*   END-IF
  #DISP-HOME-PHONE := F-FORMATTED-PHONE(<ST-V.MEMBER-CONTACT-ID,'H'>)
*   IF ST-V.NXT-OF-KIN-TELE-NUMBER NE 0
*     MOVE EDITED NXT-OF-KIN-TELE-NUMBER (EM=999-999-9999) TO #DISP-WORK-PHONE
*   END-IF
  #DISP-WORK-PHONE := F-FORMATTED-PHONE(<ST-V.MEMBER-CONTACT-ID,'B'>)
*
  #DISP-ADDRESS-1      := #FIRST-ADDRESS
*  MOVE EDITED ST-V.ZIP-CODE-5 (EM=99999) TO #DISP-ADDRESS-3                             /* vt1 \/
*   IF ST-V.ZIP-CODE-4N NE 0
*     COMPRESS #DISP-ADDRESS-3 '-' ST-V.ZIP-CODE-4 INTO #DISP-ADDRESS-3 LEAVING NO
*   END-IF                                                                               /* vt1  /\
  IF #ADDR-3 NE ' '
    COMPRESS #ADDR-3 #DISP-ADDRESS-3 INTO #DISP-ADDRESS-3
    #DISP-ADDRESS-2    := #ADDR-2
  ELSE
    COMPRESS #ADDR-2 #DISP-ADDRESS-3 INTO #DISP-ADDRESS-3
  END-IF
* YK1
* IF NOT ST-V.INTERNET-ADDR = ' ' OR = '999' OR = '???'
*   #DISP-EMAIL := ST-V.INTERNET-ADDR
* END-IF
  #DISP-EMAIL := F-PREF-EMAIL(<ST-V.MEMBER-CONTACT-ID>)
*
  MOVE EDITED #TOTAL-INS (EM=ZZZ,ZZZ,ZZ9) TO #DISP-CURRENT-FACE
  MOVE EDITED #AVAILABLE-INS (EM=ZZZ,ZZZ,ZZ9) TO #DISP-TARGET-FACE
  MOVE EDITED #MPREM (EM=Z,ZZZ,ZZ9.99) TO #DISP-MONTHLY-PREM
*
  COMPRESS  #DISP-CN-NUMBER       #TAB
    #DISP-MEMBER-CN       #TAB
    #DISP-MEMBER          #TAB
    #DISP-SPOUSE          #TAB
    #DISP-ARMY            #TAB
    #DISP-AIR-FORCE       #TAB
    #DISP-ACTIVE          #TAB
    #DISP-MALE            #TAB
    #DISP-FEMALE          #TAB
    #DISP-RANK            #TAB
    #DISP-INSURED-NAME    #TAB
    #DISP-FORMATTED-NAME  #TAB
    #DISP-MEMBER-NAME     #TAB
    #DISP-BIRTH-DATE      #TAB
    #DISP-HOME-PHONE      #TAB
    #DISP-WORK-PHONE      #TAB
    #DISP-ADDRESS-1       #TAB
    #DISP-ADDRESS-2       #TAB
    #DISP-ADDRESS-3       #TAB
    #DISP-EMAIL           #TAB
    #DISP-CURRENT-FACE    #TAB
    #DISP-TARGET-FACE     #TAB
    #DISP-MONTHLY-PREM    INTO #EXTRACT-RECORD LEAVING NO
  IF ST-V.INTRO-FLAG = 'S'
    WRITE WORK 3 #EXTRACT-RECORD
  ELSE
    WRITE WORK 2 #EXTRACT-RECORD
  END-IF
  RESET #EXTRACT-RECORD
*
END-READ
*
************************************************************************
DEFINE SUBROUTINE COMPUTE-AGE
************************************************************************
*
#WORK-PARM-YYYY := #PARM-YYYY
#WORK-PARM-MM   := #PARM-MM
#WORK-PARM-DD   := #DATE-OF-BIRTH-DD              /* TT1
*
COMPUTE #AGE = #WORK-PARM-DATE - #DATE-OF-BIRTH   /* TT1
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE LOAD-PARM-DATE
************************************************************************
*
READ WORK 1 ONCE #JCL-PARM
READ WORK 1 ONCE #JCL-PARM
ADD +1 TO #PARM-MM
IF #PARM-MM GT 12
  SUBTRACT 12 FROM #PARM-MM
  ADD +1 TO #PARM-YYYY
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-INSURANCE-AMOUNTS
************************************************************************
*
RESET #AVAILABLE-INS
DECIDE FOR FIRST CONDITION
  WHEN ST-V.INTRO-FLAG = 'S'
    IF #TOTAL-INS GE 300000
      RESET #AVAILABLE-INS
    ELSE
      COMPUTE #AVAILABLE-INS = 300000 - #TOTAL-INS
    END-IF
  WHEN NONE
    IF #TOTAL-INS GE 800000
      RESET #AVAILABLE-INS
    ELSE
      COMPUTE #AVAILABLE-INS = 800000 - #TOTAL-INS
    END-IF
END-DECIDE
*
COMPUTE #FACE-CALC = #AVAILABLE-INS / 5000
*
COMPUTE #AVAILABLE-INS = #FACE-CALC * 5000
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CALL-G3500SAD
************************************************************************
*
IF ST-V.INTRO-FLAG = 'S'
  #USE-SPS-GEN-INFO := TRUE
ELSE
  RESET #USE-SPS-GEN-INFO
END-IF
#CGA-SCR-ID := ST-V.ID-NUMBER
RESET #CONTACT-SEG
*
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-PREMIUM
************************************************************************
*
RESET #GPREM-APREM #MPREM
*
IF #AGE-YYYY GT 99
  #GPREM-AGE        := 99
ELSE
  #GPREM-AGE        := #AGE-YYYY
END-IF
*
#GPREM-EFF-YYYYMMDD := #WORK-PARM-DATE
*
#GPREM-FACE-AMT     := #AVAILABLE-INS
#GPREM-SEX          := P-GENDER          /* TT1
#GPREM-PLAN         := 'LT'
#GPREM-SMOKER       := 'N'
*
DISPLAY #GPREM-AGE #GPREM-FACE-AMT #GPREM-EFF-YYYYMMDD
CALLNAT 'GPREMSXX' #CGA-SCR-COMMAND #CGA-SCR-ID #CGA-SCR-SUFFIX
  #GPREM-PLAN #GPREM-AGE #GPREM-APREM #GPREM-EFF-YYYYMMDD
  #GPREM-SMOKER #GPREM-FACE-AMT #GPREM-SEX
*
COMPUTE ROUNDED #MPREM = #GPREM-APREM / 12
DISPLAY #MPREM
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE DETERMINE-ACTIVE-ARMY-GUARD-OR-RESERVE
************************************************************************
*
DECIDE FOR FIRST CONDITION
  WHEN #MILITARY-SERVICE = 'A'
    #DISP-ARMY      := 'X'
  WHEN #MILITARY-SERVICE = 'F'
    #DISP-AIR-FORCE := 'X'
  WHEN NONE
    IGNORE
END-DECIDE
*
IF #MILITARY-STATUS = 'A'
  #DISP-ACTIVE := 'X'
END-IF
*
END-SUBROUTINE
*
END
