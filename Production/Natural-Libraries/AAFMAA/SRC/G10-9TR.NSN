* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* GP-TR  bz 02/10/2016 Write DBL FA and Ownership change to Tran register
* GPL/GP2 Proj
*                                             
DEFINE DATA 
PARAMETER
 1 p_O9-TR
   2 CN-1      (N6)   BY VALUE
   2 CN-2      (N6)   BY VALUE
   2 Sel       (N1)   BY VALUE
   2 RC        (N4)    
LOCAL
 1 ST-V VIEW OF A-STATUS
   2 PROCESS-IND
*  
 1 CNT-V VIEW OF A-CONTACTS
   2 LAST-NAME
   2 FIRST-NAME
   2 MIDDLE-NAME
   2 DATE-OF-BIRTH
   2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH_A      (A8)
*  
 1 TR-V VIEW OF A-TRANSACTION-REGISTER
    2 ID-NUMBER         (N6.0)
    2 NAME              (A25)
    2 DATE-LAST-UPDATE  (N8.0)
    2 TERMINAL-ID       (A8)
    2 CLERK-ID          (A3)
    2 TRANSACTION-DATN  (P9)
    2 TRANSACTION-TIMN  (P7)
   2 CHANGED-DATA-TABLE (1:32)
     3 OLD-DATA         (A125)
     3 REDEFINE OLD-DATA
       4 Old-Tag        (A10)
       4 Old-Txt        (A115)
     3 NEW-DATA         (A125)
     3 REDEFINE NEW-DATA
       4 New-Tag        (A10)
       4 New-Txt        (A115)
   2 PROCESS-IND        (A1)
   2 USER-COMPANY       (A1)
*
1 #d0                   (D)   INIT <*DATX>
1 #db                   (D)
1 A10                   (A10)
1 A07                   (A7)
END-DEFINE
* -------------------------------------------------------------
ON ERROR
  WRITE NOTITLE '--' *DAT4U *TIMX *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR  / 
         3x 'CN Primary:' CN-1 'CN Secondary:' CN-2 'Action:' Sel
  RC := *ERROR-NR
  ESCAPE ROUTINE
END-ERROR
* PRINT *PROGRAM p_GP-TR              /* --db

FIND(1) ST-V WITH ID-NUMBER = cn-1
END-FIND
FIND(1) CNT-V WITH ID-NUMBER = cn-1
END-FIND
     Old-Tag(1) := '9+'
     New-Tag(1) := '9+'
     COMPRESS NUMERIC 'Primary CN:' cn-1 'Secondary CN:' cn-2 INTO Old-Txt(1)
   DECIDE ON FIRST VALUE sel
     VALUE 1
           New-Txt(1) := 'Links and message added '
     VALUE 2
           New-Txt(1) := 'Links and message deleted'
     VALUE 3
           New-Txt(1) := 'Message deleted'
     NONE RC := 1
        ESCAPE ROUTINE    
   END-DECIDE

  TR-V.ID-NUMBER            := cn-1
  TR-V.PROCESS-IND          := ST-V.PROCESS-IND 
  COMPRESS LAST-NAME ',' INTO LAST-NAME LEAVING NO
  COMPRESS LAST-NAME FIRST-NAME MIDDLE-NAME  INTO NAME
  TR-V.DATE-LAST-UPDATE     := *DATN
  TR-V.CLERK-ID             := 'BAT'
  TR-V.TERMINAL-ID          := *INIT-ID
  TR-V.TRANSACTION-DATN     := *DATN
  TR-V.TRANSACTION-TIMN     := *TIMN
  STORE TR-V
END
