* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*************************************************************************
* PGM-ID: ATHPLOPD                                                      *
*                                                                       *
* FUNCTION: Pay the loan amount with extra money received.              *
*************************************************************************
*                       MODIFICATION LOG                                *
* USER   DATE     TAG     REASON                                        *
*                                                                       *
* vxt  05/23/2011     Created                                           *
* vxt  07/11/2011 vt1 Added PRER transaction.                           *
* vxt  03/30/2012 vt2 Create the text delimited file for teh report.    *
*************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
1  #JCL-PARM       (A10)
1 REDEFINE #JCL-PARM
  2 #JCL-MM            (N2)   /* DATE PARM 05/01/2011
  2 #JCL-X1            (A1)
  2 #JCL-DD            (N2)
  2 #JCL-X2            (A1)
  2 #JCL-YYYY          (N4)
*
1 #PARM-YYYYMMDD (N8)
1 REDEFINE #PARM-YYYYMMDD
  2 #PARM-YYYY    (N4)
  2 #PARM-MM      (N2)
  2 #PARM-DD      (N2)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 DTS-DATN
  2 DTS-TIMN
  2 PROCESS-IND
  2 DATE-ACCT-UPDATE
  2 SSN
  2 REDEFINE SSN
    3 SSN-A               (A9)
  2 NAME
  2 MILITARY-STATUS
  2 INTRO-FLAG
  2 CAP-ENTRY-DATE
  2 ALOT-AMT
  2 ALOT-SW
  2 DUES-ALL-PAID-IND
  2 EZPAY-CAP-LOAN-AMOUNT
  2 X-LOAN-AMOUNT (9)
  2 X-LOAN-DATE   (9)
  2 X-ALT-LP-AMT  (9)
  2 X-ALT-LP-IND  (9)
  2 CONV-DURATION (9)
  2 STATUS        (9)
  2 ISSUE-DATE    (9)
  2 MODE          (9)
  2 PLAN          (9)
  2 PAID-TO-DATE  (9)
  2 REDEFINE PAID-TO-DATE
    3 PAID-TO-DATE-OCC   (9)
      4 PAID-TO-DATE-YYYYMM (N6)
      4 PAID-TO-DATE-DD     (N2)
  2 PAID-UP-DATE  (9)
*
1 ACCT-REG VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER
  2 TRANSACTION-DATN
  2 TRANSACTION-TIMN
  2 TXN-CODE
  2 SEQUENCE-NUM
  2 NAME
  2 CASH-CODE
  2 MONTH-END
  2 TXN-ACCT-NUMBER
  2 ACCT-DATE
  2 DATE-LAST-ACCT-UPDATE
  2 DEBIT-AMOUNT
  2 CREDIT-AMOUNT
  2 DESCRIPTION
  2 REDEFINE DESCRIPTION
    3 DESC-LIT            (A4)
    3 DESC-RATE           (N2.3)
    3 DESC-SPACE          (A1)
    3 DESC-ISSUE-DATE     (A10)
  2 MESSAGE-CODE
  2 POLICY-NUMBER
  2 AALP-FLAG
  2 CLERK-ID
  2 TERMINAL-ID
  2 SG-CATEGORY-1       (A2/15)
  2 SG-CATEGORY-2       (A2/15)
  2 SG-AMOUNT           (N6.2/15)
*
1 #diff                 (n5.2)
1 #CAP-X                (I2)
1 #in-file              (a250)
1 redefine #in-file
  2 #id-number          (n6)
  2 #LP-DATE            (n8)
  2 #Issue-date         (n8)
  2 #as-amt             (p9.2)
  2 #tot-amt            (n5.2)
1 #i1                   (i2)
1 #pol-num              (a16)
1 #first-name           (a25)
1 #middle-name          (a25)
1 #last-name            (a25)
1 #suffix               (a10)
1 #name                 (a70)
1 #contact-id           (n8)
1 #cn-a                 (a) dynamic
1 #lp-date-a            (a) dynamic
1 #issue-date-a         (a) dynamic
1 #diff-a               (a) dynamic
1 #tab                  (a1) init<h'09'>
1 #year-ago             (n8)                            /* vt1 \/
1 redefine #year-ago
  2 #year-ago-yyyy        (n4)
  2 #year-ago-mm          (n2)
  2 #year-ago-dd          (n2)                          /* vt1 /\
*
1 #out-file     (a132)                              /* vt2
END-DEFINE
*
*
INCLUDE GONERCXX
*
FORMAT (1) LS=133 PS=76
WRITE  (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Cap Loans Paid with extra money received'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX / /
*
write (1) 'CN#'  9x 'Name' 61x 'TranCode' 'Pol #' 15x 'Amount' 2x 'Date'    / '-' (125)
*
compress 'CN#' #tab                                  /* vt2 \/
'Name' #tab
'TranCode' #tab
'Pol' #tab
'Amount' #tab
'Date' #tab
into #out-file leaving no
write work 3  #out-file                                      /* vt2 /\
* 
READ WORK 1 ONCE RECORD #JCL-PARM
*
MOVE #JCL-YYYY TO #PARM-YYYY
MOVE #JCL-MM   TO #PARM-MM #year-ago-mm
MOVE #JCL-DD   TO #PARM-DD #year-ago-dd
Compute #year-ago-yyyy = #jcl-yyyy -1
*
WRITE 'parm date is ' #PARM-YYYYMMDD 'date year ago' #year-ago
*
READ WORK 2 #IN-file
  separate #in-file
    into #cn-a
    #lp-date-a
    #issue-date-a
    #diff-a
  IGNORE WITH DELIMITER #tab
*
  #id-number := val (#cn-a)
  #diff      := val(#diff-a)
  #diff      := #diff /100
*
  if #in-file.#id-number ne 0                          /* vt 06292011
    FIND (1) ST-V WITH id-number = #IN-file.#id-number
      IF NO RECORDS FOUND
        WRITE 'Record not found = ' #IN-file.#id-number
      END-NOREC
*
      RESET #CAP-X
      FOR #I1 1 TO 9
        write x-alt-lp-ind (#i1) '=' #id-number
        IF  X-ALT-LP-IND (#I1)  = '2'
          MOVE #I1 TO #CAP-X
          compute st-v.x-loan-amount(#i1) := st-v.x-loan-amount(#I1) - #diff
          UPDATE (0172)
          PERFORM create-acc-reg-record
          ESCAPE BOTTOM
        END-IF
      END-FOR
*
      IF #CAP-X = 0
        WRITE 'Didn"t find loan' #IN-file.#id-number
        ESCAPE BOTTOM
      end-if
*
    END-FIND
  end-if                                             /* vt 06292011
*
  END TRANSACTION
*
END-WORK
*
*****************
DEFINE SUBROUTINE create-acc-reg-record
*****************
*
reset #name #contact-id #pol-num
*
move st-v.member-contact-id to #contact-id
#name := FUNCTION-FRM-NAME(<#CONTACT-ID,#FIRST-NAME,#MIDDLE-NAME,#LAST-NAME,#SUFFIX>)
compress #last-name ',' into #last-name leaving no
compress #last-name #first-name #middle-name into #name
*
CALLNAT 'GPLNMSXX' #POL-NUM ST-V.ID-NUMBER #cap-x
*
write (1) ST-V.ID-NUMBER
  #NAME
  'PRNF'
  3x #POL-NUM
  #diff
  #PARM-YYYYMMDD
*
compress ST-V.ID-NUMBER #tab                               /* vt2 \/
  #NAME #tab
  'PRNF' #tab
  #POL-NUM #tab
  #diff #tab
  #PARM-YYYYMMDD #tab
  into #out-file leaving no
  write work 3 #out-file                                  /* vt2 /\
*
RESET ACCT-REG.SG-CATEGORY-1 (*)
  ACCT-REG.SG-CATEGORY-2 (*)
  ACCT-REG.SG-AMOUNT     (*)
*
ACCT-REG.TXN-CODE              := 'PRNF'
ACCT-REG.TXN-ACCT-NUMBER       := 110
ACCT-REG.ID-NUMBER             := ST-V.ID-NUMBER
ACCT-REG.NAME                  := #NAME
ACCT-REG.POLICY-NUMBER         := #pol-num
ACCT-REG.DATE-LAST-ACCT-UPDATE := #PARM-YYYYMMDD
ACCT-REG.TRANSACTION-DATN      := #PARM-YYYYMMDD
*   ACCT-REG.TRANSACTION-TIMN      := *TIMN
ACCT-REG.DESCRIPTION           := 'Extra Loan Pmt'
ACCT-REG.MONTH-END             := 'Y'
ACCT-REG.CLERK-ID              := 'EDS'
ACCT-REG.TERMINAL-ID           := *INIT-ID
ACCT-REG.ACCT-DATE             := #PARM-YYYYMMDD
ACCT-REG.CASH-CODE             := 'A'
ACCT-REG.CREDIT-AMOUNT         := #diff
ACCT-REG.DEBIT-AMOUNT          := 0
ACCT-REG.SG-CATEGORY-1 (1)     := 'CP'
ACCT-REG.SG-CATEGORY-2 (1)     := '**'
ACCT-REG.SG-AMOUNT     (1)     := #diff
*
STORE ACCT-REG
end transaction
*
RESET ACCT-REG.SG-CATEGORY-1 (*)                                     /* vt1 \/
  ACCT-REG.SG-CATEGORY-2 (*)
  ACCT-REG.SG-AMOUNT     (*)
*
ACCT-REG.TXN-CODE              := 'PRER'
ACCT-REG.TXN-ACCT-NUMBER       := 999
ACCT-REG.ID-NUMBER             := ST-V.ID-NUMBER
ACCT-REG.NAME                  := #NAME
ACCT-REG.POLICY-NUMBER         := #pol-num
ACCT-REG.DATE-LAST-ACCT-UPDATE := #PARM-YYYYMMDD
ACCT-REG.TRANSACTION-DATN      := #PARM-YYYYMMDD
* ACCT-REG.TRANSACTION-TIMN      := *TIMN
ACCT-REG.DESCRIPTION           := 'Extra Loan Pmt'
ACCT-REG.MONTH-END             := 'Y'
ACCT-REG.CLERK-ID              := 'EDS'
ACCT-REG.TERMINAL-ID           := *INIT-ID
ACCT-REG.ACCT-DATE             := #PARM-YYYYMMDD
ACCT-REG.CASH-CODE             := 'A'
ACCT-REG.CREDIT-AMOUNT         := 0
ACCT-REG.DEBIT-AMOUNT          := #diff
if st-v.issue-date(#cap-x) gt #year-ago
  ACCT-REG.SG-CATEGORY-1 (1)     := '1Y'
else
  ACCT-REG.SG-CATEGORY-1 (1)     := 'RN'
end-if
ACCT-REG.SG-CATEGORY-2 (1)     := '**'
ACCT-REG.SG-AMOUNT     (1)     := #diff
*
STORE ACCT-REG
end transaction                                                       /* vt1 /\
*
end-subroutine
*
end
