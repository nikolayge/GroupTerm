* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM ID DNXVN009                                                  *
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  08/16/2010  None    Program created                             *
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V) 1 REDEFINE #TEXT
  2 FILLER 48X
  2 #SSN-F     (A16)
LOCAL USING GONERLXX
LOCAL
1 BLANK  (A1)  CONST <' '>
1 #NOTIFIED-DATE (A15) CONST <'Notified Date:'>
1 #RELATIONSHIP  (A14) CONST <'Relationship:'>
1 #PHONE         (A8)  CONST <'<Phone:>'>
1 #NODATA  (A1)  CONST <'?'>
1 STATUS VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 NOK-CONTACT-ID
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 GENDER-CD
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DOB-YEAR  (A4)
    3 DOB-MONTH (A2)
    3 DOB-DAY   (A2)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DOD-YEAR  (A4)
    3 DOD-MONTH (A2)
    3 DOD-DAY   (A2)
  2 SSN
*
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
*
1 #ROLE              (A10)
*
END-DEFINE
*
RESET #TEXT (*)
*
GET STATUS #ISN     /* Member data
*
IF NOK-CONTACT-ID = 0
  #TEXT (1) := 'Name: ?'
  COMPRESS #TEXT (1) #PHONE #NODATA INTO #TEXT (1)
  COMPRESS #RELATIONSHIP #NODATA    INTO #TEXT (2)
ELSE
  FIND CONTACT WITH CONTACT-ID = NOK-CONTACT-ID
    #TEXT (1) := FUNCTION-CONTACT-NAME-2 (< CONTACT.CONTACT-ID >)
    #TEXT (2) := FUNCTION-DEFAULT-PHONE (< NOK-CONTACT-ID >)
    COMPRESS 'Name:' #TEXT (1) #PHONE #TEXT (2) INTO #TEXT (1)
    FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID
        AND CONTACT-2-ID = NOK-CONTACT-ID
      #ROLE := FUNCTION-DISP-RL-DESC (<ROLE-2-CD>)
    END-FIND
    COMPRESS #RELATIONSHIP #ROLE INTO #TEXT (2)
  END-FIND
END-IF
*
ON ERROR
  COMPRESS 'Application error' *ERROR-NR *ERROR-LINE *PROGRAM
    INTO #TEXT  (1)
  ESCAPE ROUTINE
END-ERROR
*
END
