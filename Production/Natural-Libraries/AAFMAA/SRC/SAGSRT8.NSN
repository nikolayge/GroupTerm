* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID  SAGSRT8
************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11242005  RH1   SS - Created for implementing sorting algorithm *
************************************************************************
DEFINE DATA PARAMETER
**
1 #SRT-TABLE           (570)
  2 #E-BOND-BY-FACE          /* by type/face
    3 #E-BOND-TYPE      (A2)
    3 #E-FACE-AMT       (N5)
    3 REDEFINE #E-FACE-AMT
      4 #E-FACE-AMT-2    (N2)
      4 #E-FACE-AMT-3    (N3)
    3 #E-PURCH-DT       (P8)
    3 #E-RED-VAL        (P7)
    3 #E-NUM-BONDS      (N2)
    3 #E-TYPE-FREQ      (A1)
**
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2)
  2 #SRT-ENTRY-LENGTH (I2)
  2 #SRT-KEY-LENGTH   (I2)
  2 #SRT-KEY-POSITION (I2)
  2 #SRT-SORT-TYPE    (A1)
**
LOCAL
**
1 #SRT-WORK         (570)
  2 #E-BOND-BY-FACE          /* by type/face
    3 #E-BOND-TYPE      (A2)
    3 #E-FACE-AMT       (N5)
    3 REDEFINE #E-FACE-AMT
      4 #E-FACE-AMT-2    (N2)
      4 #E-FACE-AMT-3    (N3)
    3 #E-PURCH-DT       (P8)
    3 #E-RED-VAL        (P7)
    3 #E-NUM-BONDS      (N2)
    3 #E-TYPE-FREQ      (A1)
  2 REDEFINE #E-BOND-BY-FACE   /* by purchase date
    3 #E-BOND-TYPE-X    (A2)
    3 #E-PURCH-DT-X     (P8)
    3 #E-FACE-AMT-X     (N5)
    3 REDEFINE #E-FACE-AMT-X
      4 #E-FACE-AMT-2-X  (N2)
      4 #E-FACE-AMT-3-X  (N3)
    3 #E-RED-VAL-X      (P7)
    3 #E-NUM-BONDS-X    (N2)
    3 #E-TYPE-FREQ-X    (A1)
1 REDEFINE #SRT-WORK
  2 #SRT-ROW                  (A19/1:570)
1 REDEFINE #SRT-WORK
  2 #SRT-REC                  (570)
    3 #SRT-KEY                (A12)
    3 #SRT-FILL               (A7)
**
1 #I                 (P7)
1 #ROWS              (P7)
1 #DONE              (L)  INIT <FALSE>
1 #TEMP-ROW          (A19)
**
END-DEFINE
**
**  first eliminate any empty rows
FOR #I EQ 1 TO 570
  IF      #SRT-TABLE.#E-BOND-TYPE (#I) EQ ' '
      AND #SRT-TABLE.#E-FACE-AMT (#I)  EQ 0
      AND #SRT-TABLE.#E-PURCH-DT (#I)  EQ 0
      AND #SRT-TABLE.#E-RED-VAL (#I)   EQ 0
      AND #SRT-TABLE.#E-NUM-BONDS (#I) EQ 0
      AND #SRT-TABLE.#E-TYPE-FREQ (#I) EQ ' '
    ESCAPE TOP
  ELSE
    ADD 1 TO #ROWS
    MOVE BY NAME #SRT-TABLE (#I) TO #SRT-WORK (#ROWS)
  END-IF
END-FOR
IF #ROWS EQ 0 ESCAPE ROUTINE END-IF
**

DECIDE ON FIRST VALUE OF #SRT-SORT-TYPE
**
  VALUE 'A'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
        IF #SRT-KEY (#I) LT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  VALUE 'D'
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT AVAILABLE - EXECUTION TERMINATED'
    STOP
**
  NONE VALUE
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT RECOGNIZED - EXECUTION TERMINATED'
    STOP
**
END-DECIDE
**
RESET #SRT-TABLE (*)
FOR #I EQ 1 TO #ROWS
  MOVE BY NAME #SRT-WORK (#I) TO #SRT-TABLE (#I)
END-FOR
#SRT-NUM-ENTRIES := #ROWS  /* SUBJECT TO CONFIRMATION
**
END
