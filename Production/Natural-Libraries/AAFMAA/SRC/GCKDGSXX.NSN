* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GCKDGSXX
*    DOES A CHECK DIGIT EDIT FOR up to 20 digits
*
DEFINE DATA PARAMETER
1 #CC-NUM  (A20)    /* MUST be left justified
1 REDEFINE #CC-NUM
 2 #CC-NUM-X  (A1/20)
1 REDEFINE #CC-NUM
 2 #CC-NUM-2  (A2)
1 REDEFINE #CC-NUM
 2 #CC-NUM-13  (A13) /* MC must be 13 nums/VISA must be 13 or 16 nums
*
1 #ERR-MSG   (A60)
*
LOCAL USING GCKDGLXX
LOCAL
*
1 #I1      (I2)
1 #I2      (I2)
1 #CC-NUM-CONV   (N3)
1 #2-X-NUM    (N2)
1 REDEFINE #2-X-NUM
 2 #2-X-NUM-X (N1/2)
1 #MOD-10-NUM   (N2)
1 #NUM-DIGS     (N2)
*
1 #SUM-OF-DIGITS  (N5)
1 REDEFINE #SUM-OF-DIGITS
 2 #SUM-OF-DIGITS-FIRS (N4)
 2 #SUM-OF-DIGITS-LAST (N1)
1 #ODD-DIGIT   (L)
1 #VISA        (L)
1 #MC          (L)
*
END-DEFINE
*******************************************************************
*
RESET #ERR-MSG
* WRITE #CC-NUM
IF #CC-NUM-X (1) = '4'
  MOVE TRUE TO #VISA
*
  IF #CC-NUM = MASK (NNNNNNNNNNNNNNNN'    ')
    MOVE 16 TO #NUM-DIGS
  ELSE
    IF #CC-NUM = MASK (NNNNNNNNNNNNN'       ')
      MOVE 13 TO #NUM-DIGS
    ELSE
      MOVE 'VISA must be 13 or 16 numbers' TO #ERR-MSG
      ESCAPE ROUTINE
    END-IF
  END-IF
ELSE
  IF #CC-NUM-2 = '51' OR = '52' OR = '53' OR = '54' OR = '55'
    MOVE TRUE TO #MC
*
    IF #CC-NUM = MASK (NNNNNNNNNNNNNNNN'    ')
      MOVE 16 TO #NUM-DIGS
    ELSE
      MOVE 'Master-Card must be 16 numbers' TO #ERR-MSG
      ESCAPE ROUTINE
    END-IF
  ELSE
    MOVE 'Must be Visa (4__) or MasterCard (51__/52__/53__/54__/55__)'
       TO #ERR-MSG
    ESCAPE ROUTINE
  END-IF
END-IF
*
* WRITE 'num-digits-' #NUM-DIGS
MOVE TRUE TO #ODD-DIGIT  /* True on every odd digit
* FOR #I1 1 TO #NUM-DIGS
FOR #I1 #NUM-DIGS TO 1 STEP -1  /* START with the check digit at right
  PERFORM CONVERT-DIGIT
  IF #ODD-DIGIT
    ADD #CC-NUM-CONV TO #SUM-OF-DIGITS
    RESET #ODD-DIGIT
  ELSE
    COMPUTE #2-X-NUM = #CC-NUM-CONV * 2
    COMPUTE #MOD-10-NUM = #2-X-NUM-X (1) + #2-X-NUM-X (2)
    ADD #MOD-10-NUM TO #SUM-OF-DIGITS
    MOVE TRUE TO #ODD-DIGIT
  END-IF
*  DISPLAY #I1 #CC-NUM-X (#I1) #CC-NUM-CONV #SUM-OF-DIGITS
END-FOR
*
IF #ERR-MSG NE ' '
  ESCAPE ROUTINE
END-IF
*
IF  (#SUM-OF-DIGITS = 0)
 OR (#SUM-OF-DIGITS-LAST NE 0)
  MOVE 'Check Digit is invalid' TO #ERR-MSG
END-IF
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CONVERT-DIGIT
* * * * * * * * * * * * * * * *
*
FOR #I2 1 TO 36
  IF #CC-NUM-X (#I1) = #VALID-CODE (#I2)
    MOVE #CODE-NBR (#I2) TO #CC-NUM-CONV
    ESCAPE ROUTINE
  END-IF
END-FOR
*
IF #I2 GT 36
  MOVE 'Invalid Character in CC Number' TO #ERR-MSG
END-IF
*
END-SUBROUTINE
*
* DISPLAY #CC-NUM #SUM-OF-DIGITS #ERR-MSG (EM=XXXXXXXXXXXXXXXXXXXXXXXXX)
*
END
