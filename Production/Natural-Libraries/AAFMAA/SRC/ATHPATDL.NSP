* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPATDL    ** DELETE SELECTED "AT" RECORDS
DEFINE DATA  /* AS OF 10/2002  DO NOT EVER RUN HIS
LOCAL  USING GONERLXX
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER     (N6)
 2 SSN           (N9)
 2 DATE-RECEIVED (N8)
 2 ACTION-FLAG   (A1)
 2 NAME          (A25)
 2 APPL-SOURCE   (A7)
 2 ST-NOTE-STATUS (A2/50)
 2 ST-NOTE-DATE   (N8/50)
*
1 #JCL-PARM         (A32)
1 REDEFINE #JCL-PARM
 2 #PARM-DATE      (A08)
 2 REDEFINE #PARM-DATE
  3 #PARM-MM      (N2)
  3 #PARM-DD      (N2)
  3 #PARM-YYYY    (N4)
*
1 #6-MONS-AGO (N8)
1 #YYYYMMDD (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY    (N4)
 2 #MM      (N2)
 2 #DD      (N2)
*
1 #I1                (I2)
1 #CNT-2             (P5)
1 #NUM-RECS          (P5)
1 #AT-DELETE   (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
READ WORK 1 ONCE RECORD #JCL-PARM   /* PROGRAM CALL
WRITE (2) 'parm 1' #JCL-PARM
READ WORK 1 ONCE RECORD #JCL-PARM   /* DATE ETC (110189123456789NAB ETC)
WRITE (2) 'parm 2' #JCL-PARM
*
MOVE #PARM-MM TO #MM
MOVE #PARM-DD TO #DD
MOVE #PARM-YYYY TO #YYYY
*
SUBTRACT 6 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #6-MONS-AGO
WRITE (2) 'delete date=' #6-MONS-AGO
*
FORMAT (2) LS=133 PS=60
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM
           8X 'DELETED "AT" RECORDS'
           5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
READ AT-V PHYSICAL
* DISPLAY AT-V.ID-NUMBER ST-NOTE-DATE (1:5) ST-NOTE-STATUS (1:5)
  RESET #AT-DELETE
  FOR #I1 1 TO 50
    IF   (ST-NOTE-DATE (#I1) = 0)
     AND (#I1 GT 1)
     AND (ST-NOTE-STATUS (#I1 - 1) = MASK ('S'))
     AND (ST-NOTE-DATE (#I1 - 1) LT #6-MONS-AGO)
      MOVE TRUE TO #AT-DELETE
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #AT-DELETE
    ADD 1 TO #NUM-RECS
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ID-NUMBER ST-NOTE-DATE (#I1 - 1)
         ST-NOTE-STATUS (#I1 - 1) SSN NAME APPL-SOURCE
    DELETE
  END-IF
*
  END TRANSACTION
END-READ
*
WRITE (2) 'TOT-RECS DELETED = ' #NUM-RECS
*
END
