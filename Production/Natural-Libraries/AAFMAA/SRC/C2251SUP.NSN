* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2251SUP - PROCESSES MEMBER INFO UPDATES (also from webpage)
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  04302006 xxx  created
*******************************************
PARAMETER
1 #PROCESS-CODE   (N1)
1 #ERR-MSG        (A75)
1 #CGA-SCR-ID   (N9)
1 #CN      (N6)
1 REDEFINE #CN
 2 #CN-A   (A6)
1 #CGA-CLERK    (A3)
1 #EFF-YYYYMMDD (N8)
*
1 #SCR-DATA-CV      (C/20)
1 #SCR-LINES (A50/20)
1 REDEFINE #SCR-LINES
 2 #SCR-LINES-OCC (20)
  3 #SCR-LABEL   (A15)
  3 #SCR-DATA    (A35)
*
LOCAL
1 #FIELD-LABELS
 2 #CNS-LBL  (A15) INIT <'CN_SSN:'>
 2 #NAM-LBL  (A15) INIT <'FULLNAME:'>
 2 #DOB-LBL  (A15) INIT <'DATE_OF_BIRTH:'>
 2 #AD1-LBL  (A15) INIT <'STREETADDRESS:'>
 2 #AD2-LBL  (A15) INIT <'ADDRESS2:'>
 2 #CIT-LBL  (A15) INIT <'CITY:'>
 2 #STA-LBL  (A15) INIT <'STATE:'>
 2 #CST-LBL  (A15) INIT <'CITY/STATE:'>
 2 #ZIP-LBL  (A15) INIT <'ZIPCODE:'>
 2 #HPH-LBL  (A15) INIT <'HOMEPHONE:'>
 2 #WPH-LBL  (A15) INIT <'WORKPHONE:'>
 2 #FAX-LBL  (A15) INIT <'FAX_NUMBER:'>
 2 #EML-LBL  (A15) INIT <'EMAIL:'>
 2 #RNK-LBL  (A15) INIT <'RANK:'>
 2 #EFF-LBL  (A15) INIT <'EFFECTIVEDATE:'>
*
1 NT-V VIEW OF A-NOTES
 2 C*NOTE-TABLE
 2 ID-NUMBER
 2 DTS-DATN  (P9)
 2 DTS-TIMN  (P7)
 2 NOTE-DATE     (N8/60)
 2 NOTE-CLERK-ID (A3/60)
 2 NOTE-LINE-NUM (P1/60)
 2 NOTE-TEXT     (A60/60)
 2 REDEFINE NOTE-TEXT
  3 NOTE-TEXT-OCC  (60)
   4 NOTE-TEXT-PAC-1ST-3 (A3)
   4 NOTE-TEXT-PAC-MM    (A2)
   4 NOTE-TEXT-PAC-FL1   (A1)
   4 NOTE-TEXT-PAC-DD    (A2)
   4 NOTE-TEXT-PAC-FL2   (A1)
   4 NOTE-TEXT-PAC-YYYY  (A4)
   4 NOTE-TEXT-PAC-FL3   (A1)
   4 NOTE-TEXT-PAC-TEXT  (A46)
*
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2)
1 #I4    (I2)
1 #I5    (I2)
1 #I6    (I2)
1 #INT   (I2)
1 #FND-ADDR2   (L)
1 #NOTE-TEXT (A60/10)
1 REDEFINE #NOTE-TEXT
 2 #NOTE-TEXT-OCC  (10)
  3 #NOTE-TEXT-HALF (A30/2)
  3 REDEFINE #NOTE-TEXT-HALF
   4 #NOTE-TEXT-HALF-OCC  (2)
    5 #NOTE-TEXT-HALF-1ST-5  (A5)
    5 #NOTE-TEXT-HALF-LST-25 (A25)
*
1 #YYYYMMDD  (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY  (N4)
 2 #MM    (N2)
 2 #DD    (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A  (A4)
 2 #MM-A    (A2)
 2 #DD-A    (A2)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A (A8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM   (N6)
*
END-DEFINE
*
* WRITE 'Top of C2251SUP code=' #PROCESS-CODE
IF #PROCESS-CODE = 1   /* 'UP' from pending "NU"
  FIND (1) NT-V WITH ID-NUMBER = #CGA-SCR-ID
    FOR #I1 1 TO 60
      IF NOTE-TEXT (#I1) = ' '
        ESCAPE BOTTOM
      END-IF
*
      IF   (NOTE-TEXT-PAC-1ST-3 (#I1) = '*@')
       AND (NOTE-TEXT-PAC-TEXT  (#I1) = MASK ('AUTO INFO CHG'))
        COMPRESS NOTE-TEXT-PAC-YYYY (#I1) NOTE-TEXT-PAC-MM (#I1)
                 NOTE-TEXT-PAC-DD   (#I1) INTO #YYYYMMDD-A LEAVING NO
*
        IF #YYYYMMDD LE *DATN
          FOR #I2 1 TO 10
            MOVE NOTE-TEXT (#I1) TO #NOTE-TEXT (#I2)
            ADD 1 TO #I1
*
            IF  (NOTE-LINE-NUM (#I1) = 1)
             OR (NOTE-TEXT (#I1) = ' ')
             OR (#I1 GT 60)
              ESCAPE BOTTOM
            END-IF
          END-FOR
*
          FOR #I2 1 TO 10
            FOR #I3 1 TO 2
              DECIDE ON FIRST VALUE OF #NOTE-TEXT-HALF-1ST-5(#I2, #I3)
     VALUE '*NAM'
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(1)
     VALUE '*RNK' MOVE #RNK-LBL TO #SCR-LABEL (12)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(12)
      RESET #SCR-DATA-CV (12)
     VALUE '*AD1' MOVE #AD1-LBL TO #SCR-LABEL (3)
      MOVE #AD2-LBL TO #SCR-LABEL (4) /* show both always
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(3)
      RESET #SCR-DATA-CV (3)
     VALUE '*AD2' MOVE #AD2-LBL TO #SCR-LABEL (4)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(4)
      RESET #SCR-DATA-CV (4)
     VALUE '*AD3' MOVE #CST-LBL TO #SCR-LABEL (5)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(5)
      RESET #SCR-DATA-CV (5)
     VALUE '*ZIP' MOVE #ZIP-LBL TO #SCR-LABEL (7)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(7)
      RESET #SCR-DATA-CV (7)
     VALUE '*HPH' MOVE #HPH-LBL TO #SCR-LABEL (8)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(8)
      RESET #SCR-DATA-CV (8)
     VALUE '*WPH' MOVE #WPH-LBL TO #SCR-LABEL (9)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(9)
      RESET #SCR-DATA-CV (9)
     VALUE '*FAX' MOVE #FAX-LBL TO #SCR-LABEL (10)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(10)
      RESET #SCR-DATA-CV (10)
     VALUE '*EML' MOVE #EML-LBL TO #SCR-LABEL (11)
      MOVE #NOTE-TEXT-HALF-LST-25(#I2,#I3) TO #SCR-DATA(11)
      RESET #SCR-DATA-CV (11)
     NONE IGNORE
              END-DECIDE
            END-FOR
          END-FOR
        END-IF
      END-IF
    END-FOR
  END-FIND
  ESCAPE ROUTINE
END-IF
*
RESET #ERR-MSG
MOVE #EFF-YYYYMMDD TO #YYYYMMDD
* notes must be built in reverse
RESET #NOTE-TEXT (*)
COMPRESS #MM-A #DD-A #YYYY-A INTO #NOTE-TEXT (10) WITH '/'
COMPRESS '*@' #NOTE-TEXT (10) 'AUTO INFO CHG' INTO #NOTE-TEXT (10)
MOVE 10 TO #I2  /* used for index on notes
MOVE 1  TO #I3  /* used for left/right switch
MOVE 1  TO #INT /* used to count notes
*
FOR #I1 1 TO 20
  DECIDE ON FIRST VALUE OF #SCR-LABEL (#I1)
    VALUE #NAM-LBL
      PERFORM SET-LOCATION
      COMPRESS '*NAM' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
    VALUE #AD1-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*AD1' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    VALUE #AD2-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*AD2' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
      MOVE TRUE TO #FND-ADDR2
     END-IF
    VALUE #CST-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
*
      IF #FND-ADDR2
        COMPRESS '*AD3' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
      ELSE
        COMPRESS '*AD2' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
       END-IF
     END-IF
    VALUE #ZIP-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*ZIP' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    VALUE #HPH-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*HPH' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    VALUE #WPH-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*WPH' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    VALUE #FAX-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*FAX' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    VALUE #EML-LBL
     IF #SCR-DATA (#I1) NE ' '
      IF #I3 = 1 MOVE 2 TO #I3 END-IF /* force beginning of line
      PERFORM SET-LOCATION
      COMPRESS '*EML' #SCR-DATA (#I1) INTO #NOTE-TEXT (#I2)
      MOVE 2 TO #I3
     END-IF
    VALUE #RNK-LBL
     IF #SCR-DATA (#I1) NE ' '
      PERFORM SET-LOCATION
      COMPRESS '*RNK' #SCR-DATA (#I1) INTO #NOTE-TEXT-HALF (#I2, #I3)
     END-IF
    NONE IGNORE
  END-DECIDE
END-FOR
*
IF (C*NOTE-TABLE + #INT) GT 60
  MOVE 'Info will exceed 60 "NU" lines - pending action bypassed'
       TO #ERR-MSG
*
  ESCAPE ROUTINE
END-IF
*
RESET #I3
FOR #I1 #I2 TO 10
  ADD 1 TO #I3
  MOVE #NOTE-TEXT (#I1) TO #NOTE-TEXT (#I3)
  RESET #NOTE-TEXT (#I1)
END-FOR
*
CALLNAT 'E4205SDM' #CN #INT #CGA-CLERK #NOTE-TEXT (1:10)
END TRANSACTION
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-LOCATION
* * * * * * * * * * * * * * * *
*
ADD 1 TO #I3
*
IF #I3 GT 2
  ADD 1 TO #INT
  SUBTRACT 1 FROM #I2
  MOVE 1 TO #I3
END-IF
*
END-SUBROUTINE
*
END
