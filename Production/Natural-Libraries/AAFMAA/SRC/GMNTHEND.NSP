* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: Gmnthend                                                 
*  THIS PROGRAM runs at Month-End in DSDCTH30 to set a date on a record
*     on the TERMINAL FILE to indicate Month-End has completed         
*******************************************                            
*                       MODIFICATION LOG                               
* USER DATE     TAG  REASON                                            
* DRW  10112007 DW1  Created                                           
*******************************************                            
*                                                                      
DEFINE DATA                                                            
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK                                
LOCAL USING GONERLXX     /*   WORK FIELDS FOR GONERCXX                 
LOCAL                                                                  
1 TM-V VIEW OF A-TERMINALS                                             
  2 TERMINAL-ID      (A8)                                              
  2 COMMENTS         (A20)                                             
  2 REDEFINE COMMENTS                                                  
   3 TU-COMMENTS-ME-DATE (N8)                                          
  2 LST-CMND-DT       (N8)    /* Date above updated                    
*                                                                      
1 #JCL-PARM       (A10)                              
1 REDEFINE #JCL-PARM                                 
 2 #JCL-MM            (N2)   /* DATE PARM 03/01/1999 
 2 #JCL-X1            (A1)                           
 2 #JCL-DD            (N2)                           
 2 #JCL-X2            (A1)                           
 2 #JCL-YYYY          (N4)                           
*                                                    
1 #PARM-YYYYMMDD   (N8)                              
1 #YYYYMMDD     (N8)                                 
1 REDEFINE #YYYYMMDD                                 
 2 #YYYY    (N4)                                     
 2 #MM      (N2)                                     
 2 #DD      (N2)                                     
1 REDEFINE #YYYYMMDD                                 
 2 #YYYY-A  (A4)                                     
 2 #MM-A    (A2)                                     
 2 #DD-A    (A2)                                     
*                                                    
END-DEFINE                                           
* * * * * * * * *                            
INCLUDE GONERCXX                             
* * * * * * * * *                            
*                                            
READ WORK 1 ONCE RECORD #JCL-PARM            
WRITE 'JCL-PARM = ' #JCL-PARM                
*                                            
MOVE #JCL-YYYY TO #YYYY                      
MOVE #JCL-MM   TO #MM                        
MOVE #JCL-DD   TO #DD                        
MOVE #YYYYMMDD TO #PARM-YYYYMMDD             
*                                            
FIND (1) TM-V WITH TERMINAL-ID = 'M-E-DATE'  
  IF NO RECORD FOUND                         
    WRITE 'TERMINAL ID (M-E-DATE) not found' 
    ESCAPE ROUTINE                           
  END-NOREC                                  
*                                            
  MOVE #PARM-YYYYMMDD TO TU-COMMENTS-ME-DATE 
  MOVE *DATN TO LST-CMND-DT                  
  WRITE 'GMNTHEND - Updated TU "M-E-DATE" Record with' #PARM-YYYYMMDD 
  UPDATE
  END TRANSACTION                                                     
END-FIND                                                              
*                                                                     
END                                                                      
