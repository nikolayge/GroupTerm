* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP001I - SET MR RQST FOR PROCESSING 4 AAFMAA (ATHJRQSI)
*
*   THE PARM FOR THIS PROGRAM SHOULD BE LOADED INTO THE NATURAL STACK
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
*   PM - 05/2000 - MADE CHANGES TO FREE NOTE RECORDS 600000-700000
*                - PROGRAM WILL NOW READ MESSAGES FROM REQUEST FILE
*
* bz  06/09/2013  Rebranding (Search for AAFMAA)
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 #AFSC-MSG   (L)
1 TERM-V VIEW OF A-TERMINALS
 2 CLERK-ID
 2 SECURITY-CODE
*
1 ST-V VIEW OF A-STATUS
 2 PROCESS-IND
*
* * * *   M A N U A L   R E Q U E S T   F I L E   * * * * * * *
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND       (N1)
   3 MSG-IND       (N1)
   3 RET-IND       (N1)
   3 FIN-IND       (N1)
   3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND       (A1)
   3 MAL-IND       (A1)
   3 BEN-SHO-IND   (A1)
   3 ACC-IND       (A1)
   3 CAP-IND       (A1)
   3 APP-IND       (A1)
   3 PIL-IND       (A1)
   3 CO-IND        (A1)
   3 VP-IND        (A1)
   3 MS-IND        (A1)
  2 MESSAGE        (A60/20)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
     4 REQUEST-MM-N (N2)
    3 REQUEST-DD   (A2)
  2 ASSUM-RET-DATE (N8)   /* YYYYMMDD
  2 REDEFINE ASSUM-RET-DATE
   3 ARD-CENT      (N2)
   3 ARD-YY        (N2)
   3 ARD-MM        (N2)
   3 ARD-DD        (N2)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
*
* * * *   REPORT FIELDS           * * * * * * * * * * * * * * *
1 #REPORT-FIELDS   (3)
 2 #RPT-CN          (A8)
 2 #RPT-NAME        (A12)
 2 #RPT-CLERK       (A3)
 2 #RPT-TYPE        (A15)
 2 #RPT-DATE        (A5)
 2 #RPT-RET-DATE    (A8)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 #DD     (A2)
*
1 #COUNTERS
 2 #LOOP-CNT       (I2)
*
1 #I1               (I2)
1 #I2               (I2)
1 #CNT-MR           (N5)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (4) LS=133 PS=66
WRITE  (4) TITLE  *PROGRAM '- 4'
             8X 'AAFMAA'
                'AAFMAA MAILING RQSTS REPORT (ATHJRQSI)'
                'AS OF' *DAT4U
             12X *PAGE-NUMBER (4) (EM=ZZ9) /
*
* * * * * * * * * *
* REQUEST PROCESSING
* * * * * * * * * *
*
MOVE *DATN TO #DATN
*
READ RECORDS XX-R01 PHYSICAL
  IF   (ZIP-CODE = 0)       /* NO REQUESTS
   AND (RQST-TABLE = ' ')
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #CNT-MR
  IF #CNT-MR GT 600   /* LIMIT OF 600 MR FIRS/FINS
    ESCAPE BOTTOM
  END-IF
*
  IF XX-R01.REQUEST-MM-N LT 50
    ADD 50 TO XX-R01.REQUEST-MM-N
  END-IF
*
  UPDATE
  END TRANSACTION
*
*  BELOW TO SELECT AAFMAA MR REQUESTS <<<<<<<<<<<<<<<<<<<
  FIND (1) TERM-V WITH CLERK-ID = XX-R01.CLERK-ID
    IF NO RECORDS FOUND
      WRITE XX-R01.CLERK-ID 'user not found in AOAP005F'
      MOVE 'B' TO #CGA-USER-COMPANY
      ESCAPE BOTTOM
    END-NOREC
*
    IF TERM-V.SECURITY-CODE = 1 OR = 2 OR = 97
      MOVE 'S' TO #CGA-USER-COMPANY
    ELSE
      MOVE 'I' TO #CGA-USER-COMPANY
    END-IF
  END-FIND
*
  FIND (1) ST-V WITH MEMBER-KEY = XX-R01.ID-NUMBER-A
  END-FIND
*
  RESET #AFSC-MSG
  IF MSG-IND = 1 OR = 3 /*reg or reg/ben msg
    IF  (MESSAGE(3)= MASK('CONGRATULATIONS ON YOUR PROMOTION! ')) /*DOP
     OR (MESSAGE(1)= MASK('ENCLOSED IS A BROCHURE REGARDING'))    /*VLT
     OR (MESSAGE(2)= MASK('ENCLOSED IS A FORM TO SEND TO THE S')) /*SSA
      MOVE TRUE TO #AFSC-MSG
    END-IF
  END-IF
*
  IF  (FIR-IND NE 0)
   OR ((MSG-IND = 1 OR = 3)
    AND ((#CGA-USER-COMPANY = 'S') OR #AFSC-MSG))
   OR (RET-IND NE 0)
   OR (FIN-IND NE 0)
   OR (FAS-IND NE 0)
   OR ((ACC-IND NE ' ') AND (ST-V.PROCESS-IND = 'G' OR = 'S'))
   OR (MS-IND NE ' ')
   OR ((MAL-IND = '3' OR = '4' OR = '5')
     AND (ST-V.PROCESS-IND = 'S'))
   OR (PIL-IND NE ' ')
    ESCAPE TOP
  END-IF
*  ABOVE TO SELECT AAFMAA MR REQUESTS <<<<<<<<<<<<<<<<<<<
*
END-ALL  /* (0114)
*
SORT BY CLERK-ID MEMBER-NAME
  USING ID-NUMBER REQUEST-DATE ZIP-CODE ASSUM-RET-DATE ASSUM-RET-DATE-2
        RQST-TABLE MESSAGE (*)                         ASSUM-RET-DATE-3
*
  IF #LOOP-CNT = 2
    PERFORM WRITE-REQUEST-DETAIL
    RESET #REPORT-FIELDS (1:2)
    RESET #LOOP-CNT
  END-IF
*
  ADD +1 TO #LOOP-CNT
  COMPRESS 'CN' ID-NUMBER-A INTO #RPT-CN (#LOOP-CNT) LEAVING NO
  COMPRESS REQUEST-MM  REQUEST-DD INTO #RPT-DATE (#LOOP-CNT) WITH '/'
  MOVE MEMBER-NAME      TO #RPT-NAME (#LOOP-CNT)
  MOVE CLERK-ID         TO #RPT-CLERK (#LOOP-CNT)
*
  IF XX-R01.MSG-IND = 1 OR = 3
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/MSG'
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'MESSAGE' ID-NUMBER
    WRITE (4) 'MESSAGE' ID-NUMBER
    FOR #I1 1 TO 8
      WRITE (2) '     ' #I1 MESSAGE (#I1)
      WRITE (4) '     ' #I1 MESSAGE (#I1)
    END-FOR
  END-IF
*
  IF XX-R01.MSG-IND = 2 OR = 3
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/MSB'
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'MESSAGE- bene' ID-NUMBER
    WRITE (4) 'MESSAGE- bene' ID-NUMBER
    FOR #I1 11 TO 14
      WRITE (2) '     ' #I1 MESSAGE (#I1)
      WRITE (4) '     ' #I1 MESSAGE (#I1)
    END-FOR
  END-IF
*
  IF XX-R01.BEN-SHO-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/BND'
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'BEN sho designation request' ID-NUMBER
  END-IF
*
  IF XX-R01.BEN-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/BN' XX-R01.BEN-IND
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'BEN designation request' ID-NUMBER
  END-IF
*
  IF XX-R01.ACC-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/AC' XX-R01.ACC-IND
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'ACCESS ltr request' ID-NUMBER
  END-IF
*
  IF XX-R01.CAP-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/CP' XX-R01.CAP-IND
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'CAP OPTIONS ltr request' ID-NUMBER
  END-IF
*
  IF XX-R01.APP-IND NE ' '
    IF XX-R01.APP-IND = 'P'
      COMPRESS #RPT-TYPE (#LOOP-CNT) '/PUP'
                               INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
      WRITE (2) 'PUP Paid up info request' ID-NUMBER
    ELSE
      COMPRESS #RPT-TYPE (#LOOP-CNT) '/AP-' XX-R01.APP-IND
                               INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
      WRITE (2) 'APPLICATION request' ID-NUMBER
    END-IF
  END-IF
*
  IF XX-R01.PIL-IND = '1'
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/PIL'
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'Policy Info request' ID-NUMBER
  END-IF
*
  IF XX-R01.CO-IND = '1'
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/CO'
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'CO Letter' ID-NUMBER
  END-IF
*
  IF XX-R01.VP-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/VP-' XX-R01.VP-IND
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'VP proj' ID-NUMBER
  END-IF
*
  IF XX-R01.MAL-IND NE ' '
    COMPRESS #RPT-TYPE (#LOOP-CNT) '/MA' XX-R01.MAL-IND
                             INTO #RPT-TYPE (#LOOP-CNT) LEAVING NO
    WRITE (2) 'Member Accounts Ltr request' ID-NUMBER
  END-IF
END-SORT  /* (0177)
*
RESET #LOOP-CNT
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE WRITE-REQUEST-DETAIL
* * * * * * * * * * * * * * * * * * *
*
DISPLAY (4) NOTITLE
   2T 'CERT NO' #RPT-CN(1) 1X 'MEMBER-NAME' #RPT-NAME(1)
   1X '-TYPE' #RPT-TYPE(1) 1X 'REQ' #RPT-CLERK(1)
   1X 'AS-OF' #RPT-DATE(1)
   1X 'ASM-R-DT' #RPT-RET-DATE(1)
   2X 'CERT NO' #RPT-CN(2) 1X 'MEMBER-NAME' #RPT-NAME(2)
   1X '-TYPE' #RPT-TYPE(2) 1X 'REQ' #RPT-CLERK(2)
   1X 'AS-OF' #RPT-DATE(2)
   1X 'ASM-R-DT' #RPT-RET-DATE(2)
*
END-SUBROUTINE  /* (0279)
*
END
