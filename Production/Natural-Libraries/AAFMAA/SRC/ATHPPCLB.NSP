* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPPCLB    - format PC LABELS
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    03122005  PM1     Reset Rank information before GRANKSXX.     *
* DRW    06062005  DW2     Add FU60 & FU90 Follow-up Processing        *
* PAM    05152006  PM3     Added fields to email mail merge            *
* SAG-AB 06062006  RH1     Changed Hex '05' to Hex '09' for ASCII Tab  *
* PAM    04102008  PM4     Changed form 30 day to 45 day follow-up     *
* PAM    02262009  PM5     Ignore CAP credit card labels               *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK                           /* PM3
LOCAL
*
1 #PC-ADDRESS-5-LINES  (5)
 2 #PC-LINE        (A86)
 2 REDEFINE #PC-LINE
  3 #PC-2-UP           (2)
   4 #PC-DATA    (A36)
   4 REDEFINE #PC-DATA
    5 #PC-DATA-X1  (A34)
    5 #PC-DATA-PG  (A2)
   4 #FILL-2     (A7)
*
1 #LABELS            (A181)
1 REDEFINE #LABELS
 2 #LBL-ID            (A9)
 2 REDEFINE #LBL-ID
  3 #LBL-AT           (A2)
  3 #LBL-CN-ID        (N6)
 2 #LBL-MBRSHPS       (N1)
 2 #LBL-NAME          (A25)
 2 #LBL-RANK          (A4)
 2 REDEFINE #LBL-RANK
  3 #LBL-RANK-3       (A3)
 2 #LBL-ACT-FLAG      (A1)
 2 #LBL-ADDR-1        (A25)
 2 #LBL-ADDR-2        (A25)
 2 #LBL-ADDR-3        (A25)
 2 #LBL-ZIP-CODE      (A10)
 2 REDEFINE #LBL-ZIP-CODE
  3 #LBL-ZIP-DC-FST-5 (A5)
  3 #LBL-ZIP-CD-LST-5 (A5)
 2 #LBL-MIL-SERVICE   (A3)
 2 #LBL-SSN           (N9)
 2 #LBL-ZIP-DPB       (N2)
 2 #LBL-CHIEF-OF-STAFF (A1)
 2 #LBL-FILL-1         (A10)
 2 #LBL-SPUSE         (A1)
 2 #LBL-ALOT-MSG      (A13)
 2 #LBL-FILL-2        (A12)
 2 #LBL-LBL-TYPE      (A3)
 2 #LBL-FILL-3        (A2)
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER     (N6)
 2 INTERNET-ADDR (A60)
 2 PLAN          (A3)                                             /* PM3
 2 RANK          (A3)                                             /* PM3
 2 NAME          (A25)                                            /* PM3
 2 USER-SOURCE   (A3)                                             /* PM3
 2 DATE-OF-BIRTH (N8)                                             /* PM3
*
1 #LET-1           (I2)                                           /* PM3
1 #LET-2           (I2)                                           /* PM3
1 #I1              (I2)
1 #I2              (I2)
1 #CNT-1           (I2)
1 #CNT-2           (I2)
1 #NUM-LBLS        (P5)
1 #NUM-SHEETS      (P3)
1 #LST-LBLS        (P3)
1 #LBLS-PER-PAGE   (P3)
1 #CAP-APPL-CNT    (P3)
1 #CAP-ALLT-STRT-CNT(P3)
1 #NON-CAP-F30-CNT  (P3)
1 #NON-CAP-F60-CNT  (P3)  /* DW2
1 #NON-CAP-F90-CNT  (P3)  /* DW2
1 #NON-CAP-APPL-CNT (P3)
1 #FU2-CNT         (P3)
1 #FU3-CNT         (P3)
*
1 #ADDR-LINE       (A150)                                         /* PM3
1 #LINE            (A86)
1 #LAST        (A25)
1 #FIRST/MID   (A25)
1 #SUFF        (A25)
1 #NAME-OVRFLO (A25)
1 #NAME        (A35)
1 #CMND            (A2) INIT <'BR'>
1 #RANK            (A3)
1 #INVALID         (L)
1 #RANK-LIT        (A23)
1 #RANK-DISPLAY    (A5)
1 #DISPL        (P2)
*
1 #SCR-ZIP-BAR-CODE  (A14)
1 #ZIP-SUM           (N3)
1 #PAGE              (N3)
1 #DATE-DISP     (A10)
1 #YYYYMMDD      (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A  (A4)
 2 #MM-A    (A2)
 2 #DD-A    (A2)
*
1 #LETTER-TABLE                                                   /* PM3
  2 #1T (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>                 /* PM3
  2 #2T (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>                 /* PM3
1 REDEFINE #LETTER-TABLE                                          /* PM3
  2 #LETTER-OCC  (26)                                             /* PM3
    3 #UPPER     (A1)                                             /* PM3
    3 #LOWER     (A1)                                             /* PM3
*
1 #HOLD-LAST-NAME        (A25)                                    /* PM3
1 REDEFINE #HOLD-LAST-NAME                                        /* PM3
 2 #HOLD-LAST-NAME-A     (A1/25)                                  /* PM3
*
1 #ANN-PREM            (N6.2)                                     /* PM3
1 #MON-PREM            (N6.2)                                     /* PM3
1 #LOAN-PMT            (N6.2) INIT <107.25>                       /* PM3
1 #TOTAL-PMT           (N7.2)                                     /* PM3
**1 #TAB                 (A1)   INIT <H'05'>  /* PM3 /* SAG-AB 06062006
1 #TAB                 (A1)   INIT <H'09'>    /* PM3 /* SAG-AB 06062006
1 #DIVIDEND            (N5.4)                                     /* PM3
1 REDEFINE #DIVIDEND                                              /* PM3
 2 #DIV-5              (N5)                                       /* PM3
 2 #REM                (N4)                                       /* PM3
1 #INVLAID             (L)                                        /* PM3
*
1 #DISPLAY-PREMIUM     (A10)                                      /* PM3
1 #DISPLAY-LOAN        (A10)                                      /* PM3
1 #DISPLAY-TTL-PREMIUM (A10)                                      /* PM3
1 #CLERK-ABB-NAME-PASS (A20)                                      /* PM3
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * *
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-DISP WITH '/'
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'PC Labels'
           5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS 'Email Address'      #TAB                                /* PM3
         'Application Number' #TAB                                /* PM3
         'Premium'            #TAB                                /* PM3
         'Loan Payment'       #TAB                                /* PM3
         'Total Payment'      #TAB                                /* PM3
         'Rank'               #TAB                                /* PM3
         'Last Name'          #TAB                                /* PM3
         'Mrep'  INTO #ADDR-LINE LEAVING NO                       /* PM3
*
WRITE WORK 3 #ADDR-LINE                                           /* PM3
RESET #ADDR-LINE                                                  /* PM3
*
MOVE 1 TO #PAGE
READ WORK 1 RECORD #LABELS
  IF #LBL-LBL-TYPE = 'REG' OR = 'CPC'  /* no REG labels yet       /* PM5
    ESCAPE TOP                         /* per 08/2002 kec
  END-IF
*
  ADD 1 TO #NUM-LBLS
END-ALL
*
SORT BY #LBL-LBL-TYPE #LBL-ACT-FLAG #LBL-CN-ID
  USING #LABELS
* DISPLAY #LBL-LBL-TYPE #LBL-ACT-FLAG #LBL-CN-ID #LBL-RANK-3
*
  IF #I1 GE 2
    FOR #I2 1 TO 5
      MOVE #PC-LINE (#I2) TO #LINE
      PERFORM WRITE-OUT-LINE
    END-FOR
*
    ADD 1 TO #LBLS-PER-PAGE
    IF #LBLS-PER-PAGE = 10 /* NO EXTRA BLANK AT END OF SHEET (CHGED 9/26
      RESET #LINE              /* ADDED 9/26/2000 FOR NEW PC
      PERFORM WRITE-OUT-LINE   /* ADDED 9/26/2000 FOR NEW PC
      ADD 1 TO #PAGE
      RESET #LBLS-PER-PAGE
    ELSE
      RESET #LINE
      PERFORM WRITE-OUT-LINE
    END-IF
*
    RESET #I1
    RESET #PC-LINE (*)
  END-IF
*
  ADD 1 TO #I1
*
  IF #LBL-LBL-TYPE = 'CAP'  /* "CAP LABELS"
    IF #LBL-ACT-FLAG = 'M'  /* send appls  (was S)
      ADD 1 TO #CAP-APPL-CNT
    ELSE
      ADD 1 TO #CAP-ALLT-STRT-CNT  /* send allot starts cards
    END-IF
  ELSE
    IF #LBL-LBL-TYPE = 'F45'     /* PM4
      ADD 1 TO #NON-CAP-F30-CNT
    END-IF
*
    IF #LBL-LBL-TYPE = 'F60'     /* DW2
      ADD 1 TO #NON-CAP-F60-CNT
    END-IF
*
    IF #LBL-LBL-TYPE = 'F90'     /* DW2
      ADD 1 TO #NON-CAP-F90-CNT
    END-IF
*
    IF #LBL-LBL-TYPE = 'REG'
      ADD 1 TO #NON-CAP-APPL-CNT
    END-IF
  END-IF
*
  IF #LBL-LBL-TYPE = 'CAP'   /* "CAP MBR'
    IF #LBL-ACT-FLAG = 'M'   /* send appl   (was S)
     COMPRESS #DATE-DISP #LBL-ID 'CAP APPL' INTO #PC-DATA (1, #I1)
    ELSE
*     COMPRESS #DATE-DISP #LBL-ID 'ALLT STRT' INTO #PC-DATA (1, #I1)
      COMPRESS #DATE-DISP #LBL-ID #LBL-ALOT-MSG INTO #PC-DATA (1, #I1)
*     PERFORM GET-INTERNET-ADDR                                   /* PM3
    END-IF
  ELSE
    IF #LBL-LBL-TYPE = 'REG'   /* "NON-CAP MBR'
      COMPRESS #DATE-DISP #LBL-ID 'APP' INTO #PC-DATA (1, #I1)
    ELSE
      IF #LBL-LBL-TYPE = 'F45'    /* DW2 PM4
        COMPRESS #DATE-DISP #LBL-ID 'F45' INTO #PC-DATA (1, #I1)  /* PM4
      ELSE
        IF #LBL-LBL-TYPE = 'F60'    /* DW2
          COMPRESS #DATE-DISP #LBL-ID 'F60' INTO #PC-DATA (1, #I1)
        ELSE
          COMPRESS #DATE-DISP #LBL-ID 'F90' INTO #PC-DATA (1, #I1)
        END-IF
      END-IF
    END-IF
  END-IF
*
  SEPARATE #LBL-NAME LEFT INTO #LAST #FIRST/MID #SUFF #NAME-OVRFLO
                                    WITH DELIMITER ','
  COMPRESS #FIRST/MID #LAST INTO #NAME
*
  IF #SUFF NE ' '
    COMPRESS #NAME ',' INTO #NAME LEAVING NO
    COMPRESS #NAME #SUFF INTO #NAME
  END-IF
*
* IF   (#LBL-MIL-STATUS = 'R')
*  AND (NOT #LBL-CHIEF-OF-STAFF NE 'X')
*  AND (NOT #LBL-RANK = 'MR' OR = 'MS' OR = 'MRS' OR = 'MIS'
*                            OR = 'REV' OR = 'DR' OR = ' ')
*   COMPRESS #NAME ', RET' INTO #NAME LEAVING NO
* END-IF
*
  RESET #DISPL #RANK-LIT #RANK-DISPLAY                            /* PM1
* DISPLAY #LBL-RANK-3 #RANK-DISPLAY
  CALLNAT 'GRANKSXX' #CMND #LBL-RANK-3 #LBL-MIL-SERVICE
           #INVALID #DISPL #RANK-LIT #RANK-DISPLAY
  COMPRESS #RANK-DISPLAY #NAME  INTO #PC-DATA (2,#I1)
* COMPRESS #LBL-RANK #LBL-NAME INTO #PC-DATA (2, #I1)
*
  IF #LBL-LBL-TYPE = 'CAP'                                        /* PM3
   AND #LBL-ACT-FLAG NE 'M'                                       /* PM3
    PERFORM GET-INTERNET-ADDR                                     /* PM3
  END-IF                                                          /* PM3
*
  MOVE #LBL-ADDR-1 TO #PC-DATA (3, #I1)
*
  IF #LBL-ZIP-CD-LST-5 = '-0000'
    RESET #LBL-ZIP-CD-LST-5
  END-IF
*
  IF #LBL-ADDR-3 NE ' '
    MOVE #LBL-ADDR-2 TO #PC-DATA (4, #I1)
    COMPRESS #LBL-ADDR-3 #LBL-ZIP-CODE INTO #PC-DATA (5, #I1)
  ELSE
    COMPRESS #LBL-ADDR-2 #LBL-ZIP-CODE INTO #PC-DATA (4, #I1)
  END-IF
*
  IF #LBLS-PER-PAGE = 0    /* only on 1st line
    MOVE EDITED #PAGE (EM=Z9) TO #PC-DATA-PG (1, 2)
  END-IF
END-SORT
*
FOR #I2 1 TO 5
  MOVE #PC-LINE (#I2) TO #LINE
  PERFORM WRITE-OUT-LINE
END-FOR
RESET #LINE
PERFORM WRITE-OUT-LINE
*
DIVIDE 20 INTO #NUM-LBLS GIVING #NUM-SHEETS REMAINDER #LST-LBLS
IF #LST-LBLS NE 0
  ADD 1 TO #NUM-SHEETS
END-IF
*
IF #LST-LBLS = 0
  MOVE 20 TO #LST-LBLS
END-IF
*
COMPRESS #NUM-LBLS      'TOT Num labels' INTO #LINE
PERFORM WRITE-OUT-LINE
COMPRESS #CAP-APPL-CNT  'CAP-APPS' INTO #LINE
PERFORM WRITE-OUT-LINE
COMPRESS #CAP-ALLT-STRT-CNT 'ALLT Strts' INTO #LINE
PERFORM WRITE-OUT-LINE
COMPRESS #NON-CAP-F30-CNT 'F-45 Lbls' INTO #LINE  /* PM4
PERFORM WRITE-OUT-LINE
COMPRESS #NON-CAP-F60-CNT 'F-60 Lbls' INTO #LINE  /* DW2
PERFORM WRITE-OUT-LINE
COMPRESS #NON-CAP-F90-CNT 'F-90 Lbls' INTO #LINE  /* DW2
PERFORM WRITE-OUT-LINE
COMPRESS #NON-CAP-APPL-CNT 'APP Lbls' INTO #LINE
PERFORM WRITE-OUT-LINE
COMPRESS #NUM-SHEETS 'sheets needed' #LST-LBLS 'on last sheet'
       INTO #LINE
PERFORM WRITE-OUT-LINE
*
WRITE 'CAP-APPLS   =' #CAP-APPL-CNT
WRITE 'CAP-STARTS  =' #CAP-ALLT-STRT-CNT
WRITE 'FU-45 lbls  =' #NON-CAP-F30-CNT    /* PM4
WRITE 'FU-60 lbls  =' #NON-CAP-F60-CNT    /* DW2
WRITE 'FU-90 lbls  =' #NON-CAP-F90-CNT    /* DW2
WRITE 'REG-APPLS   =' #NON-CAP-APPL-CNT
WRITE '            = _________________'
WRITE 'num labels  = ' #NUM-LBLS
*
* * * * * * * * * * *
DEFINE WRITE-OUT-LINE
* * * * * * * * * * *
*
* WRITE WORK 2 #LINE   /* IF A COMPLETE FILE IS NEEDED (BY KEITH)
* ESCAPE ROUTINE
*
* IF #NUM-LBLS LE 302   /* AMOUNT IS ALWAYS 2 AHEAD
  WRITE WORK 2 #LINE
* ELSE
*  IF #NUM-LBLS LE 602
*    WRITE WORK 3 #LINE
*  ELSE
*    IF #NUM-LBLS LE 902
*      WRITE WORK 4 #LINE
*    ELSE
*      WRITE WORK 5 #LINE
*    END-IF
*  END-IF
* END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * *
DEFINE GET-INTERNET-ADDR
* * * * * * * * * * * * *
*
  FIND AT-V WITH ID-NUMBER = #LBL-CN-ID
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
*
    IF INTERNET-ADDR GT ' '
     AND AT-V.PLAN NE ' ' AND AT-V.DATE-OF-BIRTH NE 0             /* PM3
      RESET #ADDR-LINE #HOLD-LAST-NAME                            /* PM3
            #DISPLAY-PREMIUM #DISPLAY-LOAN #DISPLAY-TTL-PREMIUM   /* PM3
*
      MOVE AT-V.ID-NUMBER TO #CGA-SCR-ID                          /* PM3
      CALLNAT 'GPREMSDX'                                          /* PM3
        #CGA-SCR-ID
        #ANN-PREM
        #MON-PREM
        #CGA-ORIG-COMMAND
        #CGA-CLERK
*
      COMPUTE #TOTAL-PMT = #LOAN-PMT + #MON-PREM                  /* PM3
      MOVE EDITED #MON-PREM (EM=ZZZZZ9.99) TO #DISPLAY-PREMIUM    /* PM3
      MOVE EDITED #LOAN-PMT (EM=ZZZZZ9.99) TO #DISPLAY-LOAN       /* PM3
      MOVE EDITED #TOTAL-PMT (EM=ZZZZZZ9.99) TO #DISPLAY-TTL-PREMIUM
      MOVE LEFT #DISPLAY-PREMIUM     TO #DISPLAY-PREMIUM          /* PM3
      MOVE LEFT #DISPLAY-LOAN        TO #DISPLAY-LOAN             /* PM3
      MOVE LEFT #DISPLAY-TTL-PREMIUM TO #DISPLAY-TTL-PREMIUM      /* PM3
      COMPRESS '$' #DISPLAY-PREMIUM INTO #DISPLAY-PREMIUM  LEAVING NO
      COMPRESS '$' #DISPLAY-LOAN    INTO #DISPLAY-LOAN LEAVING NO /* PM3
      COMPRESS '$' #DISPLAY-TTL-PREMIUM                           /* PM3
                                    INTO #DISPLAY-TTL-PREMIUM LEAVING NO
*
      RESET #CLERK-ABB-NAME-PASS                                  /* PM3
      CALLNAT 'GCKNMSXX' AT-V.USER-SOURCE                         /* PM3
                         #INVALID                                 /* PM3
                         #CLERK-ABB-NAME-PASS                     /* PM3
      IF #INVALID                                                 /* PM3
        MOVE AT-V.USER-SOURCE TO #CLERK-ABB-NAME-PASS             /* PM3
      END-IF                                                      /* PM3
*
      MOVE #LAST TO #HOLD-LAST-NAME                               /* PM3
      PERFORM UPPER-LOWER                                         /* PM3
*
      MOVE INTERNET-ADDR TO #ADDR-LINE
      COMPRESS #ADDR-LINE           #TAB                          /* PM3
               AT-V.ID-NUMBER       #TAB                          /* PM3
               #DISPLAY-PREMIUM     #TAB                          /* PM3
               #DISPLAY-LOAN        #TAB                          /* PM3
               #DISPLAY-TTL-PREMIUM #TAB                          /* PM3
               #RANK-LIT            #TAB                          /* PM3
               #HOLD-LAST-NAME      #TAB                          /* PM3
               #CLERK-ABB-NAME-PASS INTO #ADDR-LINE LEAVING NO    /* PM3
*
      WRITE WORK 3 #ADDR-LINE
      ESCAPE BOTTOM
    END-IF
  END-FIND
*
END-SUBROUTINE
*
************************************************************************
DEFINE UPPER-LOWER
************************************************************************
*
FOR #LET-1 2 TO 25
  FOR #LET-2 1 TO 26
    IF #HOLD-LAST-NAME-A (#LET-1) = #UPPER (#LET-2)
      MOVE #LOWER (#LET-2) TO #HOLD-LAST-NAME-A (#LET-1)
    END-IF
  END-FOR
END-FOR
*
END-SUBROUTINE
*
END
