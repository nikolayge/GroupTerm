* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* PROGRAM-ID: ATHPSYN7    DELETE AAFMAA RECORDS FROM AFSC FILE
*                         and set existing aafmaa ins info for AFSC
* DELETES AFSC Appl-Tracking records (46) ON AFSC FILE  <<<<<<<<<<<<<<
*    DO BELOW SEPARATELY
* DELETE ENTIRE VA HISTORY     FILE (30) ON AFSC FILE  <<<<<<<<<<<<<<<
* DELETE ENTIRE INTRO MEMBER   FILE (36) ON AFSC FILE  <<<<<<<<<<<<<<<
* DELETE ENTIRE DEPOSITS       FILE (45) ON AFSC FILE  <<<<<<<<<<<<<<<
* DELETE ENTIRE GUARANTEE-CSV  FILE (61) ON AFSC FILE  <<<<<<<<<<<<<<<
* DELETE ENTIRE VAR-TERM-RATES FILE (65) ON AFSC FILE  <<<<<<<<<<<<<<<
* DELETE ENTIRE INT-1099            (80) ON AFSC FILE  <<<<<<<<<<<<<<<
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* DRW    07272006          Create Program
************************************************************************
*
DEFINE DATA
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER
 2 INTRO-FLAG
*
1 SL-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PROCESS-IND
 2 INTRO-FLAG
 2 OTHER-COMPANY-ID
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A   (A6)
 2 LINKED-CN   (1)  /* Use 1st occ on sps/gen to find linked mbr
 2 REDEFINE LINKED-CN
  3 LINKED-CN-OCC   (1)
   4 LINKED-CN-A  (A6)
 2 MEMBER-FLAG
 2 WIDOW-FLAG
 2 PROCESS-IND
 2 OTHER-COMPANY-ID
 2 REDEFINE OTHER-COMPANY-ID
  3 OTHER-COMPANY-ID-A   (A6)
 2 DATA-SHARED-IND
 2 INTRO-FLAG
*
 2 CAP-ENTRY-DATE
 2 DT-MBR-CARD-SINCE
 2 TRAD-UP-SENT-DT
 2 ALOT-CHG-AUTH
 2 CAP-CONV-ACV
 2 DUES-ALL-PAID-IND
 2 DUES-BALANCE
 2 DUES-FPF
 2 DUES-TOT-PAID
 2 TERM-PREM-REF-DUE
 2 TERM-DEBIT-CREDIT
 2 TERM-LOAN-PRIN
 2 TERM-ACC-INT
 2 TERM-CAP-INT
 2 TERM-PREM-PAID
 2 TERM-CASH-VALUE
 2 MTD-ACV-PREM-ADJ  (9)
 2 MTD-ACV-INT-ADJ   (9)
 2 STM-P-I           (9)
 2 STM-TPP           (9)
 2 STM-PREM          (9)
 2 STM-COI           (9)
 2 STM-EQU           (9)
 2 STM-INT           (9)
 2 STM-BEG-CV        (9)
 2 STM-END-CV        (9)
 2 STM-BEG-DB        (9)
 2 STM-END-DB        (9)
 2 STM-P-I-LAST      (9)
 2 STM-TPP-LAST      (9)
 2 STM-PREM-LAST     (9)
 2 STM-COI-LAST      (9)
 2 STM-EQU-LAST      (9)
 2 STM-INT-LAST      (9)
 2 STM-BEG-CV-LAST   (9)
 2 STM-END-CV-LAST   (9)
 2 STM-BEG-DB-LAST   (9)
 2 STM-END-DB-LAST   (9)
 2 STATUS            (9)
 2 STM-SVC-FEE       (9)
 2 STM-SVC-FEE-LAST  (9)
 2 X-LOAN-AMOUNT         (9)
 2 X-LOAN-AMOUNT-2       (9)
 2 X-INTEREST-RATE       (9)
 2 X-PRINCIPAL-CHG-CODE  (9)
 2 X-LOAN-DATE           (9)
 2 X-LOAN-DATE-2         (9)
 2 X-ACCUM-INTEREST      (9)
 2 X-ACCUM-INTEREST-2    (9)
 2 X-NXT-YR-ACCUM-INT    (9)
 2 X-NXT-YR-ACCUM-INT-2  (9)
 2 X-BILL-INTEREST-AMT   (9)
 2 X-BILL-INTEREST-AMT-2 (9)
 2 X-CAP-AMT             (9)
 2 X-CAP-AMT-2           (9)
 2 X-OLD-LOAN-AMT        (9)
 2 X-OLD-LOAN-AMT-2      (9)
 2 X-INTEREST-RECEIVED   (9)
 2 X-INTEREST-RECEIVED-2 (9)
 2 X-ALT-LP-AMT          (9)
 2 X-ALT-LP-AMT-2        (9)
 2 X-ALT-LP-IND          (9)
 2 X-ALT-LP-IND-2        (9)
 2 X-BILL-SW             (9)
 2 X-BILL-SW-2           (9)
 2 X-LPO-PMT-RCVD        (9)
 2 X-LPO-PMT-RCVD-2      (9)
 2 X-LPO-PRIOR-LOAN-BAL  (9)
 2 X-LPO-PRIOR-LOAN-BAL-2 (9)
 2 X-LPO-PRIOR-ACC-INT    (9)
 2 X-LPO-PRIOR-ACC-INT-2  (9)
 2 LPO-PMT-RCVD           (9)
 2 LPO-PRIOR-LOAN-BAL     (9)
 2 LPO-PRIOR-ACC-INT      (9)
 2 APL-CNT
 2 NUMBER-MEMBERSHIPS
 2 ISSUE-AGE              (9)
 2 CONVERSION-FLAG        (9)
 2 SMOKER-FLAG            (9)
 2 ANNUAL-PREM            (9)
 2 EXPIRY-DAY             (9)
 2 LAST-DURATION          (9)
 2 CASH-VALUE             (9)
 2 TOT-PREMIUMS-PAID      (9)
 2 DURATION               (9)
 2 MONTH-IND              (9)
 2 MIN-CASH-VALUE         (9)
 2 NXT-YR-MIN-CASH-VALUE  (9)
 2 NXT-MON-CASH-VALUE     (9)
 2 LAST-ANN-CASH-VALUE    (9)
 2 ACC-CASH-VALUE         (9)
 2 CONV-DURATION          (9)
 2 MONTH-ALLOC-AMT        (9)
 2 PREM-DEFICIENCY        (9)
 2 BEG-CASH-VALUE         (9)
 2 NET-PR-RATIO           (9)
 2 COST-BASIS             (9)
 2 OWNERSHIP-FLAG         (9)
 2 ORIG-COST-BASIS        (9)
 2 RID-PLAN               (9)
 2 RID-EFF-DT             (9)
 2 RID-PD-TO              (9)
 2 MEC-IND                 (9)
*
 2 DEATH-BENEFIT     (9)
 2 NXT-MON-DEATH-BENEFIT (9)
*
 2 RID-FPF
 2 RID-BALANCE
 2 RID-TOT-PAID
 2 RID-SPS
*
1 FR-V VIEW OF A-FIRS
 2 ID-NUMBER
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER
*
1 CL-V VIEW OF A-CLAIM
 2 ID-NUMBER
*
1 VA-V VIEW OF A-VA-HISTORY
 2 RECORD-KEY
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (N4)
 2 #MM     (N2)
 2 #DD     (N2)
*
1 #I1                (I2)
1 #I2                (I2)
1 #I3                (I2)
1 #I4                (I2)
1 #FIRS-DELETED    (L)
1 #NO-FIRS-MBR-FND (L)
1 #NT-NUM      (N6)
1 #AT-DEL     (N7)
1 #FR-DEL     (N7)
1 #CL-DEL     (N7)
1 #NT-DEL     (N7)
1 #BD-DEL     (N7)
1 #AFSC-RECS   (N7)
1 #AAFMAA-RECS (N7)
1 #AAFMAA-DELETED (N7)
1 #AAFMAA-POLS-MODIFIED (N7)
*
1 #CNT-2   (N5)
1 #CNT-3   (N5)
1 #CNT-4   (N5)
1 #CNT-5   (N5)
1 #CNT-6   (N5)
1 #CNT-7   (N5)
1 #CNT-8   (N5)
1 #CNT-9   (N5)
1 #SPS-GEN-LINKED-TO-GF-COMBO (L)
*
1 #SUFFIX     (A3)
1 REDEFINE #SUFFIX
 2 #SUFFIX-2   (A2)
 2 #SUFFIX-X   (N1)
1 #AS-OF-DT   (N8)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #FACE-AMT    (N7.2/9)
1 #TPP         (N7.2/9)
1 #CSV-P      (P7.2/9)
*
END-DEFINE
*
ON ERROR
  WRITE   / '*E R R O R - N R    ' *ERROR-NR
          / '*E R R O R - L I N E' *ERROR-LINE
          / '*P R O G R A M = ATHPSYN1'
END-ERROR
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Exceptions' 5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA STATUS Records deleted from AFSC file'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA Others Records deleted from AFSC file'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA Combo records retained'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=133 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'SPS/GEN Records retained due to Linkage to G/F or Combo"s'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ ST-V PHYSICAL
  IF  (ST-V.ID-NUMBER GE 600000)
   OR (ST-V.ID-NUMBER LT 1900)
    ADD 1 TO #AFSC-RECS
*   END TRANSACTION
    ESCAPE TOP            /* AFSC should KEEP these records
  END-IF
*
  IF ST-V.PROCESS-IND = ' '
    WRITE ST-V.ID-NUMBER 'Process-Ind = blank'
  END-IF
  RESET #SPS-GEN-LINKED-TO-GF-COMBO
  IF ST-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'    /* SPS/GEN
    FIND SL-V WITH MEMBER-KEY = ST-V.LINKED-CN-A (1)
      IF   (SL-V.PROCESS-IND = 'G')
       OR  (SL-V.OTHER-COMPANY-ID NE 0)
        MOVE TRUE TO #SPS-GEN-LINKED-TO-GF-COMBO
*
        ADD 1 TO #CNT-5
        DISPLAY (5) #CNT-5 'CN' ST-V.ID-NUMBER
          'Process/Ind'   ST-V.PROCESS-IND
          'Intro/Flag'    ST-V.INTRO-FLAG
          'Other/Comp/ID' ST-V.OTHER-COMPANY-ID
          'Linked/CN'     ST-V.LINKED-CN-A (1)
      END-IF
    END-FIND
  END-IF
*
  IF ST-V.OTHER-COMPANY-ID NE 0  /* Members of both AAFM and AFSC
    FIND SL-V WITH MEMBER-KEY = ST-V.OTHER-COMPANY-ID-A
    END-FIND
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4 'CN' ST-V.ID-NUMBER
      'Process/Ind'   ST-V.PROCESS-IND
      'Intro/Flag'    ST-V.INTRO-FLAG
      'Other/Comp/ID' ST-V.OTHER-COMPANY-ID
      'DATA/SHARED/IND' ST-V.DATA-SHARED-IND
      'LINKED/CN'          SL-V.ID-NUMBER
      'Linked/Process/Ind' SL-V.PROCESS-IND
      'Linked/Intro/Flag'  SL-V.INTRO-FLAG
  END-IF
*
*   SAME logic in ATHPSYN1
  IF  #SPS-GEN-LINKED-TO-GF-COMBO             /* SPS/GEN LINKED to GF
   OR ((WIDOW-FLAG = 'Y')                    /* Deceased Insured
        AND (ST-V.ID-NUMBER LT 600000))      /* if processing AAFMAA mbr
   OR ((ST-V.OTHER-COMPANY-ID NE 0) /* Members of both AAFM and AFSC
        AND (ST-V.ID-NUMBER LT 600000)) /* if processing AAFMAA mbr
   OR ((ST-V.PROCESS-IND = 'G' OR = 'D' OR = 'O') /* GF DE-GF & OPTD-OT
      AND (NOT ST-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'))  /* which are
*                                                        /* not SPS/GEN
    PERFORM CLEAR-AAFMAA-INFO
    ADD 1 TO #AAFMAA-RECS
*   UPDATE (0263)                 /* AFSC should KEEP these records
  ELSE
*   DELETE (0263)
*
    ADD 1 TO #AAFMAA-DELETED
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ST-V.ID-NUMBER
       'Proc/Ind'  PROCESS-IND
       'Intr/Flag' INTRO-FLAG
       'Num/Mbrshp' NUMBER-MEMBERSHIPS
    PERFORM DELETE-OTHER-AAFMAA
  END-IF
*
* END TRANSACTION
END-READ
*
READ AT-V BY ID-NUMBER
  IF AT-V.INTRO-FLAG NE 'V'
    ADD 1 TO #AT-DEL
*   DELETE (0332)
  END-IF
*
*  END TRANSACTION
END-READ
*
WRITE 'TOT DELETED'
WRITE '#AT-DEL'  #AT-DEL
WRITE '#FR-DEL'  #FR-DEL
WRITE '#CL-DEL'  #CL-DEL
WRITE '#NT-DEL'  #NT-DEL
WRITE '#BD-DEL'  #BD-DEL
WRITE 'AAFMAA DELETED'  #AAFMAA-DELETED
*
WRITE 'TOT LEFT'
WRITE 'AFSC RECS' #AFSC-RECS
WRITE 'AAFMAA RECS' #AAFMAA-RECS
WRITE 'AAFMAA pols modified'
  #AAFMAA-POLS-MODIFIED
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CLEAR-AAFMAA-INFO
* * * * * * * * * * * * * * * * *
*
IF ST-V.WIDOW-FLAG = ' '   /* member alive
  MOVE *DATN TO #AS-OF-DT              /* <<<<<<<<<<<<<<<<<<<<<<
  CALLNAT 'CVALUSXX' /* To get current Death Benefit
    ST-V.ID-NUMBER
    #SUFFIX
    #AS-OF-DT
    #LOAN-BAL   (1:9)
    #LOAN-BAL-2 (1:9)
    #ACC-INT    (1:9)
    #ACC-INT-2  (1:9)
    #CSV        (1:9)
    #CVALUE     (1:9)
    #LVALUE     (1:9)
    #MCSV       (1:9)
    #DB         (1:9)
    #ANN-PREMIUM  (1:9)
    #FACE-AMT   (1:9)
    #TPP        (1:9)
END-IF
*
RESET
  CAP-ENTRY-DATE
  DT-MBR-CARD-SINCE
  TRAD-UP-SENT-DT
  ALOT-CHG-AUTH
  CAP-CONV-ACV
  DUES-ALL-PAID-IND
  DUES-BALANCE
  DUES-FPF
  DUES-TOT-PAID
  TERM-PREM-REF-DUE
  TERM-DEBIT-CREDIT
  TERM-LOAN-PRIN
  TERM-ACC-INT
  TERM-CAP-INT
  TERM-PREM-PAID
  TERM-CASH-VALUE
  APL-CNT
  RID-FPF
  RID-BALANCE
  RID-TOT-PAID
  RID-SPS
*
FOR #I1 1 TO 9
  IF STATUS (#I1) = 'D'
    ADD 1 TO #AAFMAA-POLS-MODIFIED
    IF MONTH-IND (#I1) NE 999   /* Already converted
      MOVE #DB (#I1) TO DEATH-BENEFIT (#I1) NXT-MON-DEATH-BENEFIT (#I1)
      MOVE 999       TO MONTH-IND     (#I1)
    END-IF
*
    RESET
     MTD-ACV-PREM-ADJ  (#I1)
     MTD-ACV-INT-ADJ   (#I1)
     STM-P-I           (#I1)
     STM-TPP           (#I1)
     STM-PREM          (#I1)
     STM-COI           (#I1)
     STM-EQU           (#I1)
     STM-INT           (#I1)
     STM-BEG-CV        (#I1)
     STM-END-CV        (#I1)
     STM-BEG-DB        (#I1)
     STM-END-DB        (#I1)
     STM-P-I-LAST      (#I1)
     STM-TPP-LAST      (#I1)
     STM-PREM-LAST     (#I1)
     STM-COI-LAST      (#I1)
     STM-EQU-LAST      (#I1)
     STM-INT-LAST      (#I1)
     STM-BEG-CV-LAST   (#I1)
     STM-END-CV-LAST   (#I1)
     STM-BEG-DB-LAST   (#I1)
     STM-END-DB-LAST   (#I1)
     STM-SVC-FEE       (#I1)
     STM-SVC-FEE-LAST  (#I1)
     X-LOAN-AMOUNT         (#I1)
     X-LOAN-AMOUNT-2       (#I1)
     X-INTEREST-RATE       (#I1)
     X-PRINCIPAL-CHG-CODE  (#I1)
     X-LOAN-DATE           (#I1)
     X-LOAN-DATE-2         (#I1)
     X-ACCUM-INTEREST      (#I1)
     X-ACCUM-INTEREST-2    (#I1)
     X-NXT-YR-ACCUM-INT    (#I1)
     X-NXT-YR-ACCUM-INT-2  (#I1)
     X-BILL-INTEREST-AMT   (#I1)
     X-BILL-INTEREST-AMT-2 (#I1)
     X-CAP-AMT             (#I1)
     X-CAP-AMT-2           (#I1)
     X-OLD-LOAN-AMT        (#I1)
     X-OLD-LOAN-AMT-2      (#I1)
     X-INTEREST-RECEIVED   (#I1)
     X-INTEREST-RECEIVED-2 (#I1)
     X-ALT-LP-AMT          (#I1)
     X-ALT-LP-AMT-2        (#I1)
     X-ALT-LP-IND          (#I1)
     X-ALT-LP-IND-2        (#I1)
     X-BILL-SW             (#I1)
     X-BILL-SW-2           (#I1)
     X-LPO-PMT-RCVD        (#I1)
     X-LPO-PMT-RCVD-2      (#I1)
     X-LPO-PRIOR-LOAN-BAL  (#I1)
     X-LPO-PRIOR-LOAN-BAL-2 (#I1)
     X-LPO-PRIOR-ACC-INT    (#I1)
     X-LPO-PRIOR-ACC-INT-2  (#I1)
     LPO-PMT-RCVD           (#I1)
     LPO-PRIOR-LOAN-BAL     (#I1)
     LPO-PRIOR-ACC-INT      (#I1)
     ISSUE-AGE              (#I1)
     CONVERSION-FLAG        (#I1)
     SMOKER-FLAG            (#I1)
     ANNUAL-PREM            (#I1)
     EXPIRY-DAY             (#I1)
     LAST-DURATION          (#I1)
     CASH-VALUE             (#I1)
     TOT-PREMIUMS-PAID      (#I1)
     DURATION               (#I1)
     MIN-CASH-VALUE         (#I1)
     NXT-YR-MIN-CASH-VALUE  (#I1)
     NXT-MON-CASH-VALUE     (#I1)
     LAST-ANN-CASH-VALUE    (#I1)
     ACC-CASH-VALUE         (#I1)
     CONV-DURATION          (#I1)
     MONTH-ALLOC-AMT        (#I1)
     PREM-DEFICIENCY        (#I1)
     BEG-CASH-VALUE         (#I1)
     NET-PR-RATIO           (#I1)
     COST-BASIS             (#I1)
     OWNERSHIP-FLAG         (#I1)
     ORIG-COST-BASIS        (#I1)
     RID-PLAN               (#I1)
     RID-EFF-DT             (#I1)
     RID-PD-TO              (#I1)
     MEC-IND                (#I1)
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DELETE-OTHER-AAFMAA
* * * * * * * * * * * * * * * * *
*
RESET #FIRS-DELETED #NO-FIRS-MBR-FND
FIND FR-V WITH MEMBER-KEY = ST-V.ID-NUMBER-A
  IF NO RECORDS FOUND
    MOVE TRUE TO #NO-FIRS-MBR-FND
    FIND FR-V WITH WIDOW-KEY = ST-V.ID-NUMBER-A
*     DELETE (0507)
      ADD 1 TO #FR-DEL
      WRITE (3) '  ****' FR-V.ID-NUMBER 'Widow FIRS Record deleted'
      MOVE TRUE TO #FIRS-DELETED
    END-FIND
  END-NOREC
*
  IF   (NOT #FIRS-DELETED)
   AND (NOT #NO-FIRS-MBR-FND)
*   DELETE (0504)
    ADD 1 TO #FR-DEL
    WRITE (3) '  ****' FR-V.ID-NUMBER 'Member FIRS Record deleted'
  END-IF
END-FIND
*
FIND NT-V WITH ID-NUMBER = ST-V.ID-NUMBER
* DELETE (0523)
  ADD 1 TO #NT-DEL
  WRITE (3) '  ****' NT-V.ID-NUMBER 'NT record deleted'
END-FIND
*
FIND CL-V WITH ID-NUMBER = ST-V.ID-NUMBER
* DELETE (0529)
  ADD 1 TO #CL-DEL
  WRITE (3) '  ****' CL-V.ID-NUMBER 'CL record deleted'
END-FIND
*
COMPUTE #NT-NUM = ST-V.ID-NUMBER + 200000
FIND NT-V WITH ID-NUMBER = #NT-NUM
* DELETE (0536)
  ADD 1 TO #BD-DEL
  WRITE (3) '  ****' NT-V.ID-NUMBER 'Pend BN record deleted'
END-FIND
*
COMPUTE #NT-NUM = ST-V.ID-NUMBER + 400000
FIND NT-V WITH ID-NUMBER = #NT-NUM
* DELETE (0543)
  ADD 1 TO #BD-DEL
  WRITE (3) '  ****' NT-V.ID-NUMBER 'Final BN record deleted'
END-FIND
*
END-SUBROUTINE
*
END
