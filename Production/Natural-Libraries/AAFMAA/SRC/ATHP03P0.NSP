* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP03P0
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* DRW    09182004  DW1  bypass accounting & maint online records with  *
*                       1st of next month date created by ATHPCPLN     *
* DRW    09262005  DW2  Extent TR Stirage to 12 months                 *
* SAG    11282005  RH1  SS - Modified the custom coded ON ERROR stmt & *
*                       replaced it with copycode GABNDCX4             *
* EDS    09272006  PM1  Removed packed time from transaction record    *
* PAM    20070329  PM2  Do not delete transactions/accounting/or       *
*                       memberships records                            *
* DRW  02/23/2008  DW3  Store CASH-CODE in ACCT Trans Output
* DRW  02/22/2008  DW4  Send SG-CATEGORY-1 to Extyract file (ATHP08P1)
* DRW  03/20/2008  DW5  Stop writes on future trans
* vxt  09/27/2013  vt1  Expand the size of used counters
************************************************************************
*  THIS PROGRAM RUNS IN BATCH MODE ONLY AND IS USED
*  TO REFORMAT THE TRANSACTION-REGISTER FILE AND
*  THE ACCOUNTING-REGISTER FILE INTO A FORMAT THAT
*  THE OLD BATCH SYSTEM IS FAMILIAR WITH (IE. ATHP10P1)
*
*  ALSO IT DELETES ALL RECORDS WITH ENTRY DATES NOT IN THE PRIOR
*  or prior/prior
*  MONTH FROM THE 3 FILES (ACCT-REG/TRANS-REG/MBRSHIP-MTD-DETAIL)
*  EXCEPT FOR 'HALF-DEADS' ON MBRSHIP-MTD-DETAIL
*
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 #WORK-FIELDS
  2 #ABEND-CODE       (I4) INIT<4095>
  2 #IN-CNT           (P11) INIT<0>        /* vt1 was p7
  2 #ACCT-IN-CNT      (P11) INIT<0>        /* vt1 was p7
  2 #OUT-CNT          (P11) INIT<0>        /* vt1 was p7
  2 #UNSETTLED        (P11) INIT<0>        /* vt1 was p7
  2 #DEL-ACCT         (P7) INIT<0>         
  2 #DEL-TRAN         (P7) INIT<0>
  2 #DEL-MTD          (P7) INIT<0>
  2 #DEL-MTD-AFSC     (P7) INIT<0>      /* PAM
  2 #KEPT-DEAD        (P7) INIT<0>
  2 #REAL-ENTRY-DATE  (N8) INIT<0>
  2 #NOT-FND          (L)
*
  2 #TIME-P7          (P7)
  2 REDEFINE #TIME-P7
    3 #TIME-A3        (A3)
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL
  2 ENTRY-DATE       (N8)
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
   3 ID-NUMBER-A     (A6)
  2 REC-ISN          (N8)
  2 USER             (A3)
  2 CODE             (A1)
*
1 ST-V     VIEW OF A-STATUS
  2 ID-NUMBER          (N6)
  2 NUMBER-MEMBERSHIPS (P1)
  2 PROCESS-IND        (A1)
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER        (N6)
  2 NAME             (A25)
  2 DATE-LAST-UPDATE (N8)
  2 REDEFINE DATE-LAST-UPDATE
    3 DLU-CENT       (A2)
    3 DLU-YY         (A2)
    3 DLU-MM         (A2)
    3 DLU-DD         (A2)
  2 TERMINAL-ID      (A8)
  2 REDEFINE TERMINAL-ID
    3 TERMINAL-ID-4  (A4)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (N9)
  2 REDEFINE TRANSACTION-DATN
    3 TD-CENT        (A3)
    3 TD-YY          (A2)
    3 TD-MM          (A2)
    3 TD-DD          (A2)
  2 TRANSACTION-TIMN (N7)
  2 REDEFINE TRANSACTION-TIMN
    3 TRANSACTION-TIMN-A6 (A6)
  2 C*CHANGED-DATA-TABLE
  2 OLD-DATA         (A125/32)
  2 NEW-DATA         (A125/32)
1 #TRX               (I2)
*
1 #DATN              (N8)
1 REDEFINE #DATN
  2 #CENT          (A2)
  2 #YYMMDD        (A6)
  2 REDEFINE #YYMMDD
    3 #YY          (A2)
    3 #MM          (A2)
    3 #DD          (A2)
1 REDEFINE #DATN
  2 #DATN-YYYYMM   (N6)
  2 #DATN-DAY      (N2)
*
1 #TIMN            (N7)
1 REDEFINE #TIMN
  2 #TIMN-A6       (A6)
*
1 ACCOUNTING-TRLOG VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER
  2 TRANSACTION-DATN
  2 TRANSACTION-TIMN
  2 TXN-CODE
  2 SEQUENCE-NUM
  2 NAME
  2 TXN-ACCT-NUMBER
  2 ACCT-DATE
  2 DATE-LAST-ACCT-UPDATE
  2 REDEFINE DATE-LAST-ACCT-UPDATE
    3 DLAU-CENT       (A2)
    3 DLAU-YY         (A2)
    3 DLAU-MM         (A2)
    3 DLAU-DD         (A2)
  2 DEBIT-AMOUNT
  2 CREDIT-AMOUNT
  2 DESCRIPTION
  2 CASH-CODE                     /* DW3
  2 MESSAGE-CODE
  2 AALP-FLAG
  2 CLERK-ID
  2 TERMINAL-ID
  2 REDEFINE TERMINAL-ID
    3 ACT-TERM-ID-4   (A4)
  2 SG-CATEGORY-1    (A2/15)     /* Used for 'CP'    /* DW4
/* END OF VIEW ACCOUNTING-TRLOG
*
*  WORK FILE RECORD - TRANSACTION-REGISTER
*
1 TRAN-PRINT-RECORD  (A220)
1 REDEFINE TRAN-PRINT-RECORD
  2 TRAN-ID          (A1)     /* INIT<'F'>
  2 TRAN-CN-NUMBER
    3 TRAN-CN-LIT    (A2)     /* INIT<'CN'>
    3 TRAN-ID-NUMBER (N6)
    3 TRAN-CN-FILL   (A1)
  2 TRAN-DATE        (A6)     /* FORMAT 'MMDDYY'
* 2 TRAN-TIME        (A3)                       /* PM1
  2 TRAN-TIME        (A6)                       /* PM1
  2 TRAN-TYPE        (A1)
  2 TRAN-LAST-UPDATE (A6)
  2 TRAN-DATA        (A125)
  2 TRAN-NAME        (A25)
  2 TRAN-FILL1       (A6)
  2 TRAN-FILL2       (A10)
* 2 TRAN-FILL3       (A8)                       /* PM1
  2 TRAN-FILL3       (A5)                       /* PM1
  2 TRAN-CLERK       (A4)
  2 TRAN-TERMINAL    (A4)
  2 TRAN-TRDATE      (A6)
  2 TRAN-TRTIME      (A6)
*
*  WORK FILE RECORD - ACCOUNTING-REGISTER
*
1 ACCT-PRINT-RECORD (A220)
1 REDEFINE ACCT-PRINT-RECORD
  2 ACCT-TYPE        (A1)     /* INIT<'B'>
  2 ACCT-FILL1       (A3)
  2 ACCT-CN-NUMBER
    3 ACCT-CN-LIT    (A2)     /* INIT<'CN'>
    3 ACCT-ID        (N6)
    3 ACCT-CN-FILL   (A1)
  2 ACCT-ACCT-DATE   (A6)     /* FORMAT 'MMDDYY'
  2 ACCT-CODE        (A4)
  2 ACCT-COL         (A1)     /* SEQUENCE NO FOR MULTI TXNS
  2 ACCT-NAME        (A25)
  2 ACCT-FILL2       (A1)
  2 ACCT-CERT        (A5)     /* NOT USED
  2 ACCT-NO          (A3)
  2 ACCT-LDTE        (A6)     /* LAST ACCTG UPDATE MMDDYY
  2 ACCT-DEBIT-AMT   (N7.2)
  2 ACCT-CREDIT-AMT  (N7.2)
  2 ACCT-REF         (A10)    /* ONLY USED IN BATCH
  2 ACCT-DESCRIPTION (A20)
  2 ACCT-MSG-CODE    (A1)
  2 ACCT-PROCESS-DATE (A6)
  2 ACCT-FILL3       (A4)
  2 ACCT-AALP-FLAG   (A1)
  2 ACCT-CLERK       (A4)
  2 ACCT-TRDATE      (A6)     /* MMDDYY
  2 ACCT-TRTIME      (A6)     /* HHMMSS
  2 ACCT-TERMINAL    (A4)
*
1 #DATAIN-REC         (A80)
1 #BATCH-PARM         (A10)
1 REDEFINE #BATCH-PARM
 2 #BATCH-PARM-MM     (N2)
 2 #FILL-1            (A1)
 2 #BATCH-PARM-DD     (N2)
 2 #FILL-2            (A1)
 2 #BATCH-PARM-YYYY   (N4)
*
1 #BATCH-PARM-YYYYMMDD  (N8)
1 #CUTOFF-X-MON-YYYYMMDD-G
 2 #CUTOFF-X-MON-YYYY (N4)
 2 #CUTOFF-X-MON-MM   (N2)
 2 #CUTOFF-X-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-X-MON-YYYYMMDD-G
 2 #CUTOFF-X-MON-YYYYMMDD (N8)
*
1 #CUTOFF-2-MON-YYYYMMDD-G
 2 #CUTOFF-2-MON-YYYY (N4)
 2 #CUTOFF-2-MON-MM   (N2)
 2 #CUTOFF-2-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-2-MON-YYYYMMDD-G
 2 #CUTOFF-2-MON-YYYYMMDD (N8)
*
* 1 #CUTOFF-3-MON-YYYYMMDD-G
*  2 #CUTOFF-3-MON-YYYY (N4)
*  2 #CUTOFF-3-MON-MM   (N2)
*  2 #CUTOFF-3-MON-DD   (N2) INIT <01>
* 1 REDEFINE #CUTOFF-3-MON-YYYYMMDD-G
*  2 #CUTOFF-3-MON-YYYYMMDD (N8)
*
1 #CUTOFF-1-MON-YYYYMMDD-G
 2 #CUTOFF-1-MON-YYYY (N4)
 2 #CUTOFF-1-MON-MM   (N2)
 2 #CUTOFF-1-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-1-MON-YYYYMMDD-G
 2 #CUTOFF-1-MON-YYYYMMDD (N8)
*
1 #CUTOFF-1-YR-YYYYMMDD-G                             /* PAM
 2 #CUTOFF-1-YR-YYYY (N4)                             /* PAM
 2 #CUTOFF-1-YR-MM   (N2)                             /* PAM
 2 #CUTOFF-1-YR-DD   (N2) INIT <01>                   /* PAM
1 REDEFINE #CUTOFF-1-YR-YYYYMMDD-G                    /* PAM
 2 #CUTOFF-1-YR-YYYYMMDD (N8)                         /* PAM
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
  INCLUDE GABNDCX4                                          /* SAG RH1 11282005 SS - END
END-ERROR
*
READ WORK 2 ONCE RECORD #BATCH-PARM                         /* DATE  (10/01/1998
WRITE 'batch-parm=' #BATCH-PARM
*
MOVE #BATCH-PARM-YYYY TO #CUTOFF-1-MON-YYYY                 /* TAKE ATHP05P0 DATE PARM
*                                                           /* AND DEVELOP  THE 1ST DAY OF
MOVE #BATCH-PARM-MM         TO #CUTOFF-1-MON-MM
MOVE #CUTOFF-1-MON-YYYYMMDD TO #CUTOFF-2-MON-YYYYMMDD
MOVE #CUTOFF-1-MON-YYYYMMDD TO #BATCH-PARM-YYYYMMDD
WRITE 'batch-parm-yyyymmdd=' #BATCH-PARM-YYYYMMDD
MOVE #CUTOFF-1-MON-YYYYMMDD TO #CUTOFF-X-MON-YYYYMMDD
MOVE #CUTOFF-1-MON-YYYYMMDD TO #CUTOFF-1-YR-YYYYMMDD       /* PAM
*
SUBTRACT 1 FROM #CUTOFF-1-YR-YYYY                          /* PAM
*
SUBTRACT 12 FROM #CUTOFF-X-MON-MM   /* keep 12 full months /* DW2
*
IF #CUTOFF-X-MON-MM LT 01
  ADD 12 TO #CUTOFF-X-MON-MM
  SUBTRACT 1 FROM #CUTOFF-X-MON-YYYY
END-IF
*
SUBTRACT 2 FROM #CUTOFF-2-MON-MM
*
IF #CUTOFF-2-MON-MM LT 01
  ADD 12 TO #CUTOFF-2-MON-MM
  SUBTRACT 1 FROM #CUTOFF-2-MON-YYYY
END-IF
*
SUBTRACT 1 FROM #CUTOFF-1-MON-MM
*
IF #CUTOFF-1-MON-MM LT 01
  ADD 12 TO #CUTOFF-1-MON-MM
  SUBTRACT 1 FROM #CUTOFF-1-MON-YYYY
END-IF
*
FORMAT LS=133
PS=66
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
* PERFORM PURGE-MBRSHIP-MTD-DETAIL                        /* PM2
*
PERFORM PURGE-TRANSACTION-LOG
*
PERFORM PURGE-ACCOUNTING-LOG
*
WRITE 'TRANSACTION-REGISTER READ'  38T #IN-CNT
  / 'ACCOUNTING-REGISTER READ'     38T #ACCT-IN-CNT
  / 'ACCT REG RECS DELETED'        38T #DEL-ACCT
  / 'TRAN REG RECS DELETED'        38T #DEL-TRAN
  / 'RECORDS WRITTEN'              38T #OUT-CNT
  / 'AAFMAA MBRS MTD DET DELETED'  38T #DEL-MTD
  / 'AFSC MBRS MTD DET DELETED'    38T #DEL-MTD-AFSC
  / 'UNSETTLED CLAIMS/POLS'        38T #UNSETTLED
  / 'MBRS "WN" ED BUT NOT "CI" ED' 38T #KEPT-DEAD
*
********************************************************************************
DEFINE SUBROUTINE PURGE-MBRSHIP-MTD-DETAIL
********************************************************************************
*
* * * * * PURGE MBRSHIP-MTD-DETAIL FILE * * * NOT USED
*
READ MBRSHIP-MTD-DET-V PHYSICAL
  IF ENTRY-DATE LT 55555555
    ADD 1 TO #UNSETTLED
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  RESET #NOT-FND
  FIND ST-V WITH MEMBER-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
    IF NO RECORDS FOUND
      FIND ST-V WITH WIDOW-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
        IF NO RECORDS FOUND
          MOVE TRUE TO #NOT-FND
          WRITE 'ST NOT FOUND USING MD ID-NUMBER'
                   MBRSHIP-MTD-DET-V.ID-NUMBER
          WRITE   'MD CODE=' MBRSHIP-MTD-DET-V.CODE
          WRITE   'MD USER=' MBRSHIP-MTD-DET-V.USER
          WRITE   'MD ENTRY DATE=' MBRSHIP-MTD-DET-V.ENTRY-DATE
          WRITE   'MD REC-ISN=' MBRSHIP-MTD-DET-V.REC-ISN
        END-NOREC
      END-FIND
    END-NOREC
  END-FIND
*
  IF #NOT-FND
    ESCAPE TOP
  END-IF
*
  IF CODE = 'D'
    IF ST-V.NUMBER-MEMBERSHIPS NE 0   /* ONLY HALF-DEAD - KEEP IT
      WRITE ID-NUMBER 'Half-dead'
      ADD 1 TO #KEPT-DEAD
      END TRANSACTION
      ESCAPE TOP
    END-IF
  END-IF
*
  COMPUTE #REAL-ENTRY-DATE = 99999999 - ENTRY-DATE
*
   IF PROCESS-IND = 'S'                               /* PAM
     IF #REAL-ENTRY-DATE LT #CUTOFF-1-YR-YYYYMMDD     /* PAM
      ADD 1 TO #DEL-MTD-AFSC                          /* PAM
      DELETE (0311)                                   /* PAM
     END-IF                                           /* PAM
   ELSE                                               /* PAM
     IF #REAL-ENTRY-DATE LT #CUTOFF-2-MON-YYYYMMDD
      ADD 1 TO #DEL-MTD                               /* PAM
      DELETE (0311)                                   /* PAM
     END-IF
   END-IF                                             /* PAM
  END TRANSACTION
END-READ
*
END-SUBROUTINE
*
********************************************************************************
DEFINE SUBROUTINE PURGE-TRANSACTION-LOG
********************************************************************************
*
MOVE 'F'  TO TRAN-ID
MOVE 'CN' TO TRAN-CN-LIT
*
READ TRLOG PHYSICAL
  ADD 1 TO #IN-CNT
*
* IF TRANSACTION-DATN LT #CUTOFF-X-MON-YYYYMMDD /* DEL IF OVER x MONS PM2
*   ADD 1 TO #DEL-TRAN                                           /* PM2
*   DELETE                                                       /* PM2
*   END TRANSACTION                                              /* PM2
*   ESCAPE TOP                                                   /* PM2
* END-IF
*
* IF TRANSACTION-DATN GE #BATCH-PARM-YYYYMMDD                    /* DW1/DW5
*  AND (CLERK-ID = 'EDS')
*   WRITE 'Bypassed future Maint Tran'
*     TRLOG.ID-NUMBER
*     TRLOG.TRANSACTION-DATN
*     TRLOG.OLD-DATA (1)   (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXX)
*     TRLOG.NEW-DATA (1)   (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXX)
* END-IF
*
  IF  (TRANSACTION-DATN LT #CUTOFF-1-MON-YYYYMMDD) /* ignore if not in
    OR ((TRANSACTION-DATN GE #BATCH-PARM-YYYYMMDD)  /* curr month  DW1
       AND (CLERK-ID = 'EDS'))
*    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  COMPRESS DLU-MM DLU-DD DLU-YY INTO TRAN-LAST-UPDATE LEAVING NO
  COMPRESS TD-MM TD-DD TD-YY    INTO TRAN-TRDATE      LEAVING NO
  MOVE ID-NUMBER     TO TRAN-ID-NUMBER
  MOVE TRAN-TRDATE   TO TRAN-DATE
  MOVE NAME          TO TRAN-NAME
  MOVE CLERK-ID      TO TRAN-CLERK
  MOVE TERMINAL-ID-4 TO TRAN-TERMINAL
  MOVE TRANSACTION-TIMN-A6 TO TRAN-TRTIME
  MOVE TRANSACTION-TIMN-A6 TO TRAN-TIME         /* PM1
* MOVE TRANSACTION-TIMN TO #TIME-P7             /* PM1
* MOVE #TIME-A3      TO TRAN-TIME               /* PM1
*
  FOR #TRX 1 TO C*CHANGED-DATA-TABLE
    MOVE '1'                    TO TRAN-TYPE
    MOVE OLD-DATA (#TRX)        TO TRAN-DATA
    WRITE WORK 1 TRAN-PRINT-RECORD
    MOVE '2'                    TO TRAN-TYPE
    MOVE NEW-DATA (#TRX)        TO TRAN-DATA
    WRITE WORK 1 TRAN-PRINT-RECORD
    ADD 2 TO #OUT-CNT
  END-FOR
*
**  END TRANSACTION
END-READ
*
END-SUBROUTINE
*
********************************************************************************
DEFINE SUBROUTINE PURGE-ACCOUNTING-LOG
********************************************************************************
*
MOVE 'B'  TO ACCT-TYPE
MOVE 'CN' TO ACCT-CN-LIT
*
**READ ACCOUNTING-TRLOG PHYSICAL
READ MULTI-FETCH OF 20 ACCOUNTING-TRLOG PHYSICAL
  ADD 1 TO #ACCT-IN-CNT
*
* IF TRANSACTION-DATN LT #CUTOFF-X-MON-YYYYMMDD  /* del if > x mons  PM2
*   ADD 1 TO #DEL-ACCT                                           /* PM2
*   DELETE                                                       /* PM2
*   END TRANSACTION                                              /* PM2
*   ESCAPE TOP                                                   /* PM2
* END-IF                                                         /* PM2
*
  IF TRANSACTION-DATN GE #BATCH-PARM-YYYYMMDD                    /* DW1
       AND (CLERK-ID = 'EDS')
    WRITE 'Bypassed future Acct Tran'
        ID-NUMBER
        TRANSACTION-DATN
        TXN-CODE
        SEQUENCE-NUM
        DEBIT-AMOUNT
        CREDIT-AMOUNT
  END-IF
*
  IF  (TRANSACTION-DATN LT #CUTOFF-1-MON-YYYYMMDD) /* ignore if not in
   OR ((TRANSACTION-DATN GE #BATCH-PARM-YYYYMMDD)   /* curr month  DW1
       AND (CLERK-ID = 'EDS'))
*    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  COMPRESS DLAU-MM DLAU-DD DLAU-YY
    INTO ACCT-LDTE LEAVING NO
  MOVE ACCT-DATE           TO #DATN
  COMPRESS #MM #DD #YY   INTO ACCT-TRDATE LEAVING NO
  MOVE ACCT-TRDATE         TO ACCT-ACCT-DATE
  MOVE ID-NUMBER           TO ACCT-ID
  MOVE DEBIT-AMOUNT        TO ACCT-DEBIT-AMT
  MOVE CREDIT-AMOUNT       TO ACCT-CREDIT-AMT
  MOVE TXN-CODE            TO ACCT-CODE
  MOVE DESCRIPTION         TO ACCT-DESCRIPTION
  MOVE AALP-FLAG           TO ACCT-AALP-FLAG
  MOVE MESSAGE-CODE        TO ACCT-MSG-CODE
  move cash-code           to acct-ref        /* for ATHP08P1    DW3
*
  if SG-CATEGORY-1 (1) = 'CP'             /* CAP    /* DW4
    move 'CAP' to acct-description
  end-if
*
  MOVE SEQUENCE-NUM        TO ACCT-COL
  MOVE TXN-ACCT-NUMBER     TO ACCT-NO
  MOVE NAME                TO ACCT-NAME
  MOVE CLERK-ID            TO ACCT-CLERK
  MOVE ACT-TERM-ID-4       TO ACCT-TERMINAL
  MOVE TRANSACTION-DATN    TO #DATN
  COMPRESS #MM #DD #YY   INTO ACCT-PROCESS-DATE LEAVING NO
  MOVE TRANSACTION-TIMN    TO #TIMN
  MOVE #TIMN-A6            TO ACCT-TRTIME
*
  WRITE WORK 1 ACCT-PRINT-RECORD
  ADD 1 TO #OUT-CNT
*  END TRANSACTION
*
END-READ
*
END-SUBROUTINE
*
END
