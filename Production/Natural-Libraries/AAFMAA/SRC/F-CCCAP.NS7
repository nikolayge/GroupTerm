* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-CCCAP                                               *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Determine if Credit Card CAP application ready to     *
*                issue                                                 *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG     REASON                                     *
* PAM    06/05/2009 None    Program Written                            *
* PAM    08/03/2009 PM1     Revised process to include cut-off date    *
*                           LT now LE                                  *
************************************************************************
*
DEFINE FUNCTION FUNCTION-CHECK-CC-CAP-ISSUE RETURNS (A1) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #AT-NUMBER                  (N6)
1 #PARM-YYYYMMDD              (N8)
*
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 AT-STATUS
 2 ACTION-DATE
 2 ST-NOTE-TBL                (50)
  3 ST-NOTE-USER
  3 ST-NOTE-DATE
  3 ST-NOTE-STATUS
*
1 #PC                         (L)
1 #I1                         (I2)
1 #PC-DATE                    (N8)
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-CHECK-CC-CAP-ISSUE
*
FIND (1) AT-V WITH AT-V.ID-NUMBER = #AT-NUMBER
  DECIDE FOR FIRST CONDITION
    WHEN AT-V.AT-STATUS = 'PC' AND AT-V.ACTION-DATE LE #PARM-YYYYMMDD  /* PM1
      FUNCTION-CHECK-CC-CAP-ISSUE   := 'Y'
    WHEN AT-V.AT-STATUS = 'PP'
      PERFORM CHECK-PC
      IF #PC AND #PC-DATE LT #PARM-YYYYMMDD
        FUNCTION-CHECK-CC-CAP-ISSUE := 'Y'
      END-IF
    WHEN NONE
      IGNORE
  END-DECIDE
END-FIND
*
************************************************************************
DEFINE SUBROUTINE CHECK-PC
************************************************************************
*
RESET #PC #PC-DATE
FOR #I1 50 TO 1 STEP -1
  IF ST-NOTE-DATE (#I1) = 0
    ESCAPE TOP
  END-IF
*
  IF AT-V.ST-NOTE-STATUS (#I1) = 'PC'
    #PC := TRUE
    #PC-DATE := AT-V.ST-NOTE-DATE (#I1)
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
END-FUNCTION
*
END
