* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM ID DNXVN004   Former spouse data                                               *
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  08/16/2010  None    Program created                             *
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 BLANK  (A1)  CONST <' '>
1 #SEX   (A4)  CONST <'Sex:'>
1 #SSN   (A4)  CONST <'SSN:'>
1 #DOB   (A4)  CONST <'DOB:'>
1 #DOD   (A4)  CONST <'DOD:'>
1 #MARRDATE (A8) CONST <'Marr-DT:'>
1 #DIVDATE  (A7) CONST <'Div-DT:'>
1 #NOMARRDATE (A10) CONST <'Marr-DT: ?'>
1 #NODIVDATE  (A10) CONST <'Div-DT: ?'>
1 #NOSEX   (A8)  CONST  <'Sex: ?'>
1 #NOSSN   (A8)  CONST  <'SSN: ?'>
1 #NODOB   (A8)  CONST  <'DOB: ?'>
1 #NODOD   (A8)  CONST  <'DOD: ?'>

1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 GENDER-CD
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DOB-YEAR  (A4)
    3 DOB-MONTH (A2)
    3 DOB-DAY   (A2)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DOD-YEAR  (A4)
    3 DOD-MONTH (A2)
    3 DOD-DAY   (A2)
  2 SSN
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
1 STATUS VIEW OF A-STATUS
  2 FORMER-SPOUSE-STATUS
  2 FORMER-SPOUSE-DATE-OF-DIVORCE    (N8) 2 redefine FORMER-SPOUSE-DATE-OF-DIVORCE
    3 DOX-YEAR  (A4)
    3 DOX-MONTH (A2)
    3 DOX-DAY   (A2)
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE   (N8) 2 redefine FORMER-SPOUSE-DATE-OF-MARRIAGE
    3 DOM-YEAR  (A4)
    3 DOM-MONTH (A2)
    3 DOM-DAY   (A2)
1 #SPOUSE-CI          (N8)
1 #SSN-F              (A16)
1 #SSN-A              (A10) 1 REDEFINE #SSN-A
  2 #SSN-3            (A3)
  2 #SSN-2            (A2)
  2 #SSN-4            (A4)
1 #FORMER-SPOUSE-STATUS     (A1/7) CONST <'B','D','H','O','G','R','U'>
END-DEFINE

RESET #TEXT (*)
GET CONTACT #ISN
#TEXT (1) := FUNCTION-CONTACT-NAME-2 (< CONTACT.CONTACT-ID >)
COMPRESS 'Name:' #TEXT (1) INTO #TEXT (1)
FIND STATUS WITH MEMBER-CONTACT-ID = #CONTACT-ID
  IF FORMER-SPOUSE-STATUS = #FORMER-SPOUSE-STATUS (*)
    #TEXT (2) := FUNCTION-CODE-1-DESC (< '032', FORMER-SPOUSE-STATUS >)
    COMPRESS #TEXT (1) '(' #TEXT (2) ')' INTO #TEXT (1)
  END-IF
END-FIND

FIND CONTACT-REL WITH CONTACT-2-ID = CONTACT.CONTACT-ID  /* SPOUSE
  IF CONTACT-REL.ROLE-2-CD = 'X'
    #SPOUSE-CI := CONTACT-REL.CONTACT-1-ID
    ESCAPE BOTTOM
  END-IF
END-FIND

FIND STATUS WITH MEMBER-CONTACT-ID = #SPOUSE-CI
END-FIND

DECIDE FOR EVERY CONDITION
  WHEN GENDER-CD = BLANK
    COMPRESS #TEXT (2) #NOSEX INTO #TEXT (2)
  WHEN GENDER-CD NE BLANK
    COMPRESS #TEXT (2) #SEX GENDER-CD INTO #TEXT (2)
  WHEN SSN = 0
    COMPRESS #TEXT (2) #NOSSN INTO #TEXT (2)
  WHEN SSN NE 0
    MOVE EDITED SSN (EM=9(9)) TO #SSN-A
    COMPRESS #SSN-3 #SSN-2 #SSN-4 INTO #SSN-F WITH DELIMITER '-'
    COMPRESS #SSN #SSN-F INTO #SSN-F
    COMPRESS #TEXT (2) #SSN-F INTO #TEXT (2)
  WHEN DATE-OF-BIRTH = 0
    COMPRESS #TEXT (2) #NODOB INTO #TEXT (2)
  WHEN DATE-OF-BIRTH NE 0
    COMPRESS DOB-MONTH DOB-DAY DOB-YEAR INTO #SSN-A WITH DELIMITER '/'
    COMPRESS #TEXT (2) #DOB #SSN-A INTO #TEXT (2)
  WHEN DATE-OF-DEATH = 0
    COMPRESS #TEXT (2) #NODOD INTO #TEXT (2)
  WHEN DATE-OF-DEATH NE 0
    COMPRESS DOD-MONTH DOD-DAY DOD-YEAR INTO #SSN-A WITH DELIMITER '/'
    COMPRESS #TEXT (2) #DOD #SSN-A INTO #TEXT (2)
  WHEN NONE
    IGNORE
END-DECIDE

ON ERROR
  COMPRESS 'Application error' *ERROR-NR *ERROR-LINE *PROGRAM
    INTO #TEXT  (1)
  ESCAPE ROUTINE
END-ERROR

END
