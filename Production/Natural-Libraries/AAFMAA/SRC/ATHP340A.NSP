* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP340A   ** APPL TRACKING DOWNLOAD
*
*                       MODIFICATIONS
* USER DATE       TAG  REASON
* JNT  08/27/2013 JT1  Address normalization 
*
************************************************************************ 
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL USING ADDA0001           /* JT1
LOCAL
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER       (N6)
 2 NAME            (A25)
 2 RANK            (A3)
 2 DATE-RECEIVED   (N8)
 2 ACTION-FLAG     (A1)
 2 CONTACT-ID      (N8)            /* JT1
*  2 ADDRESS-1       (A25)         /* JT1 
*  2 ADDRESS-2       (A25)         /* JT1
*  2 ADDRESS-3       (A25)         /* JT1
*  2 ZIP-CODE        (N9)          /* JT1
 2 MIL-STATUS      (A1)
 2 MIL-SERVICE     (A3)
*
1 #DELL      (A1)
1 #LAST-NAME    (A25)
1 #FIRST/MID    (A25)
1 #SUFFIX       (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
1 #RET         (A3)
*
1 #FORMATED-NAME   (A45)
1 #HOLD-FIRST-NAME (A45)
1 #HOLD-INTER-NAME (A45)
1 #HOLD-ZIP-CODE   (N9)
1 #HOLD-EDIT-ZIP   (A10)
1 REDEFINE #HOLD-EDIT-ZIP
  2 #HOLD-ZIP      (A5)
  2 #HOLD-ZIP2     (A5)
*  
1 #HOLD-NAME       (A45)
1 #HOLD-RANK       (A4)
1 #FIRST-ADDRESS   (A25)
1 #SECOND-ADDRESS  (A40)
1 #THIRD-ADDRESS   (A40)
1 #HOLD-ADDRESS-2  (A40)
1 #HOLD-ADDRESS-3  (A40)
*
1 #HOLD-ID-N      (N6)
1 REDEFINE #HOLD-ID-N
 2 #HOLD-ID       (A6)
*
1 #ERROR-MESSAGE  (A25)
1 #NUM-LTRS       (P5)
*
1 #FE-EXTRACT       (A50)
1 #FE-NAME          (A50)
1 #FE-FIRST-ADDR    (A40)
1 #FE-SECOND-ADDR   (A40)
1 #FE-THIRD-ADDR    (A40)
1 #FE-LAST-LINE     (A40)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* READ AT-V BY ID-NUMBER FROM 511570
READ AT-V BY ID-NUMBER
* IF ID-NUMBER GT 512139
*   ESCAPE BOTTOM
* END-IF
*
  IF   (ACTION-FLAG NE 'I')
   OR (DATE-RECEIVED LT 19931001)
   OR (DATE-RECEIVED GT 19940630)
    ESCAPE TOP
  END-IF
*
  SEPARATE NAME INTO #LAST-NAME #FIRST/MID
    #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER ','
  COMPRESS RANK #FIRST/MID INTO #HOLD-FIRST-NAME LEAVING NO
*
  IF #SUFFIX = '   '
    COMPRESS #HOLD-FIRST-NAME #LAST-NAME INTO #HOLD-NAME
  ELSE
    COMPRESS #HOLD-FIRST-NAME #LAST-NAME INTO #HOLD-INTER-NAME
    COMPRESS #HOLD-INTER-NAME ',' #SUFFIX INTO #HOLD-NAME LEAVING NO
  END-IF
*
  IF MIL-STATUS = 'R'
    MOVE 'RET' TO #RET
    MOVE ','   TO #DELL
  ELSE
    RESET #RET
    MOVE ' ' TO #DELL
  END-IF
*
  COMPRESS #HOLD-NAME #DELL INTO #HOLD-INTER-NAME LEAVING NO SPACE
  COMPRESS #HOLD-INTER-NAME #RET INTO #FORMATED-NAME
*
  MOVE ID-NUMBER TO #HOLD-ID-N
*
*  IF ADDRESS-1 = ' '                          /* JT1
*    ESCAPE TOP                                /* JT1
*  ELSE                                        /* JT1
*    MOVE ADDRESS-1 TO #FIRST-ADDRESS          /* JT1
*    MOVE ADDRESS-2 TO #HOLD-ADDRESS-2         /* JT1
*    MOVE ADDRESS-3 TO #HOLD-ADDRESS-3         /* JT1
*    MOVE ZIP-CODE  TO #HOLD-ZIP-CODE          /* JT1
*  END-IF                                      /* JT1
*
  RESET #PDA-ADDRESS                           /* JT1
  #ADD-TYPE        := 'P'                      /* JT1
  #ADD-CONTACT-ID := AT-V.CONTACT-ID           /* JT1
  CALLNAT 'ADDN0001' #PDA-ADDRESS              /* JT1
*    
  IF #ADD-LINE-1 = ' '                         /* JT1
    ESCAPE TOP                                 /* JT1
  ELSE                                         /* JT1
    MOVE #ADD-LINE-1 TO #FIRST-ADDRESS         /* JT1
    MOVE #ADD-LINE-2 TO #HOLD-ADDRESS-2        /* JT1
    MOVE #ADD-LINE-3 TO #HOLD-ADDRESS-3        /* JT1
    MOVE #ADD-ZIP-CODE  TO #HOLD-ZIP-CODE      /* JT1
  END-IF                                       /* JT1
*
  MOVE EDITED #HOLD-ZIP-CODE (EM=99999-9999) TO #HOLD-EDIT-ZIP
*
  IF #HOLD-ZIP2 = '-0000'
    MOVE '     ' TO #HOLD-ZIP2
  END-IF
*
  IF #HOLD-ADDRESS-3 = ' '
    COMPRESS #HOLD-ADDRESS-2 #HOLD-EDIT-ZIP INTO #SECOND-ADDRESS
    RESET #THIRD-ADDRESS
  ELSE
    MOVE #HOLD-ADDRESS-2 TO #SECOND-ADDRESS
    COMPRESS #HOLD-ADDRESS-3 #HOLD-EDIT-ZIP INTO #THIRD-ADDRESS
  END-IF
END-ALL
*
SORT BY AT-V.NAME
  USING #HOLD-ID
        #FORMATED-NAME
        #FIRST-ADDRESS
        #SECOND-ADDRESS
        #THIRD-ADDRESS
*
  COMPRESS 'AT' #HOLD-ID '$$' INTO #FE-EXTRACT LEAVING NO
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #FORMATED-NAME TO #FE-EXTRACT
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #FIRST-ADDRESS TO #FE-EXTRACT
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #SECOND-ADDRESS TO #FE-EXTRACT
*
  IF #THIRD-ADDRESS = ' '
    COMPRESS #SECOND-ADDRESS '@@' INTO #FE-EXTRACT LEAVING NO
  END-IF
*
  WRITE WORK FILE 1 #FE-EXTRACT
*
  IF #THIRD-ADDRESS NE ' '
    COMPRESS #THIRD-ADDRESS '@@' INTO #FE-EXTRACT LEAVING NO
    WRITE WORK FILE 1 #FE-EXTRACT
  END-IF
*
* COMPRESS '@@' #LAST-NAME '##' INTO #FE-EXTRACT LEAVING NO
* WRITE WORK FILE 1 #FE-EXTRACT
END-SORT
*
END
