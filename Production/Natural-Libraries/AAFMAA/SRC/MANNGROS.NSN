* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram MANNGROS.
/**
/** :author nguentchev
/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Gross Amount: Sum of all Monthly Payment in the calendar year + Initial Payment + 13th Check
/*  Taxable Amount: Sum of all Taxable Amount in the calendar year + Initial Payment + 13th Check
/*  Net Amount:    Gross Amount - Taxable Amount
/* Long Term Care Settlement
/*  Gross Amount: Sum of (each Monthly Payment - 20) in the calendar year
/*  Taxable Amount: Should be blank; it is determined by the IRS
/*  Net Amount: Can be left blank
/* Total paid: Gross Amount
/*
DEFINE DATA
PARAMETER
1 #ISN           (P8)
1 #TOTAL-PAID    (N7.2)
1 #GROSS         (N7.2)
1 #GROSS-TAXABLE (N7.2)
LOCAL
1 #LONG-TERM   (A2) CONST <'LT'>
1 #VALID-TYPES (A2/7) CONST <'A1','A2','LA','LT','L1','L2','L3'>
1 #VALID-STATUS (A1/4) CONST <'A','C','D','P'>
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION (N7.2)
  2 PRINCIPAL (N7.2)
  2 ANNUAL-PAYMENT (N7.2)
  2 RESERVE-AMOUNT (N7.2)
  2 WITHDRAWALS (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0)
  2 REDEFINE START-DATE
    3 YYYY (N4)
    3 MM (N2)
    3 DD (A2)
  2 NOTES (A70/1:8)
  2 ANNUITANT-NAME (A60)
  2 ANNUITANT-DOB (N8.0)
  2 PAID-UP-DATE (N8.0)
  2 REDEFINE PAID-UP-DATE
    3 YYYY-1 (A4)
    3 MM-1 (A2)
    3 DD-1 (A2)
  2 INITIAL-PAYMENT (N7.2)
  2 DEPOSIT (N7.2)
  2 T13TH-CHECK (N7.2)
  2 TAXABLE (N7.2)
1 #JDate              (A5) 1 REDEFINE #JDATE
  2 YY       (A2)
  2 #DDD     (N3)
1 #TODAY       (N8)
1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
1 K  (I4)
END-DEFINE
/*
/* NO history data are considered
/*
GET ANNUITANTS #ISN
IF *ISN (0071) <= 0
  ESCAPE ROUTINE
END-IF
IF ANNUITANT-STATUS NE 'A'
  ESCAPE ROUTINE
END-IF
MOVE *DATN TO #TODAY
IF ANNUITANTS.START-DATE > 0
  IF #YYYY = ANNUITANTS.YYYY
    K := #MM - ANNUITANTS.MM
  ELSE
    K := #MM
  END-IF
ELSE
  K := #MM - 1
END-IF
IF SETTLEMENT-OPTION = #LONG-TERM
  RESET #GROSS-TAXABLE
  #GROSS := ANNUITANTS.MONTHLY-PENSION * K
ELSE
  #GROSS := ANNUITANTS.MONTHLY-PENSION * K
  #GROSS := #GROSS + ANNUITANTS.INITIAL-PAYMENT + T13TH-CHECK
  #GROSS-TAXABLE := TAXABLE * K
  #GROSS-TAXABLE := #GROSS-TAXABLE + INITIAL-PAYMENT + T13TH-CHECK
END-IF
#TOTAL-PAID := #GROSS
END
