* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
PARAMETER
1 #ENV-MAN-RECORD (A80)
1 #INPUT
  2 #ID-NUMBER    (A6) 2 REDEFINE #ID-NUMBER
    3 #ID-NUMBER-N  (N6)
  2 #STATE-CODE   (A2)
  2 #ZIP-CODE     (A10)
  2 #ERRORS       (L)
  2 #CITY         (A) DYNAMIC
  2 #STREET-LINE  (A) DYNAMIC
  2 #UNIT-LINE    (A) DYNAMIC
  2 #ADDR-TYPE    (A1)
LOCAL
1 #UNIT-KW  (A4/22) INIT <
  'APT'   ,        /* APARTMENT
  'BSMT'  ,        /* BASEMENT
  'BLDG'  ,        /* BUILDING
  'DEPT'  ,        /* DEPARTMENT
  'FL'    ,        /* FLOOR
  'FRNT'  ,        /* FRONT
  'HNGR'  ,        /* HANGAR
  'KEY'   ,        /* KEY
  'LBBY'  ,        /* LOBBY
  'LOT'   ,        /* LOT
  'OFC'   ,        /* OFFICE
  'PH'    ,        /* PENTHOUSE
  'PMB'   ,        /* PERSONAL MAIL BOX
  'RM'    ,        /* ROOM
  'SLIP'  ,        /* SLIP
  'SPC'   ,        /* SPACE
  'STOP'  ,        /* STOP
  'STE'   ,        /* SUITE
  'TRLR'  ,        /* TRAILER
  'UNIT'  ,        /* UNIT
  'UPPR'  ,        /* UPPER
  '#'              /* NUMBER
  >
1 #ADDR-TOKENS    (A/25) DYNAMIC
1 #TOKENS         (A) DYNAMIC
1 #UNIT           (L)
1 #J              (I4)
1 #L              (I4)
1 #UNKNOWN        (A) DYNAMIC
1 #STREET-UNIT    (A) DYNAMIC
1 #CN-AT          (A) DYNAMIC
1 BLANK           (A1) CONST  <' '>
1 #TAB            (A1) CONST <H'09'>
1 #SPECIAL-CITIES (A3/3) CONST <'APO','DPO','FPO'>
END-DEFINE

RESET #UNIT
  #INPUT
EXAMINE #ENV-MAN-RECORD TRANSLATE INTO UPPER CASE
SEPARATE #ENV-MAN-RECORD INTO
  #CN-AT
  #STREET-UNIT
  #UNKNOWN
  #CITY
  #STATE-CODE
  #ZIP-CODE
IGNORE WITH DELIMITERS #TAB
SEPARATE #CN-AT INTO #ID-NUMBER #ADDR-TYPE WITH DELIMITERS '-'
MOVE RIGHT #ID-NUMBER TO #ID-NUMBER
IF #CITY = #SPECIAL-CITIES (*)
  COMPRESS #STREET-UNIT #UNKNOWN INTO #STREET-LINE
ELSE
  IF #UNKNOWN NE BLANK
    MOVE #UNKNOWN TO #UNIT-LINE
    MOVE #STREET-UNIT TO #STREET-LINE
  ELSE
    EXAMINE #STREET-UNIT FOR '#' POSITION #J
    IF #J > 1
      #L := *LENGTH ( #STREET-UNIT )
      SUBTRACT 1 FROM #J
      MOVE SUBSTR ( #STREET-UNIT,1,#J) TO #STREET-LINE
      #L := #L - #J
      ADD 1 TO #J
      MOVE SUBSTR ( #STREET-UNIT,#J,#L) TO #UNIT-LINE
      ESCAPE ROUTINE
    END-IF
    SEPARATE #STREET-UNIT INTO #ADDR-TOKENS (*) WITH DELIMITERS BLANK
    FOR #J 1 TO 25
      IF #ADDR-TOKENS ( #J ) = BLANK
        ESCAPE BOTTOM
      END-IF
      IF #ADDR-TOKENS ( #J ) = #UNIT-KW (*)
        #UNIT := TRUE
      END-IF
      IF #UNIT
        COMPRESS #UNIT-LINE #ADDR-TOKENS ( #J ) INTO #UNIT-LINE
      ELSE
        COMPRESS #STREET-LINE #ADDR-TOKENS ( #J ) INTO #STREET-LINE
      END-IF
    END-FOR
  END-IF
END-IF

ON ERROR
  #ERRORS := TRUE
  PRINT 'Address parsing error' #ENV-MAN-RECORD
  ESCAPE ROUTINE
END-ERROR

END
