* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: E4751SBD      ** beneficiary transfer form processing
DEFINE DATA
* * * * * * * * * * * * *
* M O D I F I C A T I O N   L O G 
* * * * * * * * * * * * * *
* DRW 01/27/2009 DW1 Add new-bene-processing indicator to linkage 
* bz  06/09/2013  Rebranding (Search for AAFMAA)
*
*
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-CLERK       (A3)
1 #CGA-SECURITY-CODE (N2)
*
1 #ISS-DT-OCC     (9)    /* from status file
 2 #ISS-DT         (N6)
 2 #ISS-DT-DISP    (A10)
 2 #SPLIT-IND      (N1)
 2 #STATUS         (A1)
 2 #POL-NUM        (A15)
 2 #POL-BENE-FOUND (L)
 2 #POL-BENE-SEL   (A1)
*
1 #SCR-PRIM-SEC   (2)    /* (prim/alt)
 2 #SCR-DESIG    (A60/5)
 2 #SCR-TYPE     (A1)
 2 #SCR-SHARE-TYP (A1)
*
1 #DES-SEL         (I2)
1 #SCR-NAME       (A25)
1 #SCR-OWNER      (A35)
1 REDEFINE #SCR-OWNER
 2 #SCR-OWNER-1A  (A1)
1 REDEFINE #SCR-OWNER
 2 #SCR-OWNER-6A  (A6)
 2 REDEFINE #SCR-OWNER-6A
  3 #SCR-OWNER-6N (N6)
 2 #SCR-OWNER-AST (A1) /* TO SHOW OWNER IS IN DESIG
1 REDEFINE #SCR-OWNER
 2 #SCR-OWNER-OCC (5)
  3 #SCR-OWNER-6A-XA (A6)
  3 REDEFINE #SCR-OWNER-6A-XA
   4 #SCR-OWNER-6A-XN (N6)
  3 #SCR-OWNER-SEP    (A1)
*
1 #SCR-OWN-TRNSF-DT-DISP (A10)
1 #DESIG-CNT      (P2)
1 #LST-UPD        (N8)
1 REDEFINE #LST-UPD
 2 #LST-UPD-YYYY  (N4)
 2 #LST-UPD-MM    (N2)
 2 #LST-UPD-DD    (N2)
1 #SCR-BENE-RESTRICT (A1)
1 #SCR-BENE-STATUS   (A12)
1 #TEXT              (A75)
1 #CGA-MF-SCR-COMMAND (A2)
1 REDEFINE #CGA-MF-SCR-COMMAND
 2 #TRNSF-IND    (N1)
 2 #SUCC-FORM    (N1)
1 #ZIP-CODE-PASSED (N9)
1 #INTRO-FLAG-PASSED (A1)
1 #LINKED-CN     (N6)
1 REDEFINE #LINKED-CN
 2 #LINKED-CN-A  (A6)
1 #SD-SUFF       (A15)
1 #CONT-ENT      (A25/5)
1 #EDS-DATA-VAL  (A60)
1 #NEW-LAYOUT    (L)
1 #SCR-COM-DIS   (N2)
1 #SCR-PER-STR   (A1)
*
* View below was moved from LOCAL to PARAMETERS        /* DW1
1 NT-V                /* YK1 VIEW OF A-NOTES   /* rec 800117 used to send info to ATHPANNP
 2 ID-NUMBER  (N6)
 2 NOTE-DATE  (N8/60)
 2 NOTE-TEXT  (A60/60)
 2 REDEFINE NOTE-TEXT /* below layout must match ATHPANNS
  3 NT-CN     (N6)
  3 NT-ISS-DT (N8)
  3 NT-PLAN   (A3)
  3 NT-TYPE   (A1)     /* bene-type of primary bene
  3 NT-NEW-LAYOUT (A1) /* new layout indicator of primar
  3 NT-NUM-LINES (N3)  /* # of lines entered below
  3 NT-BENE-SHO  (A79/40)   /* 79 to handle raw bene indicator in pos 1
* 
1 #NEW-BENE-PROCESSING  (L)    /*      DW1
*  
LOCAL USING G3500AAD
LOCAL
*
1 #CGA-MASTER
 2 #CGA-SCR-COMMAND-X              (A2)
 2 #CGA-SCR-ID-X                   (N9)
 2 #CGA-SCR-SUFFIX-X               (A3)
 2 #CGA-ORIG-COMMAND-X             (A2)
 2 #CGA-ORIG-ID-X                  (N6)
 2 #CGA-ORIG-SUFFIX-X              (A3)
 2 #CGA-CLERK-X                    (A3)
 2 #CGA-SECURITY-CODE-X            (N2)
 2 #CGA-USER-COMPANY               (A1)
 2 #CGA-ID-G                       (A6)
 2 #CGA-ISN                        (P8)
 2 #CGA-GOOD-TAS                   (L)
 2 #CGA-GOOD-TAS-VS                (L)
 2 #CGA-MF-SCR-COMMAND-X           (A2)
 2 #CGA-MF-SCR-PARM-1              (A9)
 2 #CGA-MF-SCR-PARM-2              (A5)
 2 #CGA-MF-ORIG-COMMAND            (A2)
 2 #CGA-MF-ORIG-PARM-1             (A9)
 2 #CGA-MF-ORIG-PARM-2             (A5)
 2 #cga-policy-number              (a16) /* ticket 4391
*
1 ST-V VIEW OF A-STATUS
 2 NAME
*
1 #TR-NAME    (A25)
1 #LAST       (A25)
1 #FIRST-MID  (A25)
1 #SUFF1      (A25)
1 #SUFF2      (A25)
1 #SUFF3      (A25)
*
1 #SCR-LINES-CV   (C/57)
1 #SCR-LINES      (A78/57)
1 REDEFINE #SCR-LINES
 2 #SCR-LINES-OCC     (57)
  3 #SCR-LINES-FIL    (A5)
  3 #SCR-LINES-INDENT (A73)
1 #SCR-POLS       (A60)
1 #SCR-POLS-2     (A30)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY       (N4)
 2 #MM         (N2)
 2 #DD         (N2)
*
1 #TODAY-MMDDYYYY-DISP  (A10)
*
1 #MONTHS (A36)  INIT
   <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
 2 #MON       (A3/12)
*
1 #POL        (A14)
1 #CNT        (P2)
1 #SCR-X      (I2)
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #I4         (I2)
1 #HOLD-ID     (N9)
1 #HOLD-SUFFIX (A3)
1 #MANUAL      (L)
1 #TRUSTEEZ   (A8)
1 #OWNERZ     (A9)
1 #POSS       (A5)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
MOVE #CGA-SCR-ID TO #CGA-SCR-ID-X
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM #DD #YYYY INTO #TODAY-MMDDYYYY-DISP WITH '/'
*
FOR #I1 1 TO 9
  IF #POL-BENE-SEL (#I1) = 'X'
    ADD 1 TO #CNT
  END-IF
END-FOR
*
RESET #SCR-POLS-2
FOR #I1 1 TO 9
  IF #POL-BENE-SEL (#I1) = 'X'
    ADD 1 TO #I2
*
    IF #I2 NE 1
      IF #I2 GE 6
        IF #CNT = #I2
          COMPRESS #SCR-POLS-2 '&' INTO #SCR-POLS-2
        ELSE
          COMPRESS #SCR-POLS-2 ',' INTO #SCR-POLS-2 LEAVING NO
        END-IF
      ELSE
        IF #CNT = #I2
          COMPRESS #SCR-POLS '&' INTO #SCR-POLS
        ELSE
          COMPRESS #SCR-POLS ',' INTO #SCR-POLS LEAVING NO
        END-IF
      END-IF
    END-IF
*
    MOVE LEFT #POL-NUM (#I1) TO #POL
*
    IF #I2 GE 5
      COMPRESS #SCR-POLS-2 #POL INTO #SCR-POLS-2
    ELSE
      COMPRESS #SCR-POLS #POL INTO #SCR-POLS
    END-IF
  END-IF
END-FOR
*
MOVE #SCR-NAME TO #TR-NAME
IF #INTRO-FLAG-PASSED = 'S' OR = 'K' OR ='G'
  FIND (1) ST-V WITH MEMBER-KEY = #LINKED-CN-A
    SEPARATE ST-V.NAME LEFT INTO #LAST #FIRST-MID #SUFF1 #SUFF2 #SUFF3
                                     WITH DELIMITER ','
    COMPRESS #FIRST-MID #LAST #SUFF1 #SUFF2 #SUFF3 INTO #TR-NAME
  END-FIND
END-IF
*
ADD 1 TO #SCR-X
MOVE '                     TRANSFER OF OWNERSHIP' TO #SCR-LINES (#SCR-X)
ADD 2 TO #SCR-X
COMPRESS '  This transfer of ownership made the ______ day of _________'
         '20___ by' INTO #SCR-LINES (#SCR-X)
*
ADD 1 TO #SCR-X
COMPRESS #TR-NAME 'to' INTO #SCR-LINES (#SCR-X)
*
DECIDE ON FIRST #TRNSF-IND
  VALUE 1 MOVE 'his'   TO #POSS
  VALUE 2 MOVE 'her'   TO #POSS
  VALUE 3 MOVE 'their' TO #POSS
  VALUE 4 MOVE 'its'   TO #POSS
  NONE IGNORE
END-DECIDE
*
IF  (NOT #SCR-TYPE (1) = 'K' OR = 'P' OR = 'X')
 OR (#SCR-SHARE-TYP (1) = 'S')    /* single
  MOVE 'trustee' TO #TRUSTEEZ
  MOVE 'owner(s),' TO #OWNERZ
ELSE
  MOVE 'trustees' TO #TRUSTEEZ
  MOVE 'owners,' TO #OWNERZ
END-IF
*
ADD 1 TO #SCR-X
IF  (#SCR-OWNER = '*')
 OR (#SCR-OWNER-AST) = '*'
* IF #SCR-TYPE (1) = 'K' OR = 'P'
*   MOVE #SCR-DESIG  (1, 3) TO #SCR-LINES-INDENT (#SCR-X)
*   IF #SCR-DESIG (1, 4) NE ' '
*     ADD 1 TO #SCR-X
*     MOVE #SCR-DESIG (1, 4) TO #SCR-LINES-INDENT (#SCR-X)
*   END-IF
* ELSE
    MOVE #SCR-DESIG  (1, 1) TO #SCR-LINES-INDENT (#SCR-X)
    IF #SCR-DESIG (1, 2) NE ' '
      ADD 1 TO #SCR-X
      MOVE #SCR-DESIG (1, 2) TO #SCR-LINES-INDENT (#SCR-X)
    END-IF
* END-IF
*
 IF   (#CGA-SCR-SUFFIX = 'BNT')
  AND (#SCR-DESIG (1, 2) NE ' ')
    ADD 1 TO #SCR-X
    MOVE #SCR-DESIG (1, 2) TO #SCR-LINES-INDENT (#SCR-X)
  END-IF
ELSE
  IF #SCR-OWNER = 'X' /* never after 7/25/2002
    COMPRESS #SCR-NAME #SD-SUFF
     INTO #SCR-LINES-INDENT (#SCR-X) LEAVING NO
  ELSE
    IF #SCR-OWNER-6A = MASK (999999)
      MOVE #CONT-ENT (1) TO #SCR-LINES-INDENT (#SCR-X)
*
      FOR #I4 2 TO 5
        IF #SCR-OWNER-6A-XA (#I4) = MASK (999999)
          ADD 1 TO #SCR-X
          MOVE #CONT-ENT (#I4) TO #SCR-LINES-INDENT (#SCR-X)
        END-IF
      END-FOR
    ELSE
      IF #SCR-OWNER = ' '
        MOVE #SCR-NAME TO #SCR-LINES-INDENT (#SCR-X)
      ELSE
        MOVE #SCR-OWNER TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
    END-IF
  END-IF
END-IF
*
IF #CGA-SCR-SUFFIX = 'BNT'
  ADD 1 TO #SCR-X
  COMPRESS 'or' #POSS 'successor, as' #TRUSTEEZ 'of the'
                                    INTO #SCR-LINES(#SCR-X)
  ADD 1 TO #SCR-X
  MOVE #SCR-DESIG (1,3) TO #SCR-LINES-INDENT (#SCR-X)
*
  IF #SCR-DESIG (1, 4) NE ' '
    ADD 1 TO #SCR-X
    MOVE #SCR-DESIG (1,4) TO #SCR-LINES-INDENT (#SCR-X)
  END-IF
*
  IF #SCR-DESIG (1, 5) NE ' '
    ADD 1 TO #SCR-X
    MOVE #SCR-DESIG (1,5) TO #SCR-LINES-INDENT (#SCR-X)
  END-IF
*
  COMPRESS #SCR-LINES (#SCR-X) '.'
                         INTO #SCR-LINES (#SCR-X) LEAVING NO
ELSE
  COMPRESS #SCR-LINES (#SCR-X) ', witnesseth:'
                         INTO #SCR-LINES (#SCR-X) LEAVING NO
END-IF
*
ADD 2 TO #SCR-X
IF #CGA-SCR-SUFFIX = 'BNL'
  COMPRESS #TR-NAME 'in consideration of natural love and affection,'
                                     INTO #SCR-LINES (#SCR-X)
  ADD 1 TO #SCR-X
  COMPRESS 'does hereby give, grant, and convey to said new' #OWNERZ
           #POSS 'heirs'       INTO #SCR-LINES (#SCR-X)
  ADD 1 TO #SCR-X
  COMPRESS 'and assigns all rights, title and interest in the'
           'life insurance'    INTO #SCR-LINES (#SCR-X) LEAVING NO
  MOVE 'represented by' TO #SCR-LINES (#SCR-X)
ELSE
  IF #CGA-SCR-SUFFIX = 'BNN'
    COMPRESS #TR-NAME 'does hereby give, grant, and convey to said'
           'new'                           INTO #SCR-LINES (#SCR-X)
    ADD 1 TO #SCR-X
    COMPRESS #OWNERZ #POSS 'heirs and assigns all rights,'
             'title and interest in the life' INTO #SCR-LINES (#SCR-X)
    ADD 1 TO #SCR-X
    MOVE 'insurance represented by'
                                   TO #SCR-LINES (#SCR-X)
  ELSE
    COMPRESS #TR-NAME 'does hereby give, grant, and convey to said'
           #TRUSTEEZ                       INTO #SCR-LINES (#SCR-X)
    ADD 1 TO #SCR-X
    COMPRESS 'all rights, title and interest in the life insurance'
             'represented by'                INTO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
ADD 1 TO #SCR-X
MOVE     'Certificate' TO #SCR-LINES-INDENT (#SCR-X)
*
IF #CNT GT 1
  COMPRESS #SCR-LINES (#SCR-X) '(s)' INTO #SCR-LINES (#SCR-X) LEAVING NO
END-IF
*
COMPRESS #SCR-LINES (#SCR-X) 'No.' #SCR-POLS INTO #SCR-LINES (#SCR-X)
*
IF #CNT GE 5
  ADD 1 TO #SCR-X
  MOVE #SCR-POLS-2 TO #SCR-LINES-INDENT (#SCR-X)
END-IF
*
ADD 1 TO #SCR-X
COMPRESS 'issued by the AAFMAA,'
         'annexed hereto.'   INTO #SCR-LINES (#SCR-X)
*
ADD 2 TO #SCR-X
COMPRESS #TR-NAME 'hereby agrees to execute and deliver any'
                             INTO #SCR-LINES (#SCR-X)
*
ADD 1 TO #SCR-X
COMPRESS 'further documents necessary to vest title in said insurance'
         'to said new'  INTO #SCR-LINES (#SCR-X)
*
ADD 1 TO #SCR-X
MOVE 'owner(s).' TO #SCR-LINES (#SCR-X)
*
ADD 3 TO #SCR-X
COMPRESS '  In witness thereof, I have set my hand and seal this'
         '________ day of'   INTO #SCR-LINES (#SCR-X)
*
ADD 2 TO #SCR-X
MOVE     '____________ 20___' TO #SCR-LINES (#SCR-X)
*
ADD 4 TO #SCR-X
COMPRESS ' Date ______________ __________________________________'
         '_______________' INTO #SCR-LINES (#SCR-X) LEAVING NO
ADD 1 TO #SCR-X
MOVE     '                     Signature of Owner'
      TO #SCR-LINES (#SCR-X)
*
ADD 3 TO #SCR-X
MOVE 'Approved by authority of the Board of Directors'
                              TO #SCR-LINES (#SCR-X)
*
ADD 2 TO #SCR-X
COMPRESS '                       Secretary _____________________'
         '_________________' INTO #SCR-LINES (#SCR-X) LEAVING NO
*
MOVE #CGA-SCR-ID     TO #HOLD-ID
MOVE #CGA-SCR-SUFFIX TO #HOLD-SUFFIX
RESET #CGA-SCR-ID #CGA-SCR-SUFFIX
*
IF #ZIP-CODE-PASSED = 0
  NEWPAGE (6)   /* was 7
  WRITE (6) NOTITLE USING FORM 'E4750FBD' /* plain white
ELSE
  NEWPAGE (4)   /* was 5
  WRITE (4) NOTITLE USING FORM 'E4750FBD' /* plain white
END-IF
*
MOVE #HOLD-ID     TO #CGA-SCR-ID
MOVE #HOLD-SUFFIX TO #CGA-SCR-SUFFIX
*
RESET  #SCR-X #SCR-LINES (*)
IF #SUCC-FORM = 1
  ADD 1 TO #SCR-X
* * * *  below is also in aoap005c
  MOVE '                  DESIGNATION OF SUCCESSOR OWNERSHIP'
               TO #SCR-LINES (#SCR-X)
  ADD 2 TO #SCR-X
  COMPRESS '  AAFMAA is requested'
           'to make the' INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  MOVE 'following change of ownership in the event of my death.'
                                     TO #SCR-LINES (#SCR-X)
  ADD 2 TO #SCR-X
  COMPRESS '  In accordance with the provisions of Certificate'
                                    INTO #SCR-LINES(#SCR-X)
  IF #CNT GT 1
    COMPRESS #SCR-LINES (#SCR-X) '(s)'
                                     INTO #SCR-LINES (#SCR-X) LEAVING NO
*
    ADD 1 TO #SCR-X
    COMPRESS 'No.' #SCR-POLS INTO #SCR-LINES (#SCR-X)
  ELSE
    COMPRESS #SCR-LINES(#SCR-X) 'No.' #SCR-POLS INTO #SCR-LINES (#SCR-X)
  END-IF
*
  ADD 1 TO #SCR-X
  COMPRESS 'on the life of' #SCR-NAME INTO #SCR-LINES (#SCR-X)
  COMPRESS #SCR-LINES (#SCR-X) ', the insured, I designate'
                                 INTO #SCR-LINES (#SCR-X) LEAVING NO
*
  ADD 2 TO #SCR-X
  COMPRESS '________________________, ______________________________'
           'as Successor Owner.' INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  COMPRESS '     Name                       Relationship to owner'
           TO #SCR-LINES (#SCR-X)
*
  ADD 4 TO #SCR-X
  COMPRESS ' Date ______________ __________________________________'
           '_______________' INTO #SCR-LINES (#SCR-X) LEAVING NO
  ADD 1 TO #SCR-X
  MOVE     '                     Signature of Owner'
        TO #SCR-LINES (#SCR-X)
*
  ADD 3 TO #SCR-X
  MOVE 'Approved by authority of the Board of Directors'
                                TO #SCR-LINES (#SCR-X)
*
  ADD 3 TO #SCR-X
  COMPRESS '                       Secretary _____________________'
           '_________________' INTO #SCR-LINES (#SCR-X) LEAVING NO
*
  MOVE #CGA-SCR-ID     TO #HOLD-ID
  MOVE #CGA-SCR-SUFFIX TO #HOLD-SUFFIX
  RESET #CGA-SCR-ID #CGA-SCR-SUFFIX
*
  IF #ZIP-CODE-PASSED = 0
    NEWPAGE (6)
    WRITE (6) NOTITLE USING FORM 'E4750FBD' /* plain white
  ELSE
    NEWPAGE (4)
    WRITE (4) NOTITLE USING FORM 'E4750FBD' /* plain white
  END-IF
*
  MOVE #HOLD-ID     TO #CGA-SCR-ID
  MOVE #HOLD-SUFFIX TO #CGA-SCR-SUFFIX
END-IF
*
END
