* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram LWSNPRUI.
/**
/** :author nguentchev
DEFINE DATA
/*PARAMETER USING LWSAPR
PARAMETER
1 POR
  2 beneficiaryrestricted    (A5)
  2 rolename                 (A33)
  2 designationid            (A8) 2 REDEFINE designationid
    3 designationid-N (N8)
  2 settlementtype           (A33)
  2 beneficiarypercent       (A10)
  2 policynumber             (A16)
  2 contactid                (A8) 2 REDEFINE contactid
    3 contactid-n    (N8)
  2 contactGUID                   (A50)
  2 policyGUID                    (A50)
  2 beneficiarydesignationGUID    (A50)
PARAMETER 
1 ErrorMsg   (A) DYNAMIC
LOCAL USING LWSLRQST  /* REQUEST codes
LOCAL
1 BLANK (A1) CONST <' '>
1 DS-V VIEW OF A-DESIGNATION
  2 DESIGNATION-GUID        (A50)
1 CNT-V VIEW OF A-CONTACTS
  2 CRM-CONTACT-GUI         (A50)
1 PC-V VIEW OF A-POLICY-CONTACTS
  2 POLICY-GUID             (A50)
1 #ID-NUMBER                (N6)
1 #DESIGNATION-ID           (N8)
END-DEFINE
/* 
IF designationid IS (N8)
    AND designationid-N > 0
  MOVE RIGHT designationid TO designationid
  FIND DS-V WITH DESIGNATION-ID = designationid-N
    beneficiarydesignationGUID := DESIGNATION-GUID
  END-FIND
END-IF
MOVE RIGHT POR.contactid TO POR.contactid
FIND (1) CNT-V WITH CONTACT-ID = POR.contactid-n
  contactGUID := CNT-V.CRM-CONTACT-GUI

  if contactGUID = BLANK
    CALLNAT 'LWSNSRV' CONTACT-GET-REQ
      POR.contactid-n
      policynumber
      #ID-NUMBER
      #DESIGNATION-ID
      ErrorMsg
    IF SUBSTR(ErrorMsg,1,4) EQ 'GUID' /* CLEAN UP
        AND *LENGTH(ErrorMsg) GT 36
      contactGUID := SUBSTR(ErrorMsg,6,36)
    END-IF
  end-if

END-FIND
FIND PC-V WITH POLICY-NUMBER = policynumber
  IF PC-V.POLICY-GUID NE BLANK
    policyGUID := PC-V.POLICY-GUID
    ESCAPE BOTTOM
  END-IF
END-FIND
END
