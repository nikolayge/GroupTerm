* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram MSSNL004.
/**
/** :author nguentchev
/* TODO Enter your code here
DEFINE DATA
PARAMETER
1 PRODUCT
  2 PRODUCT_ID    (A16)
  2 FACE_AMOUNT   (N7.2)
  2 DEATH_BENEFIT (N7.2)
  2 PRODUCT_CODE  (A3)
  2 ID-NUMBER     (N6.0)
  2 MEMBER_TYPE   (A1)
  2 STATUS_CODE   (A16)
1 MILITARY-STATUS  (A1)
1 MILITARY-SERVICE (A3)
1 #ISN             (P8)
1 #PROC-TIME       (A15)
* 1 REDEFINE #PROC-TIME
*   2 #PROC-DATE (A8)
LOCAL
1 #CADET    (A3/4) CONST <'FC','FCR','AC','ACR'>
1 #RESERVE  (A3/8) CONST <'FR','FRA','AR','ARA','CGA','CGR','MR','NRA'>
1 #NGUARD   (A3/4) CONST <'FG','FGA','AGA','AG'>
1 #VETERAN  (A3/2) CONST <'P','V'>
1 #MATCH    (L)
1 #SAVE-CODE (A1)
1 PS-V VIEW OF A-PRODUCT-STATUS
  2 ADD-PROG      (A8)
  2 PRODUCT_ID    (A16)
  2 FACE_AMOUNT   (N7.2)
  2 DEATH_BENEFIT (N7.2)
  2 PRODUCT_CODE  (A3)
  2 ID-NUMBER     (N6.0)
  2 MEMBER_TYPE   (A1)
  2 STATUS_CODE   (A16)
  2 STATUS_TIME   (A15) 2 REDEFINE STATUS_TIME
    3 #YYYYMMDD   (A8)
    3 #HHIISST    (A7)
  2 ACTIVE_CODE   (A1)
  2 MIL-BRANCH    (A1)
  2 MIL-STATUS    (A1)
  2 AGE           (N3.0)
  2 PAY-GRADE     (A3)
  2 LIVE-CHANGE   (N1)
1 MD-V VIEW OF A-PRODUCT-STATUS
  2 ADD-PROG      (A8)
  2 PRODUCT_ID    (A16)
  2 FACE_AMOUNT   (N7.2)
  2 DEATH_BENEFIT (N7.2)
  2 PRODUCT_CODE  (A3)
  2 ID-NUMBER     (N6.0)
  2 MEMBER_TYPE   (A1)
  2 STATUS_CODE   (A16) 2 REDEFINE STATUS_CODE
    3 MD-CODE-7 (A7)
  2 STATUS_TIME   (A15) 2 REDEFINE STATUS_TIME
    3 #YYYYMMDD   (A8)
    3 #HHIISST    (A7)
  2 ACTIVE_CODE   (A1)
  2 MIL-BRANCH    (A1)
  2 MIL-STATUS    (A1)
  2 AGE           (N3.0)
  2 PAY-GRADE     (A3)
  2 LIVE-CHANGE   (N1)
1 CNT-V VIEW OF A-CONTACTS
  2 DATE-OF-DEATH
  2 DATE-OF-BIRTH
  2 RANK-ID
1 RR-V VIEW OF A-RANK-REFERENCE
  2 PAY-GRADE  (A3)
END-DEFINE
*
MOVE BY NAME PRODUCT TO PS-V


**PS-V.#YYYYMMDD := #PROC-DATE
FIND (1) CNT-V WITH ID-NUMBER = PRODUCT.ID-NUMBER
  PS-V.AGE := GET-AGE-2 (< CNT-V.DATE-OF-BIRTH >)
END-FIND
DECIDE FOR FIRST CONDITION
  WHEN MILITARY-STATUS = 'C' AND MILITARY-SERVICE = 'V'
    PS-V.MIL-STATUS := 'N'  /* NON MILITARY
  WHEN MILITARY-STATUS = 'R'
    PS-V.MIL-STATUS := 'R'  /* RETIRED
  WHEN MILITARY-STATUS = 'C' AND NOT MILITARY-SERVICE = #VETERAN (*)
    PS-V.MIL-STATUS := 'V'  /* VETERAN
  WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #NGUARD (*)
    PS-V.MIL-STATUS := 'G'  /* NATIONAL GUARD
  WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #RESERVE (*)
    PS-V.MIL-STATUS := 'Z'  /* RESERVE
  WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #CADET (*)
    PS-V.MIL-STATUS := 'C'  /* CADET
  WHEN NONE
    PS-V.MIL-STATUS := 'A'  /* Active
END-DECIDE
PS-V.MIL-BRANCH := MILITARY-SERVICE
FIND (1) RR-V WITH RANK-ID = CNT-V.RANK-ID
  PS-V.PAY-GRADE := RR-V.PAY-GRADE
END-FIND
*
PS-V.ACTIVE_CODE := GetMDCode (< PS-V.STATUS_CODE >)
PS-V.LIVE-CHANGE := LifeCount (< PS-V.ACTIVE_CODE,#ISN,PS-V.PRODUCT_ID >)
*
RESET #MATCH
FIND MD-V WITH PRODUCT_ID = PS-V.PRODUCT_ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  IF MD-V.STATUS_TIME GE #PROC-TIME
    #SAVE-CODE := MD-V.ACTIVE_CODE
    IF MD-V.ACTIVE_CODE = 'D' OR= 'V' OR= 'L' OR= '2' OR= 'S' OR= 'X'
      #SAVE-CODE := '5'  /* INSURANCE END
    END-IF
    IF MD-V.ACTIVE_CODE = 'A'
      #SAVE-CODE := 'N'  /* NEW INSURANCE
    END-IF
    IF #SAVE-CODE = PS-V.ACTIVE_CODE
      #MATCH := TRUE
      IF MD-V.ACTIVE_CODE EQ 'N' OR= 'A' OR= 'D'
        IGNORE
      ELSE
        MD-V.DEATH_BENEFIT := PS-V.DEATH_BENEFIT
        MD-V.FACE_AMOUNT   := PS-V.FACE_AMOUNT
        UPDATE
      END-IF
      ESCAPE BOTTOM /* ONLY ONE MATCH ?
    END-IF
  END-IF
END-FIND
IF NOT #MATCH
  PS-V.ADD-PROG := 'BATCH'
  MOVE EDITED *TIMX (EM=YYYYMMDDHHIISST) TO PS-V.STATUS_TIME
  STORE PS-V
END-IF
*
END
