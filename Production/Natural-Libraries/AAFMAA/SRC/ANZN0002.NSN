* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ANZN0002
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    08/12/2013  None    Program written                           *
************************************************************************
DEFINE DATA
PARAMETER
1 #ZIP-FROM      (N5)
1 #ZIP-THRU      (N5)
1 #ZIP-CNT-WIDS  (P5)
1 #ZIP-CNT-ACTS  (P5)
1 #ZIP-CNT-RETS  (P5)
LOCAL
1 #ZIP-START   (N9) 1 REDEFINE #ZIP-START
  2 #Z5-START  (N5)
  2 #Z4-START  (N4)
1 #ZIP-END     (N9) 1 REDEFINE #ZIP-END
  2 #Z5-END    (N5)
  2 #Z4-END    (N4)
1 #CN          (N6)
1 AP-V VIEW OF A-ADDRESS-POOL
  2 ZIP-CODE
  2 ADDRESS-POOL-ID
1 AR-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID        (N8)
  2 ADDRESS-POOL-ID   (N8)
  2 ADDR-LOCATION     (A1)
  2 ADDR-TYPE-CD      (A1)
  2 RETURNED-MAIL-IND (A1)
  2 STATUS            (A1)
1 ST-V VIEW OF A-STATUS
  2 WIDOW-FLAG
  2 MILITARY-STATUS
END-DEFINE
*
RESET #ZIP-START
  #ZIP-CNT-WIDS
  #ZIP-CNT-ACTS
  #ZIP-CNT-RETS
MOVE #ZIP-FROM TO #Z5-START
MOVE #ZIP-THRU TO #Z5-END
#Z4-END   := 9999
FIND AP-V WITH ZIP-CODE = #ZIP-START THRU #ZIP-END
  FIND AR-V WITH ADDRESS-POOL-ID = AP-V.ADDRESS-POOL-ID
    ACCEPT        IF AR-V.STATUS NE 'D'
      AND AR-V.ADDR-TYPE-CD      EQ 'P'
*      AND AR-V.RETURNED-MAIL-IND NE 'Y'   remove undeliverable check
    #CN := F-GET-CN (< AR-V.CONTACT-ID >)
    IF #CN <= 1900
      ESCAPE TOP
    END-IF
    FIND ST-V WITH ID-NUMBER = #CN
      IF ST-V.WIDOW-FLAG = 'Y'
        ADD 1 TO #ZIP-CNT-WIDS
      ELSE
        IF ST-V.MILITARY-STATUS = 'A'
          ADD 1 TO #ZIP-CNT-ACTS
        ELSE
          ADD 1 TO #ZIP-CNT-RETS
        END-IF
      END-IF
    END-FIND
  END-FIND
END-FIND
*
END
