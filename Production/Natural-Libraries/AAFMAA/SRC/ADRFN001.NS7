* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* DESCRIPTION  : Format name into 25 byte formatted display            *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* NGG    10/29/2010  None    Program Written                           *
* PAM    07/11/2011  PM1     Added contact-type                        *
************************************************************************
*
DEFINE FUNCTION FUNCTION-FRM-NAME RETURNS (A) DYNAMIC
*
DEFINE DATA
*
PARAMETER
*
1 #CONTACT-ID              (N8)
1 #FIRST-NAME              (A25)
1 #MIDDLE-NAME             (A25)
1 #LAST-NAME               (A25)
1 #SUFFIX                  (A10)
*
LOCAL
*
1 #DISPLAY-NAME            (A) DYNAMIC
*
1 CONTACT  VIEW OF A-CONTACTS
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 CONTACT-TYPE               /* PM1
  2 FIRM-ORGANIZATION-NAME     /* PM1

END-DEFINE
*
RESET FUNCTION-FRM-NAME #DISPLAY-NAME
  #FIRST-NAME
  #MIDDLE-NAME
  #LAST-NAME
  #SUFFIX
*
FIND CONTACT WITH CONTACT.CONTACT-ID = #CONTACT-ID
  IF CONTACT.CONTACT-TYPE = 'O'                        /* PM1
    #DISPLAY-NAME := CONTACT.FIRM-ORGANIZATION-NAME    /* PM1
  ELSE                                                 /* PM1
    COMPRESS CONTACT.FIRST-NAME CONTACT.MIDDLE-NAME CONTACT.LAST-NAME
      INTO #DISPLAY-NAME
    IF SUFFIX NE ' '
      COMPRESS #DISPLAY-NAME ',' INTO #DISPLAY-NAME LEAVING NO
      COMPRESS #DISPLAY-NAME CONTACT.SUFFIX INTO #DISPLAY-NAME
    END-IF
    MOVE CONTACT.LAST-NAME   TO #LAST-NAME
    MOVE CONTACT.FIRST-NAME  TO #FIRST-NAME
    MOVE CONTACT.MIDDLE-NAME TO #MIDDLE-NAME
    MOVE CONTACT.SUFFIX      TO #SUFFIX
  END-IF                                               /* PM1
END-FIND
*
FUNCTION-FRM-NAME := #DISPLAY-NAME
*
END-FUNCTION
*
END
