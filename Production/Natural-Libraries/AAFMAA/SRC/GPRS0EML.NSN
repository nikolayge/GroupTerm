* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* object GPR0SEML
* Function Send email
* Boris Zaslavskiy 10/07/2010
* ------------------------------------------------
DEFINE DATA
PARAMETER USING GPRL0EML
LOCAL
1 v_From    (A) DYNAMIC
1 File_DIR  (A) DYNAMIC  INIT <"/usr/SAG/AAFMAA/batch/workfiles/TEMP">
1 File_Name (A) DYNAMIC
1 v_KSH     (A) DYNAMIC  INIT <"/usr/SAG/AAFMAA/batch/workfiles/PROD/PGM/mailGEN1">
1 v_shell   (A) DYNAMIC
1 nl        (A2)   INIT <H'0A0D'>
1 #i        (N2)
1 v_A7      (A7)
END-DEFINE

MOVE EDITED *TIMX (EM=HHIISST) TO v_A7
IF p_To = " " p_rc := 101 ESCAPE ROUTINE END-IF
COMPRESS "Email_" *USER "_" *DATN "_" v_A7 ".tmp"    INTO File_Name  LEAVING NO SPACE
COMPRESS File_DIR "/" File_Name  INTO File_Name  LEAVING NO SPACE

DEFINE WORK FILE 11 File_Name TYPE 'ASCII-COMPRESSED'
WRITE WORK FILE 11 VARIABLE nl
    "This is a SYSTEM GENERATED E-MAIL, please do not respond to the E-MAIL address specified above." nl 
IF p_MSG (*) <> " "
FOR #i = 1 TO 30
  WRITE WORK FILE 11 VARIABLE p_MSG(#i)
END-FOR
END-IF
IF p_Cc   = "  " p_Cc   := "no" END-IF
IF p_Subj = "  " p_Subj := "no" END-IF
COMPRESS "'" p_Subj "'" INTO p_Subj LEAVE NO SPACE
EXAMINE p_Subj for "''" REPLACE  "'"
COMPRESS "'" p_To "'" INTO p_To LEAVE NO SPACE
EXAMINE p_To for "''" REPLACE  "'"
COMPRESS "'" p_Cc "'" INTO p_Cc LEAVE NO SPACE
EXAMINE p_Cc for "''" REPLACE  "'"


  CLOSE WORK FILE 11
  v_FROM := *USER

  COMPRESS v_KSH v_FROM " " p_To " " p_Cc " " p_Subj " "  File_Name INTO v_shell
    call 'shcmd' v_shell
   p_rc := RET('shcmd')
END
