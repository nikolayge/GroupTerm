* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GUP001
* DESCRIPTION: PF4 FUNCTION - Update Contact file
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* LR  03-19-2010   LR1  Correct date format from (YYYYMMDD) to  (MMDDYYYY)
* RSE 04-23-2010   RE1  Redefine #SSN
* TMT 06-12-2014   TT1  Contact Normalization - DOB and Sex
********************************************************
*
DEFINE DATA
PARAMETER
1 #API
  2 #CO-ID-1          (N8)
  2 #CO-ID-2          (N8)
  2 #CGA-ORIG-COMMAND (A2)
  2 #CGA-ORIG-ID      (N6)
  2 #CGA-USER-COMPANY (A1)
  2 #CGA-CLERK        (A3)
  2 #MODE             (A1)  /* U - Update, V - View
  2 #MSG              (A72)
1 #UI
  2 #STATUS-ISN         (P8)
  2 #CONTACT-ISN        (P8)
  2 #FIRS-ISN           (P8)
  2 #FIRST-NAME         (A25)
  2 #MIDDLE-NAME        (A25)
  2 #LAST-NAME          (A25)
  2 #SUFFIX             (A10)
  2 #SSN                (N9)
  2 REDEFINE #SSN            /* RE1
    3 #SSN-A            (A9)
  2 #GENDER-CD          (A1)
  2 #BIRTH-CERT-CODE    (A1)
  2 #MARRIAGE-CERT-CODE (A1)
  2 #ORG-NAME           (A50)
  2 #CN                 (N6)
  2 #CID                (N8)
  2 #DOB         (A8)
  2 REDEFINE #DOB
    3 #DOB-N     (N8)
  2 REDEFINE #DOB
    3 #MM-B      (A2)
    3 #DD-B      (A2)
    3 #YYYY-B    (A4)
  2 #DOD         (A8)
  2 REDEFINE #DOD
    3 #DOD-N     (N8)
  2 REDEFINE #DOD
    3 #MM-D      (A2)
    3 #DD-D      (A2)
      3#YYYY-D    (A4)
  2 #DOM         (A8)
  2 REDEFINE #DOM
    3 #DOM-N     (N8)
  2 REDEFINE #DOM
    3 #MM-M      (A2)
    3 #DD-M      (A2)
    3 #YYYY-M    (A4)
  2 #DIV         (A8)
  2 REDEFINE #DIV
    3 #DIV-N     (N8)
  2 REDEFINE #DIV
    3 #MM-V      (A2)
    3 #DD-V      (A2)
    3 #YYYY-V    (A4)
  2 #SCR-ROLE-2-CD  (A1)
  2 #ORIG-ROLE-2-CD (A1)
  2 #ROLE-DESC      (A15)
  2 #LST-UPD        (A10)
  2 #LST-USR        (A20)
  2 #RL-TEXT        (A18)
  2 #CN-TEXT        (A3)
  2 #DOM-TEXT       (A18)
  2 #DOM-CD-TEXT    (A5)
  2 #DIV-TEXT       (A18)
  2 #DT-FORMAT-TEXT (A10)
  2 #ORG-DISPLAYED  (L)
  2 #DEFAULT-LN     (A25)

1 #CONTROL-VARS  (C/7) 1 REDEFINE #CONTROL-VARS
  2 #mod#                (C)
  2 #CNT-CV              (C)
  2 #role-mod#           (C)
  2 #BCC-MOD#            (C)
  2 #DATE-CV             (C)
  2 #ORG-NAME-CV         (C)
  2 #DOM-CV              (C)
LOCAL
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-ID       (N8)           /* D
  2 ID-NUMBER        (N6)           /* D
  2 FIRST-NAME       (A25)
  2 MIDDLE-NAME      (A25)
  2 LAST-NAME        (A25)
  2 SUFFIX           (A10)
  2 GENDER-CD        (A1)         /* --NA
  2 DATE-OF-BIRTH    (N8)
  2 SSN              (N9)           /* D
  2 DATE-OF-DEATH    (N8)
  2 BIRTH-CERT-CODE  (A1)
  2 FIRM-ORGANIZATION-NAME (A75)
  2 REDEFINE FIRM-ORGANIZATION-NAME
    3 #FIRM1         (A37)
    3 #FIRM2         (A37)
  2 CONTACT-REMARKS  (A75/100)    /* MU-FIELD
  2 LAST-USER-UPD    (A8)
  2 LAST-DATE-UPD    (N8)
1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME
*   2 SEX                          /* TT1
  2 SSN
  2 PROCESS-IND
*   2 DATE-OF-BIRTH                /* TT1
  2 DATE-OF-MARRIAGE
  2 DATE-OF-DIVORCE
  2 DATE-OF-DEATH
  2 BIRTH-CERT-CODE
  2 MARRIAGE-CERT-CODE
  2 ACCESS-AUTH-CODE
  2 SPOUSE-CONTACT-ID
  2 NAME-SP
  2 SSN-SP
*   2 SEX-SP                       /* TT1
*   2 DATE-OF-BIRTH-SP             /* TT1
  2 STATUS-CODE-SP
  2 FORMER-SPOUSE-CONTACT-ID
  2 FORMER-SPOUSE-NAME
  2 FORMER-SPOUSE-SSN
*   2 FORMER-SPOUSE-DT-OF-BIRTH    /* TT1
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 FORMER-SPOUSE-STATUS
  2 FORMER-HUSBAND-CONTACT-ID
  2 FORMER-HUSBAND-NAME
  2 FORMER-HUSBAND-SSN
  2 FS-BIRTH-CERT-CD
  2 FS-MARR-CERT-CD
  2 BIRTH-CERT-CODE-SP
  2 TITLE-SP
  2 MAILING-FLAG-SP
  2 LAST-FM-DATE
  2 DTS-DATN
  2 DTS-TIMN
1 FIRS VIEW OF A-FIRS
  2 ID-NUMBER
  2 CONTACT-ID-NUMBER
  2 DEPENDENT-TABLE  (60)
    3 DEPN-CONTACT-ID
    3 DEPN-NAME
    3 DEPN-BIRTH-CD
    3 DEPN-STATUS-CD
    3 DEPN-DATE-OF-BIRTH
    3 DEPN-SSN
    3 DEPN-REMARKS
  2 DEPN-COLL-FUND-TBL (60)
    3 DEPN-COLL-FUND (P7)
  2 DEPN-COLL-TYPE-TBL (60)
    3 DEPN-COLL-TYPE (A1)
  2 DEPN-COLL-INFO-TBL (60)
    3 DEPN-COLL-FROM-YYYY (N4)
    3 DEPN-COLL-THRU-YYYY (N4)
    3 DEPN-COLL-ANN-COST (P6)
  2 DP-LAST-FM-DATE (N8.0)
  2 DTS-DATN (P9.0)
  2 DTS-TIMN (P7.0)
1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID     (N8)           /* D
  2 ROLE-1-CD        (A1)
  2 CONTACT-2-ID     (N8)           /* D
  2 ROLE-2-CD        (A1)
  2 ACCESS-AUTH-SVC  (A1)
  2 ACCESS-AUTH-INS  (A1)
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)

END-DEFINE

MOVE #CO-ID-2 TO #CID
**MOVE #CGA-ORIG-ID TO #CN
MOVE 'CN:' TO #CN-TEXT

FIND CNT WITH CONTACT-ID = #CO-ID-1
  MOVE LAST-NAME TO #DEFAULT-LN
END-FIND
*
FIND CNT WITH CONTACT-ID = #CO-ID-2
  MOVE ID-NUMBER TO #CN                     /* RE2
  IF #CN = 0
    RESET #CN-TEXT
  END-IF
  MOVE *ISN TO #CONTACT-ISN
END-FIND
*
IF CNT.ID-NUMBER NE 0
  FIND STATUS WITH ID-NUMBER = CNT.ID-NUMBER
    MOVE *ISN TO #STATUS-ISN
  END-FIND
  FIND FIRS WITH ID-NUMBER = CNT.ID-NUMBER
    MOVE *ISN TO #FIRS-ISN
  END-FIND
ELSE
  FIND STATUS WITH ID-NUMBER = #CGA-ORIG-ID
    MOVE *ISN TO #STATUS-ISN
  END-FIND
  FIND FIRS WITH ID-NUMBER = #CGA-ORIG-ID
    MOVE *ISN TO #FIRS-ISN
  END-FIND
END-IF
*
IF #STATUS-ISN = 0
  ASSIGN #MSG = 'No record for the member'
  ESCAPE ROUTINE
END-IF

IF #CO-ID-1 = #CO-ID-2                       /* MEMBER
  MOVE 'M' TO #SCR-ROLE-2-CD #ORIG-ROLE-2-CD
  MOVE 'MEMBER' TO #ROLE-DESC
ELSE
  FIND CNT-REL WITH CONTACT-2-ID = #CO-ID-2    /* SPOUSE/DEPENDENT/OTHER
    IF CONTACT-1-ID = #CO-ID-1
      MOVE ROLE-2-CD TO #SCR-ROLE-2-CD #ORIG-ROLE-2-CD
      #ROLE-DESC := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
      ESCAPE BOTTOM
    END-IF
  END-FIND
END-IF
*
MOVE CNT.BIRTH-CERT-CODE TO #BIRTH-CERT-CODE
IF CNT.LAST-DATE-UPD NE 0
  MOVE CNT.LAST-DATE-UPD TO #YYYYMMDD
  COMPRESS #MM-A '-' #DD-A '-' #YYYY-A INTO #LST-UPD LEAVING NO
END-IF
*
IF CNT.DATE-OF-BIRTH NE 0
  MOVE CNT.DATE-OF-BIRTH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DOB LEAVING NO
END-IF
*
IF CNT.DATE-OF-DEATH NE 0
  MOVE CNT.DATE-OF-DEATH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DOD LEAVING NO
END-IF
*
IF #CO-ID-2 NE 0
  FIND (1) TERM WITH CLERK-ID = CNT.LAST-USER-UPD
    MOVE COMMENTS TO #LST-USR
  END-FIND
END-IF
*
MOVE 'Role/Relationship:' TO #RL-TEXT
IF #CGA-ORIG-COMMAND NE 'CB'
  MOVE (AD=P) TO #ROLE-MOD#
END-IF
*
IF #CGA-USER-COMPANY = 'S'
  MOVE (AD=U) TO #BCC-MOD#
ELSE
  MOVE (AD=P) TO #BCC-MOD#
END-IF
*
IF #SCR-ROLE-2-CD = 'S' OR = 'X'
  MOVE '(MMDDYYYY)' TO #DT-FORMAT-TEXT
  MOVE 'Date of Marriage :' TO #DOM-TEXT
  MOVE 'Code:' TO #DOM-CD-TEXT
  IF #SCR-ROLE-2-CD = 'S'
      AND DATE-OF-MARRIAGE NE 0
    MOVE DATE-OF-MARRIAGE TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
    MOVE (AD=D) TO #DOM-CV
  END-IF
  IF #SCR-ROLE-2-CD = 'X'
      AND FORMER-SPOUSE-DATE-OF-MARRIAGE NE 0
    MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
  END-IF
  IF #SCR-ROLE-2-CD = 'S'
    MOVE MARRIAGE-CERT-CODE TO #MARRIAGE-CERT-CODE
  ELSE
    MOVE FS-MARR-CERT-CD TO #MARRIAGE-CERT-CODE
  END-IF
  MOVE 'Date of Divorce  :' to #DIV-TEXT
  IF #SCR-ROLE-2-CD = 'S'
    IF DATE-OF-DIVORCE NE 0
        AND STATUS-CODE-SP NE 'D'
      MOVE DATE-OF-DIVORCE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
    END-IF
  ELSE
    IF FORMER-SPOUSE-DATE-OF-DIVORCE NE 0
        AND FORMER-SPOUSE-STATUS NE 'D'
        AND #SCR-ROLE-2-CD NE ' '
      MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
    END-IF
  END-IF
  MOVE (AD=U) TO #DATE-CV
ELSE
  RESET #DT-FORMAT-TEXT
  MOVE (AD=P) TO #DATE-CV
END-IF

MOVE CNT.FIRST-NAME  TO #FIRST-NAME
MOVE CNT.MIDDLE-NAME TO #MIDDLE-NAME
MOVE CNT.LAST-NAME   TO #LAST-NAME
MOVE CNT.SUFFIX      TO #SUFFIX
MOVE CNT.SSN         TO #SSN
MOVE CNT.GENDER-CD   TO #GENDER-CD
*
IF CNT-REL.ROLE-2-CD = 'A' OR = 'B' OR = 'E' OR = 'F' OR = 'I' OR = 'P' OR = 'R' OR = 'T'
  SET CONTROL 'N'
  MOVE CNT.FIRM-ORGANIZATION-NAME TO #ORG-NAME
  MOVE TRUE TO #ORG-DISPLAYED
END-IF
*
IF #MODE = 'V'
  MOVE (AD=P) TO #CNT-CV #role-mod# #BCC-MOD# #DATE-CV #ORG-NAME-CV #DOM-CV
ELSE
  MOVE (AD=U) TO #CNT-CV #role-mod#
END-IF
*
IF #DOM-TEXT = ' '  OR #MODE = 'V' OR #CGA-USER-COMPANY = 'I'
  MOVE (AD=P) TO #DOM-CV
ELSE
  MOVE (AD=U) TO #DOM-CV #DATE-CV
END-IF
*
IF #SSN = 0                                /* RE1
  RESET #SSN-A
END-IF
END
