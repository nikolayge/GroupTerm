* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP005C - PRT ben LETTERS FROM "MR" REQUEST FILE -2nd
*                      - ben-ind 5/6/7/8
* bz  06/09/2013  Rebranding (Search for AAFMAA)
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL
*
1 #REPEAT      (N1)
1 #R-X         (I2)
1 #9-AND-10  (L)
1 #SPS-GEN-LINE-1   (A30)
1 #SPS-GEN-LINE-2   (A15)
1 #FORMAT-CN        (A8)
1 #SCR-ZIP-BAR-CODE (A14)
*
1 #PARM-INPUT    (A36)
1 #PARM-CHOICE   (A12)
1 #PARM-CLERK    (A3)
1 #SCR-TBL    (A78/25)
1 REDEFINE #SCR-TBL
 2 #SCR-TBL-OCC-1 (25)
  3 #FST-4        (A4)
  3 #SCR-TBL-LST  (A74)
*
1 #MR-MSG-NOTE-NUM (P8)
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT (11:14)
*
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND       (N1)
   3 MSG-IND       (N1)
   3 RET-IND       (N1)
   3 FIN-IND       (N1)
   3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND       (A1)
   3 MAL-IND       (A1)
   3 BEN-SHO-IND   (A1)
   3 ACC-IND       (A1)
   3 CAP-IND       (A1)
   3 APP-IND       (A1)
   3 PIL-IND       (A1)
   3 CO-IND        (A1)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
     4 REQUEST-MM-N  (N2)
    3 REQUEST-DD   (A2)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
  2 MESSAGE          (A60/20)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 NUMBER-MEMBERSHIPS
  2 STATUS      (9)
  2 NUM-MBRSHIPS (9)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 REDEFINE #MM
     4 #MM-N  (N2)
    3 #DD     (A2)
*
1 #COUNTERS
  2 #BEN-RQST-CNT   (P5)
*
1 #FST-BEN  (L) INIT <TRUE>
1 #I1          (I2)
1 #I2          (I2)
1 #SC-X        (I2)
1 #ISN         (P8)
*
1 #INSURED-NAME   (A25)
1 #INSURED-POL    (A8)
1 #NUM-0ZIP       (N3)
*
END-DEFINE
*
* * * * * * * * *
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
                  5X 'BEN LETTER DETAIL'
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
INPUT #PARM-INPUT                    /* TYPE OF MAIL-OUTS
MOVE #PARM-INPUT TO #PARM-CHOICE
*
IF #PARM-CHOICE = 'RQST'
  INPUT #PARM-INPUT                    /* CLERK
END-IF
*
INPUT #PARM-INPUT /* MEMBER DATE RANGE
INPUT #PARM-INPUT /* MEMBER AGE 62 DATE RANGE
INPUT #PARM-INPUT /* WIDOW DATE RANGE
*
IF #PARM-CHOICE = 'MBR-RQST-WID' OR = 'MBR-RQST' OR = 'RQST'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
* BEN LETTER REQUEST PROCESSING
* * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
* IF (#PARM-CLERK = '***') AND (REQUEST-DATE NE 9999) /* BYPASS IF
  IF (#PARM-CLERK = '***') AND (REQUEST-MM-N LT 50) /* BYPASS IF
    ESCAPE TOP                              /* entered after job started
  END-IF
*
  IF   ((FIR-IND NE 0) OR (FIN-IND NE 0))
   AND (BEN-SHO-IND NE ' ')
    RESET BEN-SHO-IND
  END-IF
*
  IF  (XX-R01.BEN-IND NE ' ')
   OR (XX-R01.BEN-SHO-IND NE ' ')
    ADD +1 TO #BEN-RQST-CNT
  ELSE
    ESCAPE TOP
  END-IF
*
  END TRANSACTION
END-ALL  /* (0130)
*
SORT BY ID-NUMBER
  USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE RQST-TABLE
                 ASSUM-RET-DATE-2 ASSUM-RET-DATE-3 MESSAGE (*)
*
  RESET *ISN #ISN
  FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
        IF NO RECORDS FOUND
          WRITE (2) 'MBR NOT FOUND' ST-V.ID-NUMBER
          ESCAPE BOTTOM
        END-NOREC
*
        MOVE *ISN TO #ISN
      END-FIND
    END-NOREC
*
    IF #ISN = 0
      MOVE *ISN TO #ISN
    END-IF
  END-FIND
*
  IF #ISN = 0
    ESCAPE TOP
  END-IF
*
  PERFORM PROCESS-ADDR
  IF #ZIP-CODE = 0
    ADD 1 TO #NUM-0ZIP
  END-IF
*
  MOVE #ISN TO #CGA-ISN
  MOVE XX-R01.ID-NUMBER TO #CGA-ID
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
*
  RESET #9-AND-10
  IF (XX-R01.BEN-IND = '5') AND (ASSUM-RET-DATE-3 = 11)
    MOVE 1 TO #REPEAT
    MOVE TRUE TO #9-AND-10
  ELSE
    MOVE 2 TO #REPEAT
  END-IF
*
  FOR #R-X #REPEAT TO 2        /*  <<<<<<<<<<<<<<<<<<
*
  IF XX-R01.BEN-IND = '5' OR = '6' OR = '7' OR = '8'
    IF XX-R01.BEN-IND = '5'
      IF #9-AND-10
        IF #R-X = 1
          MOVE 9 TO ASSUM-RET-DATE-3
        ELSE
          MOVE 10 TO ASSUM-RET-DATE-3
        END-IF
      END-IF
*
      MOVE 'BNP' TO #CGA-SCR-SUFFIX
      RESET #SCR-TBL (*)
      IF ASSUM-RET-DATE-3 = 10
        COMPRESS '  We have received and processed your application for'
                 'insuring'       INTO #SCR-TBL-LST (1)
        MOVE 'spouse/generation coverage.' TO #SCR-TBL-LST (2)
        COMPRESS '  If a successor owner is not named prior to the'
                 'death of the Owner'     INTO #SCR-TBL-LST (4)
        COMPRESS 'of this policy, we must await issuance of Letters'
                 'Administration' INTO  #SCR-TBL-LST (5)
        MOVE 'from the Court.' TO #SCR-TBL-LST (6)
        COMPRESS '  To avoid such delay and to keep this policy'
                 'current, please'  INTO #SCR-TBL-LST (8)
        MOVE 'complete and return the enclosed form.'
           TO #SCR-TBL-LST (9)
        MOVE 9 TO #SC-X
      ELSE
        COMPRESS '  We have received and processed your application'
                 'for insurance. We note'         INTO #SCR-TBL-LST (1)
        IF ASSUM-RET-DATE-3 = 9
         OR (#9-AND-10 AND (#R-X = 1))
          COMPRESS 'that the application did not have a signature line'
                   'for the owner of the'    INTO #SCR-TBL-LST (2)
          COMPRESS 'certificate. The insured,' #INSURED-NAME
                                               INTO #SCR-TBL-LST (3)
          COMPRESS #SCR-TBL-LST (3) ', does not have the'
                        INTO #SCR-TBL-LST (3) LEAVING NO
          COMPRESS 'rights of an owner which include the beneficiary'
                   'designation. You'  INTO #SCR-TBL-LST (4)
          COMPRESS 'as the owner of the certificate need to complete'
                   'the enclosed beneficiary'    INTO #SCR-TBL-LST (5)
          MOVE 'form.' TO #SCR-TBL-LST (6)
          MOVE 6 TO #SC-X
        ELSE
          COMPRESS 'that your beneficiary designation is not in'
                 'accordance with Association'    INTO #SCR-TBL-LST (2)
          MOVE 'accepted wording.' TO #SCR-TBL-LST (3)
          MOVE 3 TO #SC-X
        END-IF
      END-IF
*
      IF ASSUM-RET-DATE-3 = 1
        ADD 2 TO #SC-X
        COMPRESS '  A "By Law" beneficiary designation is one which'
                 'the Association is not'     INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'able to utilize. If you desire to not specifically'
                 'name an individual(s),'      INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
       COMPRESS 'please insert "My Estate" in the principal beneficiary'
                 'section of the'             INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'enclosed form. The proceeds would be paid to the'
                 'executor of the estate'      INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'who would see to the ultimate distribution of the'
                 'proceeds.'                   INTO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 2
        ADD 2 TO #SC-X
        COMPRESS '  Your alternate beneficiary designation "By Law"'
                 'is one which the'           INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'Association is not able to utilize. We prefer that'
                 'a beneficiary be'    INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'listed by name and relationship. If you desire to not'
                 'specifically name'         INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'an individual(s), please insert "My Estate" in the'
                 'alternate bene-'            INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'ficiary section of the form. Should your'
                 '(relationship of principal bene-'
                                              INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'ficiary) not survive you, the proceeds would be'
                 'paid to the'               INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'Executor of your estate who would see to the ultimate'
                 'distribution of'           INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        MOVE     'the monies.' TO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 3
        ADD 2 TO #SC-X
        COMPRESS '  A beneficiary form is enclosed and we ask that'
                 'you note in the space'      INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'provided the first name of the person you wish to'
                 'designate.'               INTO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 4
        ADD 2 TO #SC-X
        COMPRESS '  In order that our records may be complete, please'
                 'note the relationship'      INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'to you of your (principal and/or alternate)'
                 'beneficiary on the enclosed' INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        MOVE     'form.'                  TO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 5
        ADD 2 TO #SC-X
        COMPRESS '  So there will be no question as to the exact'
                 'intention for payment of'   INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'the benefit, we have prepared a beneficiary form.'
                                               INTO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 6
        ADD 2 TO #SC-X
        COMPRESS '  When unequal distribution of the benefit is'
                 'requested, we prepare a'    INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'special beneficiary form so there will be no question'
                 'as to your exact'            INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        MOVE     'intentions.' TO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 7
        ADD 2 TO #SC-X
        COMPRESS '  Your application did not list a beneficiary and'
                 'we have enclosed a form'    INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'for your use in doing so. If you wish to not'
                 'specifically name an'        INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'individual, please note "My Estate" in the principal'
                 'beneficiary section'         INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'of the form. In the event of your demise, the'
                 'proceedings would be paid'   INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'to the executor of your estate who would see to the'
                 'ultimate'                    INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        MOVE     'distribution of the benefit.' TO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 = 8
        ADD 2 TO #SC-X
        COMPRESS '  A beneficiary form is enclosed which has been'
                 'prepared in accordance'     INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'with your recent telephone conservation with Ms.'
                 'Blakley.'                    INTO #SCR-TBL-LST (#SC-X)
      END-IF
*
      IF ASSUM-RET-DATE-3 NE 10
        ADD 2 TO #SC-X
        COMPRESS '  Please complete the enclosed form where indicated'
                 'and return for'          INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'recording. If a different designation is desired'
               'please advise us of'           INTO #SCR-TBL-LST (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'your exact wishes and a new form will be forwarded.'
                                             INTO #SCR-TBL-LST (#SC-X)
      END-IF
    END-IF
*
    IF XX-R01.BEN-IND = '6' OR = '7' OR = '8'
      COMPRESS ASSUM-RET-DATE-3 ASSUM-RET-DATE-2
                                  INTO #CGA-MF-SCR-COMMAND LEAVING NO
      DECIDE ON FIRST XX-R01.BEN-IND
        VALUE '6' MOVE 'BNT' TO #CGA-SCR-SUFFIX
        VALUE '7' MOVE 'BNL' TO #CGA-SCR-SUFFIX
        VALUE '8' MOVE 'BNN' TO #CGA-SCR-SUFFIX
        NONE IGNORE
      END-DECIDE
*
      RESET #SCR-TBL (*)
      COMPRESS '  As you requested, we are enclosing a Transfer'
               'of Ownership Form to be'     INTO #SCR-TBL-LST (1)
      COMPRESS 'dated, signed and returned to us. Upon receipt'
               'here, we will compute for'   INTO #SCR-TBL-LST (2)
      COMPRESS 'gift tax purposes the cash value of the insurance'
               'as of the day you'           INTO #SCR-TBL-LST (3)
      COMPRESS 'execute the Transfer of Ownership form and promptly'
               'furnish the informa-'        INTO #SCR-TBL-LST (4)
      MOVE     'tion to you.' TO #SCR-TBL-LST (5)
      COMPRESS '  It is Association practice to request that a'
               'beneficiary form be'        INTO #SCR-TBL-LST (7)
      COMPRESS 'executed by the new owner(s) on the same day or the'
               'day following transfer'      INTO #SCR-TBL-LST (8)
      COMPRESS 'of ownership. Please have the new owner(s) complete'
               'the enclosed'    INTO #SCR-TBL-LST (9)
      COMPRESS 'beneficiary form where indicated and return it to us'
               'for processing.'          INTO #SCR-TBL-LST (10)
      COMPRESS '  A duplicate copy of both forms will also be'
               'furnished in order that'     INTO #SCR-TBL-LST (12)
      COMPRESS 'they may be attached to your AAFMAA'
                                             INTO #SCR-TBL-LST (13)
      MOVE     'Certificate(s) for a permanent record.'
                                               TO #SCR-TBL-LST (14)
      MOVE 14 TO #SC-X
    END-IF
*
    ADD 2 TO #SC-X
    COMPRESS '  If you have any questions, please feel free to call'
             'Lin Blakley on the'       INTO #SCR-TBL-LST (#SC-X)
*
    ADD 1 TO #SC-X
    MOVE     'toll-free number below.' TO #SCR-TBL-LST (#SC-X)
*
    ADD 1 TO #SC-X
*
    FOR #I1 11 TO 14   /* PM - 05/2000 free note recs 600000-700000
      IF MESSAGE (#I1) NE ' '
        ADD 1 TO #SC-X
        MOVE MESSAGE (#I1) TO #SCR-TBL-LST (#SC-X)
      END-IF
    END-FOR
*
*   COMPUTE #MR-MSG-NOTE-NUM = XX-R01.ID-NUMBER + 600000
*   FIND (1) NT-V WITH ID-NUMBER = #MR-MSG-NOTE-NUM
*     FOR #I1 11 TO 14
*       IF NT-V.NOTE-TEXT (#I1) NE ' '
*         ADD 1 TO #SC-X
*         MOVE NT-V.NOTE-TEXT (#I1) TO #SCR-TBL-LST (#SC-X)
*       END-IF
*     END-FOR
*   END-FIND
*
    IF #ZIP-CODE = 0
      NEWPAGE (6)
      WRITE (6) NOTITLE USING FORM 'AOAF005D'
    ELSE
      NEWPAGE (4)
      WRITE (4) NOTITLE USING FORM 'AOAF005D'
    END-IF
*
    IF ASSUM-RET-DATE-3 = 10
      RESET #SCR-TBL (*) #SC-X
      ADD 1 TO #SC-X
* * * *  below is also in e4751sbd
      MOVE '                  DESIGNATION OF SUCCESSOR OWNERSHIP'
               TO #SCR-TBL-LST (#SC-X)
      ADD 2 TO #SC-X
      COMPRESS '  AAFMAA is'
             'requested to make the following' INTO #SCR-TBL (#SC-X)
*
      ADD 1 TO #SC-X
      MOVE 'change of ownership in the event of my death.'
                                         TO #SCR-TBL (#SC-X)
      ADD 2 TO #SC-X
      COMPRESS '  In accordance with the provisions of Certificate'
               'No.' #INSURED-POL  INTO #SCR-TBL (#SC-X)
*
      ADD 1 TO #SC-X
      COMPRESS 'on the life of' #INSURED-NAME
              INTO #SCR-TBL (#SC-X)
      COMPRESS #SCR-TBL (#SC-X) ', the insured, I designate'
                                 INTO #SCR-TBL (#SC-X) LEAVING NO
*
      ADD 2 TO #SC-X
     COMPRESS '________________________, ______________________________'
             'as Successor Owner.' INTO #SCR-TBL (#SC-X)
*
      ADD 1 TO #SC-X
      COMPRESS '     Name                       Relationship to owner'
           TO #SCR-TBL (#SC-X)
*
      ADD 4 TO #SC-X
      COMPRESS '                                         ____________'
           '______________________' INTO #SCR-TBL (#SC-X) LEAVING NO
*
      ADD 1 TO #SC-X
      COMPRESS '                                                  Sig'
               'nature of Owner' INTO #SCR-TBL (#SC-X) LEAVING NO
*
      ADD 3 TO #SC-X
      MOVE     'Date  _________________________________'
                                  TO #SCR-TBL (#SC-X)
*
      ADD 2 TO #SC-X
   COMPRESS '                                         _________________'
               '_________________' INTO #SCR-TBL(#SC-X) LEAVING NO
*
      ADD 1 TO #SC-X
 MOVE '                 Approved by authority of              Secretary'
                               TO #SCR-TBL (#SC-X)
      ADD 1 TO #SC-X
      MOVE '                  the Board of Directors'
           TO #SCR-TBL (#SC-X)
*
      IF #ZIP-CODE = 0
        NEWPAGE (6)   /* was 7
        WRITE (6) NOTITLE USING FORM 'AOAF005H'
      ELSE
        NEWPAGE (4)   /* was 5
        WRITE (4) NOTITLE USING FORM 'AOAF005H'
      END-IF
    ELSE
      FETCH RETURN 'E4700PBD'
    END-IF
  END-IF
*
  END-FOR /* (0194)       /*  <<<<<<<<<<<<<<<<<<
*
  IF #9-AND-10
    MOVE 11 TO ASSUM-RET-DATE-3
  END-IF
END-SORT  /* (0151)
*
WRITE (2) / 12T 'TOTAL BEN LTR REQUESTS' #BEN-RQST-CNT
*
IF #BEN-RQST-CNT NE 0
  EJECT (8)
  WRITE (8) / 12T 'TOTAL BEN LTR REQUESTS' #BEN-RQST-CNT
  IF #NUM-0ZIP NE 0
    WRITE (8) // 8T '******* zero zips = ' #NUM-0ZIP
  END-IF
END-IF
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-ADDR
* * * * * * * * * * * * * * * *
*
MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
COMPRESS 'CN' #ID-NUM INTO #FORMAT-CN LEAVING NO
MOVE #EDS-ZIP-BAR-CODE TO #SCR-ZIP-BAR-CODE
RESET #SPS-GEN-LINE-1 #SPS-GEN-LINE-2
IF #SPS-GEN-ID NE 0
  COMPRESS 'For' #SPS-GEN-NAME INTO #SPS-GEN-LINE-1
  COMPRESS 'With CN' #SPS-GEN-ID INTO #SPS-GEN-LINE-2
END-IF
*
IF #SPS-GEN-NAME NE ' '
  MOVE #SPS-GEN-NAME TO #INSURED-NAME
  MOVE 1 TO #I2
  FOR #I1 1 TO 9   /* find last pol #
    IF STATUS (#I1)= 'D'
      MOVE NUM-MBRSHIPS (#I1) TO #I2
    END-IF
  END-FOR
  COMPRESS ST-V.ID-NUMBER '-' #I2 INTO #INSURED-POL LEAVING NO
ELSE
  COMPRESS #FIRST-NAME #LAST-NAME INTO #INSURED-NAME
END-IF
*
END-SUBROUTINE
*
END
