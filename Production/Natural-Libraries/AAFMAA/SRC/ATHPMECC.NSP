* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPMECC - MEC processing
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    09282004  DW1     add CREDIT CARD processing                  *
* PAM    02102005  PM2     Increased #LUMP-PMT from 5.2 to 6.2.        *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)
*                    EBCDIC to HEX code '09' ASCII
* DRW    08312006  DW3     Increased size of #RECS-READ
* DRW 08/28/2008 DW4 Gen Plus Processing
* RSE 09/29/2009 RE1 Change PROCESS-IND logic for Unification
* YAK 10/09/2012 YK1 Use function to identify Level Term II plans
* JNT 09/18/2013 JT1 Address normalization
* TMT 12/03/2013 TT1 1M$ PROJECT
* NGG 01/16/2013 NG1 GP2 Project
* TMT 06/10/2014 TT2 CONTACT NORMALIZATION - DOB AND SEX
************************************************************************
*
DEFINE DATA
LOCAL
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)       /*TT1
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID     (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
* ABOVE for CALC-PREMS-PAID
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #SUFFIX              (A3)
1 #1035-NX    (A60)
1 #1035-EXCH-AMT  (P6.2)
1 #LUMP-PMT  (P6.2)                                               /* PM2
1 #MAX-7-PAY (P6.2)
1 #EXCEEDS-MEC       (P7.2)                        /* vt 01/24/2014 was p6.2
1 #AVL-TDIV          (P7.2)
1 #DB                (P9.2)
1 #MEC-PREMS-PAID-TO-DATE (P7.2)                   /* vt 01/24/2014 was p6.2
1 #OBV-MEC-BUT-NOT   (P5)
1 #PD-TO-MM (N2/4)
1 #MATCHING-PD-TO  (L)
1 #ERR-MSG   (A25)
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 WIDOW-FLAG          (A1)
  2 MEMBER-CONTACT-ID   (N8.0)
* 2 MEMBER-FLAG         (A1)
*  2 SEX                                    /* TT2
  2 INTRO-FLAG
  2 PROCESS-IND
  2 RANK                (A3)
  2 MILITARY-SERVICE    (A3)
  2 MILITARY-STATUS     (A1)
  2 NAME         (A25)
*  2 ADDRESS-1    (A25)                                           /* JT1 
  2 X-FIRST-PLACE-FEE    (9)
  2 X-BILL-ADJUST        (9)
  2 X-LOAN-AMOUNT         (9)
  2 X-LOAN-AMOUNT-2       (9)
*   2 DATE-OF-BIRTH       (N8)           /* TT2 BEGIN
*   2 REDEFINE DATE-OF-BIRTH
*     3 DOB-YYYY          (N4)
*     3 DOB-MM            (N2)
*     3 DOB-DD            (N2)           /* TT2 END
  2 ONLINE-ACTION-GN
  2 ONLINE-ACTION        (9)
  2 NUMBER-MEMBERSHIPS
  2 PAID-TO-DATE       (N8/9)
  2 REDEFINE PAID-TO-DATE
    3 PD-TO-OCC     (9)
      4 PT-YYYY    (N4)
      4 PT-MM      (N2)
      4 PT-DD      (N2)
  2 PAID-UP-DATE       (N8/9)
  2 ISSUE-DATE          (N8/9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC     (9)
      4 ID-YYYY    (N4)
      4 ID-MM      (N2)
      4 ID-DD      (N2)
  2 ISSUE-AGE           (9)
  2 REDEFINE ISSUE-AGE
    3 ISS-AGE-OCC        (9)
      4 ISS-AGE      (N2)
  2 PAY-STATUS          (9)
  2 NUM-MBRSHIPS        (9)
  2 ANNUAL-PREM         (9)
  2 FACE-AMOUNT         (9)
  2 DEATH-BENEFIT       (9)
  2 ORIG-COST-BASIS     (9)
  2 COST-BASIS          (9)
  2 CASH-VALUE          (9)
  2 SMOKER-FLAG         (9)
  2 DURATION            (9)
  2 PLAN                (9)
  2 REDEFINE PLAN
    3 PLAN-OCC  (9)
      4 PLAN-A1      (A1)
      4 PLAN-A2      (A2)
  2 STATUS              (9)
  2 MIN-CASH-VALUE      (9)
  2 NXT-YR-MIN-CASH-VALUE (9)
  2 TOT-PREMIUMS-PAID   (9)
  2 CONV-DURATION       (9)
  2 MODE                (9)
  2 MONTH-ALLOC-AMT     (9)
  2 BEG-CASH-VALUE      (9)
  2 LAST-ANN-CASH-VALUE (9)
  2 RID-PLAN            (9)
  2 MEC-IND             (9)
*
* * * *   F I R S       F I L E   * * * * * * * * * * * * * * *
1 FR-V VIEW OF A-FIRS
  2 POL-DATE     (9)
  2 POL-AMT-PAID (9)
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* DW3
  2 #CNT              (P5)
  2 #CNT-1            (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #CNT-4            (P5)
  2 #CNT-5            (P5)
  2 #CNT-6            (P5)
  2 #CNT-7            (P5)
  2 #CNT-8            (P5)
  2 #CNT-9            (P5)
  2 #CNT-10           (P5)
  2 #CNT-11           (P5)
  2 #CNT-12           (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #TOT-LOANS        (P7.2)
*
1 #DATN     (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY (N4)
  2 #DATN-MMDD (N4)
*
1 #YYYYMMDD     (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM      (N2)
  2 #DD      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM  (N6)
*
1 #OBVIOUS-MEC   (L)
1 #UPDATE-ST     (L)
1 #DISP-10    (A10)
1 #MEC-FILE   (A80)
1 #DATE-OF-BIRTH          (N8.0)            /* TT1
*
* * * * * * * * * * * * * * * * * * * * * * *
END-DEFINE
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Program Messages'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Policies currently exceeding MEC test'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Curr MEC pols not flagged as MEC but will be marked as MEC'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=150 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  '7Pay pols paying A/S/Q'
  5X 'DATE:' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=150 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Check if extra prems paid made pol become MEC'
  5X 'DATE:' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (6) LS=150 PS=76
WRITE  (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'FLEX PLANS ' 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (8) LS=133 PS=76
WRITE  (8) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Is a MEC but should not be'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (9) LS=133 PS=76
WRITE  (9) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'HAS Cost Basis/1035/ or Lump Pmt'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (10) LS=133 PS=76
WRITE  (10) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'DB Cost Basis exceeds Orig Cost Basis (loan has occured)'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (11) LS=133 PS=76
WRITE  (11) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Xtra premiums have been paid'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (12) LS=133 PS=76
WRITE  (12) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Prior to 19880602 PAID-UPS with missing TOT-PREMIUMS-PAID'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATN
READ ST-V BY MEMBER-KEY FROM '001900'  /*8 was 001900
*  IF ST-V.PROCESS-IND = 'S' OR = 'C'   /* bypass AFSC records  RE1
  IF ST-V.PROCESS-IND = 'C'                                  /* RE1
      OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)                    /* RE1
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
*  if st-v.id-number gt 24000      /* <<<<<<<<<<<<<<<<<<<<<<<
*    escape bottom
*  end-if
*
  RESET #UPDATE-ST
  ADD 1 TO #RECS-READ
*
  FOR #I1 1 TO 9
    IF STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    IF  ((ST-V.ISSUE-DATE (#I1) LE 19880620) /* non mecable pols
        AND (PAY-STATUS (#I1) = '48' OR = '47')
        AND (TOT-PREMIUMS-PAID (#I1) = 0))
      ADD 1 TO #CNT-12
      DISPLAY (12) #CNT-12
        'CN' ST-V.ID-NUMBER
        'Iss/Date' ISSUE-DATE (#I1)
        'Paid-up/Date' PAID-UP-DATE (#I1)
        'PAY/STAT' PAY-STATUS (#I1)
        'Face'     FACE-AMOUNT (#I1)
    END-IF
*
    IF   (ST-V.ISSUE-DATE (#I1) GT 19880620) /*only process mecable pols
        AND (ST-V.PAY-STATUS (#I1) NE '45')
*    AND (NOT ST-V.PLAN (#I1) = 'LT' OR = 'RT' OR = 'SO' or = 'GPL')  /* DW4 YK1
*    AND ((ST-V.PLAN (#I1) LT 'U') OR (ST-V.PLAN (#I1) GT 'Z'))       /* YK1
        AND (NOT ST-V.PLAN (#I1) = 'SO' OR = 'GPL' OR= 'GP2')         /* YK1 NG1
        AND (NOT FUNCTION-TERM-POLICY(<ST-V.PLAN(#I1)>))              /* YK1
      IGNORE
    ELSE
      ESCAPE TOP
    END-IF
*
    RESET #OBVIOUS-MEC
*    MOVE TRUE TO #UPDATE-ST /* <<<<<<< only do this the 1st time
*    RESET MEC-IND (#I1)     /* <<<<<<< only do this the 1st time  /* DW99
    IF  (PAY-STATUS (#I1) = '47')         /* obvious mecs
        OR ((PAY-STATUS (#I1) = '48')         /* PUAP within 7 years
        AND (PAID-UP-DATE (#I1) - ISSUE-DATE (#I1) LT 70000))
        OR ((PLAN-A2 (#I1) = 'PF') AND (PLAN-A1 (#I1) LT '7'))
      MOVE TRUE TO #OBVIOUS-MEC
    END-IF
*
    PERFORM CHECK-MEC-LIMIT    /* <<<<<<<<<<<<<<<<<<<<<<<<<
*
    IF  (#EXCEEDS-MEC NE 0) AND (MEC-IND (#I1) = ' ')
        AND (ISSUE-DATE (#I1) LT (*DATN - 10000))
      WRITE ST-V.ID-NUMBER ISSUE-DATE (#I1) PAY-STATUS (#I1) PLAN (#I1)
        'PD-TO' PAID-TO-DATE (#I1)
        'S/B MEC but Bypassed-over 1 yr old'
      RESET #EXCEEDS-MEC
    END-IF
*
    IF   (PAY-STATUS (#I1) LT '40')     /* must be xtra prems-paid
        AND (TOT-PREMIUMS-PAID (#I1) NE 0)
        AND ((#1035-EXCH-AMT + #LUMP-PMT) NE TOT-PREMIUMS-PAID (#I1))
      ADD 1 TO #CNT-11
      DISPLAY (11) #CNT-11
        'CN' ST-V.ID-NUMBER
        'Iss/Date' ISSUE-DATE (#I1)
        '1035'     #1035-EXCH-AMT
        'Lump/Pmt' #LUMP-PMT
        'VW-TPP'   TOT-PREMIUMS-PAID (#I1)
    END-IF
*
    IF  (COST-BASIS (#I1) NE 0)
        OR (ORIG-COST-BASIS (#I1) NE 0)
        OR (#1035-EXCH-AMT NE 0)
        OR (#LUMP-PMT NE 0)
        OR (((PLAN(#I1) = MASK (.'F')) OR (PLAN(#I1) = MASK (..'F')) /*Flex
        AND (#1035-EXCH-AMT = 0) OR (#LUMP-PMT = 0)))
        OR ((PAY-STATUS (#I1) = '48' OR = '47')  /* missing VW TPP
        AND (TOT-PREMIUMS-PAID (#I1) = 0))
      RESET #ERR-MSG
*
      IF (((ORIG-COST-BASIS (#I1) NE 0) OR (COST-BASIS (#I1) NE 0))
          AND (TOT-PREMIUMS-PAID (#I1)= 0))
          OR ((PAY-STATUS (#I1) = '48' OR = '47')
          AND (TOT-PREMIUMS-PAID (#I1) = 0))
        COMPRESS #ERR-MSG '-missing VW TPP' TO #ERR-MSG
        WRITE ST-V.ID-NUMBER ISSUE-DATE (#I1) PLAN (#I1)
          '1035' #1035-EXCH-AMT
          'Lump' #LUMP-PMT
          #ERR-MSG
      END-IF
*
      IF ((PLAN(#I1) = MASK (.'F')) OR (PLAN(#I1) = MASK (..'F')) /*Flex
          AND (#1035-EXCH-AMT = 0) AND (#LUMP-PMT = 0)
          AND (ORIG-COST-BASIS (#I1) NE 0))
        COMPRESS #ERR-MSG '-missing 1035/Lump NX' TO #ERR-MSG
        WRITE ST-V.ID-NUMBER ISSUE-DATE (#I1) PLAN (#I1)
          'VW/TPP'  TOT-PREMIUMS-PAID (#I1) #ERR-MSG
      END-IF
*
      ADD 1 TO #CNT-9
      DISPLAY (9) #CNT-9
        'CN'       ST-V.ID-NUMBER
        'Iss/Date' ISSUE-DATE (#I1)
        'Pay/Stat' PAY-STATUS (#I1)
        'Pln'      PLAN (#I1)
        'Orig/CB'  ORIG-COST-BASIS (#I1)
        'Curr/CB'  COST-BASIS (#I1)
        '1035/exch' #1035-EXCH-AMT
        'Lump/Pmt' #LUMP-PMT
        'VW/TPP'  TOT-PREMIUMS-PAID (#I1)
        'TPP'     #PREMS-PAID
*       'CSV'     CASH-VALUE (#I1)
        'Exceptions' #ERR-MSG
*
      IF COST-BASIS (#I1) GT ORIG-COST-BASIS (#I1)
        ADD 1 TO #CNT-10
        DISPLAY (10) #CNT-10 'CN' ST-V.ID-NUMBER
          'Iss/Date' ISSUE-DATE (#I1)
          'Pay/Stat' PAY-STATUS (#I1)
          'Orig/CB' ORIG-COST-BASIS (#I1)
          'Curr/CB' COST-BASIS (#I1)
          'TPP'     #PREMS-PAID
          'VW/TPP'  TOT-PREMIUMS-PAID (#I1)
          'CSV'     CASH-VALUE (#I1)
          '1035/exch' #1035-EXCH-AMT
          'Lump/Pmt' #LUMP-PMT
      END-IF
    END-IF
*
    IF (MEC-IND (#I1) = 'Y') AND (#EXCEEDS-MEC = 0)
      ADD 1 TO #CNT-8
      DISPLAY (8) #CNT-8 (EM=ZZ9)    /* IS a MEC but should not be
        'CN' ST-V.ID-NUMBER
        'ISS-dt'         ISSUE-DATE (#I1)
        'FAC'            FACE-AMOUNT (#I1) (EM=ZZZ,ZZ9)
        'PD-TO/Date'     PAID-TO-DATE (#I1)
        'PL'             PLAN (#I1)
        'Pa/St'          PAY-STATUS(#I1)
        'Iss/Age'        ISSUE-AGE  (#I1)
        'DR'             DURATION     (#I1)
        'ANN/PREM'       ANNUAL-PREM  (#I1)
        'VW/TPP'         TOT-PREMIUMS-PAID (#I1)
        'MEC Tot/PREMS/PAID' #MEC-PREMS-PAID-TO-DATE
        'MAX MEC/Amt'    #MAX-7-PAY
        'SM'             SMOKER-FLAG  (#I1)
        'Total/loans'    #TOT-LOANS (EM=ZZZ,ZZZ.99)
*
      IF #1035-NX NE ' '
        WRITE (8) ST-V.ID-NUMBER #1035-NX
      END-IF
      ADD 1 TO #OBV-MEC-BUT-NOT
    END-IF
*
    IF (MEC-IND (#I1) NE 'Y') AND (#EXCEEDS-MEC NE 0)
      ADD 1 TO #CNT-3
      DISPLAY (3) #CNT-3 (EM=ZZ9)   /* Is not a MEC but should be
        'CN' ST-V.ID-NUMBER
        'ISS-dt'         ISSUE-DATE (#I1)
        'PD-TO/Date'     PAID-TO-DATE (#I1)
        'PL'             PLAN (#I1)
        'Pa/St'          PAY-STATUS(#I1)
        'ANN/PREM'       ANNUAL-PREM  (#I1)
        'MD'             MODE (#I1)
        'VW/TPP'         TOT-PREMIUMS-PAID (#I1)  (EM=ZZZZZ.99)
        'MEC Tot/PREMS/PAID' #MEC-PREMS-PAID-TO-DATE
        'MAX MEC/Amt'    #MAX-7-PAY
        'Exceeds/MEC by' #EXCEEDS-MEC
        'SM'             SMOKER-FLAG  (#I1)
        'FAC'            FACE-AMOUNT (#I1) (EM=ZZZ,ZZ9)
        'Iss/Age'        ISSUE-AGE  (#I1)
        'DR'             DURATION     (#I1)
        'Total/loans'    #TOT-LOANS (EM=ZZZ,ZZZ.99)
*
      IF #1035-NX NE ' '
        WRITE (7) ST-V.ID-NUMBER #1035-NX
      END-IF
    END-IF
*
    IF NOT #OBVIOUS-MEC AND (#EXCEEDS-MEC NE 0)
      IF   (TOT-PREMIUMS-PAID (#I1) GT 0)
          AND (#1035-NX = ' ')
        ADD 1 TO #CNT-5
        DISPLAY (5) #CNT-5
          'CN' ST-V.ID-NUMBER
          'ISS-dt'         ISSUE-DATE (#I1)
          'PD-TO/Date'     PAID-TO-DATE (#I1)
          'PL'             PLAN (#I1)
          'Pa/St'          PAY-STATUS(#I1)
          'ANN/PREM'       ANNUAL-PREM  (#I1)
          'VW/TPP'         TOT-PREMIUMS-PAID (#I1)
          'MEC Tot/PREMS/PAID' #MEC-PREMS-PAID-TO-DATE
          'MAX MEC/Amt'    #MAX-7-PAY
          'Exceeds/MEC by' #EXCEEDS-MEC
          'SM'             SMOKER-FLAG  (#I1)
          'FAC'            FACE-AMOUNT (#I1) (EM=ZZZ,ZZ9)
          'Iss/Age'        ISSUE-AGE  (#I1)
          'DR'             DURATION     (#I1)
          'Total/loans'    #TOT-LOANS (EM=ZZZ,ZZZ.99)
      END-IF
*
      ADD 1 TO #OBV-MEC-BUT-NOT
*
      COMPRESS ST-V.ID-NUMBER
        #TAB ISSUE-DATE (#I1)
        #TAB PLAN (#I1)
        #TAB SMOKER-FLAG (#I1)
        #TAB ISSUE-AGE (#I1)
        INTO #MEC-FILE LEAVING NO
*
      MOVE EDITED ST-V.FACE-AMOUNT (#I1) (EM=ZZZZZZ.99) TO #DISP-10
      MOVE LEFT #DISP-10 TO #DISP-10
      COMPRESS #MEC-FILE
        #TAB #DISP-10
        INTO #MEC-FILE LEAVING NO
*
      MOVE EDITED ST-V.ANNUAL-PREM (#I1) (EM=ZZZZZZ.99) TO #DISP-10
      MOVE LEFT #DISP-10 TO #DISP-10
      COMPRESS #MEC-FILE
        #TAB #DISP-10
        INTO #MEC-FILE LEAVING NO
*
      MOVE EDITED TOT-PREMIUMS-PAID (#I1) (EM=-ZZZZZZ.99) TO #DISP-10
      MOVE LEFT #DISP-10 TO #DISP-10
      COMPRESS #MEC-FILE
        #TAB #DISP-10
        INTO #MEC-FILE LEAVING NO
*
      MOVE EDITED #MAX-7-PAY (EM=ZZZZZZ.99) TO #DISP-10
      MOVE LEFT #DISP-10 TO #DISP-10
      COMPRESS #MEC-FILE
        #TAB #DISP-10
        INTO #MEC-FILE LEAVING NO
*
      MOVE EDITED #EXCEEDS-MEC (EM=ZZZZZZ.99) TO #DISP-10
      MOVE LEFT #DISP-10 TO #DISP-10
      COMPRESS #MEC-FILE
        #TAB #DISP-10
        INTO #MEC-FILE LEAVING NO
*
*     WRITE WORK FILE 1 #MEC-FILE
    END-IF
*
    IF #EXCEEDS-MEC NE 0    /* to SET MEC indicator
      MOVE TRUE TO #UPDATE-ST
      MOVE 'Y' TO MEC-IND (#I1)     /* <<<<<<<<<<<<<<<<<<<<<<<
      COMPUTE #TOT-LOANS = X-LOAN-AMOUNT (#I1) + X-LOAN-AMOUNT-2 (#I1)
      ADD 1 TO #CNT-2
      DISPLAY (2) #CNT-2  (EM=ZZZ9)
        'CN' ST-V.ID-NUMBER
        'ISS-dt'         ISSUE-DATE (#I1)
        'PD-TO/Date'     PAID-TO-DATE (#I1)
        'PL'             PLAN (#I1)
        'Pa/St'          PAY-STATUS(#I1)
        'ANN/PREM'       ANNUAL-PREM  (#I1)
        'VW/TPP'         TOT-PREMIUMS-PAID (#I1)
        'MEC Tot/PREMS/PAID' #MEC-PREMS-PAID-TO-DATE
        'MAX MEC/Amt'    #MAX-7-PAY
        'Exceeds/MEC by' #EXCEEDS-MEC
        'SM'             SMOKER-FLAG  (#I1)
        'FAC'            FACE-AMOUNT (#I1) (EM=ZZZ,ZZ9)
        'Iss/Age'        ISSUE-AGE  (#I1)
        'DR'             DURATION     (#I1)
        'Total/loans'    #TOT-LOANS (EM=ZZZ,ZZZ.99)
*
      IF #1035-NX NE ' '
        WRITE (2) ST-V.ID-NUMBER #1035-NX
      END-IF
    END-IF
*
    RESET #PD-TO-MM (*)
    MOVE ID-MM (#I1) TO #PD-TO-MM (1)
    FOR #I2 2 TO 4
      IF  ((MODE (#I1) = 'A' OR = '1') AND (#I2 = 2))    /* DW1
          OR ((MODE (#I1) = 'S' OR = '2') AND (#I2 = 3))
        ESCAPE BOTTOM
      END-IF
*
      COMPUTE #PD-TO-MM (#I2) = #PD-TO-MM (#I2 - 1) + 3
      IF #PD-TO-MM (#I2) GT 12
        SUBTRACT 12 FROM #PD-TO-MM (#I2)
      END-IF
    END-FOR
*
    RESET #MATCHING-PD-TO
    FOR #I2 1 TO 4
      IF PT-MM (#I1) = #PD-TO-MM (#I2)
        MOVE TRUE TO #MATCHING-PD-TO
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF (PLAN (#I1) = MASK (N'P')) AND (PLAN-A1 (#I1) LE '7')  /* DRW
        AND (MODE (#I1) = 'A' OR = 'S' OR = 'Q' OR = '1' OR = '2' OR = '4')
        AND (PAY-STATUS (#I1) LT '40')
        AND (NOT #MATCHING-PD-TO)
*
      ADD 1 TO #CNT-4
      DISPLAY (4) #CNT-4 'CN' ST-V.ID-NUMBER
        'Issue/Date'   ISSUE-DATE (#I1)
        'Paid-to/Date' PAID-TO-DATE (#I1)
        'PLN'          PLAN (#I1)
        'MD'           MODE (#I1)
        'Iss/Age'      ISSUE-AGE  (#I1)
        'Ann/Prem'     ANNUAL-PREM (#I1)
        'Exceeds/MEC by' #EXCEEDS-MEC
        'Face'         FACE-AMOUNT(#I1)
        'TPP'          TOT-PREMIUMS-PAID (#I1)
        'SM'           SMOKER-FLAG (#I1)
        'Pa/ST'        PAY-STATUS (#I1)
*
      FOR #I2 1 TO 9
        IF (ISSUE-DATE (#I1) = ISSUE-DATE (#I2))
            AND (PLAN     (#I1) = PLAN       (#I2))
          ESCAPE TOP
        END-IF
*
        IF  (PAY-STATUS (#I2) GT '40')
            OR (NOT MODE (#I2) = 'A' OR = 'S' OR = 'Q'
            OR = '1' OR = '2' OR = '4')    /* DW1
          ESCAPE TOP
        END-IF
*
        WRITE (4) '              ' ISSUE-DATE (#I2)
          PAID-TO-DATE (#I2) PLAN (#I2) MODE (#I2)
          '     other A/S/Q policies'
      END-FOR
    END-IF
*
    IF  (PLAN (#I1) = MASK (..'F'))
        OR (PLAN (#I1) = MASK (.'F'))
      ADD 1 TO #CNT-6
      DISPLAY (6) #CNT-6 (EM=ZZ9)
        'CN' ST-V.ID-NUMBER
        'Issue/Date' ISSUE-DATE (#I1)
        'PA/ST' PAY-STATUS (#I1) 'PLN' PLAN (#I1)
        'ISS/AGE' ISSUE-AGE (#I1)
        'MD' MODE (#I1)
        'FACE/AMOUNT' FACE-AMOUNT (#I1)
        'TOT-PREMS/PAID' TOT-PREMIUMS-PAID (#I1)
*        'DB-CR/AMT' DEBIT-CREDIT-AMT
        'COST/BASIS' COST-BASIS (#I1)
*        #1035-ADJ
*       '1035-Exch/Amt' #1035-EXCH-AMT (EM=ZZ,ZZ9)
*       'LUMPPMT' #LUMP-PMT (EM=ZZ,ZZ9)
*       'PP-Prems/Paid' #PP-PREMS-PAID (EM=ZZ,ZZZ.99)
*        '1035-Prms/To Date' #1035-PRMS-TO-DATE (EM=ZZ,ZZZ.99)
        'Max/7-Pay' #MAX-7-PAY (EM=ZZZ,ZZZ.99)
        'Exceeds/MEC by' #EXCEEDS-MEC (EM=ZZZ,ZZZ.99)
*
      IF #EXCEEDS-MEC NE 0
        WRITE (6) '   Exceeds MEC by' #EXCEEDS-MEC
      END-IF
*
      IF #1035-NX NE ' '
        WRITE (6) '   ' ST-V.ID-NUMBER #1035-NX
      END-IF
      WRITE (6) ' '
    END-IF
  END-FOR
*
  IF #UPDATE-ST
    UPDATE (0235)
  END-IF
*
  END TRANSACTION
*
END-READ
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-MEC-LIMIT
* * * * * * * * * * * * * * * * * *
*
RESET  #MAX-7-PAY
  #MEC-PREMS-PAID-TO-DATE
  #EXCEEDS-MEC
  #1035-NX
*
* IF   (PLAN (#I1) = 'RT' OR = 'LT' OR = 'SO' or = 'GPL')  /* DW4 YK1
* OR ((PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z'))   /* YK1
IF (PLAN (#I1) = 'SO' OR = 'GPL')                          /* YK1
    OR  FUNCTION-TERM-POLICY(<PLAN(#I1)>)                  /* YK1
  ESCAPE ROUTINE
END-IF
*
RESET #PREMS-PAID #DATE-OF-BIRTH                                    /* TT1
#DATE-OF-BIRTH   := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)     /* TT1
*
MOVE #DATE-OF-BIRTH          TO #PP-DATE-OF-BIRTH                  /* TT1
MOVE ANNUAL-PREM (#I1)       TO #PP-ANNUAL-PREM
MOVE PAY-STATUS (#I1)        TO #PP-PAY-STATUS
MOVE TOT-PREMIUMS-PAID (#I1) TO #PP-TOT-PREMIUMS-PAID
MOVE PAID-TO-DATE (#I1)      TO #PP-PAID-TO-DATE
MOVE ISSUE-DATE (#I1)        TO #PP-ISSUE-DATE
MOVE MODE (#I1)              TO #PP-MODE
MOVE PLAN (#I1)              TO #PP-PLAN
MOVE COST-BASIS (#I1)        TO #PP-COST-BASIS
MOVE MONTH-ALLOC-AMT (#I1)   TO #PP-MONTH-ALLOC-AMT
MOVE INTRO-FLAG              TO #PP-INTRO-FLAG
MOVE X-FIRST-PLACE-FEE (#I1) TO #PP-FIRST-PLACE-FEE
MOVE X-BILL-ADJUST     (#I1) TO #PP-BADJ
*
CALLNAT 'GPRPDSCO' /* get #prems-paid
  #PP-DATE-OF-BIRTH
  #PP-ANNUAL-PREM
  #PP-PAY-STATUS
  #PP-TOT-PREMIUMS-PAID
  #PP-PAID-TO-DATE
  #PP-ISSUE-DATE
  #PP-MODE
  #PP-PLAN
  #PP-COST-BASIS
  #PP-MONTH-ALLOC-AMT
  #PP-INTRO-FLAG
  #PP-FIRST-PLACE-FEE
  #PREMS-PAID
  #SUB-PREMS-PAID
  #PP-BADJ
ADD #PP-FIRST-PLACE-FEE TO #PREMS-PAID /* assume mbr will pay fpf
* ADD #PP-BADJ   TO #PREMS-PAID /* and bill adjust /* REmoved 12/7/2004
RESET #SUFFIX
CALLNAT 'GMECCSXX' ST-V.ID-NUMBER
  #SUFFIX
  ST-V.PLAN          (#I1)
  ST-V.ISSUE-DATE    (#I1)
  ST-V.PAID-TO-DATE  (#I1)
  ST-V.PAID-UP-DATE  (#I1)
  ST-V.ISSUE-AGE     (#I1)
  ST-V.ANNUAL-PREM   (#I1)
  ST-V.SMOKER-FLAG   (#I1)
  ST-V.FACE-AMOUNT   (#I1)
  ST-V.DURATION      (#I1)
  ST-V.PAY-STATUS    (#I1)
  ST-V.TOT-PREMIUMS-PAID (#I1)
  #PREMS-PAID      /* calc tpp
*
  #MAX-7-PAY
  #MEC-PREMS-PAID-TO-DATE
  #EXCEEDS-MEC
  #1035-NX
  #1035-EXCH-AMT
  #LUMP-PMT
*
END-SUBROUTINE
*
END
