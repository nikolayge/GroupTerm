* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID GOCOF002   Defaul Prefix
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20110616   None    Program written.                            *
*                                                                      *
* 1. If Gender CD (Contact) is "F". and Intro Flag (Status) is "S",
*    then Prefix (Contact) should be "MRS"
* 2. If Gender CD (Contact) is "M" then Prefix (Contact) should be "MR"
* 3. If Gender CD (Contact) is "F". and Date of Marriage (Status) is not <blank>
*    then Prefix (Contact) should be "MRS"
* 4. If Gender CD (Contact) is "F"
*    IF age (Contact) is less than 16 years then Prefix (Contact) should be "MISS"
*    ELSE then Prefix (Contact) should be "MS"
************************************************************************
*
DEFINE FUNCTION F-DEFAULT-PREFIX RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID         (N8)
  1 #GENDER-CD          (A1) OPTIONAL
  1 #DATE-OF-BIRTH      (N8) OPTIONAL
  LOCAL
  1 CNT-V VIEW OF A-CONTACTS
    2 ID-NUMBER
    2 CONTACT-TYPE
    2 DATE-OF-BIRTH
    2 GENDER-CD
    2 LAST-NAME
  1 SPOUSE VIEW OF A-CONTACTS
    2 LAST-NAME
*
  1 CNT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
    2 CONTACT-1-ID
    2 ROLE-1-CD
    2 CONTACT-2-ID
    2 ROLE-2-CD
*
  1 STATUS VIEW OF A-STATUS
    2 INTRO-FLAG  (A1)
    2 DATE-OF-MARRIAGE (N8)
  1 #AGE (N8)
  1 #16-YEARS-OLD (N6) CONSTANT<160000>
  1 #SPOUSE-CI    (N8)
  1 #XSPOUSE-CI   (N8)
  END-DEFINE
*
RESET F-DEFAULT-PREFIX
FIND (1) CNT-V WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    IF #GENDER-CD SPECIFIED
      GENDER-CD := #GENDER-CD
    END-IF
    IF #DATE-OF-BIRTH SPECIFIED
      DATE-OF-BIRTH := #DATE-OF-BIRTH
    END-IF
    ESCAPE BOTTOM
  END-NOREC
END-FIND
IF CONTACT-TYPE = 'O'
  ESCAPE ROUTINE
END-IF
DECIDE ON FIRST VALUE OF GENDER-CD
  VALUE 'M'
    F-DEFAULT-PREFIX := 'MR'
  VALUE 'F'
    IF DATE-OF-BIRTH > 0
      #AGE := *DATN - DATE-OF-BIRTH
    ELSE
      RESET #AGE
    END-IF
    #SPOUSE-CI := FUNCTION-SPOUSE-CI (< #CONTACT-ID >)
    IF #SPOUSE-CI = 0
      #XSPOUSE-CI := FUNCTION-XSPOUSE-CI (< #CONTACT-ID >)
    END-IF
    DECIDE FOR FIRST CONDITION
      WHEN #SPOUSE-CI > 0
        FIND (1) SPOUSE WITH CONTACT-ID = #SPOUSE-CI
        END-FIND
        IF SPOUSE.LAST-NAME = CNT-V.LAST-NAME
          F-DEFAULT-PREFIX := 'MRS'
        ELSE
          F-DEFAULT-PREFIX := 'MS'
        END-IF
      WHEN #XSPOUSE-CI > 0
        F-DEFAULT-PREFIX := 'MS'
      WHEN #AGE > 0 AND #AGE < #16-YEARS-OLD
        F-DEFAULT-PREFIX := 'MISS'
      WHEN NONE
        F-DEFAULT-PREFIX := 'MS'
    END-DECIDE
  NONE VALUE IGNORE
END-DECIDE
*
END-FUNCTION
END
