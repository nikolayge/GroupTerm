* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : C2150NPO                                              *
* DESCRIPTION  : Get LTII re-new rates
************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG     REASON
* NGG    20121025         Program Written
************************************************************************
*
DEFINE DATA
PARAMETER
1 #FACE-AMOUNT       (P7.2)
1 #AGE               (N2)
1 #VT-TERM           (N2)
1 #EXTENT            (N2)
1 #CurrentQuote      (N5.2/1:V)
1 #GuaranteedQuote   (N5.2/1:V)
1 #AgeQuote          (N2/1:V) OPTIONAL
LOCAL
1 #GUARANTEED (N3.2/0:99)
1 #CURRENT    (N3.2/0:99)
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
1 LTII-V VIEW OF A-VAR-TERM-RATES
  2 VAR-TERM-RATE (P3.2/100)
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 LTII-TYPE (A2)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 RIDER-OPTION (A1)
  2 LTII-KEY     (A14)
1 J  (I2)
1 K  (I2)
1 L  (I2)
1 #R (N3.4)
END-DEFINE
*
IF #AGE < 0
  RESET #AGE
END-IF
*
RESET #CurrentQuote (*) #GuaranteedQuote (*)
*
L := #AGE + #VT-TERM
K := 100 - L
IF K > #EXTENT
  K := #EXTENT
END-IF
J := K - 1
*
PERFORM GET-RATES
#CurrentQuote (1:K) := #CURRENT ( L:L + J )
#GuaranteedQuote (1:K) := #GUARANTEED ( L:L + J )
*
IF #AgeQuote SPECIFIED
  #AgeQuote (1) := L
END-IF
#R := #FACE-AMOUNT / 10000
FOR J 1 TO K
  #CurrentQuote (J) := #CurrentQuote (J) * #R
  #GuaranteedQuote (J) := #GuaranteedQuote (J) * #R
END-FOR
IF #AgeQuote SPECIFIED
  SUBTRACT 1 FROM K
  FOR J 0 TO K
    #AgeQuote (J + 1) := L + J
  END-FOR
END-IF
*
DEFINE GET-RATES
/*     ---------
##GENDER    := 'X'
##LTII-TYPE := 'CR'   /* CURRENT KEY=XCRX99
##NICOTINE  := 'X'
##AGE       := 99
MOVE EDITED *DATX (EM=99999999) TO ##EFFECTIVE-DATE
READ LTII-V DESCENDING BY LTII-KEY STARTING FROM #LTII-KEY
  IF SUBSTR(LTII-KEY,1,6) NE SUBSTR(#LTII-KEY,1,6)
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
    ESCAPE TOP
  END-IF
  #CURRENT (0:99) := VAR-TERM-RATE ( 1:100 )
  ESCAPE BOTTOM
END-READ
##LTII-TYPE := 'GN'   /* CURRENT KEY=XCRX99
READ LTII-V DESCENDING BY LTII-KEY STARTING FROM #LTII-KEY
  IF SUBSTR(LTII-KEY,1,6) NE SUBSTR(#LTII-KEY,1,6)
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
    ESCAPE TOP
  END-IF
  #GUARANTEED (0:99) := VAR-TERM-RATE ( 1:100 )
  ESCAPE BOTTOM
END-READ
END-SUBROUTINE
*
END
