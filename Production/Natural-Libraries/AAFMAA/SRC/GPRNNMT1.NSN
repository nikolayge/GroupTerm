* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GPRNNMT1    *** Easy (No Medical) Terms Rates ***
/** Returns a single rate based on parameters passed
/** :author nguentchev
/* Subprogram GPRNNMT1
* Your initial premium is based on your age at issue; premiums increase as you enter each new
* five-year age band.  Premiums are not guaranteed.  However, your rates will only change if
* they are changed for all others in the same class of insureds under this group policy.
* A group of insureds with the same issue age and gender is an example of a class.
* The issue age is in interval 45-74. Face amounts are 10000,20000,30000,40000,50000.
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* NGG   20130325   NG1   Change EZ Term rates                          *
* NGG   20130708   NG2   Change PO command                             *
* JNT   20131030   JT1   1M Policy support                             *                                                                     *
************************************************************************ 
*
DEFINE DATA
PARAMETER
1 #AGE             (N2)
1 #GENDER          (A1)
1 #FACE-AMOUNT     (N7.2)
1 #APREM           (N7.2)                            /* JT1
LOCAL
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
1 LTII-V VIEW OF A-PRODUCT-RATES
  2 VAR-TERM-RATE    (N3.2/10)
  2 EFFECTIVE-DATE   (A8)
  2 GENDER           (A1)
  2 LTII-TYPE        (A2)
  2 NICOTINE         (A1)
  2 AGE              (A2)
  2 RIDER-OPTION     (A1)
  2 LTII-KEY         (A14)
*
1 FA-RANGE    (N6/7) INIT < 10000,12500,25000,37500,50000,75000,100000 >    /* NG1
1 J      (I2)
1 K      (I2)
END-DEFINE
*
IF #AGE < 45 OR #AGE > 79
  ESCAPE ROUTINE
END-IF
*
FOR K 1 TO 7
  IF #FACE-AMOUNT = FA-RANGE (K)
    ESCAPE BOTTOM
  END-IF
END-FOR
IF K <= 7                                                                  /* NG1
  ##GENDER    := #GENDER
  ##LTII-TYPE := 'EZ'   /* CURRENT KEY=XCRX99
  ##NICOTINE  := 'X'
  ##AGE       := #AGE
  MOVE EDITED *DATX (EM=99999999) TO ##EFFECTIVE-DATE
  READ LTII-V DESCENDING BY LTII-KEY STARTING FROM #LTII-KEY
    IF LTII-TYPE NE ##LTII-TYPE
      ESCAPE BOTTOM
    END-IF
    IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
      ESCAPE TOP
    END-IF
    #APREM := VAR-TERM-RATE ( K )
    ESCAPE BOTTOM
  END-READ
  COMPUTE #APREM = #APREM * 12
ELSE                             /* NG2
  RESET #APREM
END-IF
*
END
