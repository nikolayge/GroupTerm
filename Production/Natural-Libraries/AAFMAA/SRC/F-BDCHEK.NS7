* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-BDCHEK                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Check beneficiary for contact ID notification         *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG   REASON                                        *
* PAM  04/13/2010  None  Program Written                               *
* NGG  20121010    NG1   CN Expansion Project - beneficiary data are   *
*                        moved to new database files                   *
************************************************************************
*
DEFINE FUNCTION FUNCTION-BD-LINKS RETURNS (N6/100)
*
  DEFINE DATA
*
  PARAMETER
*
  1 #CONTACT-ID              (N8)
*
  LOCAL
*
  1 CR-V VIEW OF A-CONTACT-RELATIONSHIPS
    2 CONTACT-1-ID
    2 ROLE-1-CD
    2 CONTACT-2-ID
    2 ROLE-2-CD
*
  1 CT-V VIEW OF A-CONTACTS
    2 CONTACT-ID
    2 ID-NUMBER
*                                              NG1 START
  1 BENE-D-V VIEW OF A-BENE-DRAFT                       
    2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
    2 BENE-CONTACT-ID         (N8/60)
    2 REDEFINE BENE-CONTACT-ID
      3 BENE-CONTACT-6-POLS    (6)
        4 BENE-CONTACT-PROM-SEC (2)
          5 BENE-CONTACT-OCC     (5)
            6 BENE-CONTACT-ID-R   (N8)
*
  1 BENE-F-V VIEW OF A-BENE-FINAL
    2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
    2 BENE-CONTACT-ID         (N8/60)
    2 REDEFINE BENE-CONTACT-ID
      3 BENE-CONTACT-6-POLS    (6)
        4 BENE-CONTACT-PROM-SEC (2)
          5 BENE-CONTACT-OCC     (5)
            6 BENE-CONTACT-ID-R   (N8)
*                                              NG1 END
  1 #MEMBER-TABLE            (100)
    2 #MEMBER-ID              (N6)
*
  1 #INDEXES
    2 #MEMBER-I               (I4)
    2 #NOTE-I                 (I2)
    2 #I1                     (I2)
    2 #I2                     (I2)
    2 #I3                     (I2)
  1 #WORK-ID                 (N6)
*
  END-DEFINE
*
************************************************************************
*
RESET FUNCTION-BD-LINKS (*)
*
FIND CR-V WITH CR-V.CONTACT-2-ID = #CONTACT-ID
  FIND (1) CT-V WITH CT-V.CONTACT-ID = CR-V.CONTACT-1-ID
    #WORK-ID := CT-V.ID-NUMBER
    PERFORM FIND-BD
  END-FIND
END-FIND
*
FUNCTION-BD-LINKS (*) := #MEMBER-ID (*)
*
***************       NG1      *****************************************
DEFINE SUBROUTINE FIND-BD
************************************************************************
FIND_L1.
FIND (1) BENE-D-V WITH ID-NUMBER = #WORK-ID
  FOR #I1 1 TO 6
    FOR #I2 1 TO 2
      FOR #I3 1 TO 5
        IF BENE-D-V.BENE-CONTACT-ID-R (#I1,#I2,#I3) = #CONTACT-ID
          ADD +1 TO #MEMBER-I
          #MEMBER-ID (#MEMBER-I) := #WORK-ID
          ESCAPE BOTTOM (FIND_L1.)
        END-IF
      END-FOR
    END-FOR
  END-FOR
END-FIND
FIND_L2.
FIND (1) BENE-F-V WITH ID-NUMBER = #WORK-ID
  FOR #I1 1 TO 6
    FOR #I2 1 TO 2
      FOR #I3 1 TO 5
        IF BENE-F-V.BENE-CONTACT-ID-R (#I1,#I2,#I3) = #CONTACT-ID
          ADD +1 TO #MEMBER-I
          #MEMBER-ID (#MEMBER-I) := #WORK-ID
          ESCAPE BOTTOM (FIND_L2.)
        END-IF
      END-FOR
    END-FOR
  END-FOR
END-FIND
END-SUBROUTINE
*
END-FUNCTION
*
END
