* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Write Transaction Log - generic program
* Can be called from any process
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    07152009       Initial Creation
********************************************************
*
DEFINE DATA
PARAMETER
* Fields from Parameter Data Area GPRA0004
1 #TR-FIELDS
  2 #ID-NUMBER       (N6)
  2 #NAME            (A) DYNAMIC
  2 #TR-DATE-LAST-UPDATE (N8)
  2 #CLERK-ID        (A3)
  2 #LOG-DESCRIPTION (A) DYNAMIC
1 #SYNC-FIELD-TABLE  (N3/32)         /* Pointer for SYNC process (AFSC)
1 #OLD-DATA-TABLE   (32)
  2 #FIELD-DESCRIPTION (A10)
  2 #OLD-DATA          (A115)
1 #NEW-DATA-TABLE   (32)
  2 #NEW-DATA-TAG      (A3)
  2 #NEW-DATA          (A122)
LOCAL USING OBJLTRRG
LOCAL
1 #I                 (I2)
1 #FIRST-LINE        (A9)
1 #DATX              (D)
1 #DATN              (N8)
1 REDEFINE #DATN
  2 #DATE-A          (A8)
END-DEFINE
*
TR-V.ID-NUMBER            := #ID-NUMBER
TR-V.PROCESS-IND          := 'I'
TR-V.NAME                 := #NAME
TR-V.DATE-LAST-UPDATE     := #TR-DATE-LAST-UPDATE
TR-V.CLERK-ID             := #CLERK-ID
TR-V.TERMINAL-ID          := *INIT-ID
TR-V.TRANSACTION-DATN     := *DATN
TR-V.TRANSACTION-TIMN     := *TIMN
*
FOR #I = 1 TO 32
  IF #OLD-DATA(#I) = ' ' AND #NEW-DATA(#I) = ' '
    ESCAPE BOTTOM
  END-IF
  COMPRESS FULL #FIELD-DESCRIPTION(#I) #OLD-DATA(#I) INTO TR-V.OLD-DATA (#I)
    LEAVING NO
  SYNC-FLD-OLD-DATA(#I) := #OLD-DATA(#I)
  IF #I = 1
    COMPRESS #LOG-DESCRIPTION #FIELD-DESCRIPTION(1) INTO #FIRST-LINE LEAVING NO
    #FIELD-DESCRIPTION(1) := #FIRST-LINE
  END-IF
  COMPRESS FULL #FIELD-DESCRIPTION(#I) #NEW-DATA(#I) INTO TR-V.NEW-DATA (#I) LEAVING NO
  SYNC-FLD-NEW-DATA(#I) := #NEW-DATA(#I)
  SYNC-FLD-NUM  (#I)    := #SYNC-FIELD-TABLE(#I)
END-FOR
*
STORE TR-V
*
END
