* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G2100PCC    ** COMMAND COUNT **
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  04182006 DW1  Show which Company's CC stats you are viewing
* SAG  11252005  RH1   SS - Replaced call to assembler program WAASSRT1 with
*                      Natural subprogram SAGSRT11.
* DRW 03/10/2009 DW2 Stop CC display at 105 Commands until MAP is changed
* DRW 09/19/2009 DW3 AFFL Unification
* RSE 11/13/2009 RE1 Remove restrictions on displaying user info
* bz1 1/27/2013   Filter out old users not active after 2012
* vxt 02/10/2015 vt1 /Ticket 3343 - fix error 1316/
*******************************************
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING G2100LCC
LOCAL  USING GONERLXX
LOCAL
*
1 TM-V VIEW OF A-TERMINALS
  2 CLERK-ID
  2 TERMINAL-ID
  2 SECURITY-CODE
  2 LST-CMND
  2 LST-CMND-DT    (N8)
  2 REDEFINE LST-CMND-DT
    3 LC-DT-CENT    (N2)
    3 LC-DT-YY      (N2)
    3 LC-DT-MM      (N2)
    3 LC-DT-DD      (N2)
  2 LST-CMND-TM    (N7)
  2 LST-CMND-ID    (N6)
*
1 #AM-PM   (A2)
1 #COMPANY (A6)   /* DW1
*
* last command list **
1 #SCR-TIME    (A11/99)
1 #SCR-CV      (C/99)
1 #LAST-DT     (N8)
*
1 #SRT-TABLE (99) /* USED FOR SORT    /* WAS 66
  2 #SRT-DATE          (N8)
  2 REDEFINE #SRT-DATE
    3 #SRT-YYYY    (N4)
    3 #SRT-MM      (N2)
    3 #SRT-DD      (N2)
  2 #SRT-TIME          (N7)
  2 #SRT-USER          (A3)
  2 #SRT-CMND          (A2)
  2 #SRT-ID            (N6)
*
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2) INIT <00>
  2 #SRT-ENTRY-LENGTH (I2) INIT <26>
  2 #SRT-KEY-LENGTH   (I2) INIT <15>
  2 #SRT-KEY-POSITION (I2) INIT <0>
  2 #SRT-SORT-TYPE    (A1) INIT <'D'>
*
1 #TODAY        (N8)
*
1 #SCR-CV-DAY         (C/31)
1 #SCR-DAY            (N2/31)
1 #SCR-CV-MON         (C/12)
1 #SCR-MON            (N2/12)
1 #DAILY-ACCESSES    (P5/31)
1 #DAILY-MM          (N2/31)
1 #M-E-ACCESSES      (P7/12)
1 #MONTHLY-YYYY      (N4/12)
1 #SCR-HEADER-GR
  2 #SCR-FILL-1        (A06) INIT <'AS OF '>
  2 #SCR-HDR-DT-MM     (A02)
  2 #SCR-FILL-2        (A01) INIT <'/'>
  2 #SCR-HDR-DT-DD     (A02)
  2 #SCR-FILL-3        (A01) INIT <'/'>
  2 #SCR-HDR-DT-YYYY   (A04)
  2 #SCR-FILL-4        (A04) INIT <' AT '>
  2 #SCR-HDR-TM-HH     (A02)
  2 #SCR-FILL-5        (A01) INIT <':'>
  2 #SCR-HDR-TM-MM     (A02)
1 REDEFINE #SCR-HEADER-GR
  2 #SCR-HEADER        (A25)
*
1 #DATN             (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY       (N4)
  2 REDEFINE #DATN-YYYY
    3 #DATN-CENT       (A2)
    3 #DATN-YY         (A2)
  2 #DATN-MM         (A2)
  2 REDEFINE #DATN-MM
    3 #DATN-MM-N      (N2)
  2 #DATN-DD         (A2)
  2 REDEFINE #DATN-DD
    3 #DATN-DD-N      (N2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
  2 #TIMN-HH     (A2)
  2 REDEFINE #TIMN-HH
    3 #TIMN-HH-N  (N2)
  2 #TIMN-MM     (A2)
  2 REDEFINE #TIMN-MM
    3 #TIMN-MM-N  (N2)
  2 #TIMN-SS     (A2)
  2 #TIMN-T      (A1)
*
1 #I1         (I2)
1 #I2         (I2)
*
* PM - 06/2000 - SPLIT COMPANIES
1 #LOC-1      (I2)
1 #LOC-2      (I2)
1 #LOC-3      (I2)
1 #LOC-4      (I2)
*
*
1 #TEXT             (A60)
1 #CMND             (A2/105)
1 #NUM-ACCESSES     (P5/105)
1 #NUM-ACCESSES-MTD (P5/105)
1 #TOT-ACCESSES     (P7)
1 #TOT-ACCESSES-MTD (P7)
*
1 #LAST-MM           (N2)
1 #LAST-YYYY         (N4)
1 #xx                (I2)
*
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
*
IF #CGA-USER-COMPANY = 'S'          /* DW3
  MOVE 'AFFL' TO #COMPANY
ELSE
  MOVE 'AAFMAA' TO #COMPANY
END-IF
*
MOVE *DATN TO #DATN
*
MOVE *TIMN TO #TIMN
IF #TIMN-HH-N GT 12
  SUBTRACT 12 FROM #TIMN-HH-N
END-IF
MOVE #DATN-MM   TO #SCR-HDR-DT-MM
MOVE #DATN-DD   TO #SCR-HDR-DT-DD
MOVE #DATN-YYYY TO #SCR-HDR-DT-YYYY
MOVE #TIMN-HH   TO #SCR-HDR-TM-HH
MOVE #TIMN-MM   TO #SCR-HDR-TM-MM
*
IF #CGA-SCR-SUFFIX = 'LST'
  READ TM-V PHYSICAL
    IF  ((CLERK-ID = 'DRW') AND (LST-CMND-DT = 0))
        OR ((CLERK-ID = 'JAK') AND (LST-CMND-DT = 0))
        OR (CLERK-ID = 'EDS')
      ESCAPE TOP
    END-IF
*
* PM - 06/2000 - SPLIT COMPANIES
*
*    DECIDE FOR FIRST CONDITION                                    RE1 Start
*      WHEN #CGA-USER-COMPANY = 'B'
*        IGNORE
*       WHEN #CGA-USER-COMPANY = 'S'
*        IF NOT TM-V.SECURITY-CODE = 01 OR = 02 OR = 97 OR = 99
*          ESCAPE TOP
*        END-IF
*      WHEN #CGA-USER-COMPANY = 'I'
*        IF TM-V.SECURITY-CODE = 01 OR = 02 OR = 97
*          ESCAPE TOP
*        END-IF
*      WHEN NONE
*        IGNORE
*    END-DECIDE                                                    RE1 End
*
    IF LST-CMND-DT < 20120101                   /* bz1
*    #xx := #xx + 1
*    PRINT  #xx *COUNTER CLERK-ID TERMINAL-ID  SECURITY-CODE LST-CMND  LST-CMND-DT
      ESCAPE TOP
    END-IF
    FOR #I1 1 TO 99
      IF ((#SRT-USER (#I1) = ' ')
          OR (CLERK-ID = #SRT-USER (#I1)))
*      AND (LST-CMND NE ' ')
          AND (LST-CMND-DT NE 0)
        MOVE LST-CMND    TO #SRT-CMND (#I1)
        MOVE LST-CMND-DT TO #SRT-DATE (#I1)
        MOVE LST-CMND-TM TO #SRT-TIME (#I1)
        MOVE CLERK-ID    TO #SRT-USER (#I1)
        MOVE LST-CMND-ID TO #SRT-ID   (#I1)
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-READ
*
  FOR #I1 1 TO 99
    IF #SRT-USER (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  if #i1 gt 99                      /* vt1 \/
    move 99 to #i1
  end-if                            /* vt1 /\
*
  MOVE *DATN TO #SRT-DATE (#I1)
  MOVE *TIMN TO #TIMN
  SUBTRACT 15 FROM #TIMN-MM-N
  IF #TIMN-MM-N LT 00
    COMPUTE #TIMN-MM-N = 60 + #TIMN-MM-N
    SUBTRACT 1 FROM #TIMN-HH-N
  END-IF
*
  MOVE #TIMN TO #SRT-TIME (#I1)
  MOVE '&&'      TO #SRT-CMND (#I1)
*
  MOVE #I1 TO #SRT-NUM-ENTRIES
*
*   CALL 'WAASSRT1' #SRT-DATE (1) #SRT-PARM    /* SAG RH1 11252005 SS
  CALLNAT 'SAGSRT11'  #SRT-TABLE(*) #SRT-PARM  /* SAG RH1 11252005 SS
*
  FOR #I1 1 TO #SRT-NUM-ENTRIES
    IF #SRT-CMND (#I1) = '&&'
      ESCAPE TOP
    END-IF
*
    IF   (#LAST-DT NE 0)
        AND (#SRT-DATE (#I1) NE 0)
        AND (#SRT-DATE (#I1) NE #LAST-DT)
        AND (#SRT-NUM-ENTRIES LT 99)
      ADD 1 TO #SRT-NUM-ENTRIES    /* to increase size
      ADD 1 TO #I1
      FOR #I2 #SRT-NUM-ENTRIES TO #I1 STEP -1
        MOVE #SRT-DATE (#I2 - 1) TO #SRT-DATE (#I2)
        MOVE #SRT-TIME (#I2 - 1) TO #SRT-TIME (#I2)
        MOVE #SRT-USER (#I2 - 1) TO #SRT-USER (#I2)
        MOVE #SRT-CMND (#I2 - 1) TO #SRT-CMND (#I2)
        MOVE #SRT-ID   (#I2 - 1) TO #SRT-ID   (#I2)
      END-FOR
*
      MOVE '**' TO #SRT-CMND (#I1 - 1)  /* to indicate day break
    END-IF
*
    MOVE #SRT-DATE (#I1) TO #LAST-DT
  END-FOR
*
  FOR #I1 1 TO #SRT-NUM-ENTRIES
    IF #SRT-DATE (#I1) NE 0
      IF #SRT-CMND (#I1) = '**'
        COMPRESS #SRT-MM (#I1) #SRT-DD (#I1)  #SRT-YYYY (#I1)
          INTO #SCR-TIME (#I1) WITH '-'
        COMPRESS '*' #SCR-TIME (#I1) INTO #SCR-TIME (#I1) LEAVING NO
        RESET             #SRT-CMND (#I1)
        RESET             #SRT-ID   (#I1)
        RESET             #SRT-USER (#I1)
        MOVE (AD=I)    TO #SCR-CV   (#I1)
      ELSE
        IF #SRT-CMND (#I1) = '&&'
          MOVE '15 Minutes' TO #SCR-TIME (#I1)
          RESET               #SRT-CMND (#I1)
          MOVE (AD=I)      TO #SCR-CV   (#I1)
        ELSE
          MOVE #SRT-TIME (#I1) TO #TIMN
*
          IF   (#TIMN-HH-N LT 12)
              OR  (#TIMN-HH-N GT 23)
            MOVE 'AM' TO #AM-PM
          ELSE
            MOVE 'PM' TO #AM-PM
          END-IF
*
          IF #TIMN-HH-N GT 12
            SUBTRACT 12 FROM #TIMN-HH-N
          END-IF
*
          COMPRESS #TIMN-HH #TIMN-MM #TIMN-SS
            INTO #SCR-TIME(#I1) WITH ':'
          COMPRESS #SCR-TIME (#I1) #AM-PM INTO #SCR-TIME (#I1)
        END-IF
      END-IF
    END-IF
  END-FOR
*
  MOVE '*' TO #CGA-SCR-COMMAND
  RESET       #CGA-SCR-SUFFIX
  MOVE 'PRESS ENTER TO SEE UPDATED SCREEN' TO #TEXT
  INPUT TEXT #TEXT USING MAP 'G2102MCC'
*
  IF #CGA-SCR-COMMAND = '*' OR = ' '
    IGNORE
  ELSE
    FETCH 'G1000PXX'
  END-IF
*
  MOVE 'CC' TO #CGA-SCR-COMMAND
  MOVE #CGA-ORIG-SUFFIX TO #CGA-SCR-SUFFIX
  FETCH 'G1000PXX'
END-IF
*
COMPUTE #LAST-YYYY = #DATN-YYYY - 1
COMPUTE #LAST-MM = #DATN-MM-N - 1
*
IF #LAST-MM LT 1
  MOVE 12 TO #LAST-MM
END-IF
*
READ CM-V BY COMMAND
* IF (#CGA-CLERK = 'DRW') AND (COMMAND = 'AI')
*   DISPLAY NUM-ACCESSES #CGA-CLERK
*   RESET NUM-ACCESSES
*   DISPLAY NUM-ACCESSES
* END-IF
  ADD 1 TO #I1
*
  IF #I1 GT 105                                  /* DW2
    MOVE 'Max of 105 Commands reached - there are more' TO #TEXT
    ESCAPE TOP
  END-IF
*
  MOVE COMMAND TO #CMND (#I1)
*
* PM - 06/2000 - SPLIT COMPANIES
*
  IF #CGA-USER-COMPANY EQ 'S'
    MOVE NUM-ACCESSES-SVC     TO #NUM-ACCESSES (#I1)
    MOVE NUM-ACCESSES-MTD-SVC TO #NUM-ACCESSES-MTD (#I1)
    ADD  NUM-ACCESSES-SVC     TO #NUM-ACCESSES-MTD (#I1)
    ADD  NUM-ACCESSES-SVC     TO #TOT-ACCESSES
    ADD  NUM-ACCESSES-MTD-SVC TO #TOT-ACCESSES-MTD
    ADD  NUM-ACCESSES-SVC     TO #TOT-ACCESSES-MTD
  ELSE
    MOVE NUM-ACCESSES     TO #NUM-ACCESSES (#I1)
    MOVE NUM-ACCESSES-MTD TO #NUM-ACCESSES-MTD (#I1)
    ADD  NUM-ACCESSES     TO #NUM-ACCESSES-MTD (#I1)
    ADD  NUM-ACCESSES     TO #TOT-ACCESSES
    ADD  NUM-ACCESSES-MTD TO #TOT-ACCESSES-MTD
    ADD  NUM-ACCESSES     TO #TOT-ACCESSES-MTD
  END-IF
END-READ
*
* PM - 06/2000 - SPLIT COMPANIES
*
IF #CGA-USER-COMPANY EQ 'S'
  MOVE 5 TO #LOC-1
  MOVE 6 TO #LOC-2
  MOVE 7 TO #LOC-3
  MOVE 8 TO #LOC-4
ELSE
  MOVE 1 TO #LOC-1
  MOVE 2 TO #LOC-2
  MOVE 3 TO #LOC-3
  MOVE 4 TO #LOC-4
END-IF
*
FIND (1) NT-V WITH ID-NUMBER = 800002
  RESET #I2
  FOR #I1 1 TO 15               /* DAY 1 THRU 15
    ADD 1 TO #I2
    MOVE NT-V.COMMAND-CNTS (#LOC-1, #I1) TO #DAILY-ACCESSES (#I2)
  END-FOR
*
  FOR #I1 1 TO 15                /* DAY 16 THRI 30
    ADD 1 TO #I2
    MOVE NT-V.COMMAND-CNTS (#LOC-2, #I1) TO #DAILY-ACCESSES (#I2)
  END-FOR
*
  ADD 1 TO #I2                   /* DAY 31
  MOVE NT-V.COMMAND-CNTS (#LOC-3, 1) TO #DAILY-ACCESSES (#I2)
*
  FOR #I2 1 TO 31
*
    MOVE #I2 TO #SCR-DAY (#I2)
    IF   (#I2 LE NT-V.NOTE-DATE-DD (#LOC-1))
        AND (NT-V.NOTE-DATE-MM (#LOC-1) = #DATN-MM-N)
      MOVE (AD=I) TO #SCR-CV-DAY (#I2)
      MOVE #DATN-MM-N TO #DAILY-MM (#I2)
    ELSE
      MOVE #LAST-MM TO #DAILY-MM (#I2)
    END-IF
  END-FOR
*
  FOR #I1 1 TO 12                /* MONTH 1 THRU 12
    MOVE #I1 TO #SCR-MON (#I1)
    MOVE NT-V.COMMAND-CNTS (#LOC-4, #I1) TO #M-E-ACCESSES (#I1)
  END-FOR
*
  FOR #I2 1 TO 12
    IF #I2 LT #DATN-MM-N
      MOVE (AD=I) TO #SCR-CV-MON (#I2)
      MOVE #DATN-YYYY TO #MONTHLY-YYYY (#I2)
    ELSE
      MOVE #LAST-YYYY TO #MONTHLY-YYYY (#I2)
    END-IF
  END-FOR
END-FIND
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF #TEXT = ' '                               /* DW2
  MOVE  'PRESS ENTER TO SEE COMMAND HISTORY' TO #TEXT
END-IF
*
INPUT TEXT #TEXT USING MAP 'G2100MCC'
*
IF #CGA-SCR-COMMAND = '*' OR = ' '
  IGNORE
ELSE
  FETCH 'G1000PXX'
END-IF
*
MOVE 'CC' TO #CGA-SCR-COMMAND
INPUT USING MAP 'G2101MCC'
*
FETCH 'G1000PXX'
*
END
