* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSNPBC.
/**
/** :author nguentchev
/* TODO Enter your code here
************************************************************************
* AUTHOR       : Nikolay Guentchev                                     *
* DESCRIPTION  : Input parameter is #POLICY-NUMBER. This Subprogram    *
*    will return all policy pending bene information. If the optional  *
*    parameter #RELATIONSHIP-TYPE is specified then this subprogram    *
*    will only return policy relationship information pertinent to     *
*    the specified relationship type: primary or contingent            *
*                                                                      *
************************************************************************
* Possible values for optional parameter #RELATIONSHIP-TYPE and the    *
* meaning behind them:  SEE POSLROLE                                   *
*                                                                      *
************************************************************************
*                       MODIFICATION LOG                               *
* USER  DATE       TAG   REASON                                        *
* NG  06/8/2015  NONE  Initial Creation                                *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #POLICY-NUMBER        (A16)  /* Input
PARAMETER USING POSAPBC
PARAMETER
1 #MSG (A) DYNAMIC
LOCAL USING ERROR_L
LOCAL USING POSL0004
LOCAL
1 #INSURED-CONTACT  (N8)
*
1 ST-V VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N2)
  2 POLICY-ID (A16/9)
  2 STATUS    (A1/9)
1 I              (I2)
1 #ERROR-CODE    (I4)
1 #ERROR-LOG     (I4) CONST <3>
END-DEFINE
*
RESET #NUMBER-OF-BENE
  #INSURED-CONTACT
XX.
FIND PB-V WITH POLICY-NUMBER = #POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  IF #BENE-TYPE > 0
      AND #BENE-TYPE NE PB-V.RELATION
    ESCAPE TOP
  END-IF
  ADD 1 TO #NUMBER-OF-BENE
  EXPAND ARRAY #BENE-RELATIONS TO (1:#NUMBER-OF-BENE)
  EXPAND ARRAY #BISNS TO (1:#NUMBER-OF-BENE)
  #BISNS(#NUMBER-OF-BENE) := *ISN(XX.)
  MOVE BY NAME PB-V TO #BENE-RELATIONS(#NUMBER-OF-BENE)
END-FIND
*
IF #NUMBER-OF-BENE = 0
  #MSG := #NOT-EXIST      /* 'No record exist'
ELSE
  RESET #MSG
  IF #INSURED-CONTACT > 0
    FIND (1) ST-V WITH MEMBER-CONTACT-ID = #INSURED-CONTACT
      FOR I 1 TO C*ACTIVE-SEGMENTS
        IF POLICY-ID (I) = #POLICY-NUMBER
          POLICY-STATUS (*) := ST-V.STATUS (I)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
  END-IF
END-IF
*
ON ERROR
  #ERROR-CODE := *ERROR-NR
  #MSG := 'INTERNAL ERROR'
  CALLNAT 'AUCLOG' #ERROR-CODE #MSG #ERROR-LOG
END-ERROR
*
END
