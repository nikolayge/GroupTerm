* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVYE8 - VALUE-ADDED YEAR-END PROCESSING
*                        SUMMARIZE LIABILITY PROJECTIONS
*
* 12/12/2012 vxt extend the field sizes
*******************************************************
DEFINE DATA
LOCAL
*
1 #JCL-PARM          (A23)
1 REDEFINE #JCL-PARM
 2 #JCL-FIL-1        (A12) /* 'TODAYS DATE='  (ASSUMES USING CURR FILE)
 2 #JCL-CURR-YYYY    (N4)
 2 #JCL-CURR-MMDD    (N4)
 2 REDEFINE #JCL-CURR-MMDD
  3 #JCL-CURR-MM     (N2)
  3 #JCL-CURR-DD     (N2)
 2 #JCL-ALL          (A3)
*
1 #PROJ-RECORD       (A80)
1 #HOLD-EXTRACT-REC-1  (A197)
1 #HOLD-EXTRACT-REC-2  (A197)
*
1 #EXTR-RECORD
 2 #RECORD-1             (A66)
 2 REDEFINE #RECORD-1
  3 #ID-NUMBER-1          (N6)
  3 REDEFINE #ID-NUMBER-1
   4 #ID-NUMBER-1-A       (A6)
  3 #F1-01                (A1)
  3 #PLAN                 (A3)
  3 REDEFINE #PLAN
   4 #PLAN-2              (A2)
   4 #PLAN-FILL           (A1)
  3 #F1-02                (A1)
  3 #ISS-DATE             (N8)
  3 REDEFINE #ISS-DATE
   4 #ISS-DATE-YYYY       (N4)
   4 #ISS-DATE-MM         (N2)
   4 #ISS-DATE-DD         (N2)
  3 #F1-03                (A1)
  3 #ISS-AGE              (A2)
  3 REDEFINE #ISS-AGE
   4 #ISS-AGE-N           (N2)
  3 #F1-04                (A1)
  3 #LAST-DURATION        (N2)
  3 #F1-05                (A1)
  3 #MONTH-IND            (N2)
  3 #F1-06                (A1)
  3 #CONV-DUR             (N2)
  3 #F1-07                (A1)
  3 #PAY-STATUS           (A2)
  3 #F1-08                (A1)
  3 #ANN-PREM             (N6.2)
  3 #F1-9                 (A1)
  3 #MODE                 (A1)
  3 #F1-10                (A1)
  3 #SMOKER-FLAG          (A1)
  3 #F1-11                (A1)
  3 #DOB                  (N8)
  3 #F1-12                (A1)
  3 #ACV                  (N7.2)
*
 2 #RECORD-2             (A61)                       /* vt1 was a59
 2 REDEFINE #RECORD-2
  3 #ID-NUMBER-2          (N6)
  3 #F2-01                (A1)
  3 #CV                   (N7.2)
  3 #F2-02                (A1)
  3 #MIN-CV               (N7.2)                     /* vt1 was n6.2
  3 #F2-03                (A1)
  3 #NXT-YR-MIN-CV        (N7.2)                      /* vt1 was n6.2 
  3 #F2-04                (A1)
  3 #DB                   (N7.2)
  3 #F2-05                (A1)
  3 #FACE-AMT             (N6.2)
  3 #F2-06                (A1)
  3 #GROWTH-RATE          (N0.5)
*
 2 #RECORD-3             (A72)
 2 REDEFINE #RECORD-3
  3 #ID-NUMBER-3          (N6)
  3 #F3-01                (A1)
  3 #NAME                 (A15)
  3 #F3-02                (A1)
  3 #MONTHLY-ALLOC-CRED   (N3.2)
  3 #F3-03                (A1)
  3 #MONTHLY-PREM-DEFIC   (N3.2)
  3 #F3-04                (A1)
  3 #NET-PREM-RATIO       (N1.6)
  3 #F3-05                (A1)
  3 #LOAN-AMT             (N5.2)
  3 #F3-06                (A1)
  3 #LOAN-INT-RATE        (N0.5)
  3 #F3-07                (A1)
  3 #SEX                  (A1)
  3 #F3-08                (A1)
  3 #CELL-COUNT           (N5)
  3 #F3-09                (A1)
  3 #LOAN-AMT-2           (N5.2)
1 REDEFINE #EXTR-RECORD
 2 #EXTR-RECORD-A         (A197)
*
1 #COUNTERS
  2 #RECS-READ        (P7)
  2 #RECS-WRITTEN     (P7)
  2 #CNT              (P7)
  2 #I1               (I2)
  2 #I2               (I2)
*
 1 #LAST-PLAN         (A3)
 1 #LAST-ISS-DATE     (N8)
 1 #LAST-ISS-AGE      (A3)
*
*    1 - IN FORCE CELL TOTALS
*    2 - IN FORCE SUB TOTALS
*    3 - IN FORCE GRAND TOTALS
*
1 #TOTALS            (3)
 2 #ACC-DB             (P13.2)
 2 #ACC-FAC-AMT        (P11.2)
 2 #ACC-ACV            (P13.2)
 2 #ACC-CV             (P13.2)
 2 #ACC-ALLOC-CRED     (P9.2)
 2 #ACC-PREM-DEFIC     (P9.2)
 2 #ACC-PLANS          (P7)
 2 #ACC-LOAN-AMT       (P11.2)
 2 #ACC-LOAN-AMT-2     (P11.2)
 2 #ACC-LOAN-INT-RATE  (P8.5)
 2 #ACC-ANN-PREM       (P9.2)
 2 #ACC-NET-PR-RATIO   (P8.5)
 2 #ACC-NUM-CELLS      (P7)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
               'FORMAT REPORT FOR YEAR-END VALUE-ADDED PROCESSING FOR '
               #JCL-CURR-YYYY
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX  //
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
               'IN-FORCE SUMMARY OF LIABILILITY PROCECTIONS FOR '
               #JCL-CURR-YYYY
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX  //
*
READ WORK 1 RECORD #EXTR-RECORD
  ADD +1 TO #RECS-READ
*
  IF #JCL-ALL NE 'ALL'
    IF  ((#ID-NUMBER-1 =  8816) AND (#ISS-DATE = 19290630))
     OR ((#ID-NUMBER-1 = 17000) AND (#ISS-DATE = 19480630))
     OR ((#ID-NUMBER-1 = 17039) AND (#ISS-DATE = 19480630))
     OR ((#ID-NUMBER-1 = 64539) AND (#ISS-DATE = 19780630))
     OR ((#ID-NUMBER-1 = 43000) AND (#ISS-DATE = 19650630))
     OR ((#ID-NUMBER-1 = 23000) AND (#ISS-DATE = 19540630))
     OR ((#ID-NUMBER-1 = 52000) AND (#ISS-DATE = 19690630))
     OR ((#ID-NUMBER-1 = 71821) AND (#ISS-DATE = 19860701))
     OR ((#ID-NUMBER-1 = 80000) AND (#ISS-DATE = 19880701))
     OR ((#ID-NUMBER-1 = 81000) AND (#ISS-DATE = 19880701))
     OR ((#ID-NUMBER-1 = 43549) AND (#ISS-DATE = 19650630))
     OR ((#ID-NUMBER-1 = 43549) AND (#ISS-DATE = 19840630))
     OR ((#ID-NUMBER-1 = 82000) AND (#ISS-DATE = 19890701))
     OR ((#ID-NUMBER-1 = 83000) AND (#ISS-DATE = 19890701))
     OR ((#ID-NUMBER-1 = 89001) AND (#ISS-DATE = 19920701))
     OR                             (#ISS-DATE = 19230630)
      IGNORE
    ELSE
      ESCAPE TOP
    END-IF
  END-IF
*
  IF #RECS-READ = 1
    PERFORM DETAIL-PROCESSING
    PERFORM ACCUM-CELL-TOTALS
    ESCAPE TOP
  END-IF
*
  IF  (#PLAN           NE #LAST-PLAN)
   OR (#ISS-DATE       NE #LAST-ISS-DATE)
   OR (#ISS-AGE        NE #LAST-ISS-AGE)
*  OR (#UNITS          NE #LAST-UNITS)      /* <<<<<<<<<<<<<<
    PERFORM CELL-PROCESSING
    PERFORM ACCUM-SUB-TOTALS
  END-IF
*
  IF  #PLAN NE #LAST-PLAN
    PERFORM PLAN-PROCESSING
    PERFORM ACCUM-GRAND-TOTALS
  END-IF
*
  PERFORM DETAIL-PROCESSING
  PERFORM ACCUM-CELL-TOTALS
*
END-WORK
*
PERFORM CELL-PROCESSING
PERFORM ACCUM-SUB-TOTALS
PERFORM PLAN-PROCESSING
PERFORM ACCUM-GRAND-TOTALS
PERFORM GRAND-TOTAL-PROCESSING
*
RESET #RECORD-1
MOVE '******' TO #ID-NUMBER-1-A
MOVE #RECORD-1 TO #PROJ-RECORD
WRITE WORK 2 #PROJ-RECORD
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DETAIL-PROCESSING
* * * * * * * * * * * * * * * * * *
*
MOVE #PLAN          TO #LAST-PLAN
MOVE #ISS-DATE      TO #LAST-ISS-DATE
MOVE #ISS-AGE       TO #LAST-ISS-AGE
* MOVE #UNITS         TO #LAST-UNITS
*
MOVE #EXTR-RECORD-A TO #HOLD-EXTRACT-REC-1
*
END-SUBROUTINE /* (0216)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CELL-PROCESSING
* * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (1) NE 0
*
  MOVE #EXTR-RECORD-A TO #HOLD-EXTRACT-REC-2
  MOVE #HOLD-EXTRACT-REC-1 TO #EXTR-RECORD-A
*
  MOVE 999999 TO #ID-NUMBER-1
* MOVE 999999 TO #ID-NUMBER-2    /* KEEP LAST ID FOR REFERENCE
* MOVE 999999 TO #ID-NUMBER-3
  MOVE 'G'    TO #MODE
  MOVE 'M'    TO #SEX
  COMPUTE ROUNDED #FACE-AMT     = #ACC-FAC-AMT    (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #ACV          = #ACC-ACV        (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #CV           = #ACC-CV         (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #DB           = #ACC-DB         (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #MONTHLY-ALLOC-CRED
                                = #ACC-ALLOC-CRED (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #MONTHLY-PREM-DEFIC
                                = #ACC-PREM-DEFIC (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #LOAN-AMT     = #ACC-LOAN-AMT   (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #LOAN-AMT-2   = #ACC-LOAN-AMT-2 (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #LOAN-INT-RATE
                             = #ACC-LOAN-INT-RATE (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #NET-PREM-RATIO
                             = #ACC-NET-PR-RATIO  (1) / #ACC-PLANS (1)
  COMPUTE ROUNDED #ANN-PREM
                             = #ACC-ANN-PREM      (1) / #ACC-PLANS (1)
*
  IF #PLAN = 'PUP'
    MOVE '47' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  IF #PLAN = 'RPU'
    MOVE '45' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  IF #PLAN = 'ETI'
    MOVE '44' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  MOVE #ACC-PLANS (1) TO #CELL-COUNT
  ADD 1 TO #ACC-NUM-CELLS (1)
*
  DISPLAY (2) NOTITLE
              'PL'         #LAST-PLAN
              'ISS/DT'     #LAST-ISS-DATE
              'ISS/AGE'    #LAST-ISS-AGE
              'NUM/PLNS'   #ACC-PLANS              (1) (EM=ZZZ9)
              'FACE/AMT'   #ACC-FAC-AMT            (1) (EM=ZZ,ZZZ,ZZ9)
              'ACV'        #ACV                        (EM=ZZ,ZZZ,ZZ9)
              'CASH/VALUE' #CV                         (EM=ZZ,ZZZ,ZZ9)
              'DEATH/BENE' #DB                         (EM=ZZ,ZZZ,ZZ9)
              'PREM/DEFC'  #MONTHLY-PREM-DEFIC         (EM=ZZ,ZZ9)
              'ALLOC/CRED' #MONTHLY-ALLOC-CRED         (EM=ZZ,ZZ9)
              'ANN/PREM'   #ANN-PREM                   (EM=ZZZ,ZZ9)
              'LOAN/AMT'   #LOAN-AMT                   (EM=Z,ZZZ,ZZ9)
              'INT/RATE'   #LOAN-INT-RATE              (EM=ZZZZ9.99999)
              'LOAN/AMT-2' #LOAN-AMT-2                 (EM=Z,ZZZ,ZZ9)
              'NT PR/RTIO' #NET-PREM-RATIO             (EM=ZZZZ9.99999)
*
* DISPLAY (2) NOTITLE
*                          #LAST-PLAN
*                          #LAST-ISS-DATE
*                          #LAST-ISS-AGE
*                          #LAST-UNITS
*                          #ACC-PLANS              (1) (EM=ZZZ9)
*                          #ACC-UNITS              (1) (EM=ZZZ9)
*                          #ACC-FAC-AMT            (1) (EM=ZZ,ZZZ,ZZ9)
*                          #ACV                        (EM=ZZ,ZZZ,ZZ9)
*                          #CV                         (EM=ZZ,ZZZ,ZZ9)
*                          #LAST-ANN-CV                (EM=ZZ,ZZZ,ZZ9)
*                          #DB                         (EM=ZZ,ZZZ,ZZ9)
*                          #MONTHLY-PREM-DEFIC         (EM=ZZ,ZZ9)
*                          #MONTHLY-ALLOC-CRED         (EM=ZZ,ZZ9)
*                          #ACC-ANN-PREM           (1) (EM=ZZZ,ZZ9)
*                          #ACC-LOAN-AMT           (1) (EM=Z,ZZZ,ZZ9)
*                          #ACC-LOAN-INT-RATE      (1) (EM=ZZZZ9.99999)
*                          #ACC-LOAN-AMT-2         (1) (EM=Z,ZZZ,ZZ9)
*                          #ACC-NET-PR-RATIO       (1) (EM=ZZZZ9.99999)
*
  MOVE #RECORD-1 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
  MOVE #RECORD-2 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
  MOVE #RECORD-3 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
* * * * * * * * NEW BUSINESS PROCESSING * * * * *
*
  IF   (#ISS-DATE-YYYY = #JCL-CURR-YYYY - 1)            /* LAST YEAR
   AND (NOT #PAY-STATUS = '44' OR = '45' OR = '47')     /* NOT PAID UP
    MOVE 888888 TO #ID-NUMBER-1
    RESET #ACV
          #CV
          #DB
    MOVE 0 TO #LAST-DURATION
    MOVE 1 TO #CONV-DUR
    MOVE 6 TO #MONTH-IND    /* TO GET 6 MONTHS IN FIRST YEAR
*
    ADD 01     TO  #ISS-DATE-YYYY    /* MAKE IT ISSUED IN CURRENT YEAR
    ADD 010000 TO  #DOB              /* MAKE IT 1 YEAR YOUNGER
*
*   DISPLAY (2) NOTITLE
*             'PL'         #LAST-PLAN
*             'ISS/DT'     #LAST-ISS-DATE
*             'ISS/AGE'    #LAST-ISS-AGE
*             'UN/ITS'     #LAST-UNITS
*             'NUM/PLNS'   #ACC-PLANS              (1) (EM=ZZZ9)
*             'TOT/UNTS'   #ACC-UNITS              (1) (EM=ZZZ9)
*             'FACE/AMT'   #ACC-FAC-AMT            (1) (EM=ZZ,ZZZ,ZZ9)
*             'ACV'        #ACC-ACV                (1) (EM=ZZ,ZZZ,ZZ9)
*             'CASH/VALUE' #ACC-CV                 (1) (EM=ZZ,ZZZ,ZZ9)
*             'LST/ANN CV' #ACC-LST-ANN-CV         (1) (EM=ZZ,ZZZ,ZZ9)
*             'DEATH/BENE' #ACC-DB                 (1) (EM=ZZ,ZZZ,ZZ9)
*             'PREM/DEFC'  #ACC-PREM-DEFIC         (1) (EM=ZZ,ZZ9)
*             'ALLOC/CRED' #ACC-ALLOC-CRED         (1) (EM=ZZ,ZZ9)
*             'ANN/PREM'   #ACC-ANN-PREM           (1) (EM=ZZZ,ZZ9)
*             'LOAN/AMT'   #ACC-LOAN-AMT           (1) (EM=Z,ZZZ,ZZ9)
*             'INT/RATE'   #ACC-LOAN-INT-RATE      (1) (EM=ZZZZ9.99999)
*             'LOAN/AMT-2' #ACC-LOAN-AMT-2         (1) (EM=Z,ZZZ,ZZ9)
*             'NT PR/RTIO' #ACC-NET-PR-RATIO       (1) (EM=ZZZZ9.99999)
    MOVE #RECORD-1 TO #PROJ-RECORD
    WRITE WORK 2 #PROJ-RECORD
*
    MOVE #RECORD-2 TO #PROJ-RECORD
    WRITE WORK 2 #PROJ-RECORD
*
    MOVE #RECORD-3 TO #PROJ-RECORD
    WRITE WORK 2 #PROJ-RECORD
  END-IF
*
  MOVE #HOLD-EXTRACT-REC-2 TO #EXTR-RECORD-A
END-IF
*
END-SUBROUTINE /* (0229)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PLAN-PROCESSING
* * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (2) NE 0
  NEWPAGE (2)
  WRITE (2) // 'PLAN TYPE ' #LAST-PLAN
  WRITE (2) / '**** PLAN TYPE TOTALS ****' /
  WRITE (2) 'TOTAL CELLS       = ' #ACC-NUM-CELLS (2)
  WRITE (2) 'TOTAL PLANS       = ' #ACC-PLANS     (2)
  WRITE (2) 'TOTAL FACE AMT    = ' #ACC-FAC-AMT   (2)
  WRITE (2) 'TOTAL CASH VALUE  = ' #ACC-CV        (2)
  WRITE (2) 'TOTAL ACC CSV     = ' #ACC-ACV       (2)
  WRITE (2) 'TOTAL DEATH BENE  = ' #ACC-DB        (2)
  WRITE (2) 'TOTAL PREM DEFIC  = ' #ACC-PREM-DEFIC(2)
  WRITE (2) 'TOTAL AMT OF LOANS= ' #ACC-LOAN-AMT  (2)
  WRITE (2) 'TOTAL AMT OF LOANS-2= ' #ACC-LOAN-AMT-2  (2)
  NEWPAGE (2)
END-IF
*
END-SUBROUTINE /* (0373)
*
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GRAND-TOTAL-PROCESSING
* * * * * * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (3) NE 0
  NEWPAGE (2)
  WRITE (2) // '**** GRAND TOTALS ****' /
  WRITE (2) 'TOTAL CELLS       = ' #ACC-NUM-CELLS (3)
  WRITE (2) 'TOTAL PLANS       = ' #ACC-PLANS     (3)
  WRITE (2) 'TOTAL FACE AMT    = ' #ACC-FAC-AMT   (3)
  WRITE (2) 'TOTAL CASH VALUE  = ' #ACC-CV        (3)
  WRITE (2) 'TOTAL ACC CSV     = ' #ACC-ACV       (3)
  WRITE (2) 'TOTAL DEATH BENE  = ' #ACC-DB        (3)
  WRITE (2) 'TOTAL PREM DEFIC  = ' #ACC-PREM-DEFIC(3)
  WRITE (2) 'TOTAL AMT OF LOANS= ' #ACC-LOAN-AMT  (3)
  WRITE (2) 'TOTAL AMT OF LOANS-2= ' #ACC-LOAN-AMT-2  (3)
  NEWPAGE (2)
END-IF
*
END-SUBROUTINE /* (0373)
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-CELL-TOTALS
* * * * * * * * * * * * * * * * * *
*
ADD #DB                   TO #ACC-DB             (1)
ADD #FACE-AMT             TO #ACC-FAC-AMT        (1)
ADD #ACV                  TO #ACC-ACV            (1)
ADD #CV                   TO #ACC-CV             (1)
ADD #MONTHLY-ALLOC-CRED   TO #ACC-ALLOC-CRED     (1)
ADD #MONTHLY-PREM-DEFIC   TO #ACC-PREM-DEFIC     (1)
ADD 1                     TO #ACC-PLANS          (1)
ADD #LOAN-AMT             TO #ACC-LOAN-AMT       (1)
ADD #LOAN-AMT-2           TO #ACC-LOAN-AMT-2     (1)
ADD #LOAN-INT-RATE        TO #ACC-LOAN-INT-RATE  (1)
ADD #ANN-PREM             TO #ACC-ANN-PREM       (1)
ADD #NET-PREM-RATIO       TO #ACC-NET-PR-RATIO   (1)
*
END-SUBROUTINE /* (0416)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-SUB-TOTALS
* * * * * * * * * * * * * * * * *
*
ADD #ACC-DB             (1) TO #ACC-DB             (2)
ADD #ACC-FAC-AMT        (1) TO #ACC-FAC-AMT        (2)
ADD #ACC-ACV            (1) TO #ACC-ACV            (2)
ADD #ACC-CV             (1) TO #ACC-CV             (2)
ADD #ACC-ALLOC-CRED     (1) TO #ACC-ALLOC-CRED     (2)
ADD #ACC-PREM-DEFIC     (1) TO #ACC-PREM-DEFIC     (2)
ADD #ACC-PLANS          (1) TO #ACC-PLANS          (2)
ADD #ACC-LOAN-AMT       (1) TO #ACC-LOAN-AMT       (2)
ADD #ACC-LOAN-AMT-2     (1) TO #ACC-LOAN-AMT-2     (2)
ADD #ACC-LOAN-INT-RATE  (1) TO #ACC-LOAN-INT-RATE  (2)
ADD #ACC-ANN-PREM       (1) TO #ACC-ANN-PREM       (2)
ADD #ACC-NET-PR-RATIO   (1) TO #ACC-NET-PR-RATIO   (2)
ADD #ACC-NUM-CELLS      (1) TO #ACC-NUM-CELLS      (2)
*
RESET #ACC-DB             (1)
      #ACC-FAC-AMT        (1)
      #ACC-ACV            (1)
      #ACC-CV             (1)
      #ACC-ALLOC-CRED     (1)
      #ACC-PREM-DEFIC     (1)
      #ACC-PLANS          (1)
      #ACC-LOAN-AMT       (1)
      #ACC-LOAN-AMT-2     (1)
      #ACC-LOAN-INT-RATE  (1)
      #ACC-ANN-PREM       (1)
      #ACC-NET-PR-RATIO   (1)
      #ACC-NUM-CELLS      (1)
*
END-SUBROUTINE /* (0435)
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-GRAND-TOTALS
* * * * * * * * * * * * * * * * * *
*
ADD #ACC-DB             (2) TO #ACC-DB             (3)
ADD #ACC-FAC-AMT        (2) TO #ACC-FAC-AMT        (3)
ADD #ACC-ACV            (2) TO #ACC-ACV            (3)
ADD #ACC-CV             (2) TO #ACC-CV             (3)
ADD #ACC-ALLOC-CRED     (2) TO #ACC-ALLOC-CRED     (3)
ADD #ACC-PREM-DEFIC     (2) TO #ACC-PREM-DEFIC     (3)
ADD #ACC-PLANS          (2) TO #ACC-PLANS          (3)
ADD #ACC-LOAN-AMT       (2) TO #ACC-LOAN-AMT       (3)
ADD #ACC-LOAN-AMT-2     (2) TO #ACC-LOAN-AMT-2     (3)
ADD #ACC-LOAN-INT-RATE  (2) TO #ACC-LOAN-INT-RATE  (3)
ADD #ACC-ANN-PREM       (2) TO #ACC-ANN-PREM       (3)
ADD #ACC-NET-PR-RATIO   (2) TO #ACC-NET-PR-RATIO   (3)
ADD #ACC-NUM-CELLS      (2) TO #ACC-NUM-CELLS      (3)
*
RESET #ACC-DB             (2)
      #ACC-FAC-AMT        (2)
      #ACC-ACV            (2)
      #ACC-CV             (2)
      #ACC-ALLOC-CRED     (2)
      #ACC-PREM-DEFIC     (2)
      #ACC-PLANS          (2)
      #ACC-LOAN-AMT       (2)
      #ACC-LOAN-AMT-2     (2)
      #ACC-LOAN-INT-RATE  (2)
      #ACC-ANN-PREM       (2)
      #ACC-NET-PR-RATIO   (2)
      #ACC-NUM-CELLS      (2)
*
END-SUBROUTINE /* (0469)
*
END
