* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
ON ERROR
  CALLNAT 'USR2001N' P_PARMS  /* Get the actual error line details
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
*   Exclude this logic for RPC 
#NAT-ERR-PGM := *ETID
IF SUBSTRING(#NAT-ERR-PGM,1,6) = 'RPCSRV'
  DEFINE WORK FILE 13 '/usr/SAG/tmp/RPC_IAS_LOG.TXT' TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
  MOVE EDITED *TIMX (EM=MM/DD/YYYY' 'HH:II:SS) TO #NAT-ERR-MSG
  COMPRESS ':' #NAT-ERR-MSG *PROGRAM 
        'Obj:'  P_ERROR_PROGRAM 
        'Err:'  P_ERROR_NUM 
        'Line:' P_ERROR_LINE 
        'CN:'   #CGA-ID
        'Cmd:'  #CGA-SCR-COMMAND
        'Sfx:'  #CGA-SCR-SUFFIX
                               INTO #NAT-ERR-MSG
  WRITE WORK FILE 13 #NAT-ERR-MSG
  #CGA-CLERK := 'ERR'
  ESCAPE ROUTINE                   /* This is the only Important Logic

END-IF
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =

  IF *DEVICE = 'BATCH'

    #NAT-ERR-NBR     := P_ERROR_NUM
    #NAT-ERR-PGM     := *PROGRAM
    #NAT-ERR-OBJ     := P_ERROR_PROGRAM
    #NAT-ERR-LINE    := P_ERROR_LINE
    #BATCH-LITERAL-1 := #CGA-ID
    WRITE
      // ' ERROR DATE AND TIME' *TIMX (EM=MM/DD/YYYY' at 'HH:II:SS)
      /  '   EXECUTING PROGRAM' *PROGRAM
      /  '     ERROR IN OBJECT' #NAT-ERR-OBJ
      /  'NATURAL ERROR NUMBER' #NAT-ERR-NBR
      /  '          ERROR LINE' #NAT-ERR-LINE
      /  '      CURRENT RECORD' #BATCH-LITERAL-1 /* ASSIGN VALUES OF THE CURRENT RECORD TO
      /  '                    ' #BATCH-LITERAL-2 /* THESE VARIABLES TO WRITE AS PART OF ERROR MESSAGE.
      /  '                    ' #BATCH-LITERAL-3
    NEWPAGE
    WRITE ' '           /* TO INSURE ABOVE GETS PRINTED
  ELSE
    IF *ERROR-NR = 3145
      MOVE 'RECORD IN USE, TRY AGAIN LATER' TO #NAT-ERR-MSG
    ELSE
      #NAT-ERR-NBR  := P_ERROR_NUM
      #NAT-ERR-PGM  := *PROGRAM
      #NAT-ERR-OBJ  := P_ERROR_PROGRAM
      #NAT-ERR-LINE := P_ERROR_LINE
    END-IF
    INPUT TEXT #NAT-ERR-MSG USING MAP 'G1000MXX'
    FETCH 'G1000PXX'
  END-IF
END-ERROR
