* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: E4702SBD      ** beneficiary processing (tran reg)
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE     TAG     REASON                                       *
* PAM  04/10/2010 PM1  Adjusted for new dependant process              *
************************************************************************
*
DEFINE DATA     /*
PARAMETER
1 #CGA-ID     (N6)
1 #PROCESS-IND (A1) /* PM - 06/2000 - SPLIT COMPANIES
1 #NAME       (A25)
1 #CGA-CLERK  (A3)
1 #BENE-X     (I2)
1 #TYPE       (A3)
*
1 NT-V                    /* note file used for pending beneficiaries
 2 ID-NUMBER          (N6)    /* let D = bene designation (1-4)
 2 NOTE-DATE          (N8/60) /* LET P/A = PRINCIPAL/ALTERNATE DESING
 2 REDEFINE NOTE-DATE         /* let L = linked policies
  3 BENE-DATES-4-POLS (6)
   4 BENE-ISS-DT       (N6/7)   /* (D, L)
   4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
   4 BENE-LST-UPD      (N8)     /* (D)
   4 BENE-STRT-PEND    (N8)     /* (D)
   4 BENE-FOL-UP-IND   (N1)     /* (D)
   4 BENE-OWN-TRNSF-DT (N8)     /* (D)
   4 BENE-SUC-OWN      (N1)     /* (d)
   4 BENE-NEW-LAYOUT   (N1)     /* (d)
   4 BENE-COM-DIS      (N2)     /* (d)
   4 BENE-FIL1         (A2)
 2 NOTE-CLERK-ID       (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-4-POLS  (6)
   4 BENE-STATUS      (A1)     /* (D)
   4 BENE-RESTRICT    (A1)     /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)    /* (D, P/A)
    5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT          (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-4-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC    (5)
     6 BENE-TEXT        (A60)   /* (D, P/A, text-line)
     6 REDEFINE BENE-TEXT
      7 BENE-TEXT-1ST-34   (A34)
      7 BENE-TEXT-LST-26   (A26)
 2 BENE-CONTACT-ID         (N8/60)    /* PM1
 2 REDEFINE BENE-CONTACT-ID           /* PM1
  3 BENE-CONTACT-6-POLS    (6)        /* PM1
   4 BENE-CONTACT-PROM-SEC (2)        /* PM1
    5 BENE-CONTACT-OCC     (5)        /* PM1
     6 BENE-CONTACT-ID-R   (N8)       /* PM1
 2 LST-BD-FINALIZATION-USER (A3)
*
1 NT-F-V                   /* final beneficiaries
 2 ID-NUMBER          (N6)
 2 NOTE-DATE          (N8/60)
 2 REDEFINE NOTE-DATE
  3 BENE-DATES-4-POLS (6)
   4 BENE-ISS-DT       (N6/7)   /* (D, L)
   4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
   4 BENE-LST-UPD      (N8)     /* (D)
   4 BENE-STRT-PEND    (N8)     /* (D)
   4 BENE-FOL-UP-IND   (N1)     /* (D)
   4 BENE-OWN-TRNSF-DT (N8)     /* (D)
   4 BENE-SUC-OWN      (N1)     /* (d)
   4 BENE-NEW-LAYOUT   (N1)     /* (d)
   4 BENE-COM-DIS      (N2)     /* (d)
   4 BENE-FIL1         (A2)
 2 NOTE-CLERK-ID      (A3/60)
 2 REDEFINE NOTE-CLERK-ID
  3 BENE-MISC-4-POLS  (6)
   4 BENE-STATUS      (A1)     /* (D)
   4 BENE-RESTRICT    (A1)     /* (D)
   4 BENE-MISC-PRIM-SEC (2)
    5 BENE-TYPE        (A1)    /* (D, P/A)
    5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
    5 BENE-MISC-FIL-2  (A12)
 2 NOTE-TEXT          (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-4-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC    (5)
     6 BENE-TEXT        (A60)   /* (D, P/A, text-line)
     6 REDEFINE BENE-TEXT
      7 BENE-TEXT-1ST-34   (A34)
      7 BENE-TEXT-LST-26   (A26)
 2 BENE-CONTACT-ID         (N8/60)    /* PM1
 2 REDEFINE BENE-CONTACT-ID           /* PM1
  3 BENE-CONTACT-6-POLS    (6)        /* PM1
   4 BENE-CONTACT-PROM-SEC (2)        /* PM1
    5 BENE-CONTACT-OCC     (5)        /* PM1
     6 BENE-CONTACT-ID-R   (N8)       /* PM1
 2 LST-BD-FINALIZATION-USER (A3)
*
LOCAL
*
1 TR-V VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER        (N6)
 2 PROCESS-IND      (A1) /* PM - 06/2000 - SPLIT COMPANIES
 2 NAME             (A25)
 2 DATE-LAST-UPDATE (N8)
 2 TERMINAL-ID      (A8)
 2 CLERK-ID         (A3)
 2 TRANSACTION-DATN (P9)
 2 TRANSACTION-TIMN (P7)
 2 OLD-DATA         (A125/7)
 2 NEW-DATA         (A125/7)
*
1 #I1 (I2)
*
END-DEFINE
* * * * * * * * * * * * * * *
*
RESET TR-V #I1
MOVE #CGA-ID         TO TR-V.ID-NUMBER
MOVE #NAME           TO TR-V.NAME
MOVE #PROCESS-IND    TO TR-V.PROCESS-IND /* PM - 06/2000 - SPLIT COMP
MOVE NT-F-V.BENE-LST-UPD (#BENE-X) TO TR-V.DATE-LAST-UPDATE
MOVE *INIT-ID        TO TR-V.TERMINAL-ID
MOVE #CGA-CLERK      TO TR-V.CLERK-ID
MOVE *DATN           TO TR-V.TRANSACTION-DATN
MOVE *TIMN           TO TR-V.TRANSACTION-TIMN
ADD 1 TO #I1
COMPRESS 'BD-' #TYPE 'Beneficiary chg for policies'
               NT-V.BENE-ISS-DT (#BENE-X, *) INTO NEW-DATA (#I1)
COMPRESS 'BD-' #TYPE 'Beneficiary chg for policies'
               NT-F-V.BENE-ISS-DT (#BENE-X, *) INTO OLD-DATA (#I1)
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 1, 1)
        NE NT-V.BENE-TEXT (#BENE-X, 1, 1))
 OR     (NT-F-V.BENE-TEXT (#BENE-X, 1, 2)
        NE NT-V.BENE-TEXT (#BENE-X, 1, 2))
 OR     (NT-F-V.BENE-TYPE (#BENE-X, 1)
         NE NT-V.BENE-TYPE (#BENE-X, 1))
 OR     (NT-F-V.BENE-SHARE-TYP (#BENE-X, 1)
        NE NT-V.BENE-SHARE-TYP (#BENE-X, 1))
 OR     (NT-F-V.BENE-RESTRICT (#BENE-X)
        NE NT-V.BENE-RESTRICT (#BENE-X))
 OR     (NT-F-V.BENE-SUC-OWN  (#BENE-X)
        NE NT-V.BENE-SUC-OWN  (#BENE-X))
  ADD 1 TO #I1
  COMPRESS 'Prin 1/2' NT-F-V.BENE-TEXT (#BENE-X, 1, 1)
           NT-F-V.BENE-TEXT (#BENE-X, 1, 2)
           'Tp' NT-F-V.BENE-TYPE (#BENE-X, 1)
           '#' NT-F-V.BENE-SHARE-TYP (#BENE-X, 1)
           'Rst' NT-F-V.BENE-RESTRICT (#BENE-X)
           'S-o' NT-F-V.BENE-SUC-OWN  (#BENE-X)
                                         INTO OLD-DATA (#I1)
  COMPRESS 'Prin 1/2' NT-V.BENE-TEXT (#BENE-X, 1, 1)
           NT-V.BENE-TEXT (#BENE-X, 1, 2)
           'Tp' NT-V.BENE-TYPE (#BENE-X, 1)
           '#' NT-V.BENE-SHARE-TYP (#BENE-X, 1)
           'Rst' NT-V.BENE-RESTRICT (#BENE-X)
           'S-o' NT-V.BENE-SUC-OWN  (#BENE-X)
                                         INTO NEW-DATA (#I1)
 END-IF
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 1, 3)
        NE NT-V.BENE-TEXT (#BENE-X, 1, 3))
 OR     (NT-F-V.BENE-TEXT (#BENE-X, 1, 4)
        NE NT-V.BENE-TEXT (#BENE-X, 1, 4))
  ADD 1 TO #I1
  COMPRESS 'Prin 3/4' NT-F-V.BENE-TEXT (#BENE-X, 1, 3)
           NT-F-V.BENE-TEXT (#BENE-X, 1, 4)
                                       INTO OLD-DATA (#I1)
  COMPRESS 'Prin 3/4' NT-V.BENE-TEXT (#BENE-X, 1, 3)
           NT-V.BENE-TEXT (#BENE-X, 1, 4)
                                         INTO NEW-DATA (#I1)
END-IF
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 1, 5)
        NE NT-V.BENE-TEXT (#BENE-X, 1, 5))
  ADD 1 TO #I1
  COMPRESS 'Prin 5' NT-F-V.BENE-TEXT (#BENE-X, 1, 5)
                                         INTO OLD-DATA (#I1)
  COMPRESS 'Prin 5' NT-V.BENE-TEXT (#BENE-X, 1, 5)
                                         INTO NEW-DATA (#I1)
END-IF
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 2, 1)
        NE NT-V.BENE-TEXT (#BENE-X, 2, 1))
 OR     (NT-F-V.BENE-TEXT (#BENE-X, 2, 2)
        NE NT-V.BENE-TEXT (#BENE-X, 2, 2))
 OR     (NT-F-V.BENE-TYPE (#BENE-X, 2)
        NE NT-V.BENE-TYPE (#BENE-X, 2))
 OR     (NT-F-V.BENE-SHARE-TYP (#BENE-X, 2)
        NE NT-V.BENE-SHARE-TYP (#BENE-X, 2))
  ADD 1 TO #I1
  COMPRESS 'Alt 1/2' NT-F-V.BENE-TEXT (#BENE-X, 2, 1)
           NT-F-V.BENE-TEXT (#BENE-X, 2, 2)
          'Tp' NT-F-V.BENE-TYPE (#BENE-X, 2)
           '#' NT-F-V.BENE-SHARE-TYP (#BENE-X, 2)
                                         INTO OLD-DATA (#I1)
  COMPRESS 'Alt 1/2' NT-V.BENE-TEXT (#BENE-X, 2, 1)
           NT-V.BENE-TEXT (#BENE-X, 2, 2)
           'Tp' NT-V.BENE-TYPE (#BENE-X, 2)
           '#' NT-V.BENE-SHARE-TYP (#BENE-X, 2)
                                         INTO NEW-DATA (#I1)
END-IF
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 2, 3)
        NE NT-V.BENE-TEXT (#BENE-X, 2, 3))
 OR     (NT-F-V.BENE-TEXT (#BENE-X, 2, 4)
        NE NT-V.BENE-TEXT (#BENE-X, 2, 4))
  ADD 1 TO #I1
  COMPRESS 'Alt 3/4' NT-F-V.BENE-TEXT (#BENE-X, 2, 3)
           NT-F-V.BENE-TEXT (#BENE-X, 2, 4)
                                       INTO OLD-DATA (#I1)
  COMPRESS 'Alt 3/4' NT-V.BENE-TEXT (#BENE-X, 2, 3)
           NT-V.BENE-TEXT (#BENE-X, 2, 4)
                                         INTO NEW-DATA (#I1)
END-IF
*
IF      (NT-F-V.BENE-TEXT (#BENE-X, 2, 5)
        NE NT-V.BENE-TEXT (#BENE-X, 2, 5))
  ADD 1 TO #I1
  COMPRESS 'Alt 5 & owner' NT-F-V.BENE-TEXT (#BENE-X, 2, 5)
                                             INTO OLD-DATA (#I1)
  COMPRESS 'Alt 5 & owner' NT-V.BENE-TEXT (#BENE-X, 2, 5)
                                         INTO NEW-DATA (#I1)
END-IF
*
STORE TR-V
*
END
