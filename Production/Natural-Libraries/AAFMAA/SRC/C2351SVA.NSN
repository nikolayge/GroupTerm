* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C2351SVA  ** Calculate adjustments for premiums that
*                          were not applied - formula supplied by Crooks
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    04282004  NONE    Program written                             *
* PAM    05052004  NONE    Removed COI and ACV from necessary fields.  *
*                          Only DB is needed for formulas.             *
* PAM    08112004  PM1     Added ability to remove payments as well    *
*                          as apply missed payments.  Formula the same *
*                          per Crooks.                                 *
* DRW    09252005  DW2     Added check for months prioe to 13 Months   *
* bz     2/27/2015         OI-3374 
************************************************************************
*
DEFINE DATA
PARAMETER
*
1 #MISS-OVER          (A1)                                        /* PM1
1 #ISSUE-DATE         (A8)
1 #PREM-SHORTAGE      (N7.2)                                      /* bz OI-3374
1 #DATE-PAID          (N8)
1 REDEFINE #DATE-PAID
 2 #DATE-PAID-YYYYMM  (N6)
 2 #DATE-PAID-DD      (N2)
1 #DB-DATE-PAID       (N7.2)
1 #ACV-DATE-PAID      (N7.2)
1 #COI-DATE-PAID      (N6.2)
1 #ACV-PREM-ADJ-S     (A1)
1 #ACV-PREM-ADJ       (N7.2)
1 #ACV-INT-ADJ-S      (A1)
1 #ACV-INT-ADJ        (N5.2)
1 #ACV-COI-ADJ-S      (A1)
1 #ACV-COI-ADJ        (N3.2)
1 #HISTORY-KEY        (N15)
1 #DATE               (N8)
*
LOCAL
1 VA-V VIEW OF A-VA-HISTORY
 2 MONTHLY-HIST         (49)    /* DW2
  3 YEAR-MONTH-M        (P7.0)
  3 ACCUM-CASH-VALUE-M  (P7.2)
  3 COST-OF-INSURANCE-M (P7.2)  /* NG99
  3 DEATH-BENEFIT-M     (P7.2)
*
1 #I1                   (I2)
*
1 #CREDITING-RATE       (P1.2)  INIT <.07>
1 #DAILY-INT-RATE       (P1.7)
1 #NAR-DATE-PAID        (P7.2)
1 #DAYS-SHORT           (N8)
1 #REQUEST-DAYS-CALC    (A1)    INIT <'A'>
*
END-DEFINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #HISTORY-KEY NE 0
  FIND VA-V WITH RECORD-KEY = #HISTORY-KEY
    IF NO RECORDS FOUND
      RESET #DB-DATE-PAID #ACV-DATE-PAID #COI-DATE-PAID
      ESCAPE BOTTOM
    END-NOREC
*
    FOR #I1 1 TO 49                                   /* DW2
      IF #DATE-PAID-YYYYMM NE YEAR-MONTH-M (#I1)
        ESCAPE TOP
      END-IF
*
      MOVE ACCUM-CASH-VALUE-M (#I1)  TO #ACV-DATE-PAID
      MOVE COST-OF-INSURANCE-M (#I1) TO #COI-DATE-PAID
      MOVE DEATH-BENEFIT-M (#I1)     TO #DB-DATE-PAID
      ESCAPE BOTTOM
    END-FOR
  END-FIND
END-IF
*
IF #DB-DATE-PAID = 0
* OR #ACV-DATE-PAID = 0
* OR #COI-DATE-PAID = 0
  ESCAPE ROUTINE
END-IF
*
COMPUTE ROUNDED #DAILY-INT-RATE = (1 + #CREDITING-RATE) ** (1/365) - 1
*
COMPUTE ROUNDED #NAR-DATE-PAID = #DB-DATE-PAID - #ACV-DATE-PAID
*
CALLNAT 'GDATESXX'
  #DATE
  #DATE-PAID
  #DAYS-SHORT
  #REQUEST-DAYS-CALC
*
COMPUTE #ACV-INT-ADJ = #PREM-SHORTAGE * ((1 + #DAILY-INT-RATE) **
                       #DAYS-SHORT - 1)
*
MOVE #PREM-SHORTAGE TO #ACV-PREM-ADJ
*
COMPUTE #ACV-COI-ADJ = ((#PREM-SHORTAGE * #COI-DATE-PAID
                     * #DAYS-SHORT * 12) / (365 * #NAR-DATE-PAID))
                     * (1 + #DAILY-INT-RATE) ** #DAYS-SHORT
*
IF #MISS-OVER = 'M'                                               /* PM1
  MOVE '+' TO #ACV-PREM-ADJ-S
  MOVE '+' TO #ACV-INT-ADJ-S
  MOVE '-' TO #ACV-COI-ADJ-S
ELSE                                                              /* PM1
  MOVE '-' TO #ACV-PREM-ADJ-S                                     /* PM1
  MOVE '-' TO #ACV-INT-ADJ-S                                      /* PM1
  MOVE '+' TO #ACV-COI-ADJ-S                                      /* PM1
END-IF                                                            /* PM1
*
END
