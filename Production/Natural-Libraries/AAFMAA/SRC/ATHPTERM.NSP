* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPTERM
*  THIS PROGRAM COUNTS TERMINATIONS
* TMT  07/15/2014  Contact Normalization
DEFINE DATA
LOCAL
*
1 VA-V VIEW OF A-VA-HISTORY
 2 RECORD-KEY            (N15)
 2 REDEFINE RECORD-KEY
  3 VA-ID-NUM            (N6)
  3 REDEFINE VA-ID-NUM
   4 VA-ID-NUM-A         (A6)
  3 VA-ISS-DATE          (N8)
  3 VA-SPL-IND           (N1)
 2 TERMINATION-DATE  (P7)
 2 TERMINATION-CODE  (A3)
 2 ISSUE-AGE         (A2)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER             (N6)
 2 MEMBER-CONTACT-ID
*  2 DATE-OF-BIRTH         (N8)
 2 NAME                  (A25)
 2 RANK                  (A3)
 2 MILITARY-STATUS       (A1)
 2 MILITARY-SERVICE      (A3)
 2 INTRO-FLAG            (A1)
 2 ISSUE-DATE            (N8/5)
 2 MODE                  (A1/5)
*
1 NS-V VIEW OF A-NOTES
 2 ID-NUMBER             (N6)
 2 NOTE-TEXT             (A60/60)
*
1 #JCL-PARM              (A4)
1 REDEFINE #JCL-PARM
 2 #JCL-YYYY             (N4)
*
1 #EXTRACT
 2 #EXT-TERM-TYPE         (A1)
 2 #EXT-TERM-DATE         (N8)
 2 #EXT-RANK              (A3)
 2 #EXT-MIL-STAT          (A1)
 2 #EXT-MIL-SERV          (A3)
 2 #EXT-INTRO-FLAG        (A1)
 2 #EXT-ID-NUMBER         (N6)
 2 #EXT-ISSUE-AGE         (A2)
 2 #EXT-DOB               (N8)
 2 #EXT-NAME              (A25)
*
1 #LAST-CN               (N6)
1 #LAST-RANK             (A3)
1 #TERM-TYPE             (A1)
1 #TERM-TYPE-LABEL       (A20)
1 #TERM-AGE-MONS         (P5)
1 #DUR-MONS              (P5)
1 #AVG-TERM-AGE          (P9)
1 #AVG-DURATION          (P9)
1 #MODE                  (A1)
1 #MODE-TBL             (P5/4)
*
1 #TERM-TOTS      (4)
 2 #TBL-MBRS           (P5)
 2 #TBL-CNT            (P5)
 2 #TBL-HASH-TERM-AGE  (P9)
 2 #TBL-HASH-DUR-MONS  (P9)
*
1 #VA-TERM-DATE      (N6)
1 REDEFINE #VA-TERM-DATE
 2 #VA-TERM-YYYY     (N4)
 2 #VA-TERM-MM       (N2)
*
1 #WYYYYMMDD     (N8)
1 REDEFINE #WYYYYMMDD
 2 #WCENT-A      (A2)
 2 #WYY-A        (A2)
 2 #WMM-A        (A2)
 2 #WDD-A        (A2)
*
1 #WYYYYMMDD-LOW
 2 #WYYYY-LOW   (N4)
 2 #WMM-LOW     (N2)
 2 #WDD-LOW     (N2)
1 REDEFINE #WYYYYMMDD-LOW
 2 #WYYYYMMDD-LOW-N (N8)
*
1 #WYYYYMMDD-HIGH
 2 #WYYYY-HIGH  (N4)
 2 REDEFINE #WYYYY-HIGH
  3 #WCENT-HIGH (N2)
  3 #WYY-HIGH   (N2)
  3 REDEFINE #WYY-HIGH
   4 #WYY-HIGH-A (A2)
 2 #WMM-HIGH    (N2)
 2 REDEFINE #WMM-HIGH
  3 #WMM-HIGH-A   (A2)
 2 #WDD-HIGH    (N2)
1 REDEFINE #WYYYYMMDD-HIGH
 2 #WYYYYMMDD-HIGH-N (N8)
1 REDEFINE #WYYYYMMDD-HIGH
 2 #DATE-DIFF        (N8)
*
1 #YRS-AREA-1        (A5)
1 #YRS-AREA-2        (A5)
1 #ISS-DT-DISP       (A8)
1 #DOB-DISP          (A8)
1 #TERM-DT-DISP      (A5)
1 #CNT               (P5)
*
1 #I1             (I2)
1 #NEW-PAGE       (L)
1 #JUST-WROTE-DASHES (L)
1 #DATE-OF-BIRTH     (N8.0)     /* TT1
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=72
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM
           8X #TERM-TYPE-LABEL
           5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX 5X 'PAGE' *PAGE-NUMBER (2)
FORMAT (3) LS=133 PS=72
WRITE  (3) TITLE LEFT 'PROGRAM:' *PROGRAM
           8X 'TERMINATION SUMMARY'
           5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX 5X 'PAGE' *PAGE-NUMBER (3)
*
INPUT #JCL-PARM
* READ (20000) VA-V PHYSICAL
READ VA-V PHYSICAL
  MOVE TERMINATION-DATE TO #VA-TERM-DATE
  IF  (NOT (TERMINATION-CODE = 'CSV' OR = 'LAP'))
   OR (#VA-TERM-YYYY NE #JCL-YYYY)
    ESCAPE TOP
  END-IF
*
  RESET NS-V.NOTE-TEXT (*)
  FIND (1) NS-V WITH ID-NUMBER = VA-ID-NUM
    FOR #I1 1 TO 60
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'CSV''D '))
       OR (NOTE-TEXT (#I1) = MASK('*CSV '))
       OR (NOTE-TEXT (#I1) = MASK('*CSV''D'))
       OR (TERMINATION-CODE = 'CSV')
        MOVE '1' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPS NO'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED NON'))
        MOVE '3' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPS OV'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED OVER'))
        MOVE '4' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'LAPSED'))
       OR (NOTE-TEXT (#I1) = MASK('*LAPSED'))
        MOVE '5' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*RESIGN'))
       OR (NOTE-TEXT (#I1) = MASK('*RSIGN'))
        MOVE '6' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF  (NOTE-TEXT (#I1) = MASK('*'..................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*'...................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*'....................'CSV EX'))
       OR (NOTE-TEXT (#I1) = MASK('*1035 EX'))
        MOVE '2' TO #TERM-TYPE
        ESCAPE BOTTOM
      END-IF
*
      IF #I1 = 2
        IF TERMINATION-CODE = 'CSV'
          MOVE '1' TO #TERM-TYPE     /* ASSUME REGULAr CASH VALUE
        ELSE
          MOVE '3' TO #TERM-TYPE     /* ASSUME LAPSE NON-PAY
        END-IF
      END-IF
    END-FOR
  END-FIND
*
  RESET #MODE
  FIND (1) ST-V WITH MEMBER-KEY = VA-ID-NUM-A
    FOR #I1 1 TO 5
      IF ISSUE-DATE (#I1) = VA-ISS-DATE
        MOVE MODE (#I1) TO #MODE
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
*   
  RESET #DATE-OF-BIRTH                                                 /* TT1
  #DATE-OF-BIRTH    := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)     /* TT1
*
  MOVE #TERM-TYPE            TO #EXT-TERM-TYPE
  MOVE TERMINATION-DATE      TO #EXT-TERM-DATE
  MOVE ST-V.RANK             TO #EXT-RANK
  MOVE ST-V.MILITARY-STATUS  TO #EXT-MIL-STAT
  MOVE ST-V.MILITARY-SERVICE TO #EXT-MIL-SERV
  MOVE ST-V.INTRO-FLAG       TO #EXT-INTRO-FLAG
  MOVE VA-ID-NUM             TO #EXT-ID-NUMBER
  MOVE VA-V.ISSUE-AGE        TO #EXT-ISSUE-AGE
  MOVE #DATE-OF-BIRTH    TO #EXT-DOB
  MOVE ST-V.NAME             TO #EXT-NAME
  WRITE WORK 1 #EXTRACT
END-ALL
*
SORT BY #TERM-TYPE TERMINATION-DATE ST-V.RANK ST-V.MILITARY-STATUS
        ST-V.MILITARY-SERVICE ST-V.INTRO-FLAG RECORD-KEY
   USING VA-V.ISSUE-AGE
         #DATE-OF-BIRTH                                                /* TT1
         ST-V.NAME
         #MODE
*
  AT BREAK ST-V.MILITARY-SERVICE
    RESET #LAST-CN
    IF NOT #JUST-WROTE-DASHES
      WRITE (2) '------------------------------------------'
      MOVE TRUE TO #JUST-WROTE-DASHES
    END-IF
  END-BREAK
*
  AT BREAK ST-V.MILITARY-STATUS
    RESET #LAST-CN
    IF NOT #JUST-WROTE-DASHES
      WRITE (2) '------------------------------------------'
      MOVE TRUE TO #JUST-WROTE-DASHES
    END-IF
  END-BREAK
*
  AT BREAK ST-V.RANK
    RESET #LAST-CN
    COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (1) / #TBL-CNT (1)
    DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
    COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (1) / #TBL-CNT (1)
    DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
    WRITE (2) 'RANK-TOTALS  - COUNT = ' #TBL-CNT (1)
                            ' MEMBERS = ' #TBL-MBRS (1)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
    WRITE (3) 'RANK-TOTALS  - COUNT = ' #TBL-CNT (1)
                            ' MEMBERS = ' #TBL-MBRS (1)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
              #TERM-TYPE-LABEL ' ' #TERM-DT-DISP ' ' #LAST-RANK
    RESET #TBL-MBRS          (1)
          #TBL-CNT           (1)
          #TBL-HASH-TERM-AGE (1)
          #TBL-HASH-DUR-MONS (1)
    WRITE (2) '******************************************'
  END-BREAK
*
  AT BREAK TERMINATION-DATE
    RESET #LAST-CN
    COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (2) / #TBL-CNT (2)
    DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
    COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (2) / #TBL-CNT (2)
    DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
    WRITE (2) 'MONTH-TOTALS - COUNT = ' #TBL-CNT (2)
                            ' MEMBERS = ' #TBL-MBRS (2)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
    WRITE (3) 'MONTH-TOTALS - COUNT = ' #TBL-CNT (2)
                            ' MEMBERS = ' #TBL-MBRS (2)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
              #TERM-TYPE-LABEL ' ' #TERM-DT-DISP
    RESET #TBL-MBRS          (2)
          #TBL-CNT           (2)
          #TBL-HASH-TERM-AGE (2)
          #TBL-HASH-DUR-MONS (2)
    MOVE TRUE TO #NEW-PAGE
  END-BREAK
*
  AT BREAK #TERM-TYPE
    RESET #LAST-CN
    COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (3) / #TBL-CNT (3)
    DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
    COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (3) / #TBL-CNT (3)
    DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
    COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
    WRITE (2) 'TYPE-TOTALS  - COUNT = ' #TBL-CNT (3)
                            ' MEMBERS = ' #TBL-MBRS (3)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
    WRITE (3) 'TYPE-TOTALS  - COUNT = ' #TBL-CNT (3)
                            ' MEMBERS = ' #TBL-MBRS (3)
                            ' AVG TERM AGE = ' #YRS-AREA-1
                            ' AVG DURATION = ' #YRS-AREA-2
              #TERM-TYPE-LABEL
    WRITE (2) 'TYPE-MODE TOTALS - '
                             ' MODE G = ' #MODE-TBL (1)
                             ' MODE Q = ' #MODE-TBL (2)
                             ' MODE S = ' #MODE-TBL (3)
                             ' MODE A = ' #MODE-TBL (4)
    WRITE (3) 'TYPE-MODE TOTALS - '
                             ' MODE G = ' #MODE-TBL (1)
                             ' MODE Q = ' #MODE-TBL (2)
                             ' MODE S = ' #MODE-TBL (3)
                             ' MODE A = ' #MODE-TBL (4)
    NEWPAGE (3)
    RESET #TBL-MBRS          (3)
          #TBL-CNT           (3)
          #TBL-HASH-TERM-AGE (3)
          #TBL-HASH-DUR-MONS (3)
    MOVE TRUE TO #NEW-PAGE
  END-BREAK
*
  IF #NEW-PAGE
    RESET #NEW-PAGE
    NEWPAGE (2)
  END-IF
*
  PERFORM SET-LABELS
*
  MOVE TERMINATION-DATE TO #VA-TERM-DATE
  MOVE VA-ISS-DATE   TO #WYYYYMMDD-LOW-N
  MOVE #VA-TERM-DATE TO #WYYYYMMDD-HIGH-N
  MULTIPLY #WYYYYMMDD-HIGH-N BY 100
  ADD 15 TO #WYYYYMMDD-HIGH-N
  PERFORM CALC-DATE-DIFF
  COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
  COMPUTE #DUR-MONS = (#WYY-HIGH  * 12) + #WMM-HIGH
*
  MOVE #DATE-OF-BIRTH TO #WYYYYMMDD-LOW-N                              /* TT1
  MOVE #VA-TERM-DATE      TO #WYYYYMMDD-HIGH-N
  MULTIPLY #WYYYYMMDD-HIGH-N BY 100
  ADD 15 TO #WYYYYMMDD-HIGH-N
  PERFORM CALC-DATE-DIFF
  COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
  COMPUTE #TERM-AGE-MONS = (#WYY-HIGH  * 12) + #WMM-HIGH
*
  MOVE VA-ISS-DATE TO #WYYYYMMDD
  COMPRESS #WMM-A #WDD-A #WYY-A INTO #ISS-DT-DISP WITH '-'
*
  MOVE #DATE-OF-BIRTH TO #WYYYYMMDD                                    /* TT1
  COMPRESS #WMM-A #WDD-A #WYY-A INTO #DOB-DISP WITH '-'
*
  MOVE #VA-TERM-DATE TO #WYYYYMMDD
  MULTIPLY #WYYYYMMDD BY 100
  COMPRESS #WMM-A #WYY-A INTO #TERM-DT-DISP WITH '-'
*
  IF VA-V.VA-ID-NUM NE #LAST-CN
    ADD 1 TO #TBL-MBRS (*)
  END-IF
  MOVE VA-V.VA-ID-NUM TO #LAST-CN
  MOVE ST-V.RANK      TO #LAST-RANK
*
  DECIDE FOR FIRST CONDITION
    WHEN #MODE = 'G' ADD 1 TO #MODE-TBL (1)
    WHEN #MODE = 'Q' ADD 1 TO #MODE-TBL (2)
    WHEN #MODE = 'S' ADD 1 TO #MODE-TBL (3)
    WHEN #MODE = 'A' ADD 1 TO #MODE-TBL (4)
    WHEN NONE  IGNORE
  END-DECIDE
*
  ADD 1              TO #TBL-CNT           (*)
  ADD #TERM-AGE-MONS TO #TBL-HASH-TERM-AGE (*)
  ADD #DUR-MONS      TO #TBL-HASH-DUR-MONS (*)
*
  ADD 1 TO #CNT
  DISPLAY (2) 'COUNT'           #TBL-CNT (1)
              'TERM/DT'         #TERM-DT-DISP
              'ISS-DT'          #ISS-DT-DISP
              'DOB'             #DOB-DISP
              'ISS/AGE'         VA-V.ISSUE-AGE
              'YRS-MONS/DUR'    #YRS-AREA-1
              'TERM/AGE'        #YRS-AREA-2
              'ID'              VA-ID-NUM
              'MIL/ID'          ST-V.MILITARY-SERVICE
              'MIL/STAT'        ST-V.MILITARY-STATUS
              'RANK'            ST-V.RANK
              '       NAME'     ST-V.NAME
              'INTRO/FLAG'      ST-V.INTRO-FLAG
              'MODE'            #MODE
  RESET #JUST-WROTE-DASHES
END-SORT
*
COMPUTE #AVG-TERM-AGE = #TBL-HASH-TERM-AGE (4) / #TBL-CNT (4)
DIVIDE 12 INTO #AVG-TERM-AGE GIVING #WYY-HIGH REMAINDER #WMM-HIGH
COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-1 LEAVING NO
COMPUTE #AVG-DURATION = #TBL-HASH-DUR-MONS (4) / #TBL-CNT (4)
DIVIDE 12 INTO #AVG-DURATION GIVING #WYY-HIGH REMAINDER #WMM-HIGH
COMPRESS #WYY-HIGH-A '-' #WMM-HIGH-A INTO #YRS-AREA-2 LEAVING NO
WRITE (2) 'GRAND-TOTALS - COUNT = ' #TBL-CNT (4)
                        ' MEMBERS = ' #TBL-MBRS (4)
                        ' AVG TERM AGE = ' #YRS-AREA-1
                        ' AVG DURATION = ' #YRS-AREA-2
WRITE (3) 'GRAND-TOTALS - COUNT = ' #TBL-CNT (4)
                        ' MEMBERS = ' #TBL-MBRS (4)
                        ' AVG TERM AGE = ' #YRS-AREA-1
                        ' AVG DURATION = ' #YRS-AREA-2
*
* * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-DATE-DIFF
* * * * * * * * * * * * * * * * * * * * * * * *
*
IF #WDD-LOW GT #WDD-HIGH
  SUBTRACT 1 FROM #WMM-HIGH
  ADD      1 TO   #WDD-HIGH
  IF #WMM-HIGH = 02
    ADD 28 TO #WDD-HIGH
  ELSE
    IF #WMM-HIGH = 04 OR = 06 OR = 09 OR = 11
      ADD 30 TO #WDD-HIGH
    ELSE
      ADD 31 TO #WDD-HIGH
    END-IF
  END-IF
END-IF
*
IF #WMM-LOW GT #WMM-HIGH
  SUBTRACT 1 FROM #WYYYY-HIGH
  ADD 12 TO #WMM-HIGH
END-IF
*
SUBTRACT #WYYYYMMDD-LOW-N FROM #WYYYYMMDD-HIGH-N
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-LABELS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
DECIDE ON FIRST VALUE OF #TERM-TYPE
  VALUE '1'  MOVE 'CASH-VALUES'    TO #TERM-TYPE-LABEL
  VALUE '2'  MOVE '1035-EXCHANGES' TO #TERM-TYPE-LABEL
  VALUE '3'  MOVE 'LAPSE NON-PMT'  TO #TERM-TYPE-LABEL
  VALUE '4'  MOVE 'LAPSE OVERLOAN' TO #TERM-TYPE-LABEL
  VALUE '5'  MOVE 'REG LAPSES'     TO #TERM-TYPE-LABEL
  VALUE '6'  MOVE 'RESIGNATIONS'   TO #TERM-TYPE-LABEL
  NONE       MOVE 'UNKNOWN'        TO #TERM-TYPE-LABEL
END-DECIDE
*
END-SUBROUTINE
*
END
