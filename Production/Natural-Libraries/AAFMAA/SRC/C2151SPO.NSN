* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: C2151SPO - Input menu for Possible Options for Insurance
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    01052005  DW1     IF SUFFIX IS OLD USE 20041231 LTII RATES    *
* DRW    05042005  DW2     Fix Minimum age for GEN test                *
* DRW    05132005  DW3     NO RT FOR GEN                               *
* DRW    05162005  DW4     Add SEX for LTII premiums                   *
* PAM    07282005  PM5     Change Default to mark Sex Instead of DOB   *
* PAM    12022005  PM6     Allow LTII issue age up to 75               *
* DRW    05122006  DW7     Do not show 'including rider' if ss/se      *
*                          term<25                                     *
* PAM    05122008  PM8     Adjust for 2008 RT Refunds                  *
* DRW  08/13/2008  DW9     Generations Plus processing                 *
* PAM  12/24/2009  PM10    Added new generational insured requirements *
* NGG  20121023    NG1     Level term Re-newable                       *
* NGG  20130305    NG2     Ticket 1854                                 *
* NGG  20130305    NG3     Ticket 1881                                 *
* NGG  20130325    NG4     Changes for navigation for EZ Term          *
* NGG  06/25/2013  NG5     Changes for Ticket 1584 and Ticket 1540     *
* TMT  12/01/2013  TT1     1 MIL POLICY SUPPORT                        *
* TMT  12/31/2013  TT2     ADDED GP2 POLICY                            *
* vxt  02/24/2014  vt1     /ticket 2604/                               *
************************************************************************
DEFINE DATA
PARAMETER USING C2150APO
PARAMETER
1 #SCN-AOD-YYYYMMDD (N8)
1 #SCN-AS-OF-DT     (A8)
1 REDEFINE #SCN-AS-OF-DT
  2 #SCN-AOD-N       (N8)
1 REDEFINE #SCN-AS-OF-DT
  2 #SCN-AOD-MMDD    (A4)
  2 #SCN-AOD-YYYY    (A4)
1 #DATE-OF-BIRTH    (N8)
1 #SCN-DOB          (A8)
1 REDEFINE #SCN-DOB
  2 #SCN-DOB-N       (N8)
1 REDEFINE #SCN-DOB
  2 #SCN-DOB-MMDD    (A4)
  2 #SCN-DOB-YYYY    (A4)
1 #SCN-SPS-GEN      (A1)
1 #SCN-SPS-GEN-CV   (C)
1 #SSN              (N9)
1 #DESIRED-FACE-AMT (N7/3)
1 #DESIRED-PLAN     (A1/10)
1 #FIRST-TIME       (L)
1 #FROM-AT          (L)
1 #FROM-IM          (L)
1 #AT-V-MEMBER-CN   (A6)
1 #SO-MBR           (L)
1 #TERMED-MBR       (L)
1 #RESTRICT-MBR-SW  (A1)
1 #SUSPEND-CODE     (A1)
1 #VT-TYPE    (A2)  /* SS/SE/ST
1 #VT-TERM    (N2)
1 REDEFINE #VT-TERM
  2 #VT-TERM-A (A2)
1 #VT-RIDER   (A1)  /* (Y/N)
1 #RATED-CLASS     (N1)           /* NG5
1 REDEFINE #RATED-CLASS
  2 #RATED-CLASS-A  (A1)
*
LOCAL USING GRTL0002
LOCAL USING GRTL0003
LOCAL
1 #VT-RENEW        (A1)  INIT  <'Y'>     /* (Y/N)  NG1
1 YES              (A1)  CONST <'Y'>
1 NO               (A1)  CONST <'N'>
1 BLANK            (A1)  CONST <' '>
1 X                (A1)
1 #APREM           (N3.2/6,5)
1 #LINE-ITEMS      (A78) 1 REDEFINE #LINE-ITEMS
  2 #COL1   (A9/6)
1 #PO-PLAN-REQUEST (A1/10)
*
1 #CLASS1          (N4.2/3)
1 #CLASS2          (N4.2/3)
1 #CLASS3          (N4.2/3)
*
1 #RATED-TEXT  (A15)
1 #HOLD-SCR-COMMAND  (A2)
1 #SEL-RT-COMP    (A1)
1 #SEL-RT-YEARS   (N2)
1 #REM-YEARS      (N3)
1 #BEST-PLAN      (A5)
1 #DIFF         (N5.2)
1 #REFUND         (N6.2)
1 #RT-REFUND-RATE (N1.2) INIT <0.30>
1 #RT-REFUND-RATE-2008 (N1.2) INIT <0.10>      /* PM10
1 #LT-REFUND-RATE (N1.2) INIT <0.20>
1 #DOING-RATE-COMPARE (L)
1 #TOT-YEARS    (N2)
1 REDEFINE #TOT-YEARS
  2 #TOT-YEARS-A (A2)
1 #TOT-RT-PREM       (N5.2)
1 #TOT-RT-PREM-W-REF (N5.2)
1 #TOT-LV2-PREM      (N5.2)
*
1 MR-V VIEW OF A-REQUEST
  2 ZIP-CODE         (N5)
  2 REDEFINE ZIP-CODE
    3 FIR-IND         (N1)
    3 MSG-IND         (N1)
    3 RET-IND         (N1)
    3 FIN-IND         (N1)
    3 FAS-IND         (N1)
  2 RQST-TABLE       (A10)
  2 REDEFINE RQST-TABLE
    3 BEN-IND         (A1)
    3 MAL-IND         (A1)
    3 BEN-SHO-IND     (A1)
    3 ACC-IND         (A1)
    3 CAP-IND         (A1)
    3 APP-IND         (A1)
    3 PIL-IND         (A1)
    3 CO-IND          (A1)
    3 VP-IND          (A1)
    3 MS-IND          (A1)
  2 ASSUM-RET-DATE   (N8)
  2 REDEFINE ASSUM-RET-DATE
    3 ASSUM-RET-DATE-X  (N2/4)
  2 ASSUM-RET-DATE-2 (N8)
  2 REDEFINE ASSUM-RET-DATE-2
    3 ASSUM-RET-DATE-2-X (N1/8)
  2 REDEFINE ASSUM-RET-DATE-2
    3 ASSUM-RET-DATE-2-2X (N2/4)
  2 ASSUM-RET-DATE-3 (N8)
  2 REDEFINE ASSUM-RET-DATE-3
    3 ASSUM-RET-DATE-3-X (N1/8)
  2 REDEFINE ASSUM-RET-DATE-3
    3 ASSUM-RET-DATE-3-2X (N2/4)
*
1 #AGE-INELIGIBLE (L)
1 #MAX-INELIGIBLE (L)
1 #MR-OVERRIDES (L)
1 #MR-VP-DUES   (L)
1 #SCR-CV       (C)
1 #HIGH-MAX     (L)
1 #LOW-MAX     (L)
1 #AGE-4         (P4)
1 #NUM-FACES     (P3)
1 #NUM-PLANS     (P3)
1 #TOT-SELS      (P3)
1 #VT-PREM-LINE  (A78)
1 REDEFINE #VT-PREM-LINE
  2 #VT-PREM   (A9/3)
  2 #VT-LBL    (A51)
*
1 #MON-PREM      (N6.2)
1 #MON-DUES      (N3.2)
1 #TOT-PMT       (N7.2)      /*tt1
1 #2-MON-PMT     (N7.2)      /*tt1
1 #MON-PREM-X    (A9)
1 #MON-DUES-X    (A5)
1 #TOT-PMT-X     (A10)
1 #2-MON-PMT-X   (A9)
1 #PMT-8   (A8)
*
1 #RID   (A20)
1 #VAR-TERM   (L)
1 #VT-PLAN    (A3)
1 REDEFINE #VT-PLAN
  2 #VT-PLAN-A1 (A1)
  2 #VT-PLAN-N2 (N2)
  2 REDEFINE #VT-PLAN-N2
    3 #VT-PLAN-A2 (A2)
*
1 #SEL-X         (I2)
1 #FAC-X         (A8)
1 #SP-PREM-X     (A11)
1 #SP-DUES-X     (A7)
1 #SP-TOT-PMT-X  (A12)
1 #LT-AGE        (N2)
1 #MAX-AGE       (N2)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (N2)
  2 #MM      (N2)
  2 #DD      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A  (A4)
  2 #MM-A    (A2)
  2 #DD-A    (A2)
*
1 #I1          (I2)
1 #I2          (I2)
1 #I3   (I2)
1 #I4   (I2)
*
1 #DIVIDEND   (N5.4)
1 REDEFINE #DIVIDEND
  2 #DIV-5    (N5)
  2 #REM      (N4)
*
1 #PREM         (N6.2)
1 #REM-FACE     (N7)
1 #REM-FACE-TERM (N7)    /* PAM - new maximums
*
1 #MONTHS (A36)  INIT
  <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
*
* below is linkage to GPREMSXX
1 #SCR-COMMAND (A2)
1 #SCR-ID      (N9)
1 #SCR-SUFFIX  (A3)
1 #PLAN        (A3)
1 REDEFINE #PLAN
  2 #PLAN-A1    (A1)
  2 #PLAN-A2    (A2)
1 #AGE         (N2)

1 #APRM        (N7.2)
1 #ISSUE-DATE  (N8)
1 #FACE-AMT    (N7.2)
* above is linkage to GPREMSXX
*
1 #ID-NUMBER    (N6)                    /* PM10
1 REDEFINE #ID-NUMBER                   /* PM10
  2 #ID-NUMBER-A (A6)                   /* PM10
1 #SPACEN       (A27) /* Only in case users require different map layout  NG5
1 #OLTEXT       (A27) /* Only in case users require different map layout  NG5
*
1 #MAX-GP2-AGE  (N2)CONST  <14>       /* TT2 CURRENTLY 14 YEARS OLD
* 1 #DOB-YYYYMMDD     (N8)      /* TT1
* 1 REDEFINE #DOB-YYYYMMDD
*   2 #DOB-YYYY       (N4)
*   2 #DOB-MMDD       (N4)
 END-DEFINE
* * * * * * * * *
*
RESET #DESIRED-LINES (*)
*
IF *DEVICE = 'BATCH'
    OR (#PDA-SCR-SUFFIX = 'VP')  /* from aoap005e on mr vp
  RESET #MR-OVERRIDES
  IF #PDA-SCR-COMMAND = 'MR' /* FROM AOAP005A/MR APP OR 005E/MR VP
    FIND (1) MR-V WITH ID-NUMBER = #PDA-SCR-ID
      IF ((APP-IND NE BLANK) OR (VP-IND NE BLANK))
          AND (RET-IND = 0)   /* assum-ret-date would have dates
          AND (BEN-IND = BLANK) /* assum-ret-date would have ben info
          AND ((ASSUM-RET-DATE NE 0) OR (ASSUM-RET-DATE-2 NE 0))
        MOVE TRUE TO #MR-OVERRIDES
      END-IF
    END-FIND
  END-IF
*
  IF NOT #MR-OVERRIDES
    ESCAPE ROUTINE
  END-IF
*
  IF #PDA-SCR-SUFFIX = 'VP'
    FOR #I1 1 TO 3
      IF   (ASSUM-RET-DATE-X (#I1) NE 0)
          AND (ASSUM-RET-DATE-3-X (#I1 + 4) NE 0) /* NEEDS APP
        MOVE 'X' TO #DESIRED-PLAN (1) /* KEEP AT INDEX 1 FOR APP TO WORK
        COMPUTE #DESIRED-FACE-AMT (#I1) = 5000 * ASSUM-RET-DATE-X (#I1)
*
        IF ASSUM-RET-DATE-2-2X (#I1) = 0  /* NSP
          MOVE 'SI' TO #PLAN
        ELSE
          IF ASSUM-RET-DATE-2-2X (#I1) = 99
            MOVE 'OL' TO #PLAN
          ELSE
            IF ASSUM-RET-DATE-2-2X (#I1) = 7
              MOVE '7P' TO #PLAN
            ELSE
              MOVE ASSUM-RET-DATE-2-2X (#I1) TO #PLAN   /* n2 to a2
*
              IF #PLAN = '20' OR = '30'
                COMPRESS #PLAN 'P' INTO #PLAN LEAVING NO
              END-IF
            END-IF
          END-IF
        END-IF
*
        IF ASSUM-RET-DATE-3-X (#I1) = 2
          MOVE 'S' TO #SMOKER-FLAG
        ELSE
          MOVE 'N' TO #SMOKER-FLAG
        END-IF
*
        IF ASSUM-RET-DATE-3-X (#I1 + 4) = 2
          MOVE TRUE TO #MR-VP-DUES
        ELSE
          RESET #MR-VP-DUES
        END-IF
*
        MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD
        SUBTRACT #DATE-OF-BIRTH FROM #YYYYMMDD
        MOVE #YY TO #AGE
        MOVE *DATN TO #ISSUE-DATE
*
        PERFORM FORMAT-OPTIONS
      END-IF
    END-FOR
    ESCAPE ROUTINE
  ELSE
    MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD
    SUBTRACT #DATE-OF-BIRTH FROM #YYYYMMDD
    MOVE #YYYY TO #CURR-AGE
*
    MOVE 'N' TO #SMOKER-FLAG
    IF ASSUM-RET-DATE-3-2X (1) = 2
      MOVE 'S' TO #SMOKER-FLAG
    END-IF
*
    FOR #I1 1 TO 3
      IF ASSUM-RET-DATE-X (#I1) NE 0
        COMPUTE #DESIRED-FACE-AMT (#I1) = 5000 * ASSUM-RET-DATE-X (#I1)
      END-IF
    END-FOR
*
    FOR #I1 1 TO 8
      IF ASSUM-RET-DATE-2-X (#I1) NE 0
        MOVE 'X' TO #DESIRED-PLAN (#I1)
*
        IF #I1 = 8   /* VAR TERM
          MOVE ASSUM-RET-DATE-3-2X (2) TO #VT-TERM
          DECIDE ON FIRST VALUE ASSUM-RET-DATE-2-X (8)
            VALUE 1 MOVE 'ST' TO #VT-TYPE MOVE 'N' TO #VT-RIDER
            VALUE 2 MOVE 'SE' TO #VT-TYPE MOVE 'N' TO #VT-RIDER
            VALUE 3 MOVE 'SS' TO #VT-TYPE MOVE 'N' TO #VT-RIDER
            VALUE 4 MOVE 'ST' TO #VT-TYPE MOVE 'Y' TO #VT-RIDER
            VALUE 5 MOVE 'SE' TO #VT-TYPE MOVE 'Y' TO #VT-RIDER
            VALUE 6 MOVE 'SS' TO #VT-TYPE MOVE 'Y' TO #VT-RIDER
            VALUE 7 MOVE '**' TO #VT-TYPE MOVE 'N' TO #VT-RIDER
            VALUE 8 MOVE '**' TO #VT-TYPE MOVE 'Y' TO #VT-RIDER
            NONE IGNORE
          END-DECIDE
        END-IF
      END-IF
    END-FOR
  END-IF
ELSE
  IF #PDA-SCR-COMMAND = 'MR'
    MOVE (AD=P) TO #SCR-CV #SCN-SPS-GEN-CV
  END-IF
*
*   #DOB-YYYYMMDD := #DATE-OF-BIRTH                             /* TT1
*   COMPRESS FULL #DOB-MMDD #DOB-YYYY INTO #SCN-DOB LEAVING NO       /* TT1
  MOVE #PDA-SCR-COMMAND TO #HOLD-SCR-COMMAND
  MOVE '*' TO #PDA-SCR-COMMAND
  RESET #DESIRED-PLAN (*) #LOW-MAX #HIGH-MAX   /* DW2
  IF  (#FIRST-TIME) AND (#PDA-SCR-ID = 0) AND (#PDA-SCR-COMMAND NE 'MR')
    INPUT MARK *#SEX USING MAP 'C2151MPO'                         /* PM5
  ELSE
    IF #DESIRED-FACE-AMT (1) NE 0  /* VT ** return
      INPUT MARK *#VT-TERM USING MAP 'C2151MPO'
    ELSE
      INPUT MARK *#DESIRED-FACE-AMT (1) USING MAP 'C2151MPO'
    END-IF
  END-IF
  #DESIRED-PLAN (1:6) := #PO-PLAN-REQUEST (1:6)
  #DESIRED-PLAN (10) := #PO-PLAN-REQUEST (8)
  #DESIRED-PLAN (8)  := #PO-PLAN-REQUEST (10)
  #DESIRED-PLAN (9)  := #PO-PLAN-REQUEST (7)
  #DESIRED-PLAN (7)  := #PO-PLAN-REQUEST (9)
*
  RESET #FIRST-TIME
  IF #PDA-SCR-COMMAND NE '*'
    MOVE 'G1000PXX' TO #ERR-TEXT
    ESCAPE ROUTINE                      /* >>>>>>>>>>>>>>  Exit
  END-IF
*
  IF #SEL-RT-COMP = 'Y' OR = 'X'
    MOVE TRUE TO #DOING-RATE-COMPARE
    MOVE 'X' TO #DESIRED-PLAN (6)   /* rt
  END-IF
*
  IF #SCN-AS-OF-DT NE MASK (MMDDYYYY)
    REINPUT 'As Of Date must be in format MMDDYYYY'
      MARK *#SCN-AS-OF-DT
  END-IF
*
  COMPRESS #SCN-AOD-YYYY #SCN-AOD-MMDD INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO #SCN-AOD-YYYYMMDD
*
  IF NOT (#SCN-SPS-GEN = BLANK OR = 'S' OR = 'K' OR = 'G')
    REINPUT 'Must be S/K/G or blank'
      MARK *#SCN-SPS-GEN
  END-IF
  MOVE #SCN-SPS-GEN TO #INTRO-FLAG
*
  COMPRESS #SCN-DOB-YYYY #SCN-DOB-MMDD INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO #DATE-OF-BIRTH
*
  IF  (#SCN-DOB NE MASK (MMDDYYYY))
      OR (#DATE-OF-BIRTH LE 19000000)
    REINPUT 'DOB must be in format MMDDYYYY'
      MARK *#SCN-DOB
  END-IF
*
  IF #DATE-OF-BIRTH GE *DATN
    REINPUT 'DOB cannot be in the future'
      MARK *#SCN-DOB
  END-IF
*
  CALLNAT 'GMAXSXX'     /* obtain maximum amounts per insurance line
    #PDA-SCR-COMMAND
    #MAX-FACE
    #MAX-FACE-TERM
    #OVERRIDE-MAX-FACE-TOTAL
    #OVERRIDE-MAX-FACE-TERM
    #INTRO-FLAG
    #DATE-OF-BIRTH
*
  RESET #HIGH-MAX #LOW-MAX
  IF  (#DESIRED-PLAN (6) NE BLANK)  /* term
      OR (#DESIRED-PLAN (7) NE BLANK)
      OR (#DESIRED-PLAN (8) NE BLANK)
    MOVE TRUE TO #LOW-MAX
  END-IF
*
  IF  (#DESIRED-PLAN (1) NE BLANK)  /* whole-life
      OR (#DESIRED-PLAN (2) NE BLANK)
      OR (#DESIRED-PLAN (3) NE BLANK)
      OR (#DESIRED-PLAN (4) NE BLANK)
      OR (#DESIRED-PLAN (5) NE BLANK)
    MOVE TRUE TO #HIGH-MAX
  END-IF
*
  COMPUTE #REM-FACE = #MAX-FACE - #TOT-FACE
  COMPUTE #REM-FACE-TERM = #MAX-FACE-TERM - #TOT-FACE-TERM   /* PAM
  COMPUTE #DIVIDEND = #REM-FACE / 5000                       /* new max
  IF #REM NE 0
    COMPUTE #REM-FACE = 5000 * (#DIV-5 + 1)
  END-IF
*
  MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD                                               /* TT2 BEGIN
  SUBTRACT #DATE-OF-BIRTH FROM #YYYYMMDD
  MOVE #YYYY TO #CURR-AGE
*
  FOR #I1 1 TO 3
    IF #DESIRED-PLAN (9) NE BLANK            /* GEN PLUS 2  
     IF ( #CURR-AGE > #MAX-GP2-AGE ) THEN
      REINPUT 'For PLAN GP2 Age must be between 15 days and 14 years old.'
          MARK *#SCN-DOB
      END-IF
*      
      IF NOT #DESIRED-FACE-AMT (#I1) = 10000 OR = 15000 OR = 20000 OR = 25000
          OR = 30000 OR = 0 OR = 40000 OR = 50000
        REINPUT 'For PLAN GP2 Face Amount must be 10/15/20/25/30/40/50 k'
          MARK *#DESIRED-FACE-AMT (#I1)
      END-IF
    END-IF                                                                          /* TT2 END
*
    IF   (#DESIRED-FACE-AMT (#I1) GT 0)
      COMPUTE #DIVIDEND = #DESIRED-FACE-AMT (#I1) / 5000
      IF (#REM NE 0)
        REINPUT 'Face amt must be multiple of $5k between $10k & $800K for Term and $1,000K WL'
          MARK *#DESIRED-FACE-AMT (#I1)
      END-IF
*
      IF ((#DESIRED-FACE-AMT (#I1) GT #MAX-FACE) AND (#PDA-SCR-SUFFIX NE 'OVR'))
        REINPUT 'Face ammount cannot be Greater then 1,000,000 for WL and 800,000 for Term'     /*tt1
          MARK *#DESIRED-FACE-AMT (#I1)
      END-IF
*
      IF ((#DESIRED-FACE-AMT (#I1) GT #REM-FACE) AND (#PDA-SCR-SUFFIX NE 'OVR')) THEN
        REINPUT 'The Desired Face Amount Cannot be Greater than Remaing face allowed'
          MARK *#DESIRED-FACE-AMT (#I1)
      END-IF
*
      IF ((#LOW-MAX AND NOT #HIGH-MAX) /* PAM - only selected term ins
          AND (#DESIRED-FACE-AMT (#I1) GT #REM-FACE-TERM OR
          #DESIRED-FACE-AMT (#I1) GT #MAX-FACE-TERM)
          AND (#PDA-SCR-SUFFIX NE 'OVR'))
        REINPUT
          'Ineligible or Face amt must be between $10k & MAX for TERM ins'
          MARK *#DESIRED-FACE-AMT (#I1)
      END-IF
    END-IF
  END-FOR
*
  IF NOT #SMOKER-FLAG = 'S' OR = 'N'
    REINPUT 'Smoker indicator must be "S" or "N"'
      MARK *#SMOKER-FLAG
  END-IF
*
  IF NOT #SEX = 'M' OR = 'F'
    REINPUT 'Sex must be "M" or "F"'
      MARK *#SEX
  END-IF
*
  MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD
  SUBTRACT #DATE-OF-BIRTH FROM #YYYYMMDD
  MOVE #YYYY TO #CURR-AGE
*
  IF (#SMOKER-FLAG = 'S') AND (#CURR-AGE LT 16)
    REINPUT 'SMOKERS under age 16 invalid'
      MARK *#SCN-DOB
  END-IF
*
  IF #DESIRED-PLAN (6) NE BLANK /* RT
    IF (((#SMOKER-FLAG = 'S') AND (#CURR-AGE LE 35))
        OR ((#SMOKER-FLAG = 'N') AND (#CURR-AGE LE 45)))
        OR (#INTRO-FLAG = 'K' OR = 'G')    /* DW3
      REINPUT 'RT not recommended'
        MARK *#DESIRED-PLAN (6)
    END-IF
  END-IF
*
  IF   (#DESIRED-PLAN (7) NE BLANK) /* LT
    IF  ((#SMOKER-FLAG = 'S') AND (#CURR-AGE GE 40)) /* chgd from 38
        OR ((#SMOKER-FLAG = 'N') AND (#CURR-AGE GE 50)) /* on 6/2003
      REINPUT 'Too old for LT'
        MARK *#DESIRED-PLAN (7)
    END-IF
  END-IF
*
  IF #RATED-CLASS NE 0
    IF #RATED-CLASS GT 3
      REINPUT 'Only Class 1/2/3 is valid'
        MARK *#VT-TYPE
    END-IF
*
    IF  (#DESIRED-PLAN (6) NE BLANK)
        OR (#DESIRED-PLAN (7) NE BLANK)
        OR (#DESIRED-PLAN (8) NE BLANK)
        OR (#DESIRED-PLAN (9) NE BLANK)
*                                        NG5 commented out
*      IF  (#DESIRED-PLAN (2) NE BLANK)
*          OR (#DESIRED-PLAN (3) NE BLANK)
*          OR (#DESIRED-PLAN (4) NE BLANK)
*          OR (#DESIRED-PLAN (6) NE BLANK)
*          OR (#DESIRED-PLAN (7) NE BLANK)
      REINPUT 'Only Plan OL/SP/LTII can have Sub-premiums'
        MARK *#DESIRED-PLAN (1)
    END-IF
  END-IF
*
  IF   (#SCN-SPS-GEN = 'K' OR = 'G')  /* DW2
      AND (#CURR-AGE LT 18)
      AND ((#DESIRED-PLAN (6) NE BLANK) /* RT
      OR  (#DESIRED-PLAN (7) NE BLANK)) /* LT
    REINPUT 'GEN TERM Minium Age is 18'
      MARK *#DESIRED-PLAN (8)
  END-IF
*
END-IF
*
RESET #VT-PLAN #VAR-TERM
IF #DESIRED-PLAN (8) NE BLANK  /* LTII or LTII+ plan
  MOVE TRUE TO #VAR-TERM
*
  IF #DESIRED-FACE-AMT (1) LT 50000
    REINPUT '$50,000 is minimum Level Term II'
      MARK *#DESIRED-FACE-AMT (1)
  END-IF
*
  IF #VT-TERM = 0
    REINPUT 'Term required for VT'
      MARK *#VT-TERM
  END-IF
*
  IF #VT-RENEW = YES      /* NG1
    #VT-RIDER := 'R'      /* There is no Rider for Annual re-new LTII
  END-IF
*
  IF #VT-RIDER = BLANK    /* NG2
    #VT-RIDER := 'N'
  END-IF
  IF NOT #VT-RIDER = 'Y' OR = 'N' OR= 'R'
    REINPUT 'Y or N for rider'
      MARK *#VT-RIDER
  END-IF
*
  DECIDE ON FIRST VALUE #VT-TYPE
    VALUE '**'
      IF #VT-RIDER = 'Y'
        MOVE '#' TO #VT-PLAN-A1  /* #=rider
      ELSE
        MOVE '*' TO #VT-PLAN-A1  /* *=no rider
      END-IF
*
    VALUE 'ST'
      IF #VT-RIDER = 'Y'
        MOVE 'X' TO #VT-PLAN-A1
      ELSE
        IF #VT-RENEW = YES      /* NG1
          MOVE 'S' TO #VT-PLAN-A1
        ELSE
          MOVE 'U' TO #VT-PLAN-A1
        END-IF
      END-IF
*
    VALUE 'SE'
      IF #VT-RIDER = 'Y'
        MOVE 'Y' TO #VT-PLAN-A1
      ELSE
        IF #VT-RENEW = YES      /* NG1
          MOVE 'R' TO #VT-PLAN-A1
        ELSE
          MOVE 'V' TO #VT-PLAN-A1
        END-IF
      END-IF
*
    VALUE 'SS'
      IF #VT-RIDER = 'Y'
        MOVE 'Z' TO #VT-PLAN-A1
      ELSE
        IF #VT-RENEW = YES      /* NG1
          MOVE 'Q' TO #VT-PLAN-A1
        ELSE
          MOVE 'W' TO #VT-PLAN-A1
        END-IF
      END-IF
*
    NONE
      REINPUT 'Type is required (SS/SE/ST/**) for VT'
        MARK *#VT-TYPE
  END-DECIDE
*
  IF   (#RATED-CLASS NE 0)
      AND (#VT-TYPE NE 'ST')
    REINPUT 'Only "ST" is eligible for LT2 Rated Rates'
      MARK *#VT-TYPE
  END-IF
*
  COMPRESS #VT-PLAN-A1 #VT-TERM-A INTO #VT-PLAN LEAVING NO
*
*   edits below are also in c1000pat/c2000cnm/d3185sba
  IF #CURR-AGE LT 18
    REINPUT 'VAR TERM Minium Age is 18'
      MARK *#DESIRED-PLAN (8)
  END-IF
*                                         NG1 Start
  DECIDE FOR FIRST CONDITION
    WHEN #VT-PLAN-A1 = 'U'  OR= 'S'  /* Standard
      IF (#VT-PLAN-N2 LT 5) OR (#VT-PLAN-N2 GT 65)
        REINPUT 'VAR TERM Term must be between 05 and 65'
          MARK *#DESIRED-PLAN (8)
      END-IF
    WHEN #VT-PLAN-A1 = 'X'           /* Standard Rider
      IF (#VT-PLAN-N2 LT 11) OR (#VT-PLAN-N2 GT 55)
        REINPUT 'VAR TERM Rider Term must be between 11 and 55'
          MARK *#DESIRED-PLAN (8)
      END-IF
    WHEN #VT-PLAN-A1 = 'V' OR = 'W' OR= 'R' OR= 'Q' OR = '*' /* Select or SS or All
      IF (#VT-PLAN-N2 LT 5) OR (#VT-PLAN-N2 GT 30)  /* was 25
        REINPUT 'VAR TERM Term must be between 05 and 30'
          MARK *#DESIRED-PLAN (8)
      END-IF
    WHEN #VT-PLAN-A1 = 'U' OR = 'V' OR = 'W' OR='S' OR= 'R' OR='Q' OR = '*' /* Not Rider
      IF (#CURR-AGE + #VT-PLAN-N2 GT 85)
        REINPUT 'VAR TERM Iss Age + Term cannot exceed 85'
          MARK *#DESIRED-PLAN (8)
      END-IF
    WHEN #VT-PLAN-A1 = 'Y' OR = 'Z' /* Select or Super Select with rider
      IF (#VT-PLAN-N2 LT 11) OR (#VT-PLAN-N2 GT 30)  /* WAS 25
        REINPUT 'VAR TERM Term must be between 11 and 30'
          MARK *#DESIRED-PLAN (8)
      END-IF
    WHEN #VT-PLAN-A1 = 'Q' OR= 'W' OR = 'Z' /* Select or Super Select with rider
        AND (#SMOKER-FLAG = 'S')
      REINPUT 'Super Select invalid for Smokers'
        MARK *#DESIRED-PLAN (8)
    WHEN NONE IGNORE
  END-DECIDE
*                                        NG1 End
END-IF
*
RESET #NUM-FACES #NUM-PLANS #TOT-SELS
*
IF #DESIRED-PLAN(10) NE BLANK          /* NG4
  #DESIRED-FACE-AMT (1) := 10000
END-IF
FOR #I1 1 TO 10
  IF #DESIRED-PLAN (#I1) NE BLANK
    ADD 1 TO #NUM-PLANS
  END-IF
END-FOR
*
FOR #I1 1 TO 3
  IF #DESIRED-FACE-AMT (#I1) GT 0
    ADD 1 TO #NUM-FACES
  END-IF
END-FOR
COMPUTE #TOT-SELS = #NUM-FACES * #NUM-PLANS
*
IF   (#RATED-CLASS NE 0)
    AND (#TOT-SELS GT 15)        /* NG5
  REINPUT 'Only 3 combinations can be selected with Rated Requests'
    MARK *#DESIRED-PLAN (1)
END-IF
*
IF #VAR-TERM
  IF #VAR-TERM AND #TOT-SELS GT 3
    REINPUT 'Only 3 combinations can be selected with Var Term'
      MARK *#DESIRED-PLAN (1)
  END-IF
*
  IF (#VT-PLAN-A1 = '*' OR = '#') AND (#NUM-PLANS GT 1)
    REINPUT 'Only 1 Plan can be selected with Var Term **'
      MARK *#DESIRED-PLAN (1)
  END-IF
END-IF
*
IF #TOT-SELS = 0 /* show defaults if No combinations
    AND (NOT #DOING-RATE-COMPARE)
  ESCAPE ROUTINE
END-IF
*
MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD
COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #SCR-AOD-X WITH '-'
*
MOVE #SCN-AOD-YYYYMMDD TO #YYYYMMDD
SUBTRACT #DATE-OF-BIRTH FROM #YYYYMMDD
MOVE #YY TO #AGE
MOVE #SCN-AOD-YYYYMMDD TO #ISSUE-DATE
* MOVE *DATN TO #ISSUE-DATE
*
IF #INTRO-FLAG = 'K' OR = 'G'                                     /* PM10
  IF #PDA-SCR-ID NE 0 AND #PDA-SCR-ID LT 500000                   /* PM10
    #ID-NUMBER := #PDA-SCR-ID                                     /* PM10
  ELSE                                                            /* PM10
    #ID-NUMBER-A := #AT-V-MEMBER-CN                               /* PM10
  END-IF                                                          /* PM10
END-IF                                                            /* PM10
*
IF #INTRO-FLAG = 'K' OR = 'G'
    AND FUNCTION-GEN23-INSURE(<#ID-NUMBER-A>) = FALSE                /* PM10
  MOVE 24 TO #MAX-AGE
ELSE
  IF  (#INTRO-FLAG = 'S')
      OR #PDA-SCR-ID NE 0
*      OR ((#PDA-SCR-ID NE 0) AND (#PDA-SCR-ID LT 500000))  /* CUR MBR
    MOVE 80 TO #MAX-AGE
  ELSE
    IF #VAR-TERM
*     MOVE 71 TO #MAX-AGE                                         /* PM6
      MOVE 76 TO #MAX-AGE                                         /* PM6
    ELSE
      MOVE 66 TO #MAX-AGE
    END-IF
  END-IF
END-IF
*
IF ((#DESIRED-PLAN (2) NE BLANK)
    OR (#DESIRED-PLAN (3) NE BLANK)
    OR (#DESIRED-PLAN (4) NE BLANK)
    OR (#DESIRED-PLAN (6) NE BLANK)
    OR (#DESIRED-PLAN (7) NE BLANK))
    AND (#AGE GT 69)                                                              /* vt1 was 70
  REINPUT 'Only Plan OL/SP/LTII can be over 69'                   /* PM6          /* vt1 was over 70
    MARK *#DESIRED-PLAN (1)
END-IF
*
**IF   (#AGE GE #MAX-AGE) /* chged from gt to ge 10/24/2000
IF #AGE GT #MAX-AGE
    AND (*DEVICE NE 'BATCH')
  REINPUT 'Ineligible due to excessive age'
    MARK *#SCN-DOB
END-IF
*
RESET #INELIGIBLE
*
IF #PDA-SCR-SUFFIX = 'SPS' OR = 'AS' OR = 'ASN' OR = 'AST' OR = 'ASG'
    OR = 'GEN'
**  IF (#SCN-AOD-YYYYMMDD - (#MAX-AGE * 10000)) GE #DATE-OF-BIRTH
  IF #AGE GT #MAX-AGE
    MOVE TRUE TO #AGE-INELIGIBLE #INELIGIBLE
  ELSE
    IF (#PDA-SCR-SUFFIX = 'SPS')
        AND (#SUSPEND-CODE = '7' OR = '8' OR = '9') /* INS REJ/DEF INS
      MOVE TRUE TO #INELIGIBLE
    END-IF
  END-IF
ELSE
**  IF (#SCN-AOD-YYYYMMDD - (#MAX-AGE * 10000)) GE #DATE-OF-BIRTH
  IF #AGE GT #MAX-AGE
    MOVE TRUE TO #AGE-INELIGIBLE #INELIGIBLE
  ELSE
    IF #TOT-FACE GE #MAX-FACE      /* ALREADY HAS MAX COVERAGE
      MOVE TRUE TO #MAX-INELIGIBLE #INELIGIBLE
    ELSE
      IF (#SUSPEND-CODE = '5' OR = '6' OR = '9')  /* INS REJ/DEF INS
          OR (#RESTRICT-MBR-SW = 'Y')        /* RESTRICTED (SPC CATEGORY)
*     OR (#HAS-ETI)                    /* HAS AN ETI POLICY
        MOVE TRUE TO #INELIGIBLE
      END-IF
    END-IF
  END-IF
END-IF
*
IF #PDA-SCR-SUFFIX = 'OVR'  /* allow max insurance
  RESET #INELIGIBLE
END-IF
*
IF *DEVICE NE 'BATCH'
  IF #AGE-INELIGIBLE
    REINPUT 'Ineligible for Insurance due to age'
  ELSE
    IF #MAX-INELIGIBLE
      REINPUT 'Ineligible for Insurance due to maximum coverage'
    ELSE
      IF #INELIGIBLE
        REINPUT 'Ineligible for Insurance'
      END-IF
    END-IF
  END-IF
END-IF
*
IF #SMOKER-FLAG = 'S'
  MOVE 40 TO #LT-AGE
ELSE
  MOVE 50 TO #LT-AGE
END-IF
*
IF #DOING-RATE-COMPARE
  IF #NUM-FACES NE 1
    REINPUT '1 and only 1 Face Amt must be selected on a rate compare'
      MARK *#DESIRED-FACE-AMT (1)
  END-IF
*
  IF (#SEL-RT-YEARS LT 5) AND (#SEL-RT-YEARS NE 0)
    REINPUT 'Term must be at least 5 yeras'
      MARK *#SEL-RT-YEARS
  END-IF
*
  MOVE TRUE TO #DOING-RATE-COMPARE
  MOVE #DESIRED-FACE-AMT (1) TO #FACE-AMT
  MOVE 'RT'      TO #PLAN
  MOVE #CURR-AGE TO #AGE
  RESET #TOT-YEARS
  FOR #I1 1 TO 10
    IF #AGE GT 70
      ESCAPE BOTTOM
    END-IF
*
    PERFORM CALLNAT-GPREMSXX
*
    IF ((#TOT-YEARS + 5) GE #SEL-RT-YEARS)
        AND (#SEL-RT-YEARS NE 0)
      COMPUTE #REM-YEARS = #SEL-RT-YEARS - #TOT-YEARS
      ADD (#APRM * #REM-YEARS) TO #TOT-RT-PREM
*
      IF #SMOKER-FLAG = 'N'
        IF #SCN-AOD-YYYYMMDD GE 20080101                          /* PM8
          COMPUTE ROUNDED #REFUND = #APRM * #RT-REFUND-RATE-2008  /* PM8
        ELSE                                                      /* PM8
          COMPUTE ROUNDED #REFUND = #APRM * #RT-REFUND-RATE
        END-IF                                                    /* PM8
      ELSE
        RESET #REFUND
      END-IF
*
      ADD ((#APRM - #REFUND) * #REM-YEARS) TO #TOT-RT-PREM-W-REF
      ADD #REM-YEARS TO #TOT-YEARS
      ESCAPE BOTTOM
    END-IF
*
    ADD (#APRM * 5)  TO #TOT-RT-PREM
*
    IF #SMOKER-FLAG = 'N'
      IF #SCN-AOD-YYYYMMDD GE 20080101                            /* PM8
        COMPUTE ROUNDED #REFUND = #APRM * #RT-REFUND-RATE-2008    /* PM8
      ELSE                                                        /* PM8
        COMPUTE ROUNDED #REFUND = #APRM * #RT-REFUND-RATE
      END-IF                                                      /* PM8
    ELSE
      RESET #REFUND
    END-IF
*
    ADD ((#APRM - #REFUND) * 5) TO #TOT-RT-PREM-W-REF
*
    ADD 5     TO #TOT-YEARS
    ADD 5 TO #AGE    /* for next 5-year loop
  END-FOR
*
  MOVE #CURR-AGE TO #AGE
  COMPRESS 'V' #TOT-YEARS-A INTO #PLAN LEAVING NO
  PERFORM CALLNAT-GPREMSXX
  COMPUTE #TOT-LV2-PREM = #PREM * 12 * #TOT-YEARS
  COMPUTE #DIFF = #TOT-LV2-PREM - #TOT-RT-PREM-W-REF
  IF #TOT-LV2-PREM LT #TOT-RT-PREM
    MULTIPLY #DIFF BY -1
    MOVE 'LV-II' TO #BEST-PLAN
  ELSE
    MOVE 'RT' TO #BEST-PLAN
  END-IF
*
  MOVE 'PO' TO #PDA-SCR-COMMAND
  INPUT USING MAP 'C2158MPO'
*
  IF #PDA-SCR-COMMAND NE '*'
    MOVE 'G1000PXX' TO #ERR-TEXT
    ESCAPE ROUTINE
  END-IF
END-IF
*                                       NG Start
RESET #SEL-X
IF #DESIRED-PLAN(1) NE BLANK
    OR #DESIRED-PLAN(2) NE BLANK
    OR #DESIRED-PLAN(3) NE BLANK
    OR #DESIRED-PLAN(4) NE BLANK
    OR #DESIRED-PLAN(5) NE BLANK
  IGNORE
ELSE
  DECIDE FOR FIRST CONDITION
    WHEN #DESIRED-PLAN(8) NE BLANK
      MOVE #VT-PLAN TO #PLAN
      MOVE TRUE TO #VAR-TERM
      PERFORM LTII-PLAN
    WHEN #DESIRED-PLAN(10) NE BLANK
      MOVE 'EZ'  TO #PLAN
      PERFORM EZ-TERM
    WHEN #DESIRED-PLAN(1) NE BLANK /* Only in case users require different map layout
      MOVE 'OL'  TO #PLAN
      #OLTEXT := ' Pay for life'
      PERFORM VAWL-TERM
    WHEN #DESIRED-PLAN(2) NE BLANK
      MOVE '30P'  TO #PLAN
      #OLTEXT := ' Pay for 30 years'
      PERFORM VAWL-TERM
    WHEN #DESIRED-PLAN(3) NE BLANK
      MOVE '20P'  TO #PLAN
      #OLTEXT := ' Pay for 20 years'
      PERFORM VAWL-TERM
    WHEN #DESIRED-PLAN(4) NE BLANK
      MOVE '7P'  TO #PLAN
      #OLTEXT := ' Pay for 7 years'
      PERFORM VAWL-TERM
    WHEN #DESIRED-PLAN(5) NE BLANK
      MOVE 'SI'  TO #PLAN
      #OLTEXT := ' One time payment'
      PERFORM VAWL-TERM
    WHEN ANY
      ESCAPE ROUTINE
    WHEN NONE
      IGNORE
  END-DECIDE
END-IF
*                                       NG End
FOR #I1 1 TO 3
  IF #DESIRED-FACE-AMT (#I1) NE 0
    FOR #I2 1 TO 10
      RESET #PLAN #VAR-TERM
      DECIDE ON FIRST VALUE #I2
        VALUE 1  IF #DESIRED-PLAN(#I2) NE BLANK MOVE 'OL'  TO #PLAN END-IF
        VALUE 2  IF #DESIRED-PLAN(#I2) NE BLANK MOVE '30P' TO #PLAN END-IF
        VALUE 3  IF #DESIRED-PLAN(#I2) NE BLANK MOVE '20P' TO #PLAN END-IF
        VALUE 4  IF #DESIRED-PLAN(#I2) NE BLANK MOVE '7P'  TO #PLAN END-IF
        VALUE 5  IF #DESIRED-PLAN(#I2) NE BLANK MOVE 'SI'  TO #PLAN END-IF
        VALUE 6  IF #DESIRED-PLAN(#I2) NE BLANK MOVE 'RT'  TO #PLAN END-IF
        VALUE 7  IF #DESIRED-PLAN(#I2) NE BLANK MOVE 'LT'  TO #PLAN END-IF
        VALUE 9  IF #DESIRED-PLAN(#I2) NE BLANK MOVE 'GP2' TO #PLAN END-IF
        NONE IGNORE
      END-DECIDE
      IF #PLAN = BLANK ESCAPE TOP END-IF
      IF   (#PLAN = 'RT' OR = 'LT')
          AND (#DESIRED-FACE-AMT (#I1) LT 50000)
        ESCAPE TOP
      END-IF
      IF #SEL-X GE 12
        ESCAPE BOTTOM
      END-IF
      PERFORM FORMAT-OPTIONS
    END-FOR
  END-IF
END-FOR
/*     Only in case users require different map layout  NG5
DEFINE VAWL-TERM
/*     =========
ADD 3 TO #SEL-X
FOR #I3 1 TO 3
  IF #DESIRED-FACE-AMT (#I3) NE 0
    MOVE EDITED #DESIRED-FACE-AMT (#I3) (EM=ZZZ,999) TO #FAC-X
    COMPRESS '$' #FAC-X INTO #FAC-X LEAVING NO
    MOVE #FAC-X TO #VT-PREM (#I3)
  END-IF
END-FOR
COMPRESS FULL #SPACEN #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
ADD 1 TO #SEL-X
#DESIRED-LINES (#SEL-X) := 'Value-Added Whole Life'
FOR #I3 1 TO 3
  IF #DESIRED-FACE-AMT (#I3) NE 0
    MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
    PERFORM CALLNAT-GPREMSXX
    COMPUTE #MON-PREM = #APRM / 12
    MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
    COMPRESS '$' #VT-PREM (#I3)
      INTO #VT-PREM (#I3) LEAVING NO
  END-IF
END-FOR
ADD 1 TO #SEL-X
COMPRESS FULL #OLTEXT #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
FOR #RATED-CLASS 1 3
  #I3 := 1
  REPEAT UNTIL #I3 > 3 OR #DESIRED-FACE-AMT (#I3) = 0
    MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
    PERFORM CALLNAT-GPREMSXX
    COMPUTE #MON-PREM = #APRM / 12
    MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
    COMPRESS '$' #VT-PREM (#I3)
      INTO #VT-PREM (#I3) LEAVING NO
    ADD 1 TO #I3
  END-REPEAT
  COMPRESS 'Class' #RATED-CLASS INTO #VT-LBL
  ADD 1 TO #SEL-X
  COMPRESS FULL #SPACEN #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
END-FOR
END-SUBROUTINE
*
DEFINE EZ-TERM
/*     =======
ADD 1 TO #SEL-X
COMPRESS 'EZ Term policy'  '.' INTO #DESIRED-LINES (#SEL-X) LEAVING NO
FA-RANGE (1:5) := FACE-OFFERED (1:5)  /* NG4
ADD 2 TO #SEL-X                       /* NG4
#COL1 (1) := 'Iss Age'
FOR #I3 1 TO 5
  #I4 := #I3 + 1
  MOVE EDITED FA-RANGE ( #I3 ) (EM=Z99,999) TO #COL1  (#I4)
END-FOR
#DESIRED-LINES (#SEL-X) := #LINE-ITEMS
*
##GENDER    := #SEX
##LTII-TYPE := 'EZ'   /* CURRENT KEY=XCRX99
##NICOTINE  := 'X'
RESET ##EFFECTIVE-DATE
MOVE EDITED *DATN (EM=99999999) TO #YYYYMMDD-A
RESET #I1
READ LTII-V BY LTII-KEY STARTING FROM #LTII-KEY
  IF LTII-TYPE NE ##LTII-TYPE
      OR GENDER NE ##GENDER
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > #YYYYMMDD-A
    ESCAPE TOP
  END-IF
  ADD 1 TO #SEL-X
  ADD 1 TO #I1
  COMPRESS AGE-RANGE (#I1,1) AGE-RANGE (#I1,2) INTO #COL1 (1) WITH DELIMITER '-'
  #I4 := 1
  FOR #I3 1 7
    IF #I3 = 2 OR= 4    /* Skip face amounts for accelerated death bene   NG4
      ESCAPE TOP
    END-IF
    ADD 1 TO #I4
    MOVE EDITED VAR-TERM-RATE (#I3) (EM=ZZ9.99) TO #COL1 (#I4)
  END-FOR
  #DESIRED-LINES (#SEL-X) := #LINE-ITEMS
END-READ
*
END-SUBROUTINE
*                           NG1 Start
DEFINE LTII-PLAN
/*     =========
ADD 1 TO #SEL-X
IF #VT-RENEW = YES                                            /* NG3
  COMPRESS 'I am applying for a Level Term II+ -'
    #VT-TERM 'Year payment plan.' INTO #DESIRED-LINES (#SEL-X)
ELSE
  COMPRESS 'I am applying for a Level Term II -'
    #VT-TERM 'Year payment plan.' INTO #DESIRED-LINES (#SEL-X)
END-IF
ADD 1 TO #SEL-X
COMPRESS 'My monthly premium based upon my individual family'
  'medical history and my life' INTO #DESIRED-LINES (#SEL-X)
ADD 1 TO #SEL-X
COMPRESS 'style/medical history could be:'
  INTO #DESIRED-LINES (#SEL-X)
*
IF #VT-PLAN-A1 = '#'   /* Asked for rider
  MOVE 'including rider' TO #RID
END-IF
*
FOR #I3 1 TO 3
  IF #DESIRED-FACE-AMT (#I3) NE 0
    MOVE EDITED #DESIRED-FACE-AMT (#I3) (EM=ZZZ,999) TO #FAC-X
    COMPRESS '$' #FAC-X INTO #FAC-X LEAVING NO
    MOVE #FAC-X TO #VT-PREM (#I3)
  END-IF
END-FOR
*
ADD 1 TO #SEL-X
MOVE #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
*
#PLAN := #VT-PLAN
IF #VT-PLAN-A1 = '*'
  IF #VT-RENEW = YES
    COMPRESS 'Q' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  ELSE
    COMPRESS 'W' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
ELSE
  IF #VT-PLAN-A1 = '#'   /* Asked for rider
    COMPRESS 'Z' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
END-IF
IF #SMOKER-FLAG NE 'S'
    AND #PLAN-A1 = 'Q' OR= 'W' OR= 'Z'
  FOR #I3 1 TO 3
    IF #DESIRED-FACE-AMT (#I3) NE 0
      MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
      PERFORM CALLNAT-GPREMSXX
      COMPUTE #MON-PREM = #APRM / 12
      MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
      COMPRESS '$' #VT-PREM (#I3)
        INTO #VT-PREM (#I3) LEAVING NO
      IF #VT-PLAN-N2 LE 25     /* DW7
        COMPRESS 'per month Super Select' #RID INTO #VT-LBL
      ELSE
        COMPRESS 'per month Super Select' INTO #VT-LBL
      END-IF
    END-IF
  END-FOR
  ADD 1 TO #SEL-X
  MOVE #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
END-IF
*
#PLAN := #VT-PLAN
IF #VT-PLAN-A1 = '*'
  IF #VT-RENEW = YES
    COMPRESS 'R' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  ELSE
    COMPRESS 'V' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
ELSE
  IF #VT-PLAN-A1 = '#'   /* Asked for rider
    COMPRESS 'Y' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
END-IF
IF #PLAN-A1 = 'R' OR= 'V' OR= 'Y'
  FOR #I3 1 TO 3
    IF #DESIRED-FACE-AMT (#I3) NE 0
      MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
      PERFORM CALLNAT-GPREMSXX
      COMPUTE #MON-PREM = #APRM / 12
      COMPUTE #2-MON-PMT = #MON-PREM * 2
      MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
      COMPRESS '$' #VT-PREM (#I3)
        INTO #VT-PREM (#I3) LEAVING NO
      IF #VT-PLAN-N2 LE 25     /* DW7
        COMPRESS 'per month Select' #RID INTO #VT-LBL
      ELSE
        COMPRESS 'per month Select' INTO #VT-LBL
      END-IF
    END-IF
  END-FOR
  ADD 1 TO #SEL-X
  MOVE #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
END-IF
*
#PLAN := #VT-PLAN
IF #VT-PLAN-A1 = '*'
  IF #VT-RENEW = YES      /* NG1
    COMPRESS 'S' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  ELSE
    COMPRESS 'U' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
ELSE
  IF #VT-PLAN-A1 = '#'   /* Asked for rider
    COMPRESS 'X' #VT-PLAN-A2 INTO #PLAN LEAVING NO
  END-IF
END-IF
IF #PLAN-A1 = 'S' OR= 'U' OR= 'X'                      /* NG3
  FOR #I3 1 TO 3
    IF #DESIRED-FACE-AMT (#I3) NE 0
      MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
      PERFORM CALLNAT-GPREMSXX
      COMPUTE #MON-PREM = #APRM / 12
      MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
      COMPRESS '$' #VT-PREM (#I3)
        INTO #VT-PREM (#I3) LEAVING NO
      COMPRESS 'per month Standard' #RID INTO #VT-LBL
    END-IF
  END-FOR
  ADD 1 TO #SEL-X
  MOVE #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
END-IF
*
FOR #RATED-CLASS 1 3
  #I3 := 1
  REPEAT UNTIL #I3 > 3 OR #DESIRED-FACE-AMT (#I3) = 0
    MOVE #DESIRED-FACE-AMT (#I3) TO #FACE-AMT
    PERFORM CALLNAT-GPREMSXX
    COMPUTE #MON-PREM = #APRM / 12
    MOVE EDITED #MON-PREM (EM=ZZZZ.99) TO #VT-PREM (#I3)
    COMPRESS '$' #VT-PREM (#I3)
      INTO #VT-PREM (#I3) LEAVING NO
    ADD 1 TO #I3
  END-REPEAT
  COMPRESS 'per month Class' #RATED-CLASS INTO #VT-LBL
  ADD 1 TO #SEL-X
  MOVE #VT-PREM-LINE TO #DESIRED-LINES (#SEL-X)
END-FOR
END-SUBROUTINE
*                                                      NG1 End
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-OPTIONS
* * * * * * * * * * * * * * * * *
*
MOVE #DESIRED-FACE-AMT (#I1) TO #FACE-AMT
*
IF #PLAN = 'SI'
  PERFORM CALLNAT-GPREMSXX
  MOVE #APRM TO #PREM #MON-PREM /* 1-time prem cost
  PERFORM ADD-SERVICE-FEE
  COMPUTE #MON-DUES = #PREM - #MON-PREM /* 1-time dues
ELSE
  PERFORM CALLNAT-GPREMSXX
  PERFORM ADD-SERVICE-FEE
  COMPUTE #MON-PREM = (#APRM / 12)
  COMPUTE #MON-DUES = #PREM - #MON-PREM
END-IF
*
MOVE #PREM TO #TOT-PMT
COMPUTE #2-MON-PMT = #TOT-PMT * 2
*
MOVE EDITED #DESIRED-FACE-AMT (#I1) (EM=ZZZ,999) TO #FAC-X
COMPRESS '$' #FAC-X INTO #FAC-X LEAVING NO
*
RESET #RATED-TEXT
IF #RATED-CLASS NE 0
  COMPRESS 'Rated Class' #RATED-CLASS-A INTO #RATED-TEXT
END-IF
*
ADD 1 TO #SEL-X
IF #PLAN = 'OL' OR = '30P' OR = '20P' OR = '7P' OR = 'SI'
  COMPRESS 'I desire' #FAC-X 'of Interest-Sensitive Whole'
    'Life Coverage on a' #RATED-TEXT INTO #DESIRED-LINES (#SEL-X)
ELSE
  IF #PLAN = 'RT'
    COMPRESS 'I desire' #FAC-X 'of 5 Year Renewable'
      'Coverage on a' INTO #DESIRED-LINES (#SEL-X)
  ELSE
    IF #PLAN = 'LT'
      COMPRESS 'I desire' #FAC-X 'of Level Term to age'
        #LT-AGE 'Coverage on a' INTO #DESIRED-LINES (#SEL-X)
    ELSE
      IF #VAR-TERM
        COMPRESS 'I desire' #FAC-X 'of Level Term II'
          'Coverage on a' #RATED-TEXT INTO #DESIRED-LINES (#SEL-X)
      ELSE
        IF #PLAN = 'GP2'                          /* TT2
          COMPRESS 'I desire' #FAC-X 'of Generations-Plus'
            'Coverage on a' INTO #DESIRED-LINES (#SEL-X)
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
MOVE EDITED #MON-PREM (EM=Z,ZZZ.99) TO #MON-PREM-X
COMPRESS '$' #MON-PREM-X INTO #MON-PREM-X LEAVING NO
MOVE EDITED #MON-PREM (EM=ZZZ,ZZZ.99) TO #SP-PREM-X
COMPRESS '$' #SP-PREM-X INTO #SP-PREM-X LEAVING NO
*
MOVE EDITED #MON-DUES (EM=Z.99) TO #MON-DUES-X
COMPRESS '$' #MON-DUES-X INTO #MON-DUES-X LEAVING NO
MOVE EDITED #MON-DUES (EM=ZZZ.99) TO #SP-DUES-X
COMPRESS '$' #SP-DUES-X INTO #SP-DUES-X LEAVING NO
*
MOVE EDITED #TOT-PMT (EM=Z,ZZ9.99) TO #TOT-PMT-X
COMPRESS '$' #TOT-PMT-X '.' INTO #TOT-PMT-X LEAVING NO
MOVE EDITED #TOT-PMT (EM=ZZZ,ZZ9.99) TO #SP-TOT-PMT-X
COMPRESS '$' #SP-TOT-PMT-X '.' INTO #SP-TOT-PMT-X LEAVING NO
*
MOVE EDITED #2-MON-PMT (EM=Z,ZZ9.99) TO #2-MON-PMT-X
COMPRESS '$' #2-MON-PMT-X '.' INTO #2-MON-PMT-X LEAVING NO
*
IF #PLAN = 'SI'
  ADD 1 TO #SEL-X
  COMPRESS 'Single Payment Plan with a 1-time premium due of'
    #SP-PREM-X INTO #DESIRED-LINES (#SEL-X)
*
  IF #MON-DUES = 0
    COMPRESS #DESIRED-LINES (#SEL-X) '.'
      INTO #DESIRED-LINES (#SEL-X) LEAVING NO
  ELSE
    COMPRESS #DESIRED-LINES (#SEL-X) 'and'
      INTO #DESIRED-LINES (#SEL-X)
    ADD 1 TO #SEL-X
    COMPRESS '1-time dues of' #SP-DUES-X 'for a total'
      'payment of' #SP-TOT-PMT-X INTO #DESIRED-LINES (#SEL-X)
  END-IF
END-IF
*
IF #PLAN = 'OL'
  ADD 1 TO #SEL-X
  COMPRESS 'pay till age 100 payment plan with monthly'
    'premiums of' #MON-PREM-X INTO #DESIRED-LINES (#SEL-X)
END-IF
*
IF #PLAN = '30P' OR = '20P' OR = '7P'
  IF #PLAN = '30P'
    ADD 1 TO #SEL-X
    COMPRESS '30 Year payment plan with monthly'
      'premiums of' #MON-PREM-X INTO #DESIRED-LINES (#SEL-X)
  END-IF
*
  IF #PLAN = '20P'
    ADD 1 TO #SEL-X
    COMPRESS '20 Year payment plan with monthly'
      'premiums of' #MON-PREM-X INTO #DESIRED-LINES (#SEL-X)
  END-IF
*
  IF #PLAN = '7P'
    ADD 1 TO #SEL-X
    COMPRESS '7 Year payment plan with monthly'
      'premiums of' #MON-PREM-X INTO #DESIRED-LINES (#SEL-X)
  END-IF
END-IF
*
IF #PLAN = 'RT' OR = 'LT'
    OR #VAR-TERM
  ADD 1 TO #SEL-X
  IF #VAR-TERM
    COMPRESS #VT-TERM 'Year' INTO #DESIRED-LINES (#SEL-X)
  END-IF
*
  COMPRESS #DESIRED-LINES (#SEL-X)
    'payment plan with monthly premiums of' #MON-PREM-X
    INTO #DESIRED-LINES (#SEL-X)
END-IF
*
IF   (#PLAN NE 'SI')
    AND (#PLAN NE 'GP2')            /* DW9
  IF #MON-DUES = 0
    COMPRESS #DESIRED-LINES (#SEL-X) '.'
      INTO #DESIRED-LINES (#SEL-X) LEAVING NO
  ELSE
    COMPRESS #DESIRED-LINES (#SEL-X) 'and'
      INTO #DESIRED-LINES (#SEL-X)
    ADD 1 TO #SEL-X
    COMPRESS 'monthly dues of' #MON-DUES-X 'for a total'
      'payment of' #TOT-PMT-X INTO #DESIRED-LINES (#SEL-X)
  END-IF
END-IF
*
IF #PLAN = 'GP2'                       /* TT2
  ADD 1 TO #SEL-X
  COMPRESS 'pay till age 100 payment plan with monthly'
    'premiums of' #MON-PREM-X INTO #DESIRED-LINES (#SEL-X)
END-IF
*
* * * * * 2 month initial pmt
IF #PLAN = 'SI'
  ADD 1 TO #SEL-X
  COMPRESS 'I have enclosed with my application the'
    'total due of' #SP-TOT-PMT-X INTO #DESIRED-LINES (#SEL-X)
ELSE
  ADD 1 TO #SEL-X
  COMPRESS 'Enclosed with my application is' #2-MON-PMT-X
    'the required two months deposit.' INTO #DESIRED-LINES (#SEL-X)
END-IF
*
IF #MON-DUES = 0 /* force 4 lines for each plan
  ADD 1 TO #SEL-X
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ADD-SERVICE-FEE
* * * * * * * * * * * * * * * * * *
*
IF  (#PDA-SCR-SUFFIX = 'VP')   /* FROM AOAP005E ON MR VP
    AND NOT #MR-VP-DUES
  ESCAPE ROUTINE
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALLNAT-GPREMSXX
* * * * * * * * * * * * * * * * * *
*
RESET #SCR-SUFFIX
IF #RATED-CLASS NE 0
  COMPRESS 'SB' #RATED-CLASS-A INTO #SCR-SUFFIX LEAVING NO
END-IF
*
IF   (#PDA-SCR-SUFFIX = 'OLD')     /* DW1
    AND ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))
    AND (#SEX = 'F')
  MOVE 20050531 TO #ISSUE-DATE
END-IF
*
CALLNAT 'GPREMSXX' #SCR-COMMAND #SCR-ID #SCR-SUFFIX
  #PLAN #AGE #APRM #ISSUE-DATE #SMOKER-FLAG #FACE-AMT #SEX   /* DW4
*
IF    (#PDA-SCR-SUFFIX = 'OLD')    /* DW1
    AND ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))
  MOVE #SCN-AOD-YYYYMMDD TO #ISSUE-DATE
END-IF
*
COMPUTE #PREM = #APRM / 12
END-SUBROUTINE
*
END
