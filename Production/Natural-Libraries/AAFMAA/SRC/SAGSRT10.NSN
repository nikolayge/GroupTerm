* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID  SAGSRT10
************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11242005  RH1   SS - Created for implementing sorting algorithm *
************************************************************************

DEFINE DATA PARAMETER
**
1 #SRT-TABLE                 (60)
  2 #SRT-DEPN-CID            (N8)
  2 #SRT-DEPN-ISN            (N8)
  2 #SRT-DEPN-ROLE           (A1)
  2 #SRT-DEPN-ROLE-DESC      (A10)
  2 #SRT-DEPN-NAME           (A39)
  2 #SRT-DEPN-STATUS-CD      (A1)
  2 #SRT-DEPN-DATE-OF-BIRTH  (N8)
  2 REDEFINE #SRT-DEPN-DATE-OF-BIRTH
    3 #SRT-DEPN-DATE-OF-BIRTH-A (A8)
  2 #SRT-DEPN-DATE-OF-DEATH  (N8)
  2 REDEFINE #SRT-DEPN-DATE-OF-DEATH
    3 #SRT-DEPN-DATE-OF-DEATH-A (A8)
  2 #SRT-DEPN-SSN            (N9)
  2 #SRT-DEPN-SEX            (A1)
**
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES         (I2)
  2 #SRT-ENTRY-LENGTH        (I2)
  2 #SRT-KEY-LENGTH          (I2)
  2 #SRT-KEY-POSITION        (I2)
  2 #SRT-SORT-TYPE           (A1)
**
LOCAL
**
1 #SRT-WORK                  (60)
  2 #SRT-DEPN-CID            (N8)
  2 #SRT-DEPN-ISN            (N8)
  2 #SRT-DEPN-ROLE           (A1)
  2 #SRT-DEPN-ROLE-DESC      (A10)
  2 #SRT-DEPN-NAME           (A39)
  2 #SRT-DEPN-STATUS-CD      (A1)
  2 #SRT-DEPN-DATE-OF-BIRTH  (N8)
  2 #SRT-DEPN-DATE-OF-DEATH  (N8)
  2 #SRT-DEPN-SSN            (N9)
  2 #SRT-DEPN-SEX            (A1)
1 REDEFINE #SRT-WORK
  2 #SRT-ROW                 (A93/1:60)
1 REDEFINE #SRT-WORK
  2 #SRT-REC                 (60)
    3 #SRT-FILL1             (A67)
    3 #SRT-KEY               (A8)
    3 #SRT-FILL2             (A18)
**
1 #I                         (P7)
1 #ROWS                      (P7)
1 #DONE                      (L)  INIT <FALSE>
1 #TEMP-ROW                  (A93)
1 #MMDDYYYY                  (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-A                    (A2)
  2 #DD-A                    (A2)
  2 #YYYY-A                  (A4)
**
END-DEFINE
**
**  first eliminate any empty rows
FOR #I EQ 1 TO 60
  IF      #SRT-TABLE.#SRT-DEPN-CID (#I)           EQ 0
      AND #SRT-TABLE.#SRT-DEPN-ISN (#I)           EQ 0
      AND #SRT-TABLE.#SRT-DEPN-ROLE (#I)          EQ ' '
      AND #SRT-TABLE.#SRT-DEPN-ROLE-DESC (#I)     EQ ' '
      AND #SRT-TABLE.#SRT-DEPN-NAME (#I)          EQ ' '
      AND #SRT-TABLE.#SRT-DEPN-STATUS-CD (#I)     EQ ' '
      AND #SRT-TABLE.#SRT-DEPN-DATE-OF-BIRTH (#I) EQ 0
      AND #SRT-TABLE.#SRT-DEPN-DATE-OF-DEATH (#I) EQ 0
      AND #SRT-TABLE.#SRT-DEPN-SSN (#I)           EQ 0
      AND #SRT-TABLE.#SRT-DEPN-SEX (#I)           EQ ' '
    ESCAPE TOP
  ELSE
    ADD 1 TO #ROWS
    MOVE #SRT-TABLE.#SRT-DEPN-DATE-OF-BIRTH-A (#I) TO #MMDDYYYY
    COMPRESS #YYYY-A #MM-A #DD-A INTO #SRT-TABLE.#SRT-DEPN-DATE-OF-BIRTH-A (#I) LEAVING NO
    MOVE #SRT-TABLE.#SRT-DEPN-DATE-OF-DEATH-A (#I) TO #MMDDYYYY
    COMPRESS #YYYY-A #MM-A #DD-A INTO #SRT-TABLE.#SRT-DEPN-DATE-OF-DEATH-A (#I) LEAVING NO
*    DISPLAY #YYYY-A #MM-A #DD-A #SRT-TABLE.#SRT-DEPN-DATE-OF-BIRTH-A (#I)
    MOVE BY NAME #SRT-TABLE (#I) TO #SRT-WORK (#ROWS)
  END-IF
END-FOR
IF #ROWS EQ 0 ESCAPE ROUTINE END-IF
**

DECIDE ON FIRST VALUE OF #SRT-SORT-TYPE
**
  VALUE 'A'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
**display '='#i '=' #SRT-KEY (#I)  #SRT-KEY (#I - 1)
        IF #SRT-KEY (#I) LT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  VALUE 'D'
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT AVAILABLE - EXECUTION TERMINATED'
    STOP
**
  NONE VALUE
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT RECOGNIZED - EXECUTION TERMINATED'
    STOP
**
END-DECIDE
**
RESET #SRT-TABLE (*)
FOR #I EQ 1 TO #ROWS
  MOVE BY NAME #SRT-WORK (#I) TO #SRT-TABLE (#I)
END-FOR
#SRT-NUM-ENTRIES := #ROWS  /* SUBJECT TO CONFIRMATION
**
**display #SRT-TABLE.#SRT-DEPN-NAME(1:10) #SRT-TABLE.#SRT-DEPN-DATE-OF-BIRTH(1:10)
END
