* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
********************************************************************************************
* PGM-ID: MANPLIFE                                                                         *
*     Extract Annuitants for quarterly report                                              *
*                                                                                          *
*  SCL: User: AAFMAA      Name: MANPLIFE                                                   *
*                                                                                          *
*  MODIFICATIONS:                                                                          *
*                                                                                          *
*  JNT  05/28/2015  JT1 Creation                                                           *
*                       MODIFICATION LOG                                                   *
* USER   DATE      TAG     REASON                                                          *
* NG    04/05/2016 NG   Add Annuity Status column to the actuarial report                  *
*
********************************************************************************************
*
DEFINE DATA
*
LOCAL
*
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 LAST-UPD-TIME (N7.0)
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION   (N7.2)
  2 PRINCIPAL         (N7.2)
  2 ANNUAL-PAYMENT    (N7.2)
  2 RESERVE-AMOUNT    (N7.2)
  2 WITHDRAWALS       (N7.2)
  2 DEPOSIT           (N7.2)
  2 T13TH-CHECK       (N7.2)
  2 TAXABLE           (N7.2)
  2 INITIAL-PAYMENT   (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0)
  2 REDEFINE START-DATE
    3 #YYYY-1 (N4)
    3 #MM-1   (A2)
    3 #DD-1   (A2)
  2 REDEFINE START-DATE
    3 #START-DATE-A (A8)
  2 PAID-UP-DATE    (N8)
  2 REDEFINE PAID-UP-DATE
    3 PAID-UP-YYYY (N4)
    3 PAID-UP-MM   (A2)
    3 PAID-UP-DD   (A2)
*
1 C-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 CONTACT-TYPE
  2 DATE-OF-BIRTH
  2 REDEFINE DATE-OF-BIRTH
    3 #DATE-OF-BIRTH-YYYY (A4)
    3 REDEFINE #DATE-OF-BIRTH-YYYY
      4 #DATE-OF-BIRTH-YYYY-N (N4)
    3 #DATE-OF-BIRTH-MM   (A2)
    3 REDEFINE #DATE-OF-BIRTH-MM
      4 #DATE-OF-BIRTH-MM-N (N2)
    3 #DATE-OF-BIRTH-DD   (A2)
    3 REDEFINE #DATE-OF-BIRTH-DD
      4 #DATE-OF-BIRTH-DD-N (N2)
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 #DATE-OF-DEATH-YYYY (N4)
    3 #DATE-OF-DEATH-MM   (A2)
    3 #DATE-OF-DEATH-DD   (A2)
  2 SSN
  2 GENDER-CD
*
1 POLICY VIEW OF A-STATUS
  2 ID-NUMBER
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9) 2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-A (9)
      4 YYYY (A4)
      4 MM   (A2)
      4 DD   (A2)
  2 FACE-AMOUNT   (9)
*
1 #OUTPUT-RECORD-1                              (A350)
1 REDEFINE #OUTPUT-RECORD-1
  2 #CERT-NUMBER     (N6)
  2 #PERIOD-PAID     (A2)
  2 #PAID-REGULARITY (A2)
  2 #INTEREST-PAID   (A3)
  2 #NAME                            (A77)
  2 #SEX                             (A1)
  2 #DATE-OF-BIRTH                   (A10)
  2 REDEFINE #DATE-OF-BIRTH
    3 #BIRTH-MM   (A2)
    3 #FILLER1    (A1)
    3 #BIRTH-DD   (A2)
    3 #FILLER2    (A1)
    3 #BIRTH-YYYY (A4)
  2 #DATE-OF-BIRTH-C                 (A10)
  2 REDEFINE #DATE-OF-BIRTH-C
    3 #BIRTH-MM-C   (A2)
    3 #FILLER3      (A1)
    3 #BIRTH-DD-C   (A2)
    3 #FILLER4      (A1)
    3 #BIRTH-YYYY-C (A4)
  2 #DATE-OF-DEATH-C                 (A10)
  2 REDEFINE #DATE-OF-DEATH-C
    3 #DEATH-MM-C   (A2)
    3 #FILLER5      (A1)
    3 #DEATH-DD-C   (A2)
    3 #FILLER6      (A1)
    3 #DEATH-YYYY-C (A4)
  2 #ATT-AGE                         (A3)
  2 #POLICY-NUMBER                   (A16)
  2 #POLICY-ISSUE-DATE               (A10)
  2 REDEFINE #POLICY-ISSUE-DATE
    3 #ISSUE-MM     (A2)
    3 #FILLER9      (A1)
    3 #ISSUE-DD     (A2)
    3 #FILLER10     (A1)
    3 #ISSUE-YYYY   (A4)
  2 #ANNUITY-TYPE                    (A2)
  2 #ANNUITY-STATUS                  (A1)
  2 #ANNUITY-START-DATE              (A10)
  2 REDEFINE #ANNUITY-START-DATE
    3 #START-MM   (A2)
    3 #FILLER7    (A1)
    3 #START-DD   (A2)
    3 #FILLER8    (A1)
    3 #START-YYYY (A4)
  2 #MONTHLY-PAYMENT                 (A10)
  2 #PROCEEDS-AT-INTEREST            (A10)
  2 #ANNUAL-PAYMENT                  (A10)
  2 #RESERVE                         (A10)
  2 #13TH-CHECK                      (A10)
  2 #RESIDUAL-OUT                    (A10)
  2 #DEPOSIT-OUT                     (A10)
  2 #FACE-AMOUNT                     (A10)
  2 #T13TH-CHECK                     (A10)
  2 #DOD                             (A10)
  2 #TAXABLE                         (A10)
  2 #PUPD                            (A10)
  2 #WITHDRAWAL                      (A10)
*
1 #AGE             (N03.0)
1 #REPORT-DATE     (A8)
1 #REPORT-DATE-D   (D)
1 #TODAY (N8) 1 REDEFINE #TODAY
  2 #YYYY        (N4)
  2 #TODAY-MM    (N2)
  2 #TODAY-DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A        (A4)
  2 #TODAY-MM-A    (A2)
  2 #TODAY-DD-A    (A2)
1 #DATE       (N8)
1 REDEFINE #DATE
  2 PARM-YYYY (N4)
  2 PARM-MM   (N2)
  2 PARM-DD   (N2)

1 BLANK (A1) CONST <' '>
1 #THIRTHEEN-CHECK   (N7.2)
1 #YRS-CERT          (N2.2)
1 #ISN               (P8)
1 #RESIDUAL          (N7.2)
1 #TOTAL-WD          (N7.2)
1 #TOTAL-POL-WD      (N7.2)
1 #NUMBER-WD         (I4)
1 #LAST-WITHDRAWAL   (N7.2)
1 #LAST-POL-WD       (N7.2)
1 #DEPOSIT           (N7.2)
1 #I                 (I2)
1 #J                 (I2)
1 #MIDDLE-INITIAL    (A2)
1 #LAST-NAME-DISPLAY (A26)
1 #NAME-2            (A77)
1 #SEX-2             (A1)

*
1 #COL-TITLES (A/24) DYNAMIC INIT <
  'CN',
  'Name',
  'AnnuitantId',
  'Policy Number',
  'Policy Issue Date',
  'Interest',
  'Gender',
  'DoB',
  'Age',
  'Annuity Type',

  'Monthly Pension/Payment',
  'Principal/Proceeds at Interest',
  'Annual Payment',
  'Reserve',
  'Mode/Paid',
  'Years Certain/Period',
  'Start Date/Annuity Start Date',
  'Deposit',
  'Annuity Status',
  '13th Check',

  'DoD',
  'Taxable',
  'Paid-up Date',
  'Withdrawal'
  >
1 #INTEREST-DATE   (N8) CONST <20150101>
1 #INTEREST-ONLY   (A2) CONST <'IO'>
*
1 #W  (N7.2)
END-DEFINE
* - - - - - - - - -
DEFINE WORK FILE 1 '/usr/SAG/tmp/ANNUITANTS.TXT' type 'ASCII-COMPRESSED'
**DEFINE WORK FILE 1 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/ANNUITANTS.TXT' type 'ASCII-COMPRESSED'
*
COMPRESS FULL #COL-TITLES (*)
  INTO #OUTPUT-RECORD-1
  WITH DELIMITER H'09'
WRITE WORK 1 #OUTPUT-RECORD-1
*
**MOVE *DATN TO #TODAY
INPUT #TODAY
#DATE.PARM-YYYY := #TODAY.#YYYY
#DATE.PARM-MM := 1
#DATE.PARM-DD := 1
*
**RD1. READ ANNUITANTS BY REFERENCE-CN
**    starting from 13953
RD1. READ ANNUITANTS BY SETTLEMENT-OPTION
/* Only active annuitants
*   IF NOT ANNUITANT-STATUS EQ 'A' OR= 'D'
*     ESCAPE TOP
*   END-IF
  IF ANNUITANTS.START-DATE <= 0
    ESCAPE TOP
  END-IF
  IF SETTLEMENT-OPTION = 'A2'
    ESCAPE TOP
  END-IF
  IF ANNUITANTS.PAID-UP-YYYY > 0
      AND ANNUITANTS.PAID-UP-YYYY < #TODAY.#YYYY
    ESCAPE TOP
  END-IF
  RESET #OUTPUT-RECORD-1
  MOVE *ISN(RD1.)                   TO #ISN
  MOVE ANNUITANTS.REFERENCE-CN      TO #CERT-NUMBER
  MOVE RIGHT ANNUITANTS.#YYYY-1 TO #OUTPUT-RECORD-1.#START-YYYY
  MOVE RIGHT ANNUITANTS.#MM-1   TO #OUTPUT-RECORD-1.#START-MM
  MOVE RIGHT ANNUITANTS.#DD-1   TO #OUTPUT-RECORD-1.#START-DD
  MOVE '/' TO #OUTPUT-RECORD-1.#FILLER7 #OUTPUT-RECORD-1.#FILLER8
  MOVE ANNUITANTS.POLICY-NUMBER     TO #OUTPUT-RECORD-1.#POLICY-NUMBER
  IF ANNUITANTS.SETTLEMENT-OPTION = 'AL'
    #ANNUITY-TYPE := 'A1'
  ELSE
    MOVE ANNUITANTS.SETTLEMENT-OPTION TO #ANNUITY-TYPE
  END-IF
  MOVE ANNUITANTS.ANNUITANT-STATUS  TO #ANNUITY-STATUS
* Policy issue date and Interest Paid
  FIND POLICY WITH ID-NUMBER = ANNUITANTS.REFERENCE-CN
    FOR #I 1 THRU 9
      IF POLICY.POLICY-ID(#I) = ANNUITANTS.POLICY-NUMBER
        MOVE EDITED POLICY.FACE-AMOUNT(#I) (EM=9999999.99)
          TO #FACE-AMOUNT
        MOVE RIGHT POLICY.YYYY(#I) TO #OUTPUT-RECORD-1.#ISSUE-YYYY
        MOVE RIGHT POLICY.MM(#I)   TO #OUTPUT-RECORD-1.#ISSUE-MM
        MOVE RIGHT POLICY.DD(#I)   TO #OUTPUT-RECORD-1.#ISSUE-DD
        MOVE '/' TO #OUTPUT-RECORD-1.#FILLER9 #OUTPUT-RECORD-1.#FILLER10
        IF POLICY.ISSUE-DATE (#I) < #INTEREST-DATE
          #INTEREST-PAID := '4'
        ELSE
          #INTEREST-PAID := '3.5'
        END-IF
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
  IF #INTEREST-PAID = BLANK
    #INTEREST-PAID := '4'
  END-IF
  IF ANNUITANTS.PAID-UP-DATE > 0
    COMPRESS FULL
      ANNUITANTS.PAID-UP-YYYY
      ANNUITANTS.PAID-UP-MM
      ANNUITANTS.PAID-UP-DD
      INTO #PUPD WITH DELIMITER '/'
  END-IF
* Annuitant contact data
  #ATT-AGE := 'NOCNT'
  IF ANNUITANTS.CONTACT-ID > 0
    FIND(1) C-V WITH C-V.CONTACT-ID = ANNUITANTS.CONTACT-ID
    END-FIND
    IF C-V.CONTACT-ID > 0
      #NAME := EDIT_NAME_LOWCASE
        (< FUNCTION-CONTACT-NAME (<ANNUITANTS.CONTACT-ID >) >)
      #SEX := C-V.GENDER-CD
      DECIDE FOR FIRST CONDITION
        WHEN C-V.DATE-OF-DEATH > 0
          COMPRESS FULL
            #DATE-OF-DEATH-YYYY
            #DATE-OF-DEATH-MM
            #DATE-OF-DEATH-DD
            INTO #DOD WITH DELIMITER '/'
          COMPRESS FULL
            C-V.#DATE-OF-BIRTH-YYYY
            C-V.#DATE-OF-BIRTH-MM
            C-V.#DATE-OF-BIRTH-DD
            INTO #DATE-OF-BIRTH-C WITH DELIMITER '/'
          #AGE := C-V.#DATE-OF-DEATH-YYYY - C-V.#DATE-OF-BIRTH-YYYY-N
          IF C-V.#DATE-OF-DEATH-MM < C-V.#DATE-OF-BIRTH-MM
            #AGE := #AGE - 1
          ELSE
            IF C-V.#DATE-OF-DEATH-MM = C-V.#DATE-OF-BIRTH-MM
                AND C-V.#DATE-OF-DEATH-DD < C-V.#DATE-OF-BIRTH-DD
              #AGE := #AGE - 1
            END-IF
          END-IF
          MOVE #AGE TO #ATT-AGE
        WHEN C-V.DATE-OF-BIRTH = 0
          IF C-V.CONTACT-TYPE = 'O'
            RESET #ATT-AGE
          ELSE
            #ATT-AGE := 'NODOB'
          END-IF
        WHEN C-V.DATE-OF-BIRTH > 0
          #AGE := #TODAY.#YYYY - C-V.#DATE-OF-BIRTH-YYYY-N
          IF #TODAY.#TODAY-MM < C-V.#DATE-OF-BIRTH-MM-N
            #AGE := #AGE - 1
          ELSE
            IF #TODAY.#TODAY-MM = C-V.#DATE-OF-BIRTH-MM-N AND
                #TODAY-DD < C-V.#DATE-OF-BIRTH-DD-N
              #AGE := #AGE - 1
            END-IF
          END-IF
          MOVE #AGE TO #ATT-AGE
          MOVE RIGHT C-V.#DATE-OF-BIRTH-YYYY
            TO #OUTPUT-RECORD-1.#BIRTH-YYYY-C
          MOVE RIGHT C-V.#DATE-OF-BIRTH-MM
            TO #OUTPUT-RECORD-1.#BIRTH-MM-C
          MOVE RIGHT C-V.#DATE-OF-BIRTH-DD
            TO #OUTPUT-RECORD-1.#BIRTH-DD-C
          MOVE '/'  TO #OUTPUT-RECORD-1.#FILLER3
            #OUTPUT-RECORD-1.#FILLER4
        WHEN NONE
          #ATT-AGE := '????'
      END-DECIDE
    END-IF
  ELSE
    #NAME := 'UNRESOLVED CONTACT'
  END-IF
*
  IF C-V.DATE-OF-DEATH > 0
      AND #DATE-OF-DEATH-YYYY NE #TODAY.#YYYY
    ESCAPE TOP
  END-IF
  IF #NAME = ' '
    COMPRESS 'NO CONTACT NAME' ANNUITANTS.CONTACT-ID
      INTO #NAME
  END-IF
*
  #W := GET-AZ-MONTHLY-PAY (< #ISN,#TODAY >)
  MOVE EDITED #W (EM=9999999.99) TO #MONTHLY-PAYMENT

  #W := GET-AZ-ANNUAL-PAY (< #ISN,#DATE,#TODAY >)
  MOVE EDITED #W (EM=9999999.99) TO #ANNUAL-PAYMENT

  IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY   
    ANNUITANTS.TAXABLE := #W 
    IF #YYYY = #YYYY-1
      ADD ANNUITANTS.INITIAL-PAYMENT TO ANNUITANTS.TAXABLE
    END-IF
    ADD ANNUITANTS.T13TH-CHECK TO ANNUITANTS.TAXABLE
  END-IF
  MOVE EDITED ANNUITANTS.TAXABLE (EM=9999999.99)
    TO #TAXABLE

  MOVE EDITED ANNUITANTS.T13TH-CHECK (EM=9999999.99)
    TO #T13TH-CHECK

  IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
    CALLNAT 'MANNHWDP'
      #ISN
      #TODAY
      #TOTAL-POL-WD
      #LAST-POL-WD
      #NUMBER-WD
    COMPUTE #RESIDUAL := (ANNUITANTS.PRINCIPAL - #TOTAL-POL-WD)
    MOVE EDITED #RESIDUAL (EM=9999999.99) TO #RESERVE
    MOVE EDITED #TOTAL-POL-WD (EM=9999999.99) TO #WITHDRAWAL
    MOVE EDITED ANNUITANTS.PRINCIPAL (EM=9999999.99)
      TO #PROCEEDS-AT-INTEREST
  ELSE
    RESET #PROCEEDS-AT-INTEREST #RESIDUAL
    MOVE EDITED ANNUITANTS.RESERVE-AMOUNT (EM=9999999.99) TO  #RESERVE
  END-IF
  IF ANNUITANTS.#YYYY-1 = #TODAY.#YYYY
    MOVE EDITED ANNUITANTS.PRINCIPAL (EM=9999999.99) TO #DEPOSIT-OUT
  ELSE
    RESET #DEPOSIT-OUT
  END-IF
*
  MOVE EDITED #RESIDUAL (EM=9999999.99) TO #RESIDUAL-OUT
  DECIDE ON FIRST VALUE OF ANNUITANTS.SETTLEMENT-OPTION
    VALUE 'L1'
      #PERIOD-PAID := '10'
    VALUE 'L2'
      #PERIOD-PAID := '15'
    VALUE 'L3'
      #PERIOD-PAID := '20'
    VALUE 'LT'
      #PERIOD-PAID := '50'
    NONE VALUE
      RESET #PERIOD-PAID
  END-DECIDE
  #PAID-REGULARITY := '12'
*
  COMPRESS FULL
    #CERT-NUMBER
    #NAME
    ANNUITANTS.CONTACT-ID
    #POLICY-NUMBER
    #POLICY-ISSUE-DATE
    #INTEREST-PAID
    #SEX
    #DATE-OF-BIRTH-C
    #ATT-AGE
    #ANNUITY-TYPE
    #MONTHLY-PAYMENT
    #PROCEEDS-AT-INTEREST
    #ANNUAL-PAYMENT
    #RESERVE
    #PAID-REGULARITY
    #PERIOD-PAID
    #ANNUITY-START-DATE
    #DEPOSIT-OUT
    #ANNUITY-STATUS
    #T13TH-CHECK
    #DOD
    #TAXABLE
    #PUPD
    #WITHDRAWAL
    INTO #OUTPUT-RECORD-1
    WITH DELIMITER H'09'

  WRITE WORK 1 #OUTPUT-RECORD-1
*
  RESET #ISN #TOTAL-WD #NUMBER-WD #LAST-WITHDRAWAL
*
END-READ     /* RD1.
* *
ON ERROR
  WRITE ANNUITANTS.REFERENCE-CN
    ANNUITANTS.SETTLEMENT-OPTION
    ANNUITANTS.CONTACT-ID
    ANNUITANTS.POLICY-NUMBER
    *ERROR-LINE
    #TODAY.#YYYY
    C-V.#DATE-OF-BIRTH-YYYY
END-ERROR
*
END
