* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G2A00PWR
* THIS PROGRAM PROCESSES THE 'WR' (WIDOW NAME) CROSS-REFERENCE SCREEN
* Obsolete NG1 ????
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
1 #SCR-MSG         (A50)
1 #S-NAME          (A25)
1 #SCR-SSN         (N9/7)
1 #SCR-MIL-SERV    (A4/7)
1 #SCR-NUM-MBRSHPS (A2/7)
1 #S-NUM-MBRSHPS (A2)
1 REDEFINE #S-NUM-MBRSHPS
  2 #SNM   (N1)
  2 #M     (A1)
1 #SCR-ADDR1       (A25/7)
1 #SCR-ADDR2       (A25/7)
1 #SCR-ADDR3       (A25/7)
1 #S-ADDR3       (A25)
1 REDEFINE #S-ADDR3
  2 #ADDR3      (A15)
  2 #S-ZIP      (A5)
  2 #S-ZIP-DASH (A1)
  2 #S-ZIP-4    (A4)
1 #SCR-NAME        (A25/7)
1 #SCR-NAME-SP     (A25/7)
1 #SCR-ID-NO       (N6/7)
1 #SCR-RANK        (A3/7)
1 #SCR-STATUS      (A1/7)
1 I (I1)
1 CR-W01 VIEW OF A-STATUS
  2 ID-NUMBER        (N6)
  2 TITLE-SP         (A4)
  2 MILITARY-SERVICE (A3)
  2 STATUS-CODE-SP   (A1)
  2 SSN-SP           (N9)
  2 NAME             (A25)
  2 NAME-SP          (A25)
*   2 ADDRESS-1        (A25)       /*NG1 BEGIN
*   2 ADDRESS-2        (A25)
*   2 ADDRESS-3        (A25)
*   2 REDEFINE ADDRESS-3
*     3 #ADD3   (A20)
*     3 #FILLER (A5)
*   2 ZIP-CODE         (N9)
*   2 REDEFINE ZIP-CODE
*     3 #ZIP1   (A5)
*     3 #ZIP2   (A4)
*     3 REDEFINE #ZIP2
*      4 #ZIP2-N (N4)             /*NG1 END
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX        /* NATURAL ERROR ROUTINE
*
IF #CGA-SCR-SUFFIX = 'SSN'
  RESET #CGA-SCR-SUFFIX
  FETCH 'G2A00PWX'
END-IF
*
MOVE 'ENTER "SSN" IN SUFFIX FIELD TO SEE XREF BY SSN' TO #SCR-MSG
RESET #CGA-SCR-COMMAND
MOVE #CGA-SCR-SUFFIX TO #S-NAME
RESET #CGA-SCR-SUFFIX
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPEAT
*
  IF #CGA-SCR-COMMAND NE ' '
    FETCH 'G1000PXX'
  END-IF
*
  ASSIGN I = 1
*
  READ (7) CR-W01 LOGICAL WITH NAME-KEY-SP STARTING FROM #S-NAME
*
*     ASSIGN #ADDR3   = #ADD3         NG1
*     ASSIGN #S-ZIP   = #ZIP1
*     IF #ZIP2-N NE 0
*       MOVE '-'   TO #S-ZIP-DASH
*       MOVE #ZIP2 TO #S-ZIP-4
*     ELSE
*       RESET #S-ZIP-DASH
*       RESET #S-ZIP-4
*     END-IF
*
    ASSIGN #SCR-SSN(I)     = SSN-SP
    ASSIGN #SCR-NAME(I)    = NAME
    ASSIGN #SCR-NAME-SP(I) = NAME-SP
    ASSIGN #SCR-RANK(I)    = TITLE-SP
    ASSIGN #SCR-ID-NO(I)   = ID-NUMBER
    ASSIGN #SCR-STATUS(I)  = STATUS-CODE-SP
*     ASSIGN #SCR-ADDR1(I)   = ADDRESS-1    NG1
*     ASSIGN #SCR-ADDR2(I)   = ADDRESS-2
*     ASSIGN #SCR-ADDR3(I)   = #S-ADDR3
*
    IF MILITARY-SERVICE = MASK ('A')
      MOVE 'ARMY'  TO #SCR-MIL-SERV (I)
    ELSE
      IF MILITARY-SERVICE = MASK ('F')
        MOVE 'USAF'  TO #SCR-MIL-SERV (I)
      ELSE
        IF MILITARY-SERVICE = MASK ('N')
          MOVE 'USN'  TO #SCR-MIL-SERV (I)
        ELSE
          IF MILITARY-SERVICE = MASK ('M')
            MOVE 'USMC'  TO #SCR-MIL-SERV (I)
          ELSE
            MOVE 'USCG'  TO #SCR-MIL-SERV (I)
          END-IF
        END-IF
      END-IF
    END-IF
*
    ADD 1 TO I
*
  END-READ /* (0080)
*
  IF I LT 7
    ASSIGN #SCR-NAME(I) = '** END OF DATA **'
  END-IF
*
  ASSIGN #S-NAME = #SCR-NAME-SP(7)
  INPUT TEXT #SCR-MSG MARK *#S-NAME     USING MAP 'G2A00MWX'
  RESET #SCR-MSG
*
  RESET #SCR-SSN         (1:7)
  RESET #SCR-ADDR1       (1:7)
  RESET #SCR-ADDR2       (1:7)
  RESET #SCR-ADDR3       (1:7)
  RESET #SCR-NAME        (1:7)
  RESET #SCR-NAME-SP     (1:7)
  RESET #SCR-ID-NO       (1:7)
  RESET #SCR-RANK        (1:7)
  RESET #SCR-STATUS      (1:7)
  RESET #SCR-MIL-SERV    (1:7)
*
END-REPEAT /* (0072)
END
