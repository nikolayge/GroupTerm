* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPAUD2 - THIS PROGRAM READS THE DATABASE AND CREATES
*             CONFIRMATION LETTERS FOR TERMINATED POLICIES
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    01252005  None    Program Written.                            *
* DRW    03052005  DW1     ADD (AL) ANNUITY LIFE PROCESSING            *
* DRW    12262006  DW2     Chg for 2006                                *
* DRW    12282006  DW3     Add Totals                                  *
* DRW  08/26/2008  DW4     Gen Plus Processimg                         *
* DRW  11/25/2008  DW5     2008 Changes                                *
* RSE  09/21/2009  RE1     Change PROCESS-IND logic for Unification    *
* PAM  12/31/2009  PM2     2009 Audit Changes                          *
* PAM  12/28/2010  PM3     2010 Audit Changes                          *
* PAM  01/11/2011  PM4     Adjusted confirmation letter layouts        *
* PAM  12/02/2011  PM5     2011 Audit Changes                          *
* vxt  09/16/2012  vt1     Allign the face, frequency  and premium     *
*                          amounts to the left /Ticket 1170/           *
* YAK  10/09/2012  YK1     Use function to identify Level Term II plans*
* YAK  11/08/2012  YK2     Add No Medical Term plan (EZ)               *
* vxt  12/29/2012  vt2     2012 Audit Changes                          *
* bz  06/09/2013  Rebranding (Search for AAFMAA)                       *
* JNT  09/16/2013  JT1     Address normalization                       *
* vxt  12/05/2013  vt3    2013 Audit Changes                           *
* TMT  01/13/2014  TT1     ADDED GP2 POLICY SUPPORT                    *
* TMT 06/16/2014   TT2  Contact Normalization - DOB and Sex            *
* vxt  11/20/2014  vt4    2014 Audit Changes                           *
* vxt  11/27/2015  vt5    2015 Audit Changes /Ticket 4043/             *
* vxt  11/10/2016  vt6    2016 Audit changes /Ticket 1826/             *
* vxt  10/19/2017  vt7    2017 Audit changes /Ticket 6084/             *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING ADDA0001                            /* JT1
LOCAL USING G3500AAD
LOCAL
*
1 #YEAR-TO-CHECK            (N4) INIT <2017>    /* DW2 DW5 PM2 PM3 PM5  /* vt2 was 2011 /* vt3 was 2012 /* vt4 was 2013 /* vt5 was 2014 /* vt6 was 2015 /* vt7 was 2016
1 #NUMBER-REQ-LAPSE         (P3) INIT <5>
1 #NUMBER-REQ-CSV           (P3) INIT <5>       /* DW2
1 #TOT-LAPS                 (P9.2)              /* DW3
1 #TOT-CSVS                 (P9.2)              /* DW3

*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER               (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A           (A6)
  2 PROCESS-IND             (A1)
*  2 ADDRESS-CHG-CODE        (A1)               /* JT1
  2 INTRO-FLAG              (A1)
  2 MEMBER-CONTACT-ID       (N8)                /* PM3
*  2 NAME                    (A25)
*   2 DATE-OF-BIRTH           (N8)              /* TT2
*   2 REDEFINE DATE-OF-BIRTH
*     3 DOB-YYYY               (N4)
*     3 DOB-MM                 (N2)
*     3 DOB-DD                 (N2)
  2 X-LOAN-AMOUNT           (P5.2/9)
  2 X-LOAN-AMOUNT-2         (P7.2/9)
*   2 ADDRESS-1                                 /* JT1 start
*   2 ADDRESS-2
*   2 ADDRESS-3
*   2 ZIP-CODE
*   2 REDEFINE ZIP-CODE
*     3 ZIP-5                 (A5)
*     3 ZIP-4                 (A4)              /* JT1 end
  2 MILITARY-STATUS
*  2 RANK
  2 STATUS                  (9)
  2 PAY-STATUS              (9)
  2 NUM-MBRSHIPS            (9)
  2 ISSUE-DATE              (9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC         (9)
      4 ISSUE-DATE-YYYY       (N4)
      4 ISSUE-DATE-MM         (N2)
      4 ISSUE-DATE-DD         (N2)
  2 SPLIT-IND               (9)
  2 FACE-AMOUNT             (9)
  2 OWNERSHIP-FLAG          (9)
  2 PLAN                    (9)
  2 REDEFINE PLAN
    3 PLAN-OCC              (9)
      4 PLAN-A1              (A1)
      4 PLAN-A2              (A2)
  2 REDEFINE PLAN
    3 PLAN-OCC-2            (9)
      4 PLAN-A-2             (A2)
      4 PLAN-A-1             (A1)
  2 ANNUAL-PREM             (9)
  2 MODE                    (9)
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER                (N6)
  2 NOTE-DATE                (N8/60)
  2 NOTE-TEXT                (A60/60)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-CSV-OCC       (60)
      4 NOTE-TEXT-CSV-POL-ISS  (A9)
      4 NOTE-TEXT-CSV-S-1      (A1)
      4 NOTE-TEXT-CSV-ISS-MM   (N2)
      4 NOTE-TEXT-CSV-SLSH-1   (A1)
      4 NOTE-TEXT-CSV-ISS-DD   (N2)
      4 NOTE-TEXT-CSV-SLSH-2   (A1)
      4 NOTE-TEXT-CSV-ISS-YYYY (N4)
      4 NOTE-TEXT-CSV-S-2      (A1)
      4 NOTE-TEXT-CSV          (A8)
      4 NOTE-TEXT-CSV-S-3      (A1)
      4 NOTE-TEXT-CSV-ON-MM    (N2)
      4 NOTE-TEXT-CSV-ON-SH-1  (A1)
      4 NOTE-TEXT-CSV-ON-DD    (N2)
      4 NOTE-TEXT-CSV-ON-SH-2  (A1)
      4 NOTE-TEXT-CSV-ON-YYYY  (N4)
      4 NOTE-TEXT-CSV-REST     (A20)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-LAPSE-OCC     (60)
      4 NOTE-TEXT-LAP-POL-ISS  (A9)
      4 NOTE-TEXT-LAP-S-1      (A1)
      4 NOTE-TEXT-LAP-ISS-MM   (N2)
      4 NOTE-TEXT-LAP-SLSH-1   (A1)
      4 NOTE-TEXT-LAP-ISS-DD   (N2)
      4 NOTE-TEXT-LAP-SLSH-2   (A1)
      4 NOTE-TEXT-LAP-ISS-YYYY (N4)
      4 NOTE-TEXT-LAP-S-2      (A1)
      4 NOTE-TEXT-LAP          (A9)
      4 NOTE-TEXT-LAP-S-3      (A1)
      4 NOTE-TEXT-LAP-ON-MM    (N2)
      4 NOTE-TEXT-LAP-ON-SH-1  (A1)
      4 NOTE-TEXT-LAP-ON-DD    (N2)
      4 NOTE-TEXT-LAP-ON-SH-2  (A1)
      4 NOTE-TEXT-LAP-ON-YYYY  (N4)
      4 NOTE-TEXT-LAP-REST     (A19)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-REI-OCC       (60)
      4 NOTE-TEXT-REI-POL-ISS  (A9)
      4 NOTE-TEXT-REI-S-1      (A1)
      4 NOTE-TEXT-REI-ISS-MM   (N2)
      4 NOTE-TEXT-REI-SLSH-1   (A1)
      4 NOTE-TEXT-REI-ISS-DD   (N2)
      4 NOTE-TEXT-REI-SLSH-2   (A1)
      4 NOTE-TEXT-REI-ISS-YYYY (N4)
      4 NOTE-TEXT-REI-S-2      (A1)
      4 NOTE-TEXT-REI          (A10)
      4 NOTE-TEXT-REI-REST     (A29)
*
1 #DOB                      (A10)
1 #I1                       (I2)
1 #I2                       (I2)
1 #I3                       (I2)
1 #POL-NUM                  (A16)
1 #FACE                     (A10)
1 #PLAN-1                   (A10)
1 #PLAN-2                   (A10)
1 #PREMIUM                  (A10)
1 #FREQ-1                   (A15)   /* PM4
1 #FREQ-2                   (A15)   /* PM4
*
1 #MOD-PREM                 (P5.2)
*
1 #F-ID                     (A8)
1 #F-NAME                   (A70)   /* PM3
1 #F-ADDR-1                 (A70)   /* PM3
1 #F-ADDR-2                 (A70)   /* PM3
1 #F-ADDR-3                 (A70)   /* PM3
* 1 #F-ZIP                  (A10)   /* PM3
*
1 #CSV                      (P5)
1 #LAPSE                    (P5)
1 #RET                      (A3)
1 #DEL                      (A1) CONST <','>
1 #DELL                     (A1)
1 #FIRST/MID                (A25)
1 #SUFFIX                   (A25)
1 #HOLD-FIRST-NAME          (A45)
1 #HOLD-INTER-NAME          (A45)
1 #HOLD-NAME                (A45)
1 #Z-HYPH                   (A1)
1 #FOUND-ONE                (L)
1 #FOUND-REIN               (L)
*
1 DATE-OF-BIRTH           (N8)              /* TT2
1 REDEFINE DATE-OF-BIRTH                    /* TT2
  2 DOB-YYYY               (N4)             /* TT2
  2 DOB-MM                 (N2)             /* TT2
  2 DOB-DD                 (N2)             /* TT2
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
FORMAT  PS=55 LS=133
WRITE  TITLE LEFT
  10X 'AAFMAAN' 12X 'PAGE'
  2X *PAGE-NUMBER(1)
  / 10X '   2017 TERMINATED POLICY AUDIT REPORT - exceptions'   /* PM3   /* vt3 was 2010 /* vt4 was 2013  /* vt5 was 2014 /* vt6 was 2015 /* vt7 was 2016
FORMAT (2) PS=55 LS=133
WRITE (2) TITLE LEFT
  10X 'AAFMAA' 12X 'PAGE'
  2X *PAGE-NUMBER(2)
  / 10X '   2017 TERMINATED POLICY AUDIT REPORT - LAPSES'       /* PM3   /* vt3 was 2010 /* vt4 was 2013  /* vt5 was 2014 /* vt6 was 2015 /* vt7 was 2016
*
FORMAT (3) PS=55 LS=133
WRITE (3) TITLE LEFT
  10X 'AAFMAA' 12X 'PAGE'
  2X *PAGE-NUMBER(3)
  / 10X '   2017 TERMINATED POLICY AUDIT REPORT - CSV"S'        /* PM3   /* vt3 was 2010 /* vt4 was 2013  /* vt5 was 2014 /* vt6 was 2015 /* vt7 was 2016 
*
READ ST-V LOGICAL WITH NAME-KEY FROM 'A'
  IF   (#CSV   GE #NUMBER-REQ-CSV)
      AND (#LAPSE GE #NUMBER-REQ-LAPSE)
    ESCAPE BOTTOM
  END-IF
*
*  IF PROCESS-IND = 'S' OR = 'C'                /* RE1
  IF PROCESS-IND = 'C'                          /* RE1
      OR ST-V.ID-NUMBER LT 001900
      OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)        /* RE1
    ESCAPE TOP
  END-IF
*

  RESET #PDA-ADDRESS                                      /* JT1 start
  MOVE 'P' TO #ADD-TYPE
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
*   IF  (ADDRESS-CHG-CODE = 'U' OR = 'V')                 /* JT1
  IF F-UNDEL-ADDRESS (< ST-V.MEMBER-CONTACT-ID >)         /* JT1
*      OR (ADDRESS-1 = ' ')                               /* JT1
      OR (#ADD-LINE-1 = ' ')                              /* JT1
*      OR (ZIP-CODE = 0)                                  /* JT1
      OR (#ADD-ZIP-CODE = 0)                              /* JT1
      OR (INTRO-FLAG = 'S' OR = 'K' OR = 'G')
    ESCAPE TOP
  END-IF
*
  RESET #FOUND-ONE
  FOR #I1 1 TO 9
    IF ST-V.STATUS (#I1) = 'D' OR = ' '
      ESCAPE TOP
    END-IF
*
    FIND (1) NT-V WITH NT-V.ID-NUMBER = ST-V.ID-NUMBER
      FOR #I2 1 TO 60
        IF NT-V.NOTE-TEXT (#I2) = ' '
          ESCAPE BOTTOM
        END-IF
*
        IF NOTE-TEXT (#I2) = MASK ('* POL ISS')
          IF NOTE-TEXT-CSV (#I2) = "CSV'D ON"
              AND NOTE-TEXT-CSV-ISS-MM   (#I2) = ST-V.ISSUE-DATE-MM   (#I1)
              AND NOTE-TEXT-CSV-ISS-DD   (#I2) = ST-V.ISSUE-DATE-DD   (#I1)
              AND NOTE-TEXT-CSV-ISS-YYYY (#I2) = ST-V.ISSUE-DATE-YYYY (#I1)
              AND NOTE-TEXT-CSV-ON-YYYY  (#I2) = #YEAR-TO-CHECK
              AND #CSV LT #NUMBER-REQ-CSV
              AND FACE-AMOUNT (#I1) GE 100000
            PERFORM CHECK-FOR-REINSTATEMENT
            MOVE TRUE TO #FOUND-ONE
            ADD +1 TO #CSV
            DISPLAY (3) 'Cnt' #CSV
              'Id Num' ST-V.ID-NUMBER
              'Iss Date' ST-V.ISSUE-DATE (#I1)
              'Plan' ST-V.PLAN (#I1)
              'Face' ST-V.FACE-AMOUNT (#I1)
            ADD ST-V.FACE-AMOUNT (#I1) TO #TOT-CSVS  /* DW3
            ESCAPE BOTTOM
          END-IF
*
          IF NOTE-TEXT-LAP (#I2) = 'LAPSED ON'
              AND NOTE-TEXT-LAP-ISS-MM   (#I2) = ST-V.ISSUE-DATE-MM   (#I1)
              AND NOTE-TEXT-LAP-ISS-DD   (#I2) = ST-V.ISSUE-DATE-DD   (#I1)
              AND NOTE-TEXT-LAP-ISS-YYYY (#I2) = ST-V.ISSUE-DATE-YYYY (#I1)
              AND NOTE-TEXT-LAP-ON-YYYY  (#I2) = #YEAR-TO-CHECK
              AND #LAPSE LT #NUMBER-REQ-LAPSE
              AND FACE-AMOUNT (#I1) GE 300000
            PERFORM CHECK-FOR-REINSTATEMENT
            MOVE TRUE TO #FOUND-ONE
            ADD +1 TO #LAPSE
            DISPLAY (2) 'Cnt' #LAPSE
              'Id Num' ST-V.ID-NUMBER
              'Iss Date' ST-V.ISSUE-DATE (#I1)
              'Plan' ST-V.PLAN (#I1)
              'Face' ST-V.FACE-AMOUNT (#I1)
            ADD ST-V.FACE-AMOUNT (#I1) TO #TOT-LAPS  /* DW3
            ESCAPE BOTTOM
          END-IF
        END-IF
      END-FOR
    END-FIND
*
    IF #FOUND-ONE
      RESET #POL-NUM #FACE #PLAN-1 #PLAN-2 #PREMIUM #FREQ-1 #FREQ-2
*
      CALLNAT 'GPLNMSXX' #POL-NUM ID-NUMBER #I1 /* build POL-#
*
      MOVE EDITED FACE-AMOUNT (#I1) (EM=ZZZ,ZZZ.99) TO #FACE
      move left justified #face to #face                     /* vt1
      DECIDE ON FIRST VALUE OF MODE (#I1)
        VALUE 'G' MOVE 'Monthly'   TO #FREQ-1
          MOVE 'Allotment' TO #FREQ-2
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 12
        VALUE 'E' MOVE 'Monthly'   TO #FREQ-1
          MOVE 'EZ-Pay'    TO #FREQ-2
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 12
        VALUE 'Q' MOVE 'Quarterly' TO #FREQ-1
          MOVE ' '    TO #FREQ-2                       /* PM4 - Removed CHECKS
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 4
        VALUE 'S' MOVE 'Semi-Ann'  TO #FREQ-1
          MOVE ' '    TO #FREQ-2                       /* PM4 - Removed CHECKS
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 2
        VALUE 'A' MOVE 'Annual'    TO #FREQ-1
          MOVE ' '    TO #FREQ-2                       /* PM4 - Removed CHECKS
          MOVE ANNUAL-PREM (#I1) TO #MOD-PREM
        VALUE 'M' MOVE 'Monthly'   TO #FREQ-1
          MOVE ' '    TO #FREQ-2                       /* PM4 - Removed CHECKS
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 12
        VALUE '0' MOVE 'Monthly'   TO #FREQ-1
          MOVE 'Credit Card' TO #FREQ-2                /* PM4
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 12
        VALUE '1' MOVE 'Annual'    TO #FREQ-1
          MOVE 'Credit Card' TO #FREQ-2                /* PM4
          MOVE ANNUAL-PREM (#I1) TO #MOD-PREM
        VALUE '2' MOVE 'Semi-ann'  TO #FREQ-1
          MOVE 'Credit Card' TO #FREQ-2                /* PM4
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 2
        VALUE '4' MOVE 'Quarterly' TO #FREQ-1
          MOVE 'Credit Card' TO #FREQ-2                /* PM4
          COMPUTE #MOD-PREM = ANNUAL-PREM (#I1) / 4
        NONE WRITE ID-NUMBER 'Invalid mode'
      END-DECIDE
*
      DECIDE ON FIRST VALUE OF PLAN (#I1)
        VALUE 'AL'  MOVE 'ANNUITY'   TO #PLAN-1   /* DW1
          MOVE 'LIFE'      TO #PLAN-2   /* DW1
        VALUE 'OL'  MOVE 'Ordinary'  TO #PLAN-1
          MOVE 'Life'      TO #PLAN-2
        VALUE '20P' MOVE '20'        TO #PLAN-1
          MOVE 'Pay'       TO #PLAN-2
        VALUE '30P' MOVE '30'        TO #PLAN-1
          MOVE 'Pay'       TO #PLAN-2
        VALUE '7P'  MOVE '7'         TO #PLAN-2
          MOVE 'Pay'       TO #PLAN-2
        VALUE 'LT'  MOVE 'Level'     TO #PLAN-1
          MOVE 'Term'      TO #PLAN-2
        VALUE 'RT'  MOVE 'Renewable' TO #PLAN-1
          MOVE 'Term'      TO #PLAN-2
        VALUE 'EZ'  MOVE 'EZ Term'   TO #PLAN-1                        /* YK2
          MOVE 'Term'      TO #PLAN-2                        /* YK2
        VALUE 'SPF' MOVE 'Single'    TO #PLAN-1
          MOVE 'Pay'       TO #PLAN-2
        VALUE 'GPL' MOVE 'Gen Plus'  TO #PLAN-1    /* DW4
          MOVE 'Life'      TO #PLAN-2
        VALUE 'GP2' MOVE 'Gen Plus 2' to #PLAN-1           /* TT1
          MOVE 'Life'      TO #PLAN-2                      /* TT1
*       NONE   IF (PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z')    /* YK1
        NONE   IF F-LT2-TRUE(<PLAN(#I1)>)                              /* YK1
            MOVE 'Level'        TO #PLAN-1
            MOVE 'Term II'      TO #PLAN-2
          ELSE
            IF PLAN-A-1 (#I1) = 'F'
              IF PLAN (#I1) = MASK ('.P.')
                MOVE PLAN-A1 (#I1) TO #PLAN-1
              ELSE
                MOVE PLAN-A-2 (#I1) TO #PLAN-1
              END-IF
*
              MOVE 'Pay' TO #PLAN-2
            ELSE
              MOVE PLAN (#I1)     TO #PLAN-1
              RESET                  #PLAN-2
              WRITE ID-NUMBER 'invalid plan' PLAN (#I1)
            END-IF
          END-IF
      END-DECIDE
      MOVE EDITED #MOD-PREM (EM=Z,ZZZ.99) TO #PREMIUM
      move left justified #premium to #premium
*
      IF PAY-STATUS (#I1) GT '40'
        MOVE 'N/A'  TO #PREMIUM             /* vt1 \/
        MOVE 'Paid' TO #FREQ-1
        MOVE 'Up'  TO #FREQ-2               /* vt1 /\
      END-IF
*
      RESET DATE-OF-BIRTH                                                          /* TT2
      DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)                 /* TT2

      COMPRESS DOB-MM DOB-DD DOB-YYYY INTO #DOB WITH '/'
      COMPRESS 'CN' ID-NUMBER-A INTO #F-ID LEAVING NO SPACE
*
      MOVE ST-V.ID-NUMBER TO #CGA-SCR-ID
      MOVE #I1 TO #CONTACT-SEG                /* get owner of policy
      CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
      IF ST-V.ID-NUMBER NE #ID-NUM
        WRITE ST-V.ID-NUMBER 'is owned by' #ID-NUM
        MOVE #ID-NUM TO #CGA-ID
        FIND (1) ST-V WITH MEMBER-KEY =#CGA-ID-G
        END-FIND
      END-IF
*
*      IF MILITARY-STATUS = 'R'
*         MOVE 'RET' TO #RET
*         MOVE ',' TO #DELL
*      ELSE
*         MOVE ' ' TO #DELL
*         MOVE '   ' TO #RET
*      END-IF
*
*      SEPARATE NAME INTO #LAST-NAME #FIRST/MID #SUFFIX
*         WITH DELIMITER #DEL
*      COMPRESS RANK #FIRST/MID INTO #HOLD-FIRST-NAME LEAVING NO SPACE
*      IF #SUFFIX = '   '
*        COMPRESS #HOLD-FIRST-NAME #LAST-NAME #SUFFIX INTO #HOLD-NAME
*      ELSE
*        COMPRESS #HOLD-FIRST-NAME #LAST-NAME INTO #HOLD-INTER-NAME
*        COMPRESS #HOLD-INTER-NAME ',' #SUFFIX INTO #HOLD-NAME LEAVING NO
*      END-IF
*
*      COMPRESS #HOLD-NAME #DELL INTO #HOLD-INTER-NAME LEAVING NO SPACE
*      COMPRESS #HOLD-INTER-NAME #RET INTO #F-NAME
*      ASSIGN #F-ADDR-1 = ADDRESS-1
*      IF ZIP-4 = '0000'
*        ASSIGN ZIP-4 = '    '
*        ASSIGN #Z-HYPH = ' '
*      ELSE
*        ASSIGN #Z-HYPH = '-'
*      END-IF
*
*      COMPRESS ZIP-5 #Z-HYPH ZIP-4 INTO #F-ZIP LEAVING NO SPACE
*      IF ADDRESS-3 = '   '
*        COMPRESS ADDRESS-2 #F-ZIP INTO #F-ADDR-2
*        RESET #F-ADDR-3
*      ELSE
*        ASSIGN #F-ADDR-2 = ADDRESS-2
*        COMPRESS ADDRESS-3 #F-ZIP INTO #F-ADDR-3
*      END-IF
*
      WRITE (4) NOTITLE USING FORM 'ATHFAUD2'
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-READ
*
WRITE (2) / '  Total Face Amount ' #TOT-LAPS  /* DW3
WRITE (3) / '  Total Face Amount ' #TOT-CSVS  /* DW3
* * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-FOR-REINSTATEMENT
* * * * * * * * * * * * * * * * * * * * *
*
RESET #FOUND-REIN
FOR #I3 1 TO 60
  IF NT-V.NOTE-TEXT (#I3) = ' '
    ESCAPE BOTTOM
  END-IF
*
  IF NOTE-TEXT (#I3) = MASK ('* POL ISS')
    IF NOTE-TEXT-REI (#I3) = 'REINSTATED'
        AND NOTE-TEXT-REI-ISS-MM   (#I3) = ST-V.ISSUE-DATE-MM   (#I1)
        AND NOTE-TEXT-REI-ISS-DD   (#I3) = ST-V.ISSUE-DATE-DD   (#I1)
        AND NOTE-TEXT-REI-ISS-YYYY (#I3) = ST-V.ISSUE-DATE-YYYY (#I1)
      MOVE TRUE TO #FOUND-REIN
    END-IF
  END-IF
END-FOR
*
IF #FOUND-REIN
  WRITE 'Found  reinstatement for id ' ST-V.ID-NUMBER
    'policy = ' ST-V.ISSUE-DATE (#I1)
  ESCAPE BOTTOM
END-IF
*
END-SUBROUTINE
*
END
