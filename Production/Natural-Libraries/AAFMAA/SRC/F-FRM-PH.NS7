* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** Function F-FRM-PH.
/** :author ykegeles
DEFINE FUNCTION F-FORMATTED-PHONE RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID    (N8)
  1 #PHONE-TYPE   (A1)
  LOCAL USING OBJLCOPH
  LOCAL
  1 #PHONE (A) DYNAMIC
  1 #DEL   (A1) CONST <'-'>
  1 #DIAL-NUMBER (N7)
  1 REDEFINE #DIAL-NUMBER
   2 #DIAL-NUMBER-3 (A3)
   2 #DIAL-NUMBER-4 (A4)
  1 #EXT            (A) DYNAMIC
  END-DEFINE
*
FIND CNT-PHONE-V WITH CONTACT-ID = #CONTACT-ID
  DECIDE FOR FIRST CONDITION
    WHEN PHONE-TYPE-CODE = #PHONE-TYPE AND TYPE = 'D'
      #DIAL-NUMBER := CNT-PHONE-V.DIAL-NUMBER
      COMPRESS FULL CNT-PHONE-V.AREA-CODE #DIAL-NUMBER-3 #DIAL-NUMBER-4 INTO
        #PHONE WITH DELIMITER #DEL
        ESCAPE BOTTOM
    WHEN PHONE-TYPE-CODE = #PHONE-TYPE AND TYPE = 'I'
      #PHONE := CNT-PHONE-V.INTL-DIAL-NUMBER
      #PHONE := *TRIM(#PHONE)
   COMPRESS FULL CNT-PHONE-V.INTL-ACCESS-CODE #PHONE INTO #PHONE WITH DELIMITER #DEL
        ESCAPE BOTTOM
    WHEN NONE
      IGNORE
  END-DECIDE
END-FIND
IF #PHONE <> ' '
  IF CNT-PHONE-V.EXT <> 0
    MOVE EDITED CNT-PHONE-V.EXT (EM=ZZZZZ9) TO #EXT
    COMPRESS FULL #PHONE 'Ext.' *TRIM(#EXT) INTO #PHONE
  END-IF
  F-FORMATTED-PHONE := #PHONE
END-IF
END-FUNCTION
END
