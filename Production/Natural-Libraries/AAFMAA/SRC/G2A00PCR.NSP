* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: G2A00PCR
* THIS PROGRAM PROCESSES THE 'CR' (ALPHA) CROSS-REFERENCE SCREEN
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG REASON                                            *
* PAM  06112000 PM1 Split porgrams for AAFMAA and AFSC                *
* PAM  04262005 PM2 Added Lockbox by SSN                              *
* SAG  11242005 RH1  SS - Replaced all calls to assembler program WAASSRT1
*                    with Natural subprogram SAGSRT7.
* DRW  12132005 DW3 Allow AFSC access to ALL Dual mbrs (2 cns aafm/afsc)
* SAG  03302006 RH1  AS - Commented out incorrectly coded "IF" statement
* YAK  06282007 YK1  Fix logic to populate first line on the screen when
*                    searching by Application Tracking name
* DRW  09/27/2007 DW4 Delete AFSC Suffix's (AFS)
* DRW  07/23/2009 DW5 SAS Unification
* DRW  03042007  DW6  Show samples up to 1900
* DRW  03192007  DW7  Show RANK in INS PLUS AAFMAA Members
* DRW 08/10/2009 DW8 Use Superdescriptor PROCESS-NAME KEY
* DRW 09/22/2009 DW9 Change default of Commands to go to / Unification
* NGG 11/23/2009 NG1 Fix the incorrect number of messages on the NX screen
* NGG 07/19/2010 NG2 Call subprogram instead of function
* NGG 01/12/2011 NG3 AFSC selection fix
* RSE 02/16/2011 RE1 Use ZIP-CODE as search criteria for correct sorting
* NGG 20130801   NG4 Address normalization
* TMT 05/14/2014 TT1 Contact Normalization - Emails
************************************************************************
*
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX       /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL  USING A-IFAFFL   /* PDA used with FUNCTION F-if-AFFL     /* DW9
LOCAL USING ADDA0001      /*NG4
LOCAL
*
1 BLANK (A1) CONST <' '>
1 #SAS-IAS   (A8/7)         /* DW9
1 #HAS-AAFM    (L)          /* DW9
*
1 #SERVICES          (A1) INIT<'S'>  /* PM1
1 #INSURANCE         (A1) INIT<'I'>  /* PM1
1 #OPT-OUT           (A1) INIT<'O'>  /* PM1
1 #DE-GRANDFATHERED  (A1) INIT<'D'>  /* PM1
1 #BOTH              (A1) INIT<'B'>  /* PM1
1 #GRANDFATHERED     (A1) INIT<'G'>  /* PM1
1 #BLANK             (A1) INIT<' '>  /* PM1
1 #SERVICES-AT-FLAG  (A1) INIT<'V'>  /* PM1
*
1 #ZIP-FROM      (N5)
1 #ZIP-THRU      (N5)
1 #ZIP-CNT-WIDS  (P5)
1 #ZIP-CNT-ACTS  (P5)
1 #ZIP-CNT-RETS  (P5)
1 #ZIP-CNT-ALLS  (P5)
1 #FOUND         (I4)
1 #CN-SELECTED   (N6/1:140)
1 #ZIP-SELECTED  (N9/1:140)
1 #ZIP-CODE-A    (A9)
1 REDEFINE #ZIP-CODE-A
  2 #ZIP-CODE    (N9)
1 REDEFINE #ZIP-CODE-A
  2 #ZIP5        (N5)
  2 #ZIP4        (N4)
1 LN             (I4)
1 L              (I4)
*
1 #CR-DEPOSIT    (A1)                                             /* PM2
1 #CR-TYPE       (A1/15)
1 REDEFINE #CR-TYPE
  2 #CR-TYPE-2    (A1/15)
1 #X             (I2)
1 #RANK-LBL      (A4) INIT <'Rank'>
1 #CR-NAME-SSN   (A1)   /* "N" or 'S'
1 #CR-START      (A25/15)
1 REDEFINE #CR-START
  2 #CR-START-2   (A25/15)
1 REDEFINE #CR-START
  2 #CR-START-3   (A25/15)
1 #USER-START    (A3)
1 #SEL-DD     (N2)
1 REDEFINE #SEL-DD
  2 #SEL-DD-A  (A2)
1 #SEL-MONTH     (N2)
1 REDEFINE #SEL-MONTH
  2 #SEL-MONTH-A  (A2)
1 #SEL-YYYY      (N4)
1 REDEFINE #SEL-YYYY
  2 #SEL-YYYY-A   (A4)
*
1 #END-DATE      (N8)
1 REDEFINE #END-DATE
  2 #END-YYYY     (N4)
  2 #END-MM       (N2)
  2 #END-DD       (N2)
*
1 #SEL-APPL-SOURCE (A7)
1 #SEL-ACT-FLAG    (A1)
1 #SEL-APPL-STATUS (A2)
1 REDEFINE #SEL-APPL-STATUS
  2 #SEL-APPL-STAT-1 (A1)
  2 #SEL-APPL-STAT-2 (A1)
1 #SEL-APPL-SEQ     (A1) INIT <'D'>
*
1 #INT-ADDR-EXT  (A3)
1 REDEFINE #INT-ADDR-EXT
  2 #INT-ADDR-EXT-X (A1/3)
1 #E-MAIL-EXT    (A3)
*
1 #NO-MENU       (L)
1 #RESET-SEARCH  (L)
1 #NEXT-ZIP      (N9)   /* RE1 formerly N5
1 #TOP-ZIP       (N5)
1 #ZIP-CNT       (N5)
1 #TEXT          (A75)
1 #SKIP-TO       (A25)
1 REDEFINE #SKIP-TO
  2 #SKIP-TO-ZIP (N5)
1 REDEFINE #SKIP-TO     /* RE1
  2 #SKIP-TO-ZIP-9 (N9) /* RE1
1 REDEFINE #SKIP-TO
  2 #SKIP-TO-SSN (N9)
1 REDEFINE #SKIP-TO
  2 #SKIP-TO-SSN-A (A9)                                            /* PM2
1 REDEFINE #SKIP-TO
  2 #SKIP-TO-1  (A1/25)
1 REDEFINE #SKIP-TO
  2 #SKIP-TO-INT (A6)
*
1 #SCR-NAME-C   (C)
1 #SCR-SSN-C    (C)
1 #SCR-INTERNET-C (C)
1 #SCR-ZIP-CODE-C (C)
1 #SCR-SELECT-CMND (A2/7)
1 #SCR-NAME     (A25/7)
1 #SCR-NAME-DECEASED (A25/7)
1 #SCR-SSN         (N9/7)
1 #SCR-INTERNET    (A60/7)
1 REDEFINE #SCR-INTERNET
  2 #SCR-INTERNET-OCC  (7)
    3 #SCR-INTERNET-FILL  (A41)
    3 #SCR-DATE-RCVD      (A19)
1 #SCR-MIL-SERV    (A4/7)
1 #SCR-APPL-SOURCE (A6/7)
1 #SCR-NUM-MBRSHPS (A2/7)
1 #SCR-ADDR1       (A25/7)
1 #SCR-ADDR2       (A25/7)
1 #SCR-ADDR3       (A25/7)
1 #SCR-ZIP-5       (A5/7)
1 #SCR-ZIP-4       (A5/7)
1 #SCR-ID-NO       (N6/7)
1 #SCR-RANK        (A3/7)
1 #SCR-STATUS      (A1/7)
*   ar fields
1 #AR-SCR-SELECT-CMND (A2/18)
1 #AR-SCR-PHONE       (A12/18)
1 #AR-SCR-SSN         (A9/18)
1 REDEFINE #AR-SCR-SSN
  2 #AR-SCR-SSN-N (N9/18)
1 #AR-SCR-ACTION-FLAG (A1/18)
1 #AR-SCR-APPL-STATUS (A2/18)
1 #AR-SCR-NAME        (A25/18)
1 #AR-SCR-REC-DATE    (A10/18)
1 #AR-SCR-USER        (A3/18)
1 #AR-SCR-APPL-SOURCE (A7/18)
1 #AR-SCR-TYPE        (A1/18)
1 #AR-SCR-INQ-ID      (A6/18)
1 REDEFINE #AR-SCR-INQ-ID
  2 #AR-SCR-INQ-ID-N   (N6/18)
*
1 I      (I1)
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2) INIT <1>
1 #I4    (I2)
*
* below must be in sync with WD-V & #hold-st-data
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER        (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A     (A6)
  2 MEMBER-FLAG      (A1)
  2 MEMBER-CONTACT-ID (N8)   /* NG99
  2 WIDOW-FLAG       (A1)
*  2 INTERNET-ADDR
*  2 REDEFINE INTERNET-ADDR
*    3 INTERNET-ADDR-25 (A25)
*  2 REDEFINE INTERNET-ADDR
*    3 INT-ADDR-X (A1/60)
  2 RANK             (A3)
  2 STATUS-CODE-SP   (A1)
  2 PROCESS-IND      (A1)    /* PM1
  2 OTHER-COMPANY-ID (N6)
  2 REDEFINE OTHER-COMPANY-ID
    3 OTHER-COMPANY-ID-A (A6)
  2 MILITARY-STATUS  (A1)
  2 MILITARY-SERVICE (A3)
  2 SSN              (N9)
  2 REDEFINE SSN
    3 SSN-2           (N2)
  2 SSN-SP           (N9)
  2 NAME             (A25)
  2 REDEFINE NAME
    3 NAME-25         (A25)
  2 REDEFINE NAME
    3 NAME-2          (A2)
  2 NAME-SP          (A25)
  2 APPL-SOURCE      (A7)
  2 TITLE-SP         (A4)
  2 NUMBER-MEMBERSHIPS (P1)
  2 STATUS  (9)
  2 C*ACTIVE-SEGMENTS
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 SSN              (N9)
  2 HOME-PHONE
  2 INTERNET-ADDR    (A60)
  2 NAME             (A25)
*   2 ADDRESS-1        (A25) /*NG4 BEGIN
*   2 ADDRESS-2        (A25)
*   2 ADDRESS-3        (A25)
*   2 REDEFINE ADDRESS-3
*     3 ADD3   (A20)
*     3 FILLER (A5)
*   2 ZIP-CODE         (N9)
*   2 REDEFINE ZIP-CODE
*     3 ZIP1   (A5)
*     3 REDEFINE ZIP1
*       4 ZIP1-N  (N5)
*     3 ZIP2   (A4)
*     3 REDEFINE ZIP2
*       4 ZIP2-N (N4)        /*NG4 END
  2 USER-SOURCE      (A3)
  2 APPL-SOURCE
  2 DATE-RECEIVED    (N8)
  2 REDEFINE DATE-RECEIVED
    3 DR-YYYY          (N4)
    3 DR-MM            (N2)
    3 DR-DD            (N2)
  2 REDEFINE DATE-RECEIVED
    3 DATE-RECEIVED-A  (A8)
  2 ACTION-FLAG      (A1)
  2 AT-STATUS        (A2)
  2 REDEFINE AT-STATUS
    3 #AT-STAT-1  (A1)
    3 #AT-STAT-2  (A1)
  2 ID-NUMBER        (N6)
  2 MEMBER-CN        (A6)
  2 POLICY-TYPE      (A1)
  2 INTRO-FLAG       (A1)   /* PM1
  2 ST-NOTE-USER     (A3/50)
  2 ST-NOTE-STATUS   (A2/50)
  2 REDEFINE ST-NOTE-STATUS
    3 ST-NOTE-STATUS-OCC (50)
      4 #AT-NOTE-STAT-1 (A1)
      4 #AT-NOTE-STAT-2 (A1)
*
  2 ST-NOTE-DATE     (N8/50)
  2 REDEFINE ST-NOTE-DATE
    3 ST-NOTE-DATE-OCC (50)
      4 #AT-YYYY        (N4)
      4 #AT-MM          (N2)
      4 #AT-DD          (N2)
  2 REDEFINE ST-NOTE-DATE
    3 #ST-NOTE-DATE-A (A8/50)
*
1 EMAIL-V VIEW OF A-CONTACT-EMAIL     /* TT1 BEGIN
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-ADDRESS
  2 REDEFINE EMAIL-ADDRESS
    3 EMAIL-ADDRESS-25 (A25)
  2 REDEFINE EMAIL-ADDRESS
    3 EMAIL-ADDRESS-X (A1/60)         /* TT1 END
*
1 #SRT-TABLE  (500)
  2 #SRT-NAME      (A25)
  2 REDEFINE #SRT-NAME
    3 #SRT-ST-DATE   (N8)
    3 #SRT-ST-STATUS (A2)
    3 #SRT-ST-USER   (A3)
    3 #SRT-AT-NAME   (A12)
  2 #SRT-ID-NUMBER  (N9)
  2 REDEFINE #SRT-ID-NUMBER
    3 #SRT-FILL-1       (A3)
    3 #SRT-ID-NUMBER-A  (A6)
  2 REDEFINE #SRT-ID-NUMBER
    3 #SRT-FILL-2      (A3)
    3 #SRT-ID-NUMBER-6 (N6)
*
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2) INIT <0>
  2 #SRT-ENTRY-LENGTH (I2) INIT <34>
  2 #SRT-KEY-LENGTH   (I2) INIT <34>
  2 #SRT-KEY-POSITION (I2) INIT <0>  /* SORT-NAME/CN
  2 #SRT-SORT-TYPE    (A1) INIT <'A'>
*
1 #YYYYMMDD     (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A  (A4)
  2 #MM-A    (A2)
  2 #DD-A    (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYY    (N4)
  2 #MM      (N2)
  2 #DD      (N2)
*
1 #SEARCH-BY-P-I      (L)     /* DW8
1 #SUFFIX             (A3)
1 REDEFINE #SUFFIX
  2 #SUFFIX-1    (A1)
1 #P-I-NAME-SUPER   (A26)     /* DW8
*
1 #ESCAPE (I4)                /* NG1
*
END-DEFINE
* * * * * * * * * * * * * *
INCLUDE GONERCXX
*
IF   (#CGA-SCR-SUFFIX = 'S' OR = 'O' OR = 'D'    /* Read by Process Ind /* DW8
    OR = 'I'  OR = 'G' OR = 'C')
    AND (NOT #CGA-SCR-COMMAND = 'AR' OR = 'IR')     /* DW8
  MOVE #CGA-SCR-SUFFIX TO #SUFFIX
  MOVE #SUFFIX-1 TO #P-I-NAME-SUPER             /* DW8
  MOVE TRUE TO #SEARCH-BY-P-I
*
  IF #CGA-SCR-COMMAND = 'WR'                       /* DW8
    MOVE 'X' TO #CR-TYPE (5)   /* Can only do member Name search by name now
  ELSE
    MOVE 'X' TO #CR-TYPE (1)   /* Can only do member Name search by name now
  END-IF
END-IF                                                     /* DW5
*
IF #CGA-SCR-COMMAND = 'WR'
  MOVE TRUE TO #NO-MENU
  IF #CGA-SCR-SUFFIX = 'SSN'
    MOVE 'X' TO #CR-TYPE (6)
  ELSE
    MOVE 'X' TO #CR-TYPE (5)
  END-IF
ELSE
  IF #CGA-SCR-COMMAND = 'IR'
    MOVE TRUE TO #NO-MENU
    IF #CGA-SCR-SUFFIX = 'SSN'
      MOVE 'X' TO #CR-TYPE (8)
    ELSE
      MOVE 'X' TO #CR-TYPE (7)
    END-IF
  ELSE
    IF #CGA-SCR-COMMAND = 'AR'
      MOVE TRUE TO #NO-MENU
      IF #CGA-SCR-SUFFIX = 'SSN'
        MOVE 'X' TO #CR-TYPE (11)
      ELSE
        MOVE 'X' TO #CR-TYPE (10)
      END-IF
    END-IF
  END-IF
END-IF
*
IF #CGA-SCR-SUFFIX = 'SMX'
  RESET #CR-TYPE (10)
  MOVE 'X' TO #CR-TYPE (12)
* MOVE 'S*' TO #SEL-APPL-STATUS
  MOVE #CGA-CLERK TO #USER-START
  MOVE 'D' TO #SEL-APPL-SEQ
  MOVE *DATN TO #YYYYMMDD
  SUBTRACT 13 FROM #DD
*
  IF #DD LT 1
    ADD 28 TO #DD
    SUBTRACT 1 FROM #MM
    IF #MM LT 1
      ADD 12 TO #MM
      SUBTRACT 1 FROM #YYYY
    END-IF
  END-IF
*
  MOVE #YYYY TO #SEL-YYYY
  MOVE #MM   TO #SEL-MONTH
  MOVE #DD   TO #SEL-DD
END-IF
*
MOVE 'Select Type of Search' TO #TEXT
MOVE '*' TO #CGA-SCR-COMMAND
*
IF #CGA-ORIG-SUFFIX = 'SM'
  RESET #CR-TYPE (10)
  MOVE 'X' TO #CR-TYPE (12)
  MOVE #CGA-CLERK TO #USER-START
  MOVE 'A'        TO #SEL-APPL-SEQ
  MOVE 'SM'       TO #SEL-APPL-STATUS
  MOVE *DATN TO #YYYYMMDD
  SUBTRACT 1 FROM #MM
  IF #MM = 0
    MOVE 12 TO #MM
    SUBTRACT 1 FROM #YYYY
  END-IF
* MOVE #YYYY TO #SEL-YYYY
* MOVE #MM   TO #SEL-MONTH
* MOVE #DD   TO #SEL-DD
*
  MOVE #YYYY TO #END-YYYY
  MOVE #MM   TO #END-MM
  MOVE #DD   TO #END-DD
*
  MOVE 2003  TO #SEL-YYYY
  MOVE 01    TO #SEL-MONTH
  MOVE 01    TO #SEL-DD
ELSE
  IF NOT #SEARCH-BY-P-I                        /* DW8
    INPUT TEXT #TEXT MARK *#CR-TYPE (1) USING MAP 'G2A00MCR'
  END-IF
END-IF
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-ID
*
DECIDE FOR FIRST CONDITION
  WHEN #CR-TYPE (1)  NE ' ' MOVE 1  TO #X PERFORM CR-MBR-NAME
  WHEN #CR-TYPE (2)  NE ' ' MOVE 2  TO #X PERFORM CR-MBR-SSN
  WHEN #CR-TYPE (3)  NE ' ' MOVE 3  TO #X PERFORM CR-MBR-INTERNET
  WHEN #CR-TYPE (4)  NE ' ' MOVE 4  TO #X PERFORM CR-MBR-ZIP
  WHEN #CR-TYPE (5)  NE ' ' MOVE 5  TO #X PERFORM CR-WID-NAME
  WHEN #CR-TYPE (6)  NE ' ' MOVE 6  TO #X PERFORM CR-WID-SSN
  WHEN #CR-TYPE (7)  NE ' ' MOVE 7  TO #X PERFORM CR-INTRO
  WHEN #CR-TYPE (8)  NE ' ' MOVE 8  TO #X PERFORM CR-INTRO
  WHEN #CR-TYPE (9)  NE ' ' MOVE 9  TO #X PERFORM CR-ZIP-COUNT
  WHEN #CR-TYPE (10) NE ' ' MOVE 10 TO #X
    IF #CGA-USER-COMPANY = #SERVICES
      PERFORM CR-APPL
    ELSE
      PERFORM CR-APPL-BY-NAME
    END-IF
  WHEN #CR-TYPE (11) NE ' ' MOVE 11 TO #X PERFORM CR-APPL
  WHEN #CR-TYPE (12) NE ' ' MOVE 12 TO #X PERFORM CR-APPL-BY-USER
  WHEN #CR-TYPE (13) NE ' ' MOVE 13 TO #X PERFORM CR-SAMPLE-NAME
  WHEN #CR-TYPE (14) NE ' ' MOVE 14 TO #X PERFORM CR-RETIREE-NAME
  WHEN #CR-TYPE (15) NE ' ' MOVE 15 TO #X PERFORM CR-ASSOCIATE-NAME
  WHEN #CR-DEPOSIT   NE ' '               PERFORM CR-DEPOSIT      /* PM2
  WHEN NONE                 MOVE 1  TO #X PERFORM CR-MBR-NAME
END-DECIDE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-MBR-NAME
* * * * * * * * * * * * * * * * *
*
MOVE 'Member Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  IF #SEARCH-BY-P-I           /* Read by Process Ind /* DW8
    COMPRESS 'Search by Process Ind =' #CGA-SCR-SUFFIX INTO #TEXT
*
    READ ST-V BY PROCESS-NAME-KEY STARTING FROM #P-I-NAME-SUPER    /* DW8
      IF ST-V.PROCESS-IND NE #SUFFIX-1
        ESCAPE BOTTOM
      END-IF

      IF ST-V.ID-NUMBER LE 1900
        ESCAPE TOP
      END-IF
*
*      PERFORM CHECK-OTHER-COMP    /* DW9
      PERFORM DO-NAME
    END-READ /* (0459)
*
    IF #RESET-SEARCH                         /* DW8
      COMPRESS #SUFFIX-1 #SKIP-TO INTO #P-I-NAME-SUPER LEAVING NO
      ESCAPE TOP
    END-IF                                   /* END DW8
*
    PERFORM SHOW-SCREEN
*
    IF #CGA-SCR-COMMAND NE '*'
      MOVE 'CR' TO #CGA-SCR-COMMAND
    END-IF
    ESCAPE TOP
  END-IF                                            /* END DW8
*
  READ ST-V BY NAME-KEY STARTING FROM #SKIP-TO
    IF ST-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
    PERFORM DO-NAME
  END-READ /* (0499)
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
*
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * *
DEFINE SUBROUTINE DO-NAME
* * * * * * * * * * * * *
*
ADD 1 TO I
RESET #RESET-SEARCH
IF I GT 7
  MOVE ST-V.NAME TO #SKIP-TO
  PERFORM SHOW-SCREEN
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  IF ST-V.NAME-25 NE #SKIP-TO
    MOVE TRUE TO #RESET-SEARCH
    ESCAPE BOTTOM
  END-IF
*
  MOVE 1 TO I
END-IF
*
PERFORM BUILD-SCREEN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-MBR-INTERNET
* * * * * * * * * * * * * * * * *
*
MOVE 'Member Internet Addr Search' TO #TEXT
MOVE (AD=I) TO #SCR-INTERNET-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  READ EMAIL-V BY EMAIL-ADDRESS STARTING FROM #SKIP-TO-INT /* PM CHG 05/2000    /* TT1
    FIND ST-V WITH MEMBER-CONTACT-ID = EMAIL-V.CONTACT-ID                       /* TT1
      IF #E-MAIL-EXT NE ' '
        MOVE 4 TO #I2
        FOR #I1 60 TO 1 STEP -1
          IF (#I2 LE 1) OR (EMAIL-ADDRESS-X (#I1) = '.')                        /* TT1
            ESCAPE BOTTOM
          END-IF
*
          IF EMAIL-ADDRESS-X (#I1) NE ' '                                       /* TT1
            SUBTRACT 1 FROM #I2
            MOVE EMAIL-ADDRESS-X (#I1) TO #INT-ADDR-EXT-X (#I2)                 /* TT1
          END-IF
        END-FOR
*
        IF #INT-ADDR-EXT NE #E-MAIL-EXT                                         /* TT1
          ESCAPE TOP
        END-IF
      END-IF
*
      IF #CGA-SCR-SUFFIX = 'UND'   /* only show undeliverable
        IF F-UNDEL-ADDRESS (< ST-V.MEMBER-CONTACT-ID >)   /* NG99
*       IF  (ST-V.ADDRESS-CHG-CODE = 'U' OR = 'V')
*           OR (ST-V.ADDRESS-1 = ' ')
          IGNORE
        ELSE
          ESCAPE TOP
        END-IF
      END-IF
*
      ADD 1 TO I
      RESET #RESET-SEARCH
      IF I GT 7
        MOVE EMAIL-ADDRESS TO #SKIP-TO         /* TT1
        PERFORM SHOW-SCREEN
*
        IF #CGA-SCR-COMMAND NE '*'
          FETCH 'G1000PXX'
        END-IF
*
*       IF ST-V.INTERNET-ADDR-25 NE #SKIP-TO         /* TT1 PLACEHOLDER
*         MOVE TRUE TO #RESET-SEARCH
*         ESCAPE BOTTOM
*       END-IF
*
        MOVE 1 TO I
      END-IF
*
      PERFORM BUILD-SCREEN
    END-FIND
  END-READ /* (0499)
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* Rewriten for Address normalization NG4 *
/*
DEFINE SUBROUTINE CR-MBR-ZIP
/*     ---------------------
MOVE (AD=I) TO #SCR-ZIP-CODE-C
MOVE #CR-START (#X) TO #SKIP-TO
RESET #ZIP-CODE-A
*
REPEAT /* User requested ZIP
  UNTIL SUBSTR(#SKIP-TO,1,9) EQ #ZIP-CODE-A
  MOVE SUBSTR(#SKIP-TO,1,9) TO #ZIP-CODE-A
  EXAMINE FULL #ZIP-CODE-A FOR ' ' REPLACE WITH '0'
  IF #ZIP-CODE-A IS (N9)
    CALLNAT 'ANZN0001'#ZIP-CODE #FOUND
      #CN-SELECTED (*)
      #ZIP-SELECTED (*)
    LN := 0
    REPEAT  /* Brouse the result
      UNTIL LN GE #FOUND
      FOR I 1 TO 7  /* 7 members per screen
        L := LN + I
        IF #CN-SELECTED (L) > 0
          FIND (1) ST-V WITH ID-NUMBER = #CN-SELECTED (L)
            #ZIP-CODE := #ZIP-SELECTED (L)
            PERFORM BUILD-SCREEN
          END-FIND
        END-IF
      END-FOR
      #NEXT-ZIP := #ZIP-CODE
      PERFORM SHOW-SCREEN
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      IF #ZIP-CODE NE #NEXT-ZIP
        ESCAPE BOTTOM
      END-IF
      ADD 7 TO LN
    END-REPEAT
  ELSE
    REINPUT 'Starting at must be Numeric'
      MARK *#CR-START (#X)
  END-IF
END-REPEAT
*
#CGA-SCR-COMMAND :=  'CR'
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-ZIP-COUNT
* * * * * * * * * * * * * * * * * *
* *
IF #ZIP-THRU - #ZIP-FROM GT 5000
  REINPUT 'Zip range cannot exceed 5000'
    MARK *#ZIP-FROM
END-IF
CALLNAT 'ANZN0002'
  #ZIP-FROM
  #ZIP-THRU
  #ZIP-CNT-WIDS
  #ZIP-CNT-ACTS
  #ZIP-CNT-RETS
* *
* READ ST-V BY ZIP-CODE-KEY STARTING FROM #ZIP-FROM
*   IF ST-V.ID-NUMBER LE 1900
*     ESCAPE TOP
*   END-IF
*   IF ST-V.ZIP1-N GT #ZIP-THRU
*     ESCAPE BOTTOM
*   END-IF
*   IF ST-V.WIDOW-FLAG = 'Y'
*     ADD 1 TO #ZIP-CNT-WIDS
*   ELSE
*     IF ST-V.MILITARY-STATUS = 'A'
*       ADD 1 TO #ZIP-CNT-ACTS
*     ELSE
*       ADD 1 TO #ZIP-CNT-RETS
*     END-IF
*   END-IF
* END-READ
* *
COMPUTE #ZIP-CNT-ALLS = #ZIP-CNT-ACTS + #ZIP-CNT-RETS + #ZIP-CNT-WIDS
WRITE /// '   From' #ZIP-FROM 'thru' #ZIP-THRU 'there are'
  //  '   ' #ZIP-CNT-ACTS 'Active-duty Members'
  //  '   ' #ZIP-CNT-RETS 'Retired/Civilian Members'
  //  '   ' #ZIP-CNT-WIDS 'Widows'
  //  '    _________'
  //  '   ' #ZIP-CNT-ALLS 'Total'
  ///// '      *** press enter to continue ***** '
*
MOVE 'CR' TO #CGA-SCR-COMMAND
FETCH 'G1000PXX'
*
END-SUBROUTINE
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-MBR-SSN
* * * * * * * * * * * * * * * * *
*
MOVE 'Member SSN Search' TO #TEXT
MOVE (AD=I) TO #SCR-SSN-C
MOVE #CR-START (#X) TO #SKIP-TO
PERFORM INITIALIZE-SSN
*
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  READ ST-V BY SSN STARTING FROM #SKIP-TO-SSN
    IF ST-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
    PERFORM DO-SSN
  END-READ /* (0499)
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * *
DEFINE SUBROUTINE DO-SSN
* * * * * * * * * * * * *
*
ADD 1 TO I
RESET #RESET-SEARCH
IF I GT 7
  MOVE ST-V.SSN TO #SKIP-TO-SSN
  PERFORM SHOW-SCREEN
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  IF ST-V.SSN NE #SKIP-TO-SSN
    PERFORM INITIALIZE-SSN
    MOVE TRUE TO #RESET-SEARCH
    ESCAPE BOTTOM
  END-IF
*
  MOVE 1 TO I
END-IF
*
PERFORM BUILD-SCREEN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-WID-NAME
* * * * * * * * * * * * * * * * *
*
MOVE 'Widow Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  IF #SEARCH-BY-P-I           /* Read by Process Ind /* DW8
    COMPRESS 'Search by Process Ind =' #CGA-SCR-SUFFIX INTO #TEXT
*
    READ ST-V BY PROCESS-NAME-KEY STARTING FROM #P-I-NAME-SUPER    /* DW8
      IF ST-V.PROCESS-IND NE #SUFFIX-1
        ESCAPE BOTTOM
      END-IF
*
      IF ST-V.WIDOW-FLAG NE 'Y'
        ESCAPE TOP
      END-IF
*
      IF ST-V.ID-NUMBER LE 1900
        ESCAPE TOP
      END-IF
*
*     PERFORM CHECK-OTHER-COMP  /* DW9
      PERFORM DO-NAME
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
    END-READ /* (0459)
*
    IF #RESET-SEARCH                         /* DW8
      COMPRESS #SUFFIX-1 #SKIP-TO INTO #P-I-NAME-SUPER LEAVING NO
      ESCAPE TOP
    END-IF                                   /* END DW8
*
    PERFORM SHOW-SCREEN
*
    IF #CGA-SCR-COMMAND NE '*'
      MOVE 'CR' TO #CGA-SCR-COMMAND
    END-IF
    ESCAPE TOP
  END-IF                                            /* END DW8

  MOVE 1 TO I
*
  READ ST-V BY NAME-KEY-SP STARTING FROM #SKIP-TO
    IF I GT 7
      ESCAPE BOTTOM
    END-IF
*
    IF ST-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
    PERFORM BUILD-SCREEN
    ADD 1 TO I
  END-READ /* (0499)
*
  PERFORM SHOW-SCREEN
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-WID-SSN
* * * * * * * * * * * * * * * * *
*
MOVE 'Widow SSN Search' TO #TEXT
MOVE (AD=I) TO #SCR-SSN-C
MOVE #CR-START (#X) TO #SKIP-TO
PERFORM INITIALIZE-SSN
*
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  MOVE 1 TO I
*
  READ ST-V BY SSN-KEY-SP STARTING FROM #SKIP-TO
    IF I GT 7
      ESCAPE BOTTOM
    END-IF
*
    IF ST-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
*
    PERFORM BUILD-SCREEN
    ADD 1 TO I
  END-READ /* (0499)
*
  PERFORM SHOW-SCREEN
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-INTRO
* * * * * * * * * * * * * * * * * *
*
MOVE #CR-START (#X) TO #SKIP-TO
*
IF #X = 8
  PERFORM INITIALIZE-SSN
  MOVE 'S' TO #CR-NAME-SSN
ELSE
  MOVE 'N' TO #CR-NAME-SSN
END-IF
*
FETCH 'G2A00PIR' #CR-NAME-SSN #SKIP-TO
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-APPL-BY-NAME
* * * * * * * * * * * * * * * * *
*
MOVE 'Name' TO #RANK-LBL
MOVE 'Application Tracking Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START-3 (9) TO #SKIP-TO
RESET #I2
*
REPEAT
*
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  READ AT-V BY NAME STARTING FROM #SKIP-TO
    IF AT-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
*
    FOR #I4 1 TO 50
      IF ST-NOTE-DATE(#I4) = 0
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF #I4 = 1
      ESCAPE TOP
    ELSE
      SUBTRACT 1 FROM #I4
    END-IF
*
    IF #I2 GE 500
      WRITE /// 'ENTRIES SELECTED EXCEEDS 500 - ONLY 500 SHOWN'
        /// ' PRESS ENTER TO CONTINUE'
      ESCAPE BOTTOM
    END-IF
*
    DECIDE FOR FIRST CONDITION      /* PM1
      WHEN #CGA-USER-COMPANY = #BOTH
        IGNORE
      WHEN #CGA-USER-COMPANY = #SERVICES
        IF AT-V.INTRO-FLAG NE #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
      WHEN NONE
        IF AT-V.INTRO-FLAG EQ #SERVICES-AT-FLAG
          ESCAPE TOP
        END-IF
    END-DECIDE
*
    ADD 1 TO I
    IF AT-V.NAME LT #SKIP-TO
      ESCAPE TOP
    END-IF
*
    RESET #RESET-SEARCH
    IF I GT 18  /* WAS 7
      MOVE AT-V.NAME TO #SKIP-TO
      PERFORM SHOW-SCREEN-AT
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
*
      IF AT-V.NAME NE #SKIP-TO
        MOVE TRUE TO #RESET-SEARCH
        ESCAPE BOTTOM
      END-IF
*
      MOVE 1 TO I
    END-IF                               /* YK1
*    ELSE                                /* YK1
    ADD 1 TO #I2
    IF AT-V.ID-NUMBER NE 0
      MOVE AT-V.ID-NUMBER TO #SRT-ID-NUMBER (#I2)
    ELSE
      MOVE AT-V.SSN TO #SRT-ID-NUMBER (#I2)
    END-IF

    MOVE ST-NOTE-DATE (#I4)  TO #SRT-ST-DATE   (#I2)
    MOVE ST-NOTE-STATUS(#I4) TO #SRT-ST-STATUS (#I2)
    MOVE AT-V.NAME           TO #SRT-AT-NAME   (#I2)
*    END-IF                              /* YK1
*
    PERFORM BUILD-SCREEN-AT
  END-READ
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN-AT
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-APPL
* * * * * * * * * * * * * * * * * *
*
MOVE #CR-START (#X) TO #SKIP-TO
*
IF #X = 11
  PERFORM INITIALIZE-SSN
  MOVE 'S' TO #CR-NAME-SSN
ELSE
  MOVE 'N' TO #CR-NAME-SSN
END-IF
*
FETCH 'C1100PAR' #CR-NAME-SSN #SKIP-TO
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-SAMPLE-NAME
* * * * * * * * * * * * * * * * *
*
READ ST-V BY MEMBER-KEY FROM '000050'
  IF  (ST-V.ID-NUMBER GE 100) AND (ST-V.ID-NUMBER LE 149)
      OR (ST-V.ID-NUMBER GE 326) AND (ST-V.ID-NUMBER LE 400)
    ESCAPE TOP
  END-IF
*
  IF (ST-V.ID-NUMBER GT 1899)   /* DW6
      OR (#SRT-NUM-ENTRIES GE 500)
    IF #SRT-NUM-ENTRIES GE 500
      WRITE 'Incomplete List - Only shows 500 - Call EDS'
      WRITE // 'Press ENTER to continue'
    END-IF
*
    ESCAPE BOTTOM
  END-IF
*
  IF ST-V.NAME = MASK('ONLY')
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #SRT-NUM-ENTRIES
  MOVE ST-V.ID-NUMBER TO #SRT-ID-NUMBER (#SRT-NUM-ENTRIES)
  MOVE ST-V.NAME      TO #SRT-NAME      (#SRT-NUM-ENTRIES)
END-READ
*
* CALL 'WAASSRT1' #SRT-NAME (1) #SRT-PARM   /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT7' #SRT-TABLE(*) #SRT-PARM   /* SAG RH1 11242005 SS
*
MOVE 'Test Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  FOR #I2 1 TO 500
    IF #I2 GT #SRT-NUM-ENTRIES
      ESCAPE BOTTOM
    END-IF
*
    FIND (1) ST-V WITH MEMBER-KEY = #SRT-ID-NUMBER-A (#I2)
    END-FIND
*
    IF #SRT-NAME (#I2) LT #SKIP-TO
      ESCAPE TOP
    END-IF
*
    ADD 1 TO I
    RESET #RESET-SEARCH
    IF I GT 7
      MOVE ST-V.NAME TO #SKIP-TO
      PERFORM SHOW-SCREEN
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
*
      IF NAME-25 NE #SKIP-TO
        MOVE TRUE TO #RESET-SEARCH
        ESCAPE BOTTOM
      END-IF
*
      MOVE 1 TO I
    END-IF
*
    PERFORM BUILD-SCREEN
  END-FOR
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-RETIREE-NAME
* * * * * * * * * * * * * * * * *
*
READ ST-V BY MEMBER-KEY FROM '000326'
  IF ST-V.ID-NUMBER GT 380
    ESCAPE BOTTOM
  END-IF
*
  IF ST-V.ID-NUMBER GT 1900
      OR (#SRT-NUM-ENTRIES GE 500)
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #SRT-NUM-ENTRIES
  MOVE ST-V.ID-NUMBER TO #SRT-ID-NUMBER (#SRT-NUM-ENTRIES)
  MOVE ST-V.NAME      TO #SRT-NAME      (#SRT-NUM-ENTRIES)
END-READ
*
* CALL 'WAASSRT1' #SRT-NAME (1) #SRT-PARM   /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT7' #SRT-TABLE(*) #SRT-PARM   /* SAG RH1 11242005 SS
*
MOVE 'Retiree Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  FOR #I2 1 TO 500
    IF #I2 GT #SRT-NUM-ENTRIES
      ESCAPE BOTTOM
    END-IF
*
    FIND (1) ST-V WITH MEMBER-KEY = #SRT-ID-NUMBER-A (#I2)
    END-FIND
*
    IF #SRT-NAME (#I2) LT #SKIP-TO
      ESCAPE TOP
    END-IF
*
    ADD 1 TO I
    RESET #RESET-SEARCH
    IF I GT 7
      MOVE ST-V.NAME TO #SKIP-TO
      PERFORM SHOW-SCREEN
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
*
      IF NAME-25 NE #SKIP-TO
        MOVE TRUE TO #RESET-SEARCH
        ESCAPE BOTTOM
      END-IF
*
      MOVE 1 TO I
    END-IF
*
    PERFORM BUILD-SCREEN
  END-FOR
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-ASSOCIATE-NAME
* * * * * * * * * * * * * * * * * *
*
READ ST-V BY MEMBER-KEY FROM '000381'
  IF ST-V.ID-NUMBER GT 400
    ESCAPE BOTTOM
  END-IF
*
  IF ST-V.ID-NUMBER GT 1900
      OR (#SRT-NUM-ENTRIES GE 500)
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #SRT-NUM-ENTRIES
  MOVE ST-V.ID-NUMBER TO #SRT-ID-NUMBER (#SRT-NUM-ENTRIES)
  MOVE ST-V.NAME      TO #SRT-NAME      (#SRT-NUM-ENTRIES)
END-READ
*
* CALL 'WAASSRT1' #SRT-NAME (1) #SRT-PARM   /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT7' #SRT-TABLE(*) #SRT-PARM   /* SAG RH1 11242005 SS
*
MOVE 'Associate Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  FOR #I2 1 TO 500
    IF #I2 GT #SRT-NUM-ENTRIES
      ESCAPE BOTTOM
    END-IF
*
    FIND (1) ST-V WITH MEMBER-KEY = #SRT-ID-NUMBER-A (#I2)
    END-FIND
*
    IF #SRT-NAME (#I2) LT #SKIP-TO
      ESCAPE TOP
    END-IF
*
    ADD 1 TO I
    RESET #RESET-SEARCH
    IF I GT 7
      MOVE ST-V.NAME TO #SKIP-TO
      PERFORM SHOW-SCREEN
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
*
      IF NAME-25 NE #SKIP-TO
        MOVE TRUE TO #RESET-SEARCH
        ESCAPE BOTTOM
      END-IF
*
      MOVE 1 TO I
    END-IF
*
    PERFORM BUILD-SCREEN
  END-FOR
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-APPL-BY-USER
* * * * * * * * * * * * * * * * *
*
MOVE 'User' TO #RANK-LBL
*                                         NG1    \/\/\/ START
IF (#SEL-YYYY NE 0) AND (#SEL-MONTH NE 0) AND (#SEL-DD NE 0)
  COMPRESS #SEL-YYYY-A #SEL-MONTH-A #SEL-DD-A
    INTO #YYYYMMDD-A LEAVING NO
  ASSIGN #END-DATE = #YYYYMMDD
ELSE
  RESET #YYYYMMDD-A
END-IF
*
READ AT-V BY USER-SOURCE FROM #USER-START
*
* 12/6/2002 - TDM
* ADD/CHG CODE TO BRING IN THE ST-NOTE-DATE INSTEAD OF DATE RECEIVED
*
  IF USER-SOURCE NE #USER-START
      AND (#USER-START NE ' ')
    ESCAPE BOTTOM
  END-IF
*
  FOR #I4 1 TO 50
    IF ST-NOTE-DATE(#I4) = 0
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  reset #escape
  IF #I4 = 1
    IF #CGA-USER-COMPANY NE #SERVICES    /* NG3
      assign #escape = 1
    END-IF
  ELSE
    SUBTRACT 1 FROM #I4
  END-IF
*
  IF #CGA-ORIG-SUFFIX = 'SM'
      AND ST-NOTE-DATE(#I4) GT #END-DATE
    if #escape = 0 assign #escape = 2 end-if
  END-IF
*
  IF #SRT-NUM-ENTRIES GE 500
    WRITE /// 'Entries selected exceeds 500 - only 500 shown'
      /// ' Press ENTER to continue'
    ESCAPE BOTTOM
  END-IF
*
  IF #YYYYMMDD-A NE BLANK
    IF  (#SEL-APPL-SEQ = 'A') AND (ST-NOTE-DATE (#I4) LT #YYYYMMDD)
        OR (#SEL-APPL-SEQ = 'D') AND (ST-NOTE-DATE (#I4) GT #YYYYMMDD)
      IF #escape = 0 assign #escape = 3 end-if
    END-IF
  END-IF
*
  IF ((#USER-START NE ' ') AND (#USER-START NE 'ALL')
      AND (USER-SOURCE NE #USER-START))   /* REPLACES LINE BELOW
      OR ((#SEL-APPL-SOURCE NE ' ')
      AND (AT-V.APPL-SOURCE NE #SEL-APPL-SOURCE))
      OR ((#SEL-APPL-STATUS NE ' ')
      AND (#SEL-APPL-STAT-2 NE '*')
      AND (ST-NOTE-STATUS (#I4) NE #SEL-APPL-STATUS))
      OR ((#SEL-APPL-STAT-2 = '*')
      AND (#AT-NOTE-STAT-1(#I4) NE #SEL-APPL-STAT-1))
      OR ((#SEL-ACT-FLAG NE ' ')
      AND (AT-V.ACTION-FLAG NE #SEL-ACT-FLAG))
    if #escape = 0 assign #escape = 4 end-if
  END-IF
*
  DECIDE FOR FIRST CONDITION      /* PM1
    WHEN #CGA-USER-COMPANY = #BOTH
      IGNORE
    WHEN #CGA-USER-COMPANY = #SERVICES
      IF AT-V.INTRO-FLAG NE #SERVICES-AT-FLAG
        if #escape = 0 assign #escape = 5 end-if
      END-IF
    WHEN NONE
      IF AT-V.INTRO-FLAG EQ #SERVICES-AT-FLAG
        if #escape = 0 assign #escape = 6 end-if
      END-IF
  END-DECIDE
*
  IF #ESCAPE > 0
    ESCAPE TOP
  END-IF
*                                                     NG1   /\/\/\   END
  ADD 1 TO #SRT-NUM-ENTRIES
  IF AT-V.ID-NUMBER NE 0
    MOVE AT-V.ID-NUMBER TO #SRT-ID-NUMBER (#SRT-NUM-ENTRIES)
  ELSE
    MOVE AT-V.SSN TO #SRT-ID-NUMBER (#SRT-NUM-ENTRIES)
  END-IF
*
  IF #I3 < 500
    MOVE ST-NOTE-DATE  (#I4) TO #SRT-ST-DATE   (#I3)
    MOVE ST-NOTE-STATUS(#I4) TO #SRT-ST-STATUS (#I3)
    MOVE ST-NOTE-USER  (#I4) TO #SRT-ST-USER   (#I3)
    MOVE AT-V.NAME           TO #SRT-AT-NAME   (#I3)
    ADD 1 TO #I3
  END-IF
*
* DISPLAY #SRT-NAME(#I3 - 1)
*
END-READ
*
IF #SEL-APPL-SEQ = 'A'
  MOVE 'A' TO #SRT-SORT-TYPE
ELSE
  MOVE 'D' TO #SRT-SORT-TYPE
END-IF
*
* CALL 'WAASSRT1' #SRT-NAME (1) #SRT-PARM   /* SAG RH1 11242005 SS
CALLNAT 'SAGSRT7' #SRT-TABLE(*) #SRT-PARM   /* SAG RH1 11242005 SS
* DISPLAY #SRT-NAME (1:20)
*
MOVE 'Application Tracking Name Search' TO #TEXT
MOVE (AD=I) TO #SCR-NAME-C
MOVE #CR-START (#X) TO #SKIP-TO
*
REPEAT
  IF #CGA-SCR-COMMAND NE  '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
  RESET #RESET-SEARCH
*
  FOR #I2 1 TO 500
    IF #I2 GT #SRT-NUM-ENTRIES
      ESCAPE BOTTOM
    END-IF
*
    IF #SRT-ID-NUMBER (#I2) LT 700000
      FIND (1) AT-V WITH ID-NUMBER = #SRT-ID-NUMBER-6 (#I2)
      END-FIND
    ELSE
      FIND AT-V WITH SSN = #SRT-ID-NUMBER (#I2)  /* FIND CORRECT SSN
        IF #CGA-USER-COMPANY = #SERVICES         /* EDS DEFAULTS TO INS
          IF AT-V.INTRO-FLAG NE #SERVICES-AT-FLAG
            ESCAPE TOP
          ELSE
            ESCAPE BOTTOM
          END-IF
        ELSE
          IF AT-V.INTRO-FLAG EQ #SERVICES-AT-FLAG
            ESCAPE TOP
          ELSE
            ESCAPE BOTTOM
          END-IF
        END-IF
      END-FIND
    END-IF
*
**  IF #SRT-NAME (#I2) LT #SKIP-TO               /* SAG 20060330 RH1 AS
**    ESCAPE TOP                                 /* SAG 20060330 RH1 AS
**  END-IF                                       /* SAG 20060330 RH1 AS
*
    ADD 1 TO I
    RESET #RESET-SEARCH
    IF I GT 18  /* was 7
      MOVE AT-V.NAME TO #SKIP-TO
      PERFORM SHOW-SCREEN-AT
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
*
      IF AT-V.NAME NE #SKIP-TO
        MOVE TRUE TO #RESET-SEARCH
        ESCAPE BOTTOM
      END-IF
*
      MOVE 1 TO I
    END-IF
*
    PERFORM BUILD-SCREEN-AT
  END-FOR
*
  IF NOT #RESET-SEARCH
    PERFORM SHOW-SCREEN-AT
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-SCREEN
* * * * * * * * * * * * * * * * * *
*
CALLNAT 'PLAN-AFA' ST-V.ID-NUMBER F-IF-AFFL-DATA /* NG2
*
RESET #HAS-AAFM
FOR #I1 1 TO 9
  IF STATUS (#I1) NE ' '
    MOVE TRUE TO #HAS-AAFM
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF   #AFFL-CLIENT                                             /* DW9
    AND (NOT #HAS-AAFM)             /* Has no Segments filled
    AND (ST-V.PROCESS-IND NE 'C')   /* Not a Contact record
  MOVE 'SVC-ONLY' TO #SAS-IAS (I)
ELSE
  IF #AFFL-CLIENT
      AND ((#HAS-AAFM)                 /* Has at least 1 Segments filled
      OR  (ST-V.PROCESS-IND = 'C'))   /* Contact record
    MOVE 'SVC&INS' TO #SAS-IAS (I)
  ELSE
    IF ST-V.PROCESS-IND = 'G'
      MOVE 'G-S&INS' TO #SAS-IAS (I)
    ELSE
      MOVE 'IAS-ONLY' TO #SAS-IAS (I)
    END-IF
  END-IF
END-IF                                                       /* END DW9
ASSIGN #SCR-INTERNET (I) = F-PREF-EMAIL(<MEMBER-CONTACT-ID>)              /* TT1
IF #X = 5 OR = 6
  MOVE ST-V.SSN-SP         TO #SCR-SSN    (I)
  MOVE ST-V.NAME-SP        TO #SCR-NAME   (I)
  MOVE ST-V.NAME           TO #SCR-NAME-DECEASED (I)
  MOVE ST-V.TITLE-SP       TO #SCR-RANK   (I)
  MOVE ST-V.STATUS-CODE-SP TO #SCR-STATUS (I)
ELSE
  MOVE ST-V.SSN             TO #SCR-SSN    (I)
  MOVE ST-V.NAME            TO #SCR-NAME   (I)
*
*   IF PLAN (1) = MASK (..'W')  /* WIDOW SPOUSE      /* DW9
  IF #HAS-3SW                                      /* DW9
    MOVE ST-V.NAME-SP       TO #SCR-NAME-DECEASED (I)
*
    IF #SCR-INTERNET (I) = ' '
      MOVE 'Widow Client is' TO #SCR-INTERNET (I)
    END-IF
  END-IF
*
  MOVE ST-V.RANK            TO #SCR-RANK   (I)
  MOVE ST-V.MILITARY-STATUS TO #SCR-STATUS (I)
*
  IF ST-V.NUMBER-MEMBERSHIPS = 0
    MOVE '0M' TO #SCR-NUM-MBRSHPS (I)
  ELSE
    IF ST-V.OTHER-COMPANY-ID NE 0
      MOVE '**' TO #SCR-NUM-MBRSHPS (I)
    END-IF
  END-IF
END-IF
*
ASSIGN #SCR-ID-NO (I) = ST-V.ID-NUMBER
ASSIGN #SCR-APPL-SOURCE (I) = ST-V.APPL-SOURCE
*                           NG1 Start
RESET #PDA-ADDRESS                                                     /*NG4 BEGIN
MOVE MEMBER-CONTACT-ID TO #ADD-CONTACT-ID
MOVE 'P' TO #ADD-TYPE
CALLNAT 'ADDN0001' #PDA-ADDRESS
#SCR-ADDR1 (I) := SUBSTR ( #ADD-LINE-1,1,25)  /* Only first 25 chars
#SCR-ADDR2 (I) := SUBSTR ( #ADD-LINE-2,1,25)  /* Only first 25 chars
#SCR-ADDR3 (I) := SUBSTR ( #ADD-LINE-3,1,25)  /* Only first 25 chars
#SCR-ZIP-5 (I) := #ZIP5
*
RESET  #SCR-ZIP-4 (I)                                                  /*NG4 END
*
IF #ZIP4 NE 0
  COMPRESS '-' #ZIP4 INTO #SCR-ZIP-4 (I) LEAVING NO
END-IF
*
IF ST-V.MILITARY-SERVICE NE ' '
  IF ST-V.MILITARY-SERVICE = MASK ('A')
    MOVE 'ARMY'  TO #SCR-MIL-SERV (I)
  ELSE
    IF ST-V.MILITARY-SERVICE = MASK ('F')
      MOVE 'USAF'  TO #SCR-MIL-SERV (I)
    ELSE
      IF ST-V.MILITARY-SERVICE = MASK ('N')
        MOVE 'USN'  TO #SCR-MIL-SERV (I)
      ELSE
        IF ST-V.MILITARY-SERVICE = MASK ('M')
          MOVE 'USMC'  TO #SCR-MIL-SERV (I)
        ELSE
          MOVE 'USCG'  TO #SCR-MIL-SERV (I)
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
* END-IF                                  /* DW9
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-SCREEN-AT
* * * * * * * * * * * * * * * * * *
*
MOVE EDITED AT-V.SSN (EM=(999999999) TO #AR-SCR-SSN         (I)
MOVE EDITED AT-V.HOME-PHONE (EM=(999-999-9999) TO #AR-SCR-PHONE (I)
MOVE AT-V.ACTION-FLAG                TO #AR-SCR-ACTION-FLAG (I)
MOVE AT-V.NAME                       TO #AR-SCR-NAME        (I)
* MOVE #SRT-ST-USER  (#I2)             TO #AR-SCR-USER        (I)
MOVE USER-SOURCE                     TO #AR-SCR-USER        (I)
MOVE #SRT-ST-STATUS(#I2)             TO #AR-SCR-APPL-STATUS (I)
MOVE #SRT-ST-DATE  (#I2) TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #AR-SCR-REC-DATE (I) WITH '/'
MOVE AT-V.APPL-SOURCE                TO #AR-SCR-APPL-SOURCE (I)
MOVE AT-V.INTRO-FLAG                 TO #AR-SCR-TYPE        (I)
IF AT-V.ID-NUMBER NE 0
  MOVE EDITED AT-V.ID-NUMBER(EM=ZZZZZZ) TO #AR-SCR-INQ-ID  (I)
ELSE
  MOVE AT-V.MEMBER-CN               TO #AR-SCR-INQ-ID      (I)
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-SCREEN
* * * * * * * * * * * * * * * *
*
IF I LE 7
* MOVE '** END OF DATA **' TO #SCR-NAME (I)
  COMPRESS #TEXT '** END OF SEARCH **' INTO #TEXT
  MOVE 'CR' TO #CGA-SCR-COMMAND
  RESET #SKIP-TO
ELSE
  IF #X = 2
    MOVE ST-V.SSN TO #SKIP-TO-SSN
  ELSE
    IF #X = 6 OR = 8 OR = 11
      MOVE #SCR-SSN (7) TO #SKIP-TO-SSN
    ELSE
      IF #X = 3
        MOVE EMAIL-ADDRESS TO #SKIP-TO                              /* TT1
      ELSE
        IF #X = 4
          MOVE #ZIP-CODE TO #SKIP-TO-ZIP-9                          /*NG4
          IF #FOUND NE 0                                            /*NG4
            COMPRESS #TEXT '** There are' #FOUND 'records with'     /*NG4
              'Zip-Code'  #ZIP5  '**'   INTO #TEXT                  /*NG4
          END-IF
        ELSE
          IF #X = 5
            MOVE ST-V.NAME-SP TO #SKIP-TO
          ELSE
            IF #X = 12
              MOVE AT-V.NAME TO #SKIP-TO
            ELSE
              MOVE ST-V.NAME TO #SKIP-TO
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
INPUT TEXT #TEXT MARK *#SKIP-TO USING MAP 'G2A01MCR'
*
IF NOT #CGA-SCR-COMMAND = '*' OR = 'CR'    /* DW9
  FETCH 'G1000PXX'
END-IF
*
FOR #I1 1 TO 7
  IF #SCR-SELECT-CMND (#I1) NE ' '
    IF #SCR-SELECT-CMND (#I1) = 'X'
      IF #CGA-USER-COMPANY = 'I'                     /* DW9
        MOVE 'ST' TO #CGA-SCR-COMMAND                /* DW9
      ELSE
        MOVE 'DI' TO #CGA-SCR-COMMAND
      END-IF
    ELSE
      MOVE #SCR-SELECT-CMND (#I1) TO #CGA-SCR-COMMAND
    END-IF
*
    MOVE #SCR-ID-NO (#I1) TO #CGA-SCR-ID
  END-IF
END-FOR
*
RESET #SCR-SSN         (*)
RESET #SCR-NUM-MBRSHPS (*)
RESET #SCR-ADDR1       (*)
RESET #SCR-ADDR2       (*)
RESET #SCR-ADDR3       (*)
RESET #SCR-ZIP-5       (*)
RESET #SCR-ZIP-4       (*)
RESET #SCR-NAME        (*)
RESET #SCR-NAME-DECEASED (*)
RESET #SCR-ID-NO       (*)
RESET #SCR-RANK        (*)
RESET #SCR-STATUS      (*)
RESET #SCR-MIL-SERV    (*)
RESET #SCR-INTERNET    (*)
RESET #SCR-APPL-SOURCE (*)
RESET #SAS-IAS         (*)                /* DW9
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-SCREEN-AT
* * * * * * * * * * * * * * * *
*
IF I LE 18
* MOVE '** END OF DATA **' TO #SCR-NAME (I)
  COMPRESS #TEXT '** END OF SEARCH **' INTO #TEXT
  MOVE 'CR' TO #CGA-SCR-COMMAND
  RESET #SKIP-TO
ELSE
  IF #X = 12 OR = 9
    MOVE AT-V.NAME TO #SKIP-TO
  END-IF
END-IF
*
IF #X = 12
  INPUT TEXT #TEXT MARK *#SKIP-TO USING MAP 'G2A02MAR'
ELSE
  INPUT TEXT #TEXT MARK *#SKIP-TO USING MAP 'G2A01MAR'
END-IF
*
FOR #I1 1 TO 18
  IF #AR-SCR-SELECT-CMND (#I1) NE ' '
    IF #AR-SCR-SELECT-CMND (#I1) = 'X'
      MOVE 'AT' TO #CGA-SCR-COMMAND
    ELSE
      MOVE #AR-SCR-SELECT-CMND (#I1) TO #CGA-SCR-COMMAND
    END-IF
*
    IF #AR-SCR-INQ-ID-N (#I1) NE 0
      MOVE #AR-SCR-INQ-ID-N (#I1) TO #CGA-SCR-ID
    ELSE
      MOVE #SCR-SSN   (#I1) TO #CGA-SCR-ID
    END-IF
*    MOVE #SCR-ID-NO (#I1) TO #CGA-SCR-ID
  END-IF
END-FOR
*
RESET #AR-SCR-SELECT-CMND (*)
RESET #AR-SCR-PHONE       (*)
RESET #AR-SCR-SSN         (*)
RESET #AR-SCR-ACTION-FLAG (*)
RESET #AR-SCR-APPL-STATUS (*)
RESET #AR-SCR-NAME        (*)
RESET #AR-SCR-REC-DATE    (*)
RESET #AR-SCR-USER        (*)
RESET #AR-SCR-APPL-SOURCE (*)
RESET #AR-SCR-TYPE        (*)
RESET #AR-SCR-INQ-ID      (*)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE INITIALIZE-SSN
* * * * * * * * * * * * * * * * * * *
*
FOR #I1 9 TO 1 STEP -1
  IF #SKIP-TO-1 (#I1) = ' '
    MOVE '0' TO #SKIP-TO-1 (#I1)
  END-IF
END-FOR
*
IF #SKIP-TO NE MASK (NNNNNNNNN)
  REINPUT 'Starting at must be Numeric'
    MARK *#CR-START (#X)
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CR-DEPOSIT                                      /* PM2
* * * * * * * * * * * * * * * * *
*
CALLNAT 'G2A03SCR'
  #SKIP-TO
  #CGA-SCR-COMMAND
  #CGA-SCR-ID
  #CGA-SCR-SUFFIX
*
END-SUBROUTINE
*
END
