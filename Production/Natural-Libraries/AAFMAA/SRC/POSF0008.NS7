* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function POSF0008.
/**
/** :author nguentchev
DEFINE FUNCTION WINDOWS-USER RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #UNIX-ID   (A8) 1 REDEFINE #UNIX-ID
    2 CID   (A3)
    2 UID   (A5)
  LOCAL USING OBJLGENT
  LOCAL USING OBJLUSER
  LOCAL
  1 #GEN-KEY     (A7)
  1 #CLERK-ID    (A3)
  1 #DEFAULT-CLERK (A3) CONST <'DFT'>
  1 #DEFAULT-USER  (A8) CONST <'DEFAULT'>
  1 #AAFMAA        (A7) CONST <'AAFMAA\'>
  END-DEFINE
/*
IF UID = ' '
  #CLERK-ID := CID
ELSE
  FIND (1) USER-V WITH TERMINAL-ID = #UNIX-ID
    IF NO RECORDS FOUND
      COMPRESS #AAFMAA #DEFAULT-USER INTO WINDOWS-USER
        LEAVING NO
      ESCAPE ROUTINE
    END-NOREC
    #CLERK-ID := USER-V.CLERK-ID
  END-FIND
END-IF

COMPRESS 'WEBU' #CLERK-ID INTO #GEN-KEY
  LEAVING NO
FIND (1) GEN-TABLE-V WITH SEC-KEY = #GEN-KEY
  IF NO RECORDS FOUND
    COMPRESS #AAFMAA #DEFAULT-USER INTO WINDOWS-USER
      LEAVING NO
    ESCAPE BOTTOM
  END-NOREC
  WINDOWS-USER := GEN-ALPHA-VALUE
END-FIND
END-FUNCTION

END
