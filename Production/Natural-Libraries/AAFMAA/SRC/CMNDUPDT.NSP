* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: CMNDUPDT
*  THIS PROGRAM DOES THE COMMANDS FILE MAINTENANCE
DEFINE DATA
LOCAL
1 #W-COMMAND    (A2)
1 #W-OPTION     (A1)
1 #W-SECUR      (N2)
1 #W-TYP-INPUT  (A1)
1 #W-FILE       (A1)
1 #W-PROGRAM    (A8)
*
1 MSG-TEXT          (A32)
1 CMND-CM01-VIEW VIEW OF A-COMMANDS
  2 COMMAND         (A2)
  2 SECURITY-CODE   (N2)
  2 PROGRAM-TO-CALL (A8)
  2 TYPE-OF-INPUT   (A1)
  2 FILES-TO-READ   (A1)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
REPEAT
*
  INPUT TEXT MSG-TEXT USING MAP 'CMNDUMAP'
*
  IF #W-OPTION EQ 'Q' OR = '.'
    RESET #W-COMMAND
    RESET #W-SECUR
    RESET #W-FILE
    RESET #W-TYP-INPUT
    RESET #W-PROGRAM
    RESET #W-OPTION
    ESCAPE ROUTINE
  END-IF
*
RD1. FIND NUMBER CMND-CM01-VIEW WITH COMMAND = #W-COMMAND
  IF *NUMBER (RD1.) = 0
    IF #W-OPTION = 'A'
      PERFORM EDIT-FIELDS
      STORE CMND-CM01-VIEW
      END TRANSACTION
      ASSIGN MSG-TEXT = 'RECORD ADDED - THANK YOU'
      ESCAPE TOP
    ELSE
      RESET #W-SECUR
      RESET #W-FILE
      RESET #W-TYP-INPUT
      RESET #W-PROGRAM
      REINPUT 'COMMAND NOT FOUND' MARK *#W-COMMAND
      ESCAPE TOP
    END-IF
  END-IF
*
  FIND (1) CMND-CM01-VIEW WITH COMMAND = #W-COMMAND
*
    IF #W-OPTION = 'C'
      PERFORM EDIT-FIELDS
      UPDATE RECORD (0058)
      END TRANSACTION
      ASSIGN MSG-TEXT = 'RECORD UPDATED - THANK YOU'
      ESCAPE TOP
    ELSE
      END TRANSACTION
      IF #W-OPTION = 'L'
        PERFORM DISPLAY-FIELDS
        ASSIGN MSG-TEXT = 'OPTION "L" WAS SELECTED'
        ESCAPE TOP
      ELSE
        IF #W-OPTION = 'A'
          PERFORM DISPLAY-FIELDS
          REINPUT 'RECORD ALREADY EXISTS ON FILE' MARK *#W-COMMAND
          ESCAPE TOP
        ELSE
          DELETE
          END TRANSACTION
          ASSIGN MSG-TEXT = 'RECORD DELETED - BYE BYE'
          ESCAPE TOP
        END-IF
      END-IF
    END-IF
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
    DEFINE SUBROUTINE DISPLAY-FIELDS
*
    MOVE SECURITY-CODE   TO #W-SECUR
    MOVE PROGRAM-TO-CALL TO #W-PROGRAM
    MOVE TYPE-OF-INPUT   TO #W-TYP-INPUT
    MOVE FILES-TO-READ   TO #W-FILE
*
  END-SUBROUTINE /* (0086)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
    DEFINE SUBROUTINE EDIT-FIELDS
*
    IF #W-SECUR = 00
      REINPUT 'SECURITY-CODE REQUIRED' MARK *#W-SECUR
      ESCAPE TOP
    END-IF
    IF #W-TYP-INPUT = ' '
      REINPUT 'TYPE-INPUT REQUIRED' MARK *#W-TYP-INPUT
      ESCAPE TOP
    END-IF
    IF #W-FILE = ' '
      REINPUT 'FILE-TO-READ REQUIRED' MARK *#W-FILE
      ESCAPE TOP
    END-IF
    IF #W-PROGRAM = ' '
      REINPUT 'PROGRAM-NAME REQUIRED' MARK *#W-PROGRAM
      ESCAPE TOP
    END-IF
*
    MOVE #W-COMMAND   TO COMMAND
    MOVE #W-SECUR     TO SECURITY-CODE
    MOVE #W-PROGRAM   TO PROGRAM-TO-CALL
    MOVE #W-TYP-INPUT TO TYPE-OF-INPUT
    MOVE #W-FILE      TO FILES-TO-READ
*
  END-SUBROUTINE /* (0095)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
  END-FIND   /* (0058)
END-REPEAT /* (0026)
END
