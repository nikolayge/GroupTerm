* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPFPFC - FPF Check
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    08312005  DW0     Create
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)
*                    EBCDIC to HEX code '09' ASCII
* DRW  09062006 DW1  Increase size of #recs-read
* RSE  09282009 RE1  Change PROCESS-IND logic for Unification
************************************************************************
DEFINE DATA
LOCAL
*
1 #TERM-NOW (A1)
1 #TERM-DATE  (N8)
1 REDEFINE #TERM-DATE
 2 #TERM-DATE-YYYYMM (N6)
 2 #TERM-DATE-DD     (N2)
1 #UPDATE   (L)
1 #FOUND-TR   (A2)
1 #MON-PREM    (P7.2)
1 #SUFFIX      (A3)
1 #AS-OF-DT    (N8)
1 #RUN-DATE    (N8)
1 #RUN-DT-1-MO-YYYYMM (N6)
1 #RUN-DT-3-MO-YYYYMM (N6)
1 #CALC-PAID-TO-DATE  (N8)
1 #FPF-PAID-TO-DATE  (N8)
*
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #FACE-AMT    (N7.2/9)
1 #TPP         (N7.2/9)
1 #CSV-P      (P7.2/9)
*
1 #MONS-5-5     (N5.5)   /* DW3
1 REDEFINE #MONS-5-5
 2 #MONS         (N5)
 2 #MONS-FRAC    (N5)
1 #AMT         (P7.2)   /* DW3
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #AMT-7  (A7)
1 #AMT-9  (A9)
1 #TOT-BADJ    (N5.2)
*
1 TR-V VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER
 2 TRANSACTION-DATN
 2 OLD-DATA    (1)
 2 NEW-DATA    (1)
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 WIDOW-FLAG          (A1)
  2 LINKED-CN       (1)
  2 MEMBER-FLAG         (A1)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 PAD-CODE
  2 DEBIT-CREDIT-AMT
  2 X-BILL-ADJUST    (9)
  2 X-FIRST-PLACE-FEE(9)
  2 ALOT-AMT
  2 DATE-ENTERED           (9)
  2 X-ALT-LP-IND           (9)
  2 X-ALT-LP-AMT           (9)
  2 X-ALT-LP-IND-2         (9)
  2 X-ALT-LP-AMT-2         (9)
  2 X-LOAN-AMOUNT         (P5.2/9)
  2 X-LOAN-AMOUNT-2       (P7.2/9)
  2 X-LOAN-DATE-2         (N8/9)
  2 X-LOAN-DATE           (N8/9)
  2 X-INTEREST-RATE      (N2.3/9)
  2 X-ACCUM-INTEREST     (9)
  2 X-ACCUM-INTEREST-2   (9)
*   2 DATE-OF-BIRTH       (N8)
  2 NUMBER-MEMBERSHIPS
  2 MTD-ACV-PREM-ADJ (9)
  2 MTD-ACV-INT-ADJ (9)
  2 MTD-ACV-COI-ADJ (9)
  2 PAID-TO-DATE      (N8/9)
  2 REDEFINE PAID-TO-DATE
   3 PD-TO-DT-OCC    (9)
    4 PD-TO-DT-YYYYMM (N6)
    4 REDEFINE PD-TO-DT-YYYYMM
     5 PD-TO-DT-YYYY  (N4)
     5 PD-TO-DT-MM    (N2)
    4 PD-TO-DT-DD     (N2)
  2 SPLIT-IND         (9)
  2 ISSUE-DATE          (N8/9)
  2 REDEFINE ISSUE-DATE
   3 ISSUE-DATE-OCC   (9)
    4 ISSUE-DATE-YYYYMM   (N6)
    4 ISSUE-DATE-DD       (N2)
  2 PAID-UP-DATE        (9)
  2 REDEFINE PAID-UP-DATE
   3 PD-UP-DT-OCC    (9)
    4 PD-UP-DT-YYYYMM (N6)
    4 REDEFINE PD-UP-DT-YYYYMM
     5 PD-UP-DT-YYYY  (N4)
     5 PD-UP-DT-MM    (N2)
    4 PD-UP-DT-DD     (N2)
  2 PAY-STATUS          (9)
  2 TOT-PREMIUMS-PAID   (N7.2/9)
  2 ANNUAL-PREM         (9)
  2 FACE-AMOUNT         (9)
  2 CONV-DURATION       (9)
  2 MONTH-ALLOC-AMT     (9)
  2 PLAN                (9)
  2 REDEFINE PLAN
   3 PLAN-OCC  (9)
    4 PLAN-A1    (A1)
    4 PLAN-A2    (A2)
  2 STATUS              (9)
  2 MODE                (9)
  2 OWNERSHIP-FLAG      (9)
  2 RID-PLAN            (9)
  2 RID-EFF-DT          (9)
  2 RID-PD-TO           (9)
  2 RID-BALANCE
*
1 #COUNTERS
  2 #RECS-READ        (P7)   /* DW1
  2 #RECS-SELECTED    (P5)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #CNT-4            (P5)
  2 #CNT-5            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMM (N6)
 2 REDEFINE #YYYYMM
  3 #YYYY   (N4)
  3 #MM     (N2)
 2 #DD     (N2)
*
END-DEFINE
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'EXCEPTIONS                    '
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'FPF policies'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'Policies with FPF In Last Month'   /* Not used
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
    'FPF policies due to expire this month'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #YYYYMMDD
* IF #MM GT 25   /* Test for next month
  ADD 1 TO #MM
  IF #MM GT 12
    MOVE 1 TO #MM
    ADD 1 TO #YYYY
  END-IF
* END-IF
MOVE 1 TO #DD
*
MOVE #YYYYMMDD TO #RUN-DATE
*
MOVE #RUN-DATE TO #YYYYMMDD
SUBTRACT 3 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
MOVE #YYYYMM TO #RUN-DT-3-MO-YYYYMM
*
MOVE #RUN-DATE TO #YYYYMMDD
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
MOVE #YYYYMM TO #RUN-DT-1-MO-YYYYMM
*
READ ST-V BY MEMBER-KEY FROM '001900'
* READ ST-V PHYSICAL
*  IF ST-V.ID-NUMBER GE 600000            /* RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1   
    END TRANSACTION
    ESCAPE TOP                            /* RE1
*    ESCAPE BOTTOM                        /* RE1
  END-IF
*
  ADD 1 TO #RECS-READ
  IF (ST-V.ID-NUMBER LE 1900)
   OR (ST-V.WIDOW-FLAG = 'Y')        /* MBR ALREADY DEAD
   OR (ST-V.NUMBER-MEMBERSHIPS = 0)       /* MBR INACTIVE
   OR (ST-V.PROCESS-IND = 'C')             /* contact entities
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #RECS-SELECTED
*
  MOVE ST-V.FACE-AMOUNT (*) TO #FACE-AMT (*)
  MOVE *DATN TO #AS-OF-DT
  RESET  #SUFFIX
  CALLNAT 'CVALUSXX'
    ST-V.ID-NUMBER
    #SUFFIX
    #AS-OF-DT
    #LOAN-BAL   (1:9)
    #LOAN-BAL-2 (1:9)
    #ACC-INT    (1:9)
    #ACC-INT-2  (1:9)
    #CSV        (1:9)
    #CVALUE     (1:9)
    #LVALUE     (1:9)
    #MCSV       (1:9)
    #DB         (1:9)
    #ANN-PREMIUM  (1:9)
    #FACE-AMT   (1:9)
    #TPP        (1:9)
*
  RESET #UPDATE
  FOR #I1 1 TO 9
    IF  (STATUS (#I1) NE 'D')
     OR (X-FIRST-PLACE-FEE (#I1) = 0)
      ESCAPE TOP
    END-IF
*
    RESET #FOUND-TR
    IF DATE-ENTERED (#I1) = 0
      READ TR-V BY ID-NUMBER FROM ST-V.ID-NUMBER
        IF TR-V.ID-NUMBER NE ST-V.ID-NUMBER
          ESCAPE BOTTOM
        END-IF
*
        IF TRANSACTION-DATN LT (ISSUE-DATE (#I1) - 00000400) /* 4 mons
      ESCAPE TOP
        END-IF
*
  DISPLAY TR-V.ID-NUMBER TRANSACTION-DATN OLD-DATA (1) (EM=XXXXXXXXXXXX)
       NEW-DATA (1) (EM=XXXXXXXXXXXX) 'date' TRANSACTION-DATN
        IF  (OLD-DATA (1) = MASK ('NM-NU'))
         OR (OLD-DATA (1) = MASK ('NM-NE'))
         OR (OLD-DATA (1) = MASK ('AI-AD'))
         OR (NEW-DATA (1) = MASK ('NM-NU'))
         OR (NEW-DATA (1) = MASK ('NM-NE'))
         OR (NEW-DATA (1) = MASK ('AI-AD'))
          MOVE TRANSACTION-DATN TO DATE-ENTERED (#I1)
          MOVE 'TR' TO #FOUND-TR
          MOVE TRUE TO #UPDATE
          ESCAPE BOTTOM
        END-IF
      END-READ
    END-IF
*
    MOVE DATE-ENTERED (#I1) TO #YYYYMMDD
    ADD 2 TO #MM
    IF #MM GT 12
      SUBTRACT 12 FROM #MM
      ADD 1 TO #YYYY
    END-IF
    MOVE #YYYYMMDD TO #TERM-DATE
*
    COMPUTE #MON-PREM = ANNUAL-PREM (#I1) / 12
    COMPUTE #MONS-5-5 =     /* truncate partial months
        X-FIRST-PLACE-FEE (#I1) / #MON-PREM
*   IF #MONS-FRAC GT 0
*     ADD 1 TO #MONS
*   END-IF
*
    CALLNAT 'GPDTOSXX'
      ISSUE-DATE (#I1)
      MONTH-ALLOC-AMT (#I1)
      PAID-TO-DATE (#I1)
      #CALC-PAID-TO-DATE
*
    MOVE *DATN TO #YYYYMMDD
    RESET #TERM-NOW
    IF ((#TERM-DATE-YYYYMM LT #YYYYMM)
     OR ((#TERM-DATE-YYYYMM = #YYYYMM) AND (#TERM-DATE-DD LE #DD)))
       AND (NOT PAD-CODE = 'P' OR = 'V' OR = 'D' OR = 'X')
       AND (#CSV (#I1) LT X-FIRST-PLACE-FEE (#I1))
      MOVE '*' TO #TERM-NOW
    END-IF
*
    IF #TERM-DATE-YYYYMM LE #YYYYMM
      ADD 1 TO #CNT-4
      DISPLAY (4) 'CNT4' #CNT-4 (EM=ZZ9) 'CN' ST-V.ID-NUMBER
        'Issue/Date'  ISSUE-DATE (#I1)
        'DATE/ENT'  DATE-ENTERED (#I1)
        'FPF'    X-FIRST-PLACE-FEE (#I1) (EM=-ZZZ,ZZZ.99)
        'CSV'    #CSV (#I1) (EM=-ZZZZ.99)
        'Pln'   PLAN (#I1)
        'Md'   MODE (#I1)
        'Term/Now' #TERM-NOW
        'Pad/Cod' PAD-CODE
        'TR'    #FOUND-TR
        'Mon/Prem' #MON-PREM    (EM=ZZZZ.99)
*       'Mns' #MONS  (EM=Z9)
        'PD-TO/Date' #CALC-PAID-TO-DATE
*       'FPF/PD-TO/Date' #FPF-PAID-TO-DATE
        'BADJ'   X-BILL-ADJUST (#I1)    (EM=-ZZZZ.99)
        'Pay/Stat' PAY-STATUS (#I1)
    ELSE
      ADD 1 TO #CNT-2
      DISPLAY (2) 'CNT2' #CNT-2 (EM=ZZ9) 'CN' ST-V.ID-NUMBER
        'Issue/Date'  ISSUE-DATE (#I1)
        'DATE/ENT'  DATE-ENTERED (#I1)
        'FPF'    X-FIRST-PLACE-FEE (#I1) (EM=-ZZZ,ZZZ.99)
        'CSV'    #CSV (#I1) (EM=-ZZZZ.99)
        'Pln'   PLAN (#I1)
        'Md'   MODE (#I1)
        'Pad/Cod' PAD-CODE
        'TR'    #FOUND-TR
        'Mon/Prem' #MON-PREM    (EM=ZZZZ.99)
*       'Mns' #MONS  (EM=Z9)
        'PD-TO/Date' #CALC-PAID-TO-DATE
*       'FPF/PD-TO/Date' #FPF-PAID-TO-DATE
        'BADJ'   X-BILL-ADJUST (#I1)    (EM=-ZZZZ.99)
        'Pay/Stat' PAY-STATUS (#I1)
    END-IF
  END-FOR
*
  IF #UPDATE
    UPDATE (0210)
  END-IF
*
   END TRANSACTION
END-READ
*
END
