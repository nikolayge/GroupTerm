* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Created to be used for any generic process through EntireX
* NATSERV0 - EntireX Driver module (dynamic parameters length)
* Call Business Object module based on ProcessName
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    03010010       Initial Creation
********************************************************
*
DEFINE DATA
LOCAL USING OBJLCONT
LOCAL USING OBJLADDR
LOCAL USING OBJLADRL
LOCAL USING OBJLCEML
LOCAL USING ADDA0001
LOCAL
1 #FIR-REQUEST-FILE (A) Dynamic INIT <'/usr/SAG/MC/FIR_REPORT.TXT'>
1 #REPORT (A) Dynamic INIT <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/COM/family_change_report.txt'>
1 #OUTPUT-RECORD    (A500)
1 #INPUT-RECORD     (A100)
1 #ACTION_ARR       (A/0:3) DYNAMIC INIT <'NONE','ADD','UPDATE','DELETE'>
1 #LOOK-UP-KEY      (A7)
1 #LOOK-UP-VALUE    (A) DYNAMIC
1 #DATN             (N8)
1 REDEFINE #DATN
  2 #DATN_A         (A8)
1 #ID-NUMBER        (N6)
1 #CONTACT-ID       (N8)
1 #DATE             (A) DYNAMIC
1 #TAB              (A1) INIT <H'09'>
1 #CN               (A) DYNAMIC
1 #FUNCTION         (A) DYNAMIC
1 #NAME             (A) DYNAMIC
1 #FIRST            (A) DYNAMIC
1 #MIDDLE           (A) DYNAMIC
1 #LAST             (A) DYNAMIC
1 #DATE-OF-BIRTH    (A) DYNAMIC
1 #RELATIONSHIP     (A) DYNAMIC
1 #ACTION-TYPE      (A) DYNAMIC
1 #ACTION           (A) DYNAMIC
1 #MEMBER-NAME      (A) DYNAMIC
1 #EMAIL            (A) DYNAMIC
1 #FAMILY           (A) DYNAMIC INIT <'FAMILY'>
1 #ROLE             (A) DYNAMIC INIT <'CODE041'>
1 #INDEX            (N1)
END-DEFINE
*
#DATN := *DATN
#LOOK-UP-KEY := #ROLE
*
DEFINE WORK FILE 1 #FIR-REQUEST-FILE TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
DEFINE WORK FILE 2 #REPORT TYPE 'ASCII-COMPRESSED'
COMPRESS 'CN' 'Member Name' 'Address1' 'Address2' 'Address3'
  'Email' 'Name' 'Date Of Birth' 'Relationship to Member' 'Action'
  INTO #OUTPUT-RECORD WITH DELIMITER #TAB
WRITE WORK FILE 2 #OUTPUT-RECORD
*
READ WORK FILE 1 #INPUT-RECORD
  RESET #DATE #CN #FUNCTION #FIRST #MIDDLE #LAST #DATE-OF-BIRTH
        #RELATIONSHIP #ACTION-TYPE #ADD-LINE-1 #ADD-LINE-2
        #ADD-LINE-3 #EMAIL #NAME
  SEPARATE #INPUT-RECORD INTO #CN #FUNCTION #DATE #FIRST #MIDDLE #LAST
    #DATE-OF-BIRTH #RELATIONSHIP #ACTION-TYPE IGNORE
    WITH DELIMITER #TAB
*
  #DATE := *TRIM(#DATE)
  #FUNCTION := *TRIM(#FUNCTION)
*
  IF #DATE = #DATN_A AND #FUNCTION = #FAMILY
    #CN := *TRIM(#CN)
    IF #CN IS (N6)
      #ID-NUMBER := VAL(#CN)
    ELSE
      ESCAPE TOP
    END-IF
    #ACTION-TYPE := *TRIM(#ACTION-TYPE)
    IF #ACTION-TYPE IS (N1)
      #INDEX := VAL(#ACTION-TYPE)
    ELSE
      ESCAPE TOP
    END-IF
*
    PERFORM GET-OTHER-INFO
*
    COMPRESS #FIRST #MIDDLE #LAST INTO #NAME
    #LOOK-UP-VALUE := *TRIM(#RELATIONSHIP)    
    #RELATIONSHIP := F-LOOK-UP-TABLE(<#LOOK-UP-KEY,#LOOK-UP-VALUE>)
    #ACTION := #ACTION_ARR(#INDEX)
    COMPRESS #CN #TAB #MEMBER-NAME #TAB #ADD-LINE-1 #TAB #ADD-LINE-2 #TAB
        #ADD-LINE-3 #TAB #EMAIL #TAB #NAME #TAB #DATE-OF-BIRTH #TAB
        #RELATIONSHIP #TAB #ACTION INTO #OUTPUT-RECORD
    WRITE WORK FILE 2 #OUTPUT-RECORD
  END-IF
END-WORK
*
********************************
DEFINE SUBROUTINE GET-OTHER-INFO
********************************
*
FIND(1) CT-V WITH ID-NUMBER = #ID-NUMBER
  #MEMBER-NAME := Edit_Name_FMLS(<CT-V.FULL-NAME>)
  #CONTACT-ID  := CT-V.CONTACT-ID
END-FIND
*
FIND EMAIL-V WITH CONTACT-ID = #CONTACT-ID
  ACCEPT IF EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
  #EMAIL := EMAIL-V.EMAIL-ADDRESS
END-FIND
*
RESET #ADD-POOL-ID
#ADD-CONTACT-ID := #CONTACT-ID
#ADD-TYPE       := 'P'
*
CALLNAT 'ADDN0001' #PDA-ADDRESS
*
END-SUBROUTINE
*
END

