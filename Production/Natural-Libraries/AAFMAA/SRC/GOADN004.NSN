* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GOADN004 - Create Address relation
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    08/23/2011  None    Program written                           *
*
************************************************************************
DEFINE DATA
PARAMETER
1 #ADDR-REL
  2 CONTACT-ID (N8)
  2 ADDRESS-POOL-ID (N8)
  2 ADDR-TYPE-CD (A1)
  2 ADDR-LOCATION (A1)
  2 ATTENTION-LINE (A50)
  2 RETURNED-MAIL-IND (A1)
  2 PREVENT-OVER-IND  (A1)
  2 ADDRESS-SOURCE    (A1)
  2 STATUS            (A1)
  2 STATUS-DATE (N8)
1 #CLERK-ID (A3)
1 #MSG      (A) DYNAMIC
1 #UPDATED  (L)
LOCAL
1 ADR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID        (N8)
  2 ADDRESS-POOL-ID   (N8)
  2 ATTENTION-LINE    (A50)
  2 ADDR-LOCATION     (A1)
  2 ADDR-TYPE-CD      (A1)
  2 RETURNED-MAIL-IND (A1)
  2 PREVENT-OVER-IND  (A1)
  2 PREVENT-OVER-DATE (N8) 2 REDEFINE PREVENT-OVER-DATE
    3 PREVENT-OVER-DATE-A (A8)
  2 ADDRESS-SOURCE    (A1)
  2 STATUS            (A1)
  2 STATUS-DATE       (N8) 2 REDEFINE STATUS-DATE
    3 STATUS-DATE-A   (A8)
  2 LAST-DATE-UPD     (N8) 2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A     (A8)
  2 LAST-TIME-UPD     (N7) 2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A     (A7)
  2 RETURNED-MAIL-DATE (N8) 2 REDEFINE RETURNED-MAIL-DATE
    3 RETURNED-MAIL-DATE-A (A8)
  2 LAST-USER-UPD     (A8)
1 BLANK  (A1) CONST <' '>
END-DEFINE
*
IF #ADDR-REL.CONTACT-ID = 0
    OR #ADDR-REL.ADDRESS-POOL-ID = 0
  #MSG := 'CONTACT-ID and ADDRESS-POOL-ID required'
  ESCAPE ROUTINE
END-IF
*
FIND (1) ADR-REL-V WITH CONTACT-ID = #ADDR-REL.CONTACT-ID
    AND ADDRESS-POOL-ID = #ADDR-REL.ADDRESS-POOL-ID
  IF NO RECORDS FOUND
    IF #ADDR-REL.ADDR-TYPE-CD = BLANK
      #ADDR-REL.ADDR-TYPE-CD := 'P'
    END-IF
    MOVE BY NAME #ADDR-REL TO ADR-REL-V
    ADR-REL-V.LAST-DATE-UPD  := *DATN
    ADR-REL-V.LAST-TIME-UPD  := *TIMN
    IF #CLERK-ID = BLANK
      ADR-REL-V.LAST-USER-UPD := *USER
    ELSE
      ADR-REL-V.LAST-USER-UPD := #CLERK-ID
    END-IF
    STORE ADR-REL-V
    #UPDATED := TRUE
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #ADDR-REL TO ADR-REL-V
  RESET ADR-REL-V.STATUS
  ADR-REL-V.STATUS-DATE    := *DATN
  ADR-REL-V.LAST-DATE-UPD  := *DATN
  ADR-REL-V.LAST-TIME-UPD  := *TIMN
  IF #CLERK-ID = BLANK
    ADR-REL-V.LAST-USER-UPD := *USER
  ELSE
    ADR-REL-V.LAST-USER-UPD := #CLERK-ID
  END-IF
  UPDATE
  #UPDATED := TRUE
END-FIND
*
ON ERROR
  COMPRESS 'Address relations:' *ERROR-NR *ERROR-LINE *PROGRAM *DATN *TIME
    INTO #MSG WITH DELIMITER ':'
  ESCAPE ROUTINE
END-ERROR
*
END
