* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPM15A   *** PRINTS LABELS ***
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  Reset display fields for GRANKSXX                 *
************************************************************************
*
DEFINE DATA
LOCAL
*
1 #INPUT-PARM (A11)
1 REDEFINE #INPUT-PARM
 2 #PARM-NUM-UP-LBL     (A3)
 2 REDEFINE #PARM-NUM-UP-LBL
  3 #PARM-NUM-UP        (N1)
  3 #PARM-NUM-UP-UP     (A2)
 2 #FILLER              (A1)
*
 2 #PARM-NUM-ROWS-LBL   (A6)
 2 REDEFINE #PARM-NUM-ROWS-LBL
  3 #PARM-NUM-ROWS      (N2)
  3 #PARM-NUM-ROWS-ROWS (A4)
*
1 #ADDRESS-EXTRACT
 2 #ADR-ID           (A9)
 2 REDEFINE #ADR-ID
  3 #ADR-ID-CN       (A2)
  3 #ADR-ID-NUM      (N6)
  3 #ADR-ID-FIL      (A1)
 2 #ADR-MBRSHIPS     (N1)
 2 #ADR-NAME         (A25)
 2 #ADR-RANK         (A4)
 2 REDEFINE #ADR-RANK
  3 #ADR-RANK-3      (A3)
 2 #ADR-MIL-STAT     (A1)
 2 #ADR-ADDR-1       (A25)
 2 #ADR-ADDR-2       (A25)
 2 REDEFINE #ADR-ADDR-2
  3 #ADR-ADDR-2-20   (A20)
  3 #ADR-ADDR-2-LST-5 (A5)
 2 #ADR-ADDR-3       (A25)
 2 REDEFINE #ADR-ADDR-3
  3 #ADR-ADDR-3-20   (A20)
  3 #ADR-ADDR-3-LST-5 (A5)
 2 #ADR-ZIP-CODE     (A10)
 2 REDEFINE #ADR-ZIP-CODE
  3 #ADR-ZIP-5       (A5)
  3 REDEFINE #ADR-ZIP-5
   4 #ADR-ZIP-5-TBL (N1/5)
  3 #ADR-ZIP-DASH    (A1)
  3 #ADR-ZIP-LST-4   (A4)
  3 REDEFINE #ADR-ZIP-LST-4
   4 #ADR-ZIP-4-TBL  (N1/4)
 2 #ADR-MIL-SERV     (A3)
 2 #ADR-SSN          (N9)
 2 #ADR-ZIP-DPB      (A2)
 2 REDEFINE #ADR-ZIP-DPB
  3 #ADR-ZIP-DPB-TBL (N1/2)
 2 #ADR-CHIEF-OF-STAFF-IND (A1)
 2 #ADR-FILL1        (A10)
 2 #ADR-SPUSE        (A1)
 2 #ADR-LAST-NAME    (A25)
 2 #ADR-FILL2        (A5)
*
1 #X                (N2)
1 #I1               (I2)
1 #I2               (I2)
1 #ICOL             (I2)
1 #ILBL             (I2)
1 #CUR-DATE         (A8)
1 #NUM-LBLS         (P5)
1 #ZIP-CNT          (P5)
1 #ZIP              (A5)
1 #NUM-FEW-ZIPS     (P5)
1 #NUM-FEW-LBLS     (P5)
*
1 #NAME            (A30)
1 #LAST            (A25)
1 #FIRST/MID       (A25)
1 #SUFF            (A9)
1 #NAME-OVRFLO     (A25)
1 #CMND            (A2) INIT <'BR'>
1 #RANK            (A3)
1 #INVALID         (L)
1 #RANK-LIT        (A23)
1 #RANK-DISPLAY    (A5)
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #SCR-ZIP-BAR-CODE  (A14)
1 #ZIP-SUM           (N3)
1 REDEFINE #ZIP-SUM
 2 #ZIP-SUM-2        (N2)
 2 #ZIP-SUM-1        (A1)
*
1 #SCR-LABEL-COLS   (4)
 2 #SCR-LABEL-LINES (12)
  3 #SCR-BAR-CODE     (A14)
  3 #SCR-CN-DATE       (A31)
  3 #SCR-NAME          (A31)
  3 #SCR-ADDR-1        (A31)
  3 #SCR-ADDR-2        (A31)
  3 #SCR-ADDR-3        (A31)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
FORMAT (2) PS=66 LS=133
FORMAT (3) PS=66 LS=133
FORMAT (1) PS=66 LS=133
WRITE (1) TITLE LEFT 'PROGRAM:' *PROGRAM
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (6) PS=66 LS=133
WRITE (6) TITLE LEFT 'PROGRAM:' *PROGRAM 15X 'AT LEAST 10 LBLS IN 1 ZIP'
                 15X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
READ WORK 1 ONCE RECORD #INPUT-PARM
*
WRITE (1) (LS=133) 'ATHPM15A INPUT PARM =' #INPUT-PARM
*
IF #PARM-NUM-UP-LBL = '3UP'  /* to print tots in last label
  MOVE 11 TO #X
ELSE
  MOVE 7 TO #X
END-IF
*
FOR #I2 1 TO #X
  FOR #I1 1 TO #PARM-NUM-UP
    MOVE '!123456789122!' TO #SCR-BAR-CODE (#I1, #I2)
    MOVE ALL 'X' TO #SCR-CN-DATE  (#I1, #I2)
    MOVE ALL 'X' TO #SCR-NAME     (#I1, #I2)
    MOVE ALL 'X' TO #SCR-ADDR-1   (#I1, #I2)
    MOVE ALL 'X' TO #SCR-ADDR-2   (#I1, #I2)
    MOVE ALL 'X' TO #SCR-ADDR-3   (#I1, #I2)
  END-FOR
END-FOR
*
COMPUTE #ILBL = #X + 1
READ WORK 2 RECORD #ADDRESS-EXTRACT
  IF   (#ADR-ZIP-5 NE #ZIP)
   AND (#ZIP NE ' ')
    IF #ZIP-CNT GE 10
      DISPLAY (6) #ZIP #ZIP-CNT
    ELSE
      ADD 1 TO #NUM-FEW-ZIPS
      ADD #ZIP-CNT TO #NUM-FEW-LBLS
    END-IF
    RESET #ZIP-CNT
  END-IF
*
  MOVE #ADR-ZIP-5 TO #ZIP
  ADD 1 TO #ZIP-CNT
  ADD 1 TO #NUM-LBLS
  ADD 1 TO #ICOL
*
  IF #ICOL GT #PARM-NUM-UP
    MOVE 1 TO #ICOL
    ADD  1 TO #ILBL
  END-IF
*
  IF #ILBL GT #PARM-NUM-ROWS
    PERFORM PRINT-A-PAGE
  END-IF
*
  RESET #SCR-ZIP-BAR-CODE
  RESET #ZIP-SUM
  IF NOT #ADR-ZIP-5 = ' ' OR = '00000'
    RESET #ZIP-SUM
    FOR #I1 1 TO 5
      ADD #ADR-ZIP-5-TBL (#I1) TO #ZIP-SUM
    END-FOR
*
    IF #ADR-ZIP-LST-4 NE ' '
      FOR #I1 1 TO 4
        ADD #ADR-ZIP-4-TBL (#I1) TO #ZIP-SUM
      END-FOR
*
      FOR #I1 1 TO 2
        ADD #ADR-ZIP-DPB-TBL (#I1) TO #ZIP-SUM
      END-FOR
    END-IF
*
    DIVIDE 10 INTO #ZIP-SUM REMAINDER #ZIP-SUM
    COMPUTE #ZIP-SUM = 10 - #ZIP-SUM
*
    IF #ADR-ZIP-LST-4 = ' '
      COMPRESS '!' #ADR-ZIP-5 #ZIP-SUM-1 '!'
                               INTO #SCR-ZIP-BAR-CODE LEAVING NO
    ELSE
      COMPRESS '!' #ADR-ZIP-5 #ADR-ZIP-LST-4 #ADR-ZIP-DPB
             #ZIP-SUM-1 '!' INTO #SCR-ZIP-BAR-CODE LEAVING NO
    END-IF
  END-IF
*
  MOVE #SCR-ZIP-BAR-CODE TO #SCR-BAR-CODE (#ICOL, #ILBL)
 COMPRESS *DAT4U #ADR-ID-CN #ADR-ID-NUM INTO #SCR-CN-DATE (#ICOL, #ILBL)
*
  IF #ADR-MIL-SERV = 'NCO' OR = 'OFF'   /* "CAP RUN'
    IF #ADR-MIL-STAT = 'M'   /* send appl
     COMPRESS *DAT4U #ADR-ID-CN #ADR-ID-NUM #ADR-MIL-SERV 'APPL'
                         INTO #SCR-CN-DATE (#ICOL, #ILBL)
    ELSE
     COMPRESS *DAT4U #ADR-ID-CN #ADR-ID-NUM 'ALLT STRT'
                         INTO #SCR-CN-DATE (#ICOL, #ILBL)
    END-IF
  ELSE
    COMPRESS *DAT4U #ADR-ID-CN #ADR-ID-NUM #ADR-MIL-STAT #ADR-MIL-SERV
                           INTO #SCR-CN-DATE (#ICOL, #ILBL)
  END-IF
*
  SEPARATE #ADR-NAME LEFT INTO #LAST #FIRST/MID #SUFF #NAME-OVRFLO
                                    WITH DELIMITER ','
  COMPRESS #FIRST/MID #LAST INTO #NAME
*
  IF #SUFF NE ' '
    COMPRESS #NAME ',' INTO #NAME LEAVING NO
    COMPRESS #NAME #SUFF INTO #NAME
  END-IF
*
  IF   (#ADR-MIL-STAT = 'R')
   AND (NOT #ADR-CHIEF-OF-STAFF-IND NE 'X')
   AND (NOT #ADR-RANK = 'MR' OR = 'MS' OR = 'MRS' OR = 'MIS'
                             OR = 'REV' OR = 'DR' OR = ' ')
    COMPRESS #NAME ', RET' INTO #NAME LEAVING NO
  END-IF
*
  IF #PARM-NUM-UP-LBL = '3UP' /* impact prt cannot print lower case
    MOVE #ADR-RANK TO #RANK-DISPLAY
  ELSE
    RESET #DISPL #RANK-LIT #RANK-DISPLAY                          /* PM1
    CALLNAT 'GRANKSXX' #CMND #ADR-RANK-3 #ADR-MIL-SERV
           #INVALID #DISPL #RANK-LIT #RANK-DISPLAY
  END-IF
*
  COMPRESS #RANK-DISPLAY #NAME  INTO #SCR-NAME (#ICOL, #ILBL)
  MOVE #ADR-ADDR-1 TO #SCR-ADDR-1 (#ICOL, #ILBL)
*
  IF #ADR-ADDR-3 = ' '
    IF #ADR-ADDR-2-LST-5 = ' '
      COMPRESS #ADR-ADDR-2 #ADR-ZIP-CODE INTO #SCR-ADDR-2(#ICOL, #ILBL)
    ELSE
      COMPRESS #ADR-ADDR-2 #ADR-ZIP-5 INTO #SCR-ADDR-2(#ICOL, #ILBL)
    END-IF
  ELSE
    MOVE #ADR-ADDR-2 TO #SCR-ADDR-2 (#ICOL, #ILBL)
    IF #ADR-ADDR-3-LST-5 = ' '
      COMPRESS #ADR-ADDR-3 #ADR-ZIP-CODE INTO #SCR-ADDR-3(#ICOL, #ILBL)
    ELSE
      COMPRESS #ADR-ADDR-3 #ADR-ZIP-5 INTO #SCR-ADDR-3(#ICOL, #ILBL)
    END-IF
  END-IF
END-WORK
*
IF #PARM-NUM-UP-LBL = '3UP'  /* to print tots in last label
  ADD 1 TO #ICOL
*
  IF #ICOL GT #PARM-NUM-UP
    MOVE 1 TO #ICOL
    ADD  1 TO #ILBL
  END-IF
*
  IF #ILBL GT #PARM-NUM-ROWS
    PERFORM PRINT-A-PAGE
  END-IF
  COMPRESS 'TOT 3-UP LBLS = ' #NUM-LBLS INTO #SCR-NAME (#ICOL, #ILBL)
END-IF
*
IF #NUM-LBLS NE 0
  PERFORM PRINT-A-PAGE
END-IF
*
IF #ZIP-CNT GE 10
  DISPLAY (6) #ZIP #ZIP-CNT
ELSE
  ADD 1 TO #NUM-FEW-ZIPS
  ADD #ZIP-CNT TO #NUM-FEW-LBLS
END-IF
*
WRITE (6) // 'Total labels = ' #NUM-LBLS
WRITE (6) /  'Total zips with less than 10 labels' #NUM-FEW-ZIPS
WRITE (6) /  'Total lbls in above category       ' #NUM-FEW-LBLS
*
IF #PARM-NUM-UP-LBL = '3UP'
  IGNORE   /* do not put out total line due to impact printer
* NEWPAGE (2)
* WRITE   (2) /// '   TOTAL 3-UP LBLS PRINTED = ' #NUM-LBLS
ELSE
  IF (#PARM-NUM-UP-LBL = '4UP') AND (#PARM-NUM-ROWS-LBL = '08ROWS')
    NEWPAGE (3)
    WRITE   (3) /// '   TOTAL LASER CHESHIRE LBLS PRINTED = ' #NUM-LBLS
  ELSE
    NEWPAGE (4)
    WRITE   (4) /// '   TOTAL IMPACT LBLS PRINTED = ' #NUM-LBLS
  END-IF
END-IF
*
* * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-A-PAGE
* * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #PARM-NUM-UP-LBL = '3UP'
  WRITE (2) NOTITLE USING FORM 'ATHFM153'
ELSE
  IF (#PARM-NUM-UP-LBL = '4UP') AND (#PARM-NUM-ROWS-LBL = '08ROWS')
    NEWPAGE (3)                         /* FULL ZIP
    WRITE (3) NOTITLE USING FORM 'ATHFM154'
  ELSE
    WRITE (4) NOTITLE USING FORM 'ATHFM155'
  END-IF
END-IF
*
MOVE 1 TO #ICOL #ILBL
RESET #SCR-BAR-CODE (*, *)
RESET #SCR-CN-DATE  (*, *)
RESET #SCR-NAME     (*, *)
RESET #SCR-ADDR-1   (*, *)
RESET #SCR-ADDR-2   (*, *)
RESET #SCR-ADDR-3   (*, *)
*
END-SUBROUTINE
*
END
