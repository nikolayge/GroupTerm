* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GCASESXX - Convert Name from Upper Case to Lower Case ********
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  02172006 None Program Written                                   *
************************************************************************
*
DEFINE DATA
PARAMETER
*
1 #NAME                      (A25)
*
LOCAL
*
1 #LAST-NAME                 (A25)
1 #FIRST/MID                 (A25)
1 #FIRST-NAME                (A25)
1 #MIDDLE-INIT               (A25)
1 #SUFFIX                    (A25)
1 #EXTRA1                    (A25)
1 #EXTRA2                    (A25)
1 #EXTRA3                    (A25)
1 #EXTRA4                    (A25)
*
1 #I1                        (I2)
1 #I2                        (I2)
*
1 #CASE-CHANGE               (A25)
1 REDEFINE #CASE-CHANGE
 2 #CASE-CHANGE-LET-OCC      (25)
  3 #CASE-CHANGE-LET         (A1)
*
1 #LETTER-TABLE
  2 #1T                      (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>
  2 #2T                      (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>
1 REDEFINE #LETTER-TABLE
  2 #LETTER-OCC              (26)
    3 #UPPER                 (A1)
    3 #LOWER                 (A1)
*
END-DEFINE
************************************************************************
*
SEPARATE #NAME LEFT INTO #LAST-NAME #FIRST/MID
                         #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER ','
SEPARATE #FIRST/MID INTO #FIRST-NAME #MIDDLE-INIT #EXTRA3
                         #EXTRA4 WITH DELIMITER ' '
*
MOVE #LAST-NAME TO #CASE-CHANGE
PERFORM UPPER-LOWER
MOVE #CASE-CHANGE TO #LAST-NAME
*
MOVE #FIRST-NAME TO #CASE-CHANGE
PERFORM UPPER-LOWER
MOVE #CASE-CHANGE TO #FIRST-NAME
*
RESET #NAME
*
COMPRESS #FIRST-NAME #MIDDLE-INIT #EXTRA3 #EXTRA4 INTO #FIRST/MID
*
IF #LAST-NAME NE ' '
  PERFORM CHECK-FOR-COMMA
  COMPRESS #NAME #LAST-NAME INTO #NAME
END-IF
*
IF #FIRST/MID NE ' '
  PERFORM CHECK-FOR-COMMA
  COMPRESS #NAME #FIRST/MID INTO #NAME
END-IF
*
IF #SUFFIX NE ' '
  PERFORM CHECK-FOR-COMMA
  COMPRESS #NAME #SUFFIX INTO #NAME
END-IF
*
IF #EXTRA1 NE ' '
  PERFORM CHECK-FOR-COMMA
  COMPRESS #NAME #EXTRA1 INTO #NAME
END-IF
*
IF #EXTRA2 NE ' '
  PERFORM CHECK-FOR-COMMA
  COMPRESS #NAME #EXTRA2 INTO #NAME
END-IF
*
************************************************************************
DEFINE UPPER-LOWER
************************************************************************
*
FOR #I1 2 TO 25
  FOR #I2 1 TO 26
*
    IF #CASE-CHANGE-LET (#I1) = #UPPER (#I2)
      MOVE #LOWER (#I2) TO #CASE-CHANGE-LET (#I1)
    END-IF
  END-FOR
END-FOR
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE CHECK-FOR-COMMA
************************************************************************
*
IF #NAME NE ' '
  COMPRESS #NAME ',' INTO #NAME LEAVING NO
END-IF
*
END-SUBROUTINE
*
END
