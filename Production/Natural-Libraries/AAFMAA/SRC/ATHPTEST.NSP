* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDFAS - DFAS Refund Processing
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING ATHLTMRF
LOCAL
*
1 #INPUT
 2 #IN-YYYY        (A4)
 2 #IN-DEL-1       (A1)
 2 #IN-CHK-NUM     (A6)
 2 #IN-DEL-2       (A1)
 2 #IN-ID          (A6)
 2 #IN-DEL-3       (A1)
 2 #IN-DB-CR-DOLS  (A3)
 2 #IN-DB-CR-CENTS (A2)
*
1 #FA-ALOT   (P5.2)
1 #TO-INIT   (A3)
1 #ST-X   (I2)
1 #ST-ISN   (P8)
1 #FOUND-ISS-DT   (L)
1 #CANCEL         (L)
1 #ST-NOT-FOUND   (L)
*
1 #AMT    (N6.2)
1 REDEFINE #AMT
 2 #AMT-DOLS     (A6)
 2 #AMT-CENTS    (A2)
1 REDEFINE #AMT
 2 #AMT-X        (A1/8)
1 REDEFINE #AMT
 2 #AMT-A        (A8)
*
1 #ISS-DT-FOR-NOTE     (N8)
1 REDEFINE #ISS-DT-FOR-NOTE
 2 #ISS-DT-YYYY-FOR-NOTE-A (A4)
 2 #ISS-DT-MM-FOR-NOTE-A   (A2)
 2 #ISS-DT-DD-FOR-NOTE-A   (A2)
*
1 #DEAR-FORMATED-NAME  (A45)
1 #TOT-REFUND    (P7.2)
1 #TOT-DB-CR     (P7.2)
1 #TOT-BILL-ADJ  (P7.2)
1 #DB-CR-REFD    (P7.2)
1 #DB-CR-REFD-DISP (A10)
1 #NUM-DB-CR     (N5)
1 #BILL-ADJ-REFD (P7.2)
1 #BILL-ADJ-REFD-DISP (A10)
1 #NUM-BILL-ADJ  (N5)
1 #OLD-BILL-ADJ  (P7.2)
*
1 #TR-LOG-FIL-MNT-DESC  (A125)
1 REDEFINE #TR-LOG-FIL-MNT-DESC
 2 #TR-L-BADJ-LB1 (A12)
 2 #TR-L-BADJ-AMT (A10)
 2 #TR-L-APPD-DT  (A16)
 2 #TR-L-BADJ     (A87)
1 #UPDATE    (L)
1 #HOLD-CUR-ACCT-UPDT (N8)
1 #MODAL-PREM    (N5.2)
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-CLERK-ID (A3/60)
 2 NOTE-LINE-NUM (N1/60)
 2 NOTE-DATE     (N8/60)
 2 REDEFINE NOTE-DATE
  3 NOTE-DATE-OCC  (60)
   4 NOTE-DATE-YYYY  (N4)
   4 NOTE-DATE-MMDD  (N4)
 2 NOTE-TEXT     (A60/60)
 2 REDEFINE NOTE-TEXT
  3 NOTE-TEXT-OCC   (60)
   4 NOTE-TEXT-60  (A60)
   4 REDEFINE NOTE-TEXT-60
    5 NOTE-TEXT-1-20  (A20)
    5 REDEFINE NOTE-TEXT-1-20
     6 NOTE-TEXT-11-20    (A10)
     6 NOTE-TEXT-ISS-MM   (A2)
     6 NOTE-TEXT-ISS-/1   (A1)
     6 NOTE-TEXT-ISS-DD   (A2)
     6 NOTE-TEXT-ISS-/2   (A1)
     6 NOTE-TEXT-ISS-YYYY (A4)
    5 NOTE-TEXT-21-22 (A2)     /* '-2' FOR POLICY #
    5 REDEFINE NOTE-TEXT-21-22
     6 NOTE-TEXT-21   (A1)
     6 NOTE-TEXT-22   (N1)    /* POLICY #
    5 NOTE-TEXT-23-60 (A38)
    5 REDEFINE NOTE-TEXT-23-60
     6 NOTE-TEXT-23-31    (A09) /* ' RT TO LT' OR ' LT TO RT'
     6 REDEFINE NOTE-TEXT-23-31
      7 NOTE-TEXT-23           (A1)
      7 NOTE-TEXT-PRIOR-PLAN   (A2)
     6 NOTE-TEXT-34-60    (A29)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER        (N6)
 2 REDEFINE ID-NUMBER
   3 ID-NBR         (A6)
 2 DTS-DATN
 2 DTS-TIMN
 2 DATE-ACCT-UPDATE
 2 WIDOW-FLAG       (A1)
 2 ALOT-AMT
 2 PROCESS-IND      (A1)
 2 NAME
 2 INTRO-FLAG
 2 DEBIT-CREDIT-AMT
 2 X-BILL-ADJUST    (9)
 2 ISSUE-DATE       (N8/9)
 2 REDEFINE ISSUE-DATE
  3 ISS-DT-OCC      (9)
   4 ISS-DT-YYYY    (N4)
   4 ISS-DT-MM      (N2)
   4 ISS-DT-DD      (N2)
 2 STATUS              (A1/9)
 2 PLAN                (A3/9)
 2 ANNUAL-PREM         (9)
 2 MODE                (9)
*
1 ACCT-REG-V VIEW OF A-ACCOUNTING-REGISTER
 2 ID-NUMBER             (N6)
 2 PROCESS-IND           (A1)  /* PM - 06/2000 - SPLIT COMPANIES
 2 TRANSACTION-DATN      (P9)
 2 TRANSACTION-TIMN      (P7)
 2 TXN-CODE              (A4)
 2 SEQUENCE-NUM          (N1)
 2 NAME                  (A25)
 2 TXN-ACCT-NUMBER       (N3)
 2 ACCT-DATE             (N8)
 2 DATE-LAST-ACCT-UPDATE (N8)
 2 DEBIT-AMOUNT          (P7.2)
 2 CREDIT-AMOUNT         (P7.2)
 2 DESCRIPTION           (A20)
 2 MESSAGE-CODE          (A1)
 2 AALP-FLAG             (A1)
 2 CLERK-ID              (A3)
 2 TERMINAL-ID           (A8)
 2 CASH-CODE             (A1)
 2 SG-CATEGORY-1         (A2/15)
 2 SG-CATEGORY-2         (A2/15)
 2 SG-AMOUNT             (N6.2/15)
*
1 #RF-AMT-DISP  (A9)  /* $1,234.56
*
1 #COUNTERS
 2 #RECS-READ        (P5)
 2 #RECS-SELECTED    (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #CNT-5            (P5)
 2 #CNT-6            (P5)
 2 #CNT-7            (P5)
 2 #CNT-8            (P5)
 2 #CNT-9            (P5)
 2 #CNT-10           (P5)
 2 #CNT-11           (P5)
 2 #CNT-12           (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
1 #NUM-RECS          (N5)
1 #NUM-POLS          (P5)
1 #INT      (I2)
1 #NOTE-TEXT (A60/10)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYYMMDD-A    (A8)
1 REDEFINE #YYYYMMDD
 2 #YYYY          (N4)
 2 #MM            (N2)
 2 #DD            (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A        (A4)
 2 #MM-A          (A2)
 2 #DD-A          (A2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
       'PROGRAM EXCEPTIONS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Undeliverable Refunds'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Undeliverable Refunds Notes and Tr Reg'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 1 RECORD #INPUT
  IF #IN-ID = MASK ('CN')
    ESCAPE TOP
  END-IF
*
  RESET #ST-NOT-FOUND
  RESET #FOUND-ISS-DT #DB-CR-REFD #BILL-ADJ-REFD
  FIND (1) ST-V WITH MEMBER-KEY = #IN-ID
    IF NO RECORD FOUND
      MOVE TRUE TO #ST-NOT-FOUND
      ESCAPE BOTTOM
    END-NOREC
    MOVE *ISN TO #ST-ISN
*
    FOR #I1 1 TO 9
      IF PLAN(#I1) = 'RT' OR = 'LT'
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF NOT PLAN(#I1) = 'RT' OR = 'LT'
      ESCAPE TOP
    END-IF
*
    IF #ST-X = 0
      MOVE 1 TO #ST-X
    END-IF
  END-FIND
*
  IF #ST-NOT-FOUND
    WRITE #IN-ID 'CN not found'
    ESCAPE TOP
  END-IF
*
  MOVE #IN-DB-CR-DOLS  TO #AMT-DOLS
  MOVE #IN-DB-CR-CENTS TO #AMT-CENTS
*
* FOR #I1 1 TO 8
*   IF #AMT-X (#I1) = ' ' OR = '-'
*     MOVE '0' TO #AMT-X (#I1)
*   END-IF
* END-FOR
*
  GET ST-V #ST-ISN
*
  WRITE 'BEFORE ' ST-V.ID-NUMBER ST-V.DEBIT-CREDIT-AMT
   ST-V.X-BILL-ADJUST  (#I1)
  IF NOT MODE(1) = 'A' OR = 'S' OR = 'Q' OR ='M'
    IF #AMT-A NE MASK (NNNNN)
      WRITE ST-V.ID-NUMBER 'INVALID DEBIT/CREDIT AMT'
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-DB-CR
    SUBTRACT #AMT FROM ST-V.DEBIT-CREDIT-AMT
    ADD #AMT TO #TOT-DB-CR
    ADD #AMT TO #TOT-REFUND
    PERFORM ACCT-REG-AND-NOTE
  ELSE
    IF #AMT-A NE MASK (NNNNN)
      WRITE ST-V.ID-NUMBER 'INVALID BILL ADJUST AMT'
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-BILL-ADJ
    MOVE ST-V.X-BILL-ADJUST (#I1) TO #OLD-BILL-ADJ
    SUBTRACT #AMT FROM ST-V.X-BILL-ADJUST (#I1)
    ADD #AMT TO #TOT-BILL-ADJ
    ADD #AMT TO #TOT-REFUND
    PERFORM TR-REG-AND-NOTE
  END-IF
*
  IF #AMT = 0
    WRITE #IN-ID 'NO DB/CR OR BILL ADJ AMT'
    ESCAPE TOP
  END-IF
*
  MOVE ST-V.DATE-ACCT-UPDATE TO #HOLD-CUR-ACCT-UPDT
  MOVE *DATN TO ST-V.DATE-ACCT-UPDATE
  MOVE *DATN TO ST-V.DTS-DATN
  MOVE *TIMN TO ST-V.DTS-TIMN
*
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2
    'CN'       ST-V.ID-NUMBER
    'Iss/Date' ST-V.ISSUE-DATE (#ST-X)
    'DB-CR/Adjust' #AMT  (EM=-ZZ,ZZZ.99)
    'New/DB-CR' DEBIT-CREDIT-AMT (EM=-ZZZ,ZZZ.99)
    'Bill-Adjust'  #AMT (EM=-ZZ,ZZZ.99)
    'New/Bil-Adj' X-BILL-ADJUST (#I1) (EM=-ZZ,ZZZ.99)
    'Name'         NAME
*
  UPDATE (0247)
  END TRANSACTION
END-WORK
*
WRITE ///
WRITE #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE #NUM-BILL-ADJ (EM=Z,ZZ9)      '     Num Bill adjustments'
WRITE #TOT-BILL-ADJ (EM=ZZZ,ZZZ.99) 'Bill-adjust Adj'
WRITE #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT Undeliverable REFUND'
*
WRITE (2) ///
WRITE (2) #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE (2) #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE (2) #NUM-BILL-ADJ (EM=Z,ZZ9)      '     Num Bill adjustments'
WRITE (2) #TOT-BILL-ADJ (EM=ZZZ,ZZZ.99) 'Bill-adjust Adj'
WRITE (2) #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT Undeliverable REFUND'
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCT-REG-AND-NOTE
* * * * * * * * * * * * * *
*
RESET ACCT-REG-V
MOVE ST-V.ID-NUMBER        TO ACCT-REG-V.ID-NUMBER
MOVE ST-V.PROCESS-IND      TO ACCT-REG-V.PROCESS-IND /* PM - 06/2000
MOVE *DATN                 TO ACCT-REG-V.TRANSACTION-DATN
MOVE *TIMN                 TO ACCT-REG-V.TRANSACTION-TIMN
MOVE *INIT-ID              TO ACCT-REG-V.TERMINAL-ID
MOVE 'EDS'                 TO ACCT-REG-V.CLERK-ID
MOVE ST-V.NAME             TO ACCT-REG-V.NAME
MOVE 'A'                   TO ACCT-REG-V.CASH-CODE
RESET                         ACCT-REG-V.AALP-FLAG
MOVE *DATN                 TO ACCT-REG-V.ACCT-DATE
MOVE 'ADCR'                TO ACCT-REG-V.TXN-CODE
*
IF #HOLD-CUR-ACCT-UPDT = 0
  MOVE *DATN               TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
ELSE
  MOVE #HOLD-CUR-ACCT-UPDT TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
END-IF
*
MOVE #AMT TO ACCT-REG-V.DEBIT-AMOUNT
*
MOVE 450 TO ACCT-REG-V.TXN-ACCT-NUMBER
*
IF ST-V.INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q'
  MOVE 'CAP MBR' TO ACCT-REG-V.DESCRIPTION
END-IF
COMPRESS ACCT-REG-V.DESCRIPTION 'DFAS REFUND' INTO DESCRIPTION
*
* SG-category info not needed ???
*
STORE ACCT-REG-V
* WRITE (3) ST-V.ID-NUMBER 'ACCT-REG' #DB-CR-REFD DEBIT-CREDIT-AMT
*
MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #RF-AMT-DISP
COMPRESS '$' #RF-AMT-DISP INTO #RF-AMT-DISP LEAVING NO
* MPRESS 'Refund added to debit/credit' #RF-AMT-DISP INTO #NOTE-TEXT (1)
COMPRESS 'Outstanding Term Refund for' #IN-YYYY INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) '$' #RF-AMT-DISP INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) 'Issued on check #' INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) #IN-CHK-NUM  INTO #NOTE-TEXT (1)
* WRITE (3) 'NOTE WRITTEN' ST-V.ID-NUMBER #NOTE-TEXT (1)
MOVE 1 TO #INT
MOVE 'EDS' TO #CGA-CLERK
CALLNAT 'E4205SDM' ST-V.ID-NUMBER #INT #CGA-CLERK #NOTE-TEXT (1:10)
*
END-SUBROUTINE
*
* * * * * * * * * * * *
DEFINE SUBROUTINE TR-REG-AND-NOTE
* * * * * * * * * * * *
*
RESET TR-REG-V
RESET #TR-LOG-FIL-MNT-DESC
MOVE 'AM-BILL ADJ'            TO #TR-L-BADJ-LB1
MOVE EDITED #OLD-BILL-ADJ (EM=ZZ,ZZZ.99-) TO #TR-L-BADJ-AMT
MOVE #TR-LOG-FIL-MNT-DESC   TO TR-REG-V.OLD-DATA (1)
MOVE EDITED ST-V.X-BILL-ADJUST (#I1) (EM=ZZ,ZZZ.99-) TO #TR-L-BADJ-AMT
RESET #TR-L-APPD-DT
MOVE #TR-LOG-FIL-MNT-DESC   TO TR-REG-V.NEW-DATA (1)
*
MOVE ST-V.ID-NUMBER        TO TR-REG-V.ID-NUMBER
MOVE ST-V.PROCESS-IND      TO TR-REG-V.PROCESS-IND /* PM -06/2000-SPLIT
MOVE ST-V.NAME             TO TR-REG-V.NAME
MOVE #HOLD-CUR-ACCT-UPDT   TO TR-REG-V.DATE-LAST-UPDATE
MOVE *INIT-ID              TO TR-REG-V.TERMINAL-ID
MOVE 'EDS'                 TO TR-REG-V.CLERK-ID
MOVE *DATN                 TO TR-REG-V.TRANSACTION-DATN
MOVE *TIMN                 TO TR-REG-V.TRANSACTION-TIMN
STORE TR-REG-V
* WRITE (3) ST-V.ID-NUMBER 'TRAN-REG' #BILL-ADJ-REFD x-BILL-ADJUST (#i1)
*
MOVE ST-V.ISSUE-DATE (#I1) TO #ISS-DT-FOR-NOTE
MOVE EDITED #AMT (EM=Z,ZZZ.99) TO #RF-AMT-DISP
COMPRESS '$' #RF-AMT-DISP INTO #RF-AMT-DISP LEAVING NO
COMPRESS 'Outstanding Term Refund for' #IN-YYYY INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) '$' #RF-AMT-DISP INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) 'Issued on check #' INTO #NOTE-TEXT (1)
COMPRESS #NOTE-TEXT (1) #IN-CHK-NUM  INTO #NOTE-TEXT (1)
* WRITE (3) 'NOTE WRITTEN' ST-V.ID-NUMBER #NOTE-TEXT (1)
MOVE 1 TO #INT
MOVE 'EDS' TO #CGA-CLERK
CALLNAT 'E4205SDM' ST-V.ID-NUMBER #INT #CGA-CLERK #NOTE-TEXT (1:10)
*
END-SUBROUTINE
*
END
