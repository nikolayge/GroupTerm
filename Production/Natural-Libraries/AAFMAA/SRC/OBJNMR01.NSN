* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram OBJNMR01.
/**
/** :author nguentchev
/* TODO: Enter your code here
DEFINE DATA
PARAMETER
1 #REQUEST      (A) DYNAMIC
1 #MSG          (A) DYNAMIC
PARAMETER USING OBJAMR01
LOCAL USING OBJLMR01
END-DEFINE
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE 'ADD'    PERFORM ADD-RECORD
  VALUE 'UPDATE' PERFORM UPDATE-RECORD
  VALUE 'DELETE' PERFORM DELETE-RECORD
  VALUE 'GET'    PERFORM GET-RECORD
  VALUE 'EXIST'  PERFORM CHECK-EXIST
  NONE VALUE
    IGNORE
END-DECIDE
DEFINE CHECK-EXIST
*
F-NUMBER.
FIND NUMBER SVC-RATES-V WITH SVC-PLAN = #SVC-RATES.SVC-PLAN
*
END-SUBROUTINE
/*
DEFINE ADD-RECORD
PERFORM CHECK-EXIST
IF *NUMBER ( F-NUMBER.) > 0
  #MSG := 'No duplicate key allowed - record exist'
ELSE
  MOVE BY NAME #SVC-RATES TO SVC-RATES-V
  SVC-RATES-V.LAST-UPD-USER := *USER
  SVC-RATES-V.LAST-UPD-DATE := *DATN
  SVC-RATES-V.LAST-UPD-TIME := *TIMN
  STORE SVC-RATES-V
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
FIND SVC-RATES-V WITH SVC-PLAN = #SVC-RATES.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := 'No record exist for update'
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #SVC-RATES TO SVC-RATES-V
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE DELETE-RECORD
FIND SVC-RATES-V WITH SVC-PLAN = #SVC-RATES.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := 'No record to delete'
    ESCAPE BOTTOM
  END-NOREC
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE GET-RECORD
FIND SVC-RATES-V WITH SVC-PLAN = #SVC-RATES.SVC-PLAN
  MOVE BY NAME SVC-RATES-V TO #SVC-RATES
END-FIND
END-SUBROUTINE
/**
END
