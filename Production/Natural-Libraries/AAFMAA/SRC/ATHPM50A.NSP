* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
** PROGRAM-ID     ATHPM50A
** REMARKS WIDOW REPORTING SYSTEM
**          USER ABENDS-
**                 10  - BAD DATE PARM
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  06082005 DW1  Make SVC-CON 'C' be combat-related
* DRW  09162005 DW2  Add Cn to Extract to be sortrd
* SAG  11282005 RH1  SS - Modified the custom coded ON ERROR stmt &
*                    replaced it with copycode GABNDCX1
* SAG  12092005 RH1  SS - Modified the call to external object ABEND &
*                    replaced with copycode GABNDCX6.
* DRW  03132007 DW3  Stop deleting Widow records
* bz  06/09/2013  Rebranding (Search for AAFMAA)
*******************************************
**
**   REPORT-FILE        ASSIGN REPORT 1
**   WIDOW PURGE REPORT ASSIGN REPORT 2
**   WIDOW-EXTRACT-FILE ASSIGN WORK 1
*
*
*  MODIFICATIONS:
*    0688  NATURAL CONVERSION CHANGES TO FIELD SIZE   GRW
*    0789  ADD WIDOW PURGE  PER DOTSON    JAK
*    0989  ADD WIDOW STATUS TO REPORTS    PER DOTSON JAK
*    0390  ADD DEPN  STATUS TO REPORTS    PER DOUG   DRW
*    0390  ADD FURTHER WIDOW PURGE CRITERIA PER EDNA DRW
*    0690  REMOVE UNSETTLED POL/CLM REPORTS PER EDNA DRW
*    0890  BYPASS REMARIED WIDOWS FOR AGE 60 RPT     DRW
* 10/07/2004 modify SBP option logic                 DRW DW1
* 09/18/2013 Address normalization                   JNT JT1    
*
*******************************************************************
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL USING ADDA0001                        /* JT1
LOCAL USING CONTPDA1
LOCAL
*
*
1 EXTRACT-RECORD
*************************************************************
*                 A T H C M 5 0 A                           *
* EXTRACT CODES:                                            *
*         A -  WIDOW TICKLER LETTERS - AGE 60               *
*         B -  MEMBER WITH DECEASED SPOUSE - AGE 60         *
*         C -  WIDOW TICKLER LETTERS - CHILD AGE 18         *
*         D -  WIDOW TICKLER LETTERS - YOUNG CHILD AGE 18   *
*         E -  WIDOW TICKLER LETTERS - AGE 62               *
*         F -  MEMBER WITH DECEASED SPOUSE - AGE 62         *
*         G -  WIDOW TICKLER LETTERS - AGE 65               *
*         H -  MEMBER WITH DECEASED SPOUSE - AGE 65         *
*************************************************************
  2 EXTRACT-CODE             (A1)
  2 EXTRACT-KEY              (A50)
*
  2 REDEFINE EXTRACT-KEY
    3 LETTER-TYPE            (A1)
    3 LETTER-CN              (A6)   /* DW2
    3 LETTER-NAME            (A25)
*
  2 REDEFINE EXTRACT-KEY
    3 DEP-LETTER-TYPE        (A1)
    3 DEP-LETTER-CN          (A6)   /* DW2
    3 DEP-LETTER-NAME        (A25)
*
  2 REDEFINE EXTRACT-KEY
    3 DEP-16-LETTER-TYPE     (A1)
    3 DEP-16-LETTER-CN       (A6)   /* DW2
    3 DEP-16-LETTER-NAME     (A25)
*
  2 EXTRACT-DATA             (A150)
*
  2 REDEFINE EXTRACT-DATA
    3 LET-ADDR1              (A25)
    3 LET-ADDR2              (A25)
    3 LET-ADDR3              (A25)
    3 LET-ZIP                (P9)
    3 LET-ID-NO              (A9)
    3 LET-DOB                (N8)
    3 LET-MEMB               (A25)
    3 LET-WIDOW-STATUS       (A1)
*
  2 REDEFINE EXTRACT-DATA
    3 DEP-LET-ADDR1          (A25)
    3 DEP-LET-ADDR2          (A25)
    3 DEP-LET-ADDR3          (A25)
    3 DEP-LET-ZIP            (P9)
    3 DEP-LET-ID-NO          (A6)
    3 DEP-LET-CHILD-ST       (A1)
    3 DEP-LET-FILLER         (A2)
    3 DEP-LET-DOB            (N8)
    3 DEP-LET-CHILD          (A15)
    3 DEP-LET-MEMB           (A25)
    3 DEP-LET-WIDOW-STATUS   (A1)
*****************************************************************
*          W O R K I N G - S T O R A G E                        *
*****************************************************************
1 W-INDEPENDENT-ITEMS
  2 #CURR-YYYY    (N4)
  2 W-MISSING-TOT            (P7)
  2 CL-SUB                   (I2)
  2 CL-SUB-WRK               (I2)
  2 CLM-TBL-X                (I2)
  2 #I1                      (I2)
  2 #LAST-NAME               (A25)
  2 #FIRST-NAME              (A25)
  2 #EXTRA1                  (A25)
  2 #EXTRA2                  (A25)
  2 CHILD-SUB                (I2)
  2 TODAY-MINUS-45           (N8)
  2 NUM-CHILDREN-18-CNTR     (P3)
  2 YOUNGEST-CHILD-DOB       (N8)
  2 NUM-CHILDREN-16-CNTR     (P3)
  2 PARM-PLUS-3-MTHS         (A10)
  2 REDEFINE PARM-PLUS-3-MTHS
    3 MONTH-PLUS-3-MTHS      (N2)
    3 PP3F1                  (A1)
    3 DAY-PLUS-3-MTHS        (N2)
    3 PPF3F2                 (A1)
    3 YYYY-PLUS-3-MTHS       (N4)
*
  2 PARM-PLUS-6-MTHS         (A10)
  2 REDEFINE PARM-PLUS-6-MTHS
    3 MONTH-PLUS-6-MTHS      (N2)
    3 MP6-FILL1              (A1)
    3 DAY-PLUS-6-MTHS        (N2)
    3 MP6-FILL2              (A1)
    3 YYYY-PLUS-6-MTHS       (N4)
*
  2 #YYYYMMDD    (N8)
  2 REDEFINE  #YYYYMMDD
   3 #YYYYMMDD-A (A8)
  2 REDEFINE  #YYYYMMDD
    3 #YYYY      (N4)
    3 #MM        (N2)
    3 #DD        (N2)
*
1 ABEND-CODE                 (I4)
1 #ID-NUMBER-G               (A6)
1 REDEFINE #ID-NUMBER-G
  2 #ID-NUMBER               (N6)
*
1 SWITCHES
  2 NEED-CLM-WRITE           (L) INIT<FALSE>
  2 VA-DIC-FLAG              (A1)
  2 #ELIGIBLE-DEPN           (L)
  2 #UNSETTLED-POL           (L)
  2 #UNSETTLED-CLAIM         (L)
*
1 ACCUMULATORS
  2 TOTAL-FIRS-IN            (P7)
  2 TOTAL-CLAIMS-IN          (P7)
  2 TOTAL-CLAIMS-OUT         (P7)
  2 CLM-BUCKETS-IN           (P7)
  2 CLM-BUCKETS-OUT          (P7)
  2 CLM-BUCKETS-DROPPED      (P7)
*
1 CHILD-18-X        (I2)
1 CHIL-AGE-18-TBL   (14)
  2 DEP-TBL-NAME         (A25)
  2 DEP-TBL-DATA         (A138)
  2 REDEFINE DEP-TBL-DATA
    3 DEP-TBL-ADDR1          (A25)
    3 DEP-TBL-ADDR2          (A25)
    3 DEP-TBL-ADDR3          (A25)
    3 DEP-TBL-ZIP            (P9)
    3 DEP-TBL-ID-NO          (A6)
    3 DEP-TBL-CHILD-ST       (A1)
    3 DEP-TBL-FILLER         (A2)
    3 DEP-TBL-DOB            (N8)
    3 DEP-TBL-CHILD          (A15)
    3 DEP-TBL-MEMB           (A25)
    3 DEP-TBL-WIDOW-STATUS   (A1)
*
1 CHILD-16-X        (I2)
1 CHIL-AGE-16-TBL   (14)
  2 DEP-16-TBL-NAME         (A25)
  2 DEP-16-TBL-DATA         (A138)
  2 REDEFINE DEP-16-TBL-DATA
    3 DEP-16-TBL-ADDR1       (A25)
    3 DEP-16-TBL-ADDR2       (A25)
    3 DEP-16-TBL-ADDR3       (A25)
    3 DEP-16-TBL-ZIP         (P9)
    3 DEP-16-TBL-ID-NO       (A6)
    3 DEP-16-TBL-CHILD-ST    (A1)
    3 DEP-16-TBL-FILLER      (A2)
    3 DEP-16-TBL-DOB         (N8)
    3 DEP-16-TBL-CHILD       (A15)
    3 DEP-16-TBL-MEMB        (A25)
    3 DEP-16-TBL-WIDOW-STATUS  (A1)
*
1 CLAIM-VALUES    (A21/14)
     INIT (1) <'VA-DIC              V'>
          (2) <'VA-DEA               '>
          (3) <'VA-BA                '>
          (4) <'VA-PA                '>
          (5) <'F.C. SBP            G'>
          (6) <'F.C. RSFPP          G'>
          (7) <'F.C. ARREARS OF PAY G'>
          (8) <'F.C. SBP COST REFUNDG'>
          (9) <'F.C. W-4P FORM      G'>
         (10) <'S.S. MONTHLY BENEFITG'>
         (11) <'S.S. LUMP SUM       G'>
         (12) <'C.S. ANNUITY         '>
         (13) <'C.S. INSURANCE       '>
         (14) <'AFCIA                '>
1 REDEFINE CLAIM-VALUES
  2 CLM-TABLE      (14)
    3 CLM-TBL-LET  (A20)
    3 CLM-TBL-TYPE (A1)
*
1 MISC-CLM-FIX-AREA         (A20)
1 REDEFINE MISC-CLM-FIX-AREA
  2 FILLER                  (A1)
  2 CLM-MINUS-ONE           (A19)
*
1 #WIDOW-DELETE      (L) INIT <FALSE>
1 #DEPN-PURGE-FLAG   (L) INIT <TRUE>
1 #PURGE-DOB-A   (A8)
1 REDEFINE #PURGE-DOB-A
  2 #PURGE-DOB    (N8)
  2 REDEFINE #PURGE-DOB
    3 #PURGE-DOB-YYYY   (N4)
    3 #PURGE-DOB-MMDD   (N4)
1 #PURGE-DATE-A  (A8)
1 REDEFINE #PURGE-DATE-A
  2 #PURGE-DATE   (N8)
  2 REDEFINE #PURGE-DATE
    3 #PURGE-DATE-YYYY   (N4)
    3 #PURGE-DATE-MM     (N2)
    3 #PURGE-DATE-DD     (N2)
1 #REPORT-DATE   (N8)
1 REDEFINE #REPORT-DATE
  2 #REPORT-DATE-MM   (N2)
  2 #REPORT-DATE-DD   (N2)
  2 #REPORT-DATE-YYYY (N4)
1 #DATE-OF-BIRTH      (N8)      /* TT1
1 #DATE-OF-BIRTH-SP   (N8)      /* TT1
1 #SPOUSE-SEX         (A1)      /* TT1
*
*****************************************************************
*          L I N K A G E   S E C T I O N                        *
*****************************************************************
1 PARM-DATE      (A10)
1 REDEFINE PARM-DATE
  2 PARM-MONTH       (A2)
  2 PARM-FILL1       (A1)
  2 PARM-DAY         (A2)
  2 PARM-FILL2       (A1)
  2 PARM-YYYY        (A4)
*
1 FR-V VIEW OF A-FIRS
  2 C*DEPENDENT-TABLE
  2 DEPN-DATE-OF-BIRTH(14)
  2 DEPN-NAME(14)
  2 DEPN-STATUS-CD(14)
  2 C*POLICY-TABLE
  2 POL-NAME(32)
  2 POL-NUMBER(32)
  2 POL-REMARKS(32)
  2 POL-DATE-CLM-FILED(32)
  2 POL-DATE-CLM-SETTLED(32)
 /* END OF VIEW FIRS-RECORD
*
1 CL-V VIEW OF A-CLAIM
  2 DATE-TIME-STAMP
    3 DTS-DATN
    3 DTS-TIMN
  2 C*CLAIM-INFO
  2 LETTER(24)
  2 STATUS(24)
  2 REMARKS(24)
  2 DATE-FILED(24)
  2 DATE-SETTLED(24)
  2 AMOUNT(24)
 /* END OF VIEW CLAIM-RECORD
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
   3 ID-NUMBER-A    (A6)   /* DW2
  2 MEMBER-FLAG
  2 WIDOW-FLAG
  2 NAME
  2 MEMBER-CONTACT-ID                             /* JT1 start
*   2 ADDRESS-CHG-CODE
*   2 ADDRESS-1
*   2 ADDRESS-2
*   2 ADDRESS-3                                   /* JT1 end
  2 DATE-ACCT-UPDATE
  2 REDEFINE DATE-ACCT-UPDATE
    3 #DATE-ACCT-UPDATE-YYYY (N4)
    3 #DATE-ACCT-UPDATE-MM   (N2)
    3 #DATE-ACCT-UPDATE-DD   (N2)
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 #DATE-OF-DEATH-YYYY    (N4)
    3 #DATE-OF-DEATH-MM      (N2)
    3 #DATE-OF-DEATH-DD      (N2)
*   2 ZIP-CODE
  2 NAME-SP
  2 STATUS-CODE-SP
  2 FORMER-SPOUSE-NAME
  2 FORMER-SPOUSE-STATUS
  2 REMARRY-DATE
  2 REMARRY-MEMBER
  2 MIL-SBP-OPT
  2 O-E-DATE
  2 O-E-SBP-OPT
  2 O-E-99-DATE
  2 O-E-99-SBP-OPT
*   2 DATE-OF-BIRTH             /* TT1
*   2 DATE-OF-BIRTH-SP          /* TT1
  2 DATE-OF-DIVORCE
  2 REDEFINE DATE-OF-DIVORCE
    3 #DATE-OF-DIVORCE-YYYY  (N4)
    3 #DATE-OF-DIVORCE-MM    (N2)
    3 #DATE-OF-DIVORCE-DD    (N2)
  2 DEATH-SERVICE-CON
  2 STATUS              (1)
  2 PLAN                (1)
 /* END OF VIEW STATUS-RECORD
*
1 DEL-FIRS-RECORD VIEW OF A-FIRS
  2 ID-NUMBER   (N6)
*
1 DEL-CLM-RECORD VIEW OF A-CLAIM
  2 ID-NUMBER   (N6)
*
1 DEL-NOTES-RECORD VIEW OF A-NOTES
  2 ID-NUMBER   (N6)
*
1 DEL-FM-RECORD VIEW OF A-INVESTMENTS
*
* 1 #INACTIVE-PURGE (L)
* 1 #INACTIVE-PURGE-CNT (P5)
1 #MANUAL-PURGE-CNT (P5)
1 #CLMS-ISN         (P8)
1 #CLMS-FOUND       (L)
1 #FIRS-FOUND       (L)
1 #RECD-FOUND       (L)  INIT<TRUE>
1 #WIDOW-DELETED   (N4)
1 #FIRS-DELETED    (N4)
1 #FM-DELETED      (N4)
1 #CLAIMS-DELETED    (N4)
1 #NOTES-DELETED    (N4)
1 #DISPLAY-ACCT-UPDATE  (N8)
1 REDEFINE #DISPLAY-ACCT-UPDATE
  2 #DISPLAY-ACCT-UPDATE-MM   (N2)
  2 #DISPLAY-ACCT-UPDATE-DD   (N2)
  2 #DISPLAY-ACCT-UPDATE-YYYY (N4)
1 #DISPLAY-DIVORCE  (N8)
1 REDEFINE #DISPLAY-DIVORCE
  2 #DISPLAY-DIVORCE-MM   (N2)
  2 #DISPLAY-DIVORCE-DD   (N2)
  2 #DISPLAY-DIVORCE-YYYY (N4)
1 #DISPLAY-DEATH  (N8)
1 REDEFINE #DISPLAY-DEATH
  2 #DISPLAY-DEATH-MM   (N2)
  2 #DISPLAY-DEATH-DD   (N2)
  2 #DISPLAY-DEATH-YYYY (N4)
*
1 #SBP-OPT   (A1)
*
END-DEFINE
*****************************************************************
*****************************************************************
*          P R O C E D U R E   D I V I S I O N                  *
*****************************************************************
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR       /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'C U R R E N T  R C D' ST-V.ID-NUMBER
*     MOVE +4095 TO ABEND-CODE
*     CALL 'ABEND' USING ABEND-CODE
  #BATCH-LITERAL-1 := ST-V.ID-NUMBER
  INCLUDE GABNDCX1
  STOP                                                    /* SAG RH1 11282005 SS - END
END-ERROR
*
FORMAT PS=55
       LS=133
FORMAT(2) PS=55
          LS=133
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM 5X 'DATE:' *DAT4U
                  5X 'TIME:' *TIMX
*
WRITE (2) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(2) /
                     40T '        WIDOW PURGE REPORT               '
                     91T #REPORT-DATE(EM=99/99/9999) //
                     8T 'LAST' 20T 'ID' 30T 'WIDOW' 50T 'WIDOW'
                     78T 'MEMBER' 95T 'MEMBER' /
                     7T 'UPDATE' 18T 'NUMBER' 27T 'DEATH DATE'
                     50T 'NAME' 75T 'DEATH DATE' 95T 'NAME' //
READ WORK 3 ONCE RECORD PARM-DATE   /* DATE (09/01/1999)
*
IF PARM-DATE NE MASK(MM'/01/'YYYY)
*   WRITE 'PARM-DATE NOT VALID "MM/01/YYYY"'       /* SAG 12092005 RH1 SS - BEGIN
*   MOVE +10 TO ABEND-CODE
*   CALL 'ABEND' ABEND-CODE
  #BATCH-LITERAL-1 := 'PARM-DATE'
  #BATCH-LITERAL-4 := 'PARM-DATE NOT VALID "MM/01/YYYY"'
  INCLUDE GABNDCX6
  TERMINATE                                        /* SAG 12092005 RH1 SS - END
END-IF
*
PERFORM S0250-WIDOW-INIT
*
READ ST-V PHYSICAL
  IF ID-NUMBER LE 1900
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  MOVE MIL-SBP-OPT TO #SBP-OPT
  IF   (O-E-99-DATE NE 0)        /* use Open Enroll option  DW1
    MOVE O-E-99-SBP-OPT TO #SBP-OPT
  ELSE
    IF   (O-E-DATE NE 0)          /* use Open Enroll option  DW1
      MOVE O-E-SBP-OPT TO #SBP-OPT
    END-IF
  END-IF
*
RESET PDA-CONTACT #DATE-OF-BIRTH #DATE-OF-BIRTH-SP #SPOUSE-SEX
P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
CALLNAT 'CONTS001' PDA-CONTACT #DATE-OF-BIRTH-SP #SPOUSE-SEX
#DATE-OF-BIRTH   := P-DATE-OF-BIRTH
*
  IF WIDOW-FLAG NE 'Y'
    PERFORM S2001-CHECK-AGE-60-LTR-MBR
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  MOVE MIL-SBP-OPT TO #SBP-OPT
  IF   (O-E-99-DATE NE 0)        /* lived past o-e date + 1yr  DW1
   AND (O-E-99-DATE LT DATE-OF-DEATH + 10000)
    MOVE O-E-99-SBP-OPT TO #SBP-OPT
  ELSE
    IF   (O-E-DATE NE 0)        /* lived past o-e date + 1yr  DW1
     AND (O-E-DATE LT DATE-OF-DEATH + 10000)
      MOVE O-E-SBP-OPT TO #SBP-OPT
    END-IF
  END-IF
*
  MOVE FALSE TO #WIDOW-DELETE
  PERFORM S0500-WIDOW-PROCESS
*
  move false to #widow-delete   /* Stopped deleting recoeds 3/13/2007  /* DW3
*
  IF #WIDOW-DELETE
*   IF #INACTIVE-PURGE
*     ADD 1 TO #INACTIVE-PURGE-CNT
*     WRITE ID-NUMBER ' INACTIVE PURGE'
*   END-IF
*
  MOVE #DATE-ACCT-UPDATE-YYYY   TO #DISPLAY-ACCT-UPDATE-YYYY
  MOVE #DATE-OF-DIVORCE-YYYY    TO #DISPLAY-DIVORCE-YYYY
  MOVE #DATE-OF-DEATH-YYYY      TO #DISPLAY-DEATH-YYYY
  MOVE #DATE-ACCT-UPDATE-MM     TO #DISPLAY-ACCT-UPDATE-MM
  MOVE #DATE-OF-DIVORCE-MM      TO #DISPLAY-DIVORCE-MM
  MOVE #DATE-OF-DEATH-MM        TO #DISPLAY-DEATH-MM
  MOVE #DATE-ACCT-UPDATE-DD     TO #DISPLAY-ACCT-UPDATE-DD
  MOVE #DATE-OF-DIVORCE-DD      TO #DISPLAY-DIVORCE-DD
  MOVE #DATE-OF-DEATH-DD        TO #DISPLAY-DEATH-DD
*
    WRITE (2) 5T #DISPLAY-ACCT-UPDATE  (EM=99/99/9999)
               18T ID-NUMBER           (EM=Z99999)
               27T #DISPLAY-DIVORCE    (EM=99/99/9999)
               45T NAME-SP
               75T #DISPLAY-DEATH      (EM=99/99/9999)
               90T NAME
    PERFORM DELETE-ASSO-RECD
    ADD 1 TO #WIDOW-DELETED
    DELETE (0417)
  END-IF
  END TRANSACTION
END-READ
*
WRITE (2) 20T 'PURGE STATISTICS'/
    20T 'NUMBER OF WIDOWS DELETED -'48T #WIDOW-DELETED (EM=ZZZ9)
WRITE 'ATHPM50A FIRS UPDATE STATISTICS'
  /
  / 'FIRS RECS IN         =' TOTAL-FIRS-IN
  / 'CLAIM RECS IN        =' TOTAL-CLAIMS-IN
  / 'CLAIM RECS UPDATED   =' TOTAL-CLAIMS-OUT
  / 'CLAIM BUCKETS IN     =' CLM-BUCKETS-IN
  / 'CLAIM BUCKETS DROPPED=' CLM-BUCKETS-DROPPED
  / 'CLAIM BUCKETS OUT    =' CLM-BUCKETS-OUT
  / 'PURGE STATISTICS'
  / 'STATUS RECORDS DELETED=' #WIDOW-DELETED
* / 'ST RECS DEL (INACTIVE)=' #INACTIVE-PURGE-CNT
  / 'ST RECS DEL (MANUALLY)=' #MANUAL-PURGE-CNT
  / 'FIRS RECORDS DELETED=' #FIRS-DELETED
  / 'CLAIMS RECORDS DELETED=' #CLAIMS-DELETED
  / 'NOTES DELETED=' #NOTES-DELETED
  / 'FM"S DELETED=' #FM-DELETED
*
*****************************************************************
*         0 2 5 0 - W I D O W - I N I T                         *
*****************************************************************
DEFINE SUBROUTINE S0250-WIDOW-INIT
*
COMPRESS PARM-YYYY PARM-MONTH PARM-DAY INTO #YYYYMMDD-A LEAVING NO
IF #MM = 1
  COMPUTE #CURR-YYYY = #YYYY - 1
ELSE
  MOVE #YYYY TO #CURR-YYYY
END-IF
*
COMPRESS PARM-YYYY PARM-MONTH PARM-DAY INTO #YYYYMMDD-A LEAVING NO
SUBTRACT 2 FROM #MM
MOVE     15 TO #DD
*
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO TODAY-MINUS-45
*
COMPRESS PARM-YYYY PARM-MONTH PARM-DAY INTO #PURGE-DATE-A
      LEAVING NO
*
MOVE #PURGE-DATE-MM TO #REPORT-DATE-MM
MOVE #PURGE-DATE-DD TO #REPORT-DATE-DD
MOVE #PURGE-DATE-YYYY TO #REPORT-DATE-YYYY
* SUBTRACT 1 FROM #PURGE-DATE-YYYY  /* CHGD FROM 13 TO 1 MONTH 9/29/2000
 SUBTRACT 3 FROM #PURGE-DATE-MM /* chged fron 1 to 3 5/31/2001 EMD
*
IF #PURGE-DATE-MM LT 1
  ADD 12 TO #PURGE-DATE-MM
  SUBTRACT 1 FROM #PURGE-DATE-YYYY
END-IF
WRITE 'PURGE-DATE = ' #PURGE-DATE-A
*
MOVE #PURGE-DATE-A TO #PURGE-DOB-A
SUBTRACT 31 FROM #PURGE-DOB-YYYY     /* DEPN UNDER 32 MAY HAVE BENES
*
MOVE PARM-DATE TO PARM-PLUS-3-MTHS
ADD +3 TO MONTH-PLUS-3-MTHS
*
IF MONTH-PLUS-3-MTHS GT +12
  ADD +1 TO YYYY-PLUS-3-MTHS
  SUBTRACT +12 FROM MONTH-PLUS-3-MTHS
END-IF
*
MOVE PARM-PLUS-3-MTHS TO PARM-PLUS-6-MTHS
ADD +3 TO MONTH-PLUS-6-MTHS
*
IF MONTH-PLUS-6-MTHS GT +12
  ADD +1 TO YYYY-PLUS-6-MTHS
  SUBTRACT +12 FROM MONTH-PLUS-6-MTHS
END-IF
*
END-SUBROUTINE  /* (0510)
*
*****************************************************************
*            0 4 6 0 - W R I T E - E X T R A C T                *
*****************************************************************
DEFINE SUBROUTINE S0460-WRITE-EXTRACT
*
WRITE WORK 1 EXTRACT-RECORD
RESET EXTRACT-RECORD
*
END-SUBROUTINE  /* (0568)
*
*****************************************************************
*            0 5 0 0 - W I D O W - P R O C E S S                *
*****************************************************************
DEFINE SUBROUTINE S0500-WIDOW-PROCESS
*
MOVE +1 TO CHILD-18-X CHILD-16-X
MOVE 0 TO YOUNGEST-CHILD-DOB
*
IF ST-V.DEATH-SERVICE-CON = 'Y' OR = 'C'   /* DW1
  MOVE 'G' TO VA-DIC-FLAG
ELSE
  IF ST-V.DEATH-SERVICE-CON = 'N' OR = 'D'
    MOVE 'D' TO VA-DIC-FLAG
  ELSE
    MOVE 'U' TO VA-DIC-FLAG
  END-IF
END-IF
*
PERFORM S2000-CHECK-AGE-60-LETTER
MOVE ST-V.ID-NUMBER TO #ID-NUMBER
*
MOVE TRUE TO #CLMS-FOUND
FIND (1) CL-V WITH ID-NUMBER = ST-V.ID-NUMBER
  IF NO RECORDS FOUND
    RESET #CLMS-FOUND
  END-NOREC
*
  ADD +1 TO TOTAL-CLAIMS-IN
  MOVE *ISN TO #CLMS-ISN
END-FIND
*
*  * * *THIS LOGIC IS ALSO IN ATHPAFST
MOVE TRUE TO #FIRS-FOUND
FIND (1) FR-V WITH WIDOW-KEY = #ID-NUMBER-G
  IF NO RECORDS FOUND
    RESET #FIRS-FOUND
  END-NOREC
*
  ADD +1 TO TOTAL-FIRS-IN
END-FIND
*
IF   (ST-V.STATUS-CODE-SP = 'P')   /* PURGE WIDOW RECORDS
* AND (ST-V.MEMBER-FLAG NE 'Y')  /* ALREADY MEMBER PURGED BY ATHP05P0
* above replaced with below 10/96 as per edna dotson
* restored 7/1997 (never remove again)
* ABOVE REPLACED BY BELOW 9/12/2000 AS PER EMD
 AND (ST-V.#DATE-OF-DEATH-YYYY LT #CURR-YYYY)
  MOVE TRUE TO #WIDOW-DELETE
  WRITE 'MANUAL STATUS "P" PURGE ' ST-V.ID-NUMBER ST-V.DATE-OF-DEATH
                                   #CURR-YYYY
  ADD 1 TO #MANUAL-PURGE-CNT
  ESCAPE ROUTINE
END-IF
*
* RESET #INACTIVE-PURGE     /* REMOVED 5/30/91 AS PER EDNA DOTSON
* IF   (STATUS-RECORD.DATE-OF-BIRTH-SP + 910000 LT *DATN)    /* OVER 91
*  AND (STATUS-RECORD.DATE-ACCT-UPDATE +  20000 LT *DATN) /* NO ACT 2 Y
*  AND (STATUS-RECORD.ADDRESS-1 = ' ')                    /* NO ADDR
*  AND (STATUS-RECORD.ADDRESS-2 = ' ')
*  AND (STATUS-RECORD.ADDRESS-3 = ' ')
*  AND (STATUS-RECORD.ZIP-CODE  = 0)
*   MOVE TRUE TO #INACTIVE-PURGE
*   WRITE 'FOUND INACTIVE-PURGE TEST ' STATUS-RECORD.ID-NUMBER
* END-IF
*
SEPARATE ST-V.NAME-SP LEFT INTO #LAST-NAME #FIRST-NAME #EXTRA1 #EXTRA2
                                      WITH DELIMITER ','
*
IF   ((ST-V.NAME-SP NE ' ')         /* SPOUSE STILL EXISTS
 AND (#FIRST-NAME NE 'NONE')        /* INDICATES NO SPOUSE
 AND (ST-V.STATUS-CODE-SP NE 'D')
 AND (ST-V.DATE-OF-DIVORCE = 0))
* AND (NOT #INACTIVE-PURGE)                 /* BELOW CHGD 9/29/2000
 OR  (ST-V.DATE-ACCT-UPDATE GE #PURGE-DATE) /* CHGED FROM 13 TO 1 MONS
 OR  (ST-V.MEMBER-FLAG NE ' ')  /* not yet member purged by athp05p0
* OR  (ST-V.ID-NUMBER = 14472) /* NEVER DEL EMD 6/1997)/REMOVED 10/5/00
  IGNORE                      /* DO NOT DELETE THIS RECORD
ELSE
  PERFORM CHECK-UNSETTLED
*
  IF  #ELIGIBLE-DEPN
   OR #UNSETTLED-CLAIM
   OR #UNSETTLED-POL
    IGNORE
  ELSE
    MOVE TRUE TO #WIDOW-DELETE
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF   (ST-V.PLAN (1) = MASK (..'W'))  /* Inactive widow client
 AND (ST-V.STATUS (1) NE 'D')
 AND (ST-V.DATE-ACCT-UPDATE LT #PURGE-DATE)
  PERFORM CHECK-UNSETTLED
  IF  #ELIGIBLE-DEPN
   OR #UNSETTLED-CLAIM
   OR #UNSETTLED-POL
    IGNORE
  ELSE
    MOVE TRUE TO #WIDOW-DELETE
    ESCAPE ROUTINE
  END-IF
END-IF
*
IF   #FIRS-FOUND
 AND (FR-V.C*DEPENDENT-TABLE GT 0)
  FOR CHILD-SUB +1 TO FR-V.C*DEPENDENT-TABLE
    IF FR-V.DEPN-DATE-OF-BIRTH (CHILD-SUB) = 0
      ESCAPE TOP
    END-IF
*
    PERFORM S2500-PROCESS-DEP-BUCKETS
  END-FOR
END-IF
*
IF    #CLMS-FOUND
 AND (CL-V.C*CLAIM-INFO GT 0)
  RESET NEED-CLM-WRITE
  GET CL-V #CLMS-ISN
*
  FOR CL-SUB FROM +1 TO CL-V.C*CLAIM-INFO
    PERFORM S1300-PROCESS-CLM-BUCKETS
  END-FOR
*
  IF NEED-CLM-WRITE
    MOVE *DATN TO CL-V.DTS-DATN
    MOVE *TIMN TO CL-V.DTS-TIMN
    UPDATE (0693)
    ADD +1 TO TOTAL-CLAIMS-OUT
    END TRANSACTION
  ELSE
    END TRANSACTION
  END-IF
END-IF
*
IF   (CHILD-18-X GT +1)
 AND (VA-DIC-FLAG = 'G' OR = 'U')
  PERFORM S3000-WRITE-CHILD-18-LETTER
END-IF
*
IF   (CHILD-16-X GT +1)
 AND (VA-DIC-FLAG = 'G' OR = 'U')
  PERFORM S3500-WRITE-CHILD-16-LETTER
END-IF
*
*
END-SUBROUTINE  /* (0578)
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-UNSETTLED
* * * * * * * * * * * * * * * * *
*
RESET #ELIGIBLE-DEPN          /* DETERMINE IF ANY DEPENDENTS
RESET #UNSETTLED-POL          /* DETERMINE IF ANY UNSETTLED POLS
IF #FIRS-FOUND
  FOR #I1 1 TO FR-V.C*DEPENDENT-TABLE /* ELIGIBLE FOR BENEFITS
    IF  (FR-V.DEPN-STATUS-CD (#I1) = 'I' OR = 'J' OR = 'K' OR = 'L')
     OR (FR-V.DEPN-DATE-OF-BIRTH (#I1) GE #PURGE-DOB)
      MOVE TRUE TO #ELIGIBLE-DEPN
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  FOR #I1 1 TO FR-V.C*POLICY-TABLE
    IF   (FR-V.POL-DATE-CLM-FILED (#I1) NE 0)
     AND (FR-V.POL-DATE-CLM-SETTLED (#I1) = 0)
      MOVE TRUE TO #UNSETTLED-POL
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-IF
*
RESET #UNSETTLED-CLAIM        /* DETERMINE IF ANY UNSETTLED CLMS
IF #CLMS-FOUND
  FOR #I1 1 TO CL-V.C*CLAIM-INFO
    IF   (CL-V.DATE-FILED (#I1) NE 0)
     AND (CL-V.DATE-SETTLED (#I1) = 0)
     AND (NOT (CL-V.STATUS (#I1) = 'G' OR = 'D'))
      MOVE TRUE TO #UNSETTLED-CLAIM
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-IF
*
END-SUBROUTINE
*
*****************************************************************
*            2 5 0 0 - P R O C E S S - D E P - B U C K E T S    *
*****************************************************************
DEFINE SUBROUTINE S2500-PROCESS-DEP-BUCKETS
*
*****************************************************************
*  THIS CHECKS TO SEE IF ANY CHILDREN WILL                      *
*  REACH AGE 18 OR 16 IN 6 MONTHS - ONLY PERFORMED IF BIRTH DATE*
*****************************************************************
*
ADD +180000 FR-V.DEPN-DATE-OF-BIRTH (CHILD-SUB)
    GIVING #YYYYMMDD
*
  RESET #PDA-ADDRESS                                         /* JT1 start 
  MOVE 'P' TO #ADD-TYPE                                   
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS                            /* JT1 end    
*
IF (MONTH-PLUS-3-MTHS = #MM) AND (YYYY-PLUS-3-MTHS = #YYYY)
  MOVE ST-V.NAME-SP   TO DEP-TBL-NAME (CHILD-18-X)
  MOVE #ADD-LINE-1    TO DEP-TBL-ADDR1 (CHILD-18-X)              /* JT1 start
  MOVE #ADD-LINE-2    TO DEP-TBL-ADDR2 (CHILD-18-X)
  MOVE #ADD-LINE-3    TO DEP-TBL-ADDR3 (CHILD-18-X)
  MOVE #ADD-ZIP-CODE  TO DEP-TBL-ZIP (CHILD-18-X)
*   MOVE ST-V.ADDRESS-1 TO DEP-TBL-ADDR1 (CHILD-18-X)
*   MOVE ST-V.ADDRESS-2 TO DEP-TBL-ADDR2 (CHILD-18-X)
*   MOVE ST-V.ADDRESS-3 TO DEP-TBL-ADDR3 (CHILD-18-X)
*   MOVE ST-V.ZIP-CODE  TO DEP-TBL-ZIP (CHILD-18-X)              /* JT1 end
  MOVE EDITED ST-V.ID-NUMBER (EM=999999)
                               TO DEP-TBL-ID-NO (CHILD-18-X)
  MOVE ST-V.NAME      TO DEP-TBL-MEMB (CHILD-18-X)
  MOVE ST-V.STATUS-CODE-SP TO DEP-TBL-WIDOW-STATUS(CHILD-18-X)
  MOVE FR-V.DEPN-NAME(CHILD-SUB)
                               TO DEP-TBL-CHILD (CHILD-18-X)
  MOVE FR-V.DEPN-DATE-OF-BIRTH(CHILD-SUB)
                               TO DEP-TBL-DOB   (CHILD-18-X)
  MOVE FR-V.DEPN-STATUS-CD (CHILD-SUB)
                               TO DEP-TBL-CHILD-ST (CHILD-18-X)
  ADD +1 TO CHILD-18-X
END-IF
*
ADD +160000 FR-V.DEPN-DATE-OF-BIRTH (CHILD-SUB)
    GIVING #YYYYMMDD
*
IF (MONTH-PLUS-6-MTHS = #MM) AND (YYYY-PLUS-6-MTHS = #YYYY)
  MOVE ST-V.NAME-SP   TO DEP-16-TBL-NAME (CHILD-16-X)
  MOVE #ADD-LINE-1    TO DEP-16-TBL-ADDR1 (CHILD-16-X)           /* JT1 start
  MOVE #ADD-LINE-2    TO DEP-16-TBL-ADDR2 (CHILD-16-X)
  MOVE #ADD-LINE-3    TO DEP-16-TBL-ADDR3 (CHILD-16-X)
  MOVE #ADD-ZIP-CODE  TO DEP-16-TBL-ZIP (CHILD-16-X)
*   MOVE ST-V.ADDRESS-1 TO DEP-16-TBL-ADDR1 (CHILD-16-X)
*   MOVE ST-V.ADDRESS-2 TO DEP-16-TBL-ADDR2 (CHILD-16-X)
*   MOVE ST-V.ADDRESS-3 TO DEP-16-TBL-ADDR3 (CHILD-16-X)
*   MOVE ST-V.ZIP-CODE  TO DEP-16-TBL-ZIP (CHILD-16-X)           /* JT1 end
  MOVE EDITED ST-V.ID-NUMBER (EM=999999)
                               TO DEP-16-TBL-ID-NO (CHILD-16-X)
  MOVE ST-V.NAME      TO DEP-16-TBL-MEMB (CHILD-16-X)
  MOVE ST-V.STATUS-CODE-SP TO
                        DEP-16-TBL-WIDOW-STATUS (CHILD-16-X)
  MOVE FR-V.DEPN-NAME(CHILD-SUB)
                               TO DEP-16-TBL-CHILD (CHILD-16-X)
  MOVE FR-V.DEPN-DATE-OF-BIRTH(CHILD-SUB)
                               TO DEP-16-TBL-DOB   (CHILD-16-X)
  MOVE FR-V.DEPN-STATUS-CD (CHILD-SUB)
                               TO DEP-16-TBL-CHILD-ST (CHILD-18-X)
  ADD +1 TO CHILD-16-X
END-IF
*
IF FR-V.DEPN-DATE-OF-BIRTH(CHILD-SUB) GT YOUNGEST-CHILD-DOB
  MOVE FR-V.DEPN-DATE-OF-BIRTH(CHILD-SUB) TO YOUNGEST-CHILD-DOB
END-IF
*
END-SUBROUTINE  /* (0763)
*
*****************************************************************
*            1 3 0 0 - P R O C E S S - C L M - B U C K E T S    *
*****************************************************************
DEFINE SUBROUTINE S1300-PROCESS-CLM-BUCKETS
*
ADD +1 TO CLM-BUCKETS-IN
*
IF (CL-V.DATE-FILED   (CL-SUB) = 0) AND
   (CL-V.DATE-SETTLED (CL-SUB) = 0) AND
   (CL-V.STATUS       (CL-SUB) = ' ') AND
   (CL-V.AMOUNT       (CL-SUB) = 0) AND
   (CL-V.REMARKS      (CL-SUB) = ' ') AND
   (ST-V.DATE-OF-DEATH LT TODAY-MINUS-45)
   WRITE ST-V.ID-NUMBER 4X CL-V.LETTER (CL-SUB)
    ADD +1 TO CLM-BUCKETS-DROPPED
    RESET CL-V.LETTER (CL-SUB)
    MOVE TRUE TO NEED-CLM-WRITE
    IF CL-SUB LT CL-V.C*CLAIM-INFO
      PERFORM S1380-COMPRESS-CLAIMS
    END-IF
    SUBTRACT +1 FROM CL-V.C*CLAIM-INFO
    SUBTRACT +1 FROM CL-SUB
    ESCAPE ROUTINE
END-IF
*
ADD +1 TO CLM-BUCKETS-OUT
*
END-SUBROUTINE  /* (0837)
*
*****************************************************************
*            1 3 8 0 - C O M P R E S S - C L A I M S            *
*****************************************************************
DEFINE SUBROUTINE S1380-COMPRESS-CLAIMS
*
FOR CL-SUB-WRK FROM CL-SUB THRU CL-V.C*CLAIM-INFO
  IF CL-SUB-WRK = CL-V.C*CLAIM-INFO
    RESET CL-V.LETTER (CL-SUB-WRK)
          CL-V.STATUS (CL-SUB-WRK)
          CL-V.REMARKS (CL-SUB-WRK)
          CL-V.DATE-FILED (CL-SUB-WRK)
          CL-V.DATE-SETTLED (CL-SUB-WRK)
          CL-V.AMOUNT (CL-SUB-WRK)
    ESCAPE ROUTINE
  END-IF
*
  MOVE CL-V.LETTER (CL-SUB-WRK + 1)
    TO CL-V.LETTER (CL-SUB-WRK)
  MOVE CL-V.STATUS (CL-SUB-WRK + 1)
    TO CL-V.STATUS (CL-SUB-WRK)
  MOVE CL-V.REMARKS (CL-SUB-WRK + 1)
    TO CL-V.REMARKS (CL-SUB-WRK)
  MOVE CL-V.DATE-FILED (CL-SUB-WRK + 1)
    TO CL-V.DATE-FILED (CL-SUB-WRK)
  MOVE CL-V.DATE-SETTLED (CL-SUB-WRK + 1)
    TO CL-V.DATE-SETTLED (CL-SUB-WRK)
  MOVE CL-V.AMOUNT (CL-SUB-WRK + 1)
    TO CL-V.AMOUNT (CL-SUB-WRK)
END-FOR
*
END-SUBROUTINE  /* (0866)
*
*****************************************************************
*     2 0 0 0 - C H E C K - A G E - 6 0 - L E T T E R           *
*****************************************************************
DEFINE SUBROUTINE S2000-CHECK-AGE-60-LETTER
*
IF   (ST-V.STATUS-CODE-SP   NE 'D')
 AND (#DATE-OF-BIRTH-SP NE 0)                          /* TT1
*  AND (ST-V.REMARRY-DATE     = 0)
*  AND (ST-V.RAMERRY-MEMBER   = 0)  REMVD 01/10/2000 PER EMD
   AND #ADD-LINE-1 NE ' '                                      /* JT1 start
   AND F-UNDEL-ADDRESS (< ST-V.MEMBER-CONTACT-ID >) 
*  AND (ST-V.ADDRESS-1 NE ' ')
*  AND (ST-V.ADDRESS-CHG-CODE NE 'U')
*  AND (ST-V.ADDRESS-CHG-CODE NE 'V')                          /* JT1 end
*
  IF ST-V.STATUS-CODE-SP EQ 'R'
   AND ST-V.REMARRY-DATE < #DATE-OF-BIRTH-SP + +600000          /* TT1
     IGNORE
  ELSE
     COMPUTE #YYYYMMDD = #DATE-OF-BIRTH-SP + +600000             /* TT1
     IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  WIDOW TICKLER LETTER TYPES- AGE 60 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
       MOVE 'A'              TO EXTRACT-CODE
       MOVE ST-V.NAME-SP   TO LETTER-NAME
       MOVE #ADD-LINE-1    TO LET-ADDR1                           /* JT1 start
       MOVE #ADD-LINE-2    TO LET-ADDR2
       MOVE #ADD-LINE-3    TO LET-ADDR3
       MOVE #ADD-ZIP-CODE  TO LET-ZIP      
*        MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*        MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*        MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*        MOVE ST-V.ZIP-CODE  TO LET-ZIP                          /* JT1 end
       MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
       MOVE #DATE-OF-BIRTH-SP TO LET-DOB                         /* TT1
       MOVE ST-V.NAME      TO LET-MEMB
       MOVE ST-V.STATUS-CODE-SP TO LET-WIDOW-STATUS
*
       IF #SBP-OPT = 'A' OR = 'B'    /* DW1
         MOVE 'A' TO LETTER-TYPE
       ELSE
         IF #SBP-OPT = ' '
           MOVE 'C' TO  LETTER-TYPE
         ELSE
           MOVE 'B' TO LETTER-TYPE
         END-IF
       END-IF
*
       MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
       PERFORM S0460-WRITE-EXTRACT
     ELSE
       COMPUTE #YYYYMMDD = #DATE-OF-BIRTH-SP + +620000           /* TT1
       IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  WIDOW TICKLER LETTER TYPES- AGE 62 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
         MOVE 'E'              TO EXTRACT-CODE
         MOVE ST-V.NAME-SP   TO LETTER-NAME
         MOVE #ADD-LINE-1    TO LET-ADDR1                        /* JT1 start 
         MOVE #ADD-LINE-2    TO LET-ADDR2
         MOVE #ADD-LINE-3    TO LET-ADDR3
         MOVE #ADD-ZIP-CODE  TO LET-ZIP
*          MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*          MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*          MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*          MOVE ST-V.ZIP-CODE  TO LET-ZIP                        /* JT1 end 
         MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
         MOVE #DATE-OF-BIRTH-SP TO LET-DOB                       /* TT1
         MOVE ST-V.NAME      TO LET-MEMB
         MOVE ST-V.STATUS-CODE-SP TO LET-WIDOW-STATUS
*
         IF #SBP-OPT = 'A' OR = 'B'    /* DW1
           MOVE 'A' TO LETTER-TYPE
         ELSE
           IF #SBP-OPT = ' '
             MOVE 'C' TO  LETTER-TYPE
           ELSE
             MOVE 'B' TO LETTER-TYPE
           END-IF
         END-IF
*
         MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
         PERFORM S0460-WRITE-EXTRACT
       ELSE
         COMPUTE #YYYYMMDD = #DATE-OF-BIRTH-SP + +650000            /* TT1
         IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  WIDOW TICKLER LETTER TYPES- AGE 65 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
           MOVE 'G'              TO EXTRACT-CODE
           MOVE ST-V.NAME-SP   TO LETTER-NAME
           MOVE #ADD-LINE-1    TO LET-ADDR1                        /* JT1 start 
           MOVE #ADD-LINE-2    TO LET-ADDR2
           MOVE #ADD-LINE-3    TO LET-ADDR3
           MOVE #ADD-ZIP-CODE  TO LET-ZIP
*          MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*          MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*          MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*          MOVE ST-V.ZIP-CODE  TO LET-ZIP                         /* JT1 end
           MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
           MOVE #DATE-OF-BIRTH-SP TO LET-DOB                  /* TT1
           MOVE ST-V.NAME      TO LET-MEMB
           MOVE ST-V.STATUS-CODE-SP TO LET-WIDOW-STATUS
*
           IF #SBP-OPT = 'A' OR = 'B'    /* DW1
             MOVE 'A' TO LETTER-TYPE
           ELSE
             IF #SBP-OPT = ' '
               MOVE 'C' TO  LETTER-TYPE
             ELSE
               MOVE 'B' TO LETTER-TYPE
             END-IF
           END-IF
*
           MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
           PERFORM S0460-WRITE-EXTRACT
         END-IF
       END-IF
     END-IF
  END-IF
END-IF
*
END-SUBROUTINE  /* (0898)
*
*****************************************************************
*     2 0 0 1 - C H E C K - A G E - 6 0 - L t r - m b r         *
*****************************************************************
DEFINE SUBROUTINE S2001-CHECK-AGE-60-LTR-MBR
*
IF   (ST-V.STATUS-CODE-SP   =  'D')
 AND (#DATE-OF-BIRTH    NE 0)                                 /* TT1
  COMPUTE #YYYYMMDD = #DATE-OF-BIRTH + +600000                /* TT1
  IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  mbr   TICKLER LETTER TYPES- AGE 60 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
    MOVE 'B'              TO EXTRACT-CODE
    MOVE ST-V.NAME      TO LETTER-NAME
    MOVE #ADD-LINE-1    TO LET-ADDR1                        /* JT1 start 
    MOVE #ADD-LINE-2    TO LET-ADDR2
    MOVE #ADD-LINE-3    TO LET-ADDR3
    MOVE #ADD-ZIP-CODE  TO LET-ZIP
*   MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*   MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*   MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*   MOVE ST-V.ZIP-CODE  TO LET-ZIP                        /* JT1 end    
    MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
    MOVE #DATE-OF-BIRTH    TO LET-DOB                      /* TT1
    MOVE ST-V.NAME-SP   TO LET-MEMB
    RESET                  LET-WIDOW-STATUS
*
    IF #SBP-OPT = 'A' OR = 'B'    /*   DW1
      MOVE 'A' TO LETTER-TYPE
    ELSE
      IF #SBP-OPT = ' '
        MOVE 'C' TO  LETTER-TYPE
      ELSE
        MOVE 'B' TO LETTER-TYPE
      END-IF
    END-IF
*
    MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
    PERFORM S0460-WRITE-EXTRACT
  ELSE
    COMPUTE #YYYYMMDD = #DATE-OF-BIRTH + +620000              /* TT1
    IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  MBR   TICKLER LETTER TYPES- AGE 62 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
      MOVE 'F'            TO EXTRACT-CODE
      MOVE ST-V.NAME      TO LETTER-NAME
      MOVE #ADD-LINE-1    TO LET-ADDR1                        /* JT1 start 
      MOVE #ADD-LINE-2    TO LET-ADDR2
      MOVE #ADD-LINE-3    TO LET-ADDR3
      MOVE #ADD-ZIP-CODE  TO LET-ZIP
*     MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*     MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*     MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*     MOVE ST-V.ZIP-CODE  TO LET-ZIP                         /* JT1 end 
      MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
      MOVE #DATE-OF-BIRTH    TO LET-DOB                       /* TT1
      MOVE ST-V.NAME-SP   TO LET-MEMB
      RESET                  LET-WIDOW-STATUS
*
      IF #SBP-OPT = 'A' OR = 'B'    /*   DW1
        MOVE 'A' TO LETTER-TYPE
      ELSE
        IF #SBP-OPT = ' '
          MOVE 'C' TO  LETTER-TYPE
        ELSE
          MOVE 'B' TO LETTER-TYPE
        END-IF
      END-IF
*
     MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
      PERFORM S0460-WRITE-EXTRACT
    ELSE
      COMPUTE #YYYYMMDD = #DATE-OF-BIRTH + +650000               /* TT1
      IF MONTH-PLUS-3-MTHS = #MM AND YYYY-PLUS-3-MTHS = #YYYY
**    *****************************************************
**    *  MBR   TICKLER LETTER TYPES- AGE 65 IN 3 MTHS     *
**    *     A - HAVE SBP                                  *
**    *     B - NO SBP                                    *
**    *     C - UNKNOWN SBP                               *
**    *****************************************************
*
        MOVE 'H'            TO EXTRACT-CODE
        MOVE ST-V.NAME      TO LETTER-NAME
        MOVE #ADD-LINE-1    TO LET-ADDR1                        /* JT1 start 
        MOVE #ADD-LINE-2    TO LET-ADDR2
        MOVE #ADD-LINE-3    TO LET-ADDR3
        MOVE #ADD-ZIP-CODE  TO LET-ZIP
*       MOVE ST-V.ADDRESS-1 TO LET-ADDR1
*       MOVE ST-V.ADDRESS-2 TO LET-ADDR2
*       MOVE ST-V.ADDRESS-3 TO LET-ADDR3
*       MOVE ST-V.ZIP-CODE  TO LET-ZIP                         /* JT1 end 
        MOVE EDITED ST-V.ID-NUMBER (EM=999999) TO LET-ID-NO
        MOVE #DATE-OF-BIRTH    TO LET-DOB                     /* TT1
        MOVE ST-V.NAME-SP   TO LET-MEMB
        RESET                  LET-WIDOW-STATUS
*
        IF #SBP-OPT = 'A' OR = 'B'    /*   DW1
          MOVE 'A' TO LETTER-TYPE
        ELSE
          IF #SBP-OPT = ' '
            MOVE 'C' TO  LETTER-TYPE
          ELSE
            MOVE 'B' TO LETTER-TYPE
          END-IF
        END-IF
*
        MOVE ST-V.ID-NUMBER-A TO LETTER-CN   /* DW2
        PERFORM S0460-WRITE-EXTRACT
      END-IF
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE  /* (1035)
*
*****************************************************************
*       3 0 0 0 - W R I T E - C H I L D - 1 8 - L E T T E R     *
*****************************************************************
DEFINE SUBROUTINE S3000-WRITE-CHILD-18-LETTER
*
SUBTRACT +1 FROM CHILD-18-X
MOVE CHILD-18-X TO NUM-CHILDREN-18-CNTR
*
FOR CHILD-18-X FROM +1 TO NUM-CHILDREN-18-CNTR
********************************************************
*  WIDOW TICKLER LETTER TYPES - CHILD AGE 18 IN 6 MTHS *
*  A - VA-DIC GRANTED                                  *
*  B - VA-DIC DENIED                                   *
*  C - VA-DIC UNKNOWN                                  *
********************************************************
*
  MOVE 'C' TO EXTRACT-CODE
*
  IF VA-DIC-FLAG = 'D'
    MOVE 'B' TO DEP-LETTER-TYPE
  ELSE
    IF VA-DIC-FLAG = 'G'
      MOVE 'A' TO DEP-LETTER-TYPE
    ELSE
      MOVE 'C' TO DEP-LETTER-TYPE
    END-IF
  END-IF
*
  MOVE DEP-TBL-NAME(CHILD-18-X) TO DEP-LETTER-NAME
**MOVE DEP-TBL-DATA(CHILD-18-X) TO DEP-LETTER-DATA
  MOVE DEP-TBL-DATA(CHILD-18-X) TO EXTRACT-DATA
*
  MOVE ST-V.ID-NUMBER-A TO DEP-LETTER-CN   /* DW2
  PERFORM S0460-WRITE-EXTRACT
END-FOR
*
END-SUBROUTINE  /* (1159)
*
*****************************************************************
*       3 5 0 0 - W R I T E - C H I L D - 1 6 - L E T T E R     *
*****************************************************************
DEFINE SUBROUTINE S3500-WRITE-CHILD-16-LETTER
*
SUBTRACT +1 FROM CHILD-16-X
MOVE CHILD-16-X TO NUM-CHILDREN-16-CNTR
*
FOR CHILD-16-X FROM +1 TO NUM-CHILDREN-16-CNTR
  IF DEP-16-TBL-DOB(CHILD-16-X) = YOUNGEST-CHILD-DOB
    RESET                               DEP-16-LETTER-TYPE
    MOVE 'D'                         TO EXTRACT-CODE
    MOVE DEP-16-TBL-NAME(CHILD-16-X) TO DEP-LETTER-NAME
**  MOVE DEP-16-TBL-DATA(CHILD-16-X) TO DEP-LETTER-DATA
    MOVE DEP-16-TBL-DATA(CHILD-16-X) TO EXTRACT-DATA
    MOVE ST-V.ID-NUMBER-A TO DEP-16-LETTER-CN   /* DW2
    PERFORM S0460-WRITE-EXTRACT
  END-IF
END-FOR
*
END-SUBROUTINE  /* (1197)
* * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DELETE-ASSO-RECD
* * * * * * * * * * * * * * * * * * * * * * *
*
MOVE TRUE TO #RECD-FOUND
MOVE ST-V.ID-NUMBER TO #ID-NUMBER
*
FIND (1) DEL-FM-RECORD WITH ID-NUMBER = #ID-NUMBER
  IF NO RECORDS FOUND
    MOVE FALSE TO #RECD-FOUND
  END-NOREC
*
  IF #RECD-FOUND
    DELETE (1222)
    ADD 1 TO #FM-DELETED
  END-IF
END-FIND
*
MOVE TRUE TO #RECD-FOUND
FIND (1) DEL-FIRS-RECORD WITH WIDOW-KEY = #ID-NUMBER-G
  IF NO RECORDS FOUND
    MOVE FALSE TO #RECD-FOUND
  END-NOREC
*
  IF #RECD-FOUND
    DELETE (1234)
    ADD 1 TO #FIRS-DELETED
  END-IF
END-FIND
*
MOVE TRUE TO #RECD-FOUND
FIND (1) DEL-CLM-RECORD WITH ID-NUMBER = ST-V.ID-NUMBER
  IF NO RECORDS FOUND
    MOVE FALSE TO #RECD-FOUND
  END-NOREC
*
  IF #RECD-FOUND
    DELETE (1246)
    ADD 1 TO #CLAIMS-DELETED
  END-IF
END-FIND
*
MOVE TRUE TO #RECD-FOUND
FIND (1) DEL-NOTES-RECORD WITH ID-NUMBER = ST-V.ID-NUMBER
  IF NO RECORDS FOUND
    MOVE FALSE TO #RECD-FOUND
  END-NOREC
*
  IF #RECD-FOUND
    DELETE (1258)
    ADD 1 TO #NOTES-DELETED
  END-IF
END-FIND
*
END-SUBROUTINE
************************************************************************
END
