* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: athp1405 - list a-f with more than 18yr 6 mo
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  08312006 DW1  Increase size of #recs-read
*******************************************
*
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 PROCESS-IND
  2 INTRO-FLAG
  2 RANK                (A3)
  2 NAME                (A25)
  2 SSN                 (P9)
  2 BASE-PAY-EF-DATE    (N8)
  2 ACTIVE-DUTY-BASE-DATE (N8)
  2 SERVICE-DATE-1405     (N8)
  2 MILITARY-SERVICE    (A3)
  2 MILITARY-STATUS     (A1)
  2 NUMBER-MEMBERSHIPS
*
1 #COUNTERS
  2 #RECS-READ        (P7)
  2 #RECS-SELECTED    (P5)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
*
1 #BASD              (A8)
1 #PEBD              (A8)
1 #D-1405            (A8)
*
1 #1405-DATE         (N8)
1 #DATE              (N8)
1 REDEFINE #DATE
 2 #DATE-YYYY        (N4)
 2 #DATE-MM          (N2)
 2 #DATE-DD          (N2)
1 #SSN               (N9)
1 REDEFINE #SSN
 2 #SSN-1ST-3        (N3)
 2 #SSN-MID-2        (N2)
 2 #SSN-QTD          (N4)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT          (A2)
  2 #YY            (A2)
  2 #MM            (A2)
  2 #DD            (A2)
*
END-DEFINE
*
FORMAT (2) LS=133 PS=60
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
'AIR-FORCE MBRS WITH 18 1/2 OR MORE YRS SVC & NO 1405 DT (BASD NE PEBD)'
                        5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATE      /* compute 18 1/2 years ago
SUBTRACT 18 FROM #DATE-YYYY
SUBTRACT 6 FROM #DATE-MM
IF #DATE-MM LT +12
  ADD 12 TO #DATE-MM
  SUBTRACT 1 FROM #DATE-YYYY
END-IF
*
READ ST-V BY MEMBER-KEY FROM '001900'
  ADD 1 TO #RECS-READ
*
  IF (ST-V.MILITARY-SERVICE NE 'F')    /* only Air-force
   OR (ST-V.MILITARY-STATUS NE 'A')    /* only active
   OR (ST-V.SERVICE-DATE-1405 NE 0)
   OR ((ST-V.ACTIVE-DUTY-BASE-DATE NE 0)
     AND (ST-V.BASE-PAY-EF-DATE NE 0)
     AND (ST-V.BASE-PAY-EF-DATE = ST-V.ACTIVE-DUTY-BASE-DATE))
    OR (ST-V.INTRO-FLAG = 'O' OR = 'P' OR = 'Q') /* INS ONLY
    OR (ST-V.PROCESS-IND = 'S' OR = 'C')
     ESCAPE TOP
  END-IF
*
  RESET #1405-DATE
*
  IF ST-V.ACTIVE-DUTY-BASE-DATE NE 0
    MOVE ST-V.ACTIVE-DUTY-BASE-DATE TO #1405-DATE
  ELSE
    IF ST-V.BASE-PAY-EF-DATE NE 0
      MOVE ST-V.BASE-PAY-EF-DATE TO #1405-DATE
    END-IF
  END-IF
*
  IF  (#1405-DATE GE #DATE)   /* over 18 1/2 yrs a/o today
   OR (#1405-DATE = 0)
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #RECS-SELECTED
END-ALL
*
* * * * * * SORT INPUT RECORDS * * * * * *
*
SORT BY ST-V.MILITARY-SERVICE ST-V.SSN
  USING ST-V.BASE-PAY-EF-DATE
        ST-V.ACTIVE-DUTY-BASE-DATE
        ST-V.SERVICE-DATE-1405
        ST-V.RANK
        ST-V.NAME
        ST-V.ID-NUMBER
        #1405-DATE
*
    ADD 1 TO #CNT
*
    RESET #BASD #PEBD #D-1405
*
    IF ST-V.ACTIVE-DUTY-BASE-DATE NE 0
      MOVE ST-V.ACTIVE-DUTY-BASE-DATE TO #YYYYMMDD
      COMPRESS #MM #DD #YY INTO #BASD WITH '/'
    END-IF
*
    IF ST-V.BASE-PAY-EF-DATE NE 0
      MOVE ST-V.BASE-PAY-EF-DATE TO #YYYYMMDD
      COMPRESS #MM #DD #YY INTO #PEBD WITH '/'
    END-IF
*
    IF ST-V.SERVICE-DATE-1405 NE 0
      MOVE ST-V.SERVICE-DATE-1405 TO #YYYYMMDD
      COMPRESS #MM #DD #YY INTO #D-1405 WITH '/'
    END-IF
*
  ADD 1 TO #CNT-2
  DISPLAY (2) NOTITLE GIVE SYSTEM FUNCTIONS
              #CNT-2
              'MIL-SERV'       ST-V.MILITARY-SERVICE
              'SSN'            ST-V.SSN
              'ID/NUMBER'      ST-V.ID-NUMBER
              'RANK'           ST-V.RANK
              'NAME'           ST-V.NAME
              'PEBD'           #PEBD
              '1405-DT *******' #D-1405
              'BASD'           #BASD
  WRITE (2) ' '
  WRITE (2) ' '
*
END-SORT
*
WRITE (2) 'TOT-RECS-READ        = ' #RECS-READ
WRITE (2) 'TOT-RECS-SELECTED    = ' #RECS-SELECTED
*
END
