* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPBPAPPD                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Move funds from a member's CREDIT account to          *
*                ADDITIONAL PREMIUMS PAID - cloned from ATHPRFND       *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    05152008  None    Program Written                             *
* DRW  03/11/2009 DW1 Increase Note Occurance 60 to 500
* YAK  10/16/2012 YK1 Use function to identify Level Term II plans
* TMT  10/07/2013 TT1 ADDRESS NORMALIZATION
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING ATHLTMRF
LOCAL USING C2500AAM
LOCAL
*
1 #INPUT                      (A200)
*
1 #INPUT-TABLE
  2 #FIELD-1                   (A10)
  2 #FIELD-2                   (A50)
  2 #FIELD-3                   (A10)
  2 #FIELD-4                   (A16)
  2 #FIELD-5                   (A10)
  2 #FIELD-6                   (A104)
*
1 #PASS-NAME                  (A25)
1 #NAME                       (A50)
1 #AMOUNT-A                   (N8.2)
1 #POLICY-NUMBER              (A16)
1 #ISSUE-DATE                 (A10)
1 REDEFINE #ISSUE-DATE
  2 #ISSUE-DATE-MM             (N2)
  2 REDEFINE #ISSUE-DATE-MM
    3 #ISSUE-DATE-M1            (A1)
    3 #ISSUE-DATE-M2            (A1)
  2 #ISSUE-DATE-SL-1           (A1)
  2 #ISSUE-DATE-DD             (N2)
  2 REDEFINE #ISSUE-DATE-DD
    3 #ISSUE-DATE-D1            (A1)
    3 #ISSUE-DATE-D2            (A1)
  2 #ISSUE-DATE-SL-2           (A1)
  2 #ISSUE-DATE-YYYY           (N4)
*
1 #TAB                        (A1) INIT <H'09'>
1 #NEG                        (L)
1 #DO-NOT-REF                 (L)
1 #REF-NOTE                   (A60)
1 #PEACHTREE                  (A80)
*
1 #TO-INIT                    (A3)
1 #ST-ISN                     (P8)
1 #FOUND-ISS-DT               (L)
1 #ST-NOT-FOUND               (L)
*
1 #INP-CN                     (N6)
1 REDEFINE #INP-CN
  2 #INP-CN-A                  (A6)
1 #INP-DB-CR                  (N6.2)
*
1 #COMMA                      (A1) INIT <','>
1 #LAST                       (A25)
1 REDEFINE #LAST
  2 #LAST-X                    (A1/25)
1 #FIRST-20                   (A20)
1 #MI                         (A1)
1 #NAME-FML                   (A25)
1 REDEFINE #NAME-FML
  2 #NAME-FML-X                (A1/25)
1 #ERR-MSG                    (A60)
1 #AMT-8                      (A8)
1 #TOT-REFUND                 (P7.2)
1 #TOT-DB-CR                  (P7.2)
1 #TOT-MISMATCH               (P7.2)
1 #TOT-BYPASSED               (P7.2)
1 #DB-CR-REFD                 (P7.2)
1 #DB-CR-REFD-DISP            (A10)
1 #NUM-DB-CR                  (N5)
1 #MISMATCH-CNT               (N5)
1 #BYPASSED-CNT               (N5)
1 #UPDATE                     (L)
1 #MODAL-PREM                 (N5.2)
*
1 NT-V VIEW OF A-NOTES
  2 C*NOTE-TABLE                              /* DW1
  2 NOTE-CLERK-ID              (A3/500)       /* DW1
  2 NOTE-LINE-NUM              (N1/500)       /* DW1
  2 NOTE-DATE                  (N8/500)       /* DW1
  2 REDEFINE NOTE-DATE
    3 NOTE-DATE-OCC             (500)          /* DW1
      4 NOTE-DATE-YYYY           (N4)
      4 NOTE-DATE-MMDD           (N4)
  2 NOTE-TEXT                  (A60/500)      /* DW1
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-OCC             (500)          /* DW1
      4 NOTE-TEXT-60             (A60)
      4 REDEFINE NOTE-TEXT-60
        5 NOTE-TEXT-14            (A14)
        5 NT-DO-NOT-REF           (A13)
        5 NT-REST                 (A33)
*
1 ST-V VIEW OF A-STATUS                        /** TTT1 REMOVED ADDRESS AND ADDRESS CHANGE FIELDS
  2 ID-NUMBER                  (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR                   (A6)
  2 DTS-DATN
  2 DTS-TIMN
  2 DATE-ACCT-UPDATE
  2 WIDOW-FLAG                 (A1)
  2 ALOT-AMT
  2 PROCESS-IND                (A1)
  2 NAME
  2 DATE-OF-DEATH
  2 PAD-CODE
  2 INTRO-FLAG
  2 DEBIT-CREDIT-AMT
  2 STATUS                     (9)
  2 X-BILL-ADJUST              (9)
  2 ISSUE-DATE                 (N8/9)
  2 REDEFINE ISSUE-DATE
    3 ISS-DT-OCC                (9)
      4 ISS-DT-YYYY              (N4)
      4 ISS-DT-MM                (N2)
      4 ISS-DT-DD                (N2)
  2 PLAN                       (A3/9)
  2 REDEFINE PLAN
    3 PLAN-OCC                  (9)
      4 PLAN-A1                  (A1)
      4 PLAN-A2                  (A2)
  2 ANNUAL-PREM                (9)
  2 MODE                      (9)
  2 TOT-PREMIUMS-PAID          (9)
  2 MTD-ACV-PREM-ADJ           (9)
*
1 ACCT-REG-V VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER                  (N6)
  2 PROCESS-IND                (A1)
  2 TRANSACTION-DATN           (P9)
  2 TRANSACTION-TIMN           (P7)
  2 TXN-CODE                   (A4)
  2 SEQUENCE-NUM               (N1)
  2 NAME                       (A25)
  2 TXN-ACCT-NUMBER            (N3)
  2 ACCT-DATE                  (N8)
  2 DATE-LAST-ACCT-UPDATE      (N8)
  2 DEBIT-AMOUNT               (P7.2)
  2 CREDIT-AMOUNT              (P7.2)
  2 DESCRIPTION                (A20)
  2 MESSAGE-CODE               (A1)
  2 AALP-FLAG                  (A1)
  2 CLERK-ID                   (A3)
  2 TERMINAL-ID                (A8)
  2 CASH-CODE                  (A1)
  2 POLICY-NUMBER
  2 SG-CATEGORY-1              (A2/15)
  2 SG-CATEGORY-2              (A2/15)
  2 SG-AMOUNT                  (N6.2/15)
*
1 #RF-AMT-DISP  (A9)  /* $1,234.56
*
1 #COUNTERS
  2 #RECS-READ                 (P5)
  2 #RECS-SELECTED             (P5)
  2 #CNT                       (P5)
  2 #CNT-2                     (P5)
  2 #CNT-3                     (P5)
  2 #PEACHTREE-CNT             (P5)
  2 #I1                        (I2)
  2 #I2                        (I2)
  2 #I3                        (I2)
  2 #POL                       (I2)
  2 #ILN1                      (I2)
1 #PEACH-CNT-X                (A7)
1 #NUM-RECS                   (N5)
1 #NUM-POLS                   (P5)
1 #INT                        (I2)
1 #NOTE-TEXT                  (A60/10)
*
1 #TODAY-MMDDYYYY             (N8)
1 REDEFINE #TODAY-MMDDYYYY
  2 #TODAY-MMDDYYYY-A          (A8)
1 REDEFINE #TODAY-MMDDYYYY
  2 #TODAY-MM                  (A2)
  2 #TODAY-DD                  (A2)
  2 #TODAY-YYYY                (A4)
1 #YYYYMMDD                   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A                (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY                      (N4)
  2 #MM                        (N2)
  2 #DD                        (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A                    (A4)
  2 #MM-A                      (A2)
  2 #DD-A                      (A2)
*
1 #1-YR-AGO                   (N8)
1 REDEFINE #1-YR-AGO
  2 #1-YR-YYYY                 (N4)
  2 #1-YR-MMDD                 (N4)
*
1 #POL-ISSUE-DATE             (N8)
1 REDEFINE #POL-ISSUE-DATE
  2 #POL-ISSUE-DATE-YYYY       (N4)
  2 #POL-ISSUE-DATE-MM         (N2)
  2 #POL-ISSUE-DATE-DD         (N2)
*
1 #FOUND-POL                  (L)
*
* Below is linkage to C2520SAM
1 #CLERK                      (A3)
1 #DATE-OF-UPDATE             (N8)
1 #TIME-OF-UPDATE             (N7)
1 #HOLD-CUR-ACCT-UPDT         (N8)
1 #EXTRA-STOP                 (L)
1 #BANKRUPT                   (L)
*
1 #SG-TBL                     (15)
  2 #SG-CAT1                   (A2)
  2 #SG-CAT2                   (A2)
  2 #SG-AMT                    (N6.2)
1 #CMBX                       (A4)
* Above lis linkage to C2520SAM
*
1 #PEACHTREE-TABLE
  2 #PEACH-1                   (A25)
  2 #PEACH-2                   (A25)
  2 #PEACH-3                   (A25)
  2 #PEACH-4                   (A25)
  2 #PEACH-5                   (A25)
  2 #PEACH-6                   (A25)
  2 #PEACH-7                   (A25)
  2 #PEACH-8                   (A25)
*
END-DEFINE
************************************************************************
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'PROGRAM EXCEPTIONS'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (1) LS=133 PS=76
WRITE  (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'PROGRAM EXCEPTIONS'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Additional Premiums Paid from Credit Account'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'APPD Notes and Tr Reg'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
MOVE '1,2,3,4,5,6,7,8' TO #PEACHTREE
WRITE WORK 2 #PEACHTREE
*
MOVE *DATN TO #YYYYMMDD #1-YR-AGO
COMPRESS #MM-A #DD-A #YYYY-A INTO #TODAY-MMDDYYYY-A LEAVING NO
SUBTRACT 1 FROM #1-YR-YYYY
*
READ WORK 1 RECORD #INPUT
  IF #INPUT = MASK ('CN')
    ESCAPE TOP
  END-IF
*
  SEPARATE #INPUT INTO #INPUT-TABLE WITH DELIMITER #TAB
*
  MOVE RIGHT #FIELD-1 TO #FIELD-1
  MOVE RIGHT #FIELD-3 TO #FIELD-3
  MOVE RIGHT #FIELD-5 TO #FIELD-5
*
  #INP-CN    := VAL(#FIELD-1)
  #INP-DB-CR := VAL(#FIELD-3)
  #INP-DB-CR := #INP-DB-CR * -1
  #ISSUE-DATE          := #FIELD-5
  PERFORM FORMAT-ISSUE-DATE
  #POL-ISSUE-DATE-YYYY := #ISSUE-DATE-YYYY
  #POL-ISSUE-DATE-MM   := #ISSUE-DATE-MM
  #POL-ISSUE-DATE-DD   := #ISSUE-DATE-DD
  #POLICY-NUMBER       := #FIELD-4
*
  RESET #NEG
*
  RESET #ST-NOT-FOUND
  RESET #FOUND-ISS-DT #DB-CR-REFD
  FIND (1) ST-V WITH MEMBER-KEY = #INP-CN-A
    IF NO RECORD FOUND
      MOVE TRUE TO #ST-NOT-FOUND
      ESCAPE BOTTOM
    END-NOREC
    MOVE *ISN TO #ST-ISN
  END-FIND
*
  IF #ST-NOT-FOUND
    WRITE (1) #INP-CN 'Bypassed - CN not found'
    WRITE (2) #INP-CN 'Bypassed - CN not found'
    ESCAPE TOP
  END-IF
*
  GET ST-V #ST-ISN
*
  MOVE ST-V.NAME TO #PASS-NAME
  RESET #LAST #MI #FIRST-20
  CALLNAT 'GFMLNSXX' /* split name
    #PASS-NAME
    #FIRST-20
    #MI
    #LAST
    #ERR-MSG
  COMPRESS #FIRST-20 #MI #LAST INTO #NAME-FML
*
  IF #INP-DB-CR NE (DEBIT-CREDIT-AMT * -1)
    WRITE (1) ST-V.ID-NUMBER 'Bypassed - DB/CR Mismatch - File DB/CR='
      #INP-DB-CR 'ST DB/CR =' DEBIT-CREDIT-AMT
    WRITE (2) ST-V.ID-NUMBER 'Bypassed - DB/CR Mismatch - File DB/CR='
      #INP-DB-CR 'ST DB/CR =' DEBIT-CREDIT-AMT
    ADD +1 TO #MISMATCH-CNT
    ADD #INP-DB-CR TO #TOT-MISMATCH
    ESCAPE TOP
  END-IF
*
  GET ST-V #ST-ISN
*
  RESET #FOUND-POL
  DISPLAY ST-V.ID-NUMBER #POL-ISSUE-DATE
  FOR #POL 1 TO 9
    IF (ST-V.ISSUE-DATE (#POL) NE #POL-ISSUE-DATE)
*    OR (ST-V.PLAN (#POL) = 'LT' OR = 'RT')              /* YK1
*    OR ((ST-V.PLAN-A1 (#POL) GE 'U')                    /* YK1
*     AND(ST-V.PLAN-A1(#POL) LE 'Z'))                    /* YK1
        OR FUNCTION-TERM-POLICY(<ST-V.PLAN(#POL)>)          /* YK1
        OR (ST-V.STATUS (#POL) NE 'D')
      ESCAPE TOP
    END-IF
*
    #FOUND-POL := TRUE
    ESCAPE BOTTOM
  END-FOR
*
  IF NOT #FOUND-POL
    WRITE (1) #INP-CN 'Bypassed - Policy not Found'
    WRITE (2) #INP-CN 'Bypassed - Policy not Found'
    ADD +1 TO #BYPASSED-CNT
    ADD #INP-DB-CR TO #TOT-BYPASSED
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  RESET #DO-NOT-REF #REF-NOTE
  FIND (1) NT-V WITH ID-NUMBER = ST-V.ID-NUMBER
    FOR #I1 1 TO C*NOTE-TABLE                           /* DW1
      IF NT-DO-NOT-REF (#I1) = 'DO NOT REFUND'
          OR NOTE-TEXT (#I1) = MASK ('DO NOT REFUND')
          OR NOTE-TEXT (#I1) = MASK ('*DO NOT REFUND')
          OR NOTE-TEXT (#I1) = MASK ('* DO NOT REFUND')
          OR NOTE-TEXT (#I1) = MASK ('**DO NOT REFUND')
        MOVE TRUE TO #DO-NOT-REF
        MOVE NOTE-TEXT (#I1) TO #REF-NOTE
      END-IF
    END-FOR
  END-FIND
*
  IF #DO-NOT-REF
    WRITE (1) #INP-CN 'Bypassed' #REF-NOTE
    WRITE (2) #INP-CN 'Bypassed' #REF-NOTE
    ADD +1 TO #BYPASSED-CNT
    ADD #INP-DB-CR TO #TOT-BYPASSED
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  COMPUTE #DB-CR-REFD = DEBIT-CREDIT-AMT * -1
  ADD #DB-CR-REFD TO #TOT-REFUND
  ADD #DB-CR-REFD TO #TOT-DB-CR
  MOVE EDITED #DB-CR-REFD (EM=ZZ,ZZZ.99) TO #DB-CR-REFD-DISP
*
  MOVE ST-V.DATE-ACCT-UPDATE TO #HOLD-CUR-ACCT-UPDT
  MOVE *DATN TO ST-V.DATE-ACCT-UPDATE
  MOVE *DATN TO ST-V.DTS-DATN
  MOVE *TIMN TO ST-V.DTS-TIMN
*
  ADD 1 TO #NUM-DB-CR
  ADD #DB-CR-REFD TO ST-V.DEBIT-CREDIT-AMT  /* <***************
  PERFORM ACCT-REG-AND-NOTE
*
  PERFORM PEACHTREE
*
  PERFORM UPDATE-PREMIUMS
*
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2
    'CN'       ST-V.ID-NUMBER
    'Iss/Date' ST-V.ISSUE-DATE (#POL)
    'DB-CR/Adjust' #DB-CR-REFD  (EM=-ZZ,ZZZ.99)
    'New/DB-CR' DEBIT-CREDIT-AMT (EM=-ZZZ,ZZZ.99)
    'Name'         NAME
*
  UPDATE (0338)
  END TRANSACTION
END-WORK
*
NEWPAGE (1)
WRITE (1) ///
WRITE (1) #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE (1) #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE (1) #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT APPD AMOUNT'
WRITE (1) ' '
WRITE (1) #MISMATCH-CNT (EM=Z,ZZ9)      '     Num Mismatched'
WRITE (1) #TOT-MISMATCH (EM=ZZZ,ZZZ.99) 'Total mismatch amt'
WRITE (1) ' '
WRITE (1) #BYPASSED-CNT (EM=Z,ZZ9)      '     Num Bypassed - other'
WRITE (1) #TOT-BYPASSED (EM=ZZZ,ZZZ.99) 'Total bypassed - other'
*
WRITE (2) ///
WRITE (2) #NUM-DB-CR    (EM=Z,ZZ9)      '     Num DB/CR Adjustments'
WRITE (2) #TOT-DB-CR    (EM=ZZZ,ZZZ.99) 'Debit-credit Adj'
WRITE (2) #TOT-REFUND   (EM=ZZZ,ZZZ.99) 'TOT APPD AMOUNT'
WRITE (2) ' '
WRITE (2) #MISMATCH-CNT (EM=Z,ZZ9)      '     Num Mismatched'
WRITE (2) #TOT-MISMATCH (EM=ZZZ,ZZZ.99) 'Total mismatch amt'
WRITE (2) ' '
WRITE (2) #BYPASSED-CNT (EM=Z,ZZ9)      '     Num Bypassed - other'
WRITE (2) #TOT-BYPASSED (EM=ZZZ,ZZZ.99) 'Total bypassed - other'
*
MOVE EDITED #TOT-DB-CR    (EM=ZZ,ZZZ.99) TO #DB-CR-REFD-DISP
COMPRESS #NUM-DB-CR 'APPD Adjustments for $' #DB-CR-REFD-DISP
  'have been processed' TO #NOTE-TEXT (1)
*
MOVE 'EDS' TO #CGA-CLERK
MOVE '*NX' TO #CGA-SCR-SUFFIX
MOVE 'VXT' TO #TO-INIT                                   /* vt 06/19/2013
FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
END TRANSACTION
*
************************************************************************
DEFINE SUBROUTINE PEACHTREE
************************************************************************
*
ADD +1 TO #CNT-3
COMPUTE #PEACHTREE-CNT = #CNT-3 * 2
MOVE EDITED #PEACHTREE-CNT (EM=ZZZZ9) TO #PEACH-CNT-X
MOVE LEFT #PEACH-CNT-X TO #PEACH-CNT-X
*
FOR #I1 1 TO 25
  IF #NAME-FML-X (#I1) = ','
    RESET #NAME-FML-X (#I1)
  END-IF
END-FOR
*
FOR #I1 1 TO 25
  IF #LAST-X (#I1) = ','
    RESET #LAST-X (#I1)
  END-IF
END-FOR
*
MOVE EDITED #DB-CR-REFD (EM=ZZZZZ.99) TO #AMT-8
MOVE LEFT #AMT-8 TO #AMT-8
*
RESET #PEACHTREE-TABLE
COMPRESS #TODAY-MM #TODAY-DD #TODAY-YYYY INTO #PEACH-1 WITH '/'
COMPRESS 'RDCR CN' ST-V.ID-NUMBER INTO #PEACH-2 LEAVING NO
#PEACH-3 := #PEACH-CNT-X
#PEACH-4 := '2236 - 00'
COMPRESS #LAST #POLICY-NUMBER INTO #PEACH-5
#PEACH-6 := #AMT-8
*
COMPRESS #PEACH-1 ','
  #PEACH-2 ','
  #PEACH-3 ','
  #PEACH-4 ','
  #PEACH-5 ','
  #PEACH-6 ','
  #PEACH-7 ','
  #PEACH-8 INTO #PEACHTREE LEAVING NO
*
WRITE WORK 2 #PEACHTREE
RESET #PEACHTREE
*
COMPRESS 'PREP CN' ST-V.ID-NUMBER INTO #PEACH-2 LEAVING NO
IF ST-V.ISSUE-DATE (#POL) GT #1-YR-AGO
  #PEACH-4 := '4012 - 00'
ELSE
  #PEACH-4 := '4014 - 00'
END-IF
COMPRESS '-' #AMT-8 INTO #PEACH-6 LEAVING NO
*
COMPRESS #PEACH-1 ','
  #PEACH-2 ','
  #PEACH-3 ','
  #PEACH-4 ','
  #PEACH-5 ','
  #PEACH-6 ','
  #PEACH-7 ','
  #PEACH-8 INTO #PEACHTREE LEAVING NO
*
WRITE WORK 2 #PEACHTREE
RESET #PEACHTREE
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE ACCT-REG-AND-NOTE
************************************************************************
*
MOVE 1 TO #ILN1
MOVE ST-V.ID-NUMBER               TO #SCR-ID-SSN (1)
MOVE #TODAY-MMDDYYYY-A            TO #SCR-EFF-DT (1)
MOVE ST-V.ISSUE-DATE (#POL)       TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A    INTO #SCR-ISS-DT (1) LEAVING NO
MOVE '+'                          TO #SCR-ADJ (1)
MOVE #DB-CR-REFD                  TO #SCR-TRAN-AMT  (1)
MOVE 'A'                          TO #SCR-CASH-CODE (1)
MOVE *DATN                        TO #DATE-OF-UPDATE
MOVE *TIMN                        TO #TIME-OF-UPDATE
MOVE 'EDS'                        TO #CLERK
*
MOVE 'RDCR'                       TO #SCR-TRAN-CODE (1)
*
CALLNAT 'C2520SAM' #SCR-LINES
  ST-V.ID-NUMBER
  ST-V.PROCESS-IND
  ST-V.INTRO-FLAG
  ST-V.NAME
  ST-V.PLAN   (1:9)
  ST-V.DEBIT-CREDIT-AMT
  ST-V.DATE-ACCT-UPDATE
  #DATE-OF-UPDATE
  #TIME-OF-UPDATE
  #ILN1
  #HOLD-CUR-ACCT-UPDT
  #CLERK
  #EXTRA-STOP
  #SG-TBL (1:15)
  #CMBX
RESET #EXTRA-STOP
*
MOVE 'APPD'                       TO #SCR-TRAN-CODE (1)
*
CALLNAT 'C2520SAM' #SCR-LINES
  ST-V.ID-NUMBER
  ST-V.PROCESS-IND
  ST-V.INTRO-FLAG
  ST-V.NAME
  ST-V.PLAN   (1:9)
  ST-V.DEBIT-CREDIT-AMT
  ST-V.DATE-ACCT-UPDATE
  #DATE-OF-UPDATE
  #TIME-OF-UPDATE
  #ILN1
  #HOLD-CUR-ACCT-UPDT
  #CLERK
  #EXTRA-STOP
  #SG-TBL (1:15)
  #CMBX
RESET #EXTRA-STOP
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE UPDATE-PREMIUMS
************************************************************************
*
ADD #INP-DB-CR TO ST-V.TOT-PREMIUMS-PAID (#POL)
IF ST-V.MTD-ACV-PREM-ADJ (#POL) = 9999999.99
  RESET ST-V.MTD-ACV-PREM-ADJ (#POL)
END-IF
ADD #INP-DB-CR TO ST-V.MTD-ACV-PREM-ADJ (#POL)
*
IF #POL NE 1                /* FILLUP PRECEEDING MULTIPLE FIELDS
  COMPUTE #I3 = #POL - 1
  FOR #I2 1 TO #I3
    IF ST-V.MTD-ACV-PREM-ADJ (#I2) = 0
      MOVE 9999999.99 TO ST-V.MTD-ACV-PREM-ADJ (#I2)
    END-IF
  END-FOR
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE FORMAT-ISSUE-DATE
************************************************************************
*
IF #ISSUE-DATE-D1 = '/ '
  #ISSUE-DATE-M1   := #ISSUE-DATE-M2
  #ISSUE-DATE-M2   := #ISSUE-DATE-SL-1
  #ISSUE-DATE-SL-1 := '/'
  #ISSUE-DATE-D1   := '0'
END-IF
*
IF #ISSUE-DATE-M1 = ' '
  #ISSUE-DATE-M1 := '0'
END-IF
*
END-SUBROUTINE
*
END
