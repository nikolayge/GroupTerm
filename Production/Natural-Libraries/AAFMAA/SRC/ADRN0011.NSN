* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM-ID: ADRN0011 - PHONE-NUMBER update                                  *
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    08/17/2010  None    Program written                           *
* RSE    09/08/2010  RE1     Enable PF11 (Delete)                      *
* RSE    10/22/2010  RE2     Add logic to designate preferred numbers  *
* RSE    03/28/2011  RE3     Standardize TR call                       *
* YK1    05/10/2011  YK1     Correct formatting extension for TR       *
* RSE    05/15/2011  RE4     Call module to sync with CRM
* NGG    07/07/2011  NG1     Call module to sync with CRM fix
* NGG    09/20/2011  NG2     Create new groups
* NGG    10/03/2011  NG3     Last Update User Name
* RSE    03/02/2012  RE5     Reset PHONE-NUMBER after image when deleted
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID    (N8)
1 #MEMBER-ID-NUMBER (N6)
1 #PHONE-ID      (N8) 1 REDEFINE #PHONE-ID
  2 #PHONE-ID-A (A8)
LOCAL USING A-CTDATA   /* RE1
LOCAL USING GPRATR00      /* RE2
**LOCAL USING GPRA0004                /* Parameters passed to write transaction log   RE2
LOCAL
1 PHONES VIEW OF A-CONTACT-PHONE
  2 PHONE-ID                          (N8.0)  /* D PHONE-NUMBER ID
  2 CONTACT-ID                        (N8.0)  /* D Contact ID
  2 INTL-ACCESS-CODE                  (N3.0)  /*   International Access Code
  2 INTL-DIAL-NUMBER                  (N20.0) /*   International Dial Number
  2 AREA-CODE                         (N3.0)  /* D Area Code
  2 DIAL-NUMBER                       (N7.0)  /*   Dial Number
  2 EXT                               (N6.0)  /*   Extension
  2 PREFERRED-PHONE-IND               (A1)    /*   Preferred PHONE-NUMBER
  2 BEST-TIME-TO-CALL                 (A1)    /*   Morn / After / Night
  2 INVALID-IND                       (A1)    /*   PHONE-NUMBER invalid
  2 PHONE-TYPE-CODE                   (A1)    /*   Home/Work/Fax/etc
  2 TYPE                              (A1)    /*   Domestic / International
  2 REMARKS                           (A70/3)
  2 START-DATE                        (N8.0)  /*   PHONE-NUMBER start date
  2 END-DATE                          (N8.0)  /*   PHONE-NUMBER end date
  2 LAST-DATE-UPD                     (N8.0)  /*   Last date update
  2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A          (A8)
  2 LAST-TIME-UPD                     (N7.0)  /*   Last time update
  2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A          (A7)
  2 LAST-USER-UPD                     (A8)    /*   Last update user

1 #USER-DATA
  2 #REMARKS                           (A70/3)
  2 #INTL-ACCESS-CODE                  (A3)    /*   International Access Code
  2 #INTL-DIAL-NUMBER-A                (A20)   /*   International Dial Number
  2 REDEFINE #INTL-DIAL-NUMBER-A
    3 #INTL-DIAL-NUMBER                (N20)
  2 #AREA-CODE-A                       (A3)  2 REDEFINE #AREA-CODE-A
    3 #AREA-CODE                       (N3)
  2 #DIAL-NUMBER-A                     (A7)  /*   Dial Number
  2 REDEFINE #DIAL-NUMBER-A
    3 #DIAL-NUMBER                     (N7)
  2 REDEFINE #DIAL-NUMBER-A
    3 #DN3                             (A3)
    3 #DN4                             (A4)
  2 REDEFINE #DIAL-NUMBER-A
    3 #DND                             (A1/7)
  2 #EXT-A                             (A6)  /*   Extension
  2 REDEFINE #EXT-A
    3 #EXT                             (N6)

1 #COUNTRY-CODE                      (A2)
1 #COUNTRY                           (A50)
1 #CLERK                             (A20)
1 #HEADER1                           (A78)
1 #HEADER2                           (A78)
1 #ADDR-POOL-ID                      (N8)
1 #EMAIL-ID                          (N8)
1 #USER                              (A8)
1 #DATE-UPDATED                      (A10)
1 #CODES
  2  #ADDR-INTL        (A1)     /* 001
  2  #ADDR-LOCATION    (A1)     /* 002
  2  #ADDR-OVERRD      (A1)     /* 003   Y/N
  2  #ADDR-PROTECT     (A1)     /* 004
  2  #ADDR-RETURNED    (A1)     /* 005   Y/N
  2  #ADDR-TYPE        (A1)     /* 006
  2  #ADDR-VALID       (A1)     /* 007
  2  #EML-PREF         (A1)     /* 008   Y/N
  2  #EML-PROTECT      (A1)     /* 009   Y/N
  2  #EML-TYPE         (A1)     /* 010
  2  #EML-UNDLV        (A1)     /* 011   Y/N
  2  #PH-INTL          (A1)     /* 012
  2  #PH-PREF          (A1)     /* 013
  2  #PH-TIME          (A1)     /* 014
  2  #PH-TYPE          (A1)     /* 015
  2  #PH-VALID         (A1)     /* 016
  2  #ADDR-SOURCE      (A1)     /* 017
  2  #ADDR-STATUS      (A1)     /* 018
  2  #XXX              (A1)
  2  #EML-STATUS       (A1)     /* 020
1 #CODE-NAMES
  2  #ADDR-INTL-TXT        (A20)
  2  #ADDR-LOCATION-TXT    (A20)
  2  #ADDR-OVERRD-TXT      (A20)
  2  #ADDR-PROTECT-TXT     (A20)
  2  #ADDR-RETURNED-TXT    (A20)
  2  #ADDR-TYPE-TXT        (A20)
  2  #ADDR-VALID-TXT       (A20)
  2  #EML-PREF-TXT         (A20)
  2  #EML-PROTECT-TXT      (A20)
  2  #EML-TYPE-TXT         (A20)
  2  #EML-UNDLV-TXT        (A20)
  2  #PH-INTL-TXT          (A20)
  2  #PH-PREF-TXT          (A20)
  2  #PH-TIME-TXT          (A20)
  2  #PH-TYPE-TXT          (A20)
  2  #PH-VALID-TXT         (A20)
  2  #ADDR-SOURCE-TXT      (A20)
  2  #ADDR-STATUS-TXT      (A20)
  2  #XXX                  (A20)
  2  #EML-STATUS-TXT       (A20)
*
1 BLANK   (A1) CONST <' '>
1 YES     (A1) CONST <'Y'>
1 #TEST   (A8) CONST <'CR249'>
1 USA     (A2) CONST <'US'>
1 #CELL    (A1)   INIT <'C'>           /* NG2 Start
1 #HOME    (A1)   INIT <'H'>
1 #BUSINESS(A1)   INIT <'B'>
1 #FAX    (A1/3)  INIT <'A','F','X'>
1 #OTHER   (A1)   INIT <'O'>
1 #CARPHN  (A1/2) INIT <'M','P'>       /* NG2 End
*
1 #ERRORS                  (L)
1 #UPDATED                 (L)
1 #PHONE-ISN               (P8)
1 #MSG                     (A72)
1 #MODE                    (A1)    /* U - Update, V - View

1 CONTACT VIEW OF A-CONTACTS
  2 ID-NUMBER (N6)

1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
  2 SECURITY-CODE     (N2)
  2 USER-COMPANY      (A1)

1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID      /* RE3
  2 WIDOW-FLAG
  2 NAME
*   2 TELE-NUMBER            (P11.0)  /* HOME
*   2 HOME-TELE-INTR         (A6)
*   2 NXT-OF-KIN-TELE-NUMBER (P11.0)  /* WORK
*   2 WORK-TELE-INTR         (A6)
*   2 FAX-NUMBER             (P17.0)  /* FAX
*   2 FAX-NUMBER-INTR        (A6)

1 #CGA-ORIG-COMMAND (A2)
1 #CGA-USER-COMPANY (A1)
1 #CGA-CLERK        (A3)
1 #CONFIRM-DELETE   (A1)  /* RE1

1 OTHER-PHONES VIEW OF A-CONTACT-PHONE
  2 PREFERRED-PHONE-IND               (A1)    /*   Preferred PHONE-NUMBER
  2 PHONE-TYPE-CODE                   (A1)    /*   Home/Work/Fax/etc

1 #POS              (I4)
1 #ERROR-NBR        (I4)
1 #ERRORS-TXT       (A72/10) INIT <
  'Domestic PHONE-NUMBER is 7 digits',
  'Type is required',
  'Invalid date format',
  'Invalid area code',
  'Please enter international PHONE-NUMBER number',
  'Please enter domestic PHONE-NUMBER number',
  'Invalid extension',
  'International PHONE-NUMBER number is 7 digits or more',
  'Preferred is required'
  >
1 #TYPE              (A1)
1 #D                 (D)
1 #J                 (I4)
1 #L                 (I4)
1 #X                 (A) DYNAMIC

1 #F16     (A16) 1 REDEFINE #F16
  2 #PH10  (A10) 2 REDEFINE #PH10
    3 #PH10-N (N10)
  2 #PH6   (A6)

1 #STATUS-RESET   (I4)
1 #DELETE-HOME    (I4) CONST <1>
1 #DELETE-WORK    (I4) CONST <2>
1 #DELETE-FAX     (I4) CONST <3>
1 #PHONE-TYPE     (A1)
1 #STATUS-ISN     (P8)
1 #SAVE-PREF      (A1)
1 #TEXT           (A67/1) /* RE3
1 #PHONE          (A) DYNAMIC INIT<'PHONE-NUMBER'>
1 #XML            (A) DYNAMIC         /* RE4
1 #LINE           (A) DYNAMIC         /* RE4
1 #RESPONSE-2     (A) DYNAMIC         /* RE4
1 #CRM-REQUEST    (I4)                /* RE4
1 #RESPONSE-DSPL  (A16)               /* RE4
1 #SYNCHRONIZE    (L)                 /* RE4

END-DEFINE

DEFINE WINDOW Confirm-Delete       /* RE1
  SIZE 8 * 68
  BASE 13/2
  FRAMED ON POSITION SYMBOL AUTO
*
SET KEY ENTR NAMED 'Save'
SET KEY PF1 = Help
SET KEY PF3  NAMED 'Exit'
SET KEY PF4  NAMED 'Add'
SET KEY PF11 NAMED 'Del'
SET KEY PF12 NAMED 'Reln'

IF #CONTACT-ID = 0
  ESCAPE ROUTINE
END-IF

#CGA-ORIG-COMMAND := 'CG'
#MODE             := 'U'
MOVE *USER TO #USER
FIND (1) TERM WITH TERMINAL-ID = #USER
*  #TR-CLERK-ID := CLERK-ID                  /* RE3
  #CGA-CLERK   := CLERK-ID
END-FIND

EXPAND ARRAY BEFORE_IMAGE.PHONE_GROUP TO (1:5)        /* RE3
EXPAND ARRAY AFTER_IMAGE.PHONE_GROUP TO (1:5)         /* RE3

IF #PHONE-ID-A = 'NEW'
    OR #PHONE-ID = 0
  RESET #PHONE-ID
  PERFORM NEW-HOME-PHONE
  IF #UPDATED
    #MSG := 'PHONE-NUMBER record is added'
    INPUT WITH TEXT #MSG USING MAP 'ADRM0011'
  END-IF
ELSE
  PERFORM UI-PHONE
END-IF
*
IF #SYNCHRONIZE
  ASSIGN #CRM-REQUEST = 2   /* RE4 CRM SYNC PROCESS  Start
  CALLNAT 'MBRDATA'
    #STATUS-ISN             /* Status record ISN
    #XML
    #LINE
    #RESPONSE-2
    #CRM-REQUEST
    #CONTACT-ID             /* NG1
*
END-IF                      /* RE4 CRM SYNC PROCESS  End
/*
DEFINE NEW-HOME-PHONE
/*     ==============
RESET PHONES
ASSIGN PHONES.PHONE-TYPE-CODE = 'H'
ASSIGN PHONES.CONTACT-ID = #CONTACT-ID
PERFORM COLLECT-DATA
*
* print #USER-DATA #COUNTRY-CODE
*
REPEAT
  IF #COUNTRY-CODE = USA
    INPUT WITH TEXT #MSG MARK #POS USING MAP 'ADRM0011'
  ELSE
    INPUT WITH TEXT #MSG MARK #POS USING MAP 'ADRM0013'
  END-IF
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM
    VALUE 'ENTR'
      IF #TYPE NE BLANK
        RESET #USER-DATA
        IF #COUNTRY-CODE EQ BLANK
          #COUNTRY-CODE := USA
          #MSG := #ERRORS-TXT (6)
        ELSE
          RESET #COUNTRY-CODE
          #INTL-ACCESS-CODE := '011'
          #MSG := #ERRORS-TXT (5)
        END-IF
        RESET #TYPE
        ESCAPE TOP
      END-IF
      PERFORM INPUT-VALIDATION
    NONE VALUE IGNORE
  END-DECIDE
  UNTIL NOT #ERRORS
END-REPEAT
*
IF *PF-KEY NE 'PF3'
  CALLNAT 'ADRNT003' #PHONE-ID
  RESET PHONES
  PHONES.CONTACT-ID := #CONTACT-ID
  PHONES.PHONE-ID := #PHONE-ID
  STORE PHONES
  #PHONE-ISN := *ISN (0312)
  PERFORM DB-UPDATE
  IF #UPDATED
    BEFORE_IMAGE.PHONE-TYPE-CODE (1) := ' '                  /*  bz1
    BEFORE_IMAGE.PHONE-PREFERRED-IND (1) := ' '              /*  bz1
    #MSG := 'Record successfully added'
    PERFORM UPDATE-STATUS-FILE
    END TRANSACTION
    MOVE TRUE TO #SYNCHRONIZE                 /* RE4
    RESET #UPDATED
  ELSE
    #MSG := 'No updates'
    BACKOUT TRANSACTION
  END-IF
END-IF
*
END-SUBROUTINE
/*
DEFINE UI-PHONE
/*     ========
REPEAT
  FIND PHONES WITH PHONE-ID = #PHONE-ID
    #PHONE-ISN := *ISN (0334)
    PERFORM COLLECT-DATA
    REPEAT
      IF #COUNTRY-CODE = USA
        INPUT WITH TEXT #MSG MARK #POS USING MAP 'ADRM0011'
      ELSE
        INPUT WITH TEXT #MSG MARK #POS USING MAP 'ADRM0013'
      END-IF
      DECIDE ON FIRST VALUE OF *PF-KEY
        VALUE 'PF3'
          ESCAPE BOTTOM
        VALUE 'PF4'
          IF *CURS-LINE = 3
            CALLNAT 'GUPCISXX'
              #CONTACT-ID
              #CONTACT-ID
              #CGA-ORIG-COMMAND
              CONTACT.ID-NUMBER
              #CGA-USER-COMPANY
              #CGA-CLERK
              #MODE
              #MSG
          ELSE
            #PHONE-ID-A := 'NEW'
            CALLNAT 'ADRN0011' #CONTACT-ID #MEMBER-ID-NUMBER #PHONE-ID
          END-IF
        VALUE 'PF11'             /* RE1
          IF #PH-PREF = 'Y'
*            FIND PHONES WITH CONTACT-ID = #CONTACT-ID AND PREFERRED-PHONE-IND = 'Y'  /* RE won't stow anymore
            FIND PHONES WITH CONTACT-ID = #CONTACT-ID WHERE PREFERRED-PHONE-IND = 'Y'
            END-FIND
            IF *COUNTER(0364) = 1
              FIND NUMBER PHONES WITH CONTACT-ID = #CONTACT-ID
              IF *NUMBER (0367) > 1
                REINPUT 'Designate another preferred PHONE-NUMBER number before deleting.'
                  MARK *#PH-PREF
              END-IF
            END-IF
          END-IF
          PERFORM DELETE-RECORD
          ESCAPE ROUTINE
        VALUE 'PF12'
          CALLNAT 'GPF12SXX' #CONTACT-ID
        VALUE 'ENTR'
          IF #TYPE NE BLANK
            RESET #USER-DATA
            IF #COUNTRY-CODE EQ BLANK
              #COUNTRY-CODE := USA
              #MSG := #ERRORS-TXT (6)
            ELSE
              RESET #COUNTRY-CODE
              #INTL-ACCESS-CODE := '011'
              #MSG := #ERRORS-TXT (5)
            END-IF
*            #CONFIRM-DELETE := 'Y'      /* RE3
*            PERFORM UPDATE-STATUS-FILE  /* RE3
            RESET #TYPE #CONFIRM-DELETE
            ESCAPE TOP
          END-IF
          PERFORM INPUT-VALIDATION
        NONE VALUE IGNORE
      END-DECIDE
      UNTIL NOT #ERRORS
    END-REPEAT
    PERFORM DB-UPDATE
    IF #UPDATED
      #MSG := 'Record successfully updated'
      PERFORM UPDATE-STATUS-FILE
      END TRANSACTION
      MOVE TRUE TO #SYNCHRONIZE                 /* RE4
      RESET #UPDATED
    ELSE
      #MSG := 'No updates'
      BACKOUT TRANSACTION
    END-IF
  END-FIND
  UNTIL *PF-KEY = 'PF3'
END-REPEAT
END-SUBROUTINE

DEFINE COLLECT-DATA
/*     ============
IF PHONES.LAST-USER-UPD = BLANK
  PHONES.LAST-USER-UPD := *INIT-USER
END-IF
#CLERK := FUNCTION-NAME(< PHONES.LAST-USER-UPD >)          /* NG3
RESET #USER-DATA
IF PHONES.INTL-DIAL-NUMBER > 0
  MOVE EDITED PHONES.INTL-DIAL-NUMBER (EM=Z(20)) TO #INTL-DIAL-NUMBER-A
  MOVE LEFT #INTL-DIAL-NUMBER-A TO #INTL-DIAL-NUMBER-A
  #INTL-ACCESS-CODE := '011'
ELSE
  MOVE EDITED PHONES.AREA-CODE   (EM=ZZZ)  TO #AREA-CODE-A
  if PHONES.DIAL-NUMBER > 0
    MOVE EDITED PHONES.DIAL-NUMBER (EM=9(7)) TO #DIAL-NUMBER-A
  else
    RESET #DIAL-NUMBER-A
  end-if
  #COUNTRY-CODE := USA
END-IF
*
MOVE EDITED PHONES.EXT (EM=Z(6)) TO #EXT-A
MOVE LEFT #EXT-A TO #EXT-A
**IF *INIT-USER = 'RESTABI'
*  DISPLAY PHONES.EXT #EXT-A
**END-IF
#REMARKS (*)      := PHONES.REMARKS (*)
*
FIND (1) CONTACT WITH CONTACT-ID = #CONTACT-ID
END-FIND
CALLNAT 'ADRN0004' #CONTACT-ID #HEADER1 #HEADER2
CALLNAT 'ADRN0003'
  #CONTACT-ID
  #ADDR-POOL-ID   /* 0
  #PHONE-ID
  #EMAIL-ID       /* 0
  #CODES #CODE-NAMES
*
IF PHONES.LAST-DATE-UPD > 0
  MOVE EDITED PHONES.LAST-DATE-UPD-A TO #D (EM=YYYYMMDD)
  MOVE EDITED #D (EM=MM/DD/YYYY) TO #DATE-UPDATED
END-IF
*
IF PHONES.PREFERRED-PHONE-IND = ' '                        /* RE2 Start
  MOVE 'Y' TO #PH-PREF
ELSE
  MOVE PHONES.PREFERRED-PHONE-IND TO #PH-PREF #SAVE-PREF
END-IF                                                     /* RE2 End
*
IF #AREA-CODE > 0
  COMPRESS #DN3 '-' #DN4 INTO BEFORE_IMAGE.PHONE-NUMBER (1) LEAVING NO
  IF #EXT > 0
    COMPRESS BEFORE_IMAGE.PHONE-NUMBER (1) 'EXT' #EXT-A INTO BEFORE_IMAGE.PHONE-NUMBER (1)     /* YK1
  END-IF
  COMPRESS #AREA-CODE BEFORE_IMAGE.PHONE-NUMBER (1) INTO BEFORE_IMAGE.PHONE-NUMBER (1) /* RE3 LEAVING NO
  MOVE 'D' TO BEFORE_IMAGE.TYPE(1)
ELSE
  COMPRESS #INTL-ACCESS-CODE #INTL-DIAL-NUMBER-A INTO BEFORE_IMAGE.PHONE-NUMBER(1) LEAVING NO
  IF #EXT > 0
    COMPRESS BEFORE_IMAGE.PHONE-NUMBER (1) 'EXT' #EXT-A INTO BEFORE_IMAGE.PHONE-NUMBER(1)     /* YK1
  END-IF
* PRINT *PROGRAM *LINE '.' #INTL-ACCESS-CODE '.'
  IF #INTL-ACCESS-CODE <> ' '                     /*  bz1
    MOVE 'I' TO BEFORE_IMAGE.TYPE(1)             /*  bz1
  END-IF                                          /*  bz1
END-IF
MOVE #PH-TYPE TO BEFORE_IMAGE.PHONE-TYPE-CODE (1)
MOVE #PH-PREF TO BEFORE_IMAGE.PHONE-PREFERRED-IND (1)
*
BEFORE_IMAGE.DATE-LAST-UPDATE := PHONES.LAST-DATE-UPD
*
END-SUBROUTINE
/*
DEFINE INPUT-VALIDATION
/*     ================
RESET #ERRORS
CALLNAT 'ADRN0005' #CODES #CODE-NAMES
*
IF #COUNTRY-CODE EQ USA
  IF #AREA-CODE-A IS (N3)
    IF #AREA-CODE = 0
      #ERROR-NBR := 4
      #ERRORS := TRUE
      #MSG := #ERRORS-TXT ( #ERROR-NBR )
      #POS := POS ( #AREA-CODE-A )
      ESCAPE ROUTINE
    END-IF
  ELSE
    #ERROR-NBR := 4
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #AREA-CODE-A )
    ESCAPE ROUTINE
  END-IF
  IF #DN3 IS (N3) IGNORE
  ELSE
    #ERROR-NBR := 1
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #DN3 )
    ESCAPE ROUTINE
  END-IF
  IF #DN4 IS (N4) IGNORE
  ELSE
    #ERROR-NBR := 1
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #DN4)
    ESCAPE ROUTINE
  END-IF
  IF #DIAL-NUMBER = 0
    #ERROR-NBR := 6
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #DN3 )
    ESCAPE ROUTINE
  END-IF
  FOR #J 1 TO 7
    IF #DND ( #J ) = BLANK
      #ERROR-NBR := 1
      #ERRORS := TRUE
      #MSG := #ERRORS-TXT ( #ERROR-NBR )
      IF #J < 4
        #POS := POS ( #DN3 )
      ELSE
        #POS := POS ( #DN4 )
      END-IF
      ESCAPE ROUTINE
    END-IF
  END-FOR
ELSE
  MOVE LEFT #INTL-DIAL-NUMBER-A TO #INTL-DIAL-NUMBER-A
  IF #INTL-DIAL-NUMBER-A IS (N20) IGNORE
  ELSE
    #ERROR-NBR := 8
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #INTL-DIAL-NUMBER-A)
    ESCAPE ROUTINE
  END-IF
  IF SUBSTR ( #INTL-DIAL-NUMBER-A,7,1) = BLANK
    #ERROR-NBR := 8
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #INTL-DIAL-NUMBER )
    ESCAPE ROUTINE
  END-IF
END-IF
IF #EXT-A NE BLANK
  IF #EXT-A IS (N6) IGNORE
  ELSE
    #ERROR-NBR := 7
    #ERRORS := TRUE
    #MSG := #ERRORS-TXT ( #ERROR-NBR )
    #POS := POS ( #EXT-A )
    ESCAPE ROUTINE
  END-IF
END-IF
IF #PH-TYPE EQ ' '
  #ERROR-NBR := 2
  #ERRORS := TRUE
  #MSG := #ERRORS-TXT ( #ERROR-NBR )
  #POS := POS ( #PH-TYPE )
  ESCAPE ROUTINE
END-IF
IF #PH-PREF EQ ' '                     /* RE2 start
  #ERROR-NBR := 9
  #ERRORS := TRUE
  #MSG := #ERRORS-TXT ( #ERROR-NBR )
  #POS := POS ( #PH-PREF )
  ESCAPE ROUTINE
END-IF                                 /* RE2 end
#MSG := FUNCTION-CV-CHECK (<'013',#PH-PREF>)
IF #MSG NE BLANK
  #ERRORS := TRUE
  #POS := POS ( #PH-PREF )
  ESCAPE ROUTINE
END-IF
IF #PH-PREF = 'N'                      /* RE2 start
*  FIND NUMBER OTHER-PHONES WITH CONTACT-ID = #CONTACT-ID AND  /* RE won't stow anymore
*    PREFERRED-PHONE-IND = 'Y'
  FIND OTHER-PHONES WITH CONTACT-ID = #CONTACT-ID WHERE
      PREFERRED-PHONE-IND = 'Y'
  END-FIND
  IF *COUNTER (0596) < 2
    REINPUT 'One preferred PHONE-NUMBER number must be specified.'
      MARK *#PH-PREF
  END-IF
END-IF                                /* RE2 end
*
#MSG := FUNCTION-CV-CHECK (<'015',#PH-TYPE>)
IF #MSG NE BLANK
  #ERRORS := TRUE
  #POS := POS ( #PH-TYPE )
  ESCAPE ROUTINE
END-IF
#MSG := FUNCTION-CV-CHECK (<'014',#PH-TIME>)
IF #MSG NE BLANK
  #ERRORS := TRUE
  #POS := POS ( #PH-TIME )
  ESCAPE ROUTINE
END-IF
#MSG := FUNCTION-CV-CHECK (<'016',#PH-VALID>)
IF #MSG NE BLANK
  #ERRORS := TRUE
  #POS := POS ( #PH-VALID )
  ESCAPE ROUTINE
END-IF
END-SUBROUTINE
/*
DEFINE DB-UPDATE
/*     =========
#UPDATED := FALSE
if *PF-KEY = 'PF3'
  escape routine
end-if
*
GET PHONES #PHONE-ISN
*
IF #COUNTRY-CODE NE USA
  MOVE RIGHT #INTL-DIAL-NUMBER-A TO #INTL-DIAL-NUMBER-A
END-IF
IF PHONES.INTL-DIAL-NUMBER NE #INTL-DIAL-NUMBER
    OR PHONES.AREA-CODE        NE #AREA-CODE
    OR PHONES.DIAL-NUMBER      NE #DIAL-NUMBER
    OR PHONES.EXT              NE #EXT
    OR PHONES.REMARKS (1)      NE #REMARKS (1)
    OR PHONES.REMARKS (2)      NE #REMARKS (2)
    OR PHONES.REMARKS (3)      NE #REMARKS (3)
  IF #AREA-CODE NE 0
    PHONES.DIAL-NUMBER := #DIAL-NUMBER
    PHONES.AREA-CODE   := #AREA-CODE
    PHONES.TYPE := 'D'
    RESET PHONES.INTL-DIAL-NUMBER PHONES.INTL-ACCESS-CODE
  ELSE
    RESET PHONES.AREA-CODE PHONES.DIAL-NUMBER
    PHONES.INTL-ACCESS-CODE := 11
    PHONES.INTL-DIAL-NUMBER := #INTL-DIAL-NUMBER
    PHONES.TYPE := 'I'
  END-IF
  MOVE RIGHT #EXT-A TO #EXT-A
  PHONES.EXT := #EXT
  PHONES.REMARKS (*) := #REMARKS (*)
  #UPDATED := TRUE
*  PRINT PHONES
END-IF
*
IF PHONES.PREFERRED-PHONE-IND NE #PH-PREF              /* NG2 Start
    AND #PH-PREF = 'Y'
  FIND OTHER-PHONES WITH CONTACT-ID = #CONTACT-ID
    IF *ISN (0664) = #PHONE-ISN
      ESCAPE TOP
    END-IF
    DECIDE FOR FIRST CONDITION
      WHEN #PH-TYPE = #HOME
          AND OTHER-PHONES.PHONE-TYPE-CODE = #HOME
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN #PH-TYPE = #CELL
          AND OTHER-PHONES.PHONE-TYPE-CODE = #CELL
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN #PH-TYPE = #BUSINESS
          AND OTHER-PHONES.PHONE-TYPE-CODE = #BUSINESS
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN #PH-TYPE = #FAX (*)
          AND OTHER-PHONES.PHONE-TYPE-CODE = #FAX (*)
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN #PH-TYPE = #OTHER
          AND OTHER-PHONES.PHONE-TYPE-CODE = #OTHER
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN #PH-TYPE = #CARPHN (*)
          AND OTHER-PHONES.PHONE-TYPE-CODE = #CARPHN (*)
        MOVE 'N' TO OTHER-PHONES.PREFERRED-PHONE-IND
      WHEN ANY
        UPDATE (0664)
      WHEN NONE
        IGNORE
    END-DECIDE
  END-FIND
  PHONES.PREFERRED-PHONE-IND := #PH-PREF
  #UPDATED := TRUE
END-IF
*                                                      /* NG2 End
IF PHONES.BEST-TIME-TO-CALL NE #PH-TIME
    OR PHONES.PHONE-TYPE-CODE NE #PH-TYPE
    OR PHONES.INVALID-IND NE #PH-VALID
  PHONES.BEST-TIME-TO-CALL   := #PH-TIME
  RESET #STATUS-RESET
  PHONES.PHONE-TYPE-CODE := #PH-TYPE
  PHONES.INVALID-IND := #PH-VALID
  #UPDATED := TRUE
END-IF
*
IF #UPDATED
  move edited *DATX (EM=YYYYMMDD) TO PHONES.LAST-DATE-UPD-A
  move edited *TIMX (EM=HHIISST) TO PHONES.LAST-TIME-UPD-A
  PHONES.LAST-USER-UPD   := *USER
  UPDATE (0632)
END-IF
*
* PRINT PHONES
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE DELETE-RECORD  /* RE1
*******************************
RESET #CONFIRM-DELETE
IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>)
  IGNORE
END-IF
SET WINDOW 'Confirm-Delete'
REPEAT
  INPUT WINDOW='Confirm-Delete' USING MAP 'ADRM0012'
  IF *PF-KEY = 'PF3'
    ESCAPE BOTTOM
  END-IF
  IF #CONFIRM-DELETE = 'Y'
    FIND (1) PHONES WITH CONTACT-ID = #CONTACT-ID AND PHONE-ID = #PHONE-ID
      DELETE
      PERFORM UPDATE-STATUS-FILE
      END TRANSACTION
      MOVE 'Record successfully deleted.' TO #MSG
      MOVE TRUE TO #SYNCHRONIZE                 /* RE4
    END-FIND
    ESCAPE BOTTOM
  END-IF
END-REPEAT
SET WINDOW OFF
*
END-SUBROUTINE
*
DEFINE UPDATE-STATUS-FILE
/*     ==================
IF CONTACT.ID-NUMBER <= 0
  ESCAPE ROUTINE
END-IF
*
FIND (1) STATUS WITH ID-NUMBER = CONTACT.ID-NUMBER
  #STATUS-ISN := *ISN (0751)
END-FIND
*
RESET #F16
IF #CONFIRM-DELETE = 'Y'
  #PHONE-TYPE := #PH-TYPE
*  display #PHONE-TYPE
  PERFORM TRANS-REG-UPDATE
ELSE
*  IF #STATUS-RESET > 0                      /* RE2
*    #PHONE-TYPE := PHONES.PHONE-TYPE-CODE
*    PERFORM TRANS-REG-UPDATE
*  END-IF
  IF PHONES.AREA-CODE = 0 /* International
*    COMPRESS '11' PHONES.INTL-DIAL-NUMBER INTO #X LEAVING NO
    MOVE PHONES.INTL-DIAL-NUMBER TO #X
    #L := *LENGTH ( #X )
    DECIDE FOR FIRST CONDITION
      WHEN #L > 16
*        PRINT 'Status file doesn''t support very long numbers' #X
        MOVE SUBSTR (#X,1,16) TO #F16
      WHEN #L > 10
        #F16 := #X
      WHEN #L > 1
        #PH10 := #X
        MOVE RIGHT #PH10 TO #PH10
      WHEN NONE IGNORE
    END-DECIDE
  ELSE                    /* Domestic
    MOVE EDITED PHONES.DIAL-NUMBER (EM=9(7)) TO #DIAL-NUMBER-A
    MOVE EDITED PHONES.AREA-CODE (EM=999) TO #AREA-CODE-A
    COMPRESS #AREA-CODE #DIAL-NUMBER-A TO #PH10 LEAVING NO
    MOVE RIGHT #PH10 TO #PH10
  END-IF
  #PHONE-TYPE := #PH-TYPE
  PERFORM TRANS-REG-UPDATE
END-IF
*
* PRINT #PH10 #PH6
*
* GET STATUS #STATUS-ISN
* *
* IF #PH-PREF = 'Y'
*   DECIDE FOR FIRST CONDITION
*     WHEN #PH-TYPE = #HOME
*       TELE-NUMBER := #PH10-N
*       HOME-TELE-INTR := #PH6
*     WHEN #PH-TYPE = #BUSINESS
*       NXT-OF-KIN-TELE-NUMBER := #PH10-N
*       WORK-TELE-INTR := #PH6
*     WHEN #PH-TYPE = #FAX (*)
*       FAX-NUMBER := #PH10-N
*       FAX-NUMBER-INTR := #PH6
*     WHEN NONE IGNORE
*   END-DECIDE
* ELSE
*   IF #SAVE-PREF = 'Y'
*     DECIDE FOR FIRST CONDITION
*       WHEN #PH-TYPE = #HOME
*         RESET
*           TELE-NUMBER
*           HOME-TELE-INTR
*       WHEN #PH-TYPE = #BUSINESS
*         RESET
*           NXT-OF-KIN-TELE-NUMBER
*           WORK-TELE-INTR
*       WHEN #PH-TYPE = #FAX (*)
*         RESET
*           FAX-NUMBER
*           FAX-NUMBER-INTR
*       WHEN NONE IGNORE
*     END-DECIDE
*   END-IF
* END-IF
*
* UPDATE (0792)
*
* MOVE TRUE TO #SYNCHRONIZE                 /* RE4
END-SUBROUTINE
/*
DEFINE TRANS-REG-UPDATE
/*     ================
**CALLNAT 'ADRNE012' PHONES.CONTACT-ID #PHONE-ISN #TEXT(*)          /* RE3 START
**MOVE #TEXT(1) TO AFTER_IMAGE.PHONE-NUMBER(1)
**print '='#CONT-ID-NUMBER '='#MEMBER-ID-NUMBER
IF #CONT-ID-NUMBER > 0
  MOVE #CONT-ID-NUMBER TO TR_ID_NUMBER
ELSE
  MOVE #MEMBER-ID-NUMBER TO TR_ID_NUMBER
  FIND STATUS WITH ID-NUMBER = #MEMBER-ID-NUMBER
    IF F-CONTACT-INFO(<STATUS.MEMBER-CONTACT-ID,F-CONTACT-DATA>)
      IGNORE
    END-IF
  END-FIND
END-IF
MOVE #CONT-FMTD-NAME TO TR_NAME
MOVE #CGA-CLERK      TO TR_CLERK
IF #AREA-CODE > 0
  COMPRESS #DN3 '-' #DN4 INTO AFTER_IMAGE.PHONE-NUMBER(1) LEAVING NO
  IF #EXT > 0
    COMPRESS AFTER_IMAGE.PHONE-NUMBER(1) 'EXT' #EXT INTO AFTER_IMAGE.PHONE-NUMBER(1)
  END-IF
  COMPRESS #AREA-CODE AFTER_IMAGE.PHONE-NUMBER(1) INTO AFTER_IMAGE.PHONE-NUMBER(1) /* RE3 LEAVING NO
  MOVE 'D' TO AFTER_IMAGE.TYPE(1)
ELSE
  COMPRESS #INTL-ACCESS-CODE #INTL-DIAL-NUMBER INTO AFTER_IMAGE.PHONE-NUMBER(1) LEAVING NO
  IF #EXT > 0
    COMPRESS AFTER_IMAGE.PHONE-NUMBER(1) 'EXT' #EXT INTO AFTER_IMAGE.PHONE-NUMBER(1)
  END-IF
  MOVE 'I' TO AFTER_IMAGE.TYPE(1)
END-IF
MOVE #PH-TYPE TO AFTER_IMAGE.PHONE-TYPE-CODE (1)
MOVE #PH-PREF TO AFTER_IMAGE.PHONE-PREFERRED-IND (1)
IF #CONFIRM-DELETE = 'Y'                                           /* RE5
  MOVE '(Deleted)' TO AFTER_IMAGE.PHONE-NUMBER(1)                         /* RE5
  RESET AFTER_IMAGE.PHONE-TYPE-CODE(1)                             /* RE5
    AFTER_IMAGE.PHONE-PREFERRED-IND(1) AFTER_IMAGE.TYPE(1)         /* RE5
END-IF                                                             /* RE5
*
CALLNAT 'GPRNTR00' #PHONE TRAN_LOG_HEADER BEFORE_IMAGE AFTER_IMAGE  /* RE3   END
END-SUBROUTINE
/*
END
