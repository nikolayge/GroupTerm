* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GPRPDSCO    ** CALC ACTUAL PREMS PAID
*          called from C2320PCI/C1600PCO/C2600PST/G3500PAD/C2400PLM
*                      ATHPANNS/
* #sub-prems-paid is calculated on each entry of GPRPDSCO
* #sub-prems-paid is added to #prems-paid on each entry
*
* DRW 08/29/2008 DW1 Gen Plus Processing
* tmt 12/10/2013 TT1 1 Mil policy support project
* TMT 01/03/2014 TT2 ADDED SUPPORT FOR GP2 POLICY  
*
DEFINE DATA
PARAMETER
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 REDEFINE #PP-PAID-TO-DATE
 2 #PP-PAID-TO-DATE-YYYY (N4)
 2 #PP-PAID-TO-DATE-MM   (N2)
 2 #PP-PAID-TO-DATE-DD   (N2)
1 #PP-ISSUE-DATE     (N8)
1 REDEFINE #PP-ISSUE-DATE
 2 #PP-ISSUE-DATE-YYYY (N4)
 2 #PP-ISSUE-DATE-MM   (N2)
 2 #PP-ISSUE-DATE-DD   (N2)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)       /* TT1
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID         (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
*
LOCAL
*
1 #I1                   (I2)
1 #NUM-DAYS            (P3)
1 #PREM-FOR-DAYS       (P5.2)
1 #PREM-PD-MONS        (P5)
*
1 #DOB-YYYYMMDD        (N8)
1 REDEFINE #DOB-YYYYMMDD
 2 #DOB-YYYY           (N4)
 2 #DOB-MM             (N2)
 2 #DOB-DD             (N2)
1 REDEFINE #DOB-YYYYMMDD
 2 #DOB-YYYYMM         (N6)
 2 #DOB-FILL           (N2)
*
1 #CALC-PD-THRU-YYYY   (N4)
1 #CALC-PD-THRU-MM     (N2)
1 #MONTHLY-PREM        (P5.2)
1 #DAILY-PREM          (P3.5)
1 #W-PLAN            (A2)
1 REDEFINE #W-PLAN
 2 #W-PLAN-N2         (N2)
1 REDEFINE #W-PLAN
 2 #W-PLAN-N1         (N1)
*
END-DEFINE
*
* WRITE 'top of GPRPDSCO XTRA=PP=' #PP-TOT-PREMIUMS-PAID
COMPUTE ROUNDED #MONTHLY-PREM = #PP-ANNUAL-PREM / 12
COMPUTE ROUNDED #DAILY-PREM   = #PP-ANNUAL-PREM / 360
*
* if (#PP-PAY-STATUS GE '42')
* AND (#PP-PAY-STATUS NE '44')
IF  (#PP-PAY-STATUS = '41' OR = '42' OR = '47')
 OR ((#PP-PAY-STATUS = '48')
   AND (#PP-TOT-PREMIUMS-PAID NE 0)) /* if missing calc up to pd-to-dt
 OR (#PP-PAY-STATUS GT '48')
  RESET #NUM-DAYS
  RESET #PREM-FOR-DAYS
  IF   (#PP-PAY-STATUS = '47' OR = '48')
    MOVE #PP-TOT-PREMIUMS-PAID TO #SUB-PREMS-PAID
    ADD #SUB-PREMS-PAID TO #PREMS-PAID
    RESET #PREM-PD-MONS
  ELSE
    IF #PP-PAY-STATUS = '45'   /* RPU never now processed here
      MOVE #PP-PAID-TO-DATE-YYYY TO #CALC-PD-THRU-YYYY
      MOVE #PP-PAID-TO-DATE-MM   TO #CALC-PD-THRU-MM
*
      IF #PP-ISSUE-DATE-MM GT #CALC-PD-THRU-MM
        ADD 12 TO #CALC-PD-THRU-MM
        SUBTRACT 1 FROM #CALC-PD-THRU-YYYY
      END-IF
*
      SUBTRACT #PP-ISSUE-DATE-YYYY FROM #CALC-PD-THRU-YYYY
      SUBTRACT #PP-ISSUE-DATE-MM   FROM #CALC-PD-THRU-MM
      COMPUTE #PREM-PD-MONS
                          = (#CALC-PD-THRU-YYYY * 12) + #CALC-PD-THRU-MM
*
      IF #PP-MODE = 'G'     /* ALWAYS PAID THRU CURRENT MONTH
        ADD 1 TO #PREM-PD-MONS
      END-IF
    ELSE
      IF #PP-PLAN = 'OL' or = 'GPL' OR = 'GP2'          /* TT2         /* DW1
        MOVE #PP-DATE-OF-BIRTH TO #DOB-YYYYMMDD
*
        IF   (#PP-ISSUE-DATE GE 19970101)
         AND (#PP-MONTH-ALLOC-AMT NE 999.99)
          COMPUTE #CALC-PD-THRU-YYYY = #DOB-YYYY + 100
        ELSE
          COMPUTE #CALC-PD-THRU-YYYY = #DOB-YYYY + 96
        END-IF
        MOVE #DOB-MM TO #CALC-PD-THRU-MM
*
        IF #PP-ISSUE-DATE-MM GT #CALC-PD-THRU-MM
          ADD 12 TO #CALC-PD-THRU-MM
          SUBTRACT 1 FROM #CALC-PD-THRU-YYYY
        END-IF
*
        SUBTRACT #PP-ISSUE-DATE-YYYY FROM #CALC-PD-THRU-YYYY
        SUBTRACT #PP-ISSUE-DATE-MM   FROM #CALC-PD-THRU-MM
        COMPUTE #PREM-PD-MONS
                          = (#CALC-PD-THRU-YYYY * 12) + #CALC-PD-THRU-MM
      ELSE
        MOVE #PP-PLAN TO #W-PLAN
        IF #W-PLAN = MASK (NN)
          COMPUTE #PREM-PD-MONS = (#W-PLAN-N2 * 12)
        ELSE
          IF #W-PLAN = MASK (N'P')
            COMPUTE #PREM-PD-MONS = (#W-PLAN-N1 * 12)
          END-IF
        END-IF
      END-IF
    END-IF
*
    IF  (#PP-ISSUE-DATE LT 19970101)   /* bypass 1997 pols (nxt)
     OR (#PP-MONTH-ALLOC-AMT = 999.99) /* only old & new
      IF   (#PP-INTRO-FLAG NE 'I')    /* REG MEMBERS GET 1 FREE MONTH
       AND (#I1 = 1)                /* ONLY ON 1ST POLICY
        SUBTRACT 1 FROM #PREM-PD-MONS
      END-IF
    END-IF
*
    COMPUTE #SUB-PREMS-PAID = #MONTHLY-PREM * #PREM-PD-MONS
       + #PP-TOT-PREMIUMS-PAID
*
    IF #PP-PAY-STATUS = '45'    /* RPU never now processed here
      IF  (#PP-ISSUE-DATE LT 19970101)   /* bypass 1997 pols
       OR (#PP-MONTH-ALLOC-AMT = 999.99)
*     IF  (#PP-ISSUE-DATE LT 19860701)  /* CALC 1ST PLACE FEE
*      AND (#PP-ISSUE-DATE-DD NE 1)
        SUBTRACT 1 FROM #PREM-PD-MONS
        SUBTRACT #MONTHLY-PREM FROM #SUB-PREMS-PAID
        COMPUTE #NUM-DAYS = (30 - #PP-ISSUE-DATE-DD) + 1
        COMPUTE ROUNDED #PREM-FOR-DAYS = #DAILY-PREM * #NUM-DAYS
        ADD #PREM-FOR-DAYS TO #SUB-PREMS-PAID
      END-IF
    END-IF
*
    ADD #SUB-PREMS-PAID TO #PREMS-PAID
*   ADD #PP-COST-BASIS  TO #PREMS-PAID /* removed 9/98
  END-IF
*
  SUBTRACT #PP-FIRST-PLACE-FEE FROM #SUB-PREMS-PAID
  SUBTRACT #PP-FIRST-PLACE-FEE FROM #PREMS-PAID
* SUBTRACT #PP-BADJ            FROM #SUB-PREMS-PAID /* REmoved 12/7/2004
* SUBTRACT #PP-BADJ            FROM #PREMS-PAID
*
  ESCAPE ROUTINE
END-IF
*
MOVE #PP-PAID-TO-DATE-YYYY TO #CALC-PD-THRU-YYYY
MOVE #PP-PAID-TO-DATE-MM   TO #CALC-PD-THRU-MM
*
IF #PP-ISSUE-DATE-MM GT #CALC-PD-THRU-MM
  ADD 12 TO #CALC-PD-THRU-MM
  SUBTRACT 1 FROM #CALC-PD-THRU-YYYY
END-IF
*
SUBTRACT #PP-ISSUE-DATE-YYYY FROM #CALC-PD-THRU-YYYY
SUBTRACT #PP-ISSUE-DATE-MM   FROM #CALC-PD-THRU-MM
COMPUTE #PREM-PD-MONS = (#CALC-PD-THRU-YYYY * 12) + #CALC-PD-THRU-MM
* DISPLAY #PREM-PD-MONS #CALC-PD-THRU-YYYY #CALC-PD-THRU-MM
*
IF   (#PP-ISSUE-DATE GE 19970101) /* nxt pols use full months calcs
 AND (#PP-MONTH-ALLOC-AMT NE 999.99)
 AND (#PP-ISSUE-DATE-DD = 01)
  ADD 1 TO #PREM-PD-MONS
END-IF
*
COMPUTE #SUB-PREMS-PAID = (#MONTHLY-PREM * #PREM-PD-MONS)
       + #PP-TOT-PREMIUMS-PAID
*   WRITE 'sub-pp=' #SUB-PREMS-PAID #MONTHLY-PREM #PREM-PD-MONS
*             #PP-TOT-PREMIUMS-PAID
*
RESET #NUM-DAYS
RESET #PREM-FOR-DAYS
*
IF  (#PP-ISSUE-DATE LT 19970101)   /* bypass 1997 pols
 OR (#PP-MONTH-ALLOC-AMT = 999.99)
  IF ((#PP-INTRO-FLAG = 'I') /* INTRO MBRS PAID 1ST MONTH
   AND (#I1 = 1))              /* ONLY ON 1ST POLICY
    OR (#PP-INTRO-FLAG = 'B' OR = 'C'  /* CAPS PAY 1ST MONTH
                    OR = 'P' OR = 'Q') /* CAPS PAY 1ST MONTH
    ADD #MONTHLY-PREM TO #SUB-PREMS-PAID
    ADD 1 TO #PREM-PD-MONS     /* these OLD/NEW must pay the 1st month
  END-IF   /* ALL ELSE GET 1 FREE MONTH
*
  IF #PP-ISSUE-DATE-DD NE 1   /* calc final few days
    SUBTRACT #MONTHLY-PREM FROM #SUB-PREMS-PAID
    SUBTRACT 1 FROM #PREM-PD-MONS
    COMPUTE #NUM-DAYS = (30 - #PP-ISSUE-DATE-DD) + 1
    COMPUTE ROUNDED #PREM-FOR-DAYS = #DAILY-PREM * #NUM-DAYS
    ADD #PREM-FOR-DAYS TO #SUB-PREMS-PAID
  END-IF
END-IF
*
ADD #SUB-PREMS-PAID TO #PREMS-PAID
* ADD #PP-COST-BASIS TO #PREMS-PAID   /* removed 9/98
*
* IF #PP-PAY-STATUS = '12' OR = '20'  /* replaced by below
*   SUBTRACT #PP-FIRST-PLACE-FEE FROM #PREMS-PAID
* END-IF
*
SUBTRACT #PP-FIRST-PLACE-FEE FROM #SUB-PREMS-PAID
SUBTRACT #PP-FIRST-PLACE-FEE FROM #PREMS-PAID
* SUBTRACT #PP-BADJ            FROM #SUB-PREMS-PAID /* REmoved 12/7/2004
* SUBTRACT #PP-BADJ            FROM #PREMS-PAID
*  DISPLAY 'DOB' #PP-DATE-OF-BIRTH 'PLAN'#PP-PLAN
*    'STAT' #PP-PAY-STATUS
*    'PP-TPP' #PP-TOT-PREMIUMS-PAID
*    'PDT'  #PP-PAID-TO-DATE
*    'iss'  #PP-ISSUE-DATE
*    'apr'  #PP-ANNUAL-PREM
*    'prems/-paid'  #PREMS-PAID
*   'FPF'          #PP-FIRST-PLACE-FEE
*   'BADJ'         #PP-BADJ
*
END
