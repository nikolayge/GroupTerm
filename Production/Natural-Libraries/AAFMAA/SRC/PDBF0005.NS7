* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* FUNCTION OBJECT: MBRFPN
* Returns product name
*
DEFINE FUNCTION FUNCTION-PRODUCT-2 RETURNS (A/2) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #Product-code     (A3) 1 REDEFINE #Product-code
    2 #PLAN-1   (A1)
    2 #PLAN-2   (A2)
  1 NICOTINE    (A1) OPTIONAL
  LOCAL
  1 #L    (I2)
  1 #C    (I2)
  1 #LTIIP  (A1/3)       CONST < 'Q','R','S' >
  1 #LTII   (A1/3)       CONST < 'W','V','U' >
  1 #LTII-RIDER   (A1/3) CONST < 'X','Y','Z'>
  1 #LTIIP-TEXT (A/6) DYNAMIC CONST
    <
    'Level Term II+ @@ year term (Super Select)',
    'Level Term II+ @@ year term (Select)',
    'Level Term II+ @@ year term (Standard)'
    >
  1 #LTII-TEXT (A/6) DYNAMIC CONST
    <
    'Level Term II @@ year term (Super Select)',
    'Level Term II @@ year term (Select)',
    'Level Term II @@ year term (Standard)'
    >
  1 #LTII-RIDER-TEXT (A/6) DYNAMIC CONST
    <
    'Level Term II @@ year term (Super Select) + Rider',
    'Level Term II @@ year term (Select) + Rider',
    'Level Term II @@ year term (Standard) + Rider'
    >
  1 #PCodes (A3/15) CONST <'LT','RT','GPL','OL','30P','20P','7P','SP','EZ','SPF','SO','AL'  >
  1 #Product (A/15) DYNAMIC CONST
    <
    'Level Term I',
    'Five-Year Renewable Term',
    'Generations Plus',
    'Value-Added Whole Life Pay for life',
    'Value-Added Whole Life Pay for 30 years',
    'Value-Added Whole Life Pay for 20 years',
    'Value-Added Whole Life Pay for 7 years',
    'Value-Added Whole Life Single Payment',
    'EZ Term',
    'Paid-Up Whole Life',
    'Service Only',
    'Annuity Life'
    >
  1 #VAWL  (A22) INIT <'Value-Added Whole Life'>
  1 #TO50  (A9)  CONST <'To Age 50'>
  1 #TO60  (A9)  CONST <'To Age 60'>

  END-DEFINE
*
RESET FUNCTION-PRODUCT-2 (*)
*
DECIDE FOR FIRST CONDITION
  WHEN #PLAN-1 = #LTIIP (*) AND #PLAN-2 IS (N2)
    EXAMINE #LTIIP (*) FOR #PLAN-1 INDEX #L
    FUNCTION-PRODUCT-2 (2) := #LTIIP-TEXT (#L)
    EXAMINE FUNCTION-PRODUCT-2 (2) FOR '@@' REPLACE WITH #PLAN-2
    FUNCTION-PRODUCT-2 (1) := 'Level Term II+'
  WHEN #PLAN-1 = #LTII (*) AND #PLAN-2 IS (N2)
    EXAMINE #LTII (*) FOR #PLAN-1 INDEX #L
    FUNCTION-PRODUCT-2 (2) := #LTII-TEXT (#L)
    EXAMINE FUNCTION-PRODUCT-2 (2) FOR '@@' REPLACE WITH #PLAN-2
    FUNCTION-PRODUCT-2 (1) := 'Level Term II'
  WHEN #PLAN-1 = #LTII-RIDER (*) AND #PLAN-2 IS (N2)
    EXAMINE #LTII-RIDER (*) FOR #PLAN-1 INDEX #L
    FUNCTION-PRODUCT-2 (2) := #LTII-RIDER-TEXT (#L)
    EXAMINE FUNCTION-PRODUCT-2 (2) FOR '@@' REPLACE WITH #PLAN-2
    FUNCTION-PRODUCT-2 (1) := 'Level Term II'
  WHEN NONE
    EXAMINE #PCodes (*) FOR #Product-code INDEX #L
    IF #L > 0
      FUNCTION-PRODUCT-2 (2) := #Product (#L)
      EXAMINE FUNCTION-PRODUCT-2 (2) FOR #VAWL INDEX #L
      IF #L > 0
        FUNCTION-PRODUCT-2 (1) := #VAWL
      ELSE
        FUNCTION-PRODUCT-2 (1) := FUNCTION-PRODUCT-2 (2)
      END-IF
      IF FUNCTION-PRODUCT-2 (2) = #Product (1)
        IF NICOTINE SPECIFIED AND NICOTINE = 'S'
          COMPRESS FUNCTION-PRODUCT-2 (2) #TO50 INTO FUNCTION-PRODUCT-2 (2)
        ELSE
          COMPRESS FUNCTION-PRODUCT-2 (2) #TO60 INTO FUNCTION-PRODUCT-2 (2)
        END-IF
      END-IF
    ELSE
      IF #Product-code = MASK(NN'F') OR = MASK (N'PF')
        FUNCTION-PRODUCT-2 (1) := FUNCTION-PRODUCT-2 (2) := 'Flex Plan'
      ELSE
        FUNCTION-PRODUCT-2 (1) := FUNCTION-PRODUCT-2 (2) := 'Unsupported'
      END-IF
    END-IF
END-DECIDE
END-FUNCTION
END
