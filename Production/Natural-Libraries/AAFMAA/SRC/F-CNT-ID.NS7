* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE FUNCTION F-GET-CONTACT-ID RETURNS (N8) 
  DEFINE DATA 
  PARAMETER 
  1 #ID-NUMBER (N6) 
  LOCAL USING GPRA0003      /* Error Log 
  LOCAL 
  1 CT-V VIEW OF A-CONTACTS 
    2 ID-NUMBER 
    2 CONTACT-ID 
  1 ST-V VIEW OF A-STATUS 
    2 ID-NUMBER 
    2 MEMBER-CONTACT-ID 
  END-DEFINE 
* 
IF #ID-NUMBER = 999999    /* Exclude Member Center monitoring tool
  ESCAPE ROUTINE
END-IF
*  
RESET F-GET-CONTACT-ID 
FIND(1) CT-V WITH ID-NUMBER = #ID-NUMBER 
  IF NO RECORDS FOUND 
    COMPRESS 'Contact record does not exist for CN' #ID-NUMBER INTO ERROR-MESSAGE 
    ERROR-PROGRAM := *PROGRAM 
    CALLNAT 'GPRN0003' ERROR-LOG 
    ESCAPE BOTTOM 
  END-NOREC 
  IF CONTACT-ID = 0 
    COMPRESS 'Contact ID = 0 on Contact file for CN' #ID-NUMBER INTO ERROR-MESSAGE 
    ERROR-PROGRAM := *PROGRAM 
    CALLNAT 'GPRN0003' ERROR-LOG 
  ELSE 
    F-GET-CONTACT-ID := CONTACT-ID 
    ESCAPE ROUTINE 
  END-IF   
END-FIND 
FIND(1) ST-V WITH ID-NUMBER = #ID-NUMBER 
  IF NO RECORDS FOUND 
    COMPRESS 'CN' #ID-NUMBER 'does not exist on Status file' INTO ERROR-MESSAGE 
    ERROR-PROGRAM := *PROGRAM 
    CALLNAT 'GPRN0003' ERROR-LOG 
    ESCAPE BOTTOM 
  END-NOREC 
  IF ST-V.MEMBER-CONTACT-ID = 0 
    COMPRESS 'Contact ID = 0 on Status file for CN' #ID-NUMBER INTO ERROR-MESSAGE 
    ERROR-PROGRAM := *PROGRAM 
    CALLNAT 'GPRN0003' ERROR-LOG 
  ELSE 
    F-GET-CONTACT-ID := ST-V.MEMBER-CONTACT-ID 
    ESCAPE ROUTINE 
  END-IF 
END-FIND 
* 
END-FUNCTION 
* 
END 
