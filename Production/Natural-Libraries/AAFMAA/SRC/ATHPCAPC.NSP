* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
** PROGRAM-ID: ATHPCAPC - CAP COUNTS FOR STATISTICS
*                                   THIS RUNS NIGHTLY IN DSDCTH10
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  RESET DISPLAY FIELDS FOR GRANKSXX                 *
* DRW  06172005 DW2  SHOW 2005 CAP STATS                               *
* DRW  02022006 DW3  CHG OLD CAP LOAN DATE TEST TO 4 YEARS             *
* DRW  02022006 DW4  INCREASE CAP TABLE TO 12 YEARS                    *
* DRW  12042006 DW5  USE CAP ENTRY-DATE INSTEAD OF ISSUE DATE FOR STATS*
* YAK  05172007 YK1  CHANGE LOGIC TO CALCULATE NUMBER OF CAP POLICIES  *
*                    INCLUDE ONLY NEW MEMBERS.TAKE ORIGINAL NUMBER FROM*
*                    NOTE RECORD 800106(SAME AS PROSPECITVE CAP SCREEN)*
* YAK  06152007 YK2  Change program to use one map instead of creating *
*                    a new map for each year.                          *
* RSE  09212009 RE1  Change PROCESS-IND logic for Unification          *
* JNT  09172013 JT1  Address normalization                             *
* TMT  07092014 TT1  Contact Normalization                             *
* vxt  01012016 vt1  fix 1316 error in Production                      *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING G3500AAD
LOCAL
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER
  2 NOTE-DATE (1)
  2 NOTE-CLERK-ID (1)
  2 NOTE-TEXT (A60/90)   /* 3600 CHARACTERS     /* vt1 was a60/60
  2 REDEFINE NOTE-TEXT  /* CAP STATS DEVELOPED 4/1999 IN REC 800109
    3 CAP-STATS-BY-YEAR  (30) /* WAS 10 NOW 12 YS = 1996 THRU 2007     /* YK2 change to 20  /* vt1 was 20
      4 CAP-STATS-BY-MONTH (12)        /* 15 * 12 * 12 = 2160  DW4
        5 CAP-ORIG           (N3) /* 3
        5 CAP-WITH-LOAN      (N3) /* 3
        5 CAP-LN-PD-OFF      (N3) /* 3
        5 CAP-LN-PD-&-LEFT   (N3) /* 3
        5 CAP-LN-PD-&-STAYED (N3) /* 3 = 15
*
1 NT-V1 VIEW OF A-NOTES          /* YK1
  2 ID-NUMBER
  2 NOTE-DATE  (N8/1)
  2 NOTE-TEXT  (A60/40)
*
  2 REDEFINE NOTE-TEXT
    3 CAPS-CONV-BY-YEAR  (10)    /* 10 PAST YEARS
      4 CAPS-CONV-BY-ORIGIN (13)  /* 13TH IS FOR UNKNOWN
        5 CAPS-CONV-ORIG   (N5)
*
    3 CAPS-CONV-BY-MON  (12)    /* 12 LAST MONTHS (JAN THRU DEC)
      4 CAPS-CONV-MON    (N5)
*
    3 ATS-CONV-BY-FU  (5)
      4 ATS-CONV-FU      (N5)
*
    3 CAPS-AT-M-S (N5)
    3 CAPS-AT-P1 (N5)
    3 CAPS-AT-P2 (N5)
    3 CAPS-AT-P3 (N5)
    3 CAPS-AT-P4 (N5)
    3 CAPS-AT-U  (N5)
    3 CAPS-AT-W  (N5)
    3 CAPS-AT-D  (N5)
    3 CAPS-AT-A  (N5)
    3 CAPS-AT-UNUSED-1 (N5)
    3 CAPS-AT-UNUSED-2 (N5)
    3 CAPS-AT-NCO (N5)
    3 CAPS-AT-OFF (N5)
*
    3 CAPS-IM-S  (N5)
    3 CAPS-IM-M  (N5)
    3 CAPS-IM-I  (N5)
    3 CAPS-IM-C  (N5)
    3 CAPS-IM-P  (N5)
    3 CAPS-IM-X  (N5)
    3 CAPS-IM-Y  (N5)
    3 CAPS-IM-Z  (N5)
*
    3 AT-M-S-0FU  (N5)
    3 AT-M-S-1FU  (N5)
    3 AT-M-S-2FU  (N5)
    3 AT-M-S-3FU  (N5)
    3 AT-M-S-4FU  (N5)
    3 AT-P      (N5)
    3 AT-U      (N5)
    3 AT-W      (N5)
    3 AT-A      (N5)
*
    3 AT-SPS    (N5)
    3 AT-CHILD  (N5)
    3 AT-GRANDCHILD (N5)
*
    3 CAP-MON-BY-MON-ADDL (12)
      4 CAPS-CONV-MON-A  (N5)        /* YK1
      4 CAPS-CONV-MON-GR (N5)        /* YK1
*
1 #LAST-RECOUNT (A11)
1 #DATN-DISP    (A11)
*
1 #CAP-STATS   (30)   /* TO EVENTUALLY HANDLE 12 YEARS    DW4    /* vt1  was 20
  2 #CAP-BY-SERV      (N5/2) /* ARMY/AIRF
  2 #CAP-BY-RANK      (N5/3) /* OFF/NCO/WO
  2 #CAP-BY-PLAN      (N5/2) /* WL/TERM
  2 #CAP-BY-SERV-RANK (N5/4) /* ARMY OFF-WO/ARMY NCO/AIRF OFF-WO/AIRF NCO
  2 #CAP-BM-WLN       (N3/12)/* JAN/FEB/ETC   BY MON STILL HAS LOAN
  2 #CAP-BM-NOL       (N3/12)/* JAN/FEB/ETC   BY MON PD OFF LOAN
  2 #CAP-BM-TRM       (N3/12)/* JAN/FEB/ETC   BY MON TERMINATED
  2 #CAP-BM-NOLC      (N3/12)/* JAN/FEB/ETC   BY MON PD OF W/CHG
  2 #CAP-BM-LLTR      (N3/12)/* JAN/FEB/ETC   BY MON PD OFF LFT LATER
  2 #WL-PCT   (N3.1/12)  /* PERCENTS CALCED ONLINE
  2 #NL-PCT   (N3.1/12)
  2 #TM-PCT   (N3.1/12)
  2 #NLC-PCT  (N3.1/12)
  2 #LLTR-PCT (N3.1/12)  /* 0504- NEW FLD - PAID AND LEFT LATER
  2 #WL-PCT-YR   (N3.1)  /* TOT-FOR YEAR PERCENTS CALCED ONLINE
  2 #NL-PCT-YR   (N3.1)
  2 #TM-PCT-YR   (N3.1)
  2 #NLC-PCT-YR  (N3.1)
  2 #LLTR-PCT-YR (N3.1)  /* 0504- NEW FLD - PAID AND LEFT LATER
  2 #TOT-ORIG (N5)
  2 #TOT-WLN  (N5)
  2 #TOT-NOL  (N5)
  2 #TOT-TRM  (N5)
  2 #TOT-NOLC (N5)
  2 #TOT-LLTR (N5)   /*  0504- NEW FLD - PAID AND LEFT LATER
  2 #TOT-FACE-OCC  (12)
    3 #TOT-FACE (N3/10)  /* 10K/50K/100K/150K/200K/250K/300K/350K/400K
* Online Fields                               /* YK2 Start
1 #SCR-CAP-STATS
  2 #SCR-CAP-ORIG  (N3/12)
  2 #SCR-CAP-WITH-LOAN (N3/12)
  2 #SCR-CAP-LN-PD-OFF (N3/12)
  2 #SCR-CAP-LN-PD-LEFT (N3/12)
  2 #SCR-WL-PCT    (N3.1/12)  /* PERCENTS CALCED ONLINE
  2 #SCR-NL-PCT    (N3.1/12)
  2 #SCR-TM-PCT    (N3.1/12)
  2 #SCR-TOT-ORIG  (N5)
  2 #SCR-TOT-WLN   (N5)
  2 #SCR-TOT-NOL   (N5)
  2 #SCR-TOT-TRM   (N5)
  2 #SCR-WL-PCT-YR (N3.1 )    /* TOT-FOR YEAR PERCENTS CALCED ONLINE
  2 #SCR-NL-PCT-YR (N3.1)
  2 #SCR-TM-PCT-YR (N3.1)                     /* YK2 end

*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-CONTACT-ID                             /* TT1
  2 PROCESS-IND
  2 WIDOW-FLAG          (A1)
  2 NAME
  2 MBR-SINCE
  2 CAP-ENTRY-DATE
  2 REDEFINE CAP-ENTRY-DATE
    3 CAP-ENTRY-DATE-YYYY  (N4)
    3 CAP-ENTRY-DATE-MM    (N2)
  2 LAST-MBRSHIP-PHONE-DT
  2 MEMBERSHIP-DRIVE
  2 REDEFINE MEMBERSHIP-DRIVE
    3 PC-1        (A1)
    3 PC-2        (A1)
    3 PC-3        (A1)
    3 PC-4        (A1)
    3 PC-5        (A1)
    3 PC-6        (A1)
  2 INTRO-FLAG
  2 ALOT-AMT
  2 APPL-SOURCE
  2 X-LOAN-AMOUNT (9)
  2 X-LOAN-DATE   (9)
  2 REDEFINE X-LOAN-DATE
    3 X-LOAN-OCC (9)
      4 LD-YYYY    (N4)
      4 LD-MM      (N2)
      4 LD-DD      (N2)
*   2 ZIP-CODE                                     /* JT1
  2 RANK                (A3)
  2 MILITARY-SERVICE    (A3)
*   2 DATE-OF-BIRTH       (N8)           /* TT1
*   2 DATE-OF-BIRTH-SP                   /* TT1
  2 DATE-OF-MARRIAGE    (N8)
  2 STATUS-CODE-SP
  2 NAME-SP
  2 NUMBER-MEMBERSHIPS
  2 ISSUE-DATE          (N8/9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC     (9)
      4 ID-YYYYMM  (N6)
      4 REDEFINE ID-YYYYMM
        5 ID-YYYY    (N4)
        5 ID-MM      (N2)
      4 ID-DD      (N2)
  2 FACE-AMOUNT         (9)
  2 PLAN                (9)
  2 STATUS              (9)
  2 CONV-DURATION       (9)
  2 MODE                (9)
1 FM-V VIEW OF A-INVESTMENTS
  2 C*INV-GEN-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL-2
*
1 #COUNTERS
  2 #CNT              (P5)
  2 #CNT-1            (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #CNT-4            (P5)
  2 #CNT-5            (P5)
  2 #CNT-6            (P5)
  2 #CNT-7            (P5)
  2 #CNT-8            (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #AAFMAA-CAP-CNT   (P5)
  2 #WES-CAP-CNT      (P5)
*
1 #FORMATED-NAME-DEAR (A35)
1 #FORMAT-CN      (A8)
1 #SCR-DATN     (A11)
1 #CURR-YEAR-X  (I2)
*
1 #MONTHS (A36) INIT <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON (A3/12)
*
1 #OLD-CAP-LN-DT    (N8)
1 REDEFINE #OLD-CAP-LN-DT
  2 #OLD-CAP-LN-DT-YYYYMM  (N6)
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM  (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY   (N4)
    3 #MM     (N2)
  2 #DD      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A  (A4)
  2 #MM-A    (A2)
  2 #DD-A    (A2)
*
1 #UPDATE     (L)
1 #HAS-FD     (L)
1 #NT-NOT-FOUND   (L)
1 #HAS-999        (L)
1 #CAP-NOT-G      (L)
1 #RANK-X       (I2)
1 #SRV-RANK-X   (I2)
1 #SERV-X       (I2)
1 #PLAN-X       (I2)
1 #YEAR-X       (I2)
1 #MON-X        (N2)
1 REDEFINE #MON-X
  2 #MON-X-A     (A2)
1 #ISN          (P8)
1 #SRV-X        (I2)
1 #CAP-X        (I2)
1 #COMMAND    (A2) INIT <'BA'>
1 #INVALID    (L)
1 #RANK-DISPL (P2)
1 #RANK-LIT   (A23)
1 #RANK-DISPLAY (A5)
1 #TEXT   (A75)
*
1 #PCT-DEC   (N1.3)
1 #YEAR      (N4)
1 #CAP-NCOS-TOT      (P5)
1 #CAP-NCOS-HAS-LOAN (P5)
1 #CAP-NCOS-STAYED   (P5)
1 #CAP-NCOS-LEFT     (P5)
1 #ALOT-NO-CHG       (P5)
1 #MAR-NO-DATE    (N5)
1 #MAR-BEFORE     (N5)
1 #MAR-CNT        (N5/10)
1 #MAR-YRS        (N8)
1 #ISS-DT         (N8)
1 #CAP-PLUS1-DATE (N6)
1 REDEFINE #CAP-PLUS1-DATE
  2 #CAP-PLUS1-YYYY  (N4)
  2 #CAP-PLUS1-MM    (N2)
1 #SPOUSE-CN           (N8.0)               /* TT1  
1 #DATE-OF-BIRTH-SP    (N8.0)               /* TT1
* * * * * * * * * * * * * * * * * * * * * * *
END-DEFINE
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #DATN-DISP
#CURR-YEAR-X := #YYYY - 1995                       /* YK2
IF *DEVICE NE 'BATCH'
  IF #CGA-SCR-SUFFIX = 'CAP'
    FIND (1) NT-V WITH ID-NUMBER = 800109
      FOR #I1 1 TO #CURR-YEAR-X                    /* YK2
        FOR #I2 1 TO 12
          IF (#I1 = 1) AND (#I2 LT 6) /* BYPASS JAN THRU JUN 1996
            ESCAPE TOP
          END-IF
*
          ADD CAP-ORIG           (#I1, #I2) TO #TOT-ORIG (#I1)
          ADD CAP-WITH-LOAN      (#I1, #I2) TO #TOT-WLN  (#I1)
          ADD CAP-LN-PD-OFF      (#I1, #I2) TO #TOT-NOL  (#I1)
          ADD CAP-LN-PD-&-STAYED (#I1, #I2) TO #TOT-NOLC (#I1)
          ADD CAP-LN-PD-&-LEFT   (#I1, #I2) TO #TOT-TRM  (#I1)
*
          IF  (CAP-ORIG (#I1, #I2)    NE 0)
              OR (CAP-WITH-LOAN (#I1, #I2) NE 0)
            COMPUTE ROUNDED #PCT-DEC = CAP-WITH-LOAN (#I1, #I2)
              / CAP-ORIG (#I1, #I2)
            COMPUTE #WL-PCT (#I1, #I2) = #PCT-DEC * 100
            COMPUTE ROUNDED #PCT-DEC = CAP-LN-PD-OFF (#I1, #I2)
              / CAP-ORIG (#I1, #I2)
            COMPUTE #NL-PCT (#I1, #I2) = #PCT-DEC * 100
            COMPUTE ROUNDED #PCT-DEC = CAP-LN-PD-&-STAYED (#I1, #I2)
              / CAP-ORIG (#I1, #I2)
            COMPUTE #NLC-PCT (#I1, #I2) = #PCT-DEC * 100
            COMPUTE ROUNDED #PCT-DEC = CAP-LN-PD-&-LEFT (#I1, #I2)
              / CAP-ORIG (#I1, #I2)
            COMPUTE #TM-PCT (#I1, #I2) = #PCT-DEC * 100
          END-IF
        END-FOR
*
        COMPUTE ROUNDED #PCT-DEC = #TOT-WLN  (#I1) / #TOT-ORIG (#I1)
        COMPUTE #WL-PCT-YR (#I1) = #PCT-DEC * 100
        COMPUTE ROUNDED #PCT-DEC = #TOT-NOL  (#I1) / #TOT-ORIG (#I1)
        COMPUTE #NL-PCT-YR (#I1) = #PCT-DEC * 100
        COMPUTE ROUNDED #PCT-DEC = #TOT-NOLC (#I1) / #TOT-ORIG (#I1)
        COMPUTE #NLC-PCT-YR (#I1) = #PCT-DEC * 100
        COMPUTE ROUNDED #PCT-DEC = #TOT-LLTR (#I1) / #TOT-ORIG (#I1)
        COMPUTE ROUNDED #PCT-DEC = #TOT-TRM  (#I1) / #TOT-ORIG (#I1)
        COMPUTE #TM-PCT-YR (#I1) = #PCT-DEC * 100
      END-FOR
*
      MOVE NOTE-DATE (1) TO #YYYYMMDD
      COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #LAST-RECOUNT
*
      REPEAT                                    /* YK2 Start
        FOR #I1 = #CURR-YEAR-X TO 1 STEP -1
*
          MOVE '*' TO #CGA-SCR-COMMAND
          IF #I1 = #CURR-YEAR-X
            MOVE 'Press ENTER for next years CAP stats' TO #TEXT
          ELSE
            MOVE 'Press ENTER for next years CAP stats or "-" for previous'
              TO #TEXT
          END-IF
* Populate screen fields                         /* YK2
          #YEAR := 1995 + #I1
          #SCR-CAP-ORIG (*)      := CAP-ORIG (#I1,*)
          #SCR-CAP-WITH-LOAN (*) := CAP-WITH-LOAN (#I1,*)
          #SCR-CAP-LN-PD-OFF (*) := CAP-LN-PD-OFF (#I1,*)
          #SCR-CAP-LN-PD-LEFT(*) := CAP-LN-PD-&-LEFT(#I1,*)
          #SCR-WL-PCT (*)        := #WL-PCT (#I1,*)
          #SCR-NL-PCT (*)        := #NL-PCT (#I1,*)
          #SCR-TM-PCT (*)        := #TM-PCT (#I1,*)
          #SCR-TOT-ORIG          := #TOT-ORIG (#I1)
          #SCR-TOT-WLN           := #TOT-WLN  (#I1)
          #SCR-TOT-NOL           := #TOT-NOL  (#I1)
          #SCR-TOT-TRM           := #TOT-TRM  (#I1)
          #SCR-WL-PCT-YR         := #WL-PCT-YR (#I1)
          #SCR-NL-PCT-YR         := #NL-PCT-YR (#I1)
          #SCR-TM-PCT-YR         := #TM-PCT-YR (#I1)
          INPUT TEXT #TEXT USING MAP 'ATHMCAPC'
*
          IF #CGA-SCR-COMMAND = MASK ('-')
            ESCAPE BOTTOM
          END-IF
*
          IF #CGA-SCR-COMMAND NE '*'
            FETCH 'G1000PXX'
          END-IF
        END-FOR
        IF #I1 = 0
          ESCAPE BOTTOM
        END-IF
      END-REPEAT                                     /* YK2 end
    END-FIND
  END-IF
*
  FETCH 'ATHPATUP'
END-IF
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Program Messages'
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'CAP STATISTICS EXCEPTIONS  '
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'CAP FAS Letters             '
  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 4 FROM #YYYY    /* WAS 2     /* DW3
SUBTRACT 2 FROM #MM      /* WAS 8     /* DW3
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #YYYYMMDD TO #OLD-CAP-LN-DT
*
READ ST-V BY MEMBER-KEY FROM '001900'
*  IF PROCESS-IND = 'S' OR = 'C'           /* RE1
  IF PROCESS-IND = 'C'                     /* RE1
      OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)  /* RE1
    ESCAPE TOP
  END-IF
*
  RESET #UPDATE
  PERFORM ACCUM-ME-STATS
*
  END TRANSACTION
*
END-READ
*
WRITE 'Brady cnts 11/2001'
WRITE #MAR-NO-DATE  'missing marriage date'
WRITE #MAR-BEFORE   'married before membership'
WRITE 'MARRIAGE BY YEAR 1/2/3/4/5'
WRITE #MAR-CNT (*)
*
WRITE 'Crooks totals by face by month'
WRITE '          10K  50K  100K 150K 200K 250K 300K 350K 400K OTHR'
*
FOR #I1 1 TO #CURR-YEAR-X          /* YK2
  FOR #I2 1 TO 12
    IF #I2 = 1
      #YEAR := 1995 + #I1        /* YK2
    ELSE
      RESET #YEAR
    END-IF
*
    WRITE #YEAR #I2 (EM=Z9)  #TOT-FACE (#I1, #I2, 1)
      #TOT-FACE (#I1, #I2, 2)
      #TOT-FACE (#I1, #I2, 3)
      #TOT-FACE (#I1, #I2, 4)
      #TOT-FACE (#I1, #I2, 5)
      #TOT-FACE (#I1, #I2, 6)
      #TOT-FACE (#I1, #I2, 7)
      #TOT-FACE (#I1, #I2, 8)
      #TOT-FACE (#I1, #I2, 9)
      #TOT-FACE (#I1, #I2, 10)
  END-FOR
END-FOR
WRITE #CAP-NCOS-TOT         '= TOT-NCOS-CAPS'
WRITE #CAP-NCOS-HAS-LOAN    '= NCOS WITH LOAN'
WRITE #CAP-NCOS-STAYED      '= NCOS STAYED'
WRITE #CAP-NCOS-LEFT        '= NCOS LEFT'
*
WRITE #ALOT-NO-CHG      '= LOAN PAIDS"S BUT ALLOT NOT CHANGED'
*
IF (#WES-CAP-CNT NE 0) OR (#AAFMAA-CAP-CNT NE 0)
  NEWPAGE (7)
  WRITE (7) ///// '   WES    LTR CNT = ' #WES-CAP-CNT
  WRITE (7)    // '   AAFMAA LTR CNT = ' #AAFMAA-CAP-CNT
END-IF
*
RESET #NT-NOT-FOUND
FIND (1) NT-V WITH ID-NUMBER = 800109
  IF NO RECORDS FOUND
    MOVE TRUE TO #NT-NOT-FOUND
    ESCAPE BOTTOM
  END-NOREC
  MOVE *ISN TO #ISN
END-FIND
*
IF #NT-NOT-FOUND
  WRITE 'NX rec 800109 not found'
  MOVE 800109 TO NT-V.ID-NUMBER
ELSE
  GET NT-V #ISN
END-IF
*
WRITE 'CONV TOTALS BY MONTH'
WRITE '             ORIG W-LON WO-LON  LEFT STAYED'
*
MOVE *DATN TO NOTE-DATE (1)
*
FIND (1) NT-V1 WITH ID-NUMBER = 800106                /* YK1
  FOR #I2 = 1 TO #MM                                  /* YK1
    CAP-ORIG (#CURR-YEAR-X,#I2) := CAPS-CONV-MON (#I2) +
      CAPS-CONV-MON-A (#I2) + CAPS-CONV-MON-GR (#I2)    /* YK1
  END-FOR                                             /* YK1
END-FIND                                              /* YK1
*
FOR #I1 1 TO #CURR-YEAR-X    /* WAS 10   DW4          /* YK2
  FOR #I2 1 TO 12
    MOVE #CAP-BM-WLN  (#I1, #I2) TO CAP-WITH-LOAN       (#I1, #I2)
    MOVE #CAP-BM-NOL  (#I1, #I2) TO CAP-LN-PD-OFF       (#I1, #I2)
    COMPUTE CAP-LN-PD-&-LEFT (#I1, #I2) = CAP-ORIG      (#I1, #I2)
      - CAP-WITH-LOAN (#I1, #I2)
      - CAP-LN-PD-OFF (#I1, #I2)
    MOVE #CAP-BM-NOLC (#I1, #I2) TO CAP-LN-PD-&-STAYED  (#I1, #I2)
*
    IF #I2 = 1
      #YEAR := 1995 + #I1
    ELSE
      RESET #YEAR
    END-IF
    WRITE #YEAR #I2
      CAP-ORIG            (#I1, #I2)
      CAP-WITH-LOAN       (#I1, #I2)
      CAP-LN-PD-OFF       (#I1, #I2)
      CAP-LN-PD-&-LEFT    (#I1, #I2)
      CAP-LN-PD-&-STAYED  (#I1, #I2)
  END-FOR
END-FOR
*
IF #NT-NOT-FOUND
  STORE NT-V
ELSE
  UPDATE (0485)
END-IF
*
END TRANSACTION
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-ME-STATS
* * * * * * * * * * * * * * * * *
*
IF   (INTRO-FLAG = 'B' OR = 'C' OR = 'P' OR = 'Q')  /* ORIG A CAP MBR
    AND (NOT ST-V.ID-NUMBER = 93524 OR = 94554) /* NOT CAP FOR STATS
  MOVE 1 TO #CAP-X
  MOVE 1 TO #PLAN-X
  RESET #HAS-999 #CAP-NOT-G
  FOR #I1 1 TO 9
    IF   (CONV-DURATION (#I1) = 999)
        AND (STATUS (#I1) = 'D')  /* HAS NOT TERMED THIS MONTH
      IF #HAS-999
        WRITE ST-V.ID-NUMBER ISSUE-DATE (#I1) 'Has multiple CAP pols '
      ELSE
        MOVE #I1 TO #CAP-X
      END-IF
      MOVE TRUE TO #HAS-999
      MOVE ISSUE-DATE (#I1) TO #ISS-DT
*
      IF MODE (#I1) NE 'G'
        MOVE TRUE TO #CAP-NOT-G
      END-IF
*
      IF PLAN (#I1) = 'RT' OR = 'LT'
        MOVE 2 TO #PLAN-X
*       ESCAPE BOTTOM        /* WHY IS THIS HERE ?
      END-IF
    END-IF
  END-FOR
*
  MOVE *DATN TO #YYYYMMDD
  IF NOT #HAS-999                 /* DW5
      AND (NUMBER-MEMBERSHIPS NE 0)            /* OK DRW
      AND (CAP-ENTRY-DATE-YYYY GT (#YYYY - 1)) /* WITHIN 1 YEAR
    WRITE ST-V.ID-NUMBER 'CAP Mbr with no CAP pol -ENTRY-DT='
      CAP-ENTRY-DATE
  END-IF
*
  IF MILITARY-SERVICE = MASK ('A')
    MOVE 1 TO #SRV-X   /* ARMY
  ELSE
    MOVE 2 TO #SRV-X   /* AIR FORCE
  END-IF
*
  RESET #RANK-DISPL #RANK-LIT #RANK-DISPLAY                       /* PM1
  CALLNAT 'GRANKSXX' #COMMAND RANK MILITARY-SERVICE
    #INVALID #RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
  IF #INVALID OR (#RANK-DISPL = 99)
    RESET #RANK-DISPL
  END-IF
*
  IF (#RANK-DISPL GE 20) AND (#RANK-DISPL LE 30)
    MOVE 2 TO #RANK-X    /* NCO
*
    IF #SRV-X = 1
      MOVE 2 TO #SRV-RANK-X /* ARMY NCO
    ELSE
      MOVE 4 TO #SRV-RANK-X /* AIRF NCO
    END-IF
  ELSE
    IF #SRV-X = 1
      MOVE 1 TO #SRV-RANK-X /* ARMY OFF/WO
    ELSE
      MOVE 3 TO #SRV-RANK-X /* AIRF OFF-WO
    END-IF
*
    IF (#RANK-DISPL GE 15) AND (#RANK-DISPL LE 19)
      MOVE 3 TO #RANK-X   /* WARR OFF
    ELSE
      MOVE 1 TO #RANK-X   /* ALL ELSE
    END-IF
  END-IF
*
  IF #RANK-X = 2   /* NCO
    ADD 1 TO #CAP-NCOS-TOT
*
    IF X-LOAN-AMOUNT (#CAP-X) NE 0
      ADD 1 TO #CAP-NCOS-HAS-LOAN
    ELSE
      IF NUMBER-MEMBERSHIPS NE 0     /* OK  DRW
        ADD 1 TO #CAP-NCOS-STAYED
      ELSE
        ADD 1 TO #CAP-NCOS-LEFT
      END-IF
    END-IF
  END-IF
*
  IF CAP-ENTRY-DATE = 0
    WRITE ST-V.ID-NUMBER 'CAP MEMBER WITH MISSING CAP-ENTRY-DATE'
    MOVE 1 TO #YEAR-X
    MOVE 1 TO #MON-X
  ELSE
    COMPUTE #YEAR-X = CAP-ENTRY-DATE-YYYY - 1995
    MOVE CAP-ENTRY-DATE-MM TO #MON-X
  END-IF
*
  IF #YEAR-X GT 12 /* DW4
    WRITE ST-V.ID-NUMBER ST-V.ISSUE-DATE (#CAP-X)
      'Cap Ent Dt=' CAP-ENTRY-DATE 'year-x =' #YEAR-X
  END-IF
*
  IF #HAS-999
    DECIDE ON FIRST VALUE FACE-AMOUNT (#CAP-X)
      VALUE  10000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 1)
      VALUE  50000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 2)
      VALUE 100000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 3)
      VALUE 150000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 4)
      VALUE 200000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 5)
      VALUE 250000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 6)
      VALUE 300000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 7)
      VALUE 350000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 8)
      VALUE 400000  ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 9)
      NONE          ADD 1 TO #TOT-FACE (#YEAR-X, #MON-X, 10)
    END-DECIDE
*
    ADD 1 TO #CAP-BY-SERV (#YEAR-X, #SRV-X)
    ADD 1 TO #CAP-BY-RANK (#YEAR-X, #RANK-X)
    ADD 1 TO #CAP-BY-PLAN (#YEAR-X, #PLAN-X)
    ADD 1 TO #CAP-BY-SERV-RANK (#YEAR-X, #SRV-RANK-X)
*
    #CAP-PLUS1-DATE := CAP-ENTRY-DATE
    IF #CAP-PLUS1-MM = 12                      /* YK1
      #CAP-PLUS1-MM := 1
      #CAP-PLUS1-YYYY := #CAP-PLUS1-YYYY + 1
    ELSE
      #CAP-PLUS1-MM := #CAP-PLUS1-MM + 1
    END-IF                                     /* YK1
    IF X-LOAN-AMOUNT (#CAP-X) = 0
      IF #CAP-PLUS1-DATE = MBR-SINCE           /* YK1
          AND X-LOAN-DATE (#CAP-X) NE 0        /* YK1
        ADD 1 TO #CAP-BM-NOL (#YEAR-X, #MON-X) /* BY MON NO LOAN
      END-IF                                   /* YK1
      IF   (ALOT-AMT LT 165)   /* IF ALOT HAS BEEN LOWERED
          OR  #CAP-NOT-G
        ADD 1 TO #CAP-BM-NOLC (#YEAR-X, #MON-X) /* BY MON NO LOAN W CHG
*
        RESET #SPOUSE-CN #DATE-OF-BIRTH-SP                                  /* TT1
        #SPOUSE-CN := FUNCTION-SPOUSE-CI(<ST-V.MEMBER-CONTACT-ID>)          /* TT1
        #DATE-OF-BIRTH-SP   := GET-DATE-OF-BIRTH(<#SPOUSE-CN>)              /* TT1
*
        IF ((#DATE-OF-BIRTH-SP NE 0) OR (NAME-SP NE ' '))
            AND (NOT STATUS-CODE-SP = 'F' OR = 'D' OR = 'N')
          IF DATE-OF-MARRIAGE = 0
*           WRITE ST-V.ID-NUMBER 'no marriage date'
            ADD 1 TO #MAR-NO-DATE
          ELSE
            IF DATE-OF-MARRIAGE LT #ISS-DT
*             WRITE ST-V.ID-NUMBER 'MARRIED BEFORE membership'
*                  'iss=' #ISS-DT 'married=' DATE-OF-MARRIAGE
              ADD 1 TO #MAR-BEFORE
            ELSE
              COMPUTE #MAR-YRS = DATE-OF-MARRIAGE - #ISS-DT
* DISPLAY ST-V.ID-NUMBER #ISS-DT DATE-OF-MARRIAGE #MAR-YRS
*      STATUS-CODE-SP
              IF #MAR-YRS LT 10000
                ADD 1 TO #MAR-CNT (1)
              ELSE
                IF #MAR-YRS LT 20000
                  ADD 1 TO #MAR-CNT (2)
                ELSE
                  IF #MAR-YRS LT 30000
                    ADD 1 TO #MAR-CNT (3)
                  ELSE
                    IF #MAR-YRS LT 40000
                      ADD 1 TO #MAR-CNT (4)
                    ELSE
                      ADD 1 TO #MAR-CNT (5)
                    END-IF
                  END-IF
                END-IF
              END-IF
            END-IF
          END-IF
        END-IF
      ELSE
        ADD 1 TO #ALOT-NO-CHG
*       WRITE ST-V.ID-NUMBER ISSUE-DATE(#CAP-X)
*           'CAP-ENT DT' CAP-ENTRY-DATE
*           'PAID BUT DID NOT CHANGE ALLOTMENT AMT'
      END-IF
*
      MOVE *DATN TO #YYYYMMDD
*     IF ISSUE-DATE (#CAP-X) GT *DATN - 10000
      IF CAP-ENTRY-DATE-YYYY GT (#YYYY - 1) /* WITHIN 1 YR     /* DW5
        WRITE ST-V.ID-NUMBER ISSUE-DATE(#CAP-X)
          'Cap-ent dt' CAP-ENTRY-DATE 'Early CAP loan pd off '
      END-IF
    ELSE
      IF #CAP-PLUS1-DATE = MBR-SINCE                          /* YK1
        ADD 1 TO #CAP-BM-WLN (#YEAR-X, #MON-X) /* BY MON STILL HAS LOAN
      END-IF                                                  /* YK1
*
*     IF ISSUE-DATE (#CAP-X) LT #OLD-CAP-LN-DT
      IF CAP-ENTRY-DATE LT #OLD-CAP-LN-DT-YYYYMM   /* DW5
        WRITE ST-V.ID-NUMBER ISSUE-DATE(#CAP-X)
          'Cap-ent dt' CAP-ENTRY-DATE
          'Very old CAP loan not pd off ' 'OLD-DATE=' #OLD-CAP-LN-DT
      END-IF
    END-IF
  ELSE
    IF NUMBER-MEMBERSHIPS = 0              /* OK DRW
*     WRITE ST-V.ID-NUMBER '**** Must be a terminated CAP'
      IGNORE
    ELSE
*     WRITE ST-V.ID-NUMBER '**** CAP MBR MUST HAVE PAID & STAYED'
      IGNORE
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
END
