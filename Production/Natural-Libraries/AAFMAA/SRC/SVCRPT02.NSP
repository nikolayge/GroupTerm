* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**@AUTHOR Travis Tinger
************************

DEFINE DATA
LOCAL
* Data Views
1 SVC VIEW OF A-SVC-MEMBER
  2 SVC-ID-NUMBER
  2 SVC-PLAN
  2 SVC-MODE
  2 SVC-FIRST-PLACE-FEE
  2 SVC-ONE-TIME-FEE
  2 SVC-ISSUE-DATE
  2 REDEFINE SVC-ISSUE-DATE
     3 YYYY      (N4)
     3 MM        (N2)
     3 DD        (N2)

1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 DATE-OF-DEATH

1 CONT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME

* Constants
1 YES                  (A3)CONST<'YES'>
1 NO                   (A2)CONST<'NO'>
1 SAS                  (A3)CONST<'SAS'>
1 #LOOK-UP-MODE-KEY    (A4)CONST<'MODE'>
1 TAB                  (A1)CONST<H'09'>

* Local Variables
1 i                    (I4)
1 AMOUNT-PAID          (N7.2)
1 FILE-NAME            (A42)
1 #OUTPUT              (A200)

* Output fields
1 #OUTPUT-FIELDS
  2 #CN                   (A)DYNAMIC /*(N6)
  2 #FIRST-NAME           (A)DYNAMIC /*(A25)
  2 #MIDDLE-NAME          (A)DYNAMIC /*(A25)
  2 #LAST-NAME            (A)DYNAMIC /*(A25)
  2 #MODE-OF-PAYMENT      (A)DYNAMIC /*(A25)
  2 #IS-DECEASED          (A)DYNAMIC /*(A3)
  2 #AMOUNT-PAID          (A10) /*(A10)
  2 #DATE-ISSUED          (A10)
  
1 #COLUMN-NAMES
  2 #C-CN                 (A) DYNAMIC CONST <'Customer ID number'>
  2 #C-FIRST-NAME         (A) DYNAMIC CONST <'First Name'>
  2 #C-MIDDLE-NAME        (A) DYNAMIC CONST <'Middle Name'>
  2 #C-LAST-NAME          (A) DYNAMIC CONST <'Last Name'>
  2 #C-MODE-OF-PAYMENT    (A) DYNAMIC CONST <'Mode of Payment'>
  2 #C-IS-DECEASED        (A) DYNAMIC CONST <'Is Member Deceased?'>
  2 #C-AMOUNT-PAID        (A) DYNAMIC CONST <'Total Amount Paid'>
  2 #C-DATE-ISSUED        (A) DYNAMIC CONST <'Date Issued'>

END-DEFINE

* COMPRESS '/usr/SAG/tmp/service_report_2_' *DATN '.txt' INTO FILE-NAME LEAVING NO
* DEFINE WORK FILE 1 FILE-NAME TYPE 'ASCII-COMPRESSED'
*
COMPRESS #COLUMN-NAMES INTO #OUTPUT WITH DELIMITER TAB
WRITE WORK 1 #OUTPUT

* * * * * * * MAIN PROCESS * * * * * * *
READ SVC BY SVC-PLAN STARTING FROM SAS
  ACCEPT IF SVC-PLAN = SAS
  PERFORM GET-MEMBER-NAME
  PERFORM IS-MEMBER-DEAD
  PERFORM GET-AMOUNT-PAID
  PERFORM OUTPUT-REPORT
END-READ
* * * * * * * * * * * * * * * * * * * *

**===============================
DEFINE SUBROUTINE GET-MEMBER-NAME
**===============================
FIND CONT-V WITH CONT-V.ID-NUMBER = SVC.SVC-ID-NUMBER
  #CN          := CONT-V.ID-NUMBER
  #FIRST-NAME  := *TRIM(FIRST-NAME)
  #FIRST-NAME  :=  *TRIM(#FIRST-NAME)
  #MIDDLE-NAME := *TRIM(MIDDLE-NAME)
  #LAST-NAME   := *TRIM(LAST-NAME)
END-FIND
**============
END-SUBROUTINE
**============

**==============================
DEFINE SUBROUTINE IS-MEMBER-DEAD
**==============================
FIND ST-V WITH ST-V.ID-NUMBER = SVC.SVC-ID-NUMBER
  IF DATE-OF-DEATH <> 0 THEN
    #IS-DECEASED := 'Yes'
  ELSE
    #IS-DECEASED := 'No'
  END-IF
END-FIND
**============
END-SUBROUTINE
**============

**===============================
DEFINE SUBROUTINE GET-AMOUNT-PAID
**===============================
RESET AMOUNT-PAID #DATE-ISSUED
#MODE-OF-PAYMENT  := *TRIM(FUNCTION-PAYMENT-MODE(<SVC-MODE>)(1))
AMOUNT-PAID       := SVC.SVC-FIRST-PLACE-FEE - SVC.SVC-ONE-TIME-FEE
COMPRESS MM '/' DD '/' YYYY INTO #DATE-ISSUED LEAVING NO
MOVE EDITED AMOUNT-PAID (EM=Z,ZZ9.99) TO #AMOUNT-PAID
**============
END-SUBROUTINE
**============

**=============================
DEFINE SUBROUTINE OUTPUT-REPORT
**=============================
RESET #OUTPUT
COMPRESS FULL #OUTPUT-FIELDS INTO #OUTPUT WITH DELIMITER TAB
WRITE WORK 1 #OUTPUT
**============
END-SUBROUTINE
**============
END

