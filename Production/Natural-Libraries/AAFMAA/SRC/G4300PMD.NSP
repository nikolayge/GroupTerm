* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: G4300PMD
************************************************************************
*                       MODIFICATION LOG
* USER   DATE      TAG     REASON
*
* PAM    09242004  PM1 Use pending-at label if no issue date.
* DRW    02232005  DW2 Add AL (ANNUITY LIFE) processing
* DRW    06162005  DW3 Add TOT WIDS/SPSS to total line
* PAM    09272007  PM4 Added check for face amount
* PAM    03292007  PM5 Stop deleting mem detail - freeze strt date
* DRW    09182007  DW6 Bypass dob = 0 os sps age calcs
* DRW  12/11/2007  DW7 Allow deletion of entries by selected Users
* DRW  12/12/2007  DW8 Show LAPS-CAP as CAP-PDOFF if the Policy still exists
* DRW  09/22/2009  DW9 AFFL Unif - Plan & User-company for disp PROCESS-IND
* DRW  10/01/2009 DW10 Change logic to handle reading by new-superdescriptor
* DRW  10/26/2009 DW11 Add MS Users to D* Processing
* RSE  11/16/2009  RE1 Allow AFFL users to see IAS/SAS data when suffix is ALL
* RSE  07/02/2010  RE2 Display AER & 3SR
* RSE  07/08/2010  RE3 Add user company field on screen;
*                      IAS/SAS users must see all when DEATHS is selected
* NGG  07/19/2010  NG1 Call subprogram instead of function
* RSE  07/30/2010  RE4 Display date of death for Special Contracts (AER, etc.)
* NGG  12/16/2011  NG2 Remove unused SPLANLXX
* YAK  10/16/2012  YK1 Use function to identify Level Term II plans
* YAK  10/16/2012  YK2 Add code for ACDB
* vxt  09/12/2013  vt1 Increase the size of some fields /Ticket 2257/
* TMT  06/12/2014  TT1 Contact Normalization - Gender and DOB
* TMT  10/16/2014  TT2 Annuity Life Harmonization
* NGG  11/10/2014  NG3 Membership Statistics
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G4300LMD
LOCAL USING CONTPDA1        /* TT1
LOCAL USING GONERLXX        /* WORK FIELDS FOR 'GONERCXX'
LOCAL  USING A-IFAFFL   /* PDA used with FUNCTION F-if-AFFL     /* DW9
* LOCAL  USING SPLANLXX   /* 1 S-PLAN-V View of DDM A-SVC-MEMBER-PLAN   /* DW9 NG2
LOCAL
1 #MD-ISN         (P8)           /* DW10
1 #COMPANY-DATE-INVERSE-BEGIN-DATE  (A9)  /* DW10
1 #COMPANY-DATE-INVERSE-END-DATE    (A9)  /* DW10
*
1 ST-X-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 WIDOW-FLAG
  2 INTRO-FLAG
  2 PROCESS-IND             /* PM - ADDED FOR COMPANY SPLIT
  2 APPL-SOURCE
*
1 #SCR-SELECT-CMND       (A2/20)
1 #SCR-SELECT-CMND-CV    (C/20)
1 #FAC-AMT         (N7)    /* vt1 was n6
1 REDEFINE #FAC-AMT
  2 #FAC-K       (N4)
  2 #FAC-3       (N3)      /* vt1 was n3
1 #HOLD-FAC-AMT    (N7)    /* vt1 was n6                                          /* PM4
*
1 #LAST-CN     (N6)
1 #LAST-ISS-DT (N8)
1 #LAST-CODE   (A1)
1 #LAST-SEG        (I2)
1 #ISC             (I2)
1 #I               (I2)       /* RE2
1 #I1              (I2)
1 #I2              (I2)                                           /* PM4
1 #NOT-FND         (L)
*
1 #N-O             (A2)
1 #ALL             (A1)
1 #WIDOW-DEATHS    (A1)
1 #DEATHS          (A1)
1 #NEW-MBRS        (A1)
1 #NEW-S-G         (A1)
1 #ADDL-MBRS       (A1)
1 #LAPS            (A1)
1 #CONV            (A1)
1 #CSV             (A1)
1 #REINS           (A1)
*
1 #ACTUAL-ENTRY-DATE  (N8)
1 REDEFINE #ACTUAL-ENTRY-DATE
  2 #ACT-ENT-DT-CC   (A2)
  2 #ACT-ENT-DT-YY   (A2)
  2 #ACT-ENT-DT-MM   (A2)
  2 #ACT-ENT-DT-DD   (A2)
*
1 #INVERSE-BEGIN-DATE  (N8)   /* ENTERED AS END DATE
1 REDEFINE #INVERSE-BEGIN-DATE
  2 #INVERSE-BEGIN-DATE-A8   (A8)                           /* DW10
1 #BEGIN-DATE       (N8)  INIT <01012007>                       /* PM5
1 REDEFINE #BEGIN-DATE
  2 #BEGIN-DATE-MM   (N2)
  2 #BEGIN-DATE-DD   (N2)
  2 #BEGIN-DATE-YYYY (N4)
1 #FLIP-BEGIN-DATE  (N8)
1 REDEFINE #FLIP-BEGIN-DATE
  2 #FLIP-BEGIN-DATE-YYYY (N4)
  2 #FLIP-BEGIN-DATE-MM   (N2)
  2 #FLIP-BEGIN-DATE-DD   (N2)
*
1 #INVERSE-END-DATE  (N8)    /* ENTERED AS BEGIN DATE
1 REDEFINE #INVERSE-END-DATE
  2 #INVERSE-END-DATE-A8   (A8)                          /* DW10
*
1 #END-DATE        (N8)
1 REDEFINE #END-DATE
  2 #END-DATE-MM   (N2)
  2 #END-DATE-DD   (N2)
  2 #END-DATE-YYYY (N4)
1 #FLIP-END-DATE  (N8)
1 REDEFINE #FLIP-END-DATE
  2 #FLIP-END-DATE-YYYY (N4)
  2 #FLIP-END-DATE-MM   (N2)
  2 #FLIP-END-DATE-DD   (N2)
*
1 #PRIOR-SCREEN-1ST-DATE (N8)
1 #THIS-SCREEN-1ST-DATE  (N8)
1 #1ST-DATE-TBL          (N8/50)
1 #1ST-ISN-TBL           (N6/50)
1 #HOLD-ISN              (N6)
1 #IPG                   (I2)
*
1 #SCR-HDR-LINE-G
  2 #SCR-FLL-1        (A2)
  2 #SCR-H-ID         (A6) INIT <'ID-NBR'>
  2 #SCR-FLL-2        (A1)
  2 #SCR-H-ENTRY-DATE (A5) INIT <'EN-DT'>
  2 #SCR-FLL-3        (A4)
  2 #SCR-H-RANK       (A3) INIT <'RNK'>
* 2 #SCR-FLL-4        (A1)
  2 #SCR-H-NAME       (A16) INIT <'   NAME'>
* 2 #SCR-FLL-5        (A1)                   /* RE3
  2 #SCR-H-COMPANY    (A3)  INIT <' CO'>      /* RE3
  2 #SCR-H-CODE       (A13) INIT <' TYPE '>
  2 #SCR-FLL-6        (A2)
  2 #SCR-H-NUM-UNITS  (A5)  INIT <'FACE$'>    /* DW9
  2 #SCR-FLL-7        (A1)
  2 #SCR-H-STAT       (A2)  INIT <'ST'>
  2 #SCR-FLL-8        (A1)
  2 #SCR-H-DOD-ISS-DT (A8)  INIT <'DOD/ISS'>
  2 #SCR-H-LC         (A3)  INIT <' LC'>
1 REDEFINE #SCR-HDR-LINE-G
  2 #SCR-HDR-LINE     (A75)
*
1 #SCR-LINE         (A75/20)
1 REDEFINE #SCR-LINE
  2 #SCR-LINE-TBL    (20)
    3 #SCR-MIL-SERV   (A1)
    3 #SCR-FIL-1      (A1)
    3 #SCR-ID         (N6)
    3 #SCR-FIL-2      (A1)
    3 #SCR-ENTRY-DATE (A8)
    3 #SCR-FIL-3      (A1)
    3 #SCR-RANK       (A3)
    3 #SCR-FIL-4      (A1)
*  3 #SCR-NAME       (A17)          /* RE3
    3 #SCR-NAME       (A15)           /* RE3
    3 #SCR-FIL-5      (A1)
    3 #SCR-COMPANY    (A2)            /* RE3
    3 #SCR-CODE       (A13)
    3 REDEFINE #SCR-CODE
      4 #SCR-CODE-6   (A6)
    3 #SCR-FIL-6      (A1)
    3 #SCR-INTRO-IND  (A1)
    3 #SCR-FIL-7      (A1)
    3 #SCR-FACE-AMT   (A5)
    3 #SCR-STAT       (A1)
    3 #SCR-FIL-9      (A1)
    3 #SCR-DOD-ISS-DT (A8)
    3 #SCR-FIL-10     (A1)
    3 #SCR-LC         (A3)
*
1 #APPL-SRCE-TABLE   (30)
  2 #APPL-SRCE-ENT    (A7)
  2 #APPL-SRCE-CNT    (N5)
*
1 #I5               (I2)
1 #HDR-TITLE        (A8)
1 #LAST            (A25)
1 #FIRST/MID       (A25)
1 REDEFINE #FIRST/MID
  2 #FILL           (A1)
  2 #FIRST/MID-R    (A24)
1 #SUFF            (A9)
1 REDEFINE #SUFF
  2 #FILL           (A1)
  2 #SUFF-R         (A8)
1 #NAME-OVRFLO     (A25)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CC             (N2)
  2 #YY             (N2)
  2 #MM             (N2)
  2 #DD             (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY           (N4)
  2 #MMDD           (N4)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A         (A4)
  2 #MM-A           (A2)
  2 #DD-A           (A2)
*
1 #1-YR-STATS-AVAILABLE (N8)  /* PAM
1 REDEFINE #1-YR-STATS-AVAILABLE
  2 #1-YR-YYYY      (N4)
  2 #1-YR-MM        (N2)
  2 #1-YR-DD        (N2)
1 #1-YR-STATS-AVAILABLE-TEXT (A10)  /* PAM
*
1 #CURR-MM         (N2)
1 REDEFINE #CURR-MM
  2 #CURR-MM-A      (A2)
1 #CURR-YYYY       (N4)
1 #LAST-MM-YYYY    (N4)
1 #LAST-MM         (N2)
1 REDEFINE #LAST-MM
  2 #LAST-MM-A      (A2)
*
1 #LAST-LINE-MM    (A2)
1 #LAST-LINE-DD    (A2)
1 #UNITS           (N2)
*
1 #AAFMAA          (A6)  INIT <'AAFMAA'>
1 #AFSC            (A6)  INIT <'  AFSC'>
*
1 #COMPANY-LABEL   (A6)
1 #TOT-AGE-SPS   (P7)
1 #TOT-AGE-WID   (P7)
1 #NUM-SPS       (P5)
1 #NUM-SPS-TOT   (P5)   /* DW3
1 #NUM-WID       (P5)
1 #NUM-WID-TOT   (P5)   /* DW3
1 #AVG-AGE-SPS   (P4)    /* WAS 3    /* DW6
1 #AVG-AGE-WID   (P4)    /* was 3    /* DW6
1 #AFSC-ALLOWED  (L)
1 #MD-UPD     (L)
1 #TEXT          (A75)                                          /* PAM
*
1 #SCR-ISN               (P8/20)       /* DW7
1 #CAP-PAID-OFF     (L)                /* DW8
*
1 #BYPASS-IAS         (L)            /* DW9
1 #BYPASS-SAS         (L)
1 #MD-PLAN           (A3)
1 #SAVE-SCR-SUFFIX   (A3)            /* RE1
1 SPOUSE-CN          (N8.0)          /* TT1
1 DATE-OF-BIRTH      (N8.0)          /* TT1
1 DATE-OF-BIRTH-SP   (N8.0)          /* TT1
1 SPOUSE-SEX         (A1)            /* TT1
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX            /* NATURAL ERROR ROUTINE
*
MOVE *DATN TO #YYYYMMDD
MOVE #MM   TO #CURR-MM
MOVE #MM   TO #1-YR-MM                                          /* PAM
MOVE #YYYY TO #CURR-YYYY
MOVE #YYYY TO #LAST-MM-YYYY
MOVE #YYYY TO #1-YR-YYYY                                        /* PAM
COMPUTE #LAST-MM = #CURR-MM - 2
COMPUTE #1-YR-YYYY = #1-YR-YYYY - 1                             /* PAM
MOVE 01 TO #1-YR-DD                                             /* PAM
*
IF #LAST-MM LT 1
  ADD 12 TO #LAST-MM
  SUBTRACT 1 FROM #LAST-MM-YYYY
END-IF
*
* MOVE #LAST-MM-YYYY TO #BEGIN-DATE-YYYY                        /* PM5
* MOVE #LAST-MM      TO #BEGIN-DATE-MM                          /* PM5
* MOVE 01            TO #BEGIN-DATE-DD                          /* PM5
*
MOVE #YYYY   TO #END-DATE-YYYY
MOVE #MM     TO #END-DATE-MM
MOVE #DD     TO #END-DATE-DD
*
IF #CGA-SCR-SUFFIX NE 'ALL'                     /* DW9
  IF #CGA-USER-COMPANY = 'S'
    MOVE TRUE TO #BYPASS-IAS
  ELSE
    MOVE TRUE TO #BYPASS-SAS
  END-IF
END-IF
*
MOVE #CGA-SCR-SUFFIX TO #SAVE-SCR-SUFFIX        /* RE1
MOVE '*' TO #CGA-SCR-COMMAND
RESET     #CGA-SCR-ID
RESET     #CGA-SCR-SUFFIX
*
MOVE #AAFMAA TO #COMPANY-LABEL
*
INPUT TEXT #TEXT MARK *#ALL USING MAP 'G4300MMD'              /* PAM
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF   (#ALL              = ' ')
    AND (#DEATHS        = ' ')
    AND (#WIDOW-DEATHS  = ' ')
    AND (#NEW-MBRS      = ' ')
    AND (#NEW-S-G       = ' ')
    AND (#ADDL-MBRS     = ' ')
    AND (#LAPS          = ' ')
    AND (#CONV          = ' ')
    AND (#CSV           = ' ')
    AND (#REINS         = ' ')
    AND (#CGA-SCR-ID    = 0)
  REINPUT 'AT LEAST ONE TYPE SELECTION MUST BE MADE'
    MARK *#DEATHS
END-IF
*
IF #ALL NE ' '
  MOVE 'X' TO
    #DEATHS
    #WIDOW-DEATHS
    #NEW-MBRS
    #NEW-S-G
    #ADDL-MBRS
    #LAPS
    #CONV
    #CSV
    #REINS
END-IF
*
IF (#WIDOW-DEATHS NE ' ') AND (#ALL = ' ')
  MOVE 'AGE' TO #SCR-H-LC
END-IF
*
MOVE #BEGIN-DATE-YYYY TO #FLIP-BEGIN-DATE-YYYY
MOVE #BEGIN-DATE-MM   TO #FLIP-BEGIN-DATE-MM
MOVE #BEGIN-DATE-DD   TO #FLIP-BEGIN-DATE-DD
MOVE #END-DATE-YYYY TO #FLIP-END-DATE-YYYY
MOVE #END-DATE-MM   TO #FLIP-END-DATE-MM
MOVE #END-DATE-DD   TO #FLIP-END-DATE-DD
*
COMPUTE #INVERSE-BEGIN-DATE = 99999999 - #FLIP-END-DATE
COMPUTE #INVERSE-END-DATE   = 99999999 - #FLIP-BEGIN-DATE
MOVE (AD=P) TO #SCR-SELECT-CMND-CV (*)
*
REPEAT
  PERFORM PROCESS-LOOP
*
  IF #CGA-SCR-COMMAND NE MASK ('-')     /* INDICATES GO TO PRIOR SCREEN
    ESCAPE BOTTOM
  END-IF
*
  IF #IPG GT 1
    SUBTRACT 1 FROM #IPG
    MOVE #1ST-DATE-TBL (#IPG) TO #INVERSE-BEGIN-DATE
    MOVE #1ST-ISN-TBL (#IPG) TO #HOLD-ISN
    SUBTRACT 1 FROM #IPG
  END-IF
*
END-REPEAT
*
FETCH 'G1000PXX'
* * * * * * * * * *
DEFINE PROCESS-LOOP
* * * * * * * * * *
*
IF #CGA-ORIG-ID > 0    /* NG3  Start
  FIND  MBRSHIP-MTD-DET-V WITH ID-NUMBER = #CGA-ORIG-ID
    IF  MBRSHIP-MTD-DET-V.ENTRY-DATE GE  #INVERSE-BEGIN-DATE
        AND MBRSHIP-MTD-DET-V.ENTRY-DATE LE #INVERSE-END-DATE
      MOVE *ISN TO #MD-ISN
      PERFORM MD-PROCESS
    END-IF
  END-FIND
ELSE
  IF   (#CGA-USER-COMPANY = 'S')
      AND (#SAVE-SCR-SUFFIX NE 'ALL')
      AND #DEATHS = ' '
    COMPRESS 'S' #INVERSE-BEGIN-DATE-A8
      INTO #COMPANY-DATE-INVERSE-BEGIN-DATE LEAVING NO
    COMPRESS 'S' #INVERSE-END-DATE-A8
      INTO #COMPANY-DATE-INVERSE-END-DATE LEAVING NO
*
    READ MBRSHIP-MTD-DET-V BY COMPANY-DATE
        STARTING FROM #COMPANY-DATE-INVERSE-BEGIN-DATE
      IF COMPANY-DATE GT #COMPANY-DATE-INVERSE-END-DATE
        ESCAPE BOTTOM
      END-IF
      MOVE *ISN TO #MD-ISN
      PERFORM MD-PROCESS
    END-READ
  ELSE
    READ MBRSHIP-MTD-DET-V BY ENTRY-DATE FROM #INVERSE-BEGIN-DATE
        THRU #INVERSE-END-DATE
      MOVE *ISN TO #MD-ISN
      PERFORM MD-PROCESS
    END-READ
  END-IF
END-IF
*                      NG3 End
MOVE #CGA-ORIG-COMMAND TO #CGA-SCR-COMMAND
MOVE #CGA-ORIG-ID      TO #CGA-SCR-ID
MOVE #CGA-ORIG-SUFFIX  TO #CGA-SCR-SUFFIX
*
IF #ISC LE 20
    AND ((#NUM-WID NE 0) OR (#NUM-SPS NE 0))
  IF #NUM-WID NE 0
    COMPUTE #AVG-AGE-WID = #TOT-AGE-WID / #NUM-WID
  END-IF
*
  IF #NUM-SPS NE 0
    COMPUTE #AVG-AGE-SPS = #TOT-AGE-SPS / #NUM-SPS
  END-IF
*
  IF #ISC LT 20
    ADD 1 TO #ISC
    COMPRESS '----- AVG AGE AT DEATH: WID=' #AVG-AGE-WID
      'SPS=' #AVG-AGE-SPS
      '------------------------------------------'
      INTO #SCR-LINE (#ISC)
*
    COMPRESS '-- NUM WID"s=' #NUM-WID-TOT    /* DW3
      'NUM SPS"s=' #NUM-SPS-TOT    /* DW3
      '- AVG AGE AT DEATH: WID=' #AVG-AGE-WID
      'SPS=' #AVG-AGE-SPS
      '----------------'
      INTO #SCR-LINE (#ISC)
  END-IF
  RESET #TOT-AGE-SPS #NUM-SPS
  RESET #TOT-AGE-WID #NUM-WID
END-IF
*
* IF #NEW-MBRS-SUMM NE ' ' OR #LAPS-SUMM NE ' '     /* DW9
* DISPLAY #APPL-SRCE-TABLE(*)
*  INPUT MARK *#CGA-SCR-COMMAND USING MAP 'G4302MMD'
* ELSE
INPUT MARK *#SCR-SELECT-CMND (1) USING MAP 'G4301MMD'
* END-IF
*
IF #CGA-SCR-COMMAND = MASK ('-')    /* INDICATES TO GO TO PRIOR SCN
  RESET #LAST-LINE-MM    /* TO KEEP ****** FROM TOP OF PAGE
  RESET #SCR-LINE (*)
  MOVE 0 TO #ISC
ELSE
  PERFORM CHK-CMND-SELECT
END-IF
*
END-SUBROUTINE /* (0366)
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHK-CMND-SELECT
* * * * * * * * * * * * * * * * * * *
*
FOR #I1 1 TO 20
  IF #SCR-SELECT-CMND (#I1) NE ' '                     /* DW7
*    IF (#CGA-CLERK = 'DRW' OR = 'KHB') AND (#SCR-SELECT-CMND(#I1) = 'D*')
    IF (#CGA-CLERK = 'VXT'
        or = 'LR' OR = 'BMG' OR = 'ALB')  /* Add Ms Users DW11 removed 'SV' and DRW
        AND (#SCR-SELECT-CMND(#I1) = 'D*')
      GET MBRSHIP-MTD-DET-V #SCR-ISN (#I1)
      WRITE 'MD REC WILL BE DELETED FOR CN' #SCR-ID (#I1)
      WRITE 'WITH NAME OF' #SCR-NAME (#I1)
      WRITE 'PRESS ENTER TO PROCESS'
      RESET #SCR-SELECT-CMND (#I1)
      DELETE (0462)
      END TRANSACTION
      ESCAPE TOP
    END-IF
*
    IF #SCR-SELECT-CMND (#I1) = 'X'
      DECIDE ON FIRST #SCR-CODE-6 (#I1)
        VALUE 'DEATH ' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'DEATH*' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'NU-MBR' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'AD-INS' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'LAPSE ' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'CASH-V' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'REINST' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'SPS-DE' MOVE 'DI' TO #CGA-SCR-COMMAND
        VALUE 'WID-DE' MOVE 'DI' TO #CGA-SCR-COMMAND
        VALUE 'RESIGN' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'LAPS-N' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'LAPS-O' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'CV-EXC' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'NM-FBR' MOVE 'ST' TO #CGA-SCR-COMMAND
        VALUE 'AI-FBR' MOVE 'ST' TO #CGA-SCR-COMMAND
        NONE           MOVE 'DI' TO #CGA-SCR-COMMAND
      END-DECIDE
    ELSE
      MOVE #SCR-SELECT-CMND (#I1) TO #CGA-SCR-COMMAND
    END-IF
*
    MOVE #SCR-ID (#I1) TO #CGA-SCR-ID
    FETCH 'G1000PXX'
  END-IF
END-FOR
*
END-SUBROUTINE /* (5360)
*
* * * * * * * * * *
DEFINE SUBROUTINE MD-PROCESS                       /* DW10
* * * * * * * * * *
*
IF   #BYPASS-IAS                                  /* DW9
    AND (MBRSHIP-MTD-DET-V.USER-COMPANY NE 'S')
    AND #DEATHS = ' '                               /* RE3
*   WRITE 'escape top'
  ESCAPE TOP
END-IF
*
IF   #BYPASS-SAS
    AND (MBRSHIP-MTD-DET-V.USER-COMPANY = 'S')
    AND #DEATHS = ' '                               /* RE3
*   WRITE 'escape top'
  ESCAPE TOP
END-IF                                            /* END DW8
*
* WRITE 'Process this rec'
RESET #NOT-FND
FIND ST-X-V WITH MEMBER-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
  IF NO RECORDS FOUND
    FIND ST-X-V WITH WIDOW-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
      IF NO RECORDS FOUND
        MOVE TRUE TO #NOT-FND
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
IF #NOT-FND
  ESCAPE TOP
END-IF
*
CALLNAT 'PLAN-AFA' ST-X-V.ID-NUMBER F-IF-AFFL-DATA /* NG1
*
COMPUTE #ACTUAL-ENTRY-DATE = 99999999 - ENTRY-DATE
*
IF   (#CGA-SCR-COMMAND = MASK ('-'))
    AND (#MD-ISN NE #HOLD-ISN)                                /* DW10
  ESCAPE TOP
END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
*
DECIDE FOR FIRST CONDITION
  WHEN #DEATHS = ' ' AND CODE = 'D' ESCAPE TOP
  WHEN #WIDOW-DEATHS = ' ' AND CODE = 'W' ESCAPE TOP
  WHEN #NEW-MBRS = ' ' AND #NEW-S-G = ' '
      AND CODE = 'N' OR= 'Y' ESCAPE TOP
  WHEN #ADDL-MBRS = ' ' AND CODE = 'A'  OR = 'Z' ESCAPE TOP
  WHEN #LAPS = ' ' AND CODE = 'L' OR = 'S' OR = 'T' OR = 'U' OR = 'V'
    ESCAPE TOP
  WHEN #CSV = ' ' AND CODE = 'C' OR = 'X' OR = 'F' ESCAPE TOP
  WHEN #REINS = ' ' AND CODE = 'R' ESCAPE TOP
  WHEN #CONV = ' ' AND CODE = '0' OR= '1' OR= '2' ESCAPE TOP
  WHEN NONE IGNORE
END-DECIDE
* IF  ((#DEATHS    = ' ')    AND (CODE = 'D'))
*     OR ((#WIDOW-DEATHS = ' ') AND (CODE = 'W'))
*     OR ((#NEW-MBRS  = ' ') AND (#NEW-S-G = ' ')
*     AND (CODE = 'N' OR = 'Y'))
*     OR ((#ADDL-MBRS = ' ')    AND (CODE = 'A'  OR = 'Z'))
*     OR ((#LAPS      = ' ')   AND (CODE = 'L' OR = 'S' OR = 'T' OR = 'U'
*     OR = 'V'))
* OR ((#CSV       = ' ')    AND (CODE = 'C' OR = 'X' OR = 'F'))              /* YK2
*     OR ((#REINS     = ' ')    AND (CODE = 'R'))
*   ESCAPE TOP
* END-IF
*
IF  ((#NEW-MBRS NE ' ') AND (#NEW-S-G  = ' '))
    OR ((#NEW-S-G  NE ' ') AND (#NEW-MBRS = ' '))
*
  IF  ((#NEW-MBRS NE ' ')
      AND (ST-X-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'))
      OR ((#NEW-S-G NE ' ')
      AND (NOT ST-X-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'))
    ESCAPE TOP
  END-IF
END-IF
*
ADD 1 TO #ISC
*  IF #ISC LE 20 AND (#NEW-MBRS-SUMM = ' ' AND #LAPS-SUMM = ' ')   /* DW9
IF #ISC LE 20
  IF #LAST-LINE-MM NE ' '
      AND (#ACT-ENT-DT-MM NE #LAST-LINE-MM)
    IF (#NUM-WID NE 0) OR (#NUM-SPS NE 0)
      IF #NUM-SPS NE 0
        COMPUTE #AVG-AGE-SPS = #TOT-AGE-SPS / #NUM-SPS
      END-IF
*
      IF #NUM-WID NE 0
        COMPUTE #AVG-AGE-WID = #TOT-AGE-WID / #NUM-WID
      END-IF
*
      COMPRESS '-- NUM WID"s=' #NUM-WID-TOT    /* DW3
        'NUM SPS"s=' #NUM-SPS-TOT    /* DW3
        '- AVG AGE AT DEATH: WID=' #AVG-AGE-WID
        'SPS=' #AVG-AGE-SPS
        '----------------'
        INTO #SCR-LINE (#ISC)
      RESET #TOT-AGE-SPS #NUM-SPS #NUM-SPS-TOT  /* DW3
      RESET #TOT-AGE-WID #NUM-WID #NUM-WID-TOT
      ADD 1 TO #ISC
    ELSE
      MOVE ALL '-' TO #SCR-LINE (#ISC)
      ADD 1 TO #ISC
    END-IF
  ELSE
    IF #LAST-LINE-DD NE ' '
        AND (#ACT-ENT-DT-DD NE #LAST-LINE-DD)
      ADD 1 TO #ISC
    END-IF
  END-IF
END-IF
*
IF #ISC GT 20
*    IF  (#NEW-MBRS-SUMM NE ' ' OR #LAPS-SUMM NE ' ')    /* DW9
*      IGNORE
*    ELSE
  INPUT TEXT 'PRESS ENTER FOR ADDITIONAL ENTRIES'
    MARK *#SCR-SELECT-CMND (1) USING MAP 'G4301MMD'
*    END-IF
*
  RESET #LAST-LINE-MM    /* TO KEEP ****** FROM TOP OF PAGE
  RESET #LAST-LINE-DD    /* TO KEEP ****** FROM TOP OF PAGE
  MOVE 1 TO #ISC
*
  IF #CGA-SCR-COMMAND = MASK ('-')  /* INDICATES TO GO TO PRIOR SCN
    RESET #SCR-LINE (*)
    MOVE 0 TO #ISC
    ESCAPE ROUTINE
  END-IF
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  PERFORM CHK-CMND-SELECT
  RESET #SCR-LINE (*)
  MOVE (AD=P) TO #SCR-SELECT-CMND-CV (*)
END-IF
*
IF #ISC = 1
  ADD 1 TO #IPG
  MOVE ENTRY-DATE TO #1ST-DATE-TBL (#IPG)
  MOVE #MD-ISN      TO #1ST-ISN-TBL (#IPG)     /* DW10
END-IF
*
MOVE #MD-ISN TO #SCR-ISN (#ISC)   /* STORE ISN OF MTD REC   /* DW7  DW10
*
MOVE #ACT-ENT-DT-MM TO #LAST-LINE-MM
MOVE #ACT-ENT-DT-DD TO #LAST-LINE-DD
*
RESET #NOT-FND
FIND ST-V WITH MEMBER-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
  IF NO RECORDS FOUND
    FIND ST-V WITH WIDOW-KEY = MBRSHIP-MTD-DET-V.ID-NUMBER-A
      IF NO RECORDS FOUND
        MOVE TRUE TO #NOT-FND
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
IF #NOT-FND
  ESCAPE TOP
END-IF
*
*  IF #NEW-MBRS-SUMM = ' ' AND #LAPS-SUMM = ' '   /* DW9
RESET #SCR-SELECT-CMND-CV (#ISC)
MOVE ST-V.ID-NUMBER   TO #SCR-ID       (#ISC)
MOVE MILITARY-SERVICE TO #SCR-MIL-SERV (#ISC)
MOVE MILITARY-STATUS  TO #SCR-STAT     (#ISC)
COMPRESS #ACT-ENT-DT-MM #ACT-ENT-DT-DD #ACT-ENT-DT-CC #ACT-ENT-DT-YY
  INTO #SCR-ENTRY-DATE (#ISC) LEAVING NO
MOVE USER-COMPANY     TO #SCR-COMPANY (#ISC)                   /* RE3
*
*  END-IF     /*  <<><<
*
* IF (CODE = 'W') AND (WIDOW-FLAG = 'Y') /* only widow deaths
*   MOVE ' DE' TO #SCR-LC (#ISC)
*
RESET PDA-CONTACT DATE-OF-BIRTH                              /* TT1
P-CONTACT-ID := ST-V.MEMBER-CONTACT-ID                       /* TT1
CALLNAT 'CONTS001' PDA-CONTACT  DATE-OF-BIRTH-SP  SPOUSE-SEX /* TT1
DATE-OF-BIRTH := P-DATE-OF-BIRTH
*
IF CODE = 'W'
  IF MBRSHIP-MTD-DET-V.ISSUE-DATE =88888888   /* 'SPS-DETH'  /* DW3
    ADD 1 TO #NUM-SPS-TOT                  /* DW3
  ELSE
    ADD 1 TO #NUM-WID-TOT
  END-IF
*
  IF  (DATE-OF-DIVORCE = 0)
      OR (DATE-OF-BIRTH-SP = 0)    /* DW6
    MOVE '???' TO #SCR-LC (#ISC)
  ELSE
    MOVE DATE-OF-DIVORCE TO #YYYYMMDD    /* sps/wid date of death
    SUBTRACT DATE-OF-BIRTH-SP FROM #YYYYMMDD
    MOVE EDITED #YYYY (EM=ZZ9) TO #SCR-LC (#ISC)
*
    IF MBRSHIP-MTD-DET-V.ISSUE-DATE =88888888   /* 'SPS-DETH'
      ADD 1 TO #NUM-SPS
      ADD #YYYY TO #TOT-AGE-SPS
    ELSE
      ADD 1 TO #NUM-WID
      ADD #YYYY TO #TOT-AGE-WID
    END-IF
  END-IF
ELSE
  IF MILITARY-STATUS = 'A'
    MOVE ' AD' TO #SCR-LC (#ISC)
  ELSE
    IF DATE-OF-BIRTH GE *DATN - 600000
      MOVE ' SC' TO #SCR-LC (#ISC)
    ELSE
      MOVE '60+' TO #SCR-LC (#ISC)
    END-IF
  END-IF
END-IF
*
IF CODE = 'W'
  MOVE TITLE-SP     TO #SCR-RANK (#ISC)
*   SEPARATE NAME-SP INTO #LAST #FIRST/MID #SUFF #NAME-OVRFLO
*                                             WITH DELIMITER ','
*   COMPRESS #FIRST/MID #LAST #SUFF-R INTO #SCR-NAME (#ISC)
  MOVE NAME-SP TO #SCR-NAME (#ISC)
ELSE
  MOVE RANK      TO #SCR-RANK (#ISC)
*   SEPARATE NAME INTO #LAST #FIRST/MID #SUFF #NAME-OVRFLO
*                                             WITH DELIMITER ','
*   COMPRESS #FIRST/MID #LAST #SUFF-R INTO #SCR-NAME (#ISC)
* ABOVE REPLACED BY BELOW 6/2000 AS PER BJS
  MOVE NAME TO #SCR-NAME (#ISC)
END-IF
*
IF CODE = 'D'
  COMPRESS DATE-OF-DEATH-MM DATE-OF-DEATH-DD DATE-OF-DEATH-CENT
    DATE-OF-DEATH-YY INTO #SCR-DOD-ISS-DT (#ISC) LEAVING NO
ELSE
  IF CODE = 'W'
    MOVE DATE-OF-DIVORCE TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A
      INTO #SCR-DOD-ISS-DT (#ISC) LEAVING NO
  ELSE
    IF (#MD-PLAN = 'AER' OR = 'FER' OR = 'NER' OR = 'CER' OR = 'UER') AND   /* RE4
        MBRSHIP-MTD-DET-V.USER-COMPANY = 'S'
      COMPRESS DATE-OF-DEATH-MM DATE-OF-DEATH-DD DATE-OF-DEATH-CENT
        DATE-OF-DEATH-YY INTO #SCR-DOD-ISS-DT (#ISC) LEAVING NO
    ELSE
      COMPRESS MBRSHIP-MTD-DET-V.ISSUE-DATE-MM MBRSHIP-MTD-DET-V.ISSUE-DATE-DD
        MBRSHIP-MTD-DET-V.ISSUE-DATE-CENT
        MBRSHIP-MTD-DET-V.ISSUE-DATE-YY  INTO #SCR-DOD-ISS-DT (#ISC) LEAVING NO
    END-IF
  END-IF
END-IF
*
IF MBRSHIP-MTD-DET-V.USER-COMPANY = 'S'    /* An SAS trans    /* DW9
*  IF PROCESS-IND = 'S'                                          /* DW9
*    IF (#CGA-USER-COMPANY = 'S')
  MOVE ST-X-V.APPL-SOURCE TO #SCR-FACE-AMT (#ISC)  /* SHOW APPL SOURCE
*    END-IF
ELSE
  COMPUTE #FAC-AMT  = MBRSHIP-MTD-DET-V.NUM-MBRSHIPS * 5000
  COMPRESS #FAC-K 'k' INTO #SCR-FACE-AMT (#ISC) LEAVING NO
END-IF
*                    NG3 Start
IF MBRSHIP-MTD-DET-V.USER-COMPANY NE 'S'    /* An IAS trans    /* DW9     /* RE1
  RESET #CAP-PAID-OFF                              /* DW9
  MOVE #FAC-AMT TO #HOLD-FAC-AMT                                  /* PM4
  FOR #I1 1 TO C*ACTIVE-SEGMENTS /* in case units changed by corr
    IF ST-V.ISSUE-DATE (#I1) = MBRSHIP-MTD-DET-V.ISSUE-DATE
      IF ST-V.ID-NUMBER = #LAST-CN /*IN CASES OF 2 POLS w same iss-dts
          AND (ST-V.ISSUE-DATE (#I1) = #LAST-ISS-DT)
          AND (CODE                  = #LAST-CODE)
        RESET #LAST-CN
        RESET #LAST-ISS-DT
        RESET #LAST-CODE
        ESCAPE TOP
      END-IF
      MOVE ST-V.FACE-AMOUNT (#I1) TO #FAC-AMT
      COMPRESS #FAC-K 'K' INTO #SCR-FACE-AMT (#ISC) LEAVING NO
      MOVE ST-V.ID-NUMBER TO #LAST-CN
      MOVE ST-V.ISSUE-DATE (#I1) TO #LAST-ISS-DT
      MOVE CODE                  TO #LAST-CODE
      IF   (CODE = 'V')   /* Laps-cap but policy still exists
          AND (CONV-DURATION (#I1) = 999)
          AND (STATUS (#I1) = 'D')
        MOVE TRUE TO #CAP-PAID-OFF
      END-IF
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  #I2 := #I1 + 1
  REPEAT UNTIL #I2 >= C*ACTIVE-SEGMENTS
    IF ST-V.ISSUE-DATE (#I2) = MBRSHIP-MTD-DET-V.ISSUE-DATE       /* PM4
        AND ST-V.FACE-AMOUNT (#I2) = #HOLD-FAC-AMT                   /* PM4
        AND ST-V.FACE-AMOUNT (#I1) NE #HOLD-FAC-AMT                  /* PM4
      IF ST-X-V.PROCESS-IND NE 'S'                                /* PM4
        MOVE ST-V.FACE-AMOUNT (#I2) TO #FAC-AMT                   /* PM4
        COMPRESS #FAC-K 'K' INTO #SCR-FACE-AMT (#ISC) LEAVING NO  /* PM4
      END-IF
      RESET #LAST-CN #LAST-ISS-DT #LAST-CODE                      /* PM4
      MOVE #I2 TO #I1                                             /* PM4
      ESCAPE BOTTOM                                               /* PM4
    END-IF
    ADD 1 TO #I2
  END-REPEAT
END-IF
*                    NG3 End
IF   (CODE = 'C' OR = 'L' OR = 'X' OR = 'S' OR = 'T' OR = 'U'
    OR = 'V' OR = 'F')                                              /* YK2
    AND (MBRSHIP-MTD-DET-V.USER-COMPANY NE 'S')  /* An IAS trans    /* DW9
    AND (ST-V.NUMBER-MEMBERSHIPS NE 0)
  COMPRESS #SCR-FACE-AMT (#ISC) '*'
    INTO #SCR-FACE-AMT (#ISC) LEAVING NO
END-IF
*
*    below removed 4/1/2004
* IF   (#CURR-MM-A NE #ACT-ENT-DT-MM)   /* not in curr month
*  AND (NOT CODE = 'N' OR = 'A' OR = 'Y' OR = 'Z')
*   MOVE 9 TO #I1   /* to usually print blank
* END-IF
*
IF #I1 > C*ACTIVE-SEGMENTS           /* vt 08/07/2015 \/
    OR #I1 = 0
  #I1 := 1 
  #i2 := 2
END-IF                               /* vt 08/07/2015 /\
*
IF MBRSHIP-MTD-DET-V.USER-COMPANY NE 'S'    /* An IAS trans    /* DW9
  RESET #N-O
  IF MONTH-ALLOC-AMT (#I1) = 999.99
    MOVE '96' TO #N-O
  END-IF
*
* IF   (ST-V.PLAN-A1 (#I1) GE 'U')                        /* YK1
*     AND (ST-V.PLAN-A1 (#I1) LE 'Z')                     /* YK1
  IF F-LT2-TRUE(<ST-V.PLAN(#I1)>)                         /* YK1
    IF CONV-DURATION (#I1) = 662
      MOVE 'CP' TO #N-O
    ELSE
      MOVE 'RG' TO #N-O
    END-IF
  END-IF
*
  IF   (PAY-STATUS (#I1) = '47')
      AND ((NOT ST-V.PLAN (#I1) = 'SPF')               /* TT2
      AND (NOT IS-ANNUITY-LIFE(<ST-V.PLAN (#I1)>)) )   /* TT2
    MOVE 'SP' TO ST-V.PLAN (#I1)
  END-IF
END-IF                                                   /* END DW9
*          NG3 Start
IF #I1 > C*ACTIVE-SEGMENTS
    OR #I1 = 0
  #I1 := 1
END-IF
IF MBRSHIP-MTD-DET-V.USER-COMPANY = 'S'    /* An SAS trans
  IF FUNCTION-SERVICE-PLAN(<ST-V.ID-NUMBER>) NE ' '
    #MD-PLAN := FUNCTION-SERVICE-PLAN(<ST-V.ID-NUMBER>)
  ELSE
    #MD-PLAN := 'SRV'
  END-IF
ELSE
  MOVE ST-V.PLAN (#I1) TO #MD-PLAN
END-IF
*
CALLNAT 'MSSNGCDS'
  MBRSHIP-MTD-DET-V.CODE
  MBRSHIP-MTD-DET-V.ISSUE-DATE
  MBRSHIP-MTD-DET-V.USER-COMPANY
  ST-X-V.INTRO-FLAG
  ST-X-V.PROCESS-IND
  ST-V.NUMBER-MEMBERSHIPS
  ST-V.RID-PLAN (#I1)
  #MD-PLAN
  #N-O
  #CAP-PAID-OFF
  #SCR-CODE (#ISC)
*       NG3 End
IF ST-X-V.PROCESS-IND NE 'S'
  MOVE ST-V.INTRO-FLAG TO #SCR-INTRO-IND (#ISC)
END-IF
*
END-SUBROUTINE                   /* END DW10
*
END
