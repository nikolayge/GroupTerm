* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1001NAT
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    03/09/2011  None    Program written                           *
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* NGG    03/23/2011 NG1 Skip pending approval STATUS record            *
************************************************************************
DEFINE DATA
PARAMETER
1 AT-ISN                (P8)  /* populated
1 #RANK-DATA                  /* populated
  2 #BRANCH             (A3)
  2 #RANK-ID            (N6)
  2 #RANK-IN-SAS        (A3)
1 #RANK-ABBR            (A10) /* populated
1 #RANK-ID-TXT          (A10)
1 #PAY-GRADE            (A3)  /* populated
*
LOCAL
1 #RANK-ABBR-DB         (A25)
1 #PAY-IND              (A1)
1 #MESSAGE-NBR          (N3)
1 #RETURN-FLAG          (L)
1 #ID-NUMBER            (N6)
1 #BRANCH-POS           (L)
1 #RANK-POS             (L)
*
1 RR VIEW OF A-RANK-REFERENCE
  2 RANK-IN-SAS
  2 RANK-ID
  2 RANK-ABBR
  2 PAY-GRADE
*
1 AT VIEW A-APPL-TRACKING
  2 RANK
  2 RANK-ID
  2 MIL-SERVICE
  2 MIL-STATUS
  2 MEMBER-CN (A6) 2 REDEFINE MEMBER-CN
    3 MEMBER-CN-N (N6)
1 CONTACT VIEW OF A-CONTACTS
  2 RANK-ID
1 STATUS VIEW OF A-STATUS
  2 MILITARY-SERVICE    (A3)
  2 PROCESS-IND         (A1)
  2 INTRO-FLAG          (A1)
*
END-DEFINE
*
RESET #RANK-DATA #RANK-ABBR #PAY-GRADE
IF AT-ISN = 0
  ESCAPE ROUTINE
END-IF
GET AT AT-ISN
IF *ISN = 0
  ESCAPE ROUTINE
END-IF
*
#ID-NUMBER := 1
*
#BRANCH := AT.MIL-SERVICE
#RANK-ID := AT.RANK-ID
MOVE RIGHT MEMBER-CN TO MEMBER-CN
IF MEMBER-CN-N > 0
  FIND (1) STATUS WITH ID-NUMBER = MEMBER-CN-N
    IF PROCESS-IND = 'C'
        AND INTRO-FLAG = 'A'
      IGNORE /* PENDING APPROVAL          /* NG1
    ELSE
      #BRANCH  := STATUS.MILITARY-SERVICE
      FIND CONTACT WITH ID-NUMBER = MEMBER-CN-N
        #RANK-ID := CONTACT.RANK-ID
      END-FIND
    END-IF
  END-FIND
END-IF
IF #RANK-ID > 0
  FIND RR WITH RANK-ID = #RANK-ID
    #RANK-IN-SAS := RR.RANK-IN-SAS
    #RANK-ABBR   := RR.RANK-ABBR
    #PAY-GRADE   := RR.PAY-GRADE
  END-FIND
ELSE
  #RANK-IN-SAS := #RANK-ABBR := AT.RANK
END-IF
*
IF *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA'
  COMPRESS '{' #RANK-ID '.' #RANK-IN-SAS '}' INTO #RANK-ID-TXT LEAVING NO
ELSE
  RESET #RANK-ID-TXT
END-IF
*
END
