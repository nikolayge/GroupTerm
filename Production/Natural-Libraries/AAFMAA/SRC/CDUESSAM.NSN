* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID : CDUESSAM
*
* UPDATER FOR AM FPDP/FPDR/DUEP/DUER (Dues processor)
*
****
*
* MODIFICATION LOG
* USER DATE       TAG  Description
* DRW  01/30/2008 DW1  Add DUEA TRAN CODE
* DRW  02/07/2008 DW2  Never let Dues Bal be negative
* YAK  04/29/2009 YK1  Fix error processing for BATCH 
*
DEFINE DATA
PARAMETER USING C2500AAM
*
PARAMETER
1 #ST-V-DUES-FPF          (P3.2)
1 #ST-V-DUES-BALANCE      (P5.2)
1 #ST-V-DUES-TOT-PAID     (P5.2)
*
1 #ILN1       (I2)
1 #ILN2       (I2)
1 #SG-TBL (15)
 2 #SG-CAT1 (A2)
 2 #SG-CAT2 (A2)
 2 #SG-AMT  (N6.2)
1 #ERROR-MESSAGE (A75) OPTIONAL                  /* YK1
*
LOCAL
*
1 #SG-YEAR    (A2)
1 #SG-PLAN    (A2)
1 #TOTAL-AMT-DISP (A8)
1 #DISP-MSG       (A75)
*
END-DEFINE
*
*
MOVE '**' TO #SG-YEAR
MOVE '**' TO #SG-PLAN
*
MOVE #SG-YEAR TO #SG-CAT1 (1)
MOVE #SG-PLAN TO #SG-CAT2 (1)
MOVE #SCR-TRAN-AMT (#ILN1) TO #SG-AMT (1)
*
IF #SCR-TRAN-CODE (#ILN1) = 'FPDP'
  IF #SCR-TRAN-AMT (#ILN1) GT #ST-V-DUES-FPF
    MOVE EDITED #ST-V-DUES-FPF (EM=ZZZ.99) TO #TOTAL-AMT-DISP
    COMPRESS 'Tran amt exceeds DUES FPF of ' #TOTAL-AMT-DISP
                                       INTO   #DISP-MSG
    IF *DEVICE = 'BATCH'                         /* YK1
        #ERROR-MESSAGE := #DISP-MSG              /* YK1
        ESCAPE ROUTINE IMMEDIATE                 /* YK1
    ELSE                                         /* YK1
     REINPUT #DISP-MSG
     MARK *#SCR-TRAN-CODE (#ILN1)
    END-IF                                       /* YK1
  END-IF
*
  SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-FPF
  ADD #SCR-TRAN-AMT (#ILN1) TO #ST-V-DUES-TOT-PAID
  ADD #ST-V-DUES-FPF TO #ST-V-DUES-BALANCE
  RESET #ST-V-DUES-FPF
  ESCAPE ROUTINE
END-IF
*
IF #SCR-TRAN-CODE (#ILN1) = 'FPDR'
  IF #SCR-ADJ (#ILN1) = '-'
    SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-FPF
    ADD      #SCR-TRAN-AMT (#ILN1)   TO #ST-V-DUES-TOT-PAID
  ELSE
    ADD #SCR-TRAN-AMT (#ILN1) TO #ST-V-DUES-FPF
    SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-TOT-PAID
  END-IF
  ESCAPE ROUTINE
END-IF
*
IF #SCR-TRAN-CODE (#ILN1) = 'DUEP'
  SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-BALANCE
*    above removed 2/99 and reinstated 9/99 as per elb
*
  if #st-v-dues-balance lt 0   /* Never let it go negative           DW2
    reset #st-v-dues-balance
  end-if
*
  ADD #SCR-TRAN-AMT (#ILN1) TO #ST-V-DUES-TOT-PAID
  ESCAPE ROUTINE
END-IF
*
IF #SCR-TRAN-CODE (#ILN1) = 'DUER'
  IF #SCR-ADJ (#ILN1) = '-'
    SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-BALANCE
*    above removed 2/99 and reinstated 9/99 as per elb
    if #st-v-dues-balance lt 0   /* Never let it go negative DW2
      reset #st-v-dues-balance
    end-if
*
    ADD      #SCR-TRAN-AMT (#ILN1) TO   #ST-V-DUES-TOT-PAID
  ELSE
    ADD #SCR-TRAN-AMT (#ILN1)      TO #ST-V-DUES-BALANCE
*    above removed 2/99 and reinstated 9/99 as per elb
    SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-TOT-PAID
  END-IF
  ESCAPE ROUTINE
END-IF
*
IF #SCR-TRAN-CODE (#ILN1) = 'DUEA'  /* DUES Adjustment w/o DUES-TOT-PAID adj   DW1
  IF #SCR-ADJ (#ILN1) = '-'
    SUBTRACT #SCR-TRAN-AMT (#ILN1) FROM #ST-V-DUES-BALANCE
*
    if #st-v-dues-balance lt 0   /* Never let it go negative           DW2
      reset #st-v-dues-balance
    end-if
  ELSE
    ADD #SCR-TRAN-AMT (#ILN1)      TO #ST-V-DUES-BALANCE
  END-IF
  ESCAPE ROUTINE
END-IF
*
END
