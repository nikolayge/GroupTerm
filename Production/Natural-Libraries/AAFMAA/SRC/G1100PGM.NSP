* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G1100PGM   *** menu processor ******
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW    05162005  DW1     Add Sex for LTII Rates ING
* DRW  09/27/2007 DW2 Delete AFSC Suffix's (MB/RP)
* YAK  04/10/2008 YK1 Call new program for
* bz  06/09/2013  Rebranding (Search for AAFMAA)
*                 Table and Rate Information
*******************************************
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING GONERLXX
LOCAL
1 #SCR-TEXT         (A75)
1 ST-V VIEW OF A-STATUS
 2 RANK
 2 NAME
*
1 #COMPANY     (A41)
1 #PLAN        (A3)  /* THESE 5 ARE EMPTY LINKAGE PARMS TO GPREMSXX
1 #AGE         (N2)
1 #APREM       (N7.2)  /* NG1
1 #ISSUE-DATE  (N8)
1 #SMOKER-FLAG (A1)
1 #FAC-AMT     (N7.2)
1 #SEX         (A1)    /* DW1
*
1 #LAST-NAME    (A25)
1 #FIRST/MID    (A25)
1 #SUFFIX       (A25)
1 #EXTRA1       (A25)
1 #EXTRA2       (A25)
1 #FORMATTED-NAME (A35)
*
* * GM INFO * * * * * * * * * * * * * * * * * * * * * * * *
1 #SCR-GM-TYP (A1/4)
*
* * PI INFO * * * * * * * * * * * * * * * * * * * * * * * *
1 #SCR-PI-TYP-CV (C/20)
1 #SCR-PI-TYP (A1/20)
1 REDEFINE #SCR-PI-TYP
 2 #SCR-PI-TYP-2 (A1/20)
1 REDEFINE #SCR-PI-TYP
 2 #SCR-PI-TYP-3 (A1/20)
1 REDEFINE #SCR-PI-TYP
 2 #SCR-PI-TYP-4 (A1/20)
1 REDEFINE #SCR-PI-TYP
 2 #SCR-PI-TYP-5 (A1/20)
*
* * TB INFO * * * * * * * * * * * * * * * * * * * * * * * *
1 #FUNCTION-ATTRIBUTES (A68)                       /* YK1
1 REDEFINE #FUNCTION-ATTRIBUTES
  2 #PROGRAM           (A8)
  2 #SEP1              (A1)
  2 #FUNCTION-PARM     (A7)
  2 #HEADER-1          (A50)
  2 #SECURITY-CODE     (N2)
*
* * MM INFO * * * * * * * * * * * * * * * * * * * * * * * *
1 #SCR-MM-TYP-CV (C/10)
1 #SCR-MM-TYP (A1/10)
1 REDEFINE #SCR-MM-TYP
 2 #SCR-MM-TYP-2 (A1/10)
1 REDEFINE #SCR-MM-TYP
 2 #SCR-MM-TYP-3 (A1/10)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
1 #YYYYMMDD         (N8)
1 REDEFINE #YYYYMMDD
 2 #CENT         (N2)
 2 #YY           (N2)
 2 #MM           (N2)
 2 #DD           (N2)
*
1 #I1              (I2)
1 #I2              (I2)
1 #MX              (I2)
1 #I               (N2)
*
END-DEFINE
* * * * * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * * * * *
*
IF #CGA-USER-COMPANY = 'S'
  MOVE '    Armed Forces Services Corporation    ' TO #COMPANY
ELSE
  MOVE 'AAFMAA' TO #COMPANY
END-IF
*
IF (#CGA-ISN NE 0) AND (#CGA-SCR-ID NE 0)
  GET ST-V #CGA-ISN
  SEPARATE NAME INTO #LAST-NAME #FIRST/MID #SUFFIX #EXTRA1 #EXTRA2
      WITH DELIMITER ','
  COMPRESS RANK #FIRST/MID #LAST-NAME #SUFFIX #EXTRA1 #EXTRA2
      INTO #FORMATTED-NAME
END-IF
*
DECIDE ON FIRST #CGA-SCR-SUFFIX
  VALUE 'PI'  PERFORM PROCESS-PI-MENU
  VALUE 'MM'  PERFORM PROCESS-MM-MENU
  VALUE 'AR'  PERFORM PROCESS-MM-MENU
  VALUE 'LA'  PERFORM PROCESS-MM-MENU
  VALUE 'LP'  PERFORM PROCESS-MM-MENU
  VALUE 'TB'  PERFORM PROCESS-TB-MENU
  VALUE 'PRM'    /* AAFMAA/AFSC PREMIUMS/DUES RATES
    CALLNAT 'GPREMSXX' #CGA-SCR-COMMAND #CGA-SCR-ID #CGA-SCR-SUFFIX
     #PLAN #AGE #APREM #ISSUE-DATE #SMOKER-FLAG #FAC-AMT
     #SEX                    /* DW1
   NONE IGNORE
END-DECIDE
*
MOVE '*' TO #CGA-SCR-COMMAND
*
INPUT TEXT #SCR-TEXT USING MAP 'G1100MGM'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF 
*
FOR #I1 1 TO 4
  IF #SCR-GM-TYP (#I1) NE ' '
    IF #MX NE 0
      RESET #MX
      REINPUT 'ONLY 1 TYPE CAN BE SELECTED'
      MARK *#SCR-GM-TYP (#I1)
    END-IF
    MOVE #I1 TO #MX
  END-IF
END-FOR
*
IF #MX = 0
  #MX := *CURS-LINE
  #MX := #MX - 9
END-IF
*
DECIDE ON FIRST #MX
  VALUE 1  MOVE 'GM'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'PI'  TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
           PERFORM PROCESS-PI-MENU
  VALUE 2  MOVE 'GM'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'TB'  TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
           PERFORM PROCESS-TB-MENU
  VALUE 3  MOVE 'GM'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'MM'  TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
           PERFORM PROCESS-MM-MENU
  VALUE 4  MOVE 'CR'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  NONE IGNORE
END-DECIDE
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-PI-MENU
* * * * * * * * * * * * * * * * *
*
* PROTECT UNUSED LINES ********
MOVE (AD=P) TO #SCR-PI-TYP-CV (15:20)
*
MOVE '*' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
*
INPUT TEXT #SCR-TEXT MARK *#SCR-PI-TYP (1) USING MAP 'G1100MPI'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
RESET #MX
FOR #I1 1 TO 20
  IF #SCR-PI-TYP (#I1) NE ' '
    IF #MX NE 0
      RESET #MX
      REINPUT 'ONLY 1 TYPE CAN BE SELECTED'
      MARK *#SCR-PI-TYP (#I1)
    END-IF
*
    MOVE #I1 TO #MX
  END-IF
END-FOR
*
IF #MX = 0
  REINPUT 'SELECT AN OPTION'
  MARK *#SCR-PI-TYP (1)
END-IF
*
DECIDE ON FIRST #MX
  VALUE 1  MOVE 'VP'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 2  MOVE 'VP'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'OVR' TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 3  MOVE 'AV'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 4  MOVE 'VW'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 5  MOVE 'VW'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE '13M' TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 6  MOVE 'VY'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 7  MOVE 'CO'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 8  MOVE 'CI'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 9  MOVE 'AD'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'PUP' TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 10 MOVE 'PO'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 11 MOVE 'ST'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 12 MOVE 'ST'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'LON' TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 13 MOVE 'AD'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           MOVE 'PIL' TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 14 MOVE 'BD'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
   NONE IGNORE
END-DECIDE
*
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-TB-MENU
* * * * * * * * * * * * * * * * *
*
  #PROGRAM := 'G1000PUI'
  #FUNCTION-PARM := 'RSUB'
  STACK TOP DATA #FUNCTION-ATTRIBUTES
  FETCH #PROGRAM
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-MM-MENU
* * * * * * * * * * * * * * * * *
*
IF #CGA-SCR-SUFFIX = 'AR' OR = 'LA' OR = 'LP'
  FETCH 'G4200PMS'
END-IF
*
* PROTECT UNUSED LINES ********
MOVE (AD=P) TO #SCR-MM-TYP-CV (9:10)
*
MOVE '*' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
*
INPUT TEXT #SCR-TEXT MARK *#SCR-MM-TYP (1) USING MAP 'G1100MMM'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
RESET #MX
FOR #I1 1 TO 10
  IF #SCR-MM-TYP (#I1) NE ' '
    IF #MX NE 0
      RESET #MX
      REINPUT 'ONLY 1 TYPE CAN BE SELECTED'
      MARK *#SCR-MM-TYP (#I1)
    END-IF
*
    MOVE #I1 TO #MX
  END-IF
END-FOR
*
IF #MX = 0
  REINPUT 'SELECT AN OPTION'
  MARK *#SCR-MM-TYP (1)
END-IF
*
DECIDE ON FIRST #MX
  VALUE 1  MOVE 'MS'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 2  MOVE 'MK'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 3  MOVE 'MD'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 4  MOVE 'AR'  TO #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
           FETCH 'G4200PMS'
  VALUE 5  MOVE 'AS'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 6  MOVE 'AC'  TO #CGA-SCR-COMMAND #CGA-ORIG-COMMAND
           RESET         #CGA-SCR-SUFFIX  #CGA-ORIG-SUFFIX
  VALUE 7  MOVE 'LA'  TO #CGA-SCR-SUFFIX
           FETCH 'G4200PMS'
  VALUE 8  MOVE 'LP'  TO #CGA-SCR-SUFFIX
           FETCH 'G4200PMS'
   NONE IGNORE
END-DECIDE
*
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
END
