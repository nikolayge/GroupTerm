* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: G3510PAD     ** FETCHED FROM G3500PAD
***********************************************
*                       MODIFICATION LOG
* USER   DATE      TAG  REASON
* DRW    09282004  DW1  add CREDIT CARD processing
* PAM    10222004  PM1  Adjust RFG letter for multiple Resignations
*                       in the same month
* DRW    01062005  DW2  add Suffix BLB/CLR/COB
* DRW    01282005  DW3  add Suffix NEW
* PAM    03112005  PM4  Reset display fields for GRANKSXX
* DRW    05012005  DW5  Get AL Info for AD AL
* PAM    06152005  PM6  Revised AD AL to loop through all AL policies
* PAM    09222005  PM7  Adjust RFG letter for policy numbers
* PAM    09222005  PM8  Added text for replacement letters
* PAM    10112005  PM9  Fixed problem with Last-E - use rsgn-cnt not I1
* PAM    11072005  PM10 Fixed problem selection count
* DRW    05102006  DW11 check alot-chg-auth to change letter
* DRW    08132007  DW12 AW2 Series & sho spc sceeen if AW2 just like TML
* DRW  02/01/2009  DW13 Make RET SERIES go to Write (1)
* PAM  08/23/2010  PM14 Added new address storage area
* BZ    10/26/2010 bz1 fix typo
* vxt   11/12/2010 vt1 change the source for name
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* vxt   12/06/2013 vt2 fix the AD-TABLE arrea size
* NGG   07/07/2014  NG1 CONTACTS NORMALIZATION - GENDER
* TMT   10/13/2014 TT1 ANNUITY LIFE HARMONIZATION
***********************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
1 NT-V VIEW OF A-NOTES
  2 NOTE-DATE  (60)
  2 NOTE-TEXT  (A60/60)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-OCC  (60)
      4 NOTE-TEXT-60   (A60)
      4 REDEFINE NOTE-TEXT-60
        5 NOTE-TEXT-25   (A25)
        5 NT-YEAR        (A5)
        5 NOTE-TEXT-AMT  (A7)
        5 REDEFINE NOTE-TEXT-AMT
          6 NT-AMT-D-SGN (A1)
          6 NT-AMT-DOLS  (A3)
          6 NT-AMT-DEC   (A1)
          6 NT-AMT-CENTS (A2)
        5 NT-CHK-#       (A23)
      4 REDEFINE NOTE-TEXT-60    /* DW5  AL Note
        5 NT-AL-EF-POL-ISS  (A10) /* DW5
        5 NT-AL-EF-MM       (A02)
        5 NT-AL-EF-SL1      (A01)
        5 NT-AL-EF-DD       (A02)
        5 NT-AL-EF-SL2      (A01)
        5 NT-AL-EF-YYYY     (A04)
        5 NT-AL-EF-SL3      (A01)
        5 NT-AL-EF-ANNUITIZ (A14)
        5 NT-AL-ON-MM       (A02)
        5 NT-AL-EF-SL4      (A01)
        5 NT-AL-ON-DD       (A02)
        5 NT-AL-EF-SL5      (A01)
        5 NT-AL-ON-YYYY     (A04)
        5 NT-AL-REST        (A15)
*
1 ST-V VIEW OF A-STATUS                                           /* PM1
  2 ID-NUMBER         (N6)
  2 MEMBER-CONTACT-ID (N8)
* 2 SEX
  2 ALOT-CHG-AUTH
* 2 DATE-OF-BIRTH
  2 ISSUE-DATE        (N8/9)
  2 REDEFINE ISSUE-DATE
    3 PLAN-OCC         (9)
      4 ISSUE-DATE-YYYY (A4)
      4 ISSUE-DATE-MM   (A2)
      4 ISSUE-DATE-DD   (A2)
  2 STATUS            (9)
  2 PLAN              (9)
  2 FACE-AMOUNT       (9)
1 OBSOLETE-ST-V       /* NG1
  2 SEX               (A1)
  2 DATE-OF-BIRTH     (N8)
  2 DATE-OF-BIRTH-SP  (N8)
  2 SEX-SP            (A1)
  2 FORMER-SPOUSE-DT-OF-BIRTH (N8)
*
1 #AL-NT-ISS-DT    (N8)                    /* DW5
1 #AL-NT-ON-DT     (N8)                    /* DW5
1 #AL-ISS-DT       (N8/9)
1 REDEFINE #AL-ISS-DT
  2 #AL-ISS-DT-OC   (9)
    3 #AL-ISS-DT-A   (A8)
1 #AL-X-9          (I2/9)
1 #AL-X            (I2)
1 #SELECT-POL      (A1/9)
1 #POL-CV          (C/9)
1 #POL-ISS-DATE    (A10/9)
1 #POL-NUMBER      (A16/9)
1 #POL-FACE        (A13/9)
1 #POL-PLAN        (A2/9)
1 #ANNUITIZED      (L)
1 #I3              (I2)
1 #NOT-X           (I2)
*
1 #POL-NUM      (A16)
1 #BOP-DB-AN   (N7.2)   /* DW5
1 #BOP-SSN     (N9)
1 #BOP-SEX     (A1)
1 #BOP-BENE-AGE(N4)
1 #BOP-BENE-A  (N7.2)
1 #BOP-BENE-B  (N7.2)
1 #BOP-BENE-C  (N7.2)
1 #BOP-RESV-AMT-A (N9.2)
1 #BOP-RESV-AMT-B (N9.2)
1 #BOP-TAXBL-PCT-A  (N2.7)
1 #BOP-TAXBL-PCT-B  (N2.7)
1 #BOP-TAXBL-AMT-A  (N7.2)
1 #BOP-TAXBL-AMT-B  (N7.2)
1 #BOP-TAXBL-MON-A  (N7.2)
1 #BOP-TAXBL-MON-B  (N7.2)
*
1 #AMT-DISP    (A12)
1 #ISS-DT-DISP (A10)
1 #SUFFIX     (A3)       /* DW5
1 #AS-OF-DT   (N8)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #FACE-AMT    (N7.2/9)
1 #TPP         (N7.2/9)
1 #CSV-P      (P7.2/9)
*
1 #SCR-ISSUE-DATE         (A10/9)                                 /* PM1
1 REDEFINE #SCR-ISSUE-DATE
  2 #SCR-ISSUE-DATE-OCC    (9)
    3 #SCR-ISSUE-DATE-MM    (N2)
    3 #SCR-ISSUE-DATE-/1    (A1)
    3 #SCR-ISSUE-DATE-DD    (N2)
    3 #SCR-ISSUE-DATE-/2    (A1)
    3 #SCR-ISSUE-DATE-YYYY  (N4)
1 #SCR-SELECT             (A1/9)                                  /* PM1
1 #SCR-SEL-CV             (C/9)
1 #SCR-POL-NUM            (A16/9)                                 /* PM7
1 #SCR-ISS-DATE           (A10)                                   /* PM7
1 #RSGN-CNT               (I2)
1 #LAST-E                 (I2)                                    /* PM7
1 #MULT-RSGN              (L)
1 #SCR-SEL-CNT            (P5)
1 #SCR-LOAD-CNT           (P5)
1 #FIND-ID-NUMBER         (A6)
1 #OWNER-ID-NUMBER        (A6)
1 REDEFINE #OWNER-ID-NUMBER
  2 #OWNER-ID-NUMBER-DIGIT (A1/6)
1 REDEFINE #OWNER-ID-NUMBER
  2 #OWNER-ID-NUMBER-N     (N6)
*
1 #TRM-REFUND-NOTE (L)
1 #TRM-REFUND-AMT  (A7)
1 #WYYMMDD
  2 #XYY      (N2)
  2 #XMM      (N2)
  2 #XDD      (N2)
1 REDEFINE #WYYMMDD
  2 #WYYMMDD-N (N6)
*
1 #AMT    (N3.2)
1 REDEFINE #AMT
  2 #AMT-DOLS  (A3)
  2 REDEFINE #AMT-DOLS
    3 #AMT-DOLS-X  (A1/3)
  2 #AMT-CENTS (A2)
1 #FORM-IND      (A3)    /* PM - ADDED FOR DALLAS PRINT - 06/2000
1 #EDS-ZIP-BAR-CODE  (A14)
1 #DT-TO-MM/DD/YY   (A8) INIT <'XX/XX/XX'>
1 #DT-FROM-MM/DD/YY (A8) INIT <'XX/XX/XX'>
1 #MODE-DESC (A10)  INIT <'??????????'>
1 #THESE      (A6) INIT <' This'>
1 #AN         (A3) INIT <' an'>
1 #REMAINS    (A8) INIT <' remains'>
1 #THEY       (A5) INIT <' it'>
1 #LOANS      (A6) INIT <' loan'>
1 #ARE        (A4) INIT <' is'>
1 #WERE       (A5) INIT <' was'>
1 #VARIABLE   (L)
1 #FIXED      (L)
1 #BOTH       (L)
1 #RPU-TEXT (A78/16)
1 #WORK-AMT    (P7.2)
1 #DOLLARS-DISP        (A6)
1 #NEXT-MM             (N2)
*
1 #LOAN-AMT        (P6.2)
1 #LOAN-AMT-2      (P6.2)
1 #RESTRICT-MBR-SW (A1)
1 #SUSPEND-CODE    (A1)
1 #HOLD-EDIT-ZIP   (A10)
1 REDEFINE #HOLD-EDIT-ZIP
  2 #HOLD-ZIP-5     (A5)
  2 #HOLD-ZIP2      (A5)
*
1 #S-X     (I2)
1 #TEXT    (A40/22)
1 #SCR-ASS-RET-DT (A11)
1 #DATX     (A10)
1 #DATX-ESTATE (A10)
1 #I2       (I2)
1 #AD-FF-3  (A78)    1 #AD-FF-4  (A78)
1 #AD-FF-5  (A78)    1 #AD-FF-6  (A78)
1 #AD-ADDR-MSG  (A30/3)
1 REDEFINE #AD-ADDR-MSG                                           /* PM1
  2 #ADDR-MSG-OCC          (3)
    3 #ADDR-MSG-S-1         (A1)
    3 #ADDR-MSG-CN          (A7)
    3 #ADDR-MSG-S-2         (A1)
    3 #ADDR-MSG-MEM-CN-A    (A6)
    3 REDEFINE #ADDR-MSG-MEM-CN-A
      4 #ADDR-MSG-CN-DIGIT   (A1/6)
    3 #ADDR-MSG-REST        (A15)
*
1 #AD-TABLE (A78/14)                              /* vt2
1 REDEFINE #AD-TABLE
  2 #AD-FF-12 (A78)    2 #AD-FF-13 (A78)
  2 #AD-FF-14 (A78)    2 #AD-FF-15 (A78)
  2 #AD-FF-16 (A78)    2 #AD-FF-17 (A78)
  2 #AD-FF-18 (A78)    2 #AD-FF-19 (A78)
  2 #AD-FF-20 (A78)    2 #AD-FF-21 (A78)
  2 #AD-FF-22 (A78)    2 #AD-FF-23 (A78)
  2 #AD-FF-24 (A78)
*
1 #AD-TABLE-2 (A78/9)
1 #NO-MAILOUT       (L)
1 #FOREIGN-ADDR     (L)
*
1 #ISSUE-DATE (N8)
1 REDEFINE #ISSUE-DATE
  2 #ISS-DT-CENT (A2)
  2 #ISS-DT-YY   (A2)
  2 #ISS-DT-MM   (A2)
  2 #ISS-DT-DD   (A2)
*
1 #MODE  (A1)
1 #PAID-TO-DATE (N8)
1 REDEFINE #PAID-TO-DATE
  2 #PT-CENT (N2)
  2 #PT-YY   (N2)
  2 #PT-MM   (N2)
  2 REDEFINE #PT-MM
    3 #PT-MM-A (A2)
  2 #PT-DD   (N2)
1 #ERROR-MESSAGE  (A65)
1 #CLERK-ABB-PASS  (A3)
1 #INVALID   (L)
1 #CLERK-ABB-NAME-PASS (A20)
*
1 #CORR-MSG (A50) CONST<'This correspondence has been prepared by:'>
*
1 #DATU       (A8)  /* 'MM/DD/YY'
1 REDEFINE #DATU
  2 #CURR-MM  (N2)
  2 #CURR-F1  (A1)
  2 #CURR-DD  (N2)
  2 #CURR-F2  (A1)
  2 #CURR-YY  (N2)
1 #PREV-YY    (N2)
1 #DAY-LITERALS (A24) INIT <'312831303130313130313031'>
1 REDEFINE #DAY-LITERALS 2 #DAY-LIT (A2/12)
*
1 #MONTH-LITERALS (A48) INIT<'Jan Feb Mar Apr May Jun Jul Aug Sep '
  -'Oct Nov Dec '>
1 REDEFINE #MONTH-LITERALS   2 #MON-LIT (A4/12)
*
1 #F-M-L (A108) INIT<'January  February March    April    May      '
  -'June     July     August   SeptemberOctober  November December '>
1 REDEFINE #F-M-L            2 #F-MON-LIT (A9/12)
*
1 #SCR-MIL-ID              (A6)
1 #SCR-NAME-RET            (A70)                               /* vt1 was 31
*
1 #SCR-AS-OF-LINE    (A11)
1 REDEFINE #SCR-AS-OF-LINE
  2 #SCR-AS-OF-DAY    (A2)
  2 #SCR-AS-OF-FILL   (A1)
  2 #SCR-AS-OF-MON    (A4)
  2 #SCR-AS-OF-YEAR   (N4)
*
1 #YYYYMMDD  (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A   (A8)
*
1 #MON-X      (I2)
1 #W-YYYYMM   (N7)
1 REDEFINE #W-YYYYMM
  2 #W-0CENT  (N3)
  2 #W-YY     (N2)
  2 #W-MM     (N2)
*
1 #LAST-MON   (N8)
1 REDEFINE #LAST-MON
  2 #LAST-MON-YYYY  (N4)
  2 #LAST-MON-MM    (N2)
  2 #LAST-MON-DD    (N2)
*
1 #RET       (A3)
1 #DEL       (A1) CONST<','>
1 #DELL      (A1)
*
1 #A-X             (I2)
1 #D-X             (I2)
*
1 #W-AMT          (A10)
* 1 #FULL-RANK       (A23)
* 1 #EDIT-INVALID (L)
* 1 #RANK-LITERAL (A23)
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #ID-NUM               (N6)
1 REDEFINE #ID-NUM
  2 #ID-NUM-A            (A6)
* 1 #HOLD-RANK-5           (A5)                                          /* vt1 \/
* 1 REDEFINE #HOLD-RANK-5
*  2 #HOLD-RANK           (A3)
*  2 #HOLD-RANK-LST-2     (A2)                                            /* vt1 /\
1 #HOLD-NAME             (A70)                                          /* vt1 was 25
1 #HOLD-LAST-NAME        (A25)
1 #HOLD-SSN              (N9)
1 REDEFINE #HOLD-SSN
  2 #SSN-1                (A3)
  2 #SSN-2                (A2)
  2 #SSN-3                (A4)
1 #HOLD-DEBIT-CREDIT-AMT (P7.2)
1 #HOLD-TERM-PREM-REF-DUE (P5.2)
1 #HOLD-TERM-DEBIT-CREDIT (P5.2)
1 #HOLD-ALOT-AMT         (P5.2)
*
1 #FN                    (A70)                                       /* vt1 was 45 \/
1 #EXT-DEAR-FORMATED-NAME    (A70)
1 #FORMATED-NAME-LEFT    (A70)
1 #EXT-FORMATED-NAME         (A70)
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-2               (A2)
  2 #FN-LST-43              (A68)                                     /* vt1 was 43
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-5               (A5)
  2 #FN-LST-40              (A65)                                     /* vt1 was 40
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-7               (A7)
  2 #FN-LST-38              (A63)                                      /* vt1 was 38
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-10              (A10)
  2 #FN-LST-35              (A60)                                       /* vt1 was 35
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-12              (A12)
  2 #FN-LST-33              (A58)                                       /* vt was 33
1 REDEFINE #EXT-FORMATED-NAME
  2 #FN-1ST-15              (A15)
  2 #FN-LST-30              (A55)                                       /* vt1 was 30 /\
*
1 #FIRST-ADDRESS         (A70)    /* PM14
1 #SECOND-ADDRESS        (A70)    /* PM14
1 #THIRD-ADDRESS         (A70)    /* PM14
1 #MIL-STATUS            (A1)
1 #MIL-SERVICE           (A3)
1 #FORMAT-MEMSHIP        (A2)
1 #NUM-PDUP              (P2)
1 #PASSED-AMT            (P5.2)  /* passed from g3500pad
1 #HOLD-EDIT-PREM        (A8)
1 #ALLOT-NUM             (A5)
1 #COMP-CODE             (A7)
1 #DOB-SP    (N8)
1 #DOM       (N8)
1 #DOD       (N8)
1 #SSN-SP    (N9)
1 #WIDOW-FLAG (A1)
1 #NUMBER-MEMBERSHIPS  (P1)
1 #PASSED-DB       (P7.2)
1 #PASSED-LOAN-INT (P7.2)
1 #PASSED-LPO-INT  (P5.2)
1 #AMT-12          (A12)
*
1 #memo-to-name    (a70)                             /* vt1
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* - - - - - - - -
*
* INPUT #ID-NUM #HOLD-NAME #HOLD-SSN #HOLD-LAST-NAME #HOLD-RANK-5               /* vt1
INPUT #ID-NUM #HOLD-NAME #HOLD-SSN #HOLD-LAST-NAME #MEMO-TO-NAME                /* vt1
*      #FORMATED-NAME #FIRST-ADDRESS #SECOND-ADDRESS #THIRD-ADDRESS             /* vt1
  #HOLD-DEBIT-CREDIT-AMT #HOLD-ALOT-AMT
  #ext-formated-name #first-address #second-address #third-address          /* vt1
  #MIL-STATUS #MIL-SERVICE
  #MODE #PAID-TO-DATE #LOAN-AMT #LOAN-AMT-2
  #RESTRICT-MBR-SW #SUSPEND-CODE
  #EDS-ZIP-BAR-CODE #AD-FF-3 #PASSED-AMT #HOLD-TERM-PREM-REF-DUE
  #HOLD-TERM-DEBIT-CREDIT
  #ISSUE-DATE #EXT-DEAR-FORMATED-NAME #AD-ADDR-MSG (1:3)                     /* vt1
  #DOB-SP #DOM #DOD #SSN-SP #WIDOW-FLAG #NUMBER-MEMBERSHIPS
  #PASSED-DB #PASSED-LOAN-INT #PASSED-LPO-INT
*
* WRITE 'top of G3510pad' #CGA-SCR-SUFFIX
*
* MOVE #FORMATED-NAME TO #FORMATED-NAME-LEFT                        /* vt1
move #ext-formated-name to #formated-name-left                     /* vt1
*
MOVE *DATU TO #DATU
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #DATX WITH '/'
MOVE #DATX TO #DATX-ESTATE
MOVE *DATN TO #LAST-MON
SUBTRACT 1 FROM #LAST-MON-MM
IF #LAST-MON-MM LT 1
  ADD 12 TO #LAST-MON-MM
  SUBTRACT 1 FROM #LAST-MON-YYYY
END-IF
*
* MOVE #HOLD-RANK-5 TO #FULL-RANK                                               /* vt1 \/
* IF #HOLD-RANK-LST-2 = ' '
*  RESET #DISPL #RANK-LITERAL #HOLD-RANK-5                         /* PM4
*  CALLNAT 'GRANKSXX' #PASS-COMMAND #HOLD-RANK #MIL-SERVICE
*   #EDIT-INVALID #DISPL #RANK-LITERAL #HOLD-RANK-5
*  MOVE #RANK-LITERAL TO #FULL-RANK
* END-IF                                                                         /* vt1 /\
*
DECIDE FOR EVERY CONDITION
  WHEN #CGA-SCR-SUFFIX = 'REF' OR = 'RFS' OR = 'RFC' OR = 'RFG'
      OR = 'RFF' OR = 'RFD' OR = 'RFZ' OR = 'RFX'
      OR = 'AL' OR = 'RPL'                            /* DW5 PM8
    PERFORM REF-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'BLB' OR = 'CLR' OR = 'COB'   /* DW2
      OR = 'NEW'                         /* DW3
    PERFORM BEN-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'RAN'   /* BRAD ENTERS ON SBP VALUATION SCN
    MOVE '*' TO #CGA-SCR-COMMAND
    PERFORM DISP-SPC-SCN
    MOVE 'RET' TO #CGA-SCR-SUFFIX
      #CGA-ORIG-SUFFIX
  WHEN #CGA-SCR-SUFFIX = 'RET'
    PERFORM RET-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'APL'
    PERFORM APL-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'SEP'           /* ONLY A LBL FOR SEP
    IGNORE
  WHEN #CGA-SCR-SUFFIX = 'R' OR = 'S'
    PERFORM RIQ-SIQ-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'RC '
    PERFORM RC-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'FAL'
    PERFORM FAL-PROCESS
  WHEN #CGA-SCR-SUFFIX = 'SCF' OR = 'SCG' OR = 'SCI' /* not SCH
    PERFORM SCX-PROCESS
  WHEN NONE IGNORE
END-DECIDE
*
IF   (#CGA-SCR-SUFFIX = 'R' OR = 'S' OR = 'RET' OR = 'SEP' OR = 'TML'
    OR = 'AW2' OR = 'AWS')                    /* DW12
    AND (*DEVICE NE 'BATCH')
  MOVE '*' TO #CGA-SCR-COMMAND
END-IF
*
IF #ID-NUM = 38501 /* wants to not see info on envelope
  RESET #ID-NUM #MIL-STATUS #MIL-SERVICE
END-IF
*
IF *DEVICE = 'BATCH'
  IF #CGA-SCR-SUFFIX = 'SCH'
    NEWPAGE (8)
*    WRITE (8) NOTITLE USING FORM 'G3513FAD'         /*  db--bz
    ESCAPE ROUTINE     /*    form prints in g3500pad
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'FAL' OR = 'RFS' OR = 'RFC' OR = 'RFZ' OR = 'APL'
      OR = 'SCF' OR = 'SCG' OR = 'SCI' OR = 'REF' OR = 'RFX'
    MOVE #CGA-SCR-SUFFIX TO #FORM-IND  /* PM - 06/2000 - DALLAS PRINT
    MOVE ' ' TO #CGA-SCR-SUFFIX        /* PM - 06/2000 - DALLAS PRINT
    NEWPAGE (8)
*   WRITE (8) NOTITLE USING FORM 'G3506FAD'  /* 6X9
    WRITE (8) NOTITLE USING FORM 'G3509FAD'  /* #10
    ESCAPE ROUTINE
  END-IF
  IF #CGA-CLERK NE 'RT*'  /* 'rt*' is 2nd ret assum dt request
    IF #CGA-SCR-SUFFIX = 'RET'
      MOVE 'RET' TO #FORM-IND
    END-IF
    EJECT (1)                                     /* DW13
    WRITE (1) NOTITLE USING FORM 'S3503FAD'       /* DW13 /* bz01
    MOVE 1 TO #CGA-SECURITY-CODE
  END-IF
ELSE
  IF #CGA-SCR-SUFFIX = 'RFG' OR = 'REF'
    IF ALOT-CHG-AUTH = 'Y' OR = 'S'    /* DW11
      MOVE 'ST' TO #CGA-SCR-COMMAND
    ELSE
      MOVE 'FA' TO #CGA-SCR-COMMAND
    END-IF
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'RPL'                                      /* PM8
    MOVE 'ST' TO #CGA-SCR-COMMAND                                 /* PM8
    RESET #CGA-SCR-SUFFIX                                         /* PM8
  END-IF                                                          /* PM8
*
  IF #CGA-SCR-SUFFIX = 'LPS' OR = 'LPL' OR = 'RPU'
    INPUT WITH TEXT #ERROR-MESSAGE USING MAP 'G3511MAD'
  ELSE
    IF #CGA-ORIG-SUFFIX NE 'AWS' /* No scn if doing AW2 Summary/* DW12
      INPUT WITH TEXT #ERROR-MESSAGE USING MAP 'G3500MAD'
    END-IF
  END-IF
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
END-IF
*
IF #CGA-SCR-SUFFIX = 'R' OR = 'S' OR = 'RET' OR = 'SEP' OR = 'TML'
*     OR = 'AW2'   /* later removed       /* DW12
  PERFORM DISP-SPC-SCN      /* SPECIAL SCREEN SCREEN
END-IF
*
IF *DEVICE = 'BATCH'    /* TO MAKE POSSIBLE FETCH RETURN FROM AOAP006E
  ESCAPE ROUTINE
END-IF
*
IF (#CGA-ORIG-SUFFIX = 'RET' OR = 'SEP' OR = 'R' OR = 'S'
    OR = 'TML' OR = 'AW2' OR = 'AWS')        /* DW16 DW12
    AND (#CGA-SCR-ID = #CGA-ORIG-ID)
  MOVE 'DI'  TO #CGA-SCR-COMMAND
END-IF
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FAL-PROCESS
* * * * * * * * * * * * * * * *
*
IF #MIL-STATUS NE 'A'
  MOVE 'Must be Active' TO #ERROR-MESSAGE
  ESCAPE ROUTINE
END-IF
*
* below logic is also in c2800pfa
IF #ID-NUM GE 600000
  RESET #ALLOT-NUM
  IF   (#MIL-SERVICE = MASK ('A'))
      AND (#MIL-STATUS = 'A')
    MOVE 'X085301' TO #COMP-CODE
  ELSE
    IF   (#MIL-SERVICE = MASK ('F'))
        AND (#MIL-STATUS = 'A')
      MOVE 'S957893' TO #COMP-CODE
    ELSE
      IF  ((#MIL-SERVICE = MASK ('A'))
          OR  (#MIL-SERVICE = MASK ('F'))
          OR  (#MIL-SERVICE = MASK ('M'))
          OR  (#MIL-SERVICE = MASK ('N')))
          AND (#MIL-STATUS = 'R')
        MOVE 'N90' TO #COMP-CODE
      ELSE
        MOVE '???????' TO #COMP-CODE
      END-IF
    END-IF
  END-IF
ELSE
  IF #MIL-SERVICE = MASK ('A')
    MOVE '064' TO #ALLOT-NUM
    MOVE 'K000966' TO #COMP-CODE
  ELSE
    MOVE 'N060041' TO #COMP-CODE
  END-IF
END-IF
*
MOVE EDITED #PASSED-AMT (EM=Z,ZZZ.99) TO #HOLD-EDIT-PREM
*
MOVE #EXT-DEAR-FORMATED-NAME TO #AD-FF-12                               /* vt1
*
IF #ID-NUM GE 600000
  COMPRESS '  Per your request, the information needed to help you'
    'establish an allotment'                INTO #AD-FF-13
  COMPRESS 'for your AFSC service processing is furnished.'
    'This information is what is'        INTO #AD-FF-14
  COMPRESS 'needed to complete the Finance Allotment Form at your'
    'local office.'            INTO #AD-FF-15
  COMPRESS '----------------------------------------------------------'
    '-------------------'      INTO #AD-FF-16 LEAVING NO
  COMPRESS 'Account No:' #CGA-ID ' Monthly Amt of Allotment Start: $'
    #HOLD-EDIT-PREM            INTO #AD-FF-17
  COMPRESS 'Type of Allotment  - Descretionary'
    INTO #AD-FF-18
  COMPRESS '                                       Company Code #'
    #COMP-CODE                 INTO #AD-FF-19
  COMPRESS 'Please start my Allotment to: AFSC (Money Market)'
    INTO #AD-FF-20
  COMPRESS '                              2800'
    'Shirlington Road Suite 350'
    INTO #AD-FF-21
  COMPRESS '                              Arlington, VA 22206-3601'
    INTO #AD-FF-22
ELSE
  COMPRESS '  Per your request, the information needed to help you'
    'establish an allotment'                INTO #AD-FF-14
  COMPRESS 'for your AAFMAA premiums and/or dues is furnished.'
    'This information is what is'        INTO #AD-FF-15
  COMPRESS 'needed to complete the Finance Allotment Form at your'
    'local office.'            INTO #AD-FF-16
  COMPRESS '----------------------------------------------------------'
    '-------------------'      INTO #AD-FF-17 LEAVING NO
  COMPRESS 'Account No:' #CGA-ID ' Monthly Amt of Allotment Start: $'
    #HOLD-EDIT-PREM            INTO #AD-FF-18
  COMPRESS 'Type of Allotment  - Life Insurance    Blanket Allotment #'
    #ALLOT-NUM                 INTO #AD-FF-19
  COMPRESS '                                       Company Code #'
    #COMP-CODE                 INTO #AD-FF-20
  COMPRESS 'Please start my Allotment to: AAFMAA'
    INTO #AD-FF-21
  COMPRESS '                              102 Sheridan Ave  Fort Myer'
    'Va 22211-1110'            INTO #AD-FF-22
END-IF
*
MOVE 22 TO #I2
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE /* (5165)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SCX-PROCESS
* * * * * * * * * * * * * * * *
*
MOVE #EXT-DEAR-FORMATED-NAME TO #AD-FF-12                                /* vt1
*
MOVE EDITED #PASSED-DB (EM=ZZZ,ZZZ.99) TO #AMT-12
COMPRESS 'During an address search, we found what we believe to'
  'be your current address.'      INTO #AD-FF-13
COMPRESS 'We will continue to use this updated address unless you'
  'notify us differently.'        INTO #AD-FF-14
MOVE EDITED #PASSED-DB (EM=ZZZ,ZZZ.99) TO #AMT-12
COMPRESS '$' #AMT-12 '.' INTO #AMT-12 LEAVING NO
COMPRESS 'The current death benefit of your policy(ies) is' #AMT-12
  'This amount' INTO #AD-FF-15
MOVE EDITED #PASSED-LOAN-INT (EM=ZZZ,ZZZ.99) TO #AMT-12
COMPRESS '$' #AMT-12 '.' INTO #AMT-12 LEAVING NO
COMPRESS 'will be reduced by your outstanding loan and accrued'
  'interest of' #AMT-12    INTO #AD-FF-16
COMPRESS 'You are not currently paying premiums. Our Constitution'
  '(your life insurance'     INTO #AD-FF-17
COMPRESS 'policy) allows us to issue a loan at our current interest'
  'rate against the'         INTO #AD-FF-18
COMPRESS 'cash value of your policy to pay premiums. To restore'
  'your account to full'     INTO #AD-FF-19
COMPRESS 'paying status, please call our Financial'
  'Management Department at'  INTO #AD-FF-20
COMPRESS '1-800-522-5221, ext 405 or 407 or complete and return'
  'the enclosed form.'     INTO #AD-FF-21
*
MOVE 9 TO #I2    /* (20 - 11)
PERFORM CORR-MSG-FORMAT
*
IF *DEVICE NE 'BATCH'
  MOVE #AD-TABLE (13) TO #AD-TABLE (12)
  MOVE 13 TO #I2
END-IF
*
DECIDE ON FIRST VALUE #CGA-SCR-SUFFIX
  VALUE 'SCF'
    COMPRESS '                                          Financial'
      'Mgmt Dept'     INTO #AD-TABLE (#I2)
  VALUE 'SCG'
    COMPRESS '                                          Ins'
      'Admin Dept'    INTO #AD-TABLE (#I2)
  VALUE 'SCH'
    COMPRESS '                                          Member'
      'Services Dept' INTO #AD-TABLE (#I2)
  VALUE 'SCI'
    COMPRESS '                                          Membership'
      'Dept'          INTO #AD-TABLE (#I2)
  NONE IGNORE
END-DECIDE
*
IF *DEVICE NE 'BATCH'
  MOVE 'SCP' TO #CGA-SCR-SUFFIX
  MOVE 'Press ENTER to process "SCP"' TO #ERROR-MESSAGE
END-IF
*
END-SUBROUTINE /* (5165)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE REF-PROCESS
* * * * * * * * * * * * * * *
*
FIND ST-V WITH MEMBER-KEY = #ID-NUM-A               /* DW11
END-FIND
*
IF #CGA-SCR-SUFFIX = 'AL'                /* DW5
  PERFORM AL-PROCESSING
  ESCAPE ROUTINE
END-IF
*
IF #CGA-SCR-SUFFIX = 'RFG' OR = 'RPL'                             /* PM8
  RESET #SCR-ISSUE-DATE (*) #SCR-SELECT (*) #SCR-SEL-CV (*)       /* PM1
    #MULT-RSGN #SCR-SEL-CNT #SCR-LOAD-CNT
    #LAST-E #SCR-POL-NUM (*)                                  /* PM7
  MOVE (AD=P) TO #SCR-SEL-CV (*)
  IF #ADDR-MSG-CN (2) = 'With CN'
    FOR #I1 1 TO 6
      IF #ADDR-MSG-CN-DIGIT (2,#I1) = ' '
        ESCAPE BOTTOM
      ELSE
        MOVE #ADDR-MSG-CN-DIGIT (2,#I1) TO #OWNER-ID-NUMBER-DIGIT (#I1)
      END-IF
    END-FOR
*
    FOR #I1 #I1 TO 6
      COMPRESS '0' #OWNER-ID-NUMBER INTO #OWNER-ID-NUMBER LEAVING NO
    END-FOR
*
    MOVE EDITED #OWNER-ID-NUMBER-N (EM=999999) TO #FIND-ID-NUMBER
  ELSE
    MOVE EDITED #ID-NUM (EM=999999) TO #FIND-ID-NUMBER
  END-IF
*
  FIND ST-V WITH MEMBER-KEY = #FIND-ID-NUMBER
    FOR #I1 1 TO 9
      IF STATUS (#I1)  = 'E'
        ADD +1 TO #RSGN-CNT
        COMPRESS ISSUE-DATE-MM (#I1) ISSUE-DATE-DD (#I1)
          ISSUE-DATE-YYYY (#I1) INTO #SCR-ISSUE-DATE (#RSGN-CNT)
          WITH '/'
        CALLNAT 'GPLNMSXX' #SCR-POL-NUM (#RSGN-CNT) ID-NUMBER #I1 /* PM7
        MOVE 'X' TO #SCR-SELECT (#RSGN-CNT)
        MOVE (AD=NU) TO #SCR-SEL-CV (#RSGN-CNT)
        MOVE #RSGN-CNT   TO #LAST-E                               /* PM9
      END-IF
    END-FOR
  END-FIND
*
  IF #RSGN-CNT > 1                                                /* PM1
    MOVE TRUE TO #MULT-RSGN
    INPUT MARK *#SCR-SELECT (1) USING MAP 'G3517MAD'
    RESET #SCR-SEL-CNT                                           /* PM10
    FOR #I1 1 TO 9
      IF #SCR-SELECT (#I1) NE ' '
        ADD +1 TO #SCR-SEL-CNT
        MOVE #I1 TO #LAST-E                                       /* PM9
      END-IF
    END-FOR
*
    IF #SCR-SEL-CNT GT 2                                          /* PM7
      REINPUT WITH TEXT 'Maximum of 2 policies can be selected'   /* PM7
        MARK *#SCR-SELECT (1)                                       /* PM7
    END-IF                                                        /* PM7
  END-IF
*
  MOVE #HOLD-TERM-PREM-REF-DUE TO #WORK-AMT
  IF #HOLD-TERM-DEBIT-CREDIT GT 0
    ADD  #HOLD-TERM-DEBIT-CREDIT TO #WORK-AMT
  END-IF
ELSE
  MOVE #HOLD-DEBIT-CREDIT-AMT TO #WORK-AMT
END-IF
*
IF #CGA-SCR-SUFFIX = 'REF' OR = 'RFG' OR = 'RFD' OR = 'RFX'
    OR = 'RPL'                                        /* PM8
  ASSIGN *CURS-LINE = 3
  ASSIGN *CURS-COL = 27
*
  IF #WORK-AMT NE 0
    MOVE '           REFUND $' TO #AD-FF-4
    MOVE EDITED #WORK-AMT (EM=ZZZ,ZZZ.99) TO #W-AMT
    COMPRESS #AD-FF-4 #W-AMT INTO #AD-FF-4 LEAVING NO SPACE
  END-IF
END-IF
*
MOVE #EXT-DEAR-FORMATED-NAME TO #AD-FF-13                      /* vt1
*
IF #CGA-SCR-SUFFIX = 'REF' OR = 'RFG' OR = 'RFD' OR = 'RFF' OR = 'RFX'
    OR = 'RPL'                                        /* PM8
  IF #CGA-SCR-SUFFIX = 'RFD'
    COMPRESS 'The Finance Center has stopped your'
      '$          allotment with'     INTO #AD-FF-15
    COMPRESS 'last payment received for'
      #F-MON-LIT (#LAST-MON-MM) #LAST-MON-YYYY INTO #AD-FF-16
    COMPRESS #AD-FF-16 '.' INTO #AD-FF-16 LEAVING NO
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'RFG' OR = 'RFF' OR = 'RPL'                /* PM8
    IF #NUMBER-MEMBERSHIPS = 0
        AND NOT #CGA-SCR-SUFFIX = 'RPL'                              /* PM8
      COMPRESS 'While we sincerely regret to lose you as a mem'
        'ber, in accordance'  INTO #AD-FF-14 LEAVING NO
      COMPRESS 'with your request, we have discontinued your'
        'membership and resigned' INTO #AD-FF-15           /* PM7
      MOVE 'your' TO #AD-FF-16                                    /* PM7
*     IF #MULT-RSGN                                               /* PM1
      IF #SCR-SEL-CNT > 1                                         /* PM1
        COMPRESS #AD-FF-16 'policies' INTO #AD-FF-16              /* PM7
        FOR #I1 1 TO #RSGN-CNT                                    /* PM7
          IF #SCR-SELECT (#I1) NE ' '
            COMPRESS #AD-FF-16 #SCR-POL-NUM (#I1) INTO #AD-FF-16
            ADD +1 TO #SCR-LOAD-CNT
            IF #SCR-SEL-CNT GT 1 AND #SCR-LOAD-CNT LT #SCR-SEL-CNT
              IF #SCR-LOAD-CNT + 1 = #SCR-SEL-CNT
                COMPRESS #AD-FF-16 'and' INTO #AD-FF-16
              ELSE
                COMPRESS #AD-FF-16 ',' INTO #AD-FF-16 LEAVING NO
              END-IF
            END-IF
          END-IF
        END-FOR                                                   /* PM7
*
        COMPRESS #AD-FF-16 'issued on' INTO #AD-FF-16             /* PM7
        RESET #SCR-LOAD-CNT                                       /* PM7
        FOR #I1 1 TO #RSGN-CNT
          IF #SCR-SELECT (#I1) NE ' '
            COMPRESS #AD-FF-16 #SCR-ISSUE-DATE (#I1) INTO #AD-FF-16
            ADD +1 TO #SCR-LOAD-CNT
            IF #SCR-SEL-CNT GT 1 AND #SCR-LOAD-CNT LT #SCR-SEL-CNT
              IF #SCR-LOAD-CNT + 1 = #SCR-SEL-CNT
                COMPRESS #AD-FF-16 'and' INTO #AD-FF-16
              ELSE
                COMPRESS #AD-FF-16 ',' INTO #AD-FF-16 LEAVING NO
              END-IF
            END-IF
          END-IF
        END-FOR
        COMPRESS #AD-FF-16 '.' INTO #AD-FF-16 LEAVING NO
      ELSE
        COMPRESS #AD-FF-16 'policy' INTO #AD-FF-16                /* PM7
        IF #LAST-E = 0                                            /* PM7
            OR #MULT-RSGN AND #SCR-SEL-CNT = 0                       /* PM7
          COMPRESS #AD-FF-16 'xxxxxx-1 issued on 00/00/0000'      /* PM7
            INTO #AD-FF-16                                       /* PM7
        ELSE
          COMPRESS #AD-FF-16 #SCR-POL-NUM (#LAST-E) 'issued on'   /* PM7
            #SCR-ISSUE-DATE (#LAST-E) INTO #AD-FF-16             /* PM7
        END-IF                                                    /* PM7
*       COMPRESS  #ISS-DT-MM #ISS-DT-DD #ISS-DT-YY
*            INTO #SCR-ISS-DATE WITH '/'                          /* PM7
*       COMPRESS #AD-FF-16 #SCR-ISS-DATE INTO #AD-FF-16           /* PM7
        COMPRESS #AD-FF-16 'only.' INTO #AD-FF-16                 /* PM7
      END-IF
*
    ELSE
      COMPRESS 'In accordance with your request, we have resigned'/* PM7
        'your' INTO #AD-FF-14                             /* PM7
*     IF #MULT-RSGN                                               /* PM1
      IF #SCR-SEL-CNT > 1                                         /* PM1
        COMPRESS #AD-FF-14 'policies' INTO #AD-FF-14              /* PM7
        FOR #I1 1 TO #RSGN-CNT                                    /* PM7
          IF #SCR-SELECT (#I1) NE ' '
            COMPRESS #AD-FF-15 #SCR-POL-NUM (#I1) INTO #AD-FF-15  /* PM7
            ADD +1 TO #SCR-LOAD-CNT
            IF #SCR-SEL-CNT GT 1 AND #SCR-LOAD-CNT LT #SCR-SEL-CNT
              IF #SCR-LOAD-CNT + 1 = #SCR-SEL-CNT
                COMPRESS #AD-FF-15 'and' INTO #AD-FF-15
              ELSE
                COMPRESS #AD-FF-15 ',' INTO #AD-FF-15 LEAVING NO
              END-IF
            END-IF
          END-IF
        END-FOR                                                   /* PM7
*
        COMPRESS #AD-FF-15 'issued on' INTO #AD-FF-15             /* PM7
        RESET #SCR-LOAD-CNT                                       /* PM7
*
        FOR #I1 1 TO #RSGN-CNT
          IF #SCR-SELECT (#I1) NE ' '
            COMPRESS #AD-FF-15 #SCR-ISSUE-DATE (#I1) INTO #AD-FF-15
            ADD +1 TO #SCR-LOAD-CNT
            IF #SCR-SEL-CNT GT 1 AND #SCR-LOAD-CNT LT #SCR-SEL-CNT
              IF #SCR-LOAD-CNT + 1 = #SCR-SEL-CNT
                COMPRESS #AD-FF-15 'and' INTO #AD-FF-15
              ELSE
                COMPRESS #AD-FF-15 ',' INTO #AD-FF-15 LEAVING NO
              END-IF
            END-IF
          END-IF
        END-FOR
      ELSE
        COMPRESS #AD-FF-14 'policy' INTO #AD-FF-14                /* PM7
        IF #LAST-E = 0                                            /* PM7
            OR #MULT-RSGN AND #SCR-SEL-CNT = 0                       /* PM7
          COMPRESS #AD-FF-15 'xxxxxx-1 issued on 00/00/0000'      /* PM7
            INTO #AD-FF-15                                       /* PM7
        ELSE
          COMPRESS #AD-FF-15 #SCR-POL-NUM (#LAST-E) 'issued on'   /* PM7
            #SCR-ISSUE-DATE (#LAST-E) INTO #AD-FF-15             /* PM7
        END-IF                                                    /* PM7
*       COMPRESS #AD-FF-15 #ISS-DT-MM INTO #AD-FF-15
*       COMPRESS #AD-FF-15  #ISS-DT-DD #ISS-DT-YY
*           INTO #AD-FF-15 WITH '/'
      END-IF
      COMPRESS #AD-FF-15 'only.' INTO #AD-FF-15
    END-IF
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'REF' OR = 'RFX'
    IF *DEVICE = 'BATCH'
      MOVE 'KHB' TO #CGA-CLERK
    END-IF
* below is only used after term ref db/cr adjust & until chks are sent
    RESET #TRM-REFUND-NOTE #TRM-REFUND-AMT
*   FIND (1) NT-V WITH ID-NUMBER = #ID-NUM
*     FOR #I1 1 TO 60
*       IF ((NOTE-TEXT (#I1) = MASK ('Outstanding Trm Rfnd'))
*       OR  (NOTE-TEXT (#I1) = MASK ('OUTSTANDING TRM RFND')))
*        AND (NOTE-DATE (#I1) = 20030529)    /* specific note
*         MOVE TRUE TO #TRM-REFUND-NOTE
*         MOVE NOTE-TEXT-AMT (#I1) TO #TRM-REFUND-AMT
*         MOVE NT-AMT-DOLS  (#I1) TO #AMT-DOLS
*         MOVE NT-AMT-CENTS (#I1) TO #AMT-CENTS
*
*         FOR #I2 1 TO 3
*           IF #AMT-DOLS-X (#I2) = ' '
*             MOVE '0' TO #AMT-DOLS-X (#I2)
*           END-IF
*         END-FOR
*         MULTIPLY #AMT BY -1
*         ESCAPE BOTTOM
*       END-IF
*     END-FOR
*   END-FIND
*
    COMPRESS 'Enclosed please find our check refunding the amount'
      'due you.'  INTO #AD-FF-15
*
    IF #TRM-REFUND-NOTE
      IF #AMT = #HOLD-DEBIT-CREDIT-AMT
        COMPRESS #AD-FF-15 'This amount' INTO #AD-FF-15
        MOVE 'is' TO #AD-FF-16
      ELSE
        IF #AMT GT #HOLD-DEBIT-CREDIT-AMT
          COMPRESS #AD-FF-15 'Included in' INTO #AD-FF-15
          MOVE 'this amount is' TO #AD-FF-16
        ELSE
          COMPRESS #AD-FF-15 'This refund' INTO #AD-FF-15
          MOVE 'is part of' TO #AD-FF-16
        END-IF
      END-IF
*
      COMPRESS #AD-FF-16 'the outstanding term refund for'
        NT-YEAR (#I1) #TRM-REFUND-AMT NT-CHK-# (#I1) INTO #AD-FF-16
      COMPRESS #AD-FF-16 '.' INTO #AD-FF-16 LEAVING NO
    END-IF
*
    COMPRESS 'Be advised that the Association does "NOT" pay interest'
      'on credit accounts' INTO #AD-FF-17
*
    IF #CGA-SCR-SUFFIX = 'RFX'
      COMPRESS #AD-FF-17 '.' INTO #AD-FF-17 LEAVING NO
      COMPRESS 'It is a pleasure to serve you. Please call us if you'
        'have questions.'   INTO #AD-FF-19
    ELSE
      COMPRESS #AD-FF-17 ';' INTO #AD-FF-17 LEAVING NO
      COMPRESS 'it is therefore to your benefit to adjust your'
        'allotment as soon as'   INTO #AD-FF-18
      MOVE     'possible.' TO #AD-FF-19
*
      IF NOT ALOT-CHG-AUTH = 'Y' OR = 'S'    /* DW11
        COMPRESS 'Enclosed you will find a form to help you adjust'
          'your allotment.'         INTO #AD-FF-21
      END-IF
*
      COMPRESS 'It is a pleasure to serve you. Please call us if you'
        'have questions.'   INTO #AD-FF-22
    END-IF
    MOVE 23 TO #I2
    PERFORM CORR-MSG-FORMAT
    ESCAPE ROUTINE
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'RFF'
    MOVE 'RFG' TO #CGA-SCR-SUFFIX
    MOVE 23 TO #I2
    PERFORM CORR-MSG-FORMAT
    ESCAPE ROUTINE
  END-IF
*
  IF #WORK-AMT NE 0
    COMPRESS 'Enclosed please find our check refunding the'
      INTO #AD-FF-17
*
    IF #CGA-SCR-SUFFIX = 'RFG' OR = 'RPL'                         /* PM8
      IF #ISSUE-DATE LT 19990101
          AND #CGA-SCR-SUFFIX = 'RFG'                                /* PM8
        COMPRESS #AD-FF-17 'amount due you.' INTO #AD-FF-17
        COMPRESS  'This amount represents premiums paid in under'
          'your money back'                        TO #AD-FF-18
        MOVE      'guarantee application.' TO #AD-FF-19
      ELSE
        COMPRESS #AD-FF-17 'amount due you.' INTO #AD-FF-17
      END-IF
*
      IF #HOLD-ALOT-AMT NE 0
          AND #CGA-SCR-SUFFIX = 'RFG'                                /* PM8
*
        IF NOT ALOT-CHG-AUTH = 'Y' OR = 'S'    /* DW11
          COMPRESS  'Also enclosed is a form to assist you in'
            'stopping your allotment.'    INTO #AD-FF-20
        END-IF
*
        COMPRESS  'When we are advised by the Finance Center of'
          'the stop, we will'           INTO #AD-FF-21
        MOVE      'refund any premiums overpaid.' TO #AD-FF-22
      END-IF
    ELSE
      COMPRESS #AD-FF-17 'amount due you.' INTO #AD-FF-17
      COMPRESS  'Also enclosed please find a form to help you'
        'adjust your allotment'                  TO #AD-FF-18
      COMPRESS  'appropriately. Be advised that the Association'
        'does *not* pay interest'                TO #AD-FF-19
      COMPRESS  'on credits on accounts; it is therefore to your'
        'benefit to get your'                  TO #AD-FF-20
      COMPRESS  'allotment adjusted as soon as possible.'
        TO #AD-FF-21
    END-IF
  END-IF
*
  IF #CGA-SCR-SUFFIX = 'RPL'                                      /* PM8
    COMPRESS 'Please note, if you resigned this insurance to'     /* PM8
      'replace it with another,' INTO #AD-FF-19            /* PM8
    COMPRESS 'the new policy has been approved and will be'       /* PM8
      'mailed to you under' INTO #AD-FF-20                 /* PM8
    MOVE 'separate cover.' TO #AD-FF-21                           /* PM8
  END-IF                                                          /* PM8
ELSE
  MOVE EDITED #HOLD-ALOT-AMT (EM=Z,ZZZ.99) TO #W-AMT
*
  IF #CGA-SCR-SUFFIX = 'RFS'
    COMPRESS 'The Finance Center has started your $' #W-AMT
      'allotment for credit to your'     INTO #AD-FF-15
  ELSE
    IF #CGA-SCR-SUFFIX = 'RFC'
      COMPRESS 'The Finance Center has changed your'
        'allotment to $' #W-AMT 'for credit to'   INTO #AD-FF-15
    ELSE
      IF #CGA-SCR-SUFFIX = 'RFZ'
        MOVE EDITED #PASSED-AMT (EM=ZZ9.99) TO #DOLLARS-DISP
        COMPRESS 'Your first $' #DOLLARS-DISP 'EZ-pay plan'
          'payment was received for credit to your' TO #AD-FF-15
      END-IF
    END-IF
  END-IF
*
  MOVE EDITED #WORK-AMT (EM=ZZZ,ZZZ.99) TO #W-AMT
  IF #ID-NUM GE 600000
    COMPRESS #F-MON-LIT (#CURR-MM) 'fees.' INTO #AD-FF-16
  ELSE
    COMPRESS #F-MON-LIT(#CURR-MM) 'premiums and/or dues.' INTO #AD-FF-16
  END-IF
*
  IF #WORK-AMT GT 0
    COMPRESS #AD-FF-16 'However, they are in arrears. To balance'
      'your'     INTO #AD-FF-16
    COMPUTE #NEXT-MM = #CURR-MM + 1
*
    IF #NEXT-MM GT 12
      MOVE 1 TO #NEXT-MM
    END-IF
*
    COMPRESS 'account, please forward a check for $'
      #W-AMT 'by' #DAY-LIT (#NEXT-MM) #F-MON-LIT (#NEXT-MM)
      INTO #AD-FF-17
    COMPRESS #AD-FF-17 '.' INTO #AD-FF-17 LEAVING NO
    COMPRESS 'A postage paid envelope is enclosed'
      'for your convenience.'                  INTO #AD-FF-18
  ELSE
    COMPRESS #AD-FF-16 'As a result, a credit refund of $'
      #W-AMT                 INTO #AD-FF-16
    COMPRESS 'is due you. To your benefit, we have applied this'
      'credit to your account.'           INTO #AD-FF-17
*
    COMPRESS 'This amount, automatically added to your cash'
      'value, will start'                  INTO #AD-FF-18
*
    COMPRESS 'earning interest at our current crediting rate'
      'of 7.0%.'                                 TO #AD-FF-19
*
    COMPRESS 'If you prefer to receive a check for the'
      'above refund, please let'               INTO #AD-FF-20
    COMPRESS 'us know. A postage paid envelope is enclosed'
      'for your convenience.'                  INTO #AD-FF-21
  END-IF
END-IF
*
MOVE 23 TO #I2
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE /* (0706)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BEN-PROCESS     /* DW2
* * * * * * * * * * * * * * *
*
MOVE 'RE: Beneficiary Designation' TO #AD-FF-14
MOVE #EXT-DEAR-FORMATED-NAME TO #AD-FF-16                                  /* vt1
*
IF #CGA-SCR-SUFFIX = 'BLB'
  COMPRESS 'We have enclosed a blank beneficiary form that requires'
    'your attention.'    INTO #AD-FF-18
  COMPRESS 'Please complete, sign, date and return to us for'
    'endorsement.'          INTO #AD-FF-19
END-IF
*
IF #CGA-SCR-SUFFIX = 'CLR'
  COMPRESS 'In order to prevent any misunderstanding, we are'
    'sending you a revised'      INTO #AD-FF-18
  COMPRESS 'beneficiary form. If the form meets with your'
    'approval, please sign' INTO #AD-FF-19
  COMPRESS 'it; otherwise complete the required information'
    'and return it back to us.' INTO #AD-FF-20
END-IF
*
IF #CGA-SCR-SUFFIX = 'NEW'
  COMPRESS 'In order to prevent any misunderstanding, we are'
    'sending you a prepared'      INTO #AD-FF-18
  COMPRESS 'beneficiary form. If the form meets with your'
    'approval, please sign' INTO #AD-FF-19
  COMPRESS 'it; otherwise complete the required information'
    'and return it back to us.' INTO #AD-FF-20
END-IF
*
IF #CGA-SCR-SUFFIX = 'COB'
  COMPRESS 'Enclosed you will find the acknowledgement of your'
    'beneficiary designation'    INTO #AD-FF-18
  COMPRESS 'which should be attached to your Certificate(s)'
    'of your Membership Policy.' INTO #AD-FF-19
END-IF
*
COMPRESS 'As always, if you have any questions, please do not'
  'hesitate to call us.'   INTO #AD-FF-21
*
MOVE 23 TO #I2
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE /* (0706)
*
*********************************
DEFINE SUBROUTINE CORR-MSG-FORMAT
*********************************
*
MOVE #CGA-CLERK TO #CLERK-ABB-PASS
MOVE ' ' TO #CLERK-ABB-NAME-PASS
CALLNAT 'GCKNMSXX' #CLERK-ABB-PASS
  #INVALID
  #CLERK-ABB-NAME-PASS
*
IF *DEVICE = 'BATCH'
    AND (#I2 LT 12)
  ADD 2 TO #I2
ELSE
  MOVE 13 TO #I2
END-IF
*
COMPRESS #CORR-MSG #CLERK-ABB-NAME-PASS INTO #AD-TABLE (#I2)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE RET-PROCESS
* * * * * * * * * * * * * * *
*
IF (#MIL-STATUS NE 'A')
  MOVE '"RET/TML" SERIES CAN ONLY BE USED ON ACTIVE DUTY MEMBERS'
    TO #ERROR-MESSAGE
  RESET #CGA-SCR-SUFFIX
END-IF
*
MOVE '    DOCUMENTS NEEDED AFTER RETIREMENT:'      TO #AD-FF-15
MOVE '        1. ORIGINAL DD FORM 214 (COPY #4)'   TO #AD-FF-16
MOVE '        2. COPY RETIREMENT ORDER'            TO #AD-FF-17
MOVE '        3. COPY LAST ACTIVE DUTY LES'        TO #AD-FF-18
MOVE '        4. COPY FIRST RETIRED PAY STATEMENT' TO #AD-FF-19
MOVE '        5. COPY VA DISABILITY AWARD LETTER'  TO #AD-FF-20
MOVE '        6. CONVERSION SGLI TO VGLI  YES __  NO __' TO #AD-FF-21
COMPRESS   ' FOR ITEMS 2 THRU 5 PROVIDE COPIES THAT MAY BE DES'
  'TROYED AFTER MICROFILMING' INTO #AD-FF-22 LEAVING NO
END-SUBROUTINE /* (1178)
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE RIQ-SIQ-PROCESS
* * * * * * * * * * * * * * * * *
*
IF (#MIL-STATUS NE 'A')
  MOVE '"RIQ/SIQ" SERIES CAN ONLY BE USED ON ACTIVE DUTY MEMBERS'
    TO #ERROR-MESSAGE
  RESET #CGA-SCR-SUFFIX
END-IF
*
END-SUBROUTINE /* (1199)
*
*****************************
DEFINE SUBROUTINE RC-PROCESS
*****************************
*
MOVE '        DEAR MEMBER:' TO #AD-FF-12
MOVE '        WE ARE RETURNING YOUR CHECK FOR THE REASON INDICATED:'
  TO #AD-FF-14
MOVE '        _________SIGNATURE'                        TO #AD-FF-16
MOVE '        _________WRITTEN AND FIGURE AMOUNTS DO NOT AGREE'
  TO #AD-FF-17
MOVE '        _________IMPROPER PAYEE (PROPER PAYEE AAFMAA)'
  TO #AD-FF-18
MOVE '        _________INCORRECT DATE'                   TO #AD-FF-19
MOVE '        _________OTHER'                            TO #AD-FF-20
*
MOVE 20 TO #I2
PERFORM CORR-MSG-FORMAT
END-SUBROUTINE /* (1211)
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE APL-PROCESS
* * * * * * * * * * * * * * *
*
IF (#PT-MM NE 0) AND (#PT-MM LT 13)
    AND (#MODE = 'A' OR = 'S' OR = 'Q' OR = 'M'
    OR = '0' OR = '1' OR = '2' OR = '4')  /* DW1
  COMPRESS #PT-MM-A #DAY-LIT (#PT-MM) #PT-YY
    INTO #DT-TO-MM/DD/YY WITH '/'
  IF #MODE = 'A' OR = '1'   /* DW1
    MOVE '    Annual' TO #MODE-DESC
    SUBTRACT 11 FROM #PT-MM
  ELSE
    IF #MODE = 'S' OR = '2' /* DW1
      MOVE 'Semiannual' TO #MODE-DESC
      SUBTRACT 5 FROM #PT-MM
    ELSE
      IF #MODE = 'Q' OR = '$' /* DW1
        MOVE 'Quarterly' TO #MODE-DESC
        SUBTRACT 2 FROM #PT-MM
      ELSE
        MOVE ' Monthly' TO #MODE-DESC
        SUBTRACT 0 FROM #PT-MM
      END-IF
    END-IF
  END-IF
*
  IF #PT-MM LT 1
    ADD 12 TO #PT-MM
    SUBTRACT 1 FROM #PT-YY
  END-IF
*
  COMPRESS #PT-MM-A '01' #PT-YY INTO #DT-FROM-MM/DD/YY WITH '/'
END-IF
*
IF (#LOAN-AMT NE 0) AND (#LOAN-AMT-2 NE 0)
  MOVE ' These' TO #THESE
  MOVE ' loans' TO #LOANS
  MOVE ' are'   TO #ARE
  MOVE ' they'  TO #THEY
  MOVE ' were'  TO #WERE
  MOVE ' remain' TO #REMAINS
  RESET           #AN
  MOVE TRUE TO #BOTH
ELSE
  IF #LOAN-AMT NE 0
    MOVE TRUE TO #FIXED
  ELSE
    MOVE TRUE TO #VARIABLE
  END-IF
END-IF
*
MOVE #EXT-DEAR-FORMATED-NAME TO #AD-FF-12                       /* vt1
*
COMPRESS ' Thank you for your check which we applied towards '
  'your premiums and/or dues'        INTO #AD-FF-14
COMPRESS 'for the period' #DT-FROM-MM/DD/YY INTO #AD-FF-15
COMPRESS #AD-FF-15 '-' #DT-TO-MM/DD/YY INTO #AD-FF-15 LEAVING NO
COMPRESS #AD-FF-15 '. Just as a reminder, there' #REMAINS #AN
  ' out-'      INTO #AD-FF-15 LEAVING NO
COMPRESS 'standing automatic premium' #LOANS ' on your account. Th'
  'e' #LOANS #WERE ' issued when' INTO #AD-FF-16 LEAVING NO
COMPRESS 'your premiums and/or dues were 60 days in arrears f'
  'rom the date they were due.'     INTO #AD-FF-17 LEAVING NO
COMPRESS 'This action was taken to insure that your membersh'
  'ip in the Association would'     INTO #AD-FF-18 LEAVING NO
COMPRESS 'not lapse.' #THESE #LOANS #ARE ' currently accruing interest'
  INTO #AD-FF-19 LEAVING NO
IF #BOTH
  COMPRESS #AD-FF-19 '.' INTO #AD-FF-19 LEAVING NO
ELSE
  COMPRESS #AD-FF-19 ' at' INTO #AD-FF-19 LEAVING NO
  IF #VARIABLE
    COMPRESS #AD-FF-19 'a 8.10% variable rate.' INTO #AD-FF-19
  ELSE
    COMPRESS #AD-FF-19 '12%.'    INTO #AD-FF-19
  END-IF
END-IF
COMPRESS ' If you wish to pay off the' #LOANS ', please contact '
  ' our Financial Accounts'    INTO #AD-FF-20 LEAVING NO
COMPRESS 'Department for the amount owed. If you do not'
  ' desire to pay off the' #LOANS ',' #THEY INTO #AD-FF-21 LEAVING NO
COMPRESS 'will remain as an outstanding indebtedness against'
  'your policy and you will'    INTO #AD-FF-22
MOVE 'receive an interest bill annually.' TO #AD-FF-23
*
MOVE 23 TO #I2
PERFORM CORR-MSG-FORMAT
*
END-SUBROUTINE /* (1222)
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DISP-SPC-SCN
* * * * * * * * * * * * * * * *
*
IF   (#CGA-ORIG-SUFFIX = 'RET')
    AND (*DEVICE = 'BATCH')
  MOVE #CGA-MF-SCR-PARM-1-N TO #YYYYMMDD
  MOVE #MM TO #I1
  MOVE #MON-LIT (#I1)  TO #SCR-AS-OF-MON
  MOVE EDITED #DD (EM=Z9) TO #SCR-AS-OF-DAY
  MOVE #YYYY TO #SCR-AS-OF-YEAR
  MOVE #SCR-AS-OF-LINE TO #SCR-ASS-RET-DT
END-IF
*
MOVE *DATN TO #YYYYMMDD
MOVE #MM TO #I1
MOVE #MON-LIT (#I1)  TO #SCR-AS-OF-MON
MOVE EDITED #DD (EM=Z9) TO #SCR-AS-OF-DAY
MOVE #YYYY TO #SCR-AS-OF-YEAR
*
IF #MIL-SERVICE = MASK ('A')
  MOVE ', USA' TO #SCR-MIL-ID
ELSE
  IF #MIL-SERVICE = MASK ('F')
    MOVE ', USAF' TO #SCR-MIL-ID
  ELSE
    IF #MIL-SERVICE = MASK ('M')
      MOVE ', USMC' TO #SCR-MIL-ID
    ELSE
      IF #MIL-SERVICE = MASK ('N')
        MOVE ', USN' TO #SCR-MIL-ID
      ELSE
        IF #MIL-SERVICE = MASK ('CG')
          MOVE ', USCG' TO #SCR-MIL-ID
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF #EXT-FORMATED-NAME                                                            /* vt1 was #formated-name \/
    = MASK (...............'                              ')
  MOVE #EXT-FORMATED-NAME TO #FN
  MOVE #FN            TO #FN-LST-30
  RESET #FN-1ST-15
ELSE
  IF #EXT-FORMATED-NAME
      = MASK (....................'                         ')
    MOVE #EXT-FORMATED-NAME TO #FN
    MOVE #FN            TO #FN-LST-33
    RESET #FN-1ST-12
  ELSE
    IF #EXT-FORMATED-NAME
        = MASK (.........................'                    ')
      MOVE #EXT-FORMATED-NAME TO #FN
      MOVE #FN            TO #FN-LST-35
      RESET #FN-1ST-10
    ELSE
      IF #EXT-FORMATED-NAME
          = MASK (..............................'               ')
        MOVE #EXT-FORMATED-NAME TO #FN
        MOVE #FN            TO #FN-LST-38
        RESET #FN-1ST-7
      ELSE
        IF #EXT-FORMATED-NAME
            = MASK (...................................'          ')
          MOVE #EXT-FORMATED-NAME TO #FN
          MOVE #FN            TO #FN-LST-40
          RESET #FN-1ST-5
        ELSE
          IF #EXT-FORMATED-NAME
              = MASK (........................................'     ')
            MOVE #EXT-FORMATED-NAME TO #FN
            MOVE #FN            TO #FN-LST-43
            RESET #FN-1ST-2
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
COMPRESS #EXT-FORMATED-NAME #SCR-MIL-ID INTO #SCR-NAME-RET LEAVING NO                  /* vt1 /\
*
IF *DEVICE = 'BATCH'
  IF #CGA-CLERK = 'AKW' OR = 'DRW' OR = 'JAK'
    RESET #AD-FF-24
  ELSE
    MOVE 22 TO #I2
    PERFORM CORR-MSG-FORMAT
  END-IF
*
  MOVE '*' TO #CGA-SCR-COMMAND
  EJECT
  IF #CGA-CLERK = 'RT*'  /* 'rt*' is 2nd ret assum dt request
    MOVE '*' TO #CGA-SCR-COMMAND
    NEWPAGE (1)                               /* DW13
    WRITE (1) NOTITLE USING FORM 'G3554FAD'       /* DW13
  ELSE
    NEWPAGE (1)                               /* DW13
    WRITE (1) NOTITLE USING FORM 'G3553FAD'       /* DW13
  END-IF
*
  MOVE '*' TO #CGA-SCR-COMMAND
  ADD 1 TO #CGA-SECURITY-CODE
ELSE
  IF #CGA-ORIG-SUFFIX = 'RAN'
    INPUT USING MAP 'G3555MAD'
  ELSE
    IF #CGA-ORIG-SUFFIX = 'TML' OR = 'AW2' OR = 'AWS'    /* DW12
      IF #CGA-ORIG-SUFFIX NE 'AWS' /* No scn if doing AW2 Summary/* DW12
        INPUT USING MAP 'G3550MAD'
      END-IF
    ELSE
      MOVE 22 TO #I2
      PERFORM CORR-MSG-FORMAT
      IF #CGA-ORIG-SUFFIX = 'RET' OR = 'R'
        INPUT TEXT #ERROR-MESSAGE USING MAP 'G3551MAD'
      ELSE
        INPUT TEXT #ERROR-MESSAGE USING MAP 'G3552MAD'
      END-IF
    END-IF
  END-IF
END-IF
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
END-SUBROUTINE
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE AL-PROCESSING    /* PM6
* * * * * * * * * * * * * * * * *
*
MOVE (AD=P) TO #POL-CV (*)
FIND ST-V WITH MEMBER-KEY = #ID-NUM-A
  FOR #I1 1 TO 9
    IF  IS-ANNUITY-LIFE(<ST-V.PLAN(#I1)>)      /* TT1
      ADD +1 TO #I3
      COMPRESS ISSUE-DATE-YYYY (#I1) ISSUE-DATE-MM (#I1)
        ISSUE-DATE-DD (#I1) INTO #AL-ISS-DT-A (#I3) LEAVING NO
      COMPRESS ISSUE-DATE-MM (#I1) ISSUE-DATE-DD (#I1)
        ISSUE-DATE-YYYY (#I1) INTO #POL-ISS-DATE (#I3) WITH '/'
      MOVE ST-V.PLAN (#I1) TO #POL-PLAN (#I3)
      MOVE EDITED ST-V.FACE-AMOUNT (#I1) (EM=Z,ZZZ,ZZ9.99)
        TO #POL-FACE (#I3)
      COMPRESS '$' #POL-FACE (#I3) INTO #POL-FACE (#I3) LEAVING NO
      CALLNAT 'GPLNMSXX'
        #POL-NUMBER (#I3)
        ST-V.ID-NUMBER
        #I1
      MOVE #I1 TO #AL-X-9 (#I3)
      MOVE (AD=U) TO  #POL-CV (#I3)
    END-IF
  END-FOR
*
  IF #I3 = 0
    MOVE 'AL policy not found' TO #ERROR-MESSAGE
    ESCAPE ROUTINE
  ELSE
    MOVE '*' TO #CGA-SCR-COMMAND
    INPUT MARK *#SELECT-POL (1) USING MAP 'G3518MAD'
    IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
    END-IF
*
    FOR #I3 1 TO 9
      IF #SELECT-POL (#I3) = ' '
        ESCAPE TOP
      END-IF
*
      MOVE #AL-X-9 (#I3) TO #AL-X
      RESET #AL-NT-ON-DT
      FIND (1) NT-V WITH ID-NUMBER = #ID-NUM
        RESET #ANNUITIZED
        FOR #NOT-X 1 TO 60
          IF   (NT-AL-EF-POL-ISS  (#NOT-X) = '* POL ISS')
              AND (NT-AL-EF-ANNUITIZ (#NOT-X) = 'ANNUITIZED ON')
            COMPRESS NT-AL-EF-YYYY(#NOT-X) NT-AL-EF-MM(#NOT-X)    /* bz1
              NT-AL-EF-DD(#NOT-X) INTO #YYYYMMDD-A LEAVING NO
            MOVE #YYYYMMDD TO #AL-NT-ISS-DT
            COMPRESS NT-AL-ON-YYYY(#NOT-X) NT-AL-ON-MM(#NOT-X)
              NT-AL-ON-DD(#NOT-X) INTO #YYYYMMDD-A LEAVING NO
            MOVE #YYYYMMDD TO #AL-NT-ON-DT
            IF (#AL-ISS-DT (#I3) = #AL-NT-ISS-DT)   /* Matches Policy
                AND (ST-V.STATUS (#AL-X) = 'P')  /* Already Annuitized
              MOVE TRUE TO #ANNUITIZED
              ESCAPE BOTTOM
            END-IF
          END-IF
        END-FOR
      END-FIND
*
      IF #ANNUITIZED                    /* Already Annuitized
        MOVE #AL-NT-ON-DT TO #AS-OF-DT  /* Get day of CI for CSV
        MOVE 'AL' TO #SUFFIX            /* For GVALUSXX
      ELSE
        MOVE *DATN TO #AS-OF-DT
        MOVE 'CSV' TO #SUFFIX           /* For GVALUSXX
      END-IF
*
      MOVE ST-V.FACE-AMOUNT (*) TO #FACE-AMT (*)
      CALLNAT 'CVALUSXX'
        ST-V.ID-NUMBER
        #SUFFIX
        #AS-OF-DT
        #LOAN-BAL   (1:9)
        #LOAN-BAL-2 (1:9)
        #ACC-INT    (1:9)
        #ACC-INT-2  (1:9)
        #CSV        (1:9)
        #CVALUE     (1:9)
        #LVALUE     (1:9)
        #MCSV       (1:9)
        #DB         (1:9)
        #ANN-PREMIUM (1:9)
        #FACE-AMT   (1:9)
        #TPP        (1:9)
*
*
      CALLNAT 'CONTS001'
        ST-V.MEMBER-CONTACT-ID
        ST-V.ID-NUMBER
        OBSOLETE-ST-V.SEX
        OBSOLETE-ST-V.DATE-OF-BIRTH
        OBSOLETE-ST-V.DATE-OF-BIRTH-SP
        OBSOLETE-ST-V.SEX-SP
        OBSOLETE-ST-V.FORMER-SPOUSE-DT-OF-BIRTH
*
      MOVE OBSOLETE-ST-V.SEX TO #BOP-SEX
      MOVE #AS-OF-DT TO #YYYYMMDD
      SUBTRACT OBSOLETE-ST-V.DATE-OF-BIRTH FROM #YYYYMMDD
      MOVE #YYYY TO #BOP-BENE-AGE
      MOVE #CSV (#AL-X) TO #BOP-DB-AN
*
      CALLNAT 'GANNUSXX'    /* DW5
        #CGA-SCR-COMMAND
        #CGA-SCR-ID
        #CGA-SCR-SUFFIX
        #CGA-CLERK
*
        #BOP-DB-AN
        #BOP-SEX
        #BOP-BENE-AGE
        #BOP-BENE-A
        #BOP-BENE-B
        #BOP-BENE-C
        #BOP-RESV-AMT-A
        #BOP-RESV-AMT-B
        #BOP-TAXBL-PCT-A
        #BOP-TAXBL-PCT-B
        #BOP-TAXBL-AMT-A
        #BOP-TAXBL-AMT-B
        #BOP-TAXBL-MON-A
        #BOP-TAXBL-MON-B
*
      COMPRESS 'In accordance with your application, we have annuitized'
        'your ANNUITYLIFE'     INTO #AD-FF-15
      MOVE #AL-ISS-DT (#I3) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #ISS-DT-DISP WITH '/'
      COMPRESS 'policy issued on' #ISS-DT-DISP INTO #AD-FF-16
      MOVE EDITED #BOP-BENE-A (EM=ZZZ,ZZZ.99) TO #AMT-12
      MOVE LEFT #AMT-12 TO #AMT-12
      COMPRESS '$' #AMT-12 INTO #AMT-12 LEAVING NO
      COMPRESS 'Beginning immediately you will receive a monthly'
        'check of' #AMT-12 'of which'     INTO #AD-FF-18
      MOVE EDITED #BOP-TAXBL-MON-A (EM=ZZZ,ZZZ.99) TO #AMT-12
      MOVE LEFT #AMT-12 TO #AMT-12
      COMPRESS '$' #AMT-12 INTO #AMT-12 LEAVING NO
      COMPRESS #AMT-12 'is taxable.' INTO #AD-FF-19
      COMPRESS 'Feel free to contact AAFMAA should you have questions'
        'or we could be of'    INTO #AD-FF-21
      COMPRESS 'further assistance. As always it is a pleasure to'
        'serve you.'     INTO #AD-FF-22
*
      MOVE 22 TO #I2
      PERFORM CORR-MSG-FORMAT
*
      INPUT WITH TEXT #ERROR-MESSAGE USING MAP 'G3500MAD'
*
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
    END-FOR
  END-IF
END-FIND
*
MOVE 'AD' TO #CGA-SCR-COMMAND
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
END
