* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVYE2 - VALUE-ADDED YEAR-END PROCESSING
*                        SUMMARIZE INTEREST PROJECTIONS
*
DEFINE DATA
LOCAL
*
1 #JCL-PARM          (A29)
1 REDEFINE #JCL-PARM
 2 #JCL-FIL-1        (A12) /* 'TODAYS DATE='  (ASSUMES USING CURR FILE)
 2 #JCL-CURR-YYYY    (N4)
 2 #JCL-CURR-MMDD    (N4)
 2 REDEFINE #JCL-CURR-MMDD
  3 #JCL-CURR-MM     (N2)
  3 #JCL-CURR-DD     (N2)
 2 #JCL-SELECT       (A9)
*
1 #PROJ-RECORD       (A80)
1 #HOLD-EXTRACT-REC-1  (A205)
1 #HOLD-EXTRACT-REC-2  (A205)
*
1 #EXTR-RECORD
 2 #RECORD-1             (A67)
 2 REDEFINE #RECORD-1
  3 #ID-NUMBER-1          (N6)
  3 REDEFINE #ID-NUMBER-1
   4 #ID-NUMBER-1-A       (A6)
  3 #LOAN-IND             (A1)
  3 #PLAN                 (A3)
  3 REDEFINE #PLAN
   4 #PLAN-2              (A2)
   4 #PLAN-FILL           (A1)
  3 #F1-02                (A1)
  3 #ISS-DATE             (N8)
  3 REDEFINE #ISS-DATE
   4 #ISS-DATE-YYYY       (N4)
   4 #ISS-DATE-MM         (N2)
   4 #ISS-DATE-DD         (N2)
  3 #F1-03                (A1)
  3 #ISS-AGE              (A2)
  3 REDEFINE #ISS-AGE
   4 #ISS-AGE-N           (N2)
  3 #F1-04                (A1)
  3 #UNITS                (N1)
  3 #F1-05                (A1)
  3 #LAST-DURATION        (N2)
  3 #F1-06                (A1)
  3 #MONTH-IND            (N2)
  3 #F1-07                (A1)
  3 #CONV-DUR             (N2)
  3 #F1-08                (A1)
  3 #PAY-STATUS           (A2)
  3 #F1-09                (A1)
  3 #ANN-PREM             (N5.2)
  3 #F1-10                (A1)
  3 #MODE                 (A1)
  3 #F1-11                (A1)
  3 #SMOKER-FLAG          (A1)
  3 #F1-12                (A1)
  3 #DOB                  (N8)
  3 #F1-13                (A1)
  3 #ACV                  (N7.2)
*
 2 #RECORD-2             (A66)
 2 REDEFINE #RECORD-2
  3 #ID-NUMBER-2          (N6)
  3 #F2-01                (A1)
  3 #CV                   (N7.2)
  3 #F2-02                (A1)
  3 #MIN-CV               (N5.2)
  3 #F2-03                (A1)
  3 #NXT-YR-MIN-CV        (N5.2)
  3 #F2-04                (A1)
  3 #DB                   (N7.2)
  3 #F2-05                (A1)
  3 #LAST-ANN-CV          (N7.2)
  3 #F2-06                (A1)
  3 #FACE-AMT             (N5.2)
  3 #F2-07                (A1)
  3 #GROWTH-RATE          (N0.5)
*
 2 #RECORD-3             (A74)
 2 REDEFINE #RECORD-3
  3 #ID-NUMBER-3          (N6)
  3 #F3-01                (A1)
  3 #NAME                 (A15)
  3 #F3-02                (A1)
  3 #MONTHLY-ALLOC-CRED   (N3.2)
  3 #F3-03                (A1)
  3 #MONTHLY-PREM-DEFIC   (N3.2)
  3 #F3-04                (A1)
  3 #NET-PREM-RATIO       (N1.6)
  3 #F3-05                (A1)
  3 #LOAN-AMT             (N5.2)
  3 #F3-06                (A1)
  3 #LOAN-INT-RATE        (N0.5)
  3 #F3-07                (A1)
  3 #SEX                  (A1)
  3 #F3-08                (A1)
  3 #CELL-COUNT           (N5)
  3 #REAL-ISS-DATE        (N8)
  3 #REAL-ISS-AGE         (A2)
1 REDEFINE #EXTR-RECORD
 2 #EXTR-RECORD-A         (A205)
*
1 #COUNTERS
  2 #RECS-READ        (P7)
  2 #RECS-WRITTEN     (P7)
  2 #CNT              (P7)
  2 #I1               (I2)
  2 #I2               (I2)
*
 1 #LAST-LOAN-IND     (A1)
 1 #LAST-PLAN         (A3)
 1 #LAST-ISS-DATE     (N8)
 1 #LAST-ISS-AGE      (A3)
 1 #LAST-UNITS        (N3)
*
*    1 - IN FORCE CELL TOTALS
*    2 - IN FORCE SUB TOTALS
*    3 - IN FORCE GRAND TOTALS
*
1 #TOTALS            (3)
 2 #ACC-DB             (P13.2)
 2 #ACC-UNITS          (P7)
 2 #ACC-FAC-AMT        (P11.2)
 2 #ACC-ACV            (P13.2)
 2 #ACC-CV             (P13.2)
 2 #ACC-LST-ANN-CV     (P13.2)
 2 #ACC-ALLOC-CRED     (P9.2)
 2 #ACC-PREM-DEFIC     (P9.2)
 2 #ACC-PLANS          (P7)
 2 #ACC-LOAN-AMT       (P11.2)
 2 #ACC-LOAN-INT-RATE  (P8.5)
 2 #ACC-ANN-PREM       (P9.2)
 2 #ACC-NET-PR-RATIO   (P8.5)
 2 #ACC-NUM-CELLS      (P7)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
               'FORMAT REPORT FOR YEAR-END VALUE-ADDED PROCESSING FOR '
               #JCL-CURR-YYYY
               5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
               'IN-FORCE SUMMARY OF INTEREST PROJECTIONS FOR '
               #JCL-CURR-YYYY
               5X 'DATE: '*DATX 5X 'TIME: ' *TIMX  //
*
READ WORK 1 RECORD #EXTR-RECORD
  ADD +1 TO #RECS-READ
*
  IF   #JCL-SELECT = 'LOANS'
    IF #LOAN-IND = ' '
      ESCAPE TOP
    END-IF
  ELSE
    IF   #JCL-SELECT = 'NON-LOANS'
      IF #LOAN-IND = 'L'
        ESCAPE TOP
      END-IF
    ELSE
      IF #JCL-SELECT NE 'ALL'
        IF  (#ID-NUMBER-1 = 66000)  AND (#REAL-ISS-DATE = 19790401)
*       IF  (#ID-NUMBER-1 = 32929)  AND (#REAL-ISS-DATE = 19600401)
          IGNORE
        ELSE
          ESCAPE TOP
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF #RECS-READ = 1
    PERFORM DETAIL-PROCESSING
    PERFORM ACCUM-CELL-TOTALS
    ESCAPE TOP
  END-IF
*
  IF  (#LOAN-IND       NE #LAST-LOAN-IND)
   OR (#PLAN           NE #LAST-PLAN)
   OR (#ISS-DATE       NE #LAST-ISS-DATE)
   OR (#ISS-AGE        NE #LAST-ISS-AGE)
   OR (#UNITS          NE #LAST-UNITS)
    PERFORM CELL-PROCESSING
    PERFORM ACCUM-SUB-TOTALS
  END-IF
*
  IF  (#PLAN NE #LAST-PLAN)
   OR (#LOAN-IND NE #LAST-LOAN-IND)
    PERFORM PLAN-PROCESSING
    PERFORM ACCUM-GRAND-TOTALS
  END-IF
*
  PERFORM DETAIL-PROCESSING
  PERFORM ACCUM-CELL-TOTALS
*
END-WORK
*
PERFORM CELL-PROCESSING
PERFORM ACCUM-SUB-TOTALS
PERFORM PLAN-PROCESSING
PERFORM ACCUM-GRAND-TOTALS
PERFORM GRAND-TOTAL-PROCESSING
*
RESET #RECORD-1
MOVE '******' TO #ID-NUMBER-1-A
MOVE #RECORD-1 TO #PROJ-RECORD
WRITE WORK 2 #PROJ-RECORD
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DETAIL-PROCESSING
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
MOVE #LOAN-IND      TO #LAST-LOAN-IND
MOVE #PLAN          TO #LAST-PLAN
MOVE #ISS-DATE      TO #LAST-ISS-DATE
MOVE #ISS-AGE       TO #LAST-ISS-AGE
MOVE #UNITS         TO #LAST-UNITS
*
MOVE #EXTR-RECORD-A TO #HOLD-EXTRACT-REC-1
*
END-SUBROUTINE /* (0221)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CELL-PROCESSING
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (1) NE 0
*
  MOVE #EXTR-RECORD-A TO #HOLD-EXTRACT-REC-2
  MOVE #HOLD-EXTRACT-REC-1 TO #EXTR-RECORD-A
*
  MOVE 999999 TO #ID-NUMBER-1
* MOVE 999999 TO #ID-NUMBER-2    /* KEEP LAST ID FOR REFERENCE
* MOVE 999999 TO #ID-NUMBER-3
* MOVE 'G'    TO #MODE
* MOVE 'M'    TO #SEX
* COMPUTE ROUNDED #ACV          = #ACC-ACV        (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #CV           = #ACC-CV         (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #LAST-ANN-CV  = #ACC-LST-ANN-CV (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #DB           = #ACC-DB         (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #MONTHLY-ALLOC-CRED
*                               = #ACC-ALLOC-CRED (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #MONTHLY-PREM-DEFIC
*                               = #ACC-PREM-DEFIC (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #LOAN-AMT     = #ACC-LOAN-AMT   (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #LOAN-INT-RATE
*                            = #ACC-LOAN-INT-RATE (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #NET-PREM-RATIO
*                            = #ACC-NET-PR-RATIO  (1) / #ACC-PLANS (1)
* COMPUTE ROUNDED #ANN-PREM
*                            = #ACC-ANN-PREM      (1) / #ACC-PLANS (1)
*
  IF #PLAN = 'PUP'
    MOVE '47' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  IF #PLAN = 'RPU'
    MOVE '45' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  IF #PLAN = 'ETI'
    MOVE '44' TO #PAY-STATUS
    MOVE 'OL ' TO #PLAN
  END-IF
*
  MOVE #ACC-PLANS (1) TO #CELL-COUNT
  ADD 1 TO #ACC-NUM-CELLS (1)
*
  DISPLAY (2) NOTITLE
              'ID-NUM'     #ID-NUMBER-2
              'LN'         #LAST-LOAN-IND
              'PL'         #LAST-PLAN
              'ISS/DT'     #REAL-ISS-DATE
              'ISS/AGE'    #REAL-ISS-AGE
              'UN/ITS'     #LAST-UNITS
              'NUM/PLNS'   #ACC-PLANS              (1) (EM=ZZZ9)
              'TOT/UNTS'   #ACC-UNITS              (1) (EM=ZZZ9)
*             'FACE/AMT'   #ACC-FAC-AMT            (1) (EM=ZZ,ZZZ,ZZ9)
              'ACV'        #ACV                        (EM=ZZ,ZZZ,ZZ9)
              'CASH/VALUE' #CV                         (EM=ZZ,ZZZ,ZZ9)
              'LST/ANN CV' #LAST-ANN-CV                (EM=ZZ,ZZZ,ZZ9)
              'DEATH/BENE' #DB                         (EM=ZZ,ZZZ,ZZ9)
              'PREM/DEFC'  #MONTHLY-PREM-DEFIC         (EM=ZZ,ZZ9)
              'ALLOC/CRED' #MONTHLY-ALLOC-CRED         (EM=ZZ,ZZ9)
              'ANN/PREM'   #ANN-PREM                   (EM=ZZZ,ZZ9)
              'LOAN/AMT'   #LOAN-AMT                   (EM=Z,ZZZ,ZZ9)
              'INT/RATE'   #LOAN-INT-RATE              (EM=ZZZZ9.99999)
              'NT PR/RTIO' #NET-PREM-RATIO             (EM=ZZZZ9.99999)
*
* DISPLAY (2) NOTITLE
*                          #LAST-PLAN
*                          #LAST-ISS-DATE
*                          #LAST-ISS-AGE
*                          #LAST-UNITS
*                          #ACC-PLANS              (1) (EM=ZZZ9)
*                          #ACC-UNITS              (1) (EM=ZZZ9)
*                          #ACC-FAC-AMT            (1) (EM=ZZ,ZZZ,ZZ9)
*                          #ACV                        (EM=ZZ,ZZZ,ZZ9)
*                          #CV                         (EM=ZZ,ZZZ,ZZ9)
*                          #LAST-ANN-CV                (EM=ZZ,ZZZ,ZZ9)
*                          #DB                         (EM=ZZ,ZZZ,ZZ9)
*                          #MONTHLY-PREM-DEFIC         (EM=ZZ,ZZ9)
*                          #MONTHLY-ALLOC-CRED         (EM=ZZ,ZZ9)
*                          #ACC-ANN-PREM           (1) (EM=ZZZ,ZZ9)
*                          #ACC-LOAN-AMT           (1) (EM=Z,ZZZ,ZZ9)
*                          #ACC-LOAN-INT-RATE      (1) (EM=ZZZZ9.99999)
*                          #ACC-NET-PR-RATIO       (1) (EM=ZZZZ9.99999)
*
  MOVE #RECORD-1 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
  MOVE #RECORD-2 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
  MOVE #RECORD-3 TO #PROJ-RECORD
  WRITE WORK 2 #PROJ-RECORD
*
  MOVE #HOLD-EXTRACT-REC-2 TO #EXTR-RECORD-A
END-IF
*
END-SUBROUTINE /* (0235)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PLAN-PROCESSING
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (2) NE 0
  NEWPAGE (2)
  WRITE (2) // 'PLAN TYPE ' #LAST-PLAN
  WRITE (2) / '**** PLAN TYPE TOTALS ****' /
  WRITE (2) 'TOTAL CELLS       = ' #ACC-NUM-CELLS (2)
  WRITE (2) 'TOTAL PLANS       = ' #ACC-PLANS     (2)
  WRITE (2) 'TOTAL MEMBERSHIPS = ' #ACC-UNITS     (2)
  WRITE (2) 'TOTAL FACE AMT    = ' #ACC-FAC-AMT   (2)
  WRITE (2) 'TOTAL CASH VALUE  = ' #ACC-CV        (2)
  WRITE (2) 'TOTAL ACC CSV     = ' #ACC-ACV       (2)
  WRITE (2) 'TOTAL DEATH BENE  = ' #ACC-DB        (2)
  WRITE (2) 'TOTAL PREM DEFIC  = ' #ACC-PREM-DEFIC(2)
  WRITE (2) 'TOTAL AMT OF LOANS= ' #ACC-LOAN-AMT  (2)
  NEWPAGE (2)
END-IF
*
END-SUBROUTINE /* (0337)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE GRAND-TOTAL-PROCESSING
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #ACC-PLANS (3) NE 0
  NEWPAGE (2)
  WRITE (2) // '**** GRAND TOTALS ****' /
  WRITE (2) 'TOTAL CELLS       = ' #ACC-NUM-CELLS (3)
  WRITE (2) 'TOTAL PLANS       = ' #ACC-PLANS     (3)
  WRITE (2) 'TOTAL MEMBERSHIPS = ' #ACC-UNITS     (3)
  WRITE (2) 'TOTAL FACE AMT    = ' #ACC-FAC-AMT   (3)
  WRITE (2) 'TOTAL CASH VALUE  = ' #ACC-CV        (3)
  WRITE (2) 'TOTAL ACC CSV     = ' #ACC-ACV       (3)
  WRITE (2) 'TOTAL DEATH BENE  = ' #ACC-DB        (3)
  WRITE (2) 'TOTAL PREM DEFIC  = ' #ACC-PREM-DEFIC(3)
  WRITE (2) 'TOTAL AMT OF LOANS= ' #ACC-LOAN-AMT  (3)
  NEWPAGE (2)
END-IF
*
END-SUBROUTINE /* (0337)
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-CELL-TOTALS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
ADD #DB                   TO #ACC-DB             (1)
ADD #UNITS                TO #ACC-UNITS          (1)
ADD #FACE-AMT             TO #ACC-FAC-AMT        (1)
ADD #ACV                  TO #ACC-ACV            (1)
ADD #CV                   TO #ACC-CV             (1)
ADD #LAST-ANN-CV          TO #ACC-LST-ANN-CV     (1)
ADD #MONTHLY-ALLOC-CRED   TO #ACC-ALLOC-CRED     (1)
ADD #MONTHLY-PREM-DEFIC   TO #ACC-PREM-DEFIC     (1)
ADD 1                     TO #ACC-PLANS          (1)
ADD #LOAN-AMT             TO #ACC-LOAN-AMT       (1)
ADD #LOAN-INT-RATE        TO #ACC-LOAN-INT-RATE  (1)
ADD #ANN-PREM             TO #ACC-ANN-PREM       (1)
ADD #NET-PREM-RATIO       TO #ACC-NET-PR-RATIO   (1)
*
END-SUBROUTINE /* (0380)
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-SUB-TOTALS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
ADD #DB                 TO #ACC-DB             (2)
ADD #UNITS              TO #ACC-UNITS          (2)
ADD #FACE-AMT           TO #ACC-FAC-AMT        (2)
ADD #ACV                TO #ACC-ACV            (2)
ADD #CV                 TO #ACC-CV             (2)
ADD #LAST-ANN-CV        TO #ACC-LST-ANN-CV     (2)
ADD #MONTHLY-ALLOC-CRED TO #ACC-ALLOC-CRED     (2)
ADD #MONTHLY-PREM-DEFIC TO #ACC-PREM-DEFIC     (2)
ADD #LOAN-AMT           TO #ACC-LOAN-AMT       (2)
ADD #LOAN-INT-RATE      TO #ACC-LOAN-INT-RATE  (2)
ADD #ANN-PREM           TO #ACC-ANN-PREM       (2)
ADD #NET-PREM-RATIO     TO #ACC-NET-PR-RATIO   (2)
ADD #ACC-PLANS          (1) TO #ACC-PLANS          (2)
ADD #ACC-NUM-CELLS      (1) TO #ACC-NUM-CELLS      (2)
*
RESET #ACC-DB             (1)
      #ACC-UNITS          (1)
      #ACC-FAC-AMT        (1)
      #ACC-ACV            (1)
      #ACC-CV             (1)
      #ACC-LST-ANN-CV     (1)
      #ACC-ALLOC-CRED     (1)
      #ACC-PREM-DEFIC     (1)
      #ACC-PLANS          (1)
      #ACC-LOAN-AMT       (1)
      #ACC-LOAN-INT-RATE  (1)
      #ACC-ANN-PREM       (1)
      #ACC-NET-PR-RATIO   (1)
      #ACC-NUM-CELLS      (1)
*
END-SUBROUTINE /* (0400)
*
* * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCUM-GRAND-TOTALS
* * * * * * * * * * * * * * * * * * * * * * * * * *
*
ADD #ACC-DB             (2) TO #ACC-DB             (3)
ADD #ACC-UNITS          (2) TO #ACC-UNITS          (3)
ADD #ACC-FAC-AMT        (2) TO #ACC-FAC-AMT        (3)
ADD #ACC-ACV            (2) TO #ACC-ACV            (3)
ADD #ACC-CV             (2) TO #ACC-CV             (3)
ADD #ACC-LST-ANN-CV     (2) TO #ACC-LST-ANN-CV     (3)
ADD #ACC-ALLOC-CRED     (2) TO #ACC-ALLOC-CRED     (3)
ADD #ACC-PREM-DEFIC     (2) TO #ACC-PREM-DEFIC     (3)
ADD #ACC-PLANS          (2) TO #ACC-PLANS          (3)
ADD #ACC-LOAN-AMT       (2) TO #ACC-LOAN-AMT       (3)
ADD #ACC-LOAN-INT-RATE  (2) TO #ACC-LOAN-INT-RATE  (3)
ADD #ACC-ANN-PREM       (2) TO #ACC-ANN-PREM       (3)
ADD #ACC-NET-PR-RATIO   (2) TO #ACC-NET-PR-RATIO   (3)
ADD #ACC-NUM-CELLS      (2) TO #ACC-NUM-CELLS      (3)
*
RESET #ACC-DB             (2)
      #ACC-UNITS          (2)
      #ACC-FAC-AMT        (2)
      #ACC-ACV            (2)
      #ACC-CV             (2)
      #ACC-LST-ANN-CV     (2)
      #ACC-ALLOC-CRED     (2)
      #ACC-PREM-DEFIC     (2)
      #ACC-PLANS          (2)
      #ACC-LOAN-AMT       (2)
      #ACC-LOAN-INT-RATE  (2)
      #ACC-ANN-PREM       (2)
      #ACC-NET-PR-RATIO   (2)
      #ACC-NUM-CELLS      (2)
*
END-SUBROUTINE /* (0436)
*
END
