* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: FIXCTCD  - CORRECTS CERT CODE ON SELECTED RECORDS
*
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 BIRTH-CERT-CODE     (A1)
  2 MARRIAGE-CERT-CODE  (A1)
  2 BIRTH-CERT-CODE-SP  (A1)
  2 DEATH-CERT-CODE     (A1)
* * * *   F I R S   F I L E   * * * * * * * * * * * * * * *
1 FIRS-V VIEW OF A-FIRS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 DEPN-BIRTH-CD       (A1/14)         /* OCCURS 14 TIMES
  2 DEPN-NAME           (A15/14)        /* OCCURS 14 TIMES
* * * *   S E L E C T     F I L E   * * * * * * * * * * * * * *
1 SELECT-FILE
  2 SF-ID-NUM-0    (N1)
  2 SF-ID-NUMBER   (N5)
  2 SF-CODE        (A3)
  2 SF-DEPN-NAME   (A15)
*
 1 #LAST-ID-NUMBER-GR
  2 #LAST-ID-NUMBER-N1   (N1)
  2 #LAST-ID-NUMBER-N5   (N5)
 1 REDEFINE #LAST-ID-NUMBER-GR
  2 #LAST-ID-NUMBER  (A6)
*
1 #COUNTERS
  2 #NUM-RECS-READ  (P5)
  2 #MEMB-RECS-FND  (P5)
  2 #WIDO-RECS-FND  (P5)
  2 #FIRS-RECS-FND  (P5)
  2 #NUM-RECS-FOUND (P5)
  2 #NUM-DEPN-MATCHES (P5)
  2 #NUM-ST-RECS-NOT-FOUND (P5)
  2 #NUM-FIRS-RECS-NOT-FOUND (P5)
  2 #MEM-BIRTH-CODE (P5)
  2 #MEM-MARR-CODE  (P5)
  2 #SPS-BIRTH-CODE (P5)
  2 #DEP-BIRTH-CODE (P5)
  2 #DEATH-CERT-CODE (P5)
  2 #NUM-N           (P5)
  2 #NUM-U           (P5)
  2 #NUM-A           (P5)
  2 #NUM-CHG-U       (P5)
  2 #ST-NOT-FOUND     (L)
  2 #FIRS-NOT-FOUND   (L)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #ST-ISN           (P8)
  2 #FIRS-ISN         (P8)
*
 1 #ID-GR
  2 #ID-N1        (N1)
  2 #ID-N5        (N5)
 1 REDEFINE #ID-GR
  2 #ID-A         (A6)
*
 1 #TABLE                (25)
  2 #TB-CODE       (A3)
  2 #TB-DEPN-NAME  (A15)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
*
RESET #I1
READ WORK 1 RECORD SELECT-FILE
  ADD +1 TO #NUM-RECS-READ
*
  IF #NUM-RECS-READ = 1
    MOVE 0            TO #LAST-ID-NUMBER-N1
    MOVE SF-ID-NUMBER TO #LAST-ID-NUMBER-N5
    ADD +1 TO #I1
    MOVE SF-CODE      TO #TB-CODE     (#I1)
    MOVE SF-DEPN-NAME TO #TB-DEPN-NAME (#I1)
    ESCAPE TOP
  END-IF
*
  IF SF-ID-NUMBER NE #LAST-ID-NUMBER-N5
    PERFORM UPDATE-RECORD
    RESET #TABLE (*)
    RESET #I1
    MOVE 0            TO #LAST-ID-NUMBER-N1
    MOVE SF-ID-NUMBER TO #LAST-ID-NUMBER-N5
  END-IF
*
  ADD +1 TO #I1
  MOVE SF-CODE      TO #TB-CODE     (#I1)
  MOVE SF-DEPN-NAME TO #TB-DEPN-NAME (#I1)
*
END-WORK
*
PERFORM UPDATE-RECORD
*
WRITE 'NUM-RECS-READ        ' #NUM-RECS-READ
  /   'MEMB-RECS-FOUND      ' #MEMB-RECS-FND
  /   'WIDO-RECS-FOUND      ' #WIDO-RECS-FND
  /   'FIRS-RECS-FOUND      ' #FIRS-RECS-FND
  /   'NUM-RECS-FOUND       ' #NUM-RECS-FOUND
  /   'NUM-DEPN-MATCHES     ' #NUM-DEPN-MATCHES
  /   'NUM-ST-RECS-NOT-FOUND' #NUM-ST-RECS-NOT-FOUND
  /   'NUM-FIRS-RECS-NT-FND ' #NUM-FIRS-RECS-NOT-FOUND
  /   'MEM-BIRTH-CODE       ' #MEM-BIRTH-CODE
  /   'MEM-MARR-CODE        ' #MEM-MARR-CODE
  /   'SPS-BIRTH-CODE       ' #SPS-BIRTH-CODE
  /   'DEP-BIRTH-CODE       ' #DEP-BIRTH-CODE
  /   'DEATH-CERT-CODE      ' #DEATH-CERT-CODE
  /   'NUM-IGNORED N-RECS   ' #NUM-N
  /   'NUM-U RECS           ' #NUM-U
  /   'NUM-A RECS           ' #NUM-A
  /   'NUM-CHGED-U RECS     ' #NUM-CHG-U
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-RECORD
*
RESET #ST-NOT-FOUND
RESET #FIRS-NOT-FOUND
*
FIND (1) ST-V WITH MEMBER-KEY = #LAST-ID-NUMBER
  IF NO RECORDS FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #LAST-ID-NUMBER
      IF NO RECORDS FOUND
        MOVE TRUE TO #ST-NOT-FOUND
        ADD 1 TO #NUM-ST-RECS-NOT-FOUND
        ESCAPE ROUTINE
      END-NOREC
      MOVE *ISN TO #ST-ISN
      ADD 1 TO #WIDO-RECS-FND
      ADD 1 TO #NUM-RECS-FOUND
      ESCAPE BOTTOM (0133)
    END-FIND
  END-NOREC
  MOVE *ISN TO #ST-ISN
  ADD 1 TO #MEMB-RECS-FND
  ADD 1 TO #NUM-RECS-FOUND
  ESCAPE BOTTOM (0133)
END-FIND
*
GET ST-V #ST-ISN
*
FIND (1) FIRS-V WITH MEMBER-KEY = #LAST-ID-NUMBER
  IF NO RECORDS FOUND
    FIND (1) FIRS-V WITH WIDOW-KEY = #LAST-ID-NUMBER
      IF NO RECORDS FOUND
        MOVE TRUE TO #FIRS-NOT-FOUND
        ADD 1 TO #NUM-FIRS-RECS-NOT-FOUND
      END-NOREC
      MOVE *ISN TO #FIRS-ISN
      ESCAPE BOTTOM (0155)
    END-FIND
  END-NOREC
  MOVE *ISN TO #FIRS-ISN
  ADD 1 TO #FIRS-RECS-FND
  ADD 1 TO #NUM-RECS-FOUND
END-FIND
*
GET FIRS-V #FIRS-ISN
*
FOR #I1 1 TO 25
*
  IF #TB-CODE (#I1) = ' '
    ESCAPE TOP
  END-IF
*
  IF #TB-CODE (#I1) = 'MMB' OR = 'SMB'
    IF ST-V.BIRTH-CERT-CODE = 'N'
      WRITE 'MB N-RECS'    #LAST-ID-NUMBER
      ADD 1 TO #NUM-N
    ELSE
      IF ST-V.BIRTH-CERT-CODE = 'A'
        ADD 1 TO #NUM-A
      ELSE
        IF ST-V.BIRTH-CERT-CODE = 'U'
          WRITE 'MB U-RECS     '    #LAST-ID-NUMBER
          ADD 1 TO #NUM-U
        END-IF
      END-IF
*
      WRITE 'MB UPDATED            '    #LAST-ID-NUMBER
      MOVE 'U' TO ST-V.BIRTH-CERT-CODE
      ADD 1 TO #NUM-CHG-U
      ADD 1 TO #MEM-BIRTH-CODE
    END-IF
  ELSE
    IF #TB-CODE (#I1) = 'MMM' OR = 'SMM'
        IF ST-V.MARRIAGE-CERT-CODE = 'N'
        WRITE 'MM N-RECS'    #LAST-ID-NUMBER
        ADD 1 TO #NUM-N
      ELSE
        IF ST-V.MARRIAGE-CERT-CODE = 'A'
          ADD 1 TO #NUM-A
        ELSE
          IF ST-V.MARRIAGE-CERT-CODE = 'U'
            WRITE 'MM U-RECS     '    #LAST-ID-NUMBER
            ADD 1 TO #NUM-U
          END-IF
        END-IF
*
        WRITE 'MM UPDATED            '    #LAST-ID-NUMBER
        MOVE 'U' TO ST-V.MARRIAGE-CERT-CODE
        ADD 1 TO #MEM-MARR-CODE
        ADD 1 TO #NUM-CHG-U
      END-IF
    ELSE
      IF #TB-CODE (#I1) = 'MSB' OR = 'SSB'
        IF ST-V.BIRTH-CERT-CODE-SP = 'N'
          WRITE 'SB N-RECS'    #LAST-ID-NUMBER
          ADD 1 TO #NUM-N
        ELSE
          IF ST-V.BIRTH-CERT-CODE-SP = 'A'
            ADD 1 TO #NUM-A
          ELSE
            IF ST-V.BIRTH-CERT-CODE-SP = 'U'
              WRITE 'SB U-RECS     '    #LAST-ID-NUMBER
              ADD 1 TO #NUM-U
            END-IF
          END-IF
*
          WRITE 'SB UPDATED            '    #LAST-ID-NUMBER
          MOVE 'U' TO ST-V.BIRTH-CERT-CODE-SP
          ADD 1 TO #SPS-BIRTH-CODE
          ADD 1 TO #NUM-CHG-U
        END-IF
      ELSE
        IF #TB-CODE (#I1) = 'SMD'
          IF ST-V.DEATH-CERT-CODE = 'N'
            WRITE 'MD N-RECS'    #LAST-ID-NUMBER
            ADD 1 TO #NUM-N
          ELSE
            IF ST-V.DEATH-CERT-CODE = 'A'
              ADD 1 TO #NUM-A
            ELSE
              IF ST-V.DEATH-CERT-CODE = 'U'
                WRITE 'MD U-RECS     '    #LAST-ID-NUMBER
                ADD 1 TO #NUM-U
              END-IF
            END-IF
*
              WRITE 'MD UPDATED            '    #LAST-ID-NUMBER
              MOVE 'U' TO ST-V.DEATH-CERT-CODE
              ADD 1 TO #DEATH-CERT-CODE
            ADD 1 TO #NUM-CHG-U
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF (#TB-CODE (#I1) = 'MDB' OR = 'SDB') AND (NOT #FIRS-NOT-FOUND)
    FOR #I2 1 TO 14
      IF FIRS-V.DEPN-NAME (#I2) = #TB-DEPN-NAME (#I1)
        IF FIRS-V.DEPN-BIRTH-CD (#I2) = 'N'
          WRITE 'DB N-RECS'    #LAST-ID-NUMBER
          ADD 1 TO #NUM-N
        ELSE
          IF FIRS-V.DEPN-BIRTH-CD (#I2) = 'A'
            ADD 1 TO #NUM-A
          ELSE
            IF FIRS-V.DEPN-BIRTH-CD (#I2) = 'U'
              WRITE 'DP U-RECS     '    #LAST-ID-NUMBER
              ADD 1 TO #NUM-U
            END-IF
          END-IF
*
          WRITE 'DP UPDATED            '    #LAST-ID-NUMBER
          MOVE 'U' TO FIRS-V.DEPN-BIRTH-CD (#I2)
          ADD 1 TO #NUM-CHG-U
          ADD 1 TO #NUM-DEPN-MATCHES
          ADD 1 TO #DEP-BIRTH-CODE
          ESCAPE BOTTOM
        END-IF
      END-IF
    END-FOR
  END-IF
*
END-FOR /*(0173)
*
*  UPDATE (0153)
*
* IF NOT #FIRS-NOT-FOUND
*   UPDATE (0171)
* END-IF
* END TRANSACTION
*
END-SUBROUTINE
*
END
