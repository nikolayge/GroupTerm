* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPPEND - "AFSC ADHOC APPL PENDING BY MONTH"
************************************************************************
*                               MODIFICATION LOG                       *
* USER DATE       TAG  REASON                                          *
* JNT  8/29/2013  JT1  account for address deletion in databases       *
*                                                                      *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING G3500AAD  /* PM ADDED 03/2000 FOR FU5 LETTER
LOCAL
* * * *   A P P L I C A T I O N   T R A C K I N G   F I L E   * *
1 AT-V VIEW OF A-APPL-TRACKING
 2 AGING-KEY
 2 ID-NUMBER      (N6)
 2 NAME           (A25)
 2 RANK           (A3)
 2 DATE-OF-BIRTH  (N8)
 2 MEMBER-CN      (A6)
 2 MIL-STATUS     (A1)
 2 MIL-SERVICE    (A3)
 2 PLAN           (A3)
 2 MODE           (A1)
 2 PMT-PLAN       (A1)
 2 INS-ONLY       (A1)
 2 INTRO-FLAG     (A1)
*  2 ADDRESS-1      (A25)                      /* JT1 start
*  2 ADDRESS-2      (A25)
*  2 ADDRESS-3      (A25)
*  2 ZIP-CODE       (N9)
*  2 REDEFINE ZIP-CODE
*   3 ZIP-CODE-5     (A5)
*   3 ZIP-CODE-4     (A4)                      /* JT1 end
 2 SSN            (N9)
 2 DATE-RECEIVED  (N8)
 2 REDEFINE DATE-RECEIVED
  3 #RECV-DATE-N
   4 #RECV-DATE-YYYY (N4)
   4 #RECV-DATE-MM   (N2)
   4 #RECV-DATE-DD   (N2)
 2 REDEFINE DATE-RECEIVED
  3 #RECV-YYYYMM     (N6)
  3 #FILLER1         (N2)
 2 ACTION-DATE    (N8)
 2 REDEFINE ACTION-DATE
  3 #ACTION-DATE-N
   4 #ACTION-DATE-YYYY (N4)
   4 #ACTION-DATE-MM   (N2)
   4 #ACTION-DATE-DD   (N2)
 2 REDEFINE ACTION-DATE
  3 #ACTION-YYYYMM     (N6)
  3 #FILLER            (N2)
 2 ACTION-FLAG    (A1)
 2 APPL-SOURCE    (A7)
 2 CAP-IND        (A1)
 2 USER-SOURCE    (A3)
 2 FOLLOW-UP-DATE (N8/5)
 2 REDEFINE FOLLOW-UP-DATE
  3 FUD-OCC       (5)
   4 FUD-YYYY     (N4)
   4 FUD-MM       (N2)
   4 FUD-DD       (N2)
 2 VA-CALC-TYPE   (A1)
 2 HOME-PHONE     (N10)
 2 REDEFINE HOME-PHONE
  3 HOME-PHONE-A  (A10)
 2 HOME-PHONE-INTR (A6)
 2 WORK-PHONE     (N10)
 2 REDEFINE WORK-PHONE
  3 WORK-PHONE-A  (A10)
 2 WORK-PHONE-INTR (A6)
*
* * * * * * * *
1 SA-V VIEW OF A-SOURCE-OF-APPL
 2 SOURCE-CODE-GROUP  (A1)
*
1 #AT-RECS-READ   (N4)
1 #MTH-LIT (A48) INIT<'JAN FEB MAR APR MAY JUN JUL AUG SEP '
  -'OCT NOV DEC '>
1 REDEFINE #MTH-LIT   2 #MON-LIT (A4/12)
1 #APPL-PEND (N4/12)
1 #APPL-SOURCE (A42) INIT <'CAP   AAFMAAPO    WOM   INTNETAT    OTHER '>
1 REDEFINE #APPL-SOURCE
 2 #APPL-SRCE  (A6/7)
1 #APPL-CNT  (N3/7)
1 #AP-SRCE-READ  (N4)
1 #AT-YYYY (A4/12)
*
1 #I1        (I2) INIT <1>
1 #I2        (I2) INIT <1>
*
END-DEFINE
*
FORMAT (1) LS=133 PS=76
WRITE  (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                  'APPLICATIONS PENDING BY MONTH'
                   5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ AT-V BY NAME
  IF NOT INTRO-FLAG = 'V'
    ESCAPE TOP
  ELSE
    IF NOT DATE-RECEIVED GE 20011001
      ESCAPE TOP
    END-IF
  END-IF
*
  IF ACTION-FLAG = 'I'
    MOVE #RECV-DATE-MM TO #I1
    MOVE #RECV-DATE-YYYY TO #AT-YYYY(#I1)
    ADD 1 TO #APPL-PEND(#I1)
    ADD 1 TO #AT-RECS-READ
    FOR #I2 1 TO 7
      IF #APPL-SRCE(#I2) = APPL-SOURCE
        ADD 1 TO #APPL-CNT(#I2)
        ESCAPE BOTTOM
      ELSE
        IF #I2 = 7
          ADD 1 TO #APPL-CNT(#I2)
          ESCAPE BOTTOM
        END-IF
      END-IF
    END-FOR
  ELSE
    ESCAPE TOP
  END-IF
*
  AT END OF DATA
    FOR #I1 1 TO 12
      IF #APPL-PEND(#I1) > 0
        WRITE (1)  5T #MON-LIT(#I1) #AT-YYYY(#I1)':' 20T #APPL-PEND(#I1)
      END-IF
    END-FOR
    WRITE (1) 1T "TOTAL AT'S BY MONTH :" #AT-RECS-READ
    SKIP 2
    FOR #I2 1 TO 7
      IF #APPL-CNT(#I2) > 0
        WRITE (1) 5T #APPL-SRCE(#I2)':' 15T #APPL-CNT(#I2)
        ADD #APPL-CNT(#I2) TO #AP-SRCE-READ
      END-IF
    END-FOR
    WRITE (1) 1T "TOTAL AT'S BY APPL/SRC :" #AP-SRCE-READ
  END-ENDDATA
*
END-READ
*
* WRITE 'TOT-READ=' #AT-RECS-READ
END
