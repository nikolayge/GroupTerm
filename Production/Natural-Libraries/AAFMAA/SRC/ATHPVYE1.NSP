* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVYE1 - YEAR-END VALUE-ADDED PROCESSING
*                      - STEP 1 - EXTRACT STATUS FILE INFO
*   <<<<< MUST HANDLE VAR TERM INS FOR 2000 YEAR-END <<<<<<<<<
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  09062006 DW1  Increase size of #recs-read
* RSE  09302009 RE1  Change 600K logic for Unification
* YAK  10112012 YK1  Use function to identify Level Term II plans
* vxt  12122012 vt1  Extend fieldd sizes
* vxt  12302013 vt2  Extend the field sizes
* TMT  06112014 TT1  Contact Normalization - Date of Birth
*******************************************
*
DEFINE DATA
LOCAL USING ATHLVYE1
LOCAL
1 #JCL-PARM       (A34)
1 REDEFINE #JCL-PARM
  2 #JCL-FIL-1        (A13)    /* 'PROCESS YEAR='
  2 #JCL-PROCESS-YYYY (N4)     /* '1988'
  2 #JCL-FIL-2           (A15)    /* ' PROCESS MONTH='
  2 #JCL-PROCESS-MM      (N02)    /* 03
*
1 #EXTR-RECORD
  2 #ID-NUMBER            (N6)                                      /* 1 - 6
  2 #ISSUE-DATE           (N8)                                      /* 7 - 8
  2 #PAY-STATUS           (A2)    /* Prior to 1997 y/e              /* 15 -2
  2 #NUM-SHIPS            (N1)    /* P1                             /* 17 -1
  2 #FACE-AMT             (N7.2)  /* P5.2     /* vt2 was n6.2       /* 18 -9

  2 #ANN-PREM             (N5.2)  /* P5.2                           /* 27 - 7
  2 #MIN-CV               (N7.2)  /* P5.2     /* vt1 was n6.2       /* 34 - 9
  2 #NXT-YR-MIN-CV        (N7.2)  /* P5.2     /* vt1 was n6.2       /* 43 - 9
  2 #INT-RATE             (N2.3)  /* P2.3                           /* 52 -5
  2 #MODE                 (A1)     /*      = 54 CHARS               /* 57 - 1
*       below added 1997 y/e
  2 #INTRO-FLAG           (A1)                                      /* 58 - 1
  2 #PLAN                 (A3)                                      /* 59 -3
  2 REDEFINE #PLAN
    3 #PLAN-A1    (A1)
    3 #PLAN-N2    (N2)
  2 #DOB                  (N8)
  2 #ACV                  (N7.2)
  2 #CV                   (N7.2)
  2 #CONT-FUND            (N7.2)  /* beg-cash-value in database
  2 #CURR-ACQ             (N4.2)  /* month-alloc-amt in database
  2 #MON-AMORT-ACQ        (N3.2)
  2 #DB                   (N7.2)
  2 #FIXED-LOAN           (N7.2)
  2 #FIXED-ACC-INT        (N5.2)
  2 #VAR-LOAN             (N7.2)
  2 #VAR-ACC-INT          (N5.2)
  2 #DB-CR                (N7.2)
  2 #PAID-TO-DT           (N8)
  2 #PAID-UP-DT           (N8)     /* + 116 = 170 chars  a/o 1/1998
  2 #5-5-DEATH-BENEFIT    (N7.2)   /* + 125 = 179 chars  a/o  4/1998
* below added 12/1998
  2 #BOTH-LOANS           (N7.2)
  2 #CAP-POL-FLAG         (A1)
  2 #SMOKER-FLAG          (A1)
  2 #USES-1996-FLAG       (A1)
  2 #DUES-FLAG            (A1)     /* + 138 = 192 chars  a/o  12/98
  2 #NXT-MON-CASH-VALUE   (N7.2)
  2 #NXT-MON-DEATH-BENEFIT (N7.2)
  2 #YTD-DUES-PAID        (N5.2)   /* + 163 = 217 chars  a/o  12/98
* BELOW ADDED 12/1999
  2 #RID-PLAN             (A2)
  2 #RID-EFF-DT           (N8)
  2 #RID-PDTO-DT          (N8)     /* + 18 = 235 chars  a/o  12/99
*
1 #COUNTERS
  2 #RECS-READ        (P7)    /* DW1
  2 #RECS-WRITTEN     (P6)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I2               (I2)
*
1 #TOT-CSV   (N7.2)
1 #FOUND-CAP-WL-POL (L)
*
1 #SVC-FEE    (N2.2)
1 #SVC-FEE-2P (N2.2) INIT <18.50>
1 #SVC-FEE-3P (N2.2) INIT <12.50>
1 #SVC-FEE-4P (N1.2) INIT <9.50>
1 #SVC-FEE-5P (N1.2) INIT <7.50>
1 #SVC-FEE-6P (N1.2) INIT <6.50>
1 #SVC-FEE-7P (N1.2) INIT <6.00>
1 #SVC-FEE-10 (N1.2) INIT <4.50>
1 #SVC-FEE-20 (N1.2) INIT <3.00>
1 #SVC-FEE-30 (N1.2) INIT <2.50>
1 #SVC-FEE-OL (N1.2) INIT <2.00>
1 #SVC-FEE-TM (N1.2) INIT <2.00>
1 #SVC-FEE-SP (N3.2) INIT <450.00>
1 #SVC-FEE-SO (N4.2) INIT <1550.00>
*
1 DATE-OF-BIRTH (N8.0)                                        /* TT1 BEGIN
1 REDEFINE DATE-OF-BIRTH
  2 DATE-OF-BIRTH-YYYY (N4)
  2 DATE-OF-BIRTH-MMDD (N4)                                   /* TT1 END
END-DEFINE
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'STATUS EXTRACT FOR YEAR-END VALUE-ADDED PROCESSING'
  5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
READ ST-V BY MEMBER-KEY FROM '001900'
*  IF ID-NUMBER GE 600000    /* stop at afsc   RE1 START
*    ESCAPE BOTTOM
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)
    ESCAPE TOP                              /* RE1 END
  END-IF
*
  ADD 1 TO #RECS-READ
*
  RESET #TOT-CSV
  FOR #I1 1 TO 9
    IF   (STATUS (#I1) NE 'D')
*      below no longer needed (past 2 years)
*    OR  (((ISSUE-DATE (#I1) LT 19970101)    /* old or new not nxt
*    OR    (MONTH-ALLOC-AMT (#I1) = 999.99))  /* forced new
*    AND   (LAST-DURATION (#I1) LT 2))
      ESCAPE TOP
    END-IF
*
    ADD CASH-VALUE (#I1) TO #TOT-CSV
  END-FOR
*
  FOR #I1 1 TO 9
    IF ST-V.STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    RESET DATE-OF-BIRTH                                                /* TT1
    DATE-OF-BIRTH   := GET-DATE-OF-BIRTH(<MEMBER-CONTACT-ID>)          /* TT1
*    
    MOVE ID-NUMBER                  TO #ID-NUMBER
    MOVE ISSUE-DATE           (#I1) TO #ISSUE-DATE
    MOVE PAY-STATUS           (#I1) TO #PAY-STATUS
    MOVE NUM-MBRSHIPS         (#I1) TO #NUM-SHIPS
    MOVE FACE-AMOUNT          (#I1) TO #FACE-AMT
    MOVE ANNUAL-PREM          (#I1) TO #ANN-PREM
    MOVE MIN-CASH-VALUE       (#I1) TO #MIN-CV
    MOVE NXT-YR-MIN-CASH-VALUE(#I1) TO #NXT-YR-MIN-CV
    MOVE X-INTEREST-RATE      (#I1) TO #INT-RATE
    MOVE MODE                 (#I1) TO #MODE
*
    MOVE INTRO-FLAG            TO #INTRO-FLAG
    MOVE PLAN            (#I1) TO #PLAN
    MOVE DATE-OF-BIRTH         TO #DOB
    MOVE ACC-CASH-VALUE  (#I1) TO #ACV
    MOVE CASH-VALUE      (#I1) TO #CV
*
    RESET #BOTH-LOANS #CAP-POL-FLAG #USES-1996-FLAG #DUES-FLAG
      #YTD-DUES-PAID
      #RID-PLAN #RID-EFF-DT #RID-PDTO-DT
    IF   (ISSUE-DATE (#I1) GE 19970101)
        AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
*
      IF (NOT PLAN (#I1) = 'SO' OR = 'FSO')
          AND (NOT INTRO-FLAG = 'S' OR = 'K' OR = 'G'
          OR = 'O' OR = 'P' OR = 'Q')
          AND (#I1 = 1)
        MOVE 'Y' TO #DUES-FLAG
        PERFORM CALC-DUES
      END-IF
*
      MOVE BEG-CASH-VALUE  (#I1) TO #CONT-FUND
      MOVE MONTH-ALLOC-AMT (#I1) TO #CURR-ACQ
      MOVE PREM-DEFICIENCY (#I1) TO #MON-AMORT-ACQ
      MOVE RID-PLAN        (#I1) TO #RID-PLAN
      MOVE RID-EFF-DT      (#I1) TO #RID-EFF-DT
      MOVE RID-PD-TO       (#I1) TO #RID-PDTO-DT
    ELSE
      RESET #CONT-FUND
      RESET #CURR-ACQ
      RESET #MON-AMORT-ACQ
    END-IF
*
    MOVE DEATH-BENEFIT   (#I1) TO #DB
    MOVE X-LOAN-AMOUNT      (#I1) TO #FIXED-LOAN
    MOVE X-ACCUM-INTEREST   (#I1) TO #FIXED-ACC-INT
    MOVE X-LOAN-AMOUNT-2    (#I1) TO #VAR-LOAN
    MOVE X-ACCUM-INTEREST-2 (#I1) TO #VAR-ACC-INT
    MOVE X-LOAN-AMOUNT      (#I1) TO #BOTH-LOANS
    ADD  X-LOAN-AMOUNT-2    (#I1) TO #BOTH-LOANS
    MOVE DEBIT-CREDIT-AMT      TO #DB-CR
    MOVE PAID-TO-DATE    (#I1) TO #PAID-TO-DT
    MOVE PAID-UP-DATE    (#I1) TO #PAID-UP-DT
    MOVE SMOKER-FLAG     (#I1) TO #SMOKER-FLAG
*
    IF ISSUE-DATE (#I1) LT 19860701
      MOVE BEG-CASH-VALUE (#I1) TO #5-5-DEATH-BENEFIT
    ELSE
      RESET #5-5-DEATH-BENEFIT
    END-IF
*
    IF MONTH-ALLOC-AMT (#I1) = 999.99
      MOVE 'Y' TO #USES-1996-FLAG
    END-IF
*
    RESET #FOUND-CAP-WL-POL
    FOR #I2 1 TO 9
      IF CONV-DURATION (#I2) = 999    /* cap policy
        MOVE TRUE TO #FOUND-CAP-WL-POL
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF NOT #FOUND-CAP-WL-POL AND (INTRO-FLAG = 'C' OR = 'B'
        OR = 'P' OR = 'Q')
      FOR #I2 1 TO 9    /* find first TERM pol
*       IF  (#PLAN = 'LT' OR = 'RT')   /* SET CAP POL INDICATOR         /* YK1
*        OR ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))                   /* YK1
        IF FUNCTION-TERM-POLICY(<#PLAN>)                                /* YK1
          MOVE 999 TO CONV-DURATION (#I2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-IF
*
    IF CONV-DURATION (#I1) = 999    /* cap policy
      MOVE 'Y' TO #CAP-POL-FLAG
      IF X-LOAN-AMOUNT-2 (#I1) NE 0
        WRITE ID-NUMBER 'CAP MBR WITH VAR LOAN'
          'FIXED =' X-LOAN-AMOUNT (#I1) 'VAR =' X-LOAN-AMOUNT-2 (#I1)
      END-IF
    END-IF
*
    DISPLAY 'ID' ID-NUMBER 'iss/date' ISSUE-DATE (#I1)
      'PLN' PLAN (#I1)
      'CNV/DUR' CONV-DURATION (#I1)
      'CSV' CASH-VALUE (#I1)
      'ln' X-LOAN-AMOUNT (#I1) 'ln-2' X-LOAN-AMOUNT-2 (#I1)
*
    MOVE NXT-MON-CASH-VALUE    (#I1) TO #NXT-MON-CASH-VALUE
    MOVE NXT-MON-DEATH-BENEFIT (#I1) TO #NXT-MON-DEATH-BENEFIT
    WRITE WORK 2 #EXTR-RECORD
    ADD 1 TO #RECS-WRITTEN
  END-FOR
END-READ
*
WRITE  'TOT-RECS-READ     = ' #RECS-READ
WRITE  'TOT-RECS-WRITTEN  = ' #RECS-WRITTEN
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE CALC-DUES
* * * * * * * * * * * * * *
*
IF PAY-STATUS (#I1) = '47' OR = '48'
  IF (PU-DT-YYYY (#I1) = #JCL-PROCESS-YYYY)
    IF PLAN (#I1) = 'SO'
      MOVE #SVC-FEE-SO TO #YTD-DUES-PAID
    ELSE
      MOVE #SVC-FEE-SP TO #YTD-DUES-PAID
    END-IF
  END-IF
*
  ESCAPE ROUTINE
END-IF
*
IF PAY-STATUS (#I1) LT '40'
  RESET #SVC-FEE
  DECIDE ON FIRST VALUE PLAN (#I1)
    VALUE '2PF' MOVE #SVC-FEE-2P TO #SVC-FEE
    VALUE '3PF' MOVE #SVC-FEE-3P TO #SVC-FEE
    VALUE '4PF' MOVE #SVC-FEE-4P TO #SVC-FEE
    VALUE '5PF' MOVE #SVC-FEE-5P TO #SVC-FEE
    VALUE '6PF' MOVE #SVC-FEE-6P TO #SVC-FEE
    VALUE '7PF' MOVE #SVC-FEE-7P TO #SVC-FEE
    VALUE '8PF' MOVE #SVC-FEE-7P TO #SVC-FEE
    VALUE '9PF' MOVE #SVC-FEE-7P TO #SVC-FEE
    VALUE '7P'  MOVE #SVC-FEE-7P TO #SVC-FEE
    VALUE '20P' MOVE #SVC-FEE-20 TO #SVC-FEE
    VALUE '30P' MOVE #SVC-FEE-30 TO #SVC-FEE
    VALUE 'OL'  MOVE #SVC-FEE-OL TO #SVC-FEE
*   VALUE 'RT'  MOVE #SVC-FEE-TM TO #SVC-FEE                  /* YK1
*   VALUE 'LT'  MOVE #SVC-FEE-TM TO #SVC-FEE                  /* YK1
    NONE        IGNORE
  END-DECIDE
*
* IF (PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z')        /* YK1
  IF FUNCTION-TERM-POLICY(<PLAN(#I1)>)                        /* YK1
    MOVE #SVC-FEE-TM TO #SVC-FEE
  END-IF
*
*
  IF PLAN (#I1) = MASK (NN'F')
    IF PLAN (#I1) LE '19F' MOVE #SVC-FEE-10 TO #SVC-FEE ELSE
      IF PLAN (#I1) LE '29F' MOVE #SVC-FEE-20 TO #SVC-FEE ELSE
        IF PLAN (#I1) LE '34F' MOVE #SVC-FEE-30 TO #SVC-FEE ELSE
          MOVE #SVC-FEE-OL TO #SVC-FEE
        END-IF END-IF END-IF
  END-IF
*
  IF ISS-DT-YYYY (#I1) = #JCL-PROCESS-YYYY
    COMPUTE #YTD-DUES-PAID = #SVC-FEE * (12 - ISS-DT-MM (#I1) + 1)
  ELSE
    COMPUTE #YTD-DUES-PAID = #SVC-FEE * 12
  END-IF
END-IF
*
WRITE #ID-NUMBER 'DUES = ' #SVC-FEE #YTD-DUES-PAID
*
END-SUBROUTINE
*
END
