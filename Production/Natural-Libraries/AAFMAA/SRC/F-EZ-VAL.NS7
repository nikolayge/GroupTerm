* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-EZ-FACE-LIMIT (F-EZ-VAL)                                 *
* DESCRIPTION  : Determine if EZ Term is valid by accumulating face-amt*
*                which should not exceed 100K                           *
************************************************************************
*                       MODIFICATION LOG    
* USER   DATE        TAG     REASON         
* RSE    01/07/2013          Program Written
************************************************************************
*
DEFINE FUNCTION F-EZ-FACE-LIMIT RETURNS (L) BY VALUE
*
DEFINE DATA
PARAMETER
1 #MEMBER-CN      (A6) BY VALUE
1 REDEFINE #MEMBER-CN
  2 #MEMBER-CN-N  (N6)
1 #FACE-AMOUNT    (N6.2) BY VALUE
*
LOCAL
1 ST-V VIEW OF A-STATUS
  2 STATUS (9)
  2 PLAN (9)
  2 FACE-AMOUNT(9)
  2 MEMBER-CONTACT-ID
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 AT-STATUS
*
1 #I              (N2)
*
END-DEFINE
*
MOVE TRUE TO F-EZ-FACE-LIMIT
*
FIND ST-V WITH ID-NUMBER = #MEMBER-CN-N
  FIND AT-V WITH CONTACT-ID = MEMBER-CONTACT-ID WHERE AT-STATUS NE 'A'
    IF NO RECORD FOUND
      ESCAPE ROUTINE
    END-NOREC
  END-FIND
  FOR #I 1 9
    IF STATUS (#I) = 'D' AND PLAN (#I) = 'EZ'
      ADD FACE-AMOUNT (#I) TO #FACE-AMOUNT
      IF #FACE-AMOUNT > 100000
        MOVE FALSE TO F-EZ-FACE-LIMIT
        ESCAPE ROUTINE
      END-IF
    END-IF
  END-FOR
END-FIND
*
END-FUNCTION
*
END
