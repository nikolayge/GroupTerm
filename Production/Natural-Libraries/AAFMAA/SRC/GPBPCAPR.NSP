* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* GPBPCAPR
* CAP REPORT
* RE 01/25/2012 Created
*
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE 02-06-2012        Creation
**************************************************************************
DEFINE DATA 
LOCAL USING GPBACAPD
LOCAL USING ADDA0001        
LOCAL
1 PR-V VIEW OF A-PROCESS-REQUEST
  2 ID-NUMBER
  2 REQUEST-FINAL-STATUS
  2 REQUEST-DATA
*
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 MBR-SINCE
  2 REDEFINE MBR-SINCE
    3 #M-YYYY(A4)
    3 #M-MM  (A2)
  2 ACTIVE-SEGMENTS (9)
    3 STATUS
    3 PAY-STATUS
  2 C*ACTIVE-SEGMENTS
  2 X-LOAN-DATE (9)
  2 REDEFINE X-LOAN-DATE
    3 X-LOAN-DATE-OCC (9)
      4 X-LOAN-DATE-YYYY (N4)
      4 X-LOAN-DATE-MM (N2)
      4 X-LOAN-DATE-DD (N2)
  2 X-ACCUM-INTEREST (9)
  2 X-NXT-YR-ACCUM-INT (9)
  2 X-LOAN-AMOUNT (9)
  2 X-LOAN-AMOUNT-2 (9)
  2 X-INTEREST-RATE (9)
*
1 CNT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 RANK-ID
  2 SSN
  2 DATE-OF-BIRTH
  2 REDEFINE DATE-OF-BIRTH
    3 #D-YYYY (A4)
    3 #D-MM   (A2)
    3 #D-DD   (A2)
  2 MILITARY-STATUS
*
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 MILITARY-BRANCH
*
1 TERM-V VIEW OF A-TERMINALS
  2 CLERK-ID              (A3)      
  2 INIT-USER
  2 COMMENTS
*
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 ADDRESS-POOL-ID
  2 ADDR-TYPE-CD
*
1 p_GPRS0CNT
   2 p_Name       (A)    DYNAMIC
   2 p_DOB_N      (N8)
   2 p_DOB        (A10)
   2 p_DOD_N      (N8)
   2 p_DOD        (A10)
   2 p_SEX        (A1)
   2 p_SSN        (N9)
*
1 p_PhoneN1M (10)
  2 Phone          (A36)   INIT ALL <' '>
  2 Phone-Type     (A16)   INIT ALL <' '>
  2 Phone-Type-CD  (A1)    INIT ALL <' '>
  2 Phone-Pref     (A1)    INIT ALL <' '>
*
1 #CN             (A6)
1 #M-NAME         (A50)
1 #M-DOB          (A10)
1 #INIT-USER      (A8)
1 #USER           (A40)
1 #I              (I2)
1 #J              (I2)
1 #HOLD-AMOUNT-BAL(P7.2)
1 #HOLD-AMOUNT-INT(P7.2)
1 #HOLD-AMOUNT-TOT(P7.2)
1 #UNPAID-PR-BAL  (A20)
1 #CURR-ACCUM-INT (A20)
1 #TOT-AMOUNT     (A20)
1 #DAILY-INT-RATE (P0.7)
*
1 #WYYYYMMDD
  2 #WYYYY  (N4)
  2 #WMM    (N2)
  2 #WDD    (N2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYY-AA(A4)
  2 #WMM-AA  (A2)
  2 #WDD-AA  (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N  (N8.0)
1 #WMMDDYYYY
  2 #WMM   (N2)
  2 #WDD   (N2)
  2 #WYYYY (N4)
1 REDEFINE #WMMDDYYYY
  2 #WMMDDYYYY-N (N8)
1 REDEFINE #WMMDDYYYY
  2 #WMM-A   (A2)
  2 #WDD-A   (A2)
  2 #WYYYY-A (A4)
*
END-DEFINE
*
FORMAT (1) LS=121 PS=250
**IF F-IS-DEV(<>)
*  f_FTP_DIR := f_FTP_DIR_Dev
**ELSE
*   f_FTP_DIR := f_FTP_DIR_Prod
**END-IF
MOVE *INIT-USER TO #INIT-USER
FIND TERM-V WITH TERMINAL-ID = #INIT-USER
  MOVE COMMENTS TO #USER
END-FIND
*
READ PR-V BY ID-NUMBER 
  ACCEPT IF REQUEST-FINAL-STATUS = 'P'
  FIND CNT-V WITH ID-NUMBER = PR-V.ID-NUMBER
  END-FIND
  FIND ST-V WITH ID-NUMBER = PR-V.ID-NUMBER
  END-FIND
  FIND RANK-V WITH RANK-ID = CNT-V.RANK-ID
  END-FIND
  PERFORM WRITE-HEADER-MEMBER-INFO
  PERFORM WRITE-CAP-LOAN-INFO
  PERFORM WRITE-LETTER-TO-MEMBER
  PERFORM WRITE-ST-INFO
  PERFORM WRITE-NOTES
END-READ
*
******************************************
DEFINE SUBROUTINE WRITE-HEADER-MEMBER-INFO
******************************************
MOVE PR-V.ID-NUMBER TO #CN
#M-NAME := EDIT_NAME_FMLS(<CNT-V.FIRST-NAME,CNT-V.MIDDLE-NAME,CNT-V.LAST-NAME,CNT-V.SUFFIX>)
WRITE (1) ' '
WRITE (1) NOTITLE 4x #CN 19x '^' //
  4x  "AAFMAA CAP LOAN COLLECTION SUMMARY" 2x *DAT4U  2x *TIME (AL=8) 2x *PROGRAM /
WRITE (1) '-----------------------------------------------      Member Information'
WRITE (1) 7x 'Member Name         :' #M-NAME
WRITE (1) 7x 'CN                  :' #CN
WRITE (1) 7x 'SSN                 :' CNT-V.SSN (EM=999-99-9999)
COMPRESS #D-MM '/' #D-DD '/' #D-YYYY INTO #M-DOB LEAVING NO
WRITE (1) 7x 'Date of Birth       :' #M-DOB //
*
END-SUBROUTINE
*************************************
DEFINE SUBROUTINE WRITE-CAP-LOAN-INFO
*************************************
MOVE PR-V.ID-NUMBER TO #ID-NUMBER
MOVE PR-V.REQUEST-DATA TO #POL-NUM
CALLNAT 'GPBSCAPD' #POLICY-CAP-FIELDS
*
WRITE (1) '-----------------------------------------------      CAP Loan Information'
FOR #I 1 17
  WRITE (1) 7X #CAP-INFO (#I) 
END-FOR
*
NEWPAGE (1)
*
END-SUBROUTINE
****************************************
DEFINE SUBROUTINE WRITE-LETTER-TO-MEMBER
****************************************
WRITE (1) '-------------------------  Letter Sent to Member ------------------------' //
  7X *DAT4U 5x #CN 2x '*' CNT-V.MILITARY-STATUS 2x RANK-V.MILITARY-BRANCH //
  7X #M-NAME 
*
FIND ADDR-REL-V WITH CONTACT-ID = CNT-V.CONTACT-ID
  IF ADDR-TYPE-CD NE 'P'
    ESCAPE TOP
  END-IF
  #ADD-CONTACT-ID := CONTACT-ID
  #ADD-TYPE       := ADDR-TYPE-CD
  #ADD-OLD-FORMAT := 'N'
  #ADD-POOL-ID    := ADDRESS-POOL-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
END-FIND                                         
*
WRITE (1) 7X #ADD-LINE-1 / 7X #ADD-LINE-2 / 7X #ADD-LINE-3 /
*
PERFORM COMPUTE-AMOUNTS
*
WRITE (1)
  'The purpose of this letter is to notify you that this is the FINAL NOTICE' /
  'requesting payment of money owed regarding your AAFMAA Career Assistance' /
  'Program (CAP) loan. Due to your payment default, the maturity of your' /
  'loan has been accelerated and the entire loan and all accrued interest' /
  'are due and payable in full.'  //
  'Demand is hereby made upon you for immediate and full payment of the' / 
  'following loan account:' //
  7x 'Unpaid Principal Balance    :' #UNPAID-PR-BAL /
  7X 'Accrued and Paid Interest   :' #CURR-ACCUM-INT /
  7x 'TOTAL AMOUNT DUE            :' #TOT-AMOUNT //
  'This correspondence has been prepared for you by: Kathie H. Ballard' /
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE COMPUTE-AMOUNTS
*********************************
MOVE *DATN TO #WYYYYMMDD-N
FOR #I 1 TO C*ACTIVE-SEGMENTS
  IF  (STATUS (#I) NE 'D')
      OR (PAY-STATUS (#I) = '44')
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #J
  IF X-LOAN-AMOUNT (#I) GT 0
    #HOLD-AMOUNT-BAL := X-LOAN-AMOUNT(#I) + X-LOAN-AMOUNT-2(#I)
    MOVE EDITED #HOLD-AMOUNT-BAL (EM=Z,ZZZ,ZZ9.99) TO #UNPAID-PR-BAL
    COMPUTE ROUNDED #DAILY-INT-RATE = ST-V.X-INTEREST-RATE (#I) / 365
*
    IF   (X-LOAN-DATE-YYYY (#I) = #WYYYYMMDD.#WYYYY)  /* LOAN TAKEN THIS MON
        AND (X-LOAN-DATE-MM   (#I) = #WYYYYMMDD.#WMM)
      COMPUTE ROUNDED #HOLD-AMOUNT-INT =
        X-ACCUM-INTEREST (#I) + X-NXT-YR-ACCUM-INT (#I)
        + (X-LOAN-AMOUNT (#I) *
        ((#DAILY-INT-RATE * (#WYYYYMMDD.#WDD - X-LOAN-DATE-DD (#I))) /100))
    ELSE
      COMPUTE ROUNDED #HOLD-AMOUNT-INT =
        X-ACCUM-INTEREST (#I) + X-NXT-YR-ACCUM-INT (#I)
        + (X-LOAN-AMOUNT (#I)
         * ((#DAILY-INT-RATE * (#WYYYYMMDD.#WDD - 1)) /100))
    END-IF
*
  END-IF
END-FOR
MOVE EDITED #HOLD-AMOUNT-INT (EM=Z,ZZZ,ZZ9.99) TO #CURR-ACCUM-INT
#HOLD-AMOUNT-TOT := #HOLD-AMOUNT-BAL + #HOLD-AMOUNT-INT
MOVE EDITED #HOLD-AMOUNT-TOT (EM=Z,ZZZ,ZZ9.99) TO #TOT-AMOUNT
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE WRITE-ST-INFO
*******************************
FETCH RETURN 'GPBPCAPS' *ISN(0142)
*
END-SUBROUTINE
*****************************
DEFINE SUBROUTINE WRITE-NOTES
*****************************
NEWPAGE(1)
WRITE (1) ' '
FETCH RETURN 'GPBPCAPN' *ISN(0142)
*
END-SUBROUTINE
*****************
END
