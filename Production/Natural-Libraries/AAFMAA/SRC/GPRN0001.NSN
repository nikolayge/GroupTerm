* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*************************************************************
* SUBPROGRAM-ID: GPRN0001 - Save policy number on the request
* file on UNIX. File name is assigned based on the process.
*************************************************************
*        MODIFICATION LOG
*************************************************************
* USER   DATE       TAG  REASON
* YAK    06232008       Initial Creation
* YAK    07272010   YK1  Save Order list for Portamedic
*                       (for information only)
* bz     07/16/2014 bz   OI-1611
*************************************************************
*
DEFINE DATA
PARAMETER
1 #PROCESS-NAME  (A) DYNAMIC
1 #ID-NUMBER     (A) DYNAMIC
1 #ORDER-LIST    (A) DYNAMIC OPTIONAL       /* YK1
1 #RESPONSE-CODE (A1)
LOCAL USING GPRL0001
LOCAL
1 #UNIX-FILE-NAME   (A) DYNAMIC
1 #REQUEST-LINE     (A1000)
1 #TAB              (A1) INIT <H'09'>
1 #HEIGHT           (N2)
1 v_Edeliv          (A1)
1 v_Conv            (A1)
1 msg_1             (A49)  INIT <'     Request for printing has been sent'>
1 msg_2             (A54)  INIT <'  *** Package will be printed overnight ***'>
1 #Conversion       (L)
*
END-DEFINE
*
ON ERROR
  #RESPONSE-CODE := '1'
  ESCAPE ROUTINE
END-ERROR
* --------------------------------------
DEFINE WINDOW CONFIRMATION-WINDOW
  SIZE 6 * 50
  BASE 8 / 15
  TITLE 'Confirmation Window'
  FRAMED ON
* --------------------------------------
IF #ORDER-LIST NOT SPECIFIED              /* YK1
  RESET #ORDER-LIST                       /* YK1
END-IF                                    /* YK1
IF #RESPONSE-CODE = '?'                   /* YK2
  RESET #RESPONSE-CODE                    /* YK2
  #Conversion := true                     /* YK2 
END-IF                                    /* YK2
*
IF *DEVICE NE 'BATCH'
  INPUT WINDOW = 'CONFIRMATION-WINDOW' (IP=OFF)
    / '  Choose electronic delivery for package N/Y'  v_Edeliv(AD=MUT EM=X)
  DECIDE ON FIRST VALUE v_Edeliv
    VALUE 'Y'
      msg_1 := '    Email notification will be sent'
      msg_2 := '    for electronic delivery'
    VALUE 'N'
      IGNORE
    NONE REINPUT 'Please choose Y or N'
  END-DECIDE
*
  INPUT WINDOW = 'CONFIRMATION-WINDOW' (IP=OFF)
      msg_1 (AD=MT)
    / msg_2 (AD=MT) 
*    
  IF #Conversion
      INPUT WINDOW = 'CONFIRMATION-WINDOW' (IP=OFF)
        / '  LT/RT Conversion? N/Y'  v_Conv(AD=MUT EM=X)
      DECIDE ON FIRST VALUE v_Conv
        VALUE 'Y','N'
          IGNORE
        NONE REINPUT 'Please choose Y or N'
      END-DECIDE  
  END-IF        
END-IF
*
COMPRESS #UNIX-DIR #PRINT-REQUEST-FILE INTO #UNIX-FILE-NAME LEAVING NO
DEFINE WORK FILE 1 #UNIX-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
COMPRESS #ID-NUMBER #TAB *DATN #TAB #ORDER-LIST #TAB v_Edeliv #TAB v_Conv INTO #REQUEST-LINE LEAVING NO
WRITE WORK FILE 1 #REQUEST-LINE
CLOSE WORK FILE 1

END
