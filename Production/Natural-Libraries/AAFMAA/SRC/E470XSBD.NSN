* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  03012005 DW1  chg name format to last first MI
* DRW  06062005 DW2  Make Alt type A and F use Column PDS form
* TDM  06222005 TM1  Added CGA-USER-COMPANY to identify AFSC BENE form
* DRW  02132006 DW3  Added 'of the insured' to type A
* DRW  09142007 DW4  Reset Invalid Characters for PDF to '*'
* NGG  20121010 NG1  Add Date Of Birth to the form print - 
*                    The way "PDF" screen is filled up is changed
***********************************************************************
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-CLERK       (A3)
1 #CGA-SECURITY-CODE (N2)
1 #CGA-USER-COMPANY  (A1)
*
1 #ISS-DT-OCC     (9)    /* from status file
  2 #ISS-DT         (N6)
  2 #ISS-DT-DISP    (A10)
  2 #SPLIT-IND      (N1)
  2 #STATUS         (A1)
  2 #POL-NUM        (A15)
  2 #POL-BENE-FOUND (L)
  2 #POL-BENE-SEL   (A1)
*
1 #SCR-PRIM-SEC   (2)    /* (prim/alt)
  2 #SCR-DESIG    (A60/5)
  2 REDEFINE #SCR-DESIG
    3 #SCR-DESIG-OCC   (5)
      4 #SCR-DESIG-2   (A60)
      4 REDEFINE #SCR-DESIG-2
        5 #SCR-BN-NAME        (A25)
        5 #SCR-BN-SSN         (N9)
        5 #SCR-BN-REL         (A1)
        5 #SCR-BN-SHARE       (A10)
        5 REDEFINE #SCR-BN-SHARE
          6 #SCR-BN-SHARE-X (A1/10)
        5 #SCR-BN-SET-OPT     (A1)
        5 #SCR-BN-RESTRICTION (A1)
        5 #SCR-BN-DOB         (A8)
        5 #SCR-BN-FILL-1      (A5)
      4 REDEFINE #SCR-DESIG-2
        5 #SCR-DESIG-1ST-PART    (A37)  /* WILL FIT ON NAME BOX
        5 #SCR-DESIG-LST-PART    (A23)
  2 #SCR-TYPE     (A1)
  2 #SCR-SHARE-TYP (A1)
*
1 #DES-SEL         (I2)
1 #SCR-NAME       (A25)
1 #SCR-OWNER      (A35)
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-1A  (A1)
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-6A  (A6)
  2 REDEFINE #SCR-OWNER-6A
    3 #SCR-OWNER-6N (N6)
  2 #SCR-OWNER-AST (A1) /* TO SHOW OWNER IS IN DESIG
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-OCC (5)
    3 #SCR-OWNER-6A-XA (A6)
    3 REDEFINE #SCR-OWNER-6A-XA
      4 #SCR-OWNER-6A-XN (N6)
    3 #SCR-OWNER-SEP    (A1)
*
1 #SCR-OWN-TRNSF-DT-DISP (A10)
1 #DESIG-CNT      (P2)
1 #LST-UPD        (N8)
1 REDEFINE #LST-UPD
  2 #LST-UPD-YYYY  (N4)
  2 #LST-UPD-MM    (N2)
  2 #LST-UPD-DD    (N2)
1 #SCR-BENE-RESTRICT (A1)
1 #SCR-BENE-STATUS   (A12)
1 #TEXT              (A75)
1 #CGA-MF-SCR-COMMAND (A2)
1 REDEFINE #CGA-MF-SCR-COMMAND
  2 #TRNSF-IND    (N1)
  2 #SUCC-FORM    (N1)
1 #ZIP-CODE-PASSED  (N9)
1 #INTRO-FLAG-PASSED  (A1)
1 #LINKED-CN     (N6)
1 REDEFINE #LINKED-CN
  2 #LINKED-CN-A   (A6)
1 #SD-SUFF        (A15)
1 #CONT-ENT   (A25/5)
1 #EDS-DATA-VAL   (A60)
1 #NEW-LAYOUT     (L)
1 #SCR-NEW-LAYOUT (N1)
1 #SCR-COM-DIS    (N2)
1 #SCR-PER-STR    (A1)
*
LOCAL
*
1 #PDF-chars-100    (a100)                      /* DW4
*
1 ST-V VIEW OF A-STATUS   /* For owner
  2 NAME
  2 SSN
*
1 #CN    (N9)
1 REDEFINE #CN
  2 #CN-3   (A3)
  2 #CN-A   (A6)
*
1 #USER     (A14)
1 #MSG     (A75/6)
1 #TRUST-MSG-DONE  (L)
1 #COM-DIS-LBL (A1)
1 #COM-DIS   (A2)
1 #ALL-BENES-DONE   (L)
1 #INVALID  (L)
1 #REL-DISP  (A15)
1 #FIRST-ONE  (L)
1 #NAME      (A25)
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #MI        (A1)
1 #ERR-MSG      (A60)
1 #FILL-BLNKS (A78/24)
1 #BENE-LINE  (A60)
1 REDEFINE #BENE-LINE
  2 #BENE-LINE-X  (A1/60)
*
1 #PENDING    (L)
1 #HAS-BEEN-CONVERTED (L)
1 #FOUND-AST   (L)
1 #FOUND-SLASH (L)
1 #FREE-FORM   (L/2)
1 #WILL-FIT    (L/2)  /* WILL FIT IN NAME BOX
*
1 #BENE-DATA   (A60)
1 REDEFINE #BENE-DATA
  2 #BENE-DATA-X   (A1/60)
1 REDEFINE #BENE-DATA
  2 #BENE-DATA-N1   (N1)
1 REDEFINE #BENE-DATA
  2 #BENE-DATA-N2   (N2)
1 REDEFINE #BENE-DATA
  2 #BENE-DATA-N3   (N3)
1 REDEFINE #BENE-DATA
  2 #BENE-DATA-SSN1 (A3)
  2 #BENE-DATA-SSN2 (A2)
  2 #BENE-DATA-SSN3 (A4)
*
1 #BENE-NAME     (A60)   /* KEC has changed to overlay to rest of line
1 #BENE-REL      (A1)
1 #BENE-REL-DISP (A15)
1 #BENE-SSN      (A11)
1 #BENE-SHARE    (A5)
1 #BENE-SET-OPT  (A1)
1 #BENE-RESTRICT (A1)
1 #SCR-OWNER-NAME  (A25)
1 #SCR-OWNER-SSN   (A11)
1 #P-S           (A1)
1 #P-#           (I2)
1 #S-#           (I2)
1 #P-S-#         (I2)
*
1 #CGA-MASTER
  2 #CGA-SCR-COMMAND-X              (A2)
  2 #CGA-SCR-ID-X                   (N9)
  2 #CGA-SCR-SUFFIX-X               (A3)
  2 #CGA-ORIG-COMMAND-X             (A2)
  2 #CGA-ORIG-ID-X                  (N6)
  2 #CGA-ORIG-SUFFIX-X              (A3)
  2 #CGA-CLERK-X                    (A3)
  2 #CGA-SECURITY-CODE-X            (N2)
  2 #CGA-USER-COMPANY-X             (A1)
  2 #CGA-ID-G                       (A6)
  2 #CGA-ISN                        (P8)
  2 #CGA-GOOD-TAS                   (L)
  2 #CGA-GOOD-TAS-VS                (L)
  2 #CGA-MF-SCR-COMMAND-X           (A2)
  2 #CGA-MF-SCR-PARM-1              (A9)
  2 #CGA-MF-SCR-PARM-2              (A5)
  2 #CGA-MF-ORIG-COMMAND            (A2)
  2 #CGA-MF-ORIG-PARM-1             (A9)
  2 #CGA-MF-ORIG-PARM-2             (A5)
  2 #cga-policy-number              (a16) /* 03/30/2016 ticket 4391
*
1 #SCR-LINES-CV   (C/57)
1 #SCR-LINES      (A78/57)
1 REDEFINE #SCR-LINES
  2 #SCR-LINES-OCC     (57)
    3 #SCR-LINES-FIL    (A7)
    3 #SCR-LINES-INDENT (A71)
    3 REDEFINE #SCR-LINES-INDENT
      4 #SCR-LINES-FIL-2       (A39)
      4 #SCR-LINES-SIGNATURE   (A32)
    3 REDEFINE #SCR-LINES-INDENT
      4 #SCR-LINES-FIL-3       (A65)
      4 #SCR-LINES-LST-6       (A6)
1 #SCR-POLS       (A60)
1 #SCR-POLS-2     (A30)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY       (N4)
  2 #MM         (N2)
  2 #DD         (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
*
1 #TODAY-MMDDYYYY-DISP  (A10)
*
1 #MONTHS (A36)  INIT
  <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
*
1 #POL        (A14)
1 #CNT        (P2)
1 #SCR-X      (I2)
1 #OWN-X      (I2)
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #I4         (I2)
1 #I5         (I2)
1 #I6         (I2)
1 #HOLD-ID     (N9)
1 #HOLD-SUFFIX (A3)
1 #MANUAL      (L)
1 #TRUSTEEZ   (A10)
1 #OWNERZ     (A8)
1 #I  (A2)  /* or we
1 #IC (A2)  /* or We
1 #AM (A3)  /* or are
1 #MY (A3)  /* or our
1 #ME (A3)  /* or us
1 #WP         (L)
1 #SIGNATURE  (A20)
1 REDEFINE #SIGNATURE
  2 #SIGNATURE-FIL (A13)
  2 #SIGNATURE-SIG (A7)
1 #SIG-LINES (P3)
1 #DEF-CMP   (L)
1 #SCR-MBR   (A25)
1 #CELL  (A)   DYNAMIC     /* NG1  start
1 #STACK (A/9) DYNAMIC
1 #STACK_P (I2)
1 J        (I2)
1 L        (I2)            /* NG1 end
*
1 XX (A40) INIT <'%FDF-1.2 1 0 obj << /FDF << /Fields [' >
END-DEFINE
* * * * * * * * * * * * * * * * *
*
IF #SCR-BENE-STATUS = 'PENDING'
  MOVE TRUE TO #PENDING
END-IF
*
IF #SCR-NEW-LAYOUT = 1
  MOVE TRUE TO #HAS-BEEN-CONVERTED
END-IF
*
IF NOT #PENDING OR NOT #HAS-BEEN-CONVERTED
    OR (#CGA-SCR-SUFFIX = 'BLK')
  RESET #I1
*
  IF #CGA-SCR-SUFFIX = 'BLK'
    ADD 1 TO #I1
    MOVE 'This will be a "blank" designation with no beneficiaries'
      TO #MSG (#I1)
  ELSE
    IF NOT #PENDING
      ADD 1 TO #I1
      MOVE 'This is not a PENDING Designation and has been Finalized'
        TO #MSG (#I1)
    END-IF
*
    IF NOT #HAS-BEEN-CONVERTED
      ADD 1 TO #I1
      MOVE 'This Designation has not been converted to the new layout'
        TO #MSG (#I1)
    END-IF
  END-IF
  ADD 1 TO #I1
  MOVE '  Press enter to continue' TO #MSG (#I1)
*
  WRITE //
  FOR #I1 1 TO 6
    WRITE // #MSG (#I1)
  END-FOR
END-IF
*
MOVE #CGA-SCR-ID TO #CN    /* initialize owner as insured
FIND ST-V WITH MEMBER-KEY = #CN-A
  MOVE ST-V.NAME TO #NAME
  RESET #LAST #MI #FIRST-20
  CALLNAT 'GFMLNSXX' /* split name
    #NAME
    #FIRST-20
    #MI
    #LAST
    #ERR-MSG
  MOVE #NAME TO #SCR-OWNER-NAME   /* DW1
  MOVE EDITED ST-V.SSN (EM=999-99-9999) TO #SCR-OWNER-SSN
END-FIND
*
IF #SCR-OWNER-6N = MASK (NNNNNN)
  FIND ST-V WITH MEMBER-KEY = #SCR-OWNER-6A
*
    MOVE ST-V.NAME TO #NAME
    RESET #LAST #MI #FIRST-20
    CALLNAT 'GFMLNSXX' /* split name
      #NAME
      #FIRST-20
      #MI
      #LAST
      #ERR-MSG
    MOVE #NAME TO #SCR-OWNER-NAME   /* DW1
    MOVE EDITED SSN (EM=999-99-9999) TO #SCR-OWNER-SSN
  END-FIND
END-IF
*
IF #SCR-COM-DIS NE 0
  MOVE 'Y' TO #COM-DIS-LBL
  MOVE EDITED #SCR-COM-DIS (EM=Z9) TO #COM-DIS
END-IF
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #USER WITH '/'
COMPRESS #CGA-CLERK #USER INTO #USER
*
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER ')>>'  INTO #FILL-BLNKS (2) LEAVING NO
*
#I6 := 3 /* Dynamic map area start
RESET L #STACK_P
*
MOVE '<</T(PolNumber)/V(' TO #CELL
MOVE TRUE TO #FIRST-ONE
FOR #I1 1 TO 9
  IF  (#POL-NUM (#I1) = ' ')
      OR (#POL-BENE-SEL (#I1) NE 'X')
    ESCAPE TOP
  END-IF
  IF NOT #FIRST-ONE
    COMPRESS #CELL ',' INTO #CELL LEAVING NO
  END-IF
  COMPRESS #CELL #POL-NUM(#I1) INTO #CELL LEAVING NO
  RESET #FIRST-ONE
END-FOR
COMPRESS #CELL ')>>' INTO #CELL LEAVING NO
PERFORM PUSH-CELL
*
IF #CGA-SCR-SUFFIX = 'BLK'
  RESET #SCR-PER-STR #COM-DIS-LBL #COM-DIS
END-IF
*
COMPRESS '<</T(InsuredName)/V(' #SCR-NAME ')>>'  INTO #CELL LEAVING NO
PERFORM PUSH-CELL
COMPRESS '<</T(OwnerName)/V(' #SCR-OWNER-NAME ')>>'  INTO #CELL LEAVING NO
PERFORM PUSH-CELL
COMPRESS '<</T(OwnerSSN)/V(' #SCR-OWNER-SSN ')>>'  INTO #CELL LEAVING NO
PERFORM PUSH-CELL
COMPRESS '<</T(PerStirpes)/V(' #SCR-PER-STR ')>>'
  '<</T(Disaster)/V(' #COM-DIS-LBL ')>>'
  '<</T(Days)/V(' #COM-DIS ')>>'
  INTO #CELL LEAVING NO
PERFORM PUSH-CELL
*
MOVE TRUE TO #WILL-FIT (*)
IF #CGA-SCR-SUFFIX NE 'BLK'   /* blk = Blank pdf form(no beneficiaries)
  FOR #I1 1 TO 2
    RESET #ALL-BENES-DONE
    RESET #TRUST-MSG-DONE
*
    IF  (#SCR-TYPE (#I1) = 'I' OR = 'F' OR = 'T' OR = 'C' OR = 'P')
        OR (NOT #HAS-BEEN-CONVERTED)
      MOVE TRUE TO #FREE-FORM (#I1)
    END-IF
*
    FOR #I2 1 TO 5
      IF (#I1 = 1) OR ((#I1 = 2) AND (#I2 NE 5))
        IF  (#SCR-DESIG-LST-PART (#I1, #I2) NE ' ')
            OR (#SCR-TYPE (#I1) = 'T') /* filled info will not fit
          RESET #WILL-FIT (#I1)
        END-IF
      END-IF
*
      IF (#I2 = 3) AND (#SCR-TYPE (#I1) = 'P')
          AND NOT #TRUST-MSG-DONE
        MOVE TRUE TO #TRUST-MSG-DONE
        SUBTRACT 1 FROM #I2   /* to insert a line
        MOVE 'or successor(s) as trustee(s) of:'
          TO #SCR-DESIG (#I1, #I2)
      END-IF
*
      RESET #BENE-NAME #BENE-REL #BENE-SSN #BENE-SHARE #BENE-SET-OPT
        #BENE-RESTRICT
*
      IF ((#I1 = 2) AND (#SCR-TYPE (2) = 'A')  /* type A condition
          AND (#SCR-BN-NAME (#I1, #I2) = ' '))
          OR  ((#I1 = 1) AND (#SCR-TYPE (1) ='F')) /* final
          OR  (#SCR-TYPE (#I1) ='T')   /* testamentary
*
        IF #ALL-BENES-DONE
          ESCAPE BOTTOM
        END-IF
*
        MOVE 'N/A' TO #BENE-SSN
*
        IF (#I1 = 1) AND (#SCR-TYPE (1) ='F')  /* type F condition
          MOVE 'the Owner"s ESTATE' TO #BENE-NAME
          MOVE TRUE TO #ALL-BENES-DONE
        END-IF
*
        IF (#SCR-TYPE (#I1) = 'T') AND (#I2 = 1) /* Testamentary
          COMPRESS 'The TRUSTEE of the Trust established under terms'
            'of my Last'  INTO #BENE-NAME
        END-IF
*
        IF (#SCR-TYPE (#I1) = 'T') AND (#I2 = 2) /* Testamentary
            AND (#SCR-TYPE (#I1) ='T')  /* type T condition
          COMPRESS 'Will and Testament duly admitted to probate and'
            'recorded as'     INTO #BENE-NAME
        END-IF
*
        IF (#SCR-TYPE (#I1) = 'T') AND (#I2 = 3) /* Testamentary
            AND (#SCR-TYPE (#I1) ='T')  /* type T condition
          MOVE     'such.' TO #BENE-NAME
          MOVE TRUE TO #ALL-BENES-DONE
        END-IF
*
        IF (#I1 = 2) AND (#SCR-TYPE (2) = 'A') /* type A condition
            AND (#SCR-BN-NAME (#I1, #I2) = ' ')
*         MOVE 'All children, born or adopted'
          MOVE 'All children of the insured (born or adopted)' /* DW3
            TO #BENE-NAME
          MOVE TRUE TO #ALL-BENES-DONE
        END-IF
      ELSE
        IF  (#SCR-BN-NAME (#I1, #I2) = ' ')
*        OR ((#I1 = 2) AND (#I2 = 5)) /* 5th line always unusable
          ESCAPE TOP
        END-IF
*
        IF #FREE-FORM (#I1)
          move #scr-desig (#i1, #i2) to #pdf-chars-100          /* DW4
          callnat 'GPDFRSXX' #pdf-chars-100
          MOVE #pdf-chars-100 TO #BENE-NAME                      /* DW4
        ELSE
          move #scr-bn-name (#i1, #i2) to #pdf-chars-100         /* DW4
          callnat 'GPDFRSXX' #pdf-chars-100
          MOVE #pdf-chars-100 TO #NAME                           /* DW4
*
          RESET #LAST #MI #FIRST-20
          CALLNAT 'GFMLNSXX' /* split name
            #NAME
            #FIRST-20
            #MI
            #LAST
            #ERR-MSG
*         COMPRESS #FIRST-20 #MI #LAST INTO #BENE-NAME
          MOVE #NAME TO #BENE-NAME   /* DW1
          MOVE #SCR-BN-REL  (#I1, #I2) TO #BENE-REL
          MOVE #SCR-BN-SET-OPT (#I1, #I2) TO #BENE-SET-OPT
          MOVE #SCR-BN-RESTRICTION (#I1, #I2) TO #BENE-RESTRICT
          MOVE #SCR-BN-SHARE       (#I1, #I2) TO #BENE-SHARE
*
          IF #SCR-BN-SSN (#I1, #I2) NE 0
            MOVE EDITED #SCR-BN-SSN (#I1, #I2) (EM=999-99-9999)
              TO #BENE-SSN
          END-IF
        END-IF
      END-IF
*
      RESET #BENE-REL-DISP
*
      IF   (#BENE-REL NE ' ')
          AND (NOT #FREE-FORM (#I1))
        CALLNAT 'GRELASXX' #BENE-REL
          #REL-DISP
          #INVALID
          #ERR-MSG
        MOVE LEFT #REL-DISP TO #BENE-REL-DISP
      END-IF
*
      IF #I1 = 1
        MOVE 'P' TO #P-S
        ADD 1 TO #P-#
        MOVE #P-# TO #P-S-#
      ELSE
        MOVE 'S' TO #P-S
        ADD 1 TO #S-#
        MOVE #S-# TO #P-S-#
      END-IF
*
      COMPRESS '<</T(' #P-S #P-S-# 'Nam)/V(' #BENE-NAME ')>>' INTO #CELL LEAVING NO
      PERFORM PUSH-CELL
*
      IF   (NOT #FREE-FORM (#I1))
          AND (#BENE-REL-DISP NE ' ')
        COMPRESS '<</T(' #P-S #P-S-# 'Rel)/V(' #BENE-REL-DISP ')>>' INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
      END-IF
*
      IF   (NOT #FREE-FORM (#I1))
          AND ((#BENE-SSN     NE ' ') OR (#BENE-SHARE    NE ' ')
          OR (#BENE-SET-OPT NE ' ') OR (#BENE-RESTRICT NE ' '))
        COMPRESS '<</T(' #P-S #P-S-# 'N)/V(' #BENE-SSN ')>>'  INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
        COMPRESS '<</T(' #P-S #P-S-# '%)/V(' #BENE-SHARE ')>>' INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
        COMPRESS  '<</T(' #P-S #P-S-# 'S)/V(' #BENE-SET-OPT ')>>' INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
        COMPRESS '<</T(' #P-S #P-S-# 'R)/V(' #BENE-RESTRICT ')>>' INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
      END-IF
*
      IF NOT #FREE-FORM (#I1) AND #SCR-BN-DOB (#I1, #I2) NE ' '
        MOVE SUBSTR (#SCR-BN-DOB (#I1, #I2),1,2) TO #MM-A
        MOVE SUBSTR (#SCR-BN-DOB (#I1, #I2),3,2) TO #DD-A
        MOVE SUBSTR (#SCR-BN-DOB (#I1, #I2),5,4) TO #YYYY-A
        COMPRESS '<</T(' #P-S #P-S-# 'B)/V('
          #MM-A '/'
          #DD-A '/'
          #YYYY-A
          ')>>'
          INTO #CELL LEAVING NO
        PERFORM PUSH-CELL
      END-IF
    END-FOR
  END-FOR
END-IF
*
IF #I6 = 22
  PERFORM PASS-MESSAGE
END-IF
*
ADD 78 TO L   /* FORCE POP
PERFORM PUSH-CELL
PERFORM CREATE-EOM
*
MOVE 'BD' TO #CGA-SCR-COMMAND
RESET #CGA-SCR-SUFFIX
FETCH 'G1000PXX'
*                    NG1 CREATED
DEFINE PASS-MESSAGE
/*     ============
PERFORM CREATE-EOM
RESET #FILL-BLNKS (*)
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER ')>>'  INTO #FILL-BLNKS (2) LEAVING NO
#I6 := 3 /* Dynamic map area start
END-SUBROUTINE
*                    NG1 CREATED
DEFINE CREATE-EOM
/*     ==========
* ADD 1 TO #I6
IF  ((#SCR-TYPE (1) = 'B' OR = 'S')
    AND (#SCR-TYPE (2) = 'B' OR = 'S' OR = ' ' OR = 'A' OR = 'F') /* DW2
    AND #NEW-LAYOUT)
    OR (#WILL-FIT (1) AND #WILL-FIT (2)) /* both will fit in name
  MOVE '] /F (F:\BENEFICIARY DESIGNATION.PDF)>> >> endobj'
    TO #FILL-BLNKS (#I6)
ELSE
  MOVE '] /F (F:\BENEFICIARY DESIGNATION NOCOLUMNS.PDF)>> >> endobj'
    TO #FILL-BLNKS (#I6)
END-IF
*
IF #CGA-USER-COMPANY = 'S' OR #CGA-CLERK = 'TDM'              /* TM1
  MOVE '] /F (F:\PDF\BENEFICIARY DESIGNATION.PDF)>> >> endobj'
    TO #FILL-BLNKS (#I6)
END-IF
*
ADD 1 TO #I6
MOVE'trailer <</Root 1 0 R >> %%EOF'  TO #FILL-BLNKS  (#I6)
*
INPUT USING MAP 'GPDFXMXX'
END-SUBROUTINE
*                    NG1 CREATED
DEFINE PUSH-CELL
/*     ---------
ADD *LENGTH (#CELL) TO L
IF L > 78
  IF #I6 = 22
    PERFORM PASS-MESSAGE
  END-IF
  FOR J 1 TO #STACK_P
    COMPRESS #FILL-BLNKS (#I6) #STACK (J) INTO #FILL-BLNKS (#I6) LEAVING NO SPACE
  END-FOR
  ADD 1 TO #I6
  #STACK_P := 1
  #STACK ( #STACK_P ) := #CELL
  L := *LENGTH (#CELL)
ELSE
  ADD 1 TO #STACK_P
  #STACK ( #STACK_P ) := #CELL
END-IF
END-SUBROUTINE
*
END
