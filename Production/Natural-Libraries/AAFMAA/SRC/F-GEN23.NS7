* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-GEN23                                               *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Check over age 23 generational insurance reqs         *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE       TAG     REASON                                     *
* PAM    12/24/2009 None    Program Written                            *
************************************************************************
*
DEFINE FUNCTION FUNCTION-GEN23-INSURE RETURNS (L)
*
DEFINE DATA
*
PARAMETER
*
1 #ID-NUMBER                  (A6)
*
LOCAL USING OBJLSTAT
*
LOCAL
*
1 #WORK-ID-NUMBER             (N6)
1 REDEFINE #WORK-ID-NUMBER
 2 #WORK-ID-NUMBER-A          (A6)
*
1 #ISN                        (P8)
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-GEN23-INSURE #ISN
*
IF #ID-NUMBER = '098061'
    FUNCTION-GEN23-INSURE := TRUE
    ESCAPE ROUTINE
END-IF

FIND (1) ST-V WITH MEMBER-KEY = #ID-NUMBER
  IF NO RECORDS FOUND
    RESET #WORK-ID-NUMBER
    ESCAPE BOTTOM
  END-NOREC
*
  #WORK-ID-NUMBER := LINKED-CN (1)
END-FIND
*
IF #WORK-ID-NUMBER = 0
  ESCAPE ROUTINE
END-IF
*
FIND (1) ST-V WITH MEMBER-KEY = #WORK-ID-NUMBER-A
  IF NO RECORDS FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #WORK-ID-NUMBER-A
      #ISN := *ISN
       ESCAPE BOTTOM
    END-FIND
  END-NOREC
*
  #ISN := *ISN
END-FIND
*
IF #ISN = 0
  ESCAPE ROUTINE
ELSE
  GET ST-V #ISN
END-IF
*
DECIDE FOR FIRST CONDITION
  WHEN ST-V.DATE-OF-DEATH NE 0
    IF ST-V.NAME-SP NE ' '
     AND ST-V.DATE-OF-DIVORCE = 0
     AND ST-V.STATUS-CODE-SP NE 'D'
      FUNCTION-GEN23-INSURE := TRUE
    END-IF
  WHEN NONE
    IF ST-V.NUMBER-MEMBERSHIPS NE 0
      FUNCTION-GEN23-INSURE := TRUE
    END-IF
END-DECIDE
*
END-FUNCTION
*
END
