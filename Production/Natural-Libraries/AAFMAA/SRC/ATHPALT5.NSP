* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPALT5 - compare dfas start/stop with my start/stop
*
DEFINE DATA
LOCAL
*
1 DFAS-STARTS-STOPS-REC  (A80)
1 REDEFINE DFAS-STARTS-STOPS-REC
 2 DSS-CN           (N6)
 2 DSS-SERVICE      (A1)   /* A or F
 2 DSS-SSN          (A9)
 2 DSS-TYPE         (A1)   /* 1 = stop / 2 = start
 2 DSS-REAS-CODE    (A2)
 2 DSS-STATUS       (A1)   /* A or R
 2 DSS-BLANKET-CODE (A3)
 2 DSS-EFF-DATE     (A4)   /* YYMM
 2 DSS-TERM-DATE    (A4)   /* YYMM
 2 DSS-AMOUNT       (P9.2) /* takes 6 bytes
 2 DSS-NAME         (A20)
 2 DSS-FILL-1       (A14)
*
1 AAFM-STARTS-STOPS-REC  (A80)
1 REDEFINE AAFM-STARTS-STOPS-REC
 2 ASS-CN           (N6)
 2 ASS-SERVICE      (A1)   /* A or F
 2 ASS-SSN          (A9)
 2 ASS-TYPE         (A1)   /* 1 = stop / 2 = start
 2 ASS-REAS-CODE    (A2)
 2 ASS-STATUS       (A1)   /* A or R
 2 ASS-BLANKET-CODE (A3)
 2 ASS-EFF-DATE     (A4)   /* YYMM
 2 ASS-TERM-DATE    (A4)   /* YYMM
 2 ASS-AMOUNT       (P9.2) /* takes 6 bytes
 2 ASS-NAME         (A20)
 2 ASS-FILL-1       (A14)
*
1 #COUNTERS
 2 #RECS-READ        (P5)
 2 #RECS-SELECTED    (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #CNT-5            (P5)
 2 #CNT-6            (P5)
 2 #CNT-7            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY          (N4)
  2 #MM            (N2)
  2 #DD            (N2)
*
1 #TOT-STOPS        (N5)
1 #TOT-STARTS       (N5)
1 #TOT-ST-SP-RECS   (N5)
1 #AA-RECS      (N5)
1 #FA-RECS      (N5)
1 #XR-RECS      (N5)
1 #DFAS-IN      (P5)
1 #AAFM-IN      (P5)
*
1 #DFAS-SSN  (A9)
1 #AAFM-SSN  (A9)
1 #CURR-MON-IN   (P5)
1 #LAST-MON-IN   (P5)
1 #STARTS        (P5)
1 #STOPS         (P5)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA Start/Stop but no DFAS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Duplicate DFAS'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'DFAS Start/Stop but no AAFMAA'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
PERFORM READ-DFAS
PERFORM READ-AAFM
*
REPEAT UNTIL (#DFAS-SSN = '999999999')
         AND (#AAFM-SSN = '999999999')
  IF #DFAS-SSN LT #AAFM-SSN
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4 DSS-SSN DSS-AMOUNT DSS-TYPE
    PERFORM READ-DFAS
    ESCAPE TOP
  END-IF
*
  IF #DFAS-SSN GT #AAFM-SSN
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ASS-SSN ASS-AMOUNT ASS-TYPE
    PERFORM READ-AAFM
    ESCAPE TOP
  END-IF
*
  IF DSS-AMOUNT NE ASS-AMOUNT
    WRITE (4) 'Match but amounts not equal' DSS-SSN DSS-AMOUNT
        ASS-AMOUNT
  END-IF
  PERFORM READ-DFAS
  PERFORM READ-AAFM
END-REPEAT
*
WRITE #DFAS-IN  '= DFAS in'
WRITE #AAFM-IN  '= AAFM in'
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-DFAS
* * * * * * * * * * * * * * * * *
*
IF #DFAS-SSN = '999999999'
  ESCAPE ROUTINE
END-IF
*
READ WORK 2 ONCE RECORD DFAS-STARTS-STOPS-REC
  AT END
    MOVE '999999999' TO #DFAS-SSN
    ESCAPE ROUTINE
  END-ENDFILE
*
ADD 1 TO #DFAS-IN
*
* IF DSS-SSN = #DFAS-SSN
*   WRITE (4) 'DFAS duplicate recs' DSS-SSN DSS-AMOUNT
* END-IF
*
MOVE DSS-SSN TO #DFAS-SSN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-AAFM
* * * * * * * * * * * * * * * * *
*
IF #AAFM-SSN = '999999999'
  ESCAPE ROUTINE
END-IF
*
READ WORK 3 ONCE RECORD AAFM-STARTS-STOPS-REC
  AT END
    MOVE '999999999' TO #AAFM-SSN
    ESCAPE ROUTINE
  END-ENDFILE
*
ADD 1 TO #AAFM-IN
*
* IF ASS-SSN = #AAFM-SSN
*   WRITE (4) 'AAFM duplicate recs' ASS-SSN ASS-AMOUNT
* END-IF
MOVE ASS-SSN TO #AAFM-SSN
*
END-SUBROUTINE
*
END
