* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1001SAT   APPL-SOURCE STATS
DEFINE DATA
PARAMETER
1 #TYPE              (N1)
1 #PROSP             (L)
1 #HOLD-SOURCE-CODE  (A6)
1 #STAT-FOUND        (L)
1 #AP-INDEX          (I2)
*
1 #TXN-MSSG              (A78)
1 REDEFINE #TXN-MSSG
 2 #TXN-COMPLETE-MESSAGE (A9)
 2 #TXN-MID-30           (A30)
 2 #TXN-LST-39           (A39)
*
1 #HOLD-ACTION-FLAG (A1)
1 #MED-REQT-FLAG    (L)
1 #CONV-FLAG        (L)
1 #HOLD-MONTH-KEY   (N6)
*
1 ACTION-FLAG       (A1)
1 MEMBER-CN         (A6)
1 CAP-IND           (A1)
1 FACE-AMT-UNITS    (N4)
*
LOCAL
*
1 SOA-V VIEW OF A-SOURCE-OF-APPL
 2 SOURCE-CODE-GROUP     (A1)
 2 SOURCE-CODE           (A6)
 2 SOURCE-CUR-NEW-MBR    (P5/12)
 2 SOURCE-CUR-ADDL-MBR   (P5/12)
 2 SOURCE-CUR-INQUIRIES  (P5/12)
*
1 MS-V VIEW OF A-MBRSHIP-STATS
 2 MONTH-KEY              (N6)
 2 APPL-REJ-NEW-MBRS      (P5)
 2 APPL-REJ-ADDL-MBRS     (P5)
 2 APPL-REJ-UNITS         (P5)
*
1 #SOURCE-CODE-FOUND   (L)  /* PM - 06/2000 - SPLIT
1 #ADDL          (L)
1 #SRC-GROUP     (A1)
1 #I1  (I2)
1 #I2  (I2)
*
1 #SUM-CODE  (A6)
1 REDEFINE #SUM-CODE
 2 #SUM-CODE1  (A3)
 2 #SUM-CODE2  (A1)
*
END-DEFINE
******************
*
IF #TYPE = 1    /* new soa inq update
  PERFORM UPDATE-NEW
ELSE
  IF #TYPE = 2    /* misc soa inq update
    PERFORM UPDATE-MISC
  END-IF
END-IF
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-NEW
* * * * * * * * * * * * * *
*
IF #PROSP
* UPDATE SOURCE CODE RECORD FOR NEW INQUIRIES
*
  MOVE FALSE TO #SOURCE-CODE-FOUND
  FIND SOA-V WITH SOURCE-CODE = #HOLD-SOURCE-CODE  /* PM -06/2000-SPLIT
    IF NO RECORDS FOUND
      MOVE 'CALL EDS/SOURCE ERROR' TO #TXN-LST-39
      ESCAPE ROUTINE
    END-NOREC
*
    IF SOA-V.SOURCE-CODE-GROUP = MASK (A)
      MOVE TRUE TO #SOURCE-CODE-FOUND
    ELSE
      ESCAPE TOP
    END-IF
*
    ADD 1 TO SOURCE-CUR-INQUIRIES(#AP-INDEX)
    MOVE SOURCE-CODE-GROUP TO #SRC-GROUP
    MOVE 'EDS' TO #SUM-CODE1
*
    IF SOURCE-CODE-GROUP = 'N' OR = 'Y' OR = 'A'
      MOVE 'P' TO #SUM-CODE2
    ELSE
      MOVE SOURCE-CODE-GROUP TO #SUM-CODE2
    END-IF
*
    UPDATE (0076)
*
    ESCAPE BOTTOM
  END-FIND
*
  IF NOT #SOURCE-CODE-FOUND
    MOVE 'CALL EDS/SOURCE ERROR' TO #TXN-LST-39
    ESCAPE ROUTINE
  END-IF
*
* UPDATE SOURCE CODE SUMMARY RECORD FOR INQUIRIES
*
 FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
*
   IF NO RECORDS FOUND
     MOVE 'CALL EDS/SUMM. ERROR' TO #TXN-LST-39
     ESCAPE ROUTINE
   END-NOREC
*
   ADD 1 TO SOURCE-CUR-INQUIRIES(#AP-INDEX)
   UPDATE (0110)
 END-FIND
* UPDATE NEW INQUIRY RECORD
*
 IF #SRC-GROUP = 'M' OR = 'Z' /*CHGD FROM Q ON 3/96 AS PER LM
   ESCAPE ROUTINE             /* Z added 11/98
 END-IF
*
 MOVE 'EDSINW' TO #SUM-CODE
 FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
   IF NO RECORDS FOUND
     MOVE 'CALL EDS/INW ERROR' TO #TXN-LST-39
     ESCAPE BOTTOM
   END-NOREC
*
   ADD 1 TO SOURCE-CUR-INQUIRIES(#AP-INDEX)
   UPDATE (0127)
 END-FIND
ELSE
  MOVE 'EDSANE' TO #SUM-CODE  /* NEW APPL
  FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
    IF NO RECORDS FOUND
      MOVE 'CALL EDS/ANE ERROR' TO #TXN-LST-39
      ESCAPE BOTTOM
    END-NOREC
*
    IF #STAT-FOUND
      ADD 1 TO SOURCE-CUR-ADDL-MBR(#AP-INDEX)
    ELSE
      ADD 1 TO SOURCE-CUR-NEW-MBR(#AP-INDEX)
    END-IF
*
    UPDATE (0138)
  END-FIND
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE UPDATE-MISC
* * * * * * * * * * * * * *
*
IF   (ACTION-FLAG NE #HOLD-ACTION-FLAG)
 AND (ACTION-FLAG = 'W')
  PERFORM TEST-REJECT
END-IF
*
IF #MED-REQT-FLAG
* UPDATE MED REQT RECORD
  IF MEMBER-CN = '      '
    MOVE FALSE TO #ADDL
  ELSE
    MOVE TRUE TO #ADDL
  END-IF
*
  MOVE 'EDSAMR' TO #SUM-CODE
  FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
    IF NO RECORDS FOUND
      MOVE 'CALL EDS/AMR ERROR' TO #TXN-LST-39
      ESCAPE BOTTOM
    END-NOREC
*
    IF #ADDL
      ADD 1 TO SOURCE-CUR-ADDL-MBR(#AP-INDEX)
    ELSE
      ADD 1 TO SOURCE-CUR-NEW-MBR(#AP-INDEX)
    END-IF
*
    UPDATE (0174)
  END-FIND
END-IF
*
IF #CONV-FLAG
*  IF APPL-SOURCE NE MASK ('CAP') /* cap stuff added 10/1/96 as per hlm
   IF CAP-IND NE 'Y'              /* cap stuff added 10/1/96 as per hlm
     MOVE 'EDSIAC' TO #SUM-CODE
* UPDATE INQUIRY TO APPLICATION CONVERSION RECORD
     FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
     IF NO RECORDS FOUND
         MOVE 'CALL EDS/IAC ERROR' TO #TXN-LST-39
         ESCAPE BOTTOM
     END-NOREC
     ADD 1 TO SOURCE-CUR-NEW-MBR(#AP-INDEX)
     UPDATE (0195)
     END-FIND
   END-IF
*
   MOVE 'EDSANE' TO #SUM-CODE
* UPDATE NEW APPLICATION RECORD
   FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
   IF NO RECORDS FOUND
       MOVE 'CALL EDS/ANE ERROR' TO #TXN-LST-39
       ESCAPE BOTTOM
   END-NOREC
   ADD 1 TO SOURCE-CUR-NEW-MBR(#AP-INDEX)
   UPDATE (0207)
   END-FIND
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE TEST-REJECT
* * * * * * * * * * * * * * * * *
*
IF MEMBER-CN = '      '
  ASSIGN #ADDL = FALSE
ELSE
  ASSIGN #ADDL = TRUE
END-IF
*
FIND (1) MS-V WITH MONTH-KEY = #HOLD-MONTH-KEY
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
*
  IF #ADDL
    ADD 1 TO APPL-REJ-ADDL-MBRS
  ELSE
    ADD 1 TO APPL-REJ-NEW-MBRS
  END-IF
*
  COMPUTE APPL-REJ-UNITS = APPL-REJ-UNITS + (FACE-AMT-UNITS / 5)
  UPDATE (0229)
END-FIND
*
*  UPDATE APPLICATIONS REJECTED RECORD
MOVE 'EDSARJ' TO #SUM-CODE
*
FIND (1) SOA-V WITH SOURCE-CODE = #SUM-CODE
  IF NO RECORDS FOUND
    MOVE 'CALL EDS/ARJ ERROR' TO #TXN-LST-39
    ESCAPE ROUTINE
  END-NOREC
*
  IF #ADDL
    ADD 1 TO SOURCE-CUR-ADDL-MBR(#AP-INDEX)
  ELSE
    ADD 1 TO SOURCE-CUR-NEW-MBR(#AP-INDEX)
  END-IF
*
  UPDATE (0247)
END-FIND
*
END-SUBROUTINE
*
END
