* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GCSSNSXX
* FUNCTION: Checks whether SSN of Depn/Other already exists
***********************************************************
*        MODIFICATION LOG
***********************************************************
* USER   DATE       TAG  REASON
* RSE    04/15/2010      Creation
* RSE    06/22/2011 RE1  Add contact type
* RSE    07/26/2011 RE2  Add organization name
********************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-ORIG-ID      (N6)
1 #CONTACT-ID       (N8)
1 #ROLE             (A1)
1 #SSN              (N9)
1 #FIRST-NAME       (A25)
1 #MIDDLE-NAME      (A25)
1 #LAST-NAME        (A25)
1 #SUFFIX           (A10)
1 #ORG-NAME         (A75)   /* RE2
1 #SEX              (A1)
1 #DOB              (A8)
1 #BCC              (A1)
1 #DOD              (A8)
1 #DCC              (A1)
1 #DOM              (A8)
1 #DIV              (A8)
1 #CN               (N6)
1 #LST-UPD          (A10)
1 #LST-USR          (A22)
1 #NOT-FOUND        (L)
1 #PF3              (L)
1 #SRCH             (L)
LOCAL
*
1 CONT-SSN VIEW OF A-CONTACTS
  2 CONTACT-ID       (N8)           /* D
  2 ID-NUMBER        (N6)           /* D
  2 CONTACT-TYPE     (A1)   /* RE1
  2 FIRST-NAME       (A25)
  2 MIDDLE-NAME      (A25)
  2 LAST-NAME        (A25)
  2 SUFFIX           (A10)
  2 GENDER-CD        (A1)         /* --NA
  2 DATE-OF-BIRTH    (N8)
  2 SSN              (N9)           /* D
  2 DATE-OF-DEATH    (N8)
  2 BIRTH-CERT-CODE  (A1)
  2 DEATH-CERT-CODE  (A1)
  2 FIRM-ORGANIZATION-NAME(A75) /* RE1
  2 LAST-DATE-UPD    (N8)
  2 LAST-USER-UPD    (A8)
*
1 CONT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
*
1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER
  2 SPOUSE-CONTACT-ID
  2 FORMER-SPOUSE-CONTACT-ID
  2 DATE-OF-DIVORCE
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 DATE-OF-MARRIAGE
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
*
1 TERM VIEW OF A-TERMINALS
  2 CLERK-ID
  2 COMMENTS
*
1 #MMDDYYYY   (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-C     (A2)
  2 #DD-C     (A2)
  2 #YYYY-C   (A4)
  2 REDEFINE #YYYY-C
    3 #YYYY-N (N4)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 #YYYYMMDD-2  (N8)
1 REDEFINE #YYYYMMDD-2
  2 #YYYYMMDD-2-A (A8)
1 #I           (I2)
1 #J           (I2)
1 #CID-TEXT    (A8)
1 #CONTACT-ID-CV (C/18)
1 #MAP-CV (C)
1 #MARK-SSN-CV (C/18)
1 #NO-OF-SSN   (N4)
1 #AGE (N8)
1 REDEFINE #AGE
  2 #FILL (A1)
  2 #AGE-2 (N3)
1 #ARRAY (18)
  2 #MARK-SSN  (A1)
  2 #CONT-ID-NUMBER (N6)
  2 #CONT-SSN (A9)
  2 #CONT-AGE (N3)
  2 #CONT-DOB (A8)
  2 #CONT-DOD (A8)
  2 #CONT-NAME (A25)
  2 #CONT-CID (N8)
  2 #CONT-TYPE(A1)    /* RE1
1 #MARKED (L)
1 #MSG (A72)          /* RE1
END-DEFINE
*
SET KEY PF3
*
MOVE 'CONT-ID' TO #CID-TEXT
*
IF #SRCH
  RESET #I
  MOVE (AD=P) TO #MARK-SSN-CV(*)
  READ CONT-SSN BY ID-NUMBER > 1900 WHERE SSN = #SSN
  END-READ
  MOVE *COUNTER(0130) TO #NO-OF-SSN
  READ CONT-SSN BY ID-NUMBER > 1900 WHERE SSN = #SSN
    AT END OF DATA
      IF *PF-KEY = 'PF3' OR #MARKED
        ESCAPE ROUTINE
      END-IF
      PERFORM INPUT-MAP
      IF *PF-KEY = 'PF3' OR #MARKED
        IF *PF-KEY = 'PF3'
          MOVE TRUE TO #PF3
        END-IF
        ESCAPE ROUTINE
      END-IF
      RESET #I
    END-ENDDATA
    ADD 1 TO #I
    MOVE (AD=U) TO #MARK-SSN-CV(#I)
    MOVE ID-NUMBER TO #CONT-ID-NUMBER(#I)
    IF SSN > 0
      MOVE SSN TO #CONT-SSN(#I)
    END-IF
    RESET #AGE
    IF DATE-OF-DEATH = 0
      IF DATE-OF-BIRTH NE 0
        COMPUTE #AGE = *DATN - DATE-OF-BIRTH
      END-IF
    ELSE
      IF DATE-OF-BIRTH NE 0
        COMPUTE #AGE = DATE-OF-DEATH - DATE-OF-BIRTH
      END-IF
    END-IF
    MOVE #AGE-2 TO #CONT-AGE(#I)
    IF DATE-OF-BIRTH NE 0
      MOVE DATE-OF-BIRTH TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOB(#I) LEAVING NO
    END-IF
    IF DATE-OF-DEATH NE 0
      MOVE DATE-OF-DEATH TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOD(#I) LEAVING NO
    END-IF
    IF CONTACT-TYPE = 'O'                           /* RE1 START
      #CONT-NAME(#I) := FIRM-ORGANIZATION-NAME
    ELSE
      #CONT-NAME(#I) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    END-IF                                          /* RE1 END
    MOVE CONTACT-ID TO #CONT-CID(#I)
    MOVE CONTACT-TYPE TO #CONT-TYPE(#I)             /* RE1
    IF #I = 18
      PERFORM INPUT-MAP
      IF *PF-KEY = 'PF3' OR #MARKED
        ESCAPE ROUTINE
      END-IF
      RESET #I #ARRAY(*)
    END-IF
  END-READ
ELSE
  RESET #I
  FIND NUMBER CONT-SSN WITH SSN = #SSN
  IF *NUMBER > 0
    MOVE *NUMBER TO #NO-OF-SSN
    REPEAT
      MOVE (AD=P) TO #MARK-SSN-CV(*)
      FIND CONT-SSN WITH SSN = #SSN
        AT END OF DATA
          PERFORM INPUT-MAP
          IF *PF-KEY = 'PF3' OR #MARKED
            IF *PF-KEY = 'PF3'
              MOVE TRUE TO #PF3
            END-IF
            ESCAPE ROUTINE
          END-IF
          RESET #I
        END-ENDDATA
        ADD 1 TO #I
        MOVE (AD=U) TO #MARK-SSN-CV(#I)
        MOVE ID-NUMBER TO #CONT-ID-NUMBER(#I)
        MOVE SSN TO #CONT-SSN(#I)
        IF CONTACT-TYPE = 'O'
          #CONT-NAME(#I) := FIRM-ORGANIZATION-NAME
        ELSE
          RESET #AGE
          IF DATE-OF-DEATH = 0
            IF DATE-OF-BIRTH NE 0
              COMPUTE #AGE = *DATN - DATE-OF-BIRTH
            END-IF
          ELSE
            IF DATE-OF-BIRTH NE 0
              COMPUTE #AGE = DATE-OF-DEATH - DATE-OF-BIRTH
            END-IF
          END-IF
          MOVE #AGE-2 TO #CONT-AGE(#I)
          IF DATE-OF-BIRTH NE 0
            MOVE DATE-OF-BIRTH TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOB(#I) LEAVING NO
          END-IF
          IF DATE-OF-DEATH NE 0
            MOVE DATE-OF-DEATH TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #CONT-DOD(#I) LEAVING NO
          END-IF
          #CONT-NAME(#I) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
        END-IF
        MOVE CONTACT-ID TO #CONT-CID(#I)
        MOVE CONTACT-TYPE TO #CONT-TYPE(#I)             /* RE1
        IF #I = 18
          PERFORM INPUT-MAP
          IF *PF-KEY = 'PF3' OR #MARKED
            MOVE TRUE TO #PF3
            ESCAPE ROUTINE
          END-IF
        END-IF
      END-FIND
    END-REPEAT
  ELSE
    MOVE TRUE TO #NOT-FOUND
  END-IF
END-IF
***************************
DEFINE SUBROUTINE INPUT-MAP
***************************
REPEAT
  INPUT USING MAP 'C2235MCB'
  IF *PF-KEY = 'PF3'
    MOVE TRUE TO #PF3
    ESCAPE ROUTINE
  END-IF
  EXAMINE #MARK-SSN(*) FOR 'X' GIVING NUMBER #J
  IF #J > 1
    REINPUT 'Please make one selection only'
      MARK *#MARK-SSN(1)
  END-IF
  FOR #J 1 18
    IF NOT (#MARK-SSN(#J) = 'X' OR = ' ')
      REINPUT 'Please mark with an "X"'
        MARK *#MARK-SSN(#J)
    END-IF
    IF #MARK-SSN(#J) = 'X'
      IF #ROLE NE ' ' AND #CONT-TYPE(#J) = 'O' AND FUNCTION-CONTACT-TYPE(<#ROLE>) NE 'ORGANIZATION'
        #MSG := FUNCTION-CODE-A-DESC(<'041',#ROLE>)
        IF #MSG = 'OTHER'
          COMPRESS 'Organization cannot be selected for an' #MSG 'who is an Individual' TO #MSG
        ELSE
          COMPRESS 'Organization cannot be selected for a' #MSG TO #MSG
        END-IF
        REINPUT #MSG
          MARK *#MARK-SSN(#J)
      END-IF
*      FIND CONT-REL WITH CONTACT-2-ID = #CONT-CID(#J)
*        IF CONTACT-1-ID = #CONTACT-ID
*          REINPUT 'This Contact is already related to the Member.'
*            MARK *#CONT-CID(#J)
*        END-IF
*      END-FIND
      MOVE TRUE TO #MARKED
      FIND CONT-SSN WITH CONTACT-ID = #CONT-CID(#J)
        MOVE CONTACT-ID TO #CONTACT-ID
        MOVE SSN TO #SSN
        MOVE FIRST-NAME TO #FIRST-NAME
        MOVE MIDDLE-NAME TO #MIDDLE-NAME
        MOVE LAST-NAME TO #LAST-NAME
        MOVE SUFFIX TO #SUFFIX
        MOVE FIRM-ORGANIZATION-NAME TO #ORG-NAME          /* RE2     
        MOVE GENDER-CD TO #SEX
        MOVE #CONT-DOB(#J) TO #DOB
        MOVE #CONT-DOD(#J) TO #DOD
        MOVE BIRTH-CERT-CODE TO #BCC
        MOVE DEATH-CERT-CODE TO #DCC
        MOVE ID-NUMBER TO #CN
        IF LAST-DATE-UPD NE 0
          MOVE LAST-DATE-UPD TO #YYYYMMDD
          COMPRESS #MM-A '-' #DD-A '-' #YYYY-A INTO #LST-UPD LEAVING NO
        END-IF
        FIND (1) TERM WITH CLERK-ID = LAST-USER-UPD
          MOVE COMMENTS TO #LST-USR
        END-FIND
      END-FIND
      RESET  #DOM #DIV
      FIND STATUS WITH ID-NUMBER = #CGA-ORIG-ID
        IF SPOUSE-CONTACT-ID = #CONT-CID(#J)
          IF DATE-OF-MARRIAGE > 0
            MOVE DATE-OF-MARRIAGE TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
          END-IF
          IF #DOD = ' ' AND DATE-OF-DIVORCE > 0
            MOVE DATE-OF-DIVORCE TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
          END-IF
        END-IF
        IF FORMER-SPOUSE-CONTACT-ID = #CONT-CID(#J)
          IF FORMER-SPOUSE-DATE-OF-MARRIAGE > 0
            MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #DOM LEAVING NO
          END-IF
          IF #DOD = ' ' AND FORMER-SPOUSE-DATE-OF-DIVORCE > 0
            MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO #YYYYMMDD
            COMPRESS #MM-A #DD-A #YYYY-A INTO #DIV LEAVING NO
          END-IF
        END-IF
      END-FIND
      ESCAPE ROUTINE
    END-IF
  END-FOR
  ESCAPE BOTTOM
END-REPEAT
*
END-SUBROUTINE
*
END
