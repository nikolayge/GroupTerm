* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPBPTRBI                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Create TR entry for Bridger Indicator                 *
************************************************************************
*
************************************************************************
*                         MODIFICATION LOG                             *
* USER   DATE        TAG     REASON                                    *
* PAM    03/11/2009  None    Program Written                           *
************************************************************************
*
DEFINE DATA
*
PARAMETER USING GPBATRBI
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 NAME
 2 PROCESS-IND
 2 BRIDGER-IND
 2 DATE-OTHER-UPDATE
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER                  (N6)
 2 PROCESS-IND                (A1)
 2 NAME                       (A25)
 2 DATE-LAST-UPDATE           (N8)
 2 TERMINAL-ID                (A8)
 2 CLERK-ID                   (A3)
 2 TRANSACTION-DATN           (P9)
 2 TRANSACTION-TIMN           (P7)
 2 OLD-DATA                   (A125/32)
 2 NEW-DATA                   (A125/32)
*
1 #W-OLD-NEW-DATA             (A125)
1 REDEFINE #W-OLD-NEW-DATA
 2 #W-DESCRIPTION             (A12)
 2 #W-SPACER                  (A1)
 2 #W-DATA-1                  (A112)
*
1 #WORK-FIELDS
 2 #MEMBER-FOUND              (L)
 2 #ISN                       (P8)
 2 #WORK-ID-NUMBER            (N6)
 2 REDEFINE #WORK-ID-NUMBER
  3 #WORK-ID-NUMBER-A         (A6)
*
1 #TEXT-CONSTANTS
 2 #BRIDGER-CHG               (A12) INIT <'AT-BRIDGER ='>
*
END-DEFINE
************************************************************************
*
#WORK-ID-NUMBER := #GPBATRBI-ID-NUMBER
*
RESET #MEMBER-FOUND #ISN
FIND (1) ST-V WITH MEMBER-KEY = #WORK-ID-NUMBER-A
  #ISN          := *ISN
  #MEMBER-FOUND := TRUE
END-FIND
*
IF NOT #MEMBER-FOUND
 OR #ISN = 0
  ESCAPE ROUTINE
END-IF
*
GET ST-V #ISN
*
TRLOG.ID-NUMBER            := ST-V.ID-NUMBER
TRLOG.PROCESS-IND          := ST-V.PROCESS-IND
TRLOG.NAME                 := ST-V.NAME
TRLOG.DATE-LAST-UPDATE     := ST-V.DATE-OTHER-UPDATE
TRLOG.CLERK-ID             := #GPBATRBI-CLERK
TRLOG.TERMINAL-ID          := *INIT-ID
TRLOG.TRANSACTION-DATN     := *DATN
TRLOG.TRANSACTION-TIMN     := *TIMN
*
#W-DESCRIPTION             := #BRIDGER-CHG
IF ST-V.BRIDGER-IND = ' '
  #W-DATA-1                := 'N'
ELSE
  #W-DATA-1                := ST-V.BRIDGER-IND
END-IF
TRLOG.OLD-DATA (1)         := #W-OLD-NEW-DATA
IF #GPBATRBI-NEW-IND = ' '
  #W-DATA-1                := 'N'
ELSE
  #W-DATA-1                := #GPBATRBI-NEW-IND
END-IF
TRLOG.NEW-DATA (1)         := #W-OLD-NEW-DATA
*
STORE TRLOG
*
END
