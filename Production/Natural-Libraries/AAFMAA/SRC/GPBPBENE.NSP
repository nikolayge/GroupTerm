* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GPBPBENE - cross references beneficiaries to contact list
******************************************************************
*            MODIFICATION LOG                                    *
* USER   DATE      TAG  REASON                                   *
* -----  --------  ---- ---------------------------------------  *
* PAM    03302010  None Program Written                          *
* NGG    20121010  NG1  CN Expansion Project - beneficiary data are
*                       moved to new database files
******************************************************************
DEFINE DATA
*
LOCAL
*
1 NT-V
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 NOTE-DATE  (N8/60) /* P/A = PRIN/ALT DESIG
  2 REDEFINE NOTE-DATE /* L = linked pols
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7) /* (D, L)
      4 BENE-SPLIT-IND    (N1/7) /* (D, L)
      4 BENE-LST-UPD      (N8)   /* (D)
      4 BENE-STRT-PEND    (N8)   /* (D)
      4 BENE-FOL-UP-IND   (N1)   /* (D)
      4 BENE-OWN-TRNSF-DT (N8)   /* (D)
      4 BENE-SUC-OWN      (N1)   /* (d)
      4 BENE-NEW-LAYOUT   (N1)   /* (D)/* was -setl-opt
      4 BENE-COM-DIS      (N2)   /* (D)/* Common disaster
      4 BENE-PER-STR      (N1)   /* (D)/* Per Stirpes
      4 BENE-FIL1         (A1)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)  /* (D)
      4 BENE-RESTRICT    (A1)  /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1) /* (D, P/A)
        5 BENE-SHARE-TYP   (A1) /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 BENE-TEXTS-6-POLS (6)
      4 BENE-TEXT-PRIM-SEC (2)
        5 BENE-TEXT-OCC    (5)
          6 BENE-DESIG-2    (A60)
          6 REDEFINE BENE-DESIG-2
            7 BENE-BN-NAME   (A25)
            7 BENE-BN-SSN (A9)
            7 BENE-BN-REL (A1)
            7 BENE-BN-SHARE (A10)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 BENE-D-V VIEW OF A-BENE-DRAFT     /* NG1
  2 ID-NUMBER           (N6)
  2 NOTE-DATE           (N8/60)
  2 NOTE-CLERK-ID       (A3/60)
  2 NOTE-TEXT           (A60/60)
  2 BENE-CONTACT-ID     (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 BENE-F-V VIEW OF A-BENE-FINAL     /* NG1
  2 ID-NUMBER           (N6)
  2 NOTE-DATE           (N8/60)
  2 NOTE-CLERK-ID       (A3/60)
  2 NOTE-TEXT           (A60/60)
  2 BENE-CONTACT-ID     (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 CT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 CONTACT-ID
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 GENDER-CD
  2 DATE-OF-BIRTH
  2 SSN
  2 DELETE-FLAG
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 SSN
  2 PROCESS-IND
  2 FORMER-SPOUSE-SSN
  2 FORMER-HUSBAND-SSN
  2 SSN-SP
  2 SPOUSE-CONTACT-ID
*
1 FIND-ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 SSN
  2 PROCESS-IND
  2 FORMER-SPOUSE-SSN
  2 FORMER-SPOUSE-CONTACT-ID
  2 FORMER-HUSBAND-SSN
  2 FORMER-HUSBAND-CONTACT-ID
  2 SSN-SP
*
1 FR-V VIEW OF A-FIRS
  2 ID-NUMBER
  2 DEPN-STATUS-CD  (14)
  2 DEPN-SSN        (14)
  2 DEPN-CONTACT-ID (14)
*
1 #UPDATE     (L)
1 #CNT        (L)
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #WORK-ID    (N6)
1 #WORK-DATE  (A6)
1 REDEFINE #WORK-DATE
  2 #WORK-YY   (A2)
  2 REDEFINE #WORK-YY
    3 #WORK-YY-N(N2)
  2 #WORK-REST (A4)
1 #WORK-8    (A8)
1 #CNT-DESIG  (P11)
1 #CNT-BENES  (P11)
*
1 #EXTRACT-RECORD      (A350)
1 #TAB                 (A1)   INIT <H'09'>
*
1 #DISPLAY-LAST-UPDATE            (A10)
1 #DISPLAY-MEMBER-NUMBER          (A10)
1 #DISPLAY-POLICIES               (A100)
1 #DISPLAY-TYPE                   (A3)
1 #DISPLAY-NAME                   (A75)
1 #DISPLAY-SSN                    (A9)
1 #DISPLAY-REL                    (A3)
1 #DISPLAY-CONTACT-ID             (A10)
1 #DISPLAY-CONTACT-NAME           (A50)
*
1 #SSN-SPOUSE-TABLE               (1:*)
  2 #SPOUSE-SSN                    (A9)
  2 #SPOUSE-REF-NUMBER             (N6)
  2 #SPOUSE-CONTACT-ID             (N8)
1 #SPOUSES                        (P11)
*
1 #SSN-DEP-TABLE                  (1:*)
  2 #DEP-SSN                       (A9)
  2 #DEP-REF-NUMBER                (N6)
  2 #DEP-CONTACT-ID                (N8)
1 #DEPS                           (P11)
*
1 #SSN                            (N9)
1 REDEFINE #SSN
  2 #SSN-A                         (A9)
1 #SSN-TYPE                       (A3)
1 #REF-NUMBER                     (N6)
*
1 #REF-TYPE                       (A20)
1 #DISP-REF-NUMBER                (A10)
1 #SSN-I                          (P9)
1 #CONTACT-ID                     (N8)
*
*
1 #MEMBERS-CNT                    (P11)
1 #SPOUSES-CNT                    (P11)
1 #DEPENDANTS-CNT                 (P11)
1 #FORMER-SPOUSES-CNT             (P11)
1 #FORMER-HUSBANDS-CNT            (P11)
*
END-DEFINE
******************************************************************
*
DEFINE WORK FILE 1 '/usr/SAG/tmp/BeneReport.txt' TYPE 'ASCII-COMPRESSED'
*
COMPRESS 'Member ID'   #TAB
  'Policies'    #TAB
  'Desig Type'  #TAB
  'Bene Name'   #TAB
  'Bene SSN'    #TAB
  'Bene Contact ID' #TAB
  'Contact Name' #TAB
  'Bene Rel'    #TAB
  'Ref Number'  #TAB
  'Ref Type'    #TAB
  'Last Update' INTO #EXTRACT-RECORD LEAVING NO
WRITE WORK 1 #EXTRACT-RECORD
*
PERFORM LOAD-SSN-TABLE
*
RESET #EXTRACT-RECORD
*
READ BENE-D-V BY ID-NUMBER
  MOVE BY NAME BENE-D-V TO NT-V
  PERFORM PROCESS-RECORD
  IF #UPDATE
    GET BENE-D-V *ISN (0198)
    MOVE BY NAME NT-V TO BENE-D-V
    UPDATE (0202)
    END TRANSACTION
  END-IF
END-READ
*
READ BENE-F-V BY ID-NUMBER
  MOVE BY NAME BENE-F-V TO NT-V
  PERFORM PROCESS-RECORD
  IF #UPDATE
    GET BENE-F-V *ISN (0209)
    MOVE BY NAME NT-V TO BENE-F-V
    UPDATE (0213)
    END TRANSACTION
  END-IF
END-READ
*
WRITE WORK 1 #EXTRACT-RECORD
*
MOVE EDITED #CNT-DESIG (EM=ZZ,ZZZ,ZZZ,ZZ9) TO #EXTRACT-RECORD
COMPRESS 'BENEFICIARY DESIGNATIONS = ' #EXTRACT-RECORD INTO #EXTRACT-RECORD
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
MOVE EDITED #CNT-BENES (EM=ZZ,ZZZ,ZZZ,ZZ9) TO #EXTRACT-RECORD
COMPRESS 'BENEFICIARY INDIVIDUALS  = ' #EXTRACT-RECORD INTO #EXTRACT-RECORD
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
WRITE 'Member             = ' #MEMBERS-CNT
WRITE 'Spouses            = ' #SPOUSES-CNT
WRITE 'Dependants         = ' #DEPENDANTS-CNT
WRITE 'Former Spouses     = ' #FORMER-SPOUSES-CNT
WRITE 'Former Husbands    = ' #FORMER-HUSBANDS-CNT
*
DEFINE PROCESS-RECORD
/*     ==============
MOVE EDITED NT-V.ID-NUMBER (EM=999999) TO #DISPLAY-MEMBER-NUMBER
*
RESET #UPDATE
FOR #I1 1 TO 6
  IF NT-V.BENE-ISS-DT (#I1, 1) NE 0
*    AND NT-V.BENE-NEW-LAYOUT (#I1) = 1
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
*
  RESET #DISPLAY-POLICIES
  FOR #I3 1 TO 7
    IF NT-V.BENE-ISS-DT (#I1,#I3) = 0
      ESCAPE TOP
    END-IF
    MOVE EDITED NT-V.BENE-ISS-DT (#I1,#I3) (EM=999999) TO #WORK-DATE
    IF #WORK-YY-N LT 15
      #WORK-8 := '20'
    ELSE
      #WORK-8 := '19'
    END-IF
    COMPRESS #WORK-8 #WORK-YY #WORK-REST INTO #WORK-8 LEAVING NO
    IF #DISPLAY-POLICIES NE ' '
      COMPRESS #DISPLAY-POLICIES ',' INTO #DISPLAY-POLICIES LEAVING NO
    END-IF
    COMPRESS #DISPLAY-POLICIES #WORK-8 INTO #DISPLAY-POLICIES
  END-FOR
*
  RESET #CNT
  FOR #I2 1 TO 2
    IF NT-V.BENE-TYPE (#I1, #I2) = 'I' OR = 'C' OR = 'P'
      ESCAPE TOP
    END-IF
*
    IF #I2 = 1
      #DISPLAY-TYPE := 'PRI'
    ELSE
      #DISPLAY-TYPE := 'SEC'
    END-IF
*
    FOR #I3 1 TO 5
      IF NT-V.BENE-BN-SSN (#I1,#I2,#I3) = MASK (999999999)
          AND NT-V.BENE-BN-SSN (#I1,#I2,#I3) NE '000000000'
*
        #SSN-A        := NT-V.BENE-BN-SSN (#I1,#I2,#I3)
        PERFORM SEARCH-SSN
*
        #DISPLAY-SSN  := NT-V.BENE-BN-SSN (#I1,#I2,#I3)
        #DISPLAY-NAME := NT-V.BENE-BN-NAME (#I1,#I2,#I3)
        #DISPLAY-REL  := NT-V.BENE-BN-REL (#I1,#I2,#I3)
        MOVE EDITED NT-V.BENE-LST-UPD (#I1) (EM=99999999) TO #DISPLAY-LAST-UPDATE
        IF #CONTACT-ID NE 0
          NT-V.BENE-CONTACT-ID-R  (#I1,#I2,#I3) := #CONTACT-ID
          #UPDATE := TRUE
          MOVE EDITED #CONTACT-ID (EM=99999999) TO #DISPLAY-CONTACT-ID
*
          FIND (1) CT-V WITH CT-V.CONTACT-ID = #CONTACT-ID
            #DISPLAY-CONTACT-NAME := FUNCTION-DISP-CT-NAME(<CT-V.FIRST-NAME,CT-V.MIDDLE-NAME,
              CT-V.LAST-NAME,CT-V.SUFFIX>)
          END-FIND
        ELSE
          RESET #DISPLAY-CONTACT-ID #DISPLAY-CONTACT-NAME
        END-IF
*
        COMPRESS #DISPLAY-MEMBER-NUMBER     #TAB
          #DISPLAY-POLICIES          #TAB
          #DISPLAY-TYPE              #TAB
          #DISPLAY-NAME              #TAB
          #DISPLAY-SSN               #TAB
          #DISPLAY-CONTACT-ID        #TAB
          #DISPLAY-CONTACT-NAME      #TAB
          #DISPLAY-REL               #TAB
          #DISP-REF-NUMBER           #TAB
          #REF-TYPE                  #TAB
          #DISPLAY-LAST-UPDATE INTO #EXTRACT-RECORD LEAVING NO
        WRITE WORK 1 #EXTRACT-RECORD
        RESET #EXTRACT-RECORD
*
        IF NOT #CNT
          ADD +1 TO #CNT-DESIG
          #CNT := TRUE
        END-IF
        ADD +1 TO #CNT-BENES
      END-IF
    END-FOR
  END-FOR
END-FOR
END-SUBROUTINE
*
******************************************************************
DEFINE SUBROUTINE LOAD-SSN-TABLE
******************************************************************
*
EXPAND ARRAY #SSN-SPOUSE-TABLE TO (*:200000)
EXPAND ARRAY #SSN-DEP-TABLE TO (*:200000)
*
READ ST-V BY ID-NUMBER FROM 0001900
  IF ST-V.SSN-SP NE 0
    ADD +1 TO #SPOUSES
    MOVE EDITED ST-V.SSN-SP (EM=999999999) TO #SPOUSE-SSN (#SPOUSES)
    #SPOUSE-REF-NUMBER (#SPOUSES) := ST-V.ID-NUMBER
    #SPOUSE-CONTACT-ID (#SPOUSES) := ST-V.SPOUSE-CONTACT-ID
  END-IF
END-READ
*
READ FR-V BY ID-NUMBER FROM 0001900
  FOR #I1 1 TO 14
    IF DEPN-SSN (#I1) NE 0
      ADD +1 TO #DEPS
      MOVE EDITED FR-V.DEPN-SSN (#I1) (EM=999999999) TO #DEP-SSN (#DEPS)
      #DEP-REF-NUMBER (#DEPS) := FR-V.ID-NUMBER
      #DEP-CONTACT-ID (#DEPS) := FR-V.DEPN-CONTACT-ID (#I1)
    END-IF
  END-FOR
END-READ
*
RESIZE ARRAY #SSN-SPOUSE-TABLE TO (*:#SPOUSES)
RESIZE ARRAY #SSN-DEP-TABLE TO (*:#DEPS)
*
WRITE 'Spouses = ' #SPOUSES
WRITE 'Deps    = ' #DEPS
*
END-SUBROUTINE
*
******************************************************************
DEFINE SUBROUTINE SEARCH-SSN
******************************************************************
*
RESET #DISP-REF-NUMBER #REF-TYPE #CONTACT-ID
FIND (1) FIND-ST-V WITH FIND-ST-V.SSN = #SSN
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  #DISP-REF-NUMBER  := FIND-ST-V.ID-NUMBER
  #REF-TYPE         := 'Member'
  #CONTACT-ID       := FIND-ST-V.MEMBER-CONTACT-ID
  ADD +1 TO #MEMBERS-CNT
END-FIND
*
IF #REF-TYPE = ' '
  EXAMINE #SPOUSE-SSN (*) FOR #SSN-A GIVING INDEX #SSN-I
  IF #SSN-I LE #SPOUSES AND #SSN-I GT 0
    #DISP-REF-NUMBER := #SPOUSE-REF-NUMBER (#SSN-I)
    #REF-TYPE        := 'Spouse'
    #CONTACT-ID      := #SPOUSE-CONTACT-ID (#SSN-I)
    ADD +1 TO #SPOUSES-CNT
  END-IF
END-IF
*
IF #REF-TYPE = ' '
  EXAMINE #DEP-SSN (*) FOR #SSN-A GIVING INDEX #SSN-I
  IF #SSN-I LE #DEPS AND #SSN-I GT 0
    #DISP-REF-NUMBER := #DEP-REF-NUMBER (#SSN-I)
    #REF-TYPE        := 'Dependant'
    #CONTACT-ID      := #DEP-CONTACT-ID (#SSN-I)
    ADD +1 TO #DEPENDANTS-CNT
  END-IF
END-IF
*
IF #REF-TYPE = ' '
  FIND (1) FIND-ST-V WITH FIND-ST-V.FORMER-SPOUSE-SSN = #SSN
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
*
    #DISP-REF-NUMBER  := FIND-ST-V.ID-NUMBER
    #REF-TYPE         := 'Former Spouse'
    #CONTACT-ID       := FIND-ST-V.FORMER-SPOUSE-CONTACT-ID
    ADD +1 TO #FORMER-SPOUSES-CNT
  END-FIND
END-IF
*
IF #REF-TYPE = ' '
  FIND (1) FIND-ST-V WITH FIND-ST-V.FORMER-HUSBAND-SSN = #SSN
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
*
    #DISP-REF-NUMBER  := FIND-ST-V.ID-NUMBER
    #REF-TYPE         := 'Former Husband'
    #CONTACT-ID       := FIND-ST-V.FORMER-HUSBAND-CONTACT-ID
    ADD +1 TO #FORMER-HUSBANDS-CNT
  END-FIND
END-IF
*
END-SUBROUTINE
*
END


