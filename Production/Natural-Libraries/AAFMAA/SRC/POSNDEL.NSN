* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSNDEL.
/**
/** :author nguentchev
/*  Delete all policy role records related to the parm #CN
DEFINE DATA
PARAMETER
1 #CN  (N6)
1 #MSG (A) DYNAMIC
LOCAL USING POSLROLE
LOCAL
1 PO-D-V VIEW OF A-POLICY-CONTACTS
1 PO-V VIEW OF A-POLICY-CONTACTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 RELATION (N2.0)
  2 DESIGNATION-ID (N8)
1 PED-V VIEW OF A-PENDING-BENE
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 RELATION (N2.0)
  2 DESIGNATION-ID (N8)
1 DSGN-V VIEW OF A-DESIGNATION
1 #POLICY   (A16)
END-DEFINE
/*
#POLICY := 'A'
FIND PO-V WITH REFERENCE-NUMBER = #CN
    SORTED BY POLICY-NUMBER
  DELETE
  IF #POLICY NE PO-V.POLICY-NUMBER
    FIND PO-D-V WITH POLICY-NUMBER = #POLICY
      DELETE
    END-FIND
    #POLICY := PO-V.POLICY-NUMBER
  END-IF
  IF PO-V.RELATION = PrimaryBene OR= UnformatedBene OR= ContingentBene
    FIND DSGN-V WITH DESIGNATION-ID = PO-V.DESIGNATION-ID
      DELETE
    END-FIND
  END-IF
END-FIND
FIND PO-D-V WITH POLICY-NUMBER = #POLICY
  DELETE
END-FIND
FIND PED-V WITH INSURED-CN = #CN
  DELETE
  IF PED-V.RELATION = PrimaryBene OR= UnformatedBene OR= ContingentBene
    FIND DSGN-V WITH DESIGNATION-ID = PED-V.DESIGNATION-ID
      DELETE
    END-FIND
  END-IF
END-FIND
END TRANSACTION
/*
END
