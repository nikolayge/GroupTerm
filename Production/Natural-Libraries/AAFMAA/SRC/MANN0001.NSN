* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram MANN0001.
/**
/** :author nguentchev
* bz-Name-C 07/07/2017     Fine Names. Keep all capital
DEFINE DATA
PARAMETER
1 #REFERENCE-CN   (N6)   /* IN
1 #CONTACT-ID     (N8)   /* IN
1 #POLICY-NUM     (A16)  /* OUT
1 #MSG            (A60)
LOCAL
1 #SCREEN
  2 #SEL        (A1/12)
  2 #POLICY-N   (A16/12)
  2 #ANNU-CNT   (A30/12)
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9) 2 REDEFINE ISSUE-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 FACE-AMOUNT   (9)
1 ANN-V VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION (N7.2)
  2 PRINCIPAL (N7.2)
  2 ANNUAL-PAYMENT (N7.2)
  2 RESERVE-AMOUNT (N7.2)
  2 WITHDRAWALS (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE (N8)
  2 NOTES (A70/1:191)
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 LAST-UPD-TIME (N7.0)
1 CONTACT-V VIEW OF A-CONTACTS
  2 ID-NUMBER  (N6)
  2 CONTACT-ID (N8)
  2 GENDER-CD  (A1)
  2 SSN        (N9)
  2 DATE-OF-BIRTH (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 YYYY (N4)
    3 MM   (N2)
    3 DD   (N2)
1 BLANK    (A1) CONST <' '>
1 J   (I4)
1 K   (I4)
1 #CI (N8)
1 #TITLE   (A60)
END-DEFINE
/*
DEFINE WINDOW SelectPolicy
  SIZE 18 * 68
  BASE 3/2
  FRAMED ON POSITION SYMBOL AUTO
SET KEY PF3  NAMED 'Exit'
/*
PERFORM GET-WND-DATA
RESET J #MSG
IF K = 0
  #MSG := 'All policies are annuities for this contact'
  ESCAPE ROUTINE
END-IF
SET WINDOW 'SelectPolicy'
RPL.
REPEAT
  INPUT WINDOW='SelectPolicy' WITH TEXT #MSG USING MAP 'MANM0001'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPL.)
    VALUE 'ENTR'
      FOR J 1 TO K
        IF #SEL (J) NE BLANK
          ESCAPE BOTTOM (RPL.)
        END-IF
      END-FOR
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
SET WINDOW OFF
/*
IF J > 0 AND J <= K
  #POLICY-NUM := #POLICY-N (J)
ELSE
  RESET #POLICY-NUM
  #MSG := 'No policy selected'
END-IF
*
/*
DEFINE GET-WND-DATA
/*     -------------
RESET K #SCREEN
FIND POLICY WITH ID-NUMBER = #REFERENCE-CN
  FOR J 1 TO C*ACTIVE-SEGMENTS
    IF K = 0 OR #ANNU-CNT (K) NE BLANK
      ADD 1 TO K
    END-IF
    #POLICY-N (K) := POLICY-ID (J)
    FIND ANN-V WITH POLICY-NUMBER = POLICY-ID (J)
        AND REFERENCE-CN = #REFERENCE-CN
      IF NO RECORDS FOUND
        #ANNU-CNT (K) := FUNCTION-CONTACT-NAME (< #CONTACT-ID >)
        #ANNU-CNT (K) := *TRANSLATE(#ANNU-CNT (K), UPPER)          /* bz-Name
        ESCAPE BOTTOM
      END-NOREC
      IF  ANN-V.CONTACT-ID NE #CONTACT-ID
        IF #ANNU-CNT (K) NE BLANK
          ADD 1 TO K
        END-IF
        #POLICY-N (K) := POLICY-ID (J)
        #ANNU-CNT (K) := FUNCTION-CONTACT-NAME (< ANN-V.CONTACT-ID >)
        #ANNU-CNT (K) := *TRANSLATE(#ANNU-CNT (K), UPPER)           /* bz-Name
      END-IF
    END-FIND
  END-FOR
END-FIND
IF K > 0 AND #ANNU-CNT (K) EQ BLANK
  K:= K - 1
END-IF
#TITLE := FUNCTION-CONTACT-NAME (< #CONTACT-ID >)
#TITLE := *TRANSLATE(#TITLE, UPPER)                    /* bz-Name
COMPRESS 'Select policy for:' #TITLE INTO #TITLE 
END-SUBROUTINE
/*
END
