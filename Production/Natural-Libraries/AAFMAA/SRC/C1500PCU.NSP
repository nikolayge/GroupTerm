* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* * * * * * * * * * * * * * * * * *
* PROGRAM-ID: C1500PCU
* CU  -  UPDATE LIST OF CERTIFIED NOTICES
*
* MODIFICATIONS:
*
*
*
* * * * * * * * * * * * * * * * * *
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL
*
* * * *   N O T E   F I L E  * * * * * * * * * * * * * * * * *
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 NOTE-TEXT      (A60/40)
  2 REDEFINE NOTE-TEXT
   3 CERT-IDS-TBL   (40)
    4 CERT-IDS      (P7/15)
*
* * * *   S T A T U S   F I L E  * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 NAME           (A25)
  2 X-FIRST-PLACE-FEE  (P7.2/9)
  2 DEBIT-CREDIT-AMT (P7.2)
  2 STATUS         (9)
  2 PAY-STATUS     (9)
  2 MODE           (9)
  2 PAID-TO-DATE   (N8/9)
  2 REDEFINE PAID-TO-DATE
   3 PAID-TO-DATE-OCC   (9)
    4 PAID-TO-DATE-YYYYMM  (N6)
    4 PAID-TO-DATE-DD      (N2)
  2 ISSUE-DATE     (N8/9)
  2 REDEFINE ISSUE-DATE
   3 ISS-DT-OCC         (9)
    4 ISS-DT-YYYYMM     (N6)
    4 ISS-DT-DD         (N2)
*
1 #SCN-TYPE        (A10)
1 #SCN-IDS-1       (A6/20)
1 #SCN-IDS-2       (A6/20)
1 #SCN-IDS-3       (A6/20)
1 #SCN-IDS-4       (A6/20)
1 #SCN-IDS-5       (A6/20)
*
1 #SCN-IDS-A       (A6)
1 REDEFINE #SCN-IDS-A
 2 #SCN-IDS-N      (N6)
*
1 #SCN-NAMES-1     (A6/20)
1 #SCN-NAMES-2     (A6/20)
1 #SCN-NAMES-3     (A6/20)
1 #SCN-NAMES-4     (A6/20)
1 #SCN-NAMES-5     (A6/20)
1 #HOLD-NAME       (A6)
*
1 #TODAY-YYYYMMDD (N8)
1 REDEFINE #TODAY-YYYYMMDD
 2 #TODAY-CENT    (A2)
 2 #TODAY-YY-A    (A2)
 2 #TODAY-MM-A    (A2)
 2 #TODAY-DD-A    (A2)
1 REDEFINE #TODAY-YYYYMMDD
 2 #TODAY-YYYY    (N4)
 2 #TODAY-MM      (N2)
 2 #TODAY-DD      (N2)
1 REDEFINE #TODAY-YYYYMMDD
 2 #TODAY-YYYYMM  (N6)
 2 #FILL-DD       (N2)
*
1 #ISS-YYYYMM-PLUS-2     (N6)
1 REDEFINE #ISS-YYYYMM-PLUS-2
  2 #ISS-PLUS-2-CENT (N2)
  2 #ISS-PLUS-2-YY   (N2)
  2 #ISS-PLUS-2-MM   (N2)
*
1 #TWO-MONTHS-AGO     (N6)
1 REDEFINE #TWO-MONTHS-AGO
 2 #TWO-MONTHS-AGO-YYYY  (N4)
 2 #TWO-MONTHS-AGO-MM    (N2)
*
1 #ISC            (I2)
1 #I1             (I2)
1 #I2             (I2)
1 #I3             (I2)
1 #I4             (I2)
1 #I5             (I2)
1 #I6             (I2)
1 #FINISHED       (L)
1 #FOUND-CERT-MATCH (L)
1 #GAWT-PEND-QAPL   (L)
1 #HOLD-PAY-STATUS  (A2)
1 #ISN            (P8)
1 #NX-REC-ID      (N6)
*
1 #HOLD-ID        (N6)
1 REDEFINE #HOLD-ID
 2 #HOLD-ID-A      (A6)
*
END-DEFINE
***********************************************************************
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
***********************************************************************
*
MOVE *DATN TO #TODAY-YYYYMMDD
*
MOVE #TODAY-YYYY TO #TWO-MONTHS-AGO-YYYY
MOVE #TODAY-MM   TO #TWO-MONTHS-AGO-MM
SUBTRACT 2 FROM #TWO-MONTHS-AGO-MM
*
IF #TWO-MONTHS-AGO-MM LT 1
  ADD 12 TO #TWO-MONTHS-AGO-MM
  SUBTRACT 1 FROM #TWO-MONTHS-AGO-YYYY
END-IF
*
FOR #I6 1 TO 3
*
IF #I6 = 1
  IF #CGA-SCR-SUFFIX = 'CF' OR = 'CS'
    ESCAPE TOP
  END-IF
*
  MOVE 800009 TO #NX-REC-ID
  MOVE 'PREMIUM' TO #SCN-TYPE
ELSE
  IF #I6 = 2
    IF #CGA-SCR-SUFFIX = 'CL' OR = 'CS'
      ESCAPE TOP
    END-IF
*
    MOVE 800010 TO #NX-REC-ID
    MOVE 'FPF'     TO #SCN-TYPE
  ELSE
    IF #CGA-SCR-SUFFIX = 'CL' OR = 'CF'
      ESCAPE TOP
    END-IF
*
    MOVE 800011 TO #NX-REC-ID
    MOVE 'STOPS'   TO #SCN-TYPE
  END-IF
END-IF
*
FIND (1) NT-V WITH ID-NUMBER = #NX-REC-ID
  IF NO RECORDS FOUND
    INPUT TEXT 'NO "FS" NX REC FOUND - CALL EDS'
                                                USING MAP 'G1000MXX'
    FETCH 'G1000PXX'
  END-NOREC
*
  MOVE *ISN TO #ISN
END-FIND
*
MOVE 1 TO #I1    /* INPUT INDEX FOR 40 ENTRIES ON "NX" REC
RESET #I2 #I2    /* INPUT INDEX FOR 15 ENTRIES WITHIN EACH OF ABOVE
MOVE 1 TO #I3    /* OUTPUT INDEX FOR 40 ENTRIES ON "NX" REC
RESET #I2 #I4    /* OUTPUT INDEX FOR 15 ENTRIES WITHIN EACH OF ABOVE
*
RESET #FINISHED
REPEAT UNTIL #FINISHED
  GET NT-V #ISN
  RESET #SCN-IDS-1   (*)
  RESET #SCN-IDS-2   (*)
  RESET #SCN-IDS-3   (*)
  RESET #SCN-IDS-4   (*)
  RESET #SCN-IDS-5   (*)
  RESET #SCN-NAMES-1 (*)
  RESET #SCN-NAMES-2 (*)
  RESET #SCN-NAMES-3 (*)
  RESET #SCN-NAMES-4 (*)
  RESET #SCN-NAMES-5 (*)
*
  FOR #ISC 1 TO 100
    ADD 1 TO #I2
*
    IF #I2 GT 15
      ADD 1 TO #I1
      MOVE 1 TO #I2
    END-IF
*
    IF CERT-IDS (#I1, #I2) = 999999
      MOVE TRUE TO #FINISHED
      ESCAPE BOTTOM
    END-IF
*
    IF CERT-IDS (#I1, #I2) GE 9000000
      RESET #HOLD-ID
    ELSE
      MOVE CERT-IDS (#I1, #I2) TO #HOLD-ID
    END-IF
*
    RESET #HOLD-NAME
    FIND (1) ST-V WITH MEMBER-KEY = #HOLD-ID-A
      MOVE NAME TO #HOLD-NAME
      RESET #FOUND-CERT-MATCH
*
      IF #I6 = 1         /* PREMIUM DUE CERTIFIED
        FOR #I5 1 TO 9
          IF   (PAID-TO-DATE-YYYYMM (#I5) = #TWO-MONTHS-AGO)
           AND (STATUS (#I5) = 'D')        /* POL IS ACTIVE
            MOVE TRUE TO #FOUND-CERT-MATCH
            ESCAPE BOTTOM
          END-IF
        END-FOR
*
        IF NOT #FOUND-CERT-MATCH
          MOVE '*PAID*' TO #HOLD-NAME
        END-IF
      END-IF
*
      IF #I6 = 2          /* FPF DELINQUENT CERTIFIED
        FOR #I5 1 TO 9
          IF PAY-STATUS (#I5) = '47'
            IF X-FIRST-PLACE-FEE (#I5) LT 400.00
              MOVE '*PAID*' TO #HOLD-NAME
            END-IF
          ELSE
            IF X-FIRST-PLACE-FEE (#I5) LT 1.00
              MOVE '*PAID*' TO #HOLD-NAME
            END-IF
          END-IF
        END-FOR
      END-IF
*
      IF #I6 = 3          /* GAWT STOP CERTIFIED
        RESET #GAWT-PEND-QAPL
        FOR #I5 1 TO 9
          MOVE ISS-DT-YYYYMM (#I5) TO #ISS-YYYYMM-PLUS-2
          ADD 2 TO #ISS-PLUS-2-MM
*
          IF #ISS-PLUS-2-MM GT 12
            SUBTRACT 12 FROM #ISS-PLUS-2-MM
            ADD 100 TO #ISS-YYYYMM-PLUS-2
          END-IF
*
          IF   (MODE (#I5) = 'G')
           AND (PAY-STATUS (#I5) = '21')
           AND (PAID-TO-DATE-YYYYMM (#I5) LT #TODAY-YYYYMM)
           AND (PAID-TO-DATE-YYYYMM (#I5) NE #ISS-YYYYMM-PLUS-2)
            MOVE TRUE TO #GAWT-PEND-QAPL
          END-IF
        END-FOR
*
        IF NOT #GAWT-PEND-QAPL
          MOVE '*CHGD*' TO #HOLD-NAME
        END-IF
      END-IF
    END-FIND
*
    IF   (CERT-IDS (#I1, #I2) NE 0)
     AND (CERT-IDS (#I1, #I2) LT 9000000)
      IF #ISC LE 20
        MOVE EDITED CERT-IDS (#I1, #I2) (EM=999999)
                                    TO #SCN-IDS-1   (#ISC)
        MOVE #HOLD-NAME               TO #SCN-NAMES-1 (#ISC)
      ELSE
        IF #ISC LE 40
          MOVE EDITED CERT-IDS (#I1, #I2) (EM=999999)
                                      TO #SCN-IDS-2   (#ISC - 20)
          MOVE #HOLD-NAME               TO #SCN-NAMES-2 (#ISC - 20)
        ELSE
          IF #ISC LE 60
            MOVE EDITED CERT-IDS (#I1, #I2) (EM=999999)
                                          TO #SCN-IDS-3   (#ISC - 40)
            MOVE #HOLD-NAME               TO #SCN-NAMES-3 (#ISC - 40)
          ELSE
            IF #ISC LE 80
              MOVE EDITED CERT-IDS (#I1, #I2) (EM=999999)
                                            TO #SCN-IDS-4   (#ISC - 60)
              MOVE #HOLD-NAME               TO #SCN-NAMES-4 (#ISC - 60)
            ELSE
              MOVE EDITED CERT-IDS (#I1, #I2) (EM=999999)
                                            TO #SCN-IDS-5   (#ISC - 80)
              MOVE #HOLD-NAME               TO #SCN-NAMES-5 (#ISC - 80)
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-FOR
*
  MOVE '*' TO #CGA-SCR-COMMAND
  INPUT MARK *#SCN-IDS-1 (1) USING MAP 'C1500MCU'
*
  IF #CGA-SCR-COMMAND NE '*'
    END TRANSACTION
    FETCH 'G1000PXX'
  END-IF
*
  FOR #ISC 1 TO 100
    ADD 1 TO #I4
*
    IF #I4 GT 15
      ADD 1 TO #I3
      MOVE 1 TO #I4
    END-IF
*
    IF CERT-IDS (#I3, #I4) = 999999
      ESCAPE BOTTOM
    END-IF
*
    IF #ISC LE 20
      IF #SCN-IDS-1 (#ISC) = ' ' OR = '000000'
       AND (CERT-IDS (#I3, #I4) LT 9000000)
        ADD 9000000 TO CERT-IDS (#I3, #I4)
*       RESET CERT-IDS (#I3, #I4)
      ELSE
        IF #SCN-IDS-1 (#ISC) = MASK (NNNNNN)
          MOVE #SCN-IDS-1 (#ISC) TO #SCN-IDS-A
          MOVE #SCN-IDS-N        TO CERT-IDS (#I3, #I4)
        END-IF
      END-IF
    ELSE
      IF #ISC LE 40
        IF #SCN-IDS-2 (#ISC - 20) = ' ' OR = '000000'
         AND (CERT-IDS (#I3, #I4) LT 9000000)
          ADD 9000000 TO CERT-IDS (#I3, #I4)
*         RESET CERT-IDS (#I3, #I4)
        ELSE
          IF #SCN-IDS-2 (#ISC - 20) = MASK (NNNNNN)
            MOVE #SCN-IDS-2 (#ISC - 20) TO #SCN-IDS-A
            MOVE #SCN-IDS-N             TO CERT-IDS (#I3, #I4)
          END-IF
        END-IF
      ELSE
        IF #ISC LE 60
          IF #SCN-IDS-3 (#ISC - 40) = ' ' OR = '000000'
           AND (CERT-IDS (#I3, #I4) LT 9000000)
            ADD 9000000 TO CERT-IDS (#I3, #I4)
*           RESET CERT-IDS (#I3, #I4)
          ELSE
            IF #SCN-IDS-3 (#ISC - 40) = MASK (NNNNNN)
              MOVE #SCN-IDS-3 (#ISC - 40) TO #SCN-IDS-A
              MOVE #SCN-IDS-N             TO CERT-IDS (#I3, #I4)
            END-IF
          END-IF
        ELSE
          IF #ISC LE 80
            IF #SCN-IDS-4 (#ISC - 60) = ' ' OR = '000000'
             AND (CERT-IDS (#I3, #I4) LT 9000000)
              ADD 9000000 TO CERT-IDS (#I3, #I4)
*             RESET CERT-IDS (#I3, #I4)
            ELSE
              IF #SCN-IDS-4 (#ISC - 60) = MASK (NNNNNN)
                MOVE #SCN-IDS-4 (#ISC - 60) TO #SCN-IDS-A
                MOVE #SCN-IDS-N             TO CERT-IDS (#I3, #I4)
              END-IF
            END-IF
          ELSE
            IF #SCN-IDS-5 (#ISC - 80) = ' ' OR = '000000'
             AND (CERT-IDS (#I3, #I4) LT 9000000)
              ADD 9000000 TO CERT-IDS (#I3, #I4)
*             RESET CERT-IDS (#I3, #I4)
            ELSE
              IF #SCN-IDS-5 (#ISC - 80) = MASK (NNNNNN)
                MOVE #SCN-IDS-5 (#ISC - 80) TO #SCN-IDS-A
                MOVE #SCN-IDS-N             TO CERT-IDS (#I3, #I4)
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-FOR
*
  UPDATE (0173)
  END TRANSACTION
END-REPEAT /*(0172)
END-FOR /*(0129)
*
MOVE 'CU' TO #CGA-SCR-COMMAND
FETCH 'G1000PXX'
*
END
