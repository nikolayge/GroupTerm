* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* PROGRAM ID: GCAPN001 - UPDATE CAP-ENTRY-RANK-ID
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE           TAG     REASON                                 *
*  NGG   20121121       INTIAL CREATION                                *
************************************************************************
DEFINE DATA
PARAMETER
1 #ID-NUMBER   (N6)
1 #REQUEST     (I2)
1 #RANK        (A) DYNAMIC OPTIONAL
LOCAL
1 ST-V VIEW OF A-STATUS
  2 CAP-ENTRY-RANK-ID
1 CNT-V VIEW OF A-CONTACTS
  2 RANK-ID
1 #RESET       (I2) CONST <0>
1 #SET         (I2) CONST <1>
1 #GET         (I2) CONST <2>
END-DEFINE
*
FL.
FIND (1) ST-V WITH ID-NUMBER = #ID-NUMBER
  IF NO RECORDS FOUND
    RESET #RANK
    ESCAPE BOTTOM
  END-NOREC
  DECIDE ON FIRST VALUE OF #REQUEST
    VALUE #RESET
      RESET ST-V.CAP-ENTRY-RANK-ID
    VALUE #SET
      FIND (1) CNT-V WITH ID-NUMBER = #ID-NUMBER
        ST-V.CAP-ENTRY-RANK-ID := CNT-V.RANK-ID
      END-FIND
    VALUE #GET
      IF #RANK SPECIFIED
        #RANK := F-GET-PAY-GRADE (< ST-V.CAP-ENTRY-RANK-ID >)
      END-IF
    NONE VALUE
      IGNORE
  END-DECIDE
  IF #REQUEST = #RESET OR= #SET
    UPDATE (FL.)
  END-IF
END-FIND
END
