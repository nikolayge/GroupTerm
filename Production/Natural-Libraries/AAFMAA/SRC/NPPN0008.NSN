* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*********************************************************************
* SUBPROGRAM-ID: NPPN0008 policy print CAP Loan Amortization Schedule
*********************************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    06232008       Initial Creation
* VXT    04/12/2013 vt1 correct 5000 to 4000 for cap loan amount
* YAK    11152015  YK1  CAP2 - find correct application if exists 
********************************************************
DEFINE DATA
PARAMETER USING NPPA0001
PARAMETER
1 #SSN       (N9)
1 #CAP-MONTHLY-PAYMENT  (N5.2)
1 #CAP-INTEREST-RATE    (N2.3)
1 #CAP-PMT-DATE   (N6)
1 REDEFINE #CAP-PMT-DATE
  2 #CAP-PMT-YYYY (N4)
  2 #CAP-PMT-MM   (N2)
LOCAL
1 AT-V VIEW OF A-APPL-TRACKING
  2 SSN
  2 CAP-LOAN-AMT
  2 CAP-TERM
  2 ACTION-DATE
*
1 #I (I2)
1 #BALANCE       (N5.2)
1 #INT-PMT       (N5.2)
1 #PRIN-PMT      (N5.2)
1 #NEXT-BALANCE  (N5.2)
1 #NN             (N3)
1 #NNN            (N3)
1 #CAP-LOAN-AMOUNT      (N5.2) INIT <4000>   /* vt1 was 5000
1 #CAP-REPAYMENT-PERIOD (N1)   INIT <4>
1 #PMT-DAYS (N2/12) INIT <31,28,31,30,31,30,31,31,30,31,30,31>
1 #CAP-LOAN-AMORTIZATION (0:200)
  2 MONTH         (A7)
  2 INTEREST      (A10)
  2 PRINCIPAL     (A10)
  2 BALANCE       (A10)
* YK1 >  
1 #ACTION-DATE    (N8)         
1 REDEFINE #ACTION-DATE
  2 #ACTION-DATE-N6 (N6) 
1 REDEFINE #ACTION-DATE
  2 #ACTION-DATE-YYYY (N4)
  2 #ACTION-DATE-MM   (N2)
* < YK1  
END-DEFINE
*
C@CAP-LOAN-AMORTIZATION := 99
EXPAND ARRAY CAP-LOAN-AMORTIZATION TO (*:C@CAP-LOAN-AMORTIZATION)
FIND AT-V WITH SSN = #SSN     /* YK1
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
* YK1 >  
  #ACTION-DATE := AT-V.ACTION-DATE
  #ACTION-DATE-MM := #ACTION-DATE-MM + 2
  IF #ACTION-DATE-MM > 12
    #ACTION-DATE-MM := #ACTION-DATE-MM - 12
    #ACTION-DATE-YYYY := #ACTION-DATE-YYYY + 1
  END-IF    
  IF #CAP-PMT-DATE <= #ACTION-DATE-N6
* < YK1   
    IF AT-V.CAP-LOAN-AMT <> 0
      #CAP-LOAN-AMOUNT := AT-V.CAP-LOAN-AMT
    END-IF
    IF AT-V.CAP-TERM <> 0
      #CAP-REPAYMENT-PERIOD := CAP-TERM / 12
    END-IF
  END-IF     /* YK1
END-FIND
IF #CAP-LOAN-AMOUNT = 0
  ESCAPE ROUTINE
END-IF
MOVE EDITED #CAP-LOAN-AMOUNT (EM=Z,ZZ9) TO POLICY-INFORMATION.LOAN-AMOUNT
COMPRESS '$' POLICY-INFORMATION.LOAN-AMOUNT INTO POLICY-INFORMATION.LOAN-AMOUNT LEAVING NO
MOVE EDITED #CAP-MONTHLY-PAYMENT (EM=ZZ9.99) TO LOAN-MONTHLY-PAYMENT
COMPRESS '$' LOAN-MONTHLY-PAYMENT INTO LOAN-MONTHLY-PAYMENT LEAVING NO
COMPRESS #CAP-REPAYMENT-PERIOD 'Years' INTO LOAN-REPAYMENT-PERIOD
MOVE EDITED #CAP-INTEREST-RATE (EM=Z9.99%) TO LOAN-INTEREST-RATE
*
RESET MONTH(*) INTEREST(*) PRINCIPAL(*) BALANCE(*)
MOVE EDITED #CAP-LOAN-AMOUNT (EM=Z,ZZ9.99) TO BALANCE(0)
COMPRESS '$' BALANCE(0) INTO BALANCE(0) LEAVING NO
MONTH(0)   := 'Current'
INTEREST(0):= PRINCIPAL(0) := INTEREST(1) := '$0.00'
PRINCIPAL(1) := LOAN-MONTHLY-PAYMENT
MONTH(1) := '1'
#BALANCE := #CAP-LOAN-AMOUNT - #CAP-MONTHLY-PAYMENT
MOVE EDITED #BALANCE (EM=Z,ZZ9.99) TO BALANCE(1)
COMPRESS '$' BALANCE(1) INTO BALANCE(1) LEAVING NO
*
FOR #NN 2 TO 99
  MOVE EDITED #NN (EM=Z9) TO MONTH(#NN)
  COMPUTE ROUNDED #INT-PMT =
    #BALANCE * (#CAP-INTEREST-RATE / 100) / 365 * #PMT-DAYS(#CAP-PMT-MM)
  MOVE EDITED #INT-PMT (EM=ZZ9.99) TO INTEREST(#NN)
  COMPRESS '$' INTEREST(#NN) INTO INTEREST(#NN) LEAVING NO
  COMPUTE #PRIN-PMT = #CAP-MONTHLY-PAYMENT - #INT-PMT
  MOVE EDITED #PRIN-PMT (EM=ZZ9.99) TO PRINCIPAL(#NN)
  COMPRESS '$' PRINCIPAL(#NN) INTO PRINCIPAL(#NN) LEAVING NO
  COMPUTE #NEXT-BALANCE = #BALANCE - #PRIN-PMT
  IF #NEXT-BALANCE LT 0
    #PRIN-PMT := #BALANCE
    MOVE EDITED #PRIN-PMT (EM=ZZ9.99) TO PRINCIPAL(#NN)
    COMPRESS '$' PRINCIPAL(#NN) INTO PRINCIPAL(#NN) LEAVING NO
    #NEXT-BALANCE := #INT-PMT + #PRIN-PMT
    MOVE EDITED #NEXT-BALANCE (EM=Z,ZZ9.99) TO BALANCE(#NN)
    COMPRESS '$' BALANCE(#NN) INTO BALANCE(#NN) LEAVING NO
    C@CAP-LOAN-AMORTIZATION := #NN
    ESCAPE BOTTOM
  ELSE
    MOVE EDITED #NEXT-BALANCE (EM=Z,ZZ9.99) TO BALANCE(#NN)
    COMPRESS '$' BALANCE(#NN) INTO BALANCE(#NN) LEAVING NO
    #BALANCE := #NEXT-BALANCE
  END-IF
  #CAP-PMT-MM := #CAP-PMT-MM + 1
  IF #CAP-PMT-MM GT 12
    #CAP-PMT-MM := 1
  END-IF
END-FOR
C@CAP-LOAN-AMORTIZATION := C@CAP-LOAN-AMORTIZATION / 2
RESIZE ARRAY CAP-LOAN-AMORTIZATION TO (0:C@CAP-LOAN-AMORTIZATION)
FOR #NN = 0 TO C@CAP-LOAN-AMORTIZATION
  #NNN := #NN+C@CAP-LOAN-AMORTIZATION
  MONTH-L(#NN)     := MONTH(#NN)
  MONTH-R(#NN)     := MONTH(#NNN)
  INTEREST-L(#NN)  := INTEREST(#NN)
  INTEREST-R(#NN)  := INTEREST(#NNN)
  PRINCIPAL-L(#NN) := PRINCIPAL(#NN)
  PRINCIPAL-R(#NN) := PRINCIPAL(#NNN)
  BALANCE-L(#NN)   := BALANCE(#NN)
  BALANCE-R(#NN)   := BALANCE(#NNN)
END-FOR
* Adjust amortization table to the page size
* IF C@CAP-LOAN-AMORTIZATION = 24
*   C@CAP-LOAN-AMORTIZATION := C@CAP-LOAN-AMORTIZATION + 8
IF C@CAP-LOAN-AMORTIZATION < 32
  C@CAP-LOAN-AMORTIZATION := 32
  RESIZE ARRAY CAP-LOAN-AMORTIZATION TO (0:C@CAP-LOAN-AMORTIZATION)
END-IF
*
END
