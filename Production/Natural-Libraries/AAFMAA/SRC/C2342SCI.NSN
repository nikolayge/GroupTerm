* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C2342SCI - Update notes when issue date is changed
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    05202004  None    Program written.                            *
*                                                                      *
*                                                                      *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #ID-NUMBER              (N6)
1 #OLD-ISSUE-DATE         (N8)
1 REDEFINE #OLD-ISSUE-DATE
 2 #OLD-ISSUE-YYYY        (A4)
 2 #OLD-ISSUE-MM          (A2)
 2 #OLD-ISSUE-DD          (A2)
1 #NEW-ISSUE-DATE         (N8)
1 REDEFINE #NEW-ISSUE-DATE
 2 #NEW-ISSUE-YYYY        (A4)
 2 #NEW-ISSUE-MM          (A2)
 2 #NEW-ISSUE-DD          (A2)
*
LOCAL
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-DATE         (60)
 2 NOTE-CLERK-ID     (60)
 2 NOTE-TEXT         (60)
 2 REDEFINE NOTE-TEXT                         /*  1035 exchange note
  3 NOTE-TEXT-OCC    (60)
   4 NT-TXT-LBL           (A12)
   4 NT-TXT-1035-THOUS    (A3)
   4 NT-TXT-1035-COMMA    (A1)
   4 NT-TXT-1035-HUNDR    (A3)
   4 NT-TXT-1035-POINT    (A1)
   4 NT-TXT-1035-CENTS    (A2)
   4 NT-TEXT-FILL-1       (A8)
   4 NT-TXT-1035-MM       (A2)
   4 NT-TXT-1035-FILL1    (A1)
   4 NT-TXT-1035-DD       (A2)
   4 NT-TXT-1035-FILL2    (A1)
   4 NT-TXT-1035-YYYY     (A4)
   4 NT-TXT-FILL-2        (A1)
   4 NT-TXT-PLUS          (A6)
   4 NT-TXT-LUMP-THOUS    (A2)
   4 NT-TXT-LUMP-COMMA    (A1)
   4 NT-TXT-LUMP-HUNDR    (A3)
   4 NT-TXT-LUMP-POINT    (A1)
   4 NT-TXT-LUMP-CENTS    (A2)
   4 NT-TXT-FILL-3        (A4)
 2 REDEFINE NOTE-TEXT                          /*  Flex amount note
  3 NOTE-TEXT-OCC-FLEX (60)
   4 NT-TXT-LBL-FLEX      (A11)
   4 NT-TXT-FLEX-THOUS    (A2)
   4 NT-TXT-FLEX-COMMA    (A1)
   4 NT-TXT-FLEX-HUNDR    (A3)
   4 NT-TXT-FLEX-POINT    (A1)
   4 NT-TXT-FLEX-CENTS    (A2)
   4 NT-TEXT-FILL-4       (A8)
   4 NT-TXT-FLEX-MM       (A2)
   4 NT-TXT-FLEX-FILL1    (A1)
   4 NT-TXT-FLEX-DD       (A2)
   4 NT-TXT-FLEX-FILL2    (A1)
   4 NT-TXT-FLEX-YYYY     (A4)
   4 NT-TXT-FILL-5        (A22)
 2 REDEFINE NOTE-TEXT                          /*  all POL ISS notes
  3 NOTE-TEXT-OCC-POL     (60)
   4 NT-TXT-LBL-POL       (A9)
   4 NT-TXT-POL-SPACE     (A1)
   4 NT-TXT-POL-MM        (A2)
   4 NT-TXT-POL-SLASH-1   (A1)
   4 NT-TXT-POL-DD        (A2)
   4 NT-TXT-POL-SLASH-2   (A1)
   4 NT-TXT-POL-YYYY      (A4)
   4 NT-TXT-POL-REST      (A40)
*
1 #COUNTERS
 2 #I1                    (I2)
*
1 #UPDATE                 (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
RESET #UPDATE
FIND (1) NT-V WITH ID-NUMBER = #ID-NUMBER
  FOR #I1 1 TO 60
    IF NOTE-TEXT (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
*
    IF NT-TXT-LBL (#I1)         = '*1035 EXCH $'
     AND NT-TXT-1035-MM (#I1)   = #OLD-ISSUE-MM
     AND NT-TXT-1035-DD (#I1)   = #OLD-ISSUE-DD
     AND NT-TXT-1035-YYYY (#I1) = #OLD-ISSUE-YYYY
      MOVE #NEW-ISSUE-MM       TO NT-TXT-1035-MM (#I1)
      MOVE #NEW-ISSUE-DD       TO NT-TXT-1035-DD (#I1)
      MOVE #NEW-ISSUE-YYYY     TO NT-TXT-1035-YYYY (#I1)
      MOVE TRUE                TO #UPDATE
    END-IF
*
    IF NT-TXT-LBL-FLEX (#I1)    = '*FLEX AMT $'
     AND NT-TXT-FLEX-MM (#I1)   = #OLD-ISSUE-MM
     AND NT-TXT-FLEX-DD (#I1)   = #OLD-ISSUE-DD
     AND NT-TXT-FLEX-YYYY (#I1) = #OLD-ISSUE-YYYY
      MOVE #NEW-ISSUE-MM       TO NT-TXT-FLEX-MM (#I1)
      MOVE #NEW-ISSUE-DD       TO NT-TXT-FLEX-DD (#I1)
      MOVE #NEW-ISSUE-YYYY     TO NT-TXT-FLEX-YYYY (#I1)
      MOVE TRUE                TO #UPDATE
    END-IF
*
    IF NT-TXT-LBL-POL (#I1)     = '* POL ISS'
     AND NT-TXT-POL-MM (#I1)    = #OLD-ISSUE-MM
     AND NT-TXT-POL-DD (#I1)    = #OLD-ISSUE-DD
     AND NT-TXT-POL-YYYY (#I1)  = #OLD-ISSUE-YYYY
      MOVE #NEW-ISSUE-MM       TO NT-TXT-POL-MM (#I1)
      MOVE #NEW-ISSUE-DD       TO NT-TXT-POL-DD (#I1)
      MOVE #NEW-ISSUE-YYYY     TO NT-TXT-POL-YYYY (#I1)
      MOVE TRUE                TO #UPDATE
    END-IF
  END-FOR
*
  IF #UPDATE
    UPDATE
  END-IF
*
END-FIND
*
END
