* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHSBDSS - Search for SSN within Beneficiaries
*
* vxt 12/24/2015 vt create clone of ATHSBDSS program
DEFINE DATA
PARAMETER
1 #BENE-TEXT   (A79/40)            /* vt was a60/10
1 #NEW-LAYOUT         (L)
1 #FND-SSN            (L)
1 #FND-SSN-NEW-LAYOUT (L)
1 #HIDE-SSN           (L)
*
LOCAL
*
1 #BT-60   (A79)                   /* vt was a60
1 REDEFINE #BT-60
 2 #BT-X   (A1/79)                 /* vt was a1/60
1 REDEFINE #BT-60
 2 #BT-1   (A01)
 2 #BT-59  (A78)                    /* vt was a59
1 #I1        (I2)
1 #I2        (I2)
1 #I3        (I2)
1 #I4        (I2)
1 #I5        (I2)
1 #FOUND-SSN-9  (L)
1 #FOUND-SSN-11 (L)
*
1 #SSN-ON-FILE   (A11) INIT <'SSN ON FILE'>
1 REDEFINE #SSN-ON-FILE
 2 #SSN-ON-FILE-X (A1/11)
*
* 1 #ON-FILE   (A7) INIT <'ON FILE'>    /* vt
* 1 REDEFINE #ON-FILE
*  2 #ON-FILE-X (A1/7)                  /* vt 
*
1 #SSN-ON-FL   (A9) INIT <'SSN ON FL'>
1 REDEFINE #SSN-ON-FL
 2 #SSN-ON-FL-X (A1/9)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
******** ESCAPE ROUTINE /* Remove SSN masking
*
* WRITE 'top athsbdss' #BENE-TEXT (1)
RESET #FND-SSN #FND-SSN-NEW-LAYOUT
FOR #I1 1 TO 40                    /* vt was 10
  IF #BENE-TEXT (#I1) = ' '
    ESCAPE TOP
  END-IF
*
  MOVE #BENE-TEXT (#I1) TO #BT-60
*
  FOR #I2 1 TO 71                   /* vt was 52
    IF (#I1 = 40) AND (#I2 = 15)  /* bypass LST 35 of line 10    /* vt was 10
      ESCAPE BOTTOM
    END-IF
*
    RESET #FOUND-SSN-9 #FOUND-SSN-11
    IF   (#BT-X (#I2)     = MASK (N))  /* 3 numbers
     AND (#BT-X (#I2 + 1) = MASK (N))
     AND (#BT-X (#I2 + 2) = MASK (N))
      IF  ((#BT-X (#I2 + 3) = MASK (N))  /* SSN format xxxxxxxxx
       AND (#BT-X (#I2 + 4) = MASK (N))
       AND (#BT-X (#I2 + 5) = MASK (N))
       AND (#BT-X (#I2 + 6) = MASK (N))
       AND (#BT-X (#I2 + 7) = MASK (N))
       AND (#BT-X (#I2 + 8) = MASK (N)))
        MOVE TRUE TO #FOUND-SSN-9
        COMPUTE #I4 = #I2 + 8
      END-IF
*
      IF  ((#I2 LE 69)                     /* SSN format xxx?xx?xxxx     /* vt was 50
       AND (#BT-X (#I2 + 3) = '-' OR = '/' OR = ' ')
       AND (#BT-X (#I2 + 4) = MASK (N))
       AND (#BT-X (#I2 + 5) = MASK (N))
       AND (#BT-X (#I2 + 6) = '-' OR = '/' OR = ' ')
       AND (#BT-X (#I2 + 7) = MASK (N))
       AND (#BT-X (#I2 + 8) = MASK (N))
       AND (#BT-X (#I2 + 9) = MASK (N))
       AND (#BT-X (#I2 + 10) = MASK (N)))
        MOVE TRUE TO #FOUND-SSN-11
        COMPUTE #I4 = #I2 + 10                              
      END-IF
*
      IF #FOUND-SSN-9 OR #FOUND-SSN-11
        IF #NEW-LAYOUT AND #I2 = 26   /* must be ssn in the new format
          MOVE TRUE TO #FND-SSN-NEW-LAYOUT
        ELSE
          MOVE TRUE TO #FND-SSN
        END-IF
*
        IF #HIDE-SSN
          IF NOT #FND-SSN-NEW-LAYOUT
*    Below to clear out extra 'SSN' in front of 'SSN ON FILE'
            IF (#I2 GT 3)    /* 'SSN123456789'
             AND (#BT-X (#I2 - 3) = 'S')
             AND (#BT-X (#I2 - 2) = 'S')
             AND (#BT-X (#I2 - 1) = 'N')
              FOR #I5 #I2 TO 79                          /* vt was 60
                MOVE #BT-X (#I5) TO #BT-X (#I5 - 3)
              END-FOR
*
              MOVE ' ' TO #BT-X (77)              /* vt was 58
              MOVE ' ' TO #BT-X (78)              /* vt was 59
              MOVE ' ' TO #BT-X (79)              /* vt was 60
              SUBTRACT 3 FROM #I2
              SUBTRACT 3 FROM #I4
            END-IF
*
            IF (#I2 GT 4)    /* 'SSN 123456789'
             AND (#BT-X (#I2 - 4) = 'S')
             AND (#BT-X (#I2 - 3) = 'S')
             AND (#BT-X (#I2 - 2) = 'N')
             AND (#BT-X (#I2 - 1) = ' ')
              FOR #I5 #I2 TO 79                  /* vt was 60
                MOVE #BT-X (#I5) TO #BT-X (#I5 - 4)
              END-FOR
*
              MOVE ' ' TO #BT-X (76)     /* vt was 57
              MOVE ' ' TO #BT-X (77)     /* vt was 58
              MOVE ' ' TO #BT-X (78)     /* vt was 59
              MOVE ' ' TO #BT-X (79)     /* vt was 60
              SUBTRACT 4 FROM #I2
              SUBTRACT 4 FROM #I4
            END-IF
*    Above to clear out extra 'SSN'
          END-IF
*
          IF   #FOUND-SSN-9
           AND (#BT-X (78) = ' ')          /* vt was 59
           AND (#BT-X (79) = ' ')          /* vt was 60
           AND NOT #FND-SSN-NEW-LAYOUT
            FOR #I5 77 TO #I4 STEP -1  /* make room for 'SSN ON FILE'     /* vt was 58
              MOVE #BT-X (#I5) TO #BT-X (#I5 + 2)
            END-FOR
*
            ADD 2 TO #I4
            RESET #FOUND-SSN-9
            MOVE TRUE TO #FOUND-SSN-11
          END-IF
*
          RESET #I5
          IF #FOUND-SSN-9
            FOR #I3 #I2 TO #I4
              ADD 1 TO #I5
              MOVE #SSN-ON-FL-X (#I5) TO #BT-X (#I3)
            END-FOR
          ELSE    /* Must be #found-SSN-11
            FOR #I3 #I2 TO #I4
              ADD 1 TO #I5
              MOVE #SSN-ON-FILE-X (#I5) TO #BT-X (#I3)
            END-FOR
          END-IF
*    Below to simply replace SSN with '*'s
*         FOR #I3 #I2 TO #I4
*           IF #BT-X (#I3) = MASK (N)
*             MOVE '*' TO #BT-X (#I3)
*           END-IF
*         END-FOR
        END-IF
*
        ADD 9 TO #I2   /* jump past current SSN
      END-IF
    END-IF
  END-FOR
*
  IF #HIDE-SSN
    MOVE #BT-60 TO #BENE-TEXT (#I1)
  END-IF
END-FOR
*
END
