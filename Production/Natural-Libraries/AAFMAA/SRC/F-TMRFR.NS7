* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-TMRFR                                               *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Term Refund Rates                                     *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM  04/02/2009  None    Program Written                             *
************************************************************************
*
DEFINE FUNCTION FUNCTION-TERM-REFUND-RATE RETURNS (N7.4)
DEFINE DATA
PARAMETER
1 #SEC-KEY            (A7)
1 #DATE               (N8)
1 #FACE-AMOUNT        (N7.2)
*
LOCAL USING OBJLGENT
LOCAL
*
1 #PROGRAM            (A8)
1 #ERROR-MESSAGE      (A70)
1 #SEV-LEVEL          (A1) INIT <'S'>
1 #FACE-AMOUNT-VALUES (A50)
1 REDEFINE #FACE-AMOUNT-VALUES
 2 #BEGIN-FACE-AMOUNT (N7)
 2 #BEGIN-SPACE       (A1)
 2 #END-FACE-AMOUNT   (N7)
*
END-DEFINE
************************************************************************
*
FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY SORTED BY PRIMARY-KEY DESCENDING
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  PERFORM SET-FACE-LIMITS
*
  IF #FACE-AMOUNT = 0
    #FACE-AMOUNT := 1
  END-IF
*  
  IF   (#DATE GE EFFECTIVE-DATE AND #DATE LE END-DATE)
   AND (#FACE-AMOUNT GT #BEGIN-FACE-AMOUNT AND #FACE-AMOUNT LE #END-FACE-AMOUNT)
     IF APPROVED-FLAG NE 'A'
        ESCAPE BOTTOM
     END-IF
*
     FUNCTION-TERM-REFUND-RATE := GEN-NUMBER-VALUE / 100
     ESCAPE ROUTINE
  END-IF
END-FIND
*
***** no rates found
*
#PROGRAM := *PROGRAM
COMPRESS 'There are NO active records with the requested key'
    #SEC-KEY INTO #ERROR-MESSAGE
CALLNAT 'OBJNBERR' #PROGRAM #ERROR-MESSAGE #SEV-LEVEL
*
************************************************************************
DEFINE SUBROUTINE SET-FACE-LIMITS
************************************************************************
*
#BEGIN-FACE-AMOUNT   := 0
#END-FACE-AMOUNT     := 9999999
*
IF GEN-TABLE-V.GEN-ALPHA-VALUE NE ' '
  #FACE-AMOUNT-VALUES := GEN-TABLE-V.GEN-ALPHA-VALUE
END-IF
*
END-SUBROUTINE
*
END-FUNCTION
*
END
