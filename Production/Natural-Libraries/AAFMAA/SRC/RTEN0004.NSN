* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram RTEN0004.
/**
/** :author nguentchev
/* Only for VAWL
/* Curently the only place policy version is stored is Application tracking
/*
DEFINE DATA
PARAMETER
1 #CN        (N6)
1 #SEGMENT   (I2)
1 #VERSION   (A1)  /* OUT
LOCAL
1 AT-V VIEW OF A-APPL-TRACKING
  2 PLAN
  2 ID-NUMBER
  2 MEMBER-CN
  2 ACTION-DATE
  2 AT-STATUS
  2 RATE-DATE
  2 POLICY-NUMBER (A16)
  2 POLICY-VERSION (A1)
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 C*ACTIVE-SEGMENTS  (N3)
  2 PLAN               (A3/9)
  2 MONTH-ALLOC-AMT    (P7.2/9)
  2 ISSUE-DATE         (N8/9)
  2 POLICY-ID          (A16/9)
*
1 #VER-DATE (N8) CONST <20151231>
1 J   (N3)
END-DEFINE
/*
RESET #VERSION
IF #CN = 0 OR #SEGMENT = 0
  ESCAPE ROUTINE
END-IF
FIND (1) ST-V WITH ID-NUMBER = #CN
  IF ST-V.PLAN ( #SEGMENT ) = 'OL' OR= '7P' OR= '20P' OR= '30P' OR= 'SP'
    FIND AT-V WITH CONTACT-ID = ST-V.MEMBER-CONTACT-ID
      IF AT-V.POLICY-NUMBER = ST-V.POLICY-ID ( #SEGMENT )
        #VERSION := AT-V.POLICY-VERSION
      END-IF
    END-FIND
    DECIDE FOR FIRST CONDITION
      WHEN ISSUE-DATE (#SEGMENT) < 19860701
        #VERSION := '1'
      WHEN ISSUE-DATE (#SEGMENT) < 19970101
        #VERSION := '2'
      WHEN ISSUE-DATE (#SEGMENT) >= 19970101
          AND MONTH-ALLOC-AMT (#SEGMENT) =  999.99
        #VERSION := '2'
      WHEN ISSUE-DATE (#SEGMENT) <= #VER-DATE
        #VERSION := '3'
      WHEN NONE
        #VERSION := '4'
    END-DECIDE
    IF #VERSION = '4'
      /* Pipeline policies with issue date for Version 4
      FIND AT-V WITH CONTACT-ID = ST-V.MEMBER-CONTACT-ID
        IF AT-V.POLICY-NUMBER = ST-V.POLICY-ID ( #SEGMENT )
          IF AT-V.POLICY-VERSION NE ' '
            #VERSION := AT-V.POLICY-VERSION
          ELSE
            IF AT-V.RATE-DATE > 0
                AND AT-V.RATE-DATE <= #VER-DATE
              #VERSION := '3'
            END-IF
          END-IF
        ELSE
          IF AT-V.RATE-DATE > 0
              AND AT-V.RATE-DATE <= #VER-DATE
            #VERSION := '3'
          END-IF
        END-IF
      END-FIND
    END-IF
  END-IF
END-FIND
/*
END
