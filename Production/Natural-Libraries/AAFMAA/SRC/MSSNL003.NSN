* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram MSSNL003.
/**
/** :author nguentchev
/* TODO Enter your code here
/*
/*        Load product data except DEAD code
/*
/*  Input parms are: CN, DN-CODE, USER COMPANY
/*  InOut: #ISSUE-DATE
/*
DEFINE DATA
PARAMETER
1 #CN                 (N6)
1 #CODE               (A1)
1 #USER-COMPANY       (A1)
1 #ISSUE-DATE-O       (N8)
1 #PROGRAM            (A8)
1 #USE-TIMX           (L)   /* IF FALSE USE ISSUE DATE
LOCAL
1 #CADET    (A3/4) CONST <'FC','FCR','AC','ACR'>
1 #RESERVE  (A3/8) CONST <'FR','FRA','AR','ARA','CGA','CGR','MR','NRA'>
1 #NGUARD   (A3/4) CONST <'FG','FGA','AGA','AG'>
1 #VETERAN  (A3/2) CONST <'P','V'>
1 ACTIVE    (A1)   CONST <'D'>
1 CLAIM     (A1)   CONST <'E'>
1 BLANK     (A1)   CONST <' '>
1 #CAP-PAID-OFF         (L)
1 #MD-PLAN              (A3)
1 #I1                   (I2)
1 #CNT                  (I2)
1 #E-DATE               (N8)
1 #ISN                  (P8)
*
1 PS-V VIEW OF A-PRODUCT-STATUS
  2 ADD-PROG      (A8)
  2 PRODUCT_ID    (A16)
  2 FACE_AMOUNT   (N7.2)
  2 DEATH_BENEFIT (N7.2)
  2 PRODUCT_CODE  (A3)
  2 ID-NUMBER     (N6.0)
  2 MEMBER_TYPE   (A1)
  2 STATUS_CODE   (A16) 2 REDEFINE STATUS_CODE
    3 MD-CODE     (A13)
  2 ACTIVE_CODE   (A1)
  2 STATUS_TIME   (A15) 2 REDEFINE STATUS_TIME
    3 #YYYYMMDD   (A8)
    3 #HHIISST    (A7)
  2 MIL-BRANCH    (A1)
  2 MIL-STATUS    (A1)
  2 AGE           (N3.0)
  2 PAY-GRADE     (A3)
  2 LIVE-CHANGE   (N1)
1 CNT-V VIEW OF A-CONTACTS
  2 DATE-OF-DEATH
  2 DATE-OF-BIRTH
  2 RANK-ID
1 RR-V VIEW OF A-RANK-REFERENCE
  2 PAY-GRADE  (A3)
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 SPOUSE-CONTACT-ID
  2 ID-NUMBER
  2 MILITARY-SERVICE (A3)
  2 MILITARY-STATUS  (A1)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 NUMBER-MEMBERSHIPS
  2 C*ACTIVE-SEGMENTS (N3)
  2 PLAN          (9)
  2 STATUS        (9)
  2 ISSUE-DATE    (9)
  2 CONV-DURATION (9)
  2 DEATH-BENEFIT (9)
  2 FACE-AMOUNT   (9)
*
1 AP-NBR          (N2)
1 #RID-PLAN       (A2)
1 #N-O            (A2)
1 #MD-CODES       (A10/7)
END-DEFINE
*
IF #CODE = 'D'   /* Dead code
  CALLNAT 'MSSNL005'
    #CN
    #CODE
    #USER-COMPANY
    #ISSUE-DATE-O
    #PROGRAM
  ESCAPE ROUTINE
END-IF
RESET #CAP-PAID-OFF PS-V
FL.
FIND (1) ST-V WITH ID-NUMBER = #CN
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  #ISN := *ISN (FL.)
  FIND (1) CNT-V WITH ID-NUMBER = #CN
    PS-V.AGE := GET-AGE-2 (< CNT-V.DATE-OF-BIRTH >)
  END-FIND
  IF ST-V.INTRO-FLAG = 'S' OR= 'K' OR= 'G'
    PS-V.MEMBER_TYPE := ST-V.INTRO-FLAG
  ELSE
    PS-V.MEMBER_TYPE := 'M'
  END-IF
  DECIDE FOR FIRST CONDITION
    WHEN MILITARY-STATUS = 'C' AND MILITARY-SERVICE = 'V'
      PS-V.MIL-STATUS := 'N'  /* NON MILITARY
    WHEN MILITARY-STATUS = 'R'
      PS-V.MIL-STATUS := 'R'  /* RETIRED
    WHEN MILITARY-STATUS = 'C' AND NOT MILITARY-SERVICE = #VETERAN (*)
      PS-V.MIL-STATUS := 'V'  /* VETERAN
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #NGUARD (*)
      PS-V.MIL-STATUS := 'G'  /* NATIONAL GUARD
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #RESERVE (*)
      PS-V.MIL-STATUS := 'Z'  /* RESERVE
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #CADET (*)
      PS-V.MIL-STATUS := 'C'  /* CADET
    WHEN NONE
      PS-V.MIL-STATUS := 'A'  /* Active
  END-DECIDE
  PS-V.MIL-BRANCH := MILITARY-SERVICE
  FIND (1) RR-V WITH RANK-ID = CNT-V.RANK-ID
    PS-V.PAY-GRADE := RR-V.PAY-GRADE
  END-FIND
*
  FOR #I1 1 TO C*ACTIVE-SEGMENTS
    /* ONLY MATCHED POLICY IS RECORDED
    IF ST-V.ISSUE-DATE (#I1) = #ISSUE-DATE-O
      IF   (#CODE = 'V')   /* Laps-cap but policy still exists
          AND (CONV-DURATION (#I1) = 999)
          AND (STATUS (#I1) = 'D')
        MOVE TRUE TO #CAP-PAID-OFF
      END-IF
      MOVE ST-V.PLAN          (#I1) TO #MD-PLAN
      MOVE ST-V.FACE-AMOUNT   (#I1) TO PS-V.FACE_AMOUNT
      MOVE ST-V.DEATH-BENEFIT (#I1) TO PS-V.DEATH_BENEFIT
      CALLNAT 'GPLNMSXX' PS-V.PRODUCT_ID  #CN #I1
      IF #MD-PLAN = BLANK
        #MD-PLAN := '*'
      END-IF
      DECIDE FOR FIRST CONDITION
        WHEN #MD-PLAN = MASK (N'PF')
            OR #MD-PLAN =MASK (NN'F')
          #MD-PLAN := 'FLX'
        WHEN F-LT2PLUS-TRUE (< #MD-PLAN >)
          #MD-PLAN := 'LT+'
        WHEN F-LT2L-TRUE (< #MD-PLAN >)
          #MD-PLAN := 'LT2'
        WHEN NONE
          IGNORE
      END-DECIDE
      PS-V.PRODUCT_CODE := #MD-PLAN
      PS-V.ADD-PROG := #PROGRAM
      PS-V.ID-NUMBER := #CN
      IF #USE-TIMX
        MOVE EDITED *TIMX (EM=YYYYMMDDHHIISST) TO PS-V.STATUS_TIME
      ELSE
        MOVE EDITED #ISSUE-DATE-O (EM=99999999) TO #YYYYMMDD
        MOVE EDITED *TIMX (EM=HHIISST) TO #HHIISST
      END-IF
      PS-V.LIVE-CHANGE := LifeCount (< #CODE,#ISN,PS-V.PRODUCT_ID >)  
*
      CALLNAT 'MSSNGCDS'
        #CODE
        #ISSUE-DATE-O
        #USER-COMPANY
        ST-V.INTRO-FLAG
        ST-V.PROCESS-IND
        ST-V.NUMBER-MEMBERSHIPS
        #RID-PLAN
        #MD-PLAN
        #N-O
        #CAP-PAID-OFF
        MD-CODE        
      ESCAPE BOTTOM  /* ONLY MATCHED POLICY IS RECORDED
    END-IF
  END-FOR
END-FIND
*
IF PS-V.PRODUCT_ID NE BLANK
  PS-V.ACTIVE_CODE := #CODE
  SEPARATE MD-CODE INTO #MD-CODES(*) WITH DELIMITER BLANK IGNORE
  MD-CODE := #MD-CODES (1)
  STORE PS-V
END-IF
END
