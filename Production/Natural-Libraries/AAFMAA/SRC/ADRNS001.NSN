* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRNS001
* DESCRIPTION: PF4 FUNCTION - Search function for New Contacts
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  08/18/2010  None Creation
* RSE  12/08/2010  RE1  Add DEATH-CERT-CODE
* RSE  01/17/2012  RE2  Reset SSN
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-ORIG-ID      (N6)
1 #CO-ID-1          (N8)
1 #ROLE-2-CD        (A1)
1 #CONTACT-ID       (N8)
1 #SSN              (N9)
1 #FIRST-NAME       (A25)
1 #MIDDLE-NAME      (A25)
1 #LAST-NAME        (A25)
1 #SUFFIX           (A10)
1 #ORG-NAME         (A75)   /* RE2
1 #SEX              (A1)
1 #DOB              (A8)
1 #BCC              (A1)
1 #DOD              (A8)
1 #DCC              (A1)   /* RE1
1 #DOM              (A8)
1 #DIV              (A8)
1 #CN               (N6)
1 #LST-UPD          (A10)
1 #LST-USR          (A22)
1 #NOT-FOUND        (L)
1 #PF3              (L)
1 #SRCH-TYPE        (A3)
LOCAL USING A-CTDATA
LOCAL
1 CONT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 DATE-OF-BIRTH
  2 SSN
  2 DATE-OF-DEATH
  2 BIRTH-CERT-CODE
  2 DEATH-CERT-CODE
  2 GENDER-CD
  2 LAST-DATE-UPD
  2 LAST-USER-UPD
*
1 CONT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 CONTACT-2-ID
  2 ROLE-1-CD
  2 ROLE-2-CD
*
1 #CN-MARK     (A1)
1 #CI-MARK     (A1)
1 #LN-MARK     (A1)
1 #FN-MARK     (A1)
1 #SSN-MARK    (A1)
1 #DOB-MARK    (A1)
1 #ORG-MARK    (A1)
1 #SRCH-CN     (N6)
1 #SRCH-CI     (N8)
1 #SRCH-LAST-NAME(A25)
1 #SRCH-FIRST-NAME(A25)
1 #SRCH-SSN    (A9)
1 REDEFINE #SRCH-SSN
  2 #SRCH-SSN-N(N9)
1 #SRCH-DOB    (N8)
1 #SRCH-ORG    (A75)
1 #MARK        (N2)
1 #MSG         (A44)
1 #CID-PARM    (N8)
1 #SRCH        (L)
1 #ORGANIZATION(A50)
1 #YYYYMMDD    (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A    (A4)
  2 #MM-A      (A2)
  2 #DD-A      (A2)
1 #PAGE        (N2)
1 #TOT-PAGES   (N2)
1 #I           (I4)
1 #J           (I4)
1 #L           (I4)
1 #MAX-CODES   (I4)
1 #ISN         (P8)
1 #ISNS        (P8/99)
1 #CID         (N8/15)
1 #ID-NUMBER   (N6/15)
1 #SELECTION   (A1/15)
1 #SELECTION-CV(C/15)
*
END-DEFINE
*
DEFINE WINDOW Search
  SIZE 17 * 61
  BASE 6/5
  FRAMED ON POSITION SYMBOL AUTO
*
SET WINDOW 'Search'
INPUT WINDOW='Search' WITH TEXT #MSG USING MAP 'ADRM0006'
*
IF *PF-KEY = 'PF3'
  RESET #SSN                  /* RE2
  MOVE TRUE TO #PF3
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF
*
RESET #MARK
DECIDE FOR EVERY CONDITION
  WHEN #CN-MARK > ' '
    ADD 1 TO #MARK
  WHEN #CI-MARK > ' '
    ADD 1 TO #MARK
  WHEN #SSN-MARK > ' '
    ADD 1 TO #MARK
  WHEN #LN-MARK > ' '
    ADD 1 TO #MARK
  WHEN #ORG-MARK > ' '
    ADD 1 TO #MARK
  WHEN #MARK > 1
    REINPUT 'Make only one selection'
      MARK 1
  WHEN NONE
    IGNORE
END-DECIDE
*
DECIDE FOR FIRST CONDITION
  WHEN #CN-MARK > ' '
    IF #CN-MARK NE 'X'
      REINPUT 'Please mark selection with an "X".'
        MARK *#CN-MARK
    ELSE
      IF #SRCH-CN = 0
        REINPUT 'Please enter CN.'
          MARK *#SRCH-CN
      ELSE
        PERFORM SEARCH-BY-CN
      END-IF
    END-IF
  WHEN #CI-MARK > ' '
    IF #CI-MARK NE 'X'
      REINPUT 'Please mark selection with an "X".'
        MARK *#CI-MARK
    ELSE
      IF #SRCH-CI = 0
        REINPUT 'Please enter CI.'
          MARK *#SRCH-CI
      ELSE
        PERFORM SEARCH-BY-CI
      END-IF
    END-IF
  WHEN #SSN-MARK > ' '
    IF #SSN-MARK NE 'X'
      REINPUT 'Please mark selection with an "X".'
        MARK *#SSN-MARK
    ELSE
      MOVE #SRCH-SSN-N TO #SSN
      PERFORM SEARCH-BY-SSN
    END-IF
*  WHEN #LN-MARK > ' '
*    REINPUT 'Search is not yet available.'
*      Mark 1
*    IF #LN-MARK NE 'X'
*      REINPUT 'Please mark selection with an "X".'
*        MARK *#LN-MARK
*    ELSE
*      PERFORM SEARCH-BY-LN-OR-COMBO
*    END-IF
  WHEN #ORG-MARK > ' '
    IF #ORG-MARK NE 'X'
      REINPUT 'Please mark selection with an "X".'
        MARK *#ORG-MARK
    ELSE
      IF #SRCH-ORG = ' '
        REINPUT 'Please enter a value.'
          MARK *#SRCH-ORG
      END-IF
      PERFORM SEARCH-BY-ORGANIZATION
      IF #NOT-FOUND
        REINPUT 'Value entered does not match any record'
          MARK *#SRCH-ORG
      END-IF
    END-IF
  WHEN NONE
    REINPUT 'Please make one selection.'
      MARK 1
END-DECIDE
*
RESET #SSN                  /* RE2
SET WINDOW OFF
******************************
DEFINE SUBROUTINE SEARCH-BY-CN
******************************
MOVE 'CN' TO #SRCH-TYPE
IF #SRCH-CN = #CGA-ORIG-ID
  REINPUT 'CN owned by this Member.'
    MARK *#SRCH-CN
END-IF
*
FIND CONT-V WITH ID-NUMBER = #SRCH-CN
  IF NO RECORD FOUND
    REINPUT 'CN does not exist.'
      MARK *#SRCH-CN
  END-NOREC
  MOVE CONTACT-ID TO #CONTACT-ID
  IF SSN > 0
    MOVE SSN TO #SSN
  END-IF
END-FIND
*
FIND CONT-REL-V WITH CONTACT-1-ID = #CO-ID-1
  FIND CONT-V WITH CONTACT-ID = CONTACT-2-ID
    IF ID-NUMBER = #SRCH-CN
      DECIDE ON FIRST VALUE OF ROLE-2-CD
        VALUE 'K','G','D'
          MOVE 'CN owned by another DEPN of this Member.' TO #MSG
        VALUE 'S'
          IF #ROLE-2-CD NE 'X'
            MOVE 'CN owned by the Spouse of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        VALUE 'X'
          IF #ROLE-2-CD NE 'S'
            MOVE 'CN owned by the Fmr-Sps of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        NONE
          MOVE 'CN owned by an OTHER relation of Member.' TO #MSG
      END-DECIDE
      REINPUT #MSG
        MARK *#SRCH-CN
    END-IF
  END-FIND
END-FIND
CALLNAT 'ADRNS003' #CGA-ORIG-ID #CID-PARM #SRCH-SSN-N #FIRST-NAME #MIDDLE-NAME #LAST-NAME #SUFFIX #SEX #DOB
  #BCC #DOD #DCC #DOM #DIV #SRCH-CN #LST-UPD #LST-USR #NOT-FOUND #PF3         /* RE1
MOVE #SRCH-CN TO #CN
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE SEARCH-BY-CI
******************************
MOVE 'CI' TO #SRCH-TYPE
IF #SRCH-CI = #CO-ID-1
  REINPUT 'CI already belongs to this Member.'
    MARK *#SRCH-CI
END-IF
*
FIND CONT-V WITH CONTACT-ID = #SRCH-CI
  IF NO RECORD FOUND
    REINPUT 'CI does not exist.'
      MARK *#SRCH-CI
  END-NOREC
  MOVE CONTACT-ID TO #CONTACT-ID
  IF SSN > 0
    MOVE SSN TO #SSN
  END-IF
END-FIND
*
FIND CONT-REL-V WITH CONTACT-1-ID = #CO-ID-1
  FIND CONT-V WITH CONTACT-ID = CONTACT-2-ID
    IF CONTACT-ID = #SRCH-CI
      DECIDE ON FIRST VALUE OF ROLE-2-CD
        VALUE 'K','G','D'
          MOVE 'CI belongs to another DEPN of this Member.' TO #MSG
        VALUE 'S'
          IF #ROLE-2-CD NE 'X'
            MOVE 'CI belongs to the Spouse of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        VALUE 'X'
          IF #ROLE-2-CD NE 'S'
            MOVE 'CI belongs to the Fmr-Sps of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        NONE
          MOVE 'CI belongs to an OTHER relation of Member.' TO #MSG
      END-DECIDE
      REINPUT #MSG
        MARK *#SRCH-CN
    END-IF
  END-FIND
END-FIND
CALLNAT 'ADRNS002' #CGA-ORIG-ID #SRCH-CI #SRCH-SSN-N #FIRST-NAME #MIDDLE-NAME #LAST-NAME #SUFFIX #SEX #DOB
  #BCC #DOD #DCC #DOM #DIV #SRCH-CN #LST-UPD #LST-USR #NOT-FOUND #PF3          /* RE1
MOVE #SRCH-CN TO #CN
*
END-SUBROUTINE
*
*******************************
DEFINE SUBROUTINE SEARCH-BY-SSN
*******************************
MOVE 'SSN' TO #SRCH-TYPE
IF #SRCH-SSN-N > 0
  FIND CONT-V WITH SSN = #SRCH-SSN-N
    IF NO RECORD FOUND
      REINPUT 'SSN does not exist.'
        MARK *#SRCH-SSN
    END-NOREC
  END-FIND
END-IF
*
FIND CONT-V WITH ID-NUMBER = #CGA-ORIG-ID
  IF SSN > 0 AND SSN = #SRCH-SSN-N
    REINPUT 'SSN already belongs to this Member.'
      MARK *#SRCH-SSN
  END-IF
END-FIND
*
FIND CONT-REL-V WITH CONTACT-1-ID = #CO-ID-1
  FIND CONT-V WITH CONTACT-ID = CONTACT-2-ID
    IF SSN > 0 AND SSN = #SRCH-SSN-N
      DECIDE ON FIRST VALUE OF ROLE-2-CD
        VALUE 'K','G','D'
          MOVE 'SSN belongs to a DEPN of this Member.' TO #MSG
        VALUE 'S'
          IF #ROLE-2-CD NE 'X'
            MOVE 'SSN belongs to the Spouse of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        VALUE 'X'
          IF #ROLE-2-CD NE 'S'
            MOVE 'SSN belongs to the Fmr-Sps of this Member.' TO #MSG
          ELSE
            ESCAPE BOTTOM
          END-IF
        NONE
          MOVE 'SSN belongs to an OTHER relation of Member.' TO #MSG
      END-DECIDE
      REINPUT #MSG
        MARK *#SRCH-SSN
    END-IF
  END-FIND
END-FIND
IF #SRCH-SSN-N = 0
  MOVE TRUE TO #SRCH
END-IF
CALLNAT 'GCSSNSXX' #CGA-ORIG-ID #CID-PARM #ROLE-2-CD #SRCH-SSN-N #FIRST-NAME #MIDDLE-NAME #LAST-NAME #SUFFIX #ORG-NAME #SEX #DOB
  #BCC #DOD #DCC #DOM #DIV #CN #LST-UPD #LST-USR #NOT-FOUND #PF3 #SRCH   /* RE1
MOVE #CID-PARM TO #CONTACT-ID
*
END-SUBROUTINE
****************************************
DEFINE SUBROUTINE SEARCH-BY-ORGANIZATION
****************************************
CALLNAT 'ADRNS004' #CGA-ORIG-ID #CONTACT-ID #SRCH-ORG #SRCH-SSN-N #FIRST-NAME #MIDDLE-NAME #LAST-NAME #SUFFIX #SEX #DOB
  #BCC #DOD #DCC #DOM #DIV #CN #LST-UPD #LST-USR #NOT-FOUND #PF3         /* RE1
*
END-SUBROUTINE
*
END
