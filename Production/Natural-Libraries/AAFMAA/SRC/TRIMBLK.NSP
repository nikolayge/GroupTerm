* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
****************************************************************************
* PROGRAM: TRIMBLK
* WRITTEN: 08/04/2006 ANDY BERGMANIS, SOFTWARE AG
* PURPOSE: TO EXCUTE A KORN SHELL SCRIPT THAT WILL TRIM TRAILING BLANKS FROM
*          FILES WHICH ARE TO BE INPUT TO AN FTP PROCESS. THIS PROGRAM IS
*          EXECUTED IN A BATCH JOB STEP IMMEDIATELY PRECEDING THE FTP STEP,
*          AND WILL TRIM ANY OF THE FILES IN THAT STEP.
****************************************************************************
DEFINE DATA LOCAL
1 #MAX-LENGTH       (N3) CONST <200>
1 #PARM-CH          (A1/#MAX-LENGTH)
1 REDEFINE #PARM-CH
  2 #PARM-IN        (A100)
1 #START            (N3)
1 #FROM             (N3)
1 #LENGTH           (N3)
1 #I                (N3)
1 #PATH-IN          (A20)
1 #SUBDIR           (A20)
1 #FILE-IN          (A100)
1 #TEXT-EXTRACT     (A200)
1 #CMD-OUT          (A200)
1 rc                (I4)
END-DEFINE
**
FORMAT LS=101
REPEAT
**
  RESET #PARM-IN
  INPUT (IP=OFF) #PARM-IN
  PRINT NOTITLE #PARM-IN
**
  IF #PARM-IN EQ ' '  /* NO MORE TO DO
    ESCAPE BOTTOM
  END-IF
**
  #START := 1                  /* GET PATH
  PERFORM PARSE-TEXT
  #PATH-IN := #TEXT-EXTRACT
  #START := #I                 /* GET FILENAME
  PERFORM PARSE-TEXT
  IF #TEXT-EXTRACT NE ' '      /* FILENAME WITH PATH
    #FILE-IN := #TEXT-EXTRACT
  ELSE
    MOVE #PATH-IN TO #FILE-IN  /* FULL PATH GIVEN
    RESET #PATH-IN
  END-IF
**
  WRITE #PATH-IN  (AL=70)
  WRITE #FILE-IN  (AL=70)
**
  MOVE  '/usr/SAG/AAFMAA/scripts/trimblanks.sh /usr/SAG/AAFMAA/batch/workfiles/' TO #TEXT-EXTRACT
  DECIDE FOR FIRST CONDITION
    WHEN SUBSTR(#PATH-IN,1,1) EQ 'P'
      #SUBDIR := SUBSTR(#PATH-IN,2,19)
      COMPRESS #TEXT-EXTRACT 'PERMDA/' #SUBDIR '/' #FILE-IN INTO #CMD-OUT LEAVING NO SPACE
    WHEN SUBSTR(#PATH-IN,1,1) EQ 'S'
      #SUBDIR := SUBSTR(#PATH-IN,2,19)
      COMPRESS #TEXT-EXTRACT 'SYSDA/'  #SUBDIR '/' #FILE-IN INTO #CMD-OUT LEAVING NO SPACE    
    WHEN #PATH-IN EQ 'TEMP'
      COMPRESS #TEXT-EXTRACT 'TEMP/'               #FILE-IN INTO #CMD-OUT LEAVING NO SPACE
    WHEN NONE
      COMPRESS 'trimblanks.sh' #FILE-IN INTO #CMD-OUT
  END-DECIDE
**
  PRINT '[' #CMD-OUT ']'
  CALL 'SHCMD' #CMD-OUT
  rc := RET('SHCMD')
  WRITE *PROGRAM *DATX *TIMX 'rc=' rc 
**
END-REPEAT
****************************
DEFINE SUBROUTINE PARSE-TEXT
****************************
RESET #FROM #LENGTH
** FIND FIRST NON-BLANK
FOR #I EQ #START TO #MAX-LENGTH
  IF #PARM-CH (#I) NE ' '
    #FROM := #I
    ESCAPE BOTTOM
  END-IF
END-FOR
** FIND END OF STRING
FOR #I EQ #I TO #MAX-LENGTH
  IF #PARM-CH (#I) EQ ' '
    #LENGTH := #I - #FROM
    ESCAPE BOTTOM
  END-IF
END-FOR
RESET #TEXT-EXTRACT
IF #FROM GT 0 AND #LENGTH GT 0
  #TEXT-EXTRACT := SUBSTR(#PARM-IN,#FROM,#LENGTH)
END-IF
**
END-SUBROUTINE /* PARSE-TEXT
****************************
END
