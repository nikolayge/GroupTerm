* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID : CDUELSNM
*
* DUES LETTER FOR NM SCREEN
*
* CHANGE SUCCESSOR OWNER LETTER FOR LIN BLAKELY  1099
*
************************************************************************
*                       MODIFICATION LOG
* USER   DATE      TAG     REASON
*
* PAM    07292004  PM1     Adjusted letter for mode 'M' to handle
*                          early credit card processing.
* PAM    09092004  PM2     Adjusted ezpay letter for future drafts as
*                          per Rick B.  Added smoker to category desc.
* DRW    10212004  DW3     Force EZP ltr if mode Q & 2 month till pd-to
* DRW    02222005  DW4     Add AL (Annuity Life) processing
* PAM    04192005  PM5     Added text for Single Pay policies
* PAM    05062005  PM6     Adjusted process for FPF by SSN
* PAM    05162005  PM7     Increased Years from 2 to 3 bytes.
* PAM    05172005  PM8     Added credit card processing.
* PAM    07052005  PM9     EZpay letter check used current day instead
*                          of FPF start day.
* PAM    07212005  PM10    Adjust letter for Stand/Select/Nicotine/etc
* DRW    08122005  DW11    Reject NM LTR if iss date prior to 3 mons ago
* DRW    09092005  DW12    Add Text to EZ-PAY letter
* DRW    09092005  DW13    Adjust due date to be 60 days after Entry Dat
* PAM    09132005  PM14    Removed Rider as separate entry for LTR
* DRW    03162006  DW15    Use Date-Entered-mm to do EZPay letter check
* DRW    03162006  DW16    Move Rating info to next line
* DRW    03212006  DW17    Chg EZ-Pay text and remove DW12
* DRW    06092006  DW18    Chg 'by date' to upon receipt'
* DRW    06232006  DW19    Add EZ-Pay FPF logic
* DRW    07252006  DW20    Chg Succesor Ownership processing
* DRW    11082006  DW21    Handle PUSP Pol on DW20 test
* DRW    09132007  DW22    Addf text to Successor Owner letter
* YAK    11212007  YK2     Change letter text for active duty members
* DRW  06/25/2008  DW23    Remove last sentence or RET Ltr(Too long 4 printing)
* DRW  07/16/2008  DW24    Add text for DFAS Info for Active
* DRW  07/18/2008  DW25    Chg wording on SPS/GEN new policy letter
* DRW  08/13/2008  DW26    GEN PLUS processing
* YK3  07/30/2008  YK3     GET FIRST-PLACE-FEE from AT file if it was
*                          previously stored there
* PAM  10/31/2008  PM27    Added EZPay waiting indicator
* NGG  01/30/2012  NG1     Changes for premier services
* YAK  10/15/2012  YK4     Use function to identify Level Term II plans
* YAK  11/08/2012  YK5     Add No Medical Term policy (EZ)
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* TMT  0108/2013   TT1     ADDED GP2 POLICY SUPPORT
* YAK  11/08/2012  YK6     Change E-Deposit to real time
* NGG  20141023    NG2     Annuity multiple products
************************************************************************
*
*
DEFINE DATA
*
PARAMETER
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 #CGA-SCR-COMMAND (A2)
1 #CGA-CLERK       (A3)
1 #SSN             (N9)
1 #INSURED         (A1)
1 #NAME            (A25)
1 #CN1-A           (A6)
1 REDEFINE #CN1-A
  2 #CN1-N          (N6)
1 #APREM           (N7.2)   /* NG99
1 #MPREM           (N5.2)
1 #EFF-DT          (N8)
1 REDEFINE #EFF-DT
  2 #EFF-DT-MM-N    (N2)
  2 #EFF-DT-DD-N    (N2)
  2 #EFF-DT-YYYY-N  (N4)
1 REDEFINE #EFF-DT
  2 #EFF-DT-MM-A    (A2)
  2 #EFF-DT-DD-A    (A2)
  2 #EFF-DT-YYYY-A  (A4)
1 #FPF-START       (N8)
1 REDEFINE #FPF-START
  2 #FPFS-MM        (N2)
  2 #FPFS-DD        (N2)
  2 #FPFS-YYYY      (N4)
1 #FPF-END         (N8)
1 REDEFINE #FPF-END
  2 #FPFE-MM        (N2)
  2 #FPFE-DD        (N2)
  2 #FPFE-YYYY      (N4)
1 #MODE            (A1)
1 #FPF             (N7.2)
1 #MIL-ST          (A1)
1 #MIL-ID          (A3)
1 #FPF-DUES        (N5.2)
1 #PLAN            (A3)
1 REDEFINE #PLAN
  2 #PLAN-A1        (A1)
  2 #PLAN-N2        (N2)
1 #ALOT-AMT        (N5.2)
1 #CGA-ORIG-COMMAND (A2)
1 #E-DUES           (P5.2)
1 #G-DUES           (P5.2)
1 #A-DUES           (P5.2)
1 #S-DUES           (P5.2)
1 #Q-DUES           (P5.2)
1 #FLEX-PLAN        (L)
1 #FLEX-YEARS       (N3)                                          /* PM7
1 #CAP-IND          (A1)
1 #INSURED-POL      (A8)
1 #LINKED-CN        (N6)
1 REDEFINE #LINKED-CN
  2 #LINKED-CN-A     (A6)
1 #PAYS-DUES        (L)
1 #HAS-FBR          (L)
1 #FPF-FBR          (N3.2)
1 #FBR-MON-PREM     (N3.2)
1 #CONV-DURATION    (P3)
1 #FACE-AMOUNT      (N7.2)
1 #SMOKER-FLAG      (A1)
1 #AGE              (N2)
1 #PREM-RATING      (A1)
1 #NO-FPF           (L)
1 #DB-CR-AMT        (P7.2)
1 #WORK-PREM-HOLD   (N5.2)  /* total new prem due
1 #PAD-LAST-MON-AMT (P5.2)  /* last pad amt
1 #XTRA-AMT-DUE     (P7.2)  /* A/S/Q extra amt due
1 #INDEX            (I2)    /* new segment #
1 #DEP-RCVD         (N7.2)  /* NG99
1 #DATE-ENTERED     (N8)    /* DW13
1 #EZPAY-WAITING    (L)     /* PM27
*
LOCAL
1 ST-V VIEW OF A-STATUS
  2 STATUS  (9)
  2 ISSUE-DATE (9)
  2 PLAN       (9)
  2 PAY-STATUS (9)
*
1 NT-V VIEW OF A-NOTES
  2 NOTE-TEXT  (A60/60)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEX-OCC  (60)
      4 NOTE-TEXT-60   (A60)
      4 REDEFINE NOTE-TEXT-60
        5 NOTE-TEXT-TU-POL-ISS  (A10)
        5 NOTE-TEXT-TU-MM   (A2)
        5 NOTE-TEXT-TU-2    (A1)
        5 NOTE-TEXT-TU-DD   (A2)
        5 NOTE-TEXT-TU-3    (A1)
        5 NOTE-TEXT-TU-YYYY (A4)
        5 NOTE-TEXT-TU-4    (A1)
        5 NOTE-TEXT-TU-TU   (A13)
        5 NOTE-TEXT-TU-5    (A26)
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 SSN
  2 INTRO-FLAG
  2 ACTION-FLAG
  2 DEP-RCVD
  2 FIRST-PLACE-FEE          /* YK3
  2 E-DEPOSIT-AMT            /* YK6
  2 PLAN
  2 REDEFINE PLAN
    3 PLAN-A1   (A1)
    3 PLAN-A2   (A2)
*
1 SM-V VIEW OF A-SVC-MEMBER             /* NG1
  2 SVC-FIRST-PLACE-FEE (N7.2)
*
1 #POL-OWNED-BY-OTHER (L)   /* DW20
1 #MULTIPLE-OWNER (L)
1 #NON-INS-OWNER  (L)
1 #OWNER-OCC-9 (9)
  2 #OWNER-FLAG (A1)
  2 #OWNER-CN   (N6/10)
  2 #OWNER-CN-BY-POL (N6)
  2 #OWNER-CN-SEG   (I2/9)
  2 #ISS-DATE       (N8/9)
  2 #ISS-DATE-DISP  (A10/9)
*    beneficiary info output
  2 #BENE-TYPE       (A1/2)
  2 #BENE-TEXT       (A60/10)
*
1 #EZ-WAIT  (L)
1 #FST-X    (I2)
1 #DUES   (N3.2)
1 #E-AST (L)   /* DW12
1 #I1   (I2)
1 #I2   (I2)
1 #I3   (I2)
1 #TU     (L)
1 #DO-FA     (L)
1 #INCR-ALOT   (N5.2)
1 #NEW-EZ      (N5.2)
1 #NEW-EZ-X    (A8)
1 #PAY-DESC   (A16)
1 #PLAN-DESC   (A25)
1 #CAT-DESC    (A30)
1 #AMT-DUE     (N6.2)
1 #TOT-DUE     (N6.2)
1 #TOT-DUE-X   (A11)
1 #DEP-RCVD-X  (A11)
1 #DEP-DIFF    (N6.2)
1 #DEP-DIFF-X  (A11)
1 #FPF-X       (A11)
1 #TODAY-DISP (A10)
1 #NEXT-MON-DISP (A10)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM   (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY     (N4)
    3 #MM       (N2)
  2 #DD       (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A   (A4)
  2 #MM-A     (A2)
  2 #DD-A     (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A  (A8)
*
1 #EFF-DATE-YYYYMMDD (N8)
1 REDEFINE #EFF-DATE-YYYYMMDD
  2 #EFF-DATE-YYYYMM  (N6)
  2 REDEFINE #EFF-DATE-YYYYMM
    3 #EFF-DATE-YYYY  (N4)
    3 #EFF-DATE-MM    (N2)
  2 #EFF-DATE-DD      (N2)
1 REDEFINE #EFF-DATE-YYYYMMDD
  2 #EFF-DATE-YYYYMMDD-A   (A8)
*
1 #TODAY-YYYYMMDD (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY    (A4)
  2 #TODAY-MM      (A2)
  2 #TODAY-DD      (A2)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYYMM-N  (N6)
  2 REDEFINE #TODAY-YYYYMM-N
    3 #TODAY-YYYY-N    (N4)
    3 #TODAY-MM-N      (N2)
  2 #TODAY-DD-N      (N2)
*
1 #LAPSE-DATE-X  (A10)
1 #LINKED-NAME   (A35)
1 #ISS-DT-DISP   (A10)
1 #AMT-DISP      (A9)
1 #LAST      (A25)
1 #FIRST-MID (A25)
1 #EXTRA-1   (A25)
1 #EXTRA-2   (A25)
1 #EXTRA-3   (A25)
1 #PLAN-DESC-1   (A13)
1 #PLAN-DESC-2   (A10)
*
1 #SCN-TITLE (A40) INIT <'INFORMATION ON PAYMENT OF PREMIUM'>
1 #SCN-LINE (A75/46)
1 REDEFINE #SCN-LINE
  2 #SCN-LINE-OCC (46)
    3 #SCN-LINE-A    (A60)
    3 #SCN-LINE-B    (A15)
*
1 #START-CHG  (A6)
*
1 #ASQ-DIFF (N7.2)
1 #ASQ-DIFF-X (A8)
1 #MON-DUES (N3.2)
1 #ASQ-DUES (N2.2)
1 #ASQ-FBR  (N2.2)
1 #SVC-FEE-SP (N3.2) INIT <450>
1 #SVC-FEE-2P (N2.2) INIT <18.50>
1 #SVC-FEE-3P (N2.2) INIT <12.50>
1 #SVC-FEE-4P (N1.2) INIT <9.50>
1 #SVC-FEE-5P (N1.2) INIT <7.50>
1 #SVC-FEE-6P (N1.2) INIT <6.50>
1 #SVC-FEE-7P (N1.2) INIT <6.00>
1 #SVC-FEE-10 (N1.2) INIT <4.50>
1 #SVC-FEE-20 (N1.2) INIT <3.00>
1 #SVC-FEE-30 (N1.2) INIT <2.50>
1 #SVC-FEE-OL (N1.2) INIT <2.00>
1 #SVC-FEE-TM (N1.2) INIT <2.00>
*
1 #SCN-ROW-1 (A75)
1 REDEFINE #SCN-ROW-1
  2 #SR1-A   (A15)
  2 #SR1-B   (A58)
  2 REDEFINE #SR1-B
    3 #SR1-C  (A6)
    3 #SR1-D  (A38)   /* 52
    3 #SR1-E  (A14)
*
1 #DATE      (N24) INIT <312831303130313130313031>
1 REDEFINE #DATE
  2 #DAYS   (N2/1:12)
1 #DX    (I2)
1 #MX    (I2)
1 #M-L (A108) INIT<'January  February March    April    May      '
  -'June     July     August   SeptemberOctober  November December '>
1 REDEFINE #M-L
  2 #M-SCN-LIT  (A9/1:12)
*
1 #SE-PREM     (N5.2)
1 #SE-2-MONS-PREM (N5.2)
1 #SE-DIFF  (N5.2)
1 #SE-PLAN     (A3)
1 REDEFINE #SE-PLAN
  2 #SE-PLAN-A1  (A1)
  2 #SE-PLAN-N2  (N2)
*
1 #UR-PREM     (N5.2)    /* Un Rated
1 #UR-2-MONS-PREM (N5.2)
1 #UR-DIFF  (N5.2)
*
1 #NEW-ALOT    (N5.2)
1 #WORK-AMT-X  (A8)
1 #MONTH-PREM  (N5.2)
1 #HOLD-PREM  (A8)
1 #HOLD-EFF-DD (A2)
1 #HOLD-FPFS-DD (A2)
1 #HOLD-YEAR  (A5)
1 #HOLD-ID    (A10)
1 #HOLD-NAME  (A25)
1 #WORK-DD    (N2)
1 #FREQ       (A12)
1 #COMMA   (A1) CONST<','>
1 #LAST-NAME  (A24)
1 #FIRST-NAME  (A24)
1 #OVFLOW-NAME  (A24)
1 #FPF-DISP     (A8)
*
1 #EZ-PAY-START-MM (N2)
1 #EZ-PAY-START-YYYY (N4)
1 #SC-X  (I2)
1 #MON-PREM-X (A8)
1 #MON-DUES-X (A6)
1 #MON-FBR-X (A6)
1 #MON-TOT    (N4.2)
1 #MON-TOT-X  (A8)
1 #MON-TOT-ALL-POLS-X  (A8)
1 #2-MONS-PREM   (N5.2)
1 #2-MONS-PREM-X (A9)
1 #2-MONS-DUES   (N3.2)
1 #2-MONS-DUES-X (A6)
1 #2-MONS-FBR    (N3.2)
1 #2-MONS-FBR-X  (A6)
1 #2-MONS-TOT    (N5.2)
1 #2-MONS-TOT-X  (A9)
1 #ALOT-DIFF     (N5.2)
1 #ALOT-DIFF-X   (A6)
1 #2-MONS-ALOT-DIFF   (N5.2)
1 #2-MONS-ALOT-DIFF-X (A6)
1 #TOT-MOD-PREM       (N6.2)
1 #TOT-MOD-PREM-X     (A9)
1 #XTRA-AMT-DUE-X     (A8)
1 #LAST-EZP-X    (A8)
1 #DB-CR-X       (A10)
1 #BACKDATE      (N5.2)
1 #BACKDATE-X    (A10)
1 #ONLY-FBR      (L)
1 #FPF-LINE       (A32)                                           /* PM6
1 #FPF-RIDER-LINE (A32)                                           /* PM6
1 #FPF-CV (C)                                                     /* PM6
1 #DISPLAY-FPF    (N7.2)                                          /* PM6
1 #DISPLAY-RIDER-FPF (N3.2)                                       /* PM6
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* WRITE 'top of CDUELSNM' #EFF-DT 'INDEX=' #INDEX 'PLAN=' #PLAN #CN1-A
* 'iss-dt=' ST-V.ISSUE-DATE (1)
*
COMPRESS #EFF-DT-YYYY-A #EFF-DT-MM-A #EFF-DT-DD-A INTO    /* DW11
  #EFF-DATE-YYYYMMDD-A    LEAVING NO
*
IF #INDEX = 0   /* DW20
  FIND ST-V WITH MEMBER-KEY = #CN1-A
    FOR #I1 1 TO 9
      IF  (ST-V.ISSUE-DATE (#I1) = #EFF-DATE-YYYYMMDD)
          AND ((ST-V.PLAN (#I1) = #PLAN)
          OR ((#PLAN = 'SP') AND ST-V.PAY-STATUS (#I1) = '47'))  /* DW21
        MOVE #I1 TO #INDEX
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FIND
END-IF
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 3 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
IF  (#CGA-ORIG-COMMAND = 'NM')    /* DW11
    AND (#EFF-DATE-YYYYMMDD LT #YYYYMMDD)
    AND (#CGA-SCR-SUFFIX NE 'FPF')                   /* DW13
  INPUT TEXT 'NM XXXXX LTR Invalid on policies over 3 months old'
    USING MAP 'G1000MXX'
  FETCH 'G1000PXX'
END-IF
*
FIND (1) NT-V WITH ID-NUMBER = #CGA-SCR-ID
  FOR #I1 1 TO 9 /*this only works if tu note has been created (not OVR)
    IF   (NOTE-TEXT-TU-POL-ISS (#I1) = '* POL ISS')
        AND (NOTE-TEXT-TU-TU (#I1) = 'IS A TRADE-UP')
      COMPRESS NOTE-TEXT-TU-MM (#I1) NOTE-TEXT-TU-DD (#I1)
        NOTE-TEXT-TU-YYYY (#I1) INTO #YYYYMMDD-A LEAVING NO
      IF #YYYYMMDD NE #EFF-DT
        ESCAPE TOP
      END-IF
*
      MOVE TRUE TO #TU
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
*
MOVE *DATN TO #TODAY-YYYYMMDD
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #TODAY-DISP WITH '/'
*
ADD 1 TO #MM
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
COMPRESS #MM-A #DD-A #YYYY-A INTO #NEXT-MON-DISP WITH '/'
*
SEPARATE #NAME LEFT INTO #LAST-NAME #FIRST-NAME #OVFLOW-NAME WITH
  DELIMITER #COMMA
COMPRESS #FIRST-NAME #LAST-NAME INTO #HOLD-NAME
*
IF #OVFLOW-NAME > ' '
  COMPRESS #HOLD-NAME ',' INTO #HOLD-NAME LEAVING NO SPACE
  COMPRESS #HOLD-NAME #OVFLOW-NAME INTO #HOLD-NAME
END-IF
*
IF #PLAN = ' '
  MOVE TRUE TO #ONLY-FBR
END-IF
*
* IF  (#PLAN = 'SP' OR = 'SI' OR = 'FS' OR = 'AL')            /* DW4 PM5
*  OR (#CAP-IND = 'Y')
IF  (#PLAN = 'FS') OR (#CAP-IND = 'Y')
  IF #CGA-SCR-SUFFIX = ' '
    CALLNAT 'C2009SNM' #INDEX #CN1-N #CGA-CLERK /* to set MEC indicator
  END-IF
*
  PERFORM CHECK-FOR-OWNERSHIP
  ESCAPE ROUTINE
END-IF
*
COMPRESS '(CN' #CN1-A ')' INTO #HOLD-ID LEAVING NO SPACE
COMPRESS #FPFE-YYYY '.' INTO #HOLD-YEAR LEAVING NO SPACE
*
IF #CGA-SCR-SUFFIX = 'LTR' OR = 'OVR' OR = '   ' OR = 'OLD' OR = 'FPF'
  COMPRESS 'for' #HOLD-NAME #HOLD-ID INTO #SR1-B
ELSE
  COMPRESS 'for' #HOLD-NAME #HOLD-ID INTO #SR1-D
END-IF
*
RESET #MON-DUES
IF  ((#CGA-ORIG-COMMAND = 'NM')
    AND (NOT #INSURED = 'S' OR = 'K' OR = 'G'
    OR = 'O' OR = 'P' OR = 'Q')
    AND (#APREM NE 0))  /* WILL BE ZERO IF FLEX-PLAN PUSP
    OR #PAYS-DUES
  DECIDE ON FIRST VALUE #PLAN
    VALUE '2P' MOVE #SVC-FEE-2P TO #MON-DUES
    VALUE '3P' MOVE #SVC-FEE-3P TO #MON-DUES
    VALUE '4P' MOVE #SVC-FEE-4P TO #MON-DUES
    VALUE '5P' MOVE #SVC-FEE-5P TO #MON-DUES
    VALUE '6P' MOVE #SVC-FEE-6P TO #MON-DUES
    VALUE '7P' MOVE #SVC-FEE-7P TO #MON-DUES
    VALUE '8P' MOVE #SVC-FEE-7P TO #MON-DUES
    VALUE '9P' MOVE #SVC-FEE-7P TO #MON-DUES
    VALUE '7P' MOVE #SVC-FEE-7P TO #MON-DUES
    VALUE '20P' MOVE #SVC-FEE-20 TO #MON-DUES
    VALUE '20' MOVE #SVC-FEE-20 TO #MON-DUES
    VALUE '30' MOVE #SVC-FEE-30 TO #MON-DUES
    VALUE '30P' MOVE #SVC-FEE-30 TO #MON-DUES
    VALUE 'OL' MOVE #SVC-FEE-OL TO #MON-DUES
*   VALUE 'RT' MOVE #SVC-FEE-TM TO #MON-DUES                /* YK4
*   VALUE 'LT' MOVE #SVC-FEE-TM TO #MON-DUES                /* YK4
    NONE       IGNORE
  END-DECIDE
*
* IF (#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z')                  /* YK4
  IF FUNCTION-TERM-POLICY(<#PLAN>)                            /* YK4
    MOVE #SVC-FEE-TM TO #MON-DUES
  END-IF
*
  IF #FLEX-PLAN AND (#MON-DUES = 0)
    IF #PLAN LE '19' MOVE #SVC-FEE-10 TO #MON-DUES ELSE
      IF #PLAN LE '29' MOVE #SVC-FEE-20 TO #MON-DUES ELSE     
        IF #PLAN LE '34' MOVE #SVC-FEE-30 TO #MON-DUES ELSE
          MOVE #SVC-FEE-OL TO #MON-DUES
        END-IF END-IF END-IF
  END-IF
END-IF
*
COMPUTE #MONTH-PREM = #APREM / 12
MOVE EDITED #MONTH-PREM (EM=Z,ZZ9.99) TO #MON-PREM-X
MOVE EDITED #MON-DUES (EM=ZZ9.99)     TO #MON-DUES-X
MOVE EDITED #FBR-MON-PREM (EM=ZZ9.99) TO #MON-FBR-X
COMPUTE #MON-TOT = #MONTH-PREM + #MON-DUES + #FBR-MON-PREM
MOVE EDITED #MON-TOT (EM=Z,ZZ9.99)     TO #MON-TOT-X
MOVE EDITED #WORK-PREM-HOLD  (EM=Z,ZZ9.99) TO #MON-TOT-ALL-POLS-X
*
COMPUTE #2-MONS-PREM = (#MONTH-PREM * 2)
MOVE EDITED #2-MONS-PREM (EM=ZZ,ZZ9.99) TO #2-MONS-PREM-X
COMPUTE #2-MONS-DUES = (#MON-DUES * 2)
MOVE EDITED #2-MONS-DUES (EM=ZZ9.99) TO #2-MONS-DUES-X
COMPUTE #2-MONS-FBR = (#FBR-MON-PREM * 2)
MOVE EDITED #2-MONS-FBR (EM=ZZ9.99) TO #2-MONS-FBR-X
COMPUTE #2-MONS-TOT  = (#2-MONS-PREM + #2-MONS-DUES + #2-MONS-FBR)
MOVE EDITED #2-MONS-TOT  (EM=ZZ,ZZ9.99) TO #2-MONS-TOT-X
COMPUTE #ALOT-DIFF = #WORK-PREM-HOLD - #ALOT-AMT
MOVE EDITED #ALOT-DIFF (EM=ZZ9.99) TO #ALOT-DIFF-X
*
COMPRESS #EFF-DT-MM-A #EFF-DT-DD-A #EFF-DT-YYYY-A INTO #ISS-DT-DISP
  WITH '/'
COMPRESS 'Eff' #ISS-DT-DISP INTO #SR1-E
*
MOVE (AD=P) TO #FPF-CV                                            /* PM6
IF #CGA-SCR-SUFFIX = 'LTR' OR = 'FPF' OR = ' '                    /* YK6
  FIND AT-V WITH SSN = #SSN
    IF  (AT-V.INTRO-FLAG = 'V')
        OR (AT-V.ACTION-FLAG NE 'A')
        OR (AT-V.PLAN-A1 NE #PLAN-A1) /* best we can do
      ESCAPE TOP
    END-IF
*
*   MOVE AT-V.DEP-RCVD TO #DEP-RCVD                               /* YK6
    #DEP-RCVD := AT-V.DEP-RCVD + AT-V.E-DEPOSIT-AMT               /* YK6
    MOVE AT-V.FIRST-PLACE-FEE TO #FPF                             /* YK3
    ESCAPE BOTTOM
  END-FIND
*
  MOVE (AD=U) TO #FPF-CV                                          /* PM6
  MOVE 'Enter First Placement Fee' TO #FPF-LINE                   /* PM6
* MOVE 'Enter Rider First Placement Fee' TO #FPF-RIDER-LINE  /* PM6 PM14
  MOVE #FPF TO #DISPLAY-FPF                                       /* PM6
* MOVE #FPF-FBR TO #DISPLAY-RIDER-FPF                        /* PM6 PM14
END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF #CGA-SCR-SUFFIX = 'LTR'                                        /* PM6
  INPUT MARK *#DEP-RCVD USING MAP 'CDUE2MNM'                      /* PM6
*
  MOVE #DISPLAY-FPF TO #FPF                                       /* PM6
* MOVE #DISPLAY-RIDER-FPF TO #FPF-FBR                        /* PM6 PM14
END-IF                                                            /* PM6
*
* MOVE #FPFS-MM TO #MM            /* DW3
MOVE #DATE-ENTERED TO #YYYYMMDD   /* DW15
*
IF #FPFS-DD = 1                   /* PM9
  ADD 1 TO #MM
ELSE
  ADD 2 TO #MM
END-IF
*
IF #MM GT 12
  SUBTRACT 12 FROM #MM
END-IF
*
* IF (#FPFE-MM = #MM) AND (#MODE = 'Q')  /* DW3
IF #EZPAY-WAITING                        /* PM27
  MOVE TRUE TO #EZ-WAIT                  /* DW17
  MOVE 'EZP' TO #CGA-SCR-SUFFIX
  COMPUTE #E-DUES = #APREM / 12          /* DW17
  SUBTRACT #E-DUES FROM #TOT-MOD-PREM    /* DW17
END-IF
*
IF #CGA-SCR-SUFFIX = 'EZP'
  RESET #NO-FPF
END-IF
*
IF #CGA-SCR-SUFFIX = 'LTR' OR = ' ' OR = 'OVR'  /* 'OLD' not included
    OR = 'EZP' OR = 'FPF'                 /*  W13
  PERFORM NEW-PMT-LTR
  IF #CGA-SCR-SUFFIX = ' '
    CALLNAT 'C2009SNM' #INDEX #CN1-N #CGA-CLERK /* to set MEC indicator
  END-IF
*
  PERFORM CHECK-FOR-OWNERSHIP
*
  IF #DO-FA
    MOVE 'FA' TO #CGA-SCR-COMMAND
  ELSE
    MOVE 'ST' TO #CGA-SCR-COMMAND
  END-IF
*
  FETCH 'G1000PXX'
END-IF
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-FOR-OWNERSHIP   /* DW20
* * * * * * * * * * * * * * * * * * *
*
CALLNAT 'GOWNRSXX' #CN1-N   /* DW20
  #NON-INS-OWNER
  #MULTIPLE-OWNER
  #OWNER-OCC-9 (1:9)
*
RESET #POL-OWNED-BY-OTHER
IF   #NON-INS-OWNER                      /* DW20
    AND (#OWNER-CN-BY-POL (#INDEX) NE 0)
    AND (#OWNER-CN-BY-POL (#INDEX) NE #CN1-N)
  MOVE TRUE TO #POL-OWNED-BY-OTHER
END-IF
*
IF   (#POL-OWNED-BY-OTHER)    /* DW20
    OR  (#CONV-DURATION = 779 OR = 631 OR = 632) /* Owned by sps - DW20
    OR  ((#INSURED = 'S' OR = 'K' OR = 'G')  /* /*owned by ins on sps/gen
    AND (NOT #CONV-DURATION = 778 OR = 621 OR = 622))
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
*
RESET #SCN-TITLE #SCN-ROW-1
RESET #SCN-LINE (*) #SC-X
MOVE 'MEMORANDUM' TO #SCN-ROW-1
*
ADD 1 TO #SC-X
COMPRESS 'FROM: Lin Eagan, Beneficiary Consultant'
  INTO #SCN-LINE (#SC-X)
ADD 1 TO #SC-X
COMPRESS 'SUBJECT: Successor Owner'     /* DW20
  INTO #SCN-LINE (#SC-X)
ADD 2 TO #SC-X
COMPRESS 'Thank you for your recent purchase of a life insurance'
  'policy. Because' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'this is an insurance policy owned by you, on the life of'
  'someone else,' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'you may want to consider designating a successor owner.'
  'This becomes' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'an important issue should you predecease the insured. If'
  'this should happen,' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'this policy becomes part of your estate and if applicable the'   /* DW22
  'cash value' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'could be considered in the overall value of your estate. Additionally,'
  'it' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'would be left to the Probate Court administering your Last Will'
  'and' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'Testament or estate to determine who will own the policy. To preclude'
  'this' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'from happening you can now choose who will take over ownership'
  'of the' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'policy should you predecease the insured. We have taken the'
  'liberty of' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'enclosing a Successor Ownership form for your convenience. If you'
  'would' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'like to designate a successor owner please complete the form and'
  'mail it' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'in the envelope provided.'
  INTO #SCN-LINE(#SC-X)
ADD 2 TO #SC-X
COMPRESS 'To ensure that you are protected legally and financially, we'
  'would strongly' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'recommend you discuss these issues with your legal and/or'
  'tax advisor.' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'If you have any questions, please call me at extension #325'
  'or e-mail me at' INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
COMPRESS 'leagan@aafmaa.com.'
  INTO #SCN-LINE(#SC-X)
ADD 1 TO #SC-X
INPUT USING MAP 'CDUE1MNM'
*
RESET #SCN-LINE (*) #SC-X
ADD 1 TO #SC-X
MOVE '                  DESIGNATION OF SUCCESSOR OWNERSHIP'
  TO #SCN-LINE (#SC-X)
ADD 2 TO #SC-X
COMPRESS '  AAFMAA is'
  'requested to make the' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
MOVE 'following change of ownership in the event of my death.'
  TO #SCN-LINE (#SC-X)
ADD 2 TO #SC-X
COMPRESS '  In accordance with the provisions of Certificate'
  'No.' #INSURED-POL  INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS 'on the life of' #HOLD-NAME
  INTO #SCN-LINE (#SC-X)
COMPRESS #SCN-LINE (#SC-X) ', the insured, I designate as successor'
  INTO #SCN-LINE (#SC-X) LEAVING NO
*
ADD 1 TO #SC-X
MOVE 'owner:' TO #SCN-LINE (#SC-X)
*
ADD 2 TO #SC-X
COMPRESS '________________________, ____________________________,'
  ' _____________' INTO #SCN-LINE (#SC-X)
*
ADD 1 TO #SC-X
COMPRESS '     Name                     Relationship to owner'
  '        SSN/TIN' INTO #SCN-LINE (#SC-X)
*
ADD 3 TO #SC-X
COMPRESS 'Date  __________________                 ____________'
  '______________________' INTO #SCN-LINE (#SC-X) LEAVING NO
*
ADD 1 TO #SC-X
COMPRESS '                                            Sig'
  'nature of Owner' INTO #SCN-LINE (#SC-X) LEAVING NO
*
ADD 3 TO #SC-X
COMPRESS '                                         _________________'
  '_________________' INTO #SCN-LINE (#SC-X) LEAVING NO
*
ADD 1 TO #SC-X
MOVE '                 Approved by authority of              Secretary'
  TO #SCN-LINE (#SC-X)
ADD 1 TO #SC-X
MOVE '                  the Board of Directors'
  TO #SCN-LINE (#SC-X)
*
INPUT USING MAP 'CDUE1MNM'
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE NEW-PMT-LTR
* * * * * * * * * * * * * * *
*
IF #CGA-SCR-SUFFIX = 'EZP'
  MOVE 'E' TO #MODE
END-IF
*
IF #TU
  ADD 2 TO #SC-X
  COMPRESS 'Thank You for Trading-up.' TO #SCN-LINE (#SC-X)
END-IF
*
IF   (#MODE = 'G')
    AND (#CAP-IND NE 'Y')
  MOVE TRUE TO #DO-FA
END-IF
*
* IF (#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z')                /* YK4
IF F-LT2-TRUE(<#PLAN>)                                      /* YK4
  IF F-LT2PLUS-TRUE(<#PLAN>)                                /* YK4
    MOVE 'Level Term II+' TO #PLAN-DESC                     /* YK4
  ELSE                                                      /* YK4
    MOVE 'Level Term II' TO #PLAN-DESC
  END-IF                                                    /* YK4
ELSE
  IF #PLAN = 'RT'
    MOVE 'Five Year Renewable Term' TO #PLAN-DESC
  ELSE
    IF #PLAN = 'EZ'                                         /* YK5
      MOVE 'EZ Term' TO #PLAN-DESC                          /* YK5
    ELSE
      IF #PLAN = 'LT'
        MOVE 'Level Term I' TO #PLAN-DESC
      ELSE
        IF #PLAN = 'GPL'                              /* DW26
          MOVE 'Generations Plus' TO #PLAN-DESC
        ELSE
          IF #PLAN = 'GP2'                                    /* TT1
            MOVE 'Generations Plus 2' TO #PLAN-DESC
          ELSE
            MOVE 'Value Added Whole Life' TO #PLAN-DESC
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
IF #PLAN-A1 = 'U' OR = 'X' OR = 'S'                         /* YK4
  MOVE 'Standard'  TO #CAT-DESC
ELSE
  IF #PLAN-A1 = 'V' OR = 'Y' OR = 'R'                       /* YK4
    MOVE 'Select'  TO #CAT-DESC
  ELSE
    IF #PLAN-A1 = 'W' OR = 'Z' OR = 'Q'                     /* YK4
      MOVE 'Super Select'  TO #CAT-DESC
*   ELSE                                                         /* PM10
*     MOVE 'Standard'  TO #CAT-DESC                              /* PM10
    END-IF
  END-IF
END-IF
*
IF #SMOKER-FLAG = 'S'                                             /* PM2
* COMPRESS #CAT-DESC 'Smoker' INTO #CAT-DESC                      /* PM2
  COMPRESS #CAT-DESC 'Nicotine User' INTO #CAT-DESC              /* PM10
END-IF                                                            /* PM2
*
* IF #PREM-RATING NE ' '   /* Moved to later     /* DW16
*   COMPRESS #CAT-DESC 'Rated' #PREM-RATING INTO #CAT-DESC
* END-IF
*
DECIDE FOR FIRST CONDITION                                        /* PM8
  WHEN #MODE = 'Q'
    MOVE 'quarterly'    TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 3
    COMPRESS #FREQ 'bill' INTO #PAY-DESC
  WHEN #MODE = 'S'
    MOVE 'semi-annual'  TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 6
    COMPRESS #FREQ 'bill' INTO #PAY-DESC
  WHEN #MODE = 'A'
    MOVE 'annual'       TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 12
    COMPRESS #FREQ 'bill' INTO #PAY-DESC
  WHEN #MODE = 'E'
    MOVE 'Monthly'      TO #FREQ
    MOVE #MON-TOT       TO #TOT-MOD-PREM
    MOVE 'EZ-Pay'       TO #PAY-DESC
  WHEN #MODE = 'G'
    MOVE 'Monthly'      TO #FREQ
    MOVE #MON-TOT       TO #TOT-MOD-PREM
    MOVE 'allotment'    TO #PAY-DESC
  WHEN #MODE = 'M'
    MOVE 'Monthly'      TO #FREQ
    MOVE #MON-TOT       TO #TOT-MOD-PREM
    MOVE #FREQ          TO #PAY-DESC
  WHEN #MODE = '0'                                                /* PM8
    MOVE 'Monthly'      TO #FREQ
    MOVE #MON-TOT       TO #TOT-MOD-PREM
    MOVE #FREQ          TO #PAY-DESC
  WHEN #MODE = '4'                                                /* PM8
    MOVE 'quarterly'    TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 3
    MOVE #FREQ          TO #PAY-DESC
  WHEN #MODE = '2'                                                /* PM8
    MOVE 'semi-annual'  TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 6
    MOVE #FREQ          TO #PAY-DESC
  WHEN #MODE = '1'                                                /* PM8
    MOVE 'annual'       TO #FREQ
    COMPUTE #TOT-MOD-PREM = #MON-TOT * 12
    MOVE #FREQ          TO #PAY-DESC
  WHEN NONE
    IGNORE
END-DECIDE                                                        /* PM8
*
**IF #PLAN = 'SP' OR = 'SI' OR = 'AL' OR = 'SPF'                    /* PM5
IF #PLAN = 'SP' OR = 'SI' OR = 'SPF' OR IS-ANNUITY-LIFE (< #PLAN >) /* NG2 
  RESET #TOT-MOD-PREM
END-IF
*
FIND (1) SM-V WITH SVC-ID-NUMBER = #CN1-N                         /* NG1
    AND SVC-PLAN = 'PSM'
  IF NO RECORDS FOUND
    RESET SVC-FIRST-PLACE-FEE
  END-NOREC
END-FIND
MOVE EDITED #DEP-RCVD  (EM=ZZZ,ZZ9.99-) TO #DEP-RCVD-X
* ADD SVC-FIRST-PLACE-FEE TO #TOT-MOD-PREM                          /* NG1
MOVE EDITED #TOT-MOD-PREM (EM=ZZ,ZZ9.99) TO #TOT-MOD-PREM-X
ADD #FPF-FBR TO #FPF
MOVE EDITED #FPF          (EM=ZZZ,ZZ9.99-) TO #FPF-X
*
COMPUTE #DEP-DIFF = #FPF - #DEP-RCVD
MOVE EDITED #DEP-DIFF (EM=ZZZ,ZZ9.99-) TO #DEP-DIFF-X
*
* MOve #FPFE-MM TO #MM  /* had been eff-dt
* MOVE #FPFE-DD TO #DD
* MOVE #FPFE-YYYY TO #YYYY
MOVE *DATN TO #YYYYMMDD
IF #DATE-ENTERED NE 0                                             /* DW13
  MOVE #DATE-ENTERED TO #YYYYMMDD
END-IF
*
ADD 2 TO #MM
*
IF #MM GT 12
  SUBTRACT 12 FROM #MM
  ADD 1 TO #YYYY
END-IF
*
COMPRESS #MM-A #DD-A #YYYY-A INTO #LAPSE-DATE-X WITH '/'
ADD 2 TO #SC-X
COMPRESS 'For your' #PLAN-DESC 'effective ' #ISS-DT-DISP
  INTO #SCN-LINE (#SC-X)
*
IF #TU
  IF #MODE = 'G'
    COMPUTE #INCR-ALOT = (#G-DUES + #MON-TOT) - #ALOT-AMT
    COMPUTE #2-MONS-TOT = #INCR-ALOT * 2
    MOVE EDITED #2-MONS-TOT  (EM=ZZ,ZZ9.99) TO #2-MONS-TOT-X
    COMPUTE #DEP-DIFF = #2-MONS-TOT - #DEP-RCVD
    MOVE EDITED #DEP-DIFF (EM=ZZZ,ZZ9.99-) TO #DEP-DIFF-X
  END-IF
*
  IF #MODE = 'A' OR = 'S' OR = 'Q'
    MOVE EDITED #TOT-MOD-PREM (EM=ZZZ,ZZ9.99-) TO #FPF-X
  END-IF
END-IF
*
IF #MODE = 'E'
  FIND ST-V WITH MEMBER-KEY = #CN1-A
  END-FIND
  RESET #FST-X    /* DW17
  FOR #I1 1 TO 9
    IF ST-V.STATUS (#I1) ='D'
      MOVE #I1 TO #FST-X
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #FST-X GT 9
    MOVE 1 TO #FST-X
  END-IF
*
  RESET #DUES
  CALLNAT 'GDUESSXX' #CN1-N #FST-X #DUES /* Get Actual Dues
*
  IF  #EZ-WAIT                                                    /* DW17
    COMPUTE #NEW-EZ = #MON-TOT + #DUES                            /* DW17
  ELSE
    COMPUTE #NEW-EZ = #E-DUES + #MON-TOT + #DUES                  /* DW17
  END-IF
  MOVE EDITED #NEW-EZ (EM=Z,ZZ9.99) TO #NEW-EZ-X
END-IF
*
IF (NOT #TU
    OR (#DEP-RCVD NE 0))
    AND (#CGA-SCR-SUFFIX NE 'FPF')                                /* DW13
  ADD 2 TO #SC-X
  MOVE 'Payment with Application' TO #SCN-LINE-A (#SC-X)
  MOVE #DEP-RCVD-X                TO #SCN-LINE-B (#SC-X)
*
  ADD 1 TO #SC-X
  MOVE 'Amount due based on issue date and payment selection'
    TO #SCN-LINE-A (#SC-X)
  MOVE #FPF-X TO #SCN-LINE-B (#SC-X)
  IF SM-V.SVC-FIRST-PLACE-FEE > 0                                /* NG1
    ADD 1 TO #SC-X
    #SCN-LINE-A (#SC-X) := 'Premier Services Dues Payment'
    MOVE EDITED SM-V.SVC-FIRST-PLACE-FEE (EM=ZZZ,ZZ9.99-) TO #SCN-LINE-B (#SC-X)
  END-IF
END-IF
*
IF #CGA-SCR-SUFFIX = 'FPF'                                        /* DW13
  MOVE #FPF TO #TOT-DUE
ELSE
  IF #TU
      AND (#MODE = 'A' OR = 'S' OR = 'Q')
    MOVE #TOT-MOD-PREM TO #TOT-DUE
  ELSE
    MOVE #DEP-DIFF TO #TOT-DUE
  END-IF
END-IF
ADD SM-V.SVC-FIRST-PLACE-FEE TO #TOT-DUE                         /* NG1
*
IF #CGA-SCR-SUFFIX NE 'FPF'                                       /* DW13
  ADD 1 TO #SC-X
  MOVE '___________' TO #SCN-LINE-B (#SC-X)
END-IF
*
ADD 1 TO #SC-X
*
IF #TOT-DUE LT 0
* MOVE 'Total Amount to be refunded to you' TO #SCN-LINE-A (#SC-X)
  MOVE 'Total Amount of credit' TO #SCN-LINE-A (#SC-X) /* 8/31/2004 CEB
ELSE
  IF #CGA-SCR-SUFFIX = 'FPF'         /* DW13
    ADD 1 TO #SC-X
*   COMPRESS 'Remaining Amount due and payable by' #LAPSE-DATE-X
    COMPRESS 'Remaining Amount Due and payable upon receipt'      /* DW18
      INTO #SCN-LINE-A (#SC-X)
  ELSE
    IF #MODE = 'M' OR = '0' OR = '1' OR = '2' OR = '4'            /* PM8
      MOVE 'Total Amount Due' TO #SCN-LINE-A (#SC-X)              /* PM1
    ELSE                                                          /* PM1
*     COMPRESS 'Total Amount Due and payable by' #NEXT-MON-DISP
*      COMPRESS 'Total Amount Due and payable by' #LAPSE-DATE-X   /* DW13
      COMPRESS 'Total Amount Due and payable upon receipt'        /* DW18
        TO #SCN-LINE-A (#SC-X)
*
*     IF   (#MODE = 'E')                                          /* DW12
*      AND (#TOT-DUE GT 0)
*       MOVE TRUE TO #E-AST
*     END-IF
    END-IF
  END-IF                                                          /* PM1
END-IF
*
MOVE EDITED #TOT-DUE (EM=ZZZ,ZZ9.99-) TO #TOT-DUE-X
MOVE #TOT-DUE-X                TO #SCN-LINE-B (#SC-X)
*
* IF #E-AST                                                       /* DW12
*  COMPRESS #SCN-LINE(#SC-X) '**' INTO #SCN-LINE (#SC-X) LEAVING NO
* END-IF
*
ADD 2 TO #SC-X
*
IF #MODE = 'M' OR = '0' OR = '1' OR = '2' OR = '4'                /* PM8
    AND #TOT-DUE GT 0                                             /* PM1
* MOVE LEFT #TOT-DUE-X TO #TOT-DUE-X                              /* PM1
  COMPRESS '$' #TOT-DUE-X INTO #SCN-LINE (#SC-X) LEAVING NO       /* PM1
  COMPRESS 'Total premiums of '#SCN-LINE (#SC-X) 'will be'        /* PM1
    'charged to your credit card.' INTO #SCN-LINE (#SC-X)         /* PM1
  ADD 1 TO #SC-X                                                  /* PM1
  COMPRESS 'This completes initial payments and will bring'       /* PM1
    'your account up to date.' INTO #SCN-LINE (#SC-X)             /* PM1
  ADD 2 TO #SC-X                                                  /* PM1
END-IF                                                            /* PM1
*
**IF  (#PLAN = 'SP' OR = 'SI' OR = 'AL' OR = 'SPF')                 /* PM5
IF #PLAN = 'SP' OR = 'SI' OR = 'SPF' OR IS-ANNUITY-LIFE (< #PLAN >) /* NG2 
  MOVE 'This policy was issued as a single pay plan.'
    TO #SCN-LINE (#SC-X)
*
  IF #TOT-DUE GT 0
    ADD 1 TO #SC-X
    MOVE 'If we do not receive the total amount due by'
      TO #SCN-LINE (#SC-X)
*
*   COMPRESS #SCN-LINE (#SC-X) #NEXT-MON-DISP INTO #SCN-LINE (#SC-X)
    COMPRESS #SCN-LINE (#SC-X) #LAPSE-DATE-X INTO #SCN-LINE (#SC-X)
*
    ADD 1 TO #SC-X
    MOVE 'this policy may lapse.' TO #SCN-LINE (#SC-X)
  END-IF
*
  ADD 1 TO #SC-X
  COMPRESS  'Your future premiums will be $' #TOT-MOD-PREM-X
    '.' INTO #SCN-LINE (#SC-X) LEAVING NO
*
  RESET #DO-FA
ELSE                                                              /* PM5
* COMPRESS 'Your' #FREQ 'premium for this policy issued in the'   /* PM10
*   #CAT-DESC   INTO #SCN-LINE (#SC-X)                            /* PM10
  COMPRESS 'Your' #FREQ 'premium for this policy' INTO #SCN-LINE (#SC-X)
  IF #CAT-DESC NE ' '                                             /* PM10
    COMPRESS #SCN-LINE (#SC-X) 'issued in the' #CAT-DESC          /* PM10
      INTO #SCN-LINE (#SC-X)                                      /* PM10
    ADD 1 TO #SC-X                                                /* PM10
*
    IF #PREM-RATING NE ' '                                        /* DW16
      COMPRESS 'Rated' #PREM-RATING INTO #SCN-LINE (#SC-X)
    END-IF
    COMPRESS #SCN-LINE (#SC-X) 'Category' INTO #SCN-LINE (#SC-X)  /* DW16
  ELSE                                                            /* PM10
    COMPRESS #SCN-LINE (#SC-X) 'effective' #ISS-DT-DISP           /* PM10
      INTO #SCN-LINE (#SC-X)                                      /* PM10
    ADD 1 TO #SC-X                                                /* PM10
  END-IF                                                          /* PM10
*
  IF #TOT-DUE GT 0
      AND NOT #MODE = 'M' OR = '0' OR = '1' OR = '2' OR = '4'     /* PM8
*   ADD 1 TO #SC-X                                                /* PM10
    COMPRESS #SCN-LINE (#SC-X) ' is $' #TOT-MOD-PREM-X            /* PM10
      '. If we do not receive the total amt due by'               /* DW16
      INTO #SCN-LINE (#SC-X) LEAVING NO
*
    ADD 1 TO #SC-X
*
    IF #TU
        AND (#NEW-EZ = 0 )                                        /* DW19
*     COMPRESS #NEXT-MON-DISP 'your premium will be paid by Automatic'
      COMPRESS #LAPSE-DATE-X 'your premium will be paid by'       /* DW13
        'Automatic Premium Loan.' INTO #SCN-LINE (#SC-X)
    ELSE
      IF #NEW-EZ NE 0                                             /* DW19
        COMPRESS #LAPSE-DATE-X 'it will be added to your'         /* DW17
          'next EZ-Pay draft.'  INTO #SCN-LINE (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'If your next EZ-Pay draft is returned'
          'for insufficient funds your'  INTO #SCN-LINE (#SC-X)
        ADD 1 TO #SC-X
        COMPRESS 'policy may lapse.' INTO #SCN-LINE (#SC-X)
      ELSE
*       IF  (#PLAN = 'LT' OR = 'RT')                                        /* YK4
*           OR ((#PLAN-A1 GE 'U') AND (#PLAN-A1 LE 'Z'))          /* DW17   /* YK4
        IF FUNCTION-TERM-POLICY(<#PLAN>)                                    /* YK4
          COMPRESS #LAPSE-DATE-X 'your policy may lapse.'         /* DW13
            INTO #SCN-LINE (#SC-X)
        ELSE
*        COMPRESS #NEXT-MON-DISP 'your policy may lapse or your premium'
          COMPRESS #LAPSE-DATE-X 'your policy may lapse or your'  /* DW13
            'premium may be paid by' INTO #SCN-LINE (#SC-X)
          ADD 1 TO #SC-X
          MOVE 'Automatic Premium Loan.' TO #SCN-LINE (#SC-X)
        END-IF
      END-IF
    END-IF
*
    ADD 1 TO #SC-X
    COMPRESS  'Your future premiums will be $' #TOT-MOD-PREM-X
      ' and will be paid by'
      INTO #SCN-LINE (#SC-X) LEAVING NO
*
    ADD 1 TO #SC-X
    COMPRESS #PAY-DESC INTO #SCN-LINE (#SC-X)
*
    IF   (#MODE = 'G')          /* DW25
        AND (#INSURED = 'S' OR = 'K' OR = 'G')
      COMPRESS #SCN-LINE (#SC-X) 'from your designated allotee'
        INTO #SCN-LINE (#SC-X)
    END-IF
*
    COMPRESS #SCN-LINE (#SC-X) '.' INTO #SCN-LINE (#SC-X) LEAVING NO
*
*   IF #E-AST                                                     /* DW12
*     ADD 1 TO #SC-X
*     COMPRESS '** If the amount due is not paid prior to your first'
*         'Ez-Pay draft, it' INTO #SCN-LINE (#SC-X)
*     ADD 1 TO #SC-X
*     COMPRESS 'will be added to your first draft.'
*                   INTO #SCN-LINE (#SC-X)
*   END-IF
  ELSE
*   ADD 1 TO #SC-X                                                /* PM10
    COMPRESS #SCN-LINE (#SC-X) ' is $' #TOT-MOD-PREM-X            /* PM10
      '. Your future premiums will be $' #TOT-MOD-PREM-X ' and'   /* PM10
      INTO #SCN-LINE (#SC-X) LEAVING NO
*
    ADD 1 TO #SC-X
    IF #MODE = 'M' OR = '0' OR = '1' OR = '2' OR = '4'            /* PM8
      COMPRESS 'will be paid by a' #PAY-DESC 'charge to your'     /* PM1
        'credit card' INTO #SCN-LINE (#SC-X)                      /* PM1
      COMPRESS #SCN-LINE (#SC-X) '.' INTO #SCN-LINE (#SC-X) LEAVING NO
    ELSE                                                          /* PM1
      COMPRESS 'will be paid by' #PAY-DESC INTO #SCN-LINE (#SC-X)
      IF   (#MODE = 'G')                                          /* DW25
          AND (#INSURED = 'S' OR = 'K' OR = 'G')
        COMPRESS #SCN-LINE (#SC-X) 'from your designated allotee'
          INTO #SCN-LINE (#SC-X)
      END-IF
*
      COMPRESS #SCN-LINE (#SC-X) '.' INTO #SCN-LINE (#SC-X) LEAVING NO
    END-IF                                                        /* PM1
  END-IF
*
  IF (#E-DUES NE 0) AND (#NEW-EZ NE 0)
    ADD 1 TO #SC-X
    COMPRESS 'Your future transfers for all policies paid by'     /* PM2
      'EZ-Pay will be $' INTO #SCN-LINE (#SC-X)                   /* PM2
    COMPRESS #SCN-LINE (#SC-X) #NEW-EZ-X INTO #SCN-LINE (#SC-X)   /* PM2
      LEAVING NO                                                  /* PM2
*   COMPRESS 'Future EZ-PAY transfers will change to $' #NEW-EZ-X /* PM2
*       '.' INTO #SCN-LINE (#SC-X) LEAVING NO                     /* PM2
  END-IF
*
* ADD 1 TO #SC-X
* COMPRESS '* A credit reduces the Total Amount Due at this time and'
*      'a debit'   INTO #SCN-LINE (#SC-X)
*
* ADD 1 TO #SC-X
* MOVE 'increases it.' TO #SCN-LINE (#SC-X)
*
* IF #TOT-DUE LT 0
*   ADD 1 TO #SC-X
*   MOVE 'Your credit will be refunded to you.' TO #SCN-LINE (#SC-X)
* END-IF
*
  IF #DO-FA
    ADD 1 TO #SC-X
    COMPRESS 'Enclosed is an allotment information form.'         /* YK2
      INTO #SCN-LINE (#SC-X)
  END-IF
END-IF                                                            /* PM5
*
* Add text below for retired members with allotment               /* YK1
* and active duty members with allotment  YK2 START               /* YK2
* IF #MIL-ST = 'R' AND #MODE = 'G'
*
IF   (#MODE = 'G')
    AND (NOT #INSURED = 'S' OR = 'K' OR = 'G')                       /* DW25
  IF #MIL-ST = 'R' AND #SC-X GT 12
    ADD 1 TO #SC-X
  ELSE
    ADD 2 TO #SC-X
  END-IF
  MOVE
    'You have elected to make payments on your AAFMAA insurance through'
    TO #SCN-LINE (#SC-X)
  ADD 1 TO #SC-X
  MOVE 'government allotment.'  TO #SCN-LINE (#SC-X)
*
  IF #MIL-ST = 'A'                                               /* DW24
    COMPRESS #SCN-LINE (#SC-X)
      'AAFMAA has no authority to start/increase Active'
      INTO #SCN-LINE (#SC-X)
    ADD 1 TO #SC-X
    COMPRESS 'Duty Allotments. Allotments may NOT be started/increased'
      'through MYPAY.'    INTO #SCN-LINE (#SC-X)
    ADD 1 TO #SC-X
    COMPRESS 'This form is being provided to you to take to your Finance'
      'Office and start/increase'  INTO #SCN-LINE (#SC-X)
    ADD 1 TO #SC-X
    MOVE 'your Allotment to AAFMAA.' TO #SCN-LINE (#SC-X)
  ELSE
    COMPRESS #SCN-LINE (#SC-X)                                  /* DW99
      'As a convenience to our members, DFAS has awarded' TO #SCN-LINE (#SC-X)
    ADD 1 TO #SC-X
    COMPRESS  'AAFMAA the ability to adjust our members AAFMAA allotments'
      'on their behalf.'  INTO #SCN-LINE (#SC-X)
*
    IF #MIL-ST = 'R'
      ADD 1 TO #SC-X
      MOVE
        'DFAS requires that AAFMAA make all changes (starts, stops,changes) for all'
        TO #SCN-LINE (#SC-X)
      ADD 1 TO #SC-X
      MOVE
        'AAFMAA Retiree allotments. DFAS will no longer process Retiree allotment'
        TO #SCN-LINE (#SC-X)
      ADD 1 TO #SC-X
      MOVE 'changes for AAFMAA allotments only.'                /* DW23
        TO #SCN-LINE (#SC-X)
      ADD 1 TO #SC-X
    ELSE                                                        /* YK1
      ADD 1 TO #SC-X
      MOVE
        'We are very excited to be awarded this responsibility, which allows us to'
        TO #SCN-LINE (#SC-X)
      ADD 1 TO #SC-X
      MOVE
        'provide additional service and convenience to our members.'
        TO #SCN-LINE (#SC-X)
    END-IF                                                      /* YK2 END
  END-IF
END-IF                                                          /* YK1
*
INPUT USING MAP 'CDUE1MNM'
*
END-SUBROUTINE
*
END
