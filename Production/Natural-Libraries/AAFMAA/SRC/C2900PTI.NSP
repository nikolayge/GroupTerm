* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************
* PROGRAM-ID: C2900PTI               *
*         TI: TAXABLE INCOME SCREEN  *
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE       TAG  REASON                                          *
* NG  20130726    NG1  Address normalization                           *
* TMT 20131202    TT1  1 MILLION STABILITY PROJECT                     *
* TMT 20140612    TT2  Contact Normalization DOB and Sex               *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2900LTI
LOCAL USING ADDA0001    /* NG1
LOCAL
1 #ADDRESS-POOL-ID (N8) /* NG1
1 #PRIMARY         (A1) CONST <'P'> /* NG1
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 RESTRICT-MBR-SW
  2 NAME
  2 INTRO-FLAG
  2 SSN
  2 MEMBER-CONTACT-ID
*  2 ADDRESS-1                  /* TT2
*  2 ADDRESS-2
*  2 ZIP-CODE
*  2 DATE-OF-BIRTH              /* TT2
  2 C*ACTIVE-SEGMENTS        
  2 NUMBER-MEMBERSHIPS
  2 STATUS       (9)
  2 ANNUAL-PREM       (9)
  2 ISSUE-AGE         (9)
  2 REDEFINE ISSUE-AGE
    3 ISSUE-AGE-OCC   (9)
      4 ISSUE-AGE-R    (N2)
  2 PLAN              (9)
  2 REDEFINE PLAN
    3 PLAN-OCC    (9)
      4 PLAN-2     (A2)
      4 PLAN-1     (A1)
  2 PAY-STATUS        (9)
  2 TOT-PREMIUMS-PAID (9)
  2 PAID-TO-DATE      (9)
  2 CASH-VALUE        (9)
  2 NXT-MON-CASH-VALUE (9)
  2 ISSUE-DATE        (9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC   (9)
      4 ISS-YYYY     (A4)
      4 ISS-MM       (N2)
      4 ISS-DD       (A2)
  2 MODE              (9)
  2 COST-BASIS        (9)
  2 MONTH-ALLOC-AMT   (9)
  2 X-FIRST-PLACE-FEE (9)
  2 X-BILL-ADJUST     (9)
*
1 #I1    (I2)
1 #DATN  (N8)
1 REDEFINE #DATN
  2 #YYYY (A4)
  2 #MM   (A2)
  2 #DD   (A2)
1 REDEFINE #DATN
  2 #CENT-N (N2)
  2 #YY-N (N2)
  2 #MM-N (N2)
  2 #DD-N (N2)
1 #SLASH (A1) CONST<'/'>
1 #SCR-POLICY-NUMBER (A16)
1 #SCR-NAME          (A25)
1 #SCR-ADDR1         (A25)
1 #SCR-ADDR2         (A25)
1 #SCR-ZIP-CODE      (A10)
1 #SCR-SSN           (A11)
1 #SCR-CURR-CV       (A9)
1 #SCR-TAX-INC       (A9)
1 #TAX-INC           (N9)
1 #SCR-FED-ID        (A10)
1 #SCR-PREM-PMT      (A9)
1 #SEL-CV            (C/10)
1 #HOLD-SCR-ID       (N6)
1 #NUM-OPTIONS       (N2)
1 #W-CURR-CV         (P9)
1 #ISS-DD-A          (A2)
1 REDEFINE #ISS-DD-A
  2 #ISS-DD-N        (N2)
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)    /* Ng99
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID         (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
* ABOVE for CALC-PREMS-PAID
1 DATE-OF-BIRTH        (N8.0)              /* TT2
*
END-DEFINE
*
SET KEY PF4
DEFINE WINDOW ADDRESS
  SIZE 8 * 76
  BASE 9/2
  FRAMED ON POSITION SYMBOL AUTO
*
INCLUDE GONERCXX       /* NATURAL ERROR ROUTINE
GET ST-V #CGA-ISN
*
MOVE #CGA-SCR-ID TO #HOLD-SCR-ID
RESET #CGA-SCR-COMMAND
  #CGA-SCR-ID
  #CGA-SCR-SUFFIX
MOVE ' TAXABLE INCOME MENU SCREEN' TO #SCR-TITLE
MOVE *DATN                                 TO #DATN
MOVE ST-V.NAME                             TO #SCR-NAME
*                           NG1 Start
RESET #PDA-ADDRESS
MOVE MEMBER-CONTACT-ID TO #ADD-CONTACT-ID
MOVE 'P' TO #ADD-TYPE
CALLNAT 'ADDN0001' #PDA-ADDRESS
#SCR-ADDR1 := SUBSTR ( #ADD-LINE-1,1,25)  /* Only first 25 chars
#SCR-ADDR2 := SUBSTR ( #ADD-LINE-2,1,25)  /* Only first 25 chars
MOVE EDITED #ADD-ZIP-CODE (EM=99999-9999)  TO #SCR-ZIP-CODE
*
* MOVE ST-V.ADDRESS-1                        TO #SCR-ADDR1
* MOVE ST-V.ADDRESS-2                        TO #SCR-ADDR2
* MOVE EDITED ST-V.ZIP-CODE (EM=99999-9999)  TO #SCR-ZIP-CODE
*                           NG1 End
MOVE EDITED ST-V.SSN (EM=999-99-9999)      TO #SCR-SSN
*
IF C*ACTIVE-SEGMENTS = 0
  MOVE 'TI' TO #CGA-SCR-COMMAND
  MOVE #HOLD-SCR-ID TO #CGA-SCR-ID
  INPUT WITH TEXT 'NO ACTIVE SEGMENTS' MARK *#CGA-SCR-COMMAND USING MAP 'C2901MTI'
  FETCH 'G1000PXX'
ELSE
  PERFORM FORMAT-MENU
END-IF
*
IF #SCR-SELECTION(1) NE 'X'
  INPUT MARK *#SCR-SELECTION(1) USING MAP 'C2901MTI'
  IF #CGA-SCR-COMMAND NE ' '
      OR #CGA-SCR-ID NE 0
      OR #CGA-SCR-SUFFIX NE ' '
    FETCH 'G1000PXX'
  END-IF
  INPUT USING MAP 'ANIM0001'
END-IF
*
FOR #SCR-X = 1 TO 10
  IF #SCR-SELECTION (#SCR-X) = 'X'
    IF #SCR-X > #NUM-OPTIONS AND #NUM-OPTIONS NE 0
      PERFORM SHOW-ALL-SEGMENTS
      ESCAPE BOTTOM
    ELSE
      PERFORM SHOW-ONE-SEGMENT
      ESCAPE BOTTOM
    END-IF
  END-IF
END-FOR
*
IF #SCR-X GT 7
  REINPUT WITH 'NO SELECTION REQUESTED'
END-IF
*
INPUT MARK *#SCR-PREM-PMT USING MAP 'C2900MTI'
PERFORM POP-UP-ADDRESS
*
IF  (#CGA-SCR-COMMAND NE ' ')
    OR (#CGA-SCR-ID NE 0)
  FETCH 'G1000PXX'
ELSE
  MOVE 'TI' TO #CGA-SCR-COMMAND
  MOVE #HOLD-SCR-ID TO #CGA-SCR-ID
  FETCH 'G1000PXX'
END-IF
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-ONE-SEGMENT
* * * * * * * * * * * * * * * * * *
*
MOVE #SCR-SAVE-IDX(#SCR-X) TO #INS-INDEX
MOVE ISS-MM(#INS-INDEX) TO #MONTH-INDEX
COMPRESS ISS-DD(#INS-INDEX) #MONTH-LITERAL(#MONTH-INDEX)
  ISS-YYYY(#INS-INDEX) INTO #SCN-DATE
*
MOVE ISS-DD(#INS-INDEX) TO #ISS-DD-A
IF (#ISS-DD-N = 01) OR (#ISS-DD-N GT #DD-N)
  MOVE ROUNDED CASH-VALUE(#INS-INDEX) TO #W-CURR-CV
  MOVE EDITED #W-CURR-CV (EM=Z,ZZZ,ZZ9) TO #SCR-CURR-CV
ELSE
  MOVE ROUNDED NXT-MON-CASH-VALUE(#INS-INDEX) TO #W-CURR-CV
  MOVE EDITED #W-CURR-CV (EM=Z,ZZZ,ZZ9) TO #SCR-CURR-CV
END-IF
*
MOVE #INS-INDEX TO #I1
PERFORM CALC-PREMS-PAID-SETUP
*
RESET #W-CURR-CV
PERFORM FIND-POLICY-NUM
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-ALL-SEGMENTS
* * * * * * * * * * * * * * * * * *
*
FOR #SCR-X 1 TO #NUM-OPTIONS
  MOVE #SCR-SAVE-IDX(#SCR-X) TO #INS-INDEX
  MOVE ISS-MM(#INS-INDEX) TO #MONTH-INDEX
  COMPRESS ISS-DD(#INS-INDEX) #MONTH-LITERAL(#MONTH-INDEX)
    ISS-YYYY(#INS-INDEX) INTO #SCN-DATE
*
  MOVE ISS-DD(#INS-INDEX) TO #ISS-DD-A
  IF (#ISS-DD-N = 01) OR (#ISS-DD-N GT #DD-N)
    MOVE ROUNDED CASH-VALUE(#INS-INDEX) TO #W-CURR-CV
    MOVE EDITED #W-CURR-CV (EM=Z,ZZZ,ZZ9) TO #SCR-CURR-CV     /*TT1
  ELSE
    MOVE ROUNDED NXT-MON-CASH-VALUE(#INS-INDEX) TO #W-CURR-CV
    MOVE EDITED #W-CURR-CV (EM=Z,ZZZ,ZZ9) TO #SCR-CURR-CV     /*TT1
  END-IF
*
  MOVE #INS-INDEX TO #I1
  PERFORM CALC-PREMS-PAID-SETUP
*
  RESET #W-CURR-CV
  PERFORM FIND-POLICY-NUM
  INPUT MARK *#SCR-PREM-PMT USING MAP 'C2900MTI'
*
  IF #CGA-SCR-COMMAND NE '  ' OR #CGA-SCR-ID NE 0
    FETCH 'G1000PXX'
  END-IF
END-FOR
*
MOVE 'TI' TO #CGA-SCR-COMMAND
MOVE #HOLD-SCR-ID TO #CGA-SCR-ID
FETCH 'G1000PXX'
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE FORMAT-MENU
* * * * * * * * * * * * * * *
*
MOVE 0 TO #SCR-X
FOR #INS-INDEX = 1 TO 9
  IF STATUS(#INS-INDEX) NE 'D'
    ESCAPE TOP
  END-IF
*
  IF #INS-INDEX GT C*ACTIVE-SEGMENTS
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #SCR-X
  MOVE ISSUE-DATE(#INS-INDEX) TO #DATN
  COMPRESS #MM #DD #YYYY INTO #SCR-EFF-DATE(#SCR-X) WITH #SLASH
  MOVE '(' TO #SCR-LFT-PAREN(#SCR-X)
  MOVE ')' TO #SCR-RGT-PAREN(#SCR-X)
  MOVE #INS-INDEX TO #SCR-SAVE-IDX(#SCR-X)
END-FOR
*
IF #SCR-X = 1
  MOVE 'X' TO #SCR-SELECTION(1)
END-IF
*
IF #SCR-X > 1
  MOVE #SCR-X TO #NUM-OPTIONS
  ADD 1 TO #SCR-X
  MOVE 'ALL     ' TO #SCR-EFF-DATE(#SCR-X)
  MOVE '(' TO #SCR-LFT-PAREN(#SCR-X)
  MOVE ')' TO #SCR-RGT-PAREN(#SCR-X)
  MOVE #INS-INDEX TO #SCR-SAVE-IDX(#SCR-X)
END-IF
*
FOR #INS-INDEX = 1 TO 10
  IF #SCR-LFT-PAREN(#INS-INDEX) NE '('
    MOVE (AD=P) TO #SEL-CV(#INS-INDEX)
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-POLICY-NUM
* * * * * * * * * * * * * * * * *
*
CALLNAT 'GPLNMSXX' #SCR-POLICY-NUMBER ID-NUMBER #INS-INDEX /* bld POL-#
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE CALC-PREMS-PAID-SETUP
* * * * * * * * * * * * * * *
*
RESET #PREMS-PAID DATE-OF-BIRTH                                                 /* TT2
DATE-OF-BIRTH    := GET-DATE-OF-BIRTH(<MEMBER-CONTACT-ID>)                      /* TT2
*
MOVE DATE-OF-BIRTH           TO #PP-DATE-OF-BIRTH
MOVE ANNUAL-PREM (#I1)       TO #PP-ANNUAL-PREM
MOVE PAY-STATUS (#I1)        TO #PP-PAY-STATUS
MOVE TOT-PREMIUMS-PAID (#I1) TO #PP-TOT-PREMIUMS-PAID
MOVE PAID-TO-DATE (#I1)      TO #PP-PAID-TO-DATE
MOVE ISSUE-DATE (#I1)        TO #PP-ISSUE-DATE
MOVE MODE (#I1)              TO #PP-MODE
MOVE PLAN (#I1)              TO #PP-PLAN
MOVE COST-BASIS (#I1)        TO #PP-COST-BASIS
MOVE MONTH-ALLOC-AMT (#I1)   TO #PP-MONTH-ALLOC-AMT
MOVE ST-V.INTRO-FLAG         TO #PP-INTRO-FLAG
MOVE X-FIRST-PLACE-FEE (#I1) TO #PP-FIRST-PLACE-FEE
MOVE X-BILL-ADJUST     (#I1) TO #PP-BADJ
*
CALLNAT 'GPRPDSCO'
  #PP-DATE-OF-BIRTH
  #PP-ANNUAL-PREM
  #PP-PAY-STATUS
  #PP-TOT-PREMIUMS-PAID
  #PP-PAID-TO-DATE
  #PP-ISSUE-DATE
  #PP-MODE
  #PP-PLAN
  #PP-COST-BASIS
  #PP-MONTH-ALLOC-AMT
  #PP-INTRO-FLAG
  #PP-FIRST-PLACE-FEE
  #PREMS-PAID
  #SUB-PREMS-PAID
  #PP-BADJ
*
COMPUTE ROUNDED #TAX-INC = #W-CURR-CV - #PREMS-PAID
MOVE EDITED #PREMS-PAID (EM=Z,ZZZ,ZZ9) TO #SCR-PREM-PMT
MOVE EDITED #TAX-INC    (EM=Z,ZZZ,ZZ9) TO #SCR-TAX-INC
*
END-SUBROUTINE
*
DEFINE POP-UP-ADDRESS
/*     ==============
IF *PF-KEY EQ 'PF4'
  #ADDRESS-POOL-ID := F-GET-ADR-POOL-ID (< MEMBER-CONTACT-ID, #PRIMARY >)
  CALLNAT 'ADRN0002' MEMBER-CONTACT-ID #ADDRESS-POOL-ID
*   SET WINDOW 'ADDRESS'
*   INPUT USING MAP 'ANIM0001'
*   SET WINDOW OFF
END-IF
END-SUBROUTINE
END
