* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : OBJFLETT                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Convert upper case to lower case                      *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    02112008  None    Program Written                             *
************************************************************************
*
DEFINE FUNCTION FUNCTION-UPPER-LOWER RETURNS (A25) BY VALUE
DEFINE DATA
PARAMETER
1 #WORK-UPPER-LOWER   (A25)
*
LOCAL
*
1 #WORK-AREA          (A25)
1 REDEFINE #WORK-AREA
 2 #WORK-AREA-OCC     (25)
  3 #CHARACTER        (A1)
*
1 #LETTER-TABLE
  2 #1T               (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>
  2 #2T               (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>
*
1 REDEFINE #LETTER-TABLE
  2 #LETTER-OCC       (26)
    3 #UPPER          (A1)
    3 #LOWER          (A1)
*
1 #I1                 (I2)
1 #I2                 (I2)
*
1 #CONSTANTS
 2 #SPACE             (A1)  INIT <' ' >
 2 #PERIOD            (A1)  INIT <'.'>
 2 #DASH              (A1)  INIT <'-'>
*
END-DEFINE
************************************************************************
*
#WORK-AREA := #WORK-UPPER-LOWER
*
FOR #I1 2 TO 25
  IF #CHARACTER (#I1 - 1) = #SPACE OR = #PERIOD OR = #DASH
    ESCAPE TOP
  END-IF
*
  FOR #I2 1 TO 26
    IF #CHARACTER (#I1) = #UPPER (#I2)
      MOVE #LOWER (#I2) TO #CHARACTER (#I1)
    END-IF
  END-FOR
END-FOR
*
FUNCTION-UPPER-LOWER := #WORK-AREA
*
END-FUNCTION
*
END
