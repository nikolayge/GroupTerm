* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Program ID: PDBPALL
* Project Persona Database
*
* PERSONA FILTER : Military Status, Age, Alive, No Spouse/Kid
*
**************************************************************************
*                            MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* NG    20130506        Created
*
DEFINE DATA
PARAMETER
1 #ISN            (P10)
1 #SKIP           (L)
1 #PERSONA        (A1)
1 #AGE            (I2)
LOCAL
1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER (N6)
  2 MILITARY-STATUS (A1)
  2 DATE-OF-BIRTH (N8) 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-1 (A8)
  2 DATE-OF-DEATH (N8) 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-1 (A8)
1 ST-V VIEW OF A-STATUS
  2 INTRO-FLAG  (A1)
*
1 #AGE1           (I2)
1 #AGE2           (I2)
1 JAMES           (A1) CONST <'J'>
1 ADAM            (A1) CONST <'A'>
1 MARK            (A1) CONST <'M'>
1 TED             (A1) CONST <'T'>
1 UNNAMED         (A1) CONST <'U'>
*
END-DEFINE
*
RESET #SKIP
GET CNT-V #ISN
*
IF CNT-V.ID-NUMBER = 0
    OR CNT-V.ID-NUMBER > 0 AND CNT-V.ID-NUMBER < 1901
  #SKIP:= TRUE
  ESCAPE ROUTINE
END-IF
IF DATE-OF-DEATH > 0
  #SKIP := TRUE
  ESCAPE ROUTINE
END-IF
IF DATE-OF-BIRTH-1 = MASK (YYYYMMDD)
  IGNORE
ELSE
  #SKIP := TRUE
  ESCAPE ROUTINE
END-IF
*
#AGE := GET-AGE-2 (<DATE-OF-BIRTH>)
IF #PERSONA = ' '
  DECIDE FOR FIRST CONDITION
    WHEN #AGE > 17 AND #AGE <= 29
        AND MILITARY-STATUS EQ 'A'
      #PERSONA := ADAM
    WHEN #AGE > 29 AND #AGE <= 39
        AND MILITARY-STATUS EQ 'A'
      #PERSONA := JAMES
    WHEN #AGE > 39 AND #AGE <= 49
        AND MILITARY-STATUS EQ 'A'
      #PERSONA := MARK
    WHEN #AGE > 49 AND #AGE <= 59
        AND MILITARY-STATUS EQ 'R'
      #PERSONA := TED
    WHEN NONE
      #PERSONA := UNNAMED
  END-DECIDE
ELSE
  DECIDE ON FIRST VALUE OF MILITARY-STATUS
    VALUE 'A' /* ACTIVE
      IF #PERSONA = TED OR= ' '
        #SKIP := TRUE
      END-IF
    VALUE 'R' /* RETIRED
      IF #PERSONA NE TED
        #SKIP := TRUE
      END-IF
    NONE VALUE
      #SKIP := TRUE
  END-DECIDE
  DECIDE ON FIRST VALUE OF #PERSONA
    VALUE ADAM
      #AGE1 := 18
      #AGE2 := 29
    VALUE JAMES
      #AGE1 := 30
      #AGE2 := 39
    VALUE MARK
      #AGE1 := 40
      #AGE2 := 49
    VALUE TED
      #AGE1 := 50
      #AGE2 := 59
    NONE VALUE
      RESET #AGE1 #AGE2
  END-DECIDE
  IF #AGE < #AGE1 OR #AGE > #AGE2
    #SKIP := TRUE
  END-IF
  FIND (1) ST-V WITH ID-NUMBER = CNT-V.ID-NUMBER  /* Defined persona is not sponsored (S,K,G)
    IF INTRO-FLAG = 'S' OR= 'K' OR= 'G'
      #SKIP := TRUE
      ESCAPE ROUTINE
    END-IF
  END-FIND
  IF #PERSONA = UNNAMED  /* Reverse SKIP value
    IF #SKIP RESET #SKIP
    ELSE
      #SKIP := TRUE
    END-IF
  END-IF
END-IF
END

