* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* F-BILL-CYCLE
* Returns default bill cycle to assign to the policy based on issue date
*******************************************************************
* MODIFICATION LOG
* USER   DATE      TAG     REASON
*******************************************************************                                                                      *
* YAK    08222017          Create program
*****************************************************************
DEFINE FUNCTION F-BILL-CYCLE RETURNS (A1)
  DEFINE DATA
  PARAMETER
  1 #RUN-DAY (N2)
  1 #PROCESS (A) DYNAMIC
  LOCAL USING OBJLGENT
  LOCAL
  1 #SEC-KEY    (A7)
  1 #BILL-CYCLE-DATES (N2/3)
  1 #BILL-CYCLE (A1/3)
  1 #CC-KEY (A3) CONST <'BCC'>
  1 #EZ-KEY (A3) CONST <'BCE'>
  1 I (I2)
  END-DEFINE
*
DECIDE ON FIRST VALUE #PROCESS
  VALUE 'CC'
    COMPRESS 'CODE' #CC-KEY INTO #SEC-KEY LEAVING NO
    PERFORM FIND-DATES
  VALUE 'EZ'
    COMPRESS 'CODE' #EZ-KEY INTO #SEC-KEY LEAVING NO
    PERFORM FIND-DATES
  VALUE 'NM','AI'
    IF #RUN-DAY >= 1 AND #RUN-DAY <=15
      F-BILL-CYCLE := '1'
    ELSE
      F-BILL-CYCLE := '2'
    END-IF
  NONE VALUE
    IGNORE
END-DECIDE
*
**==========================
DEFINE SUBROUTINE FIND-DATES
**==========================
F.
FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  I := I + 1
  #BILL-CYCLE-DATES(I) := GEN-TABLE-V.GEN-NUMBER-VALUE
  #BILL-CYCLE(I) := GEN-TABLE-V.GEN-ALPHA-VALUE
END-FIND
DECIDE FOR FIRST CONDITION
  WHEN #RUN-DAY > 0 AND #RUN-DAY <= #BILL-CYCLE-DATES(1)
    F-BILL-CYCLE := #BILL-CYCLE (1)
  WHEN #RUN-DAY > #BILL-CYCLE-DATES(1) AND #RUN-DAY <= #BILL-CYCLE-DATES(2)
    F-BILL-CYCLE := #BILL-CYCLE (2)
  WHEN NONE
    F-BILL-CYCLE := '3'
END-DECIDE
END-SUBROUTINE
*
END-FUNCTION
END
