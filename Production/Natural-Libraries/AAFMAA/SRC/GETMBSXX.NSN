* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GETMBSXX - Retrieve ISN of member record
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE      TAG  REASON                                           *
* PAM  12282006  None Program Written                                  *
* RSE  10032009  RE1  Change PROCESS-IND logic for Unification         *
* bz     11/19/2009 bz1    Remove Re1. Unification: Identify SAS Only
************************************************************************
*
DEFINE DATA
PARAMETER USING GETMBLXX
*
LOCAL
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 INTRO-FLAG
 2 PROCESS-IND
 2 STATUS (9)
*
1 #CN-NUMBER                 (N6)
1 REDEFINE #CN-NUMBER
 2 #CN-A                     (A6)
1 #SAMPLE-CN                 (P5)  INIT <1900>
1 #SERVICES                  (A1)  INIT <'S'>
1 #CONTACT                   (A1)  INIT <'C'>
1 #PENDING                   (A1)  INIT <'A'>
*
1 #AFFL-ONLY (L) INIT <FALSE>                                         /*  bz1
END-DEFINE
************************************************************************
*
RESET #ISN
IF #MEMBER-ID GT 999999
  PERFORM USE-SSN
ELSE
  PERFORM USE-MEMBER-NUMBER
END-IF
*
************************************************************************
DEFINE SUBROUTINE USE-SSN
************************************************************************
*
FIND ST-V WITH SSN = #MEMBER-ID
*  IF  (ST-V.PROCESS-IND = #SERVICES)        RE1
*  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1
PERFORM  CHECK-AFFL-ONLY                                             /*  bz1
  IF   #AFFL-ONLY                                                      /*  bz1
   OR ((ST-V.PROCESS-IND = #CONTACT) AND (INTRO-FLAG NE #PENDING))
   OR (ST-V.ID-NUMBER LE #SAMPLE-CN)
    ESCAPE TOP
  END-IF
*
  #ISN := *ISN
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE USE-MEMBER-NUMBER
************************************************************************
*
#CN-NUMBER := #MEMBER-ID
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
*  IF  (ST-V.PROCESS-IND = #SERVICES)        RE1
*  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1
PERFORM  CHECK-AFFL-ONLY                                             /*  bz1
  IF   #AFFL-ONLY                                                      /*  bz1
   OR ((ST-V.PROCESS-IND = #CONTACT) AND (INTRO-FLAG NE #PENDING))
   OR (ST-V.ID-NUMBER LE #SAMPLE-CN)
    ESCAPE TOP
  END-IF
*
  #ISN := *ISN
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
** - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE CHECK-AFFL-ONLY                                       /*  bz1
*  PERFORM  CHECK-AFFL-ONLY                                             /*  bz1
* 1 #AFFL-ONLY (L) INIT <FALSE>                                         /*  bz1
 IF ( ST-V.PROCESS-IND <> "S" ) OR ( ST-V.STATUS(*) <> " " )            /*  bz1
   #AFFL-ONLY := FALSE                                                  /*  bz1
 ELSE                                                                   /*  bz1
   IF ST-V.INTRO-FLAG NE 'A'  #AFFL-ONLY := TRUE                        /*  bz1
      ELSE #AFFL-ONLY := FALSE                                          /*  bz1
   END-IF                                                               /*  bz1
 END-IF                                                                 /*  bz1
END-SUBROUTINE                                                          /*  bz1
*                                                                       /*  bz1
*
END
