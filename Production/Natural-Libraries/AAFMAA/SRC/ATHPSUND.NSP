* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPSUND - Net out PREP DC records from Sungard
* * * * * * * * * * * * * * * * * * * * *
*                       MODIFICATION LOG
* USER   DATE      TAG     REASON
* DRW    03222005  DW1     Create
*********************************************
*
DEFINE DATA
LOCAL
*
1 #SUNGARD-IN    (A44)
1 REDEFINE #SUNGARD-IN
 2 #SUN-TRAN-IN        (A4)
 2 #SUN-CAT-1-IN       (A2)
 2 #SUN-CAT-2-IN       (A2)
 2 #SUN-STATE-IN       (A2)
 2 #SUN-AMT-SIGN-IN    (A1)
 2 #SUN-AMT-IN         (N6.2)
 2 #SUN-FILL-IN        (A1)
 2 #SUN-SOURCE-IN      (A2)
 2 #SUN-CN-IN          (N6)
 2 #SUN-POL-NUM-IN     (A16)
*
1 #SUNGARD-OUT    (A44)
1 REDEFINE #SUNGARD-OUT
 2 #SUN-TRAN        (A4)
 2 #SUN-CAT-1       (A2)
 2 #SUN-CAT-2       (A2)
 2 #SUN-STATE       (A2)
 2 #SUN-AMT-SIGN    (A1)
 2 #SUN-AMT         (N6.2)
 2 #SUN-FILL        (A1)
 2 #SUN-SOURCE      (A2)
 2 #SUN-CN          (N6)
 2 #SUN-POL-NUM     (A16)
*
1 #AMT    (P7.2)
1 #AMT-SIGN  (A1)
1 #CNT-2  (P5)
1 #SUN-IN    (P13)
1 #SUN-OUT   (P13)
*
END-DEFINE
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
              'Sungard Duplicate netting'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'Sungard Netting'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 1 RECORD #SUNGARD-IN
  ADD 1 TO #SUN-IN
*
  IF   (#SUN-TRAN-IN    = 'PREP') AND (#SUN-TRAN  = 'PREP')
   AND (#SUN-CAT-1-IN   = 'DC')   AND (#SUN-CAT-1 = 'DC')
   AND (#SUN-CN-IN      = #SUN-CN)
   AND (#SUN-POL-NUM-IN = #SUN-POL-NUM)
   AND (#SUN-SOURCE-IN  = #SUN-SOURCE)
*
    RESET #AMT #AMT-SIGN
    IF #SUN-AMT-SIGN-IN = '-'
      SUBTRACT #SUN-AMT-IN FROM #AMT
    ELSE
      ADD #SUN-AMT-IN TO #AMT
    END-IF
*
    IF #SUN-AMT-SIGN = '-'
      SUBTRACT #SUN-AMT FROM #AMT
    ELSE
      ADD #SUN-AMT TO #AMT
    END-IF
*
    IF #AMT LT 0
      MOVE '-' TO #AMT-SIGN
      COMPUTE #AMT = #AMT * -1  /* Make amt positive again
    ELSE
      RESET #AMT-SIGN
    END-IF
*
    ADD 1 TO #CNT-2
    DISPLAY (2)  #CNT-2 (EM=ZZZ9)
      'CN' #SUN-CN-IN
      'CatAT/1' #SUN-CAT-1-IN
      'Amt/1' #SUN-AMT-IN
      'Sign/1' #SUN-AMT-SIGN-IN
      'Cat/2' #SUN-CAT-1
      'Amt/2' #SUN-AMT
      'Sign/2' #SUN-AMT-SIGN
      'New-Amt' #AMT   (EM=-Z,ZZZ.99)
      'New/Sign' #AMT-SIGN
*
    MOVE #AMT TO #SUN-AMT
    MOVE #SUN-AMT-SIGN TO #SUN-AMT-SIGN
*
    IF #AMT NE 0
      WRITE WORK FILE 2 #SUNGARD-OUT
      ADD 1 TO #SUN-OUT
    END-IF
*
    RESET #SUNGARD-OUT
    ESCAPE TOP
  END-IF
*
  IF #SUNGARD-OUT NE ' '
    WRITE WORK FILE 2 #SUNGARD-OUT
    ADD 1 TO #SUN-OUT
  END-IF
*
  MOVE #SUNGARD-IN TO #SUNGARD-OUT
END-WORK
*
IF #SUNGARD-OUT NE ' '
  WRITE WORK FILE 2 #SUNGARD-OUT
  ADD 1 TO #SUN-OUT
END-IF
*
WRITE (2) 'Tot-in =' #SUN-IN   (EM=ZZZ,ZZZ)
WRITE (2) 'Tot-out=' #SUN-OUT  (EM=ZZZ,ZZZ)
*
END
