* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram OBJNMP01.
/**
/** :author nguentchev
/* TODO: Enter your code here
DEFINE DATA
PARAMETER
1 #REQUEST      (A) DYNAMIC
1 #MSG          (A) DYNAMIC
PARAMETER USING OBJAMP01
LOCAL USING OBJLMP01
END-DEFINE
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE 'ADD'    PERFORM ADD-RECORD
  VALUE 'UPDATE' PERFORM UPDATE-RECORD
  VALUE 'DELETE' PERFORM DELETE-RECORD
  VALUE 'GET'    PERFORM GET-RECORD
  VALUE 'EXIST'  PERFORM CHECK-EXIST
  NONE VALUE
    IGNORE
END-DECIDE
*
DEFINE CHECK-EXIST
*
F-NUMBER.
FIND NUMBER SVC-PLAN-V WITH SVC-PLAN = #SVC-PLAN.SVC-PLAN
*
END-SUBROUTINE
/*
DEFINE ADD-RECORD
PERFORM CHECK-EXIST
IF *NUMBER ( F-NUMBER.) > 0
  #MSG := 'No duplicate key allowed - record exist'
ELSE
  MOVE BY NAME #SVC-PLAN TO SVC-PLAN-V
  SVC-PLAN-V.LAST-UPD-USER := *USER
  SVC-PLAN-V.LAST-UPD-DATE := *DATN
  SVC-PLAN-V.LAST-UPD-TIME := *TIMN
  STORE SVC-PLAN-V
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
FIND SVC-PLAN-V WITH SVC-PLAN = #SVC-PLAN.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := 'No record exist for update'
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #SVC-PLAN TO SVC-PLAN-V
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE DELETE-RECORD
FIND SVC-PLAN-V WITH SVC-PLAN = #SVC-PLAN.SVC-PLAN
  IF NO RECORDS FOUND
    #MSG := 'No record to delete'
    ESCAPE BOTTOM
  END-NOREC
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
/**
DEFINE GET-RECORD
FIND SVC-PLAN-V WITH SVC-PLAN = #SVC-PLAN.SVC-PLAN
  MOVE BY NAME SVC-PLAN-V TO #SVC-PLAN
END-FIND
END-SUBROUTINE
/**
END
