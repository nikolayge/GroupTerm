* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: GANNUSXX   ** Calculate Annuities with DEATH BENE or CASH VAL
*******************************************
*                       MODIFICATION LOG
* USER   DATE      TAG  REASON
* PAM    02242005  DW1  Create Program
* DRW    03242005  DW2  Special Calc on selected members (554375/17804)
* DRW    10062005  DW3  point above adjustment to $10,000 policy
* YAK    10152014  YK1  Replace 'AL' with function
******************************************
*
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND  (A2)
1 #CGA-SCR-ID       (N9)
1 REDEFINE #CGA-SCR-ID
  2 #CGA-SCR-ID-FILL (A3)
  2 #CN              (A6)
1 #CGA-SCR-SUFFIX   (A3)
1 #CGA-CLERK        (A3)
*
1 #BOP-ANNUI (N7.2) /* Amt to annuitize (DB if "BP'/CSV if "AL or 'CSV')
1 #BOP-SEX     (A1)
1 #BOP-BENE-AGE(N4)  /* For CSV this must be 100 - Iss-age - Duration
1 #BOP-BENE-A  (N7.2)
1 #BOP-BENE-B  (N7.2)
1 #BOP-BENE-C  (N7.2)
1 #BOP-RESV-AMT-A (N9.2)
1 #BOP-RESV-AMT-B (N9.2)
1 #BOP-TAXBL-PCT-A  (N2.7)
1 #BOP-TAXBL-PCT-B  (N2.7)
1 #BOP-TAXBL-AMT-A  (N7.2)
1 #BOP-TAXBL-AMT-B  (N7.2)
1 #BOP-TAXBL-MON-A  (N7.2)
1 #BOP-TAXBL-MON-B  (N7.2)
1 #ISSUE-DATE       (N8) OPTIONAL
*
LOCAL
1 #GUAR-MONS      (P5)
1 #INT            (N2.7)
1 #ANN            (P5.7)
1 #GUAR-INCOME    (N7.7)
1 #EXCLUDABLE-AMT (N7.7)
1 #TAXABLE-AMT    (N7.7)
1 #TAXABLE-PCT    (N5.7)
1 #EFF-DATE       (N8) CONST <20141231>
*
END-DEFINE
* * * * * * * * * * *
IF #CGA-SCR-SUFFIX = 'CSV' OR IS-ANNUITY-LIFE (<#CGA-SCR-SUFFIX>)
  COMPUTE  #GUAR-MONS = (100 - #BOP-BENE-AGE) * 12
  IF #ISSUE-DATE SPECIFIED
    IF #ISSUE-DATE > #EFF-DATE
      COMPUTE #INT = 1.035 ** (1/12)
    ELSE
      COMPUTE #INT = 1.04 ** (1/12)
    END-IF
  ELSE
    IF *DATN > #EFF-DATE
      COMPUTE #INT = 1.035 ** (1/12)
    ELSE
      COMPUTE #INT = 1.04 ** (1/12)
    END-IF
  END-IF
  COMPUTE ROUNDED #TAXABLE-AMT = 1 / #INT
  COMPUTE ROUNDED #EXCLUDABLE-AMT = 1 / #INT ** #GUAR-MONS
  COMPUTE ROUNDED #GUAR-INCOME = (1 - #EXCLUDABLE-AMT) / ( 1 - #TAXABLE-AMT )
  COMPUTE ROUNDED #GUAR-INCOME = #BOP-ANNUI / #GUAR-INCOME
*
  COMPUTE ROUNDED #EXCLUDABLE-AMT = #BOP-ANNUI / #GUAR-MONS
  COMPUTE ROUNDED #TAXABLE-AMT = #GUAR-INCOME - #EXCLUDABLE-AMT
  COMPUTE ROUNDED #TAXABLE-PCT = (#EXCLUDABLE-AMT / #GUAR-INCOME)
  MOVE #GUAR-INCOME TO #BOP-BENE-A
  MOVE #TAXABLE-AMT TO #BOP-TAXBL-MON-A
  COMPUTE #BOP-TAXBL-AMT-A = #TAXABLE-AMT * 12
  MOVE #TAXABLE-PCT TO #BOP-TAXBL-PCT-A
END-IF
*
END
