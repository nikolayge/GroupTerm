* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID  SAGSRT12
************************************************************************
*                       MODIFICATION LOG
* USER DATE      TAG   REASON
*
* SAG  11252005  RH1   SS - Created for implementing sorting algorithm *
************************************************************************
DEFINE DATA PARAMETER
**
1 #SRT-TABLE        (800)
  2 #TR-DATN        (N8)
  2 #TR-TIMN        (N7)
  2 #ISN            (N8)
**
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2)
  2 #SRT-ENTRY-LENGTH (I2)
  2 #SRT-KEY-LENGTH   (I2)
  2 #SRT-KEY-POSITION (I2)
  2 #SRT-SORT-TYPE    (A1)
**
LOCAL
**
1 #SRT-WORK         (800)
  2 #TR-DATN        (N8)
  2 #TR-TIMN        (N7)
  2 #ISN            (N8)
1 REDEFINE #SRT-WORK
  2 #SRT-ROW        (A23/1:800)
1 REDEFINE #SRT-WORK
  2 #SRT-REC        (800)
    3 #SRT-KEY      (A15)
    3 #SRT-FILL     (A8)
**
1 #I                (P7)
1 #ROWS             (P7)
1 #DONE             (L)  INIT <FALSE>
1 #TEMP-ROW         (A23)
**
END-DEFINE
**
**  first eliminate any empty rows
FOR #I EQ 1 TO 800
  IF      #SRT-TABLE.#TR-DATN (#I) EQ 0
      AND #SRT-TABLE.#TR-TIMN (#I) EQ 0
      AND #SRT-TABLE.#ISN (#I)     EQ 0
    ESCAPE TOP
  ELSE
    ADD 1 TO #ROWS
    MOVE BY NAME #SRT-TABLE (#I) TO #SRT-WORK (#ROWS)
  END-IF
END-FOR
IF #ROWS EQ 0 ESCAPE ROUTINE END-IF
**

DECIDE ON FIRST VALUE OF #SRT-SORT-TYPE
**
  VALUE 'A'
    REPEAT UNTIL #DONE
      #DONE := TRUE
      FOR #I EQ 2 TO #ROWS
        IF #SRT-KEY (#I) LT #SRT-KEY (#I - 1)
          MOVE #SRT-ROW (#I - 1) TO #TEMP-ROW
          MOVE #SRT-ROW (#I) TO #SRT-ROW (#I - 1)
          MOVE #TEMP-ROW TO #SRT-ROW (#I)
          #DONE := FALSE
        END-IF
      END-FOR
    END-REPEAT
**
  VALUE 'D'
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT AVAILABLE - EXECUTION TERMINATED'
    STOP
**
  NONE VALUE
    WRITE // 'ERROR IN SAGSORT - SORT TYPE' #SRT-SORT-TYPE
      'NOT RECOGNIZED - EXECUTION TERMINATED'
    STOP
**
END-DECIDE
**
RESET #SRT-TABLE (*)
FOR #I EQ 1 TO #ROWS
  MOVE BY NAME #SRT-WORK (#I) TO #SRT-TABLE (#I)
END-FOR
#SRT-NUM-ENTRIES := #ROWS  /* SUBJECT TO CONFIRMATION
**
END
