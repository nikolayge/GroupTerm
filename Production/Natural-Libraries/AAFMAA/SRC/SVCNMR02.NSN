* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* SERVICES                                                             *
************************************************************************
* NAME         : SVCNMR02                                              *
* AUTHOR       : Copyright: MetrostarSystems AAFMAA                    *
* DESCRIPTION  : Get service rates                                     *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG    20111208      Initial Creation
* **********************************************************************
*
DEFINE DATA
PARAMETER USING OBJAMR01
PARAMETER
1 #MSG        (A) DYNAMIC
LOCAL USING OBJLMR01
END-DEFINE
*
RESET #MSG
IF #SVC-RATES.SVC-PLAN = ' '
  #MSG := 'No Service plan'
  ESCAPE ROUTINE
END-IF
IF #SVC-RATES.SVC-PLAN-AGE-FROM = 0
  #MSG := 'No Age For Service Rates'
  ESCAPE ROUTINE
END-IF
#SVC-RATES.SVC-PLAN-AGE-TO := #SVC-RATES.SVC-PLAN-AGE-FROM
FIND SVC-RATES-V WITH SVC-PLAN = #SVC-RATES.SVC-PLAN
  IF NO RECORDS
    #MSG := 'No rates for requested plan'
    ESCAPE ROUTINE
  END-NOREC
  IF *DATN < SVC-RATES-V.SVC-RATE-EFFECTIVE-DATE
      OR *DATN > SVC-RATES-V.SVC-RATE-END-DATE
    ESCAPE TOP
  END-IF
  IF #SVC-RATES.SVC-PLAN-AGE-FROM >= SVC-RATES-V.SVC-PLAN-AGE-FROM
      AND #SVC-RATES.SVC-PLAN-AGE-TO <= SVC-RATES-V.SVC-PLAN-AGE-TO
    MOVE BY NAME SVC-RATES-V TO #SVC-RATES
    ESCAPE BOTTOM
  END-IF
END-FIND
*
ON ERROR
  COMPRESS *PROGRAM *ERROR-NR *ERROR-LINE INTO #MSG
  ESCAPE ROUTINE
END-ERROR
END
