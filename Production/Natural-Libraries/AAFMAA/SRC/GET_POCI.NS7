* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GET-POL-OWNER-CI                                      *
* DESCRIPTION  : Input parameter is #POLICY-NUMBER. This Function      *
*                returns Contact ID of the owner of the policy         *
************************************************************************
* USER  DATE       TAG   REASON                                        *
* YAK  08/01/2015  NONE  Initial Creation                              *
************************************************************************
DEFINE FUNCTION GET-POL-OWNER-CI RETURNS (N8/10)
  DEFINE DATA
  PARAMETER
  1 #POLICY-NUMBER        (A16)  BY VALUE
  LOCAL
  1 POS-V VIEW OF A-POLICY-CONTACTS
    2 POLICY-NUMBER    (A16)
    2 RELATION         (N2)
    2 REFERENCE-NUMBER (N6)
    2 CONTACT-ID       (N8)
  1 #PRIMARY-OWNER     (N2) CONSTANT    <2>
  1 #CO-OWNER          (N2) CONSTANT    <10>
  1 #I                 (I2)
  END-DEFINE
  RESET GET-POL-OWNER-CI(*)
  #I := 1
FIND POS-V WITH POLICY-NUMBER = #POLICY-NUMBER
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  IF RELATION = #PRIMARY-OWNER
    GET-POL-OWNER-CI(1) := POS-V.CONTACT-ID
  END-IF
  IF RELATION = #CO-OWNER
    #I := #I + 1
    GET-POL-OWNER-CI(#I) := POS-V.CONTACT-ID
  END-IF
END-FIND
*
END-FUNCTION
END
