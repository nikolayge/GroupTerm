* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPATST   ** DOWNLOAD AT STATUS INFO FILE TO AAFMAA
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 ST-NOTE-USER (50)
  2 ST-NOTE-DATE (50)
  2 ST-NOTE-STATUS (50)
*
1 #EXTRACTS       (P6)
1 #NUM-RECS       (P6)
1 #EXTRACT        (A23)
*
1 #I1    (I2)
1 #I2    (I2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=62
WRITE      TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
COMPRESS 'ATNum'                     /* 082203
  #TAB  'User'                      /* 082203
  #TAB  'Date'                      /* 082203
  #TAB  'Status'                    /* 082203
  INTO #EXTRACT LEAVING NO      /* 082203
ADD 1 TO #NUM-RECS                   /* 082203
WRITE WORK FILE 1 #EXTRACT           /* 082203
*
READ AT-V PHYSICAL
  IF ID-NUMBER = 0
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #EXTRACTS
* DISPLAY ID-NUMBER NAME SSN
  FOR #I1 1 TO 50
    IF ST-NOTE-USER (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
*
    COMPRESS AT-V.ID-NUMBER
      #TAB ST-NOTE-USER (#I1)
      #TAB ST-NOTE-DATE (#I1)
      #TAB ST-NOTE-STATUS (#I1)
      INTO #EXTRACT LEAVING NO
    ADD 1 TO #NUM-RECS
    WRITE WORK FILE 1 #EXTRACT
  END-FOR
END-READ /* (0028)
*
WRITE (2) 'NUMBER RECORDS  = ' #NUM-RECS
*
*
END
