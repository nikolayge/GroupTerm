* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP005e - mr's using "VP" & "CO"
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 #PARM-INPUT    (A36)
1 #PARM-CHOICE   (A12)
1 #PARM-CLERK    (A3)
*
*
* * * *   M A N U A L   R E Q U E S T   F I L E   * * * * * * *
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
   3 FIR-IND       (N1)
   3 MSG-IND       (N1)
   3 RET-IND       (N1)
   3 FIN-IND       (N1)
   3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
   3 BEN-IND       (A1)
   3 MAL-IND       (A1)
   3 BEN-SHO-IND   (A1)
   3 ACC-IND       (A1)
   3 CAP-IND       (A1)
   3 APP-IND       (A1)
   3 PIL-IND       (A1)
   3 CO-IND        (A1)
   3 VP-IND        (A1)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
     4 REQUEST-MM-N  (N2)
    3 REQUEST-DD   (A2)
  2 ASSUM-RET-DATE  (N8)
  2 CO-RQSTS      (A3/10)
  2 REDEFINE CO-RQSTS
   3 CO-RQSTS-OCC  (10)
    4 CO-CERT-SEQ  (N1)
    4 CO-CONV-TYPE (A2)
    4 REDEFINE CO-CONV-TYPE
     5 CO-CONV-TYPE-1 (A1)
     5 CO-CONV-TYPE-2 (A1)
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 RESTRICT-MBR-SW   (A1)
  2 SUSPEND-CODE      (A1)
  2 NUMBER-MEMBERSHIPS
*
1 #DATN       (N8)
1 REDEFINE #DATN
 2 #YYYY     (N4)
 2 REDEFINE #YYYY
  3 #CENT   (N2)
  3 #YY     (A2)
 2 #MMDD     (N4)
 2 REDEFINE #MMDD
  3 #MM     (A2)
  3 REDEFINE #MM
   4 #MM-N  (N2)
  3 #DD     (A2)
*
1 #COUNTERS
 2 #VP-RQST-CNT   (P5)
 2 #CO-RQST-CNT   (P5)
*
1 #I1               (I2)
1 #I2               (I2)
1 #IFL              (I2)
1 #ISN              (P8)
*
END-DEFINE
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
         5X 'VP/CO  LETTER DETAIL'
         5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
INPUT #PARM-INPUT                    /* TYPE OF MAIL-OUTS
MOVE #PARM-INPUT TO #PARM-CHOICE
*
IF #PARM-CHOICE = 'RQST'
  INPUT #PARM-CLERK                    /* CLERK
END-IF
*
INPUT #PARM-INPUT  /* MEMBER DATE RANGE
INPUT #PARM-INPUT  /* MEMBER AGE 62 DATE RANGE
INPUT #PARM-INPUT  /* WIDOW DATE RANGE
*
IF #PARM-CHOICE = 'MBR-RQST-WID' OR = 'MBR-RQST' OR = 'RQST'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
* LETTER REQUEST PROCESSING
* * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
* IF (#PARM-CLERK = '***') AND (REQUEST-DATE NE 9999) /* BYPASS IF
  IF (#PARM-CLERK = '***') AND (REQUEST-MM-N LT 50) /* BYPASS IF
    ESCAPE TOP                              /* entered after job started
  END-IF
*
  IF XX-R01.VP-IND NE ' '
    ADD +1 TO #VP-RQST-CNT
  END-IF
*
  IF XX-R01.CO-IND NE ' '
    ADD +1 TO #CO-RQST-CNT
  END-IF
*
  IF  (XX-R01.VP-IND NE ' ')
   OR (XX-R01.CO-IND NE ' ')
    IGNORE
  ELSE
    ESCAPE TOP
  END-IF
END-ALL  /* (0116)
*
SORT BY ID-NUMBER
  USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE RQST-TABLE
*
  RESET *ISN #ISN
  IF XX-R01.CO-IND NE ' '
    FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
      IF NO RECORDS FOUND
        FIND (1) ST-V WITH WIDOW-KEY = ID-NUMBER-A
          IF NO RECORDS FOUND
            WRITE 'AOAP005E -MBR NOT FOUND' ID-NUMBER-A
            WRITE (2) 'AOAP005E -MBR NOT FOUND' ID-NUMBER-A
            ESCAPE BOTTOM
          END-NOREC
*
          MOVE *ISN TO #ISN
        END-FIND
      END-NOREC
*
      IF #ISN = 0
        MOVE *ISN TO #ISN
      END-IF
    END-FIND
*
    IF #ISN = 0
      ESCAPE TOP
    END-IF
*
    MOVE #ISN TO #CGA-ISN
  END-IF
*
  MOVE XX-R01.ID-NUMBER TO #CGA-ID
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID #CGA-ORIG-ID
*
  IF XX-R01.VP-IND NE ' '
    RESET #CGA-ISN
    MOVE XX-R01.CLERK-ID TO #CGA-CLERK
    MOVE 'VP'  TO #CGA-ORIG-COMMAND #CGA-SCR-COMMAND
    MOVE 'MR'  TO #CGA-ORIG-SUFFIX  #CGA-SCR-SUFFIX
    FETCH RETURN 'C2V10PVP'
*
    IF XX-R01.VP-IND = '3' OR = '4'
      RESET #CGA-ISN
      MOVE XX-R01.CLERK-ID TO #CGA-CLERK
      MOVE 'MR'  TO #CGA-ORIG-COMMAND #CGA-SCR-COMMAND
      MOVE 'VP'  TO #CGA-ORIG-SUFFIX  #CGA-SCR-SUFFIX
      FETCH RETURN 'C2150PPO'
    END-IF
  END-IF
*
  IF XX-R01.CO-IND NE ' '
    MOVE XX-R01.CLERK-ID TO #CGA-CLERK
    MOVE 'CO'  TO #CGA-ORIG-COMMAND #CGA-SCR-COMMAND
    COMPRESS 'OW' CO-CERT-SEQ (1) INTO #CGA-SCR-SUFFIX LEAVING NO
    MOVE #CGA-SCR-SUFFIX TO #CGA-ORIG-SUFFIX
    FETCH RETURN 'C1600PCO'
  END-IF
END-SORT  /* (0138)
*
WRITE (2) /  12T 'TOTAL VP  REQUESTS' #VP-RQST-CNT
WRITE (2) /  12T 'TOTAL CO  REQUESTS' #CO-RQST-CNT
*
IF  (#VP-RQST-CNT NE 0)
  EJECT (8)
  WRITE (8) /  12T 'TOTAL VP  REQUESTS' #VP-RQST-CNT
  WRITE (8) /  12T 'TOTAL CO  REQUESTS' #CO-RQST-CNT
END-IF
*
END
