* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GPRNLTT1 - Subprogram to get rates
*************************************************************
*        MODIFICATION LOG
*************************************************************
* USER   DATE      TAG REASON
* NGG    20121107      Initial Creation
* JNT    20131030  JT1 1M Policy support
*************************************************************
DEFINE DATA
PARAMETER
1 #AGE             (N2)
1 #ISSUE-DATE      (N8)
1 #NICOTINE        (A1)
1 #LTII-TYPE       (A2)
1 #GENDER          (A1)
1 #TERM            (N2)
1 #FACE-AMT        (N7.2)
1 #APREM           (N7.2)   /* out                       /* JT1
LOCAL
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
1 #PREM-6-6           (N6.6)
1 K                   (I2)
1 #VAR-TERM-POL-FEE-2012 (N2.2) CONST <3.0>
1 LTII-V VIEW OF A-PRODUCT-RATES
  2 VAR-TERM-RATE (N3.2/65)
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 LTII-TYPE (A2)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 RIDER-OPTION (A1)
  2 LTII-KEY     (A14)
END-DEFINE
*
MOVE EDITED #ISSUE-DATE (EM=99999999) TO ##EFFECTIVE-DATE
MOVE EDITED #AGE (EM=99) TO ##AGE
##GENDER := #GENDER
##NICOTINE := #NICOTINE
IF ##NICOTINE = 'S' ASSIGN ##NICOTINE := 'Y' END-IF
##LTII-TYPE := #LTII-TYPE
*
READ LTII-V DESCENDING BY LTII-KEY STARTING FROM #LTII-KEY
  IF SUBSTR(LTII-KEY,1,6) NE SUBSTR(#LTII-KEY,1,6)
    ESCAPE BOTTOM
  END-IF
  K := #TERM - 4
  IF K < 1
    ASSIGN K = 1
  END-IF
  COMPUTE #PREM-6-6 = VAR-TERM-RATE ( K ) * (#FACE-AMT / 10000)
  IF #PREM-6-6 > 0
    ADD #VAR-TERM-POL-FEE-2012 TO #PREM-6-6
  END-IF
  MOVE ROUNDED #PREM-6-6 TO #APREM
  ESCAPE BOTTOM
END-READ
*
END
