* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
****************************************************** *********************************************
* PROGRAM-ID: NCOAOWNR                                                                             *
*                                                                                                  *
* FUNCTION  : Prepare the extract for address verification for owners extracted on the work file.  *
*                                                                                                  *
* vxt  03/03/2017  vt1  initial creation                                                           *
****************************************************************************************************
*
DEFINE DATA
LOCAL USING ADDA0001   /* Address parameter area ADDN0001
LOCAL
1 CONT   VIEW OF A-CONTACTS
  2 contact-id
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 firm-organization-name
  2 contact-type
*
1 #cn             (n6)
1 #rec-read       (n7)
1 BLANK           (A1) CONST <' '>
1 #TAB            (A1) INIT <H'09'>
1 #out-file       (A125)
1 #REASON         (A)  Dynamic
1 #ZIP-CODE       (N9)
1 REDEFINE #ZIP-CODE
  2 ZIP-CODE-5    (N5)
  2 ZIP-CODE-4    (N4)
1 REDEFINE #ZIP-CODE
  2 ZIP-CODE-5-A  (A5)
  2 ZIP-CODE-4-A  (A4)
1 #FIRST-ADDRESS  (A75)
1 #SECOND-ADDRESS (A75)
1 #FIRST-MID      (A25)
1 #NUM-RECS       (I4)
*
END-DEFINE
*
* DEFINE WORK FILE 1 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/NCOA.OWNERS.SORTED.FINAL.TXT'
*   TYPE 'ASCII'
* DEFINE WORK FILE 2 '/usr/SAG/AAFMAA/batch/workfiles/SYSDA/ATH/NCOA.OWNERS.extract.TXT'
*   TYPE 'ASCII'
*
READ work file 1 #cn
  add 1 to #rec-read
*
  PERFORM extract-owner-info
  ADD 1 TO #NUM-RECS
*
  at end of data
    write 'records read ' #rec-read /
      'records extarcted ' #num-recs /
  end-enddata
*
end-work
*
*****************
define subroutine extract-owner-info
*****************
*
find(1) cont with id-number = #cn
  if cont.CONTACT-TYPE = 'O'
    move firm-organization-name to #first-mid
  else
    COMPRESS cont.FIRST-NAME cont.MIDDLE-NAME INTO #FIRST-MID
  end-if
*
  reset #pda-address
  #ADD-CONTACT-ID := cont.contact-id
  #ADD-TYPE    := 'P'
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  move #add-zip-code to #zip-code
*
  IF #ADD-LINE-3 = BLANK
    #FIRST-ADDRESS := #ADD-LINE-1
  ELSE
    #FIRST-ADDRESS := #ADD-LINE-2
  END-IF
  #reason := 'Owner'
  COMPRESS
    #cn
    #TAB CONT.LAST-NAME
    #TAB #FIRST-MID
    #TAB #FIRST-ADDRESS
    #TAB BLANK
    #TAB #ADD-CITY
    #TAB #ADD-STATE
    #TAB ZIP-CODE-5-A
    INTO #out-file LEAVING NO
  IF ZIP-CODE-4 NE 0
    COMPRESS #out-file '-' ZIP-CODE-4-A
      INTO #out-file LEAVING NO
  END-IF
  COMPRESS #out-file
    #TAB #REASON
    INTO #out-file LEAVING NO
*
  WRITE WORK FILE 2 #out-file
end-find
*
end-subroutine
*
END
