* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPLN03 - apply loan int adjustments
*
DEFINE DATA
LOCAL
1 #LOAN-FILE-IN      /* (a21)
 2 #CN-IN      (N6)
 2 REDEFINE #CN-IN
  3 #CN-IN-A    (A6)
 2 #ISS-DT-IN  (N8)
 2 #ACC-ADJ-IN (N5.2)
*
1 #HOLD-CUR-ACCT-UPDT (N8)
1 #AMT-X   (I2)  /* 7 categories
1 #CATS-GR
 2 #CAT1   (A12)   INIT <'Under   1.01'>
 2 #CAT2   (A12)   INIT <'Under   5.01'>
 2 #CAT3   (A12)   INIT <'Under  10.01'>
 2 #CAT4   (A12)   INIT <'Under  20.01'>
 2 #CAT5   (A12)   INIT <'Under  50.01'>
 2 #CAT6   (A12)   INIT <'Under 100.01'>
 2 #CAT7   (A12)   INIT <'Over  100.00'>
1 REDEFINE #CATS-GR
 2 #CATS   (A12/7)
*
1 #DEAD-CATS       (P5/7)
1 #OTHER-WL-CATS   (P5/7)
1 #OTHER-TERM-CATS (P5/7)
1 #DEAD-CATS-AMT       (P7.2/7)
1 #OTHER-WL-CATS-AMT   (P7.2/7)
1 #OTHER-TERM-CATS-AMT (P7.2/7)
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 #ISN    (P8)
1 #CNT-2   (P5)
1 #CNT-3   (P5)
1 #CNT-4   (P5)
1 #CNT-5   (P5)
1 #CNT-6   (P5)
1 #CNT-7   (P5)
1 #CNT-8   (P5)
1 #CNT-9   (P5)
1 #CNT-10  (P5)
1 #CNT-11  (P5)
1 #ST-UPDT       (L)
1 #ST-FND        (L)
1 #POL-FND       (L)
1 #1ST-WL-X      (I2)
1 #POL-FND-X     (I2)
1 #THIS-POL-IS-WL   (L)
1 #HAS-WL           (L)
1 #THIS-POL-IS-TERM (L)
1 #HAS-TERM         (L)
1 #AIAV             (L)
1 #AIAV-X           (I2)
1 #ADCR             (L)
1 #VA               (L)
1 #VA-X             (I2)
1 #RECS-IN          (N5)
1 #TXN-CODE         (A4)
1 #INT      (I2)
1 #NOTE-TEXT (A60/10)
1 #CLERK   (A3)
1 #ACC-ADJ-DISP (A9)
1 #TXN-ACCT-NUMBER  (N3)
1 #TOT-ADJ-IN    (N6.2)
1 #NOT-PROCESSED-ADJ    (N6.2)
1 #PRIOR-INT-ADJ   (P5.2)
1 #T-INT-SGN       (A1)
1 #NUM-NOTES          (P5)
1 #NUM-PURGED         (P5)
1 #NUM-DEAD           (P5)
1 #NUM-STILL-HAS-LOAN (P5)
1 #NUM-STILL-HAS-WL   (P5)
1 #NUM-HAS-OTHER-WL   (P5)
1 #NUM-HAS-ONLY-TERM  (P5)
1 #NUM-INACTIVE       (P5)
*
1 #NUM-PURGED-AMT         (P7.2)
1 #NUM-DEAD-AMT           (P7.2)
1 #NUM-STILL-HAS-LOAN-AMT (P7.2)
1 #NUM-STILL-HAS-WL-AMT   (P7.2)
1 #NUM-HAS-OTHER-WL-AMT   (P7.2)
1 #NUM-HAS-ONLY-TERM-AMT  (P7.2)
1 #NUM-INACTIVE-AMT       (P7.2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A     (A4)
 2 #MM-A       (A2)
 2 #DD-A       (A2)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 DTS-DATN
 2 WIDOW-FLAG
 2 PROCESS-IND
 2 SSN
 2 NAME
 2 DATE-ACCT-UPDATE
 2 DEBIT-CREDIT-AMT
 2 NUMBER-MEMBERSHIPS
 2 X-LOAN-AMOUNT-2 (9)
 2 X-ACCUM-INTEREST-2 (9)
 2 STATUS      (9)
 2 ISSUE-DATE  (9)
 2 PLAN        (9)
 2 REDEFINE PLAN
  3 PLAN-OCC   (9)
   4 PLAN-A1   (A1)
   4 PLAN-A2   (A2)
 2 MTD-ACV-INT-ADJ (9)
*
1 ACCT-REG-V VIEW OF A-ACCOUNTING-REGISTER
 2 ID-NUMBER             (N6)
 2 PROCESS-IND           (A1)  /* PM - 06/2000 - SPLIT COMPANIES
 2 TRANSACTION-DATN      (P9)
 2 TRANSACTION-TIMN      (P7)
 2 TXN-CODE              (A4)
 2 SEQUENCE-NUM          (N1)
 2 NAME                  (A25)
 2 TXN-ACCT-NUMBER       (N3)
 2 ACCT-DATE             (N8)
 2 DATE-LAST-ACCT-UPDATE (N8)
 2 DEBIT-AMOUNT          (P7.2)
 2 CREDIT-AMOUNT         (P7.2)
 2 DESCRIPTION           (A20)
 2 MESSAGE-CODE          (A1)
 2 AALP-FLAG             (A1)
 2 CLERK-ID              (A3)
 2 TERMINAL-ID           (A8)
 2 CASH-CODE             (A1)
 2 SG-CATEGORY-1         (A2/15)
 2 SG-CATEGORY-2         (A2/15)
 2 SG-AMOUNT             (N6.2/15)
*
1 TR-REG-V  VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER
 2 PROCESS-IND
 2 NAME
 2 DATE-LAST-UPDATE
 2 TERMINAL-ID
 2 CLERK-ID
 2 TRANSACTION-DATN
 2 TRANSACTION-TIMN
 2 OLD-DATA            (1)
 2 NEW-DATA            (1)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=66
WRITE      TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Exceptions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (2) LS=133 PS=66
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Acct Transactions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (3) LS=133 PS=66
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Maint Transactions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (4) LS=133 PS=66
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Deaths NOT PROCESSED'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (5) LS=133 PS=66
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Purges NOT PROCESSED'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (6) LS=133 PS=66
WRITE  (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Inactives NOT PROCESSED'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (7) LS=133 PS=66
WRITE  (7) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Still has var Loan'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (8) LS=133 PS=66
WRITE  (8) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Paid Off Var loan but still pol'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (9) LS=133 PS=66
WRITE  (9) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj -Terminated this pol but has other WL pols'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (10) LS=133 PS=66
WRITE  (10) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj - Terminated this pol but has TERM pols'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (11) LS=133 PS=66
WRITE  (11) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Loan Interest Adj - NOT PROCESSED'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD #LOAN-FILE-IN   /* summarized files
  ADD 1 TO #RECS-IN
  ADD #ACC-ADJ-IN TO #TOT-ADJ-IN
*
  IF #ACC-ADJ-IN LT   1.00 MOVE 1 TO #AMT-X ELSE
  IF #ACC-ADJ-IN LT   5.00 MOVE 2 TO #AMT-X ELSE
  IF #ACC-ADJ-IN LT  10.00 MOVE 3 TO #AMT-X ELSE
  IF #ACC-ADJ-IN LT  20.00 MOVE 4 TO #AMT-X ELSE
  IF #ACC-ADJ-IN LT  50.00 MOVE 5 TO #AMT-X ELSE
  IF #ACC-ADJ-IN LT 150.00 MOVE 6 TO #AMT-X ELSE
                           MOVE 7 TO #AMT-X
  END-IF END-IF END-IF END-IF END-IF END-IF
*
  MOVE TRUE TO #ST-FND
  FIND (1) ST-V WITH MEMBER-KEY = #CN-IN-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = #CN-IN-A
        IF NO RECORDS FOUND
          RESET #ST-FND
        END-NOREC
      END-FIND
    END-NOREC
    MOVE *ISN TO #ISN
  END-FIND
*
  IF NOT #ST-FND
    ADD 1 TO #NUM-PURGED
    ADD #ACC-ADJ-IN TO #NUM-PURGED-AMT
*   WRITE #CN-IN #ISS-DT-IN 'Purged' #ACC-ADJ-IN
*
    ADD 1 TO #CNT-5
    DISPLAY (5) #CNT-5 (EM=ZZ9)
      'CN'         #CN-IN
      'Iss/Dt'     #ISS-DT-IN
      'Adj/Amt'    #ACC-ADJ-IN
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF WIDOW-FLAG = 'Y'
    ADD 1 TO #NUM-DEAD
    ADD #ACC-ADJ-IN TO #NUM-DEAD-AMT
    ADD #ACC-ADJ-IN TO #DEAD-CATS-AMT (#AMT-X)
    ADD 1           TO #DEAD-CATS (#AMT-X)
*   WRITE #CN-IN #ISS-DT-IN 'Dead' #ACC-ADJ-IN
*
    ADD 1 TO #CNT-4
    DISPLAY (4) #CNT-4 (EM=ZZ9)
      'CN'         #CN-IN
      'Iss/Dt'     #ISS-DT-IN
      'Adj/Amt'    #ACC-ADJ-IN
      'SSN'        ST-V.SSN
      'Name'       ST-V.NAME
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF NUMBER-MEMBERSHIPS = 0
    ADD 1 TO #NUM-INACTIVE
    ADD #ACC-ADJ-IN TO #NUM-INACTIVE-AMT
*   WRITE #CN-IN #ISS-DT-IN 'Inactive' #ACC-ADJ-IN
*
    ADD 1 TO #CNT-6
    DISPLAY (6) #CNT-6 (EM=ZZ9)
      'CN'         #CN-IN
      'Iss/Dt'     #ISS-DT-IN
      'Adj/Amt'    #ACC-ADJ-IN
      'Name'       ST-V.NAME
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  MOVE TRUE TO #ST-UPDT
  GET ST-V #ISN
  RESET #HAS-TERM #HAS-WL #1ST-WL-X #POL-FND #POL-FND-X
  FOR #I1 1 TO 9
    IF STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    RESET #THIS-POL-IS-TERM #THIS-POL-IS-WL
    IF   (PLAN (#I1) = 'LT' OR = 'RT')
     OR ((PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z'))
      MOVE TRUE TO #THIS-POL-IS-TERM
      MOVE TRUE TO #HAS-TERM
    ELSE
      MOVE TRUE TO #THIS-POL-IS-WL
      MOVE TRUE TO #HAS-WL
*
      IF #1ST-WL-X = 0
        MOVE #I1 TO #1ST-WL-X
      END-IF
    END-IF
*
    IF #ISS-DT-IN = ISSUE-DATE (#I1)
      MOVE TRUE TO #POL-FND
      MOVE #I1  TO #POL-FND-X
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #CN-IN = 49317
    MOVE 9 TO #I1
    PERFORM NOT-PROCESSED
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  RESET #AIAV #AIAV-X #VA #VA-X #ADCR
  IF #POL-FND
    IF X-LOAN-AMOUNT-2 (#POL-FND-X) NE 0
      MOVE TRUE TO #AIAV
      MOVE #I1 TO #AIAV-X
*     WRITE #CN-IN #ISS-DT-IN 'Still has lon on pol' #AIAV-X #ACC-ADJ-IN
    ELSE
      IF #THIS-POL-IS-WL
        ADD 1 TO #NUM-STILL-HAS-WL
        ADD #ACC-ADJ-IN TO #NUM-STILL-HAS-WL-AMT
        MOVE TRUE TO #VA
        MOVE #I1 TO #VA-X
*       WRITE #CN-IN #ISS-DT-IN 'Loan paid off but still has pol'
*           #VA-X #ACC-ADJ-IN
*
        ADD 1 TO #CNT-8
        DISPLAY (8) #CNT-8 (EM=Z,ZZ9)
          'CN'         #CN-IN
          'Iss/Dt'     #ISS-DT-IN
          'Adj/Amt'    #ACC-ADJ-IN
          'VA-x'       #VA-X
          'Iss-Date/ADJ/applied' ISSUE-DATE (#VA-X)
      ELSE
        PERFORM FIND-OTHER-POLS
      END-IF
    END-IF
  ELSE
    PERFORM FIND-OTHER-POLS
  END-IF
*                  Update record
  IF #AIAV
    IF   ((X-ACCUM-INTEREST-2 (#AIAV-X) - #ACC-ADJ-IN) LT 0)
     AND (#ACC-ADJ-IN GT 1.00)
     AND (X-LOAN-AMOUNT-2 (#AIAV-X) LT 100)
      PERFORM NOT-PROCESSED
      ESCAPE TOP      /*<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
    END-IF
*
    ADD 1 TO #NUM-STILL-HAS-LOAN
    ADD #ACC-ADJ-IN TO #NUM-STILL-HAS-LOAN-AMT
    SUBTRACT #ACC-ADJ-IN FROM X-ACCUM-INTEREST-2 (#AIAV-X)
    MOVE 'AIAV' TO #TXN-CODE
    MOVE 190    TO #TXN-ACCT-NUMBER
    PERFORM ACCT-REG
*
    ADD 1 TO #CNT-7
    DISPLAY (7) #CNT-7 (EM=Z,ZZ9)
      'CN'         #CN-IN
      'Iss/Dt'     #ISS-DT-IN
      'Adj/Amt'    #ACC-ADJ-IN
      'Loan/Bal'   X-LOAN-AMOUNT-2 (#AIAV-X)   (EM=-ZZZ,ZZZ.99)
      'New/Acc-Int' X-ACCUM-INTEREST-2 (#AIAV-X) (EM=-ZZ,ZZZ.99)
  ELSE
    IF #VA
      IF ST-V.MTD-ACV-INT-ADJ (#VA-X) = 99999.99
        RESET ST-V.MTD-ACV-INT-ADJ (#VA-X)
      END-IF
*
      MOVE ST-V.MTD-ACV-INT-ADJ (#VA-X) TO #PRIOR-INT-ADJ
      ADD #ACC-ADJ-IN TO ST-V.MTD-ACV-INT-ADJ (#VA-X)
      PERFORM MAINT-REG /* after above & B4 below to have correct amt
*
      IF ST-V.MTD-ACV-INT-ADJ (#VA-X) = 0
        MOVE 99999.99 TO ST-V.MTD-ACV-INT-ADJ (#VA-X)
      END-IF
*
      IF #VA-X NE 1      /* FILLUP PRECEEDING MULTIPLE FIELDS
        COMPUTE #I3 = #VA-X - 1
        FOR #I2 1 TO #I3
          IF ST-V.MTD-ACV-INT-ADJ (#I2) = 0
            MOVE 99999.99 TO ST-V.MTD-ACV-INT-ADJ  (#I2)
          END-IF
        END-FOR
      END-IF
*
    ELSE
      IF #ADCR
        SUBTRACT #ACC-ADJ-IN FROM DEBIT-CREDIT-AMT
        MOVE 'ADCR' TO #TXN-CODE
        MOVE 450    TO #TXN-ACCT-NUMBER
        PERFORM ACCT-REG
*
        ADD 1 TO #CNT-10
        DISPLAY (10) #CNT-10 (EM=Z,ZZ9)
          'CN'         #CN-IN
          'Iss/Dt'     #ISS-DT-IN
          'Adj/Amt'    #ACC-ADJ-IN
          'New/DB-CR'  DEBIT-CREDIT-AMT
      END-IF
    END-IF
  END-IF
*
  IF #VA
    MOVE ISSUE-DATE (#VA-X) TO #YYYYMMDD
  ELSE
    MOVE #ISS-DT-IN TO #YYYYMMDD
  END-IF
*
  COMPRESS #MM-A #DD-A #YYYY-A INTO #NOTE-TEXT (1) WITH '/'
  MOVE EDITED #ACC-ADJ-IN (EM=Z,ZZZ.99) TO #ACC-ADJ-DISP
  COMPRESS '$' #ACC-ADJ-DISP INTO #ACC-ADJ-DISP LEAVING NO
  COMPRESS '* POL ISS' #NOTE-TEXT (1) '2003 INT ADJ' #ACC-ADJ-DISP
      INTO #NOTE-TEXT (1)
*
  IF #AIAV
    COMPRESS #NOTE-TEXT (1) 'LN INT' INTO #NOTE-TEXT (1)
  ELSE
    IF #VA
      COMPRESS #NOTE-TEXT (1) 'CV INT' INTO #NOTE-TEXT (1)
    ELSE
      COMPRESS #NOTE-TEXT (1) 'DB/CR' INTO #NOTE-TEXT (1)
    END-IF
  END-IF
*
  ADD 1 TO #NUM-NOTES
  WRITE 'NOTE' #NUM-NOTES ST-V.ID-NUMBER #NOTE-TEXT (1)
  MOVE 1 TO #INT
  MOVE 'EDS' TO #CLERK
  CALLNAT 'E4205SDM' ST-V.ID-NUMBER #INT #CLERK #NOTE-TEXT (1:10)
*
  IF #ST-UPDT
    UPDATE (0275)
  END-IF
  END TRANSACTION
END-WORK
*
WRITE 'Recs-in =' #RECS-IN   (EM=ZZZ,ZZ9)
WRITE 'ADJ-in  =' #TOT-ADJ-IN   (EM=ZZZ,ZZ9.99)
*
WRITE 'Purged           =' #NUM-PURGED
    #NUM-PURGED-AMT  (EM=ZZZ,ZZZ.99)
WRITE 'Inactive         =' #NUM-INACTIVE
    #NUM-INACTIVE-AMT   (EM=ZZZ,ZZZ.99)
WRITE 'Dead             =' #NUM-DEAD
    #NUM-DEAD-AMT   (EM=ZZZ,ZZZ.99)
WRITE 'Still has loan   =' #NUM-STILL-HAS-LOAN
    #NUM-STILL-HAS-LOAN-AMT   (EM=ZZZ,ZZZ.99)
WRITE 'Still has WL     =' #NUM-STILL-HAS-WL
    #NUM-STILL-HAS-WL-AMT   (EM=ZZZ,ZZZ.99)
WRITE 'Has other-WL     =' #NUM-HAS-OTHER-WL
    #NUM-HAS-OTHER-WL-AMT   (EM=ZZZ,ZZZ.99)
WRITE 'Has only term    =' #NUM-HAS-ONLY-TERM
    #NUM-HAS-ONLY-TERM-AMT    (EM=ZZZ,ZZZ.99)
WRITE 'Not processed adj=' #CNT-11 #NOT-PROCESSED-ADJ (EM=ZZZ,ZZZ.99)
WRITE (11) 'Not processed adj=' #CNT-11 #NOT-PROCESSED-ADJ
*
WRITE '                  DEAD        OTHER-WL          OTHER-TERM'
FOR #AMT-X 1 TO 7
  WRITE #CATS (#AMT-X)
        #DEAD-CATS           (#AMT-X) (EM=ZZ9)
        #DEAD-CATS-AMT       (#AMT-X) (EM=ZZ,ZZZ.99)
        #OTHER-WL-CATS       (#AMT-X) (EM=ZZ9)
        #OTHER-WL-CATS-AMT   (#AMT-X) (EM=ZZ,ZZZ.99)
        #OTHER-TERM-CATS     (#AMT-X) (EM=ZZ9)
        #OTHER-TERM-CATS-AMT (#AMT-X) (EM=ZZ,ZZZ.99)
END-FOR
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FIND-OTHER-POLS
* * * * * * * * * * * * * * * * *
*
IF #HAS-WL
  MOVE TRUE TO #VA
  MOVE #1ST-WL-X TO #VA-X
  ADD 1 TO #NUM-HAS-OTHER-WL
  ADD #ACC-ADJ-IN TO #NUM-HAS-OTHER-WL-AMT
  ADD #ACC-ADJ-IN TO #OTHER-WL-CATS-AMT (#AMT-X)
  ADD 1           TO #OTHER-WL-CATS (#AMT-X)
* WRITE #CN-IN #ISS-DT-IN 'has-other-wl' #VA-X #ACC-ADJ-IN
*
  ADD 1 TO #CNT-9
  DISPLAY (9) #CNT-9 (EM=Z,ZZ9)
    'CN'         #CN-IN
    'Iss/Dt'     #ISS-DT-IN
    'Adj/Amt'    #ACC-ADJ-IN
    'VA-x'       #VA-X
    'Iss-Date/ADJ/applied' ISSUE-DATE (#VA-X)
ELSE
  MOVE TRUE TO #ADCR
  ADD 1 TO #NUM-HAS-ONLY-TERM
  ADD #LOAN-FILE-IN.#ACC-ADJ-IN TO #NUM-HAS-ONLY-TERM-AMT
  ADD #ACC-ADJ-IN TO #OTHER-TERM-CATS-AMT (#AMT-X)
  ADD 1           TO #OTHER-TERM-CATS     (#AMT-X)
* WRITE #CN-IN #ISS-DT-IN 'has-only-term' #ACC-ADJ-IN
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE ACCT-REG
* * * * * * * * * * * * * *
*
MOVE ST-V.DATE-ACCT-UPDATE TO #HOLD-CUR-ACCT-UPDT
RESET ACCT-REG-V
MOVE ST-V.ID-NUMBER        TO ACCT-REG-V.ID-NUMBER
MOVE ST-V.PROCESS-IND      TO ACCT-REG-V.PROCESS-IND /* PM - 06/2000
MOVE *DATN                 TO ACCT-REG-V.TRANSACTION-DATN
MOVE *TIMN                 TO ACCT-REG-V.TRANSACTION-TIMN
MOVE *INIT-ID              TO ACCT-REG-V.TERMINAL-ID
MOVE 'EDS'                 TO ACCT-REG-V.CLERK-ID
MOVE ST-V.NAME             TO ACCT-REG-V.NAME
MOVE 'A'                   TO ACCT-REG-V.CASH-CODE
RESET                         ACCT-REG-V.AALP-FLAG
MOVE *DATN                 TO ACCT-REG-V.ACCT-DATE
MOVE #TXN-CODE             TO ACCT-REG-V.TXN-CODE
*
IF #HOLD-CUR-ACCT-UPDT = 0
  MOVE *DATN               TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
ELSE
  MOVE #HOLD-CUR-ACCT-UPDT TO ACCT-REG-V.DATE-LAST-ACCT-UPDATE
END-IF
*
MOVE #ACC-ADJ-IN TO ACCT-REG-V.CREDIT-AMOUNT /* applies to ADCR/AIAV
RESET               ACCT-REG-V.DEBIT-AMOUNT
*
MOVE #TXN-ACCT-NUMBER TO ACCT-REG-V.TXN-ACCT-NUMBER
*
MOVE #ISS-DT-IN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO ACCT-REG-V.DESCRIPTION LEAVING NO
COMPRESS ACCT-REG-V.DESCRIPTION 'INTADJ 2003' INTO DESCRIPTION
*
* SG-category info not needed ???
*
STORE ACCT-REG-V
ADD 1 TO #CNT-2
DISPLAY (2) #CNT-2 (EM=Z,ZZ9)
  'CN'         #CN-IN
  'Iss/Dt'     #ISS-DT-IN
  'Adj/Amt'    #ACC-ADJ-IN
  'Code'       TXN-CODE
  'Acct/Num'   TXN-ACCT-NUMBER
  'Credit'     CREDIT-AMOUNT  (EM=Z,ZZZ.99)
  'Debit'      DEBIT-AMOUNT   (EM=Z,ZZZ.99)
  'Desc'       DESCRIPTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE MAINT-REG
* * * * * * * * * * * * * *
*
MOVE ST-V.NAME      TO TR-REG-V.NAME
MOVE ST-V.PROCESS-IND TO TR-REG-V.PROCESS-IND
MOVE ST-V.ID-NUMBER TO TR-REG-V.ID-NUMBER
MOVE ST-V.DTS-DATN  TO TR-REG-V.DATE-LAST-UPDATE
MOVE 'EDS'          TO TR-REG-V.CLERK-ID
MOVE *INIT-ID       TO TR-REG-V.TERMINAL-ID
MOVE *DATN          TO TR-REG-V.TRANSACTION-DATN
MOVE *TIMN          TO TR-REG-V.TRANSACTION-TIMN
*
IF #PRIOR-INT-ADJ LT 0
  MOVE '-' TO #T-INT-SGN
ELSE
  MOVE '+' TO #T-INT-SGN
END-IF
*
COMPRESS 'VA-ACV Prior INTADJ2003' #T-INT-SGN #PRIOR-INT-ADJ
                INTO TR-REG-V.OLD-DATA (1)
*
IF ST-V.MTD-ACV-INT-ADJ (#VA-X) LT 0
  MOVE '-' TO #T-INT-SGN
ELSE
  MOVE '+' TO #T-INT-SGN
END-IF
*
COMPRESS 'VA-ACV New INTADJ2003' #T-INT-SGN
                                           ST-V.MTD-ACV-INT-ADJ(#VA-X)
                INTO TR-REG-V.NEW-DATA (1)
*
STORE TR-REG-V
ADD 1 TO #CNT-3
DISPLAY (3) #CNT-3 (EM=Z,ZZ9)
  'CN'         #CN-IN
* 'Iss/Dt'     #ISS-DT-IN
  'Iss/Dt'     ISSUE-DATE (#VA-X)
  'Adj/Amt'    #ACC-ADJ-IN
  'Old'        OLD-DATA (1)  (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
  'New'        NEW-DATA (1)  (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE NOT-PROCESSED
* * * * * * * * * * * * * * * *
*
ADD 1 TO #CNT-11
DISPLAY (11) #CNT-11 (EM=ZZ9)
  'CN'         #CN-IN
  'Iss/Dt'     #ISS-DT-IN
  'Adj/Amt'    #ACC-ADJ-IN
  'Loan/Bal'   X-LOAN-AMOUNT-2 (#I1)
*
ADD #ACC-ADJ-IN TO #NOT-PROCESSED-ADJ
*
END-SUBROUTINE
*
END
