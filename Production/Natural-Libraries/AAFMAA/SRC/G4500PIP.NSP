* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G4500PIP
*     'IP' COMMAND PROCESSING   (INTRO MBR PROGRAM)
*
* MODIFICATIONS:
*
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING G4500LIP
LOCAL
*
1 #SCN-MSG            (A60)
1 #SCN-CLASS-YYYY     (N4/10)
1 #SCN-CLASS-YY       (N4/4)
1 #SCN-CLASS-X        (6)
 2 #SCN-CONV-PCT      (P3/10) /* USED FOR CALCULATION
 2 #SCN-CONV-PCT-NNN  (A3/10)
*
1 #SCN-CLASS-PCT      (6)
 2 #SCN-CONV-PCT-AREA (A5/10)
*
1 #SCN-SCHOOL-TOTS    (P5/6)
1 #SCN-MBR-TOTS       (P5/10)
1 #SCN-MBR-CONV-TOTS  (P5/10)
1 #SCN-CONV-PCT-TOTS  (P3/10)
1 #SCN-CONV-PCT-TOTS-NNN  (A3/10)
1 #SCN-CONV-PCT-TOTS-AREA  (A4/10)
1 #IP-UPDATE-DT       (A10)
1 #IP-CV              (C)
1 #I1         (I2)
1 #I2         (I2)
*
1 #INTRO-TOTALS (N5/4)
1 #GRAND-INTRO-TOTAL (P5)
*
1 #YYYYMMDD       (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY          (N4)
 2 REDEFINE #YYYY
  3 #CENT         (N2)
  3 #YY           (N2)
 2 #MMDD          (N4)
*
1 #CURR-IM-YYYY   (N4)
1 REDEFINE #CURR-IM-YYYY
 2 #CURR-IM-CENT (N2)
 2 #CURR-IM-YY   (N2)
*
END-DEFINE
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
MOVE *DATN TO #YYYYMMDD
*
IF #MMDD GE 0901
  COMPUTE #CURR-IM-YYYY = #YYYY + 1
ELSE
  MOVE #YYYY TO #CURR-IM-YYYY
END-IF
*
MOVE #CURR-IM-YYYY TO #SCN-CLASS-YY (4)
COMPUTE #SCN-CLASS-YY (3) = #CURR-IM-YYYY + 1
COMPUTE #SCN-CLASS-YY (2) = #CURR-IM-YYYY + 2
COMPUTE #SCN-CLASS-YY (1) = #CURR-IM-YYYY + 3
*
FOR #I1 1 TO 10
  COMPUTE #SCN-CLASS-YYYY (#I1) = #CURR-IM-YYYY - (#I1 - 1)
*
  IF #SCN-CLASS-YYYY (#I1) LT 1984
    RESET #SCN-CLASS-YYYY (#I1)
  END-IF
END-FOR
*
FIND (1) NT-V WITH ID-NUMBER = 800001
  COMPRESS NOTE-DATE-MM (1) '/' NOTE-DATE-DD (1) '/' NOTE-DATE-YYYY (1)
                                INTO #IP-UPDATE-DT LEAVING NO
*
  FOR #I1 1 TO 6
    ADD CURR-YEAR        (#I1) TO #INTRO-TOTALS (4)
    ADD CURR-YEAR-PLUS-1 (#I1) TO #INTRO-TOTALS (3)
    ADD CURR-YEAR-PLUS-2 (#I1) TO #INTRO-TOTALS (2)
    ADD CURR-YEAR-PLUS-3 (#I1) TO #INTRO-TOTALS (1)
    ADD CURR-YEAR        (#I1) TO #SCN-SCHOOL-TOTS (#I1)
    ADD CURR-YEAR-PLUS-1 (#I1) TO #SCN-SCHOOL-TOTS (#I1)
    ADD CURR-YEAR-PLUS-2 (#I1) TO #SCN-SCHOOL-TOTS (#I1)
    ADD CURR-YEAR-PLUS-3 (#I1) TO #SCN-SCHOOL-TOTS (#I1)
  END-FOR
*
  FOR #I1 1 TO 4
    ADD #INTRO-TOTALS (#I1) TO #GRAND-INTRO-TOTAL
  END-FOR
END-FIND
*
MOVE '*' TO #CGA-SCR-COMMAND
INPUT TEXT 'PRESS ENTER FOR HISTORY SCREEN' USING MAP 'G4500MIP'
*
IF #CGA-SCR-COMMAND NE  '*'
  FETCH 'G1000PXX'
END-IF
*
FIND (1) NT-V WITH ID-NUMBER = 800004
  COMPRESS NOTE-DATE-MM (1) '/' NOTE-DATE-DD (1) '/' NOTE-DATE-YYYY (1)
                                INTO #IP-UPDATE-DT LEAVING NO
*
  FOR #I1 1 TO 6
    FOR #I2 1 TO 10
      ADD HIST-INTRO-MBRS      (#I1, #I2) TO #SCN-MBR-TOTS      (#I2)
      ADD HIST-INTRO-MBRS-CONV (#I1, #I2) TO #SCN-MBR-CONV-TOTS (#I2)
      COMPUTE ROUNDED #SCN-CONV-PCT (#I1, #I2) =
       (HIST-INTRO-MBRS-CONV(#I1,#I2) * 100) / HIST-INTRO-MBRS(#I1,#I2)
      MOVE EDITED #SCN-CONV-PCT (#I1, #I2) (EM=ZZ9)
                      TO #SCN-CONV-PCT-NNN (#I1, #I2)
*
      IF #SCN-CONV-PCT (#I1, #I2) NE 0
        COMPRESS #SCN-CONV-PCT-NNN (#I1, #I2) '%'
                        INTO #SCN-CONV-PCT-AREA (#I1, #I2) LEAVING NO
      END-IF
*
      IF (#I1 = 3) AND (#SCN-CLASS-YYYY (#I2) = 1986 OR = 1987)
        COMPRESS #SCN-CONV-PCT-AREA (#I1, #I2) '*'
                        INTO #SCN-CONV-PCT-AREA (#I1, #I2) LEAVING NO
      END-IF
    END-FOR
  END-FOR
*
  FOR #I2 1 TO 10
    COMPUTE ROUNDED #SCN-CONV-PCT-TOTS (#I2)
          = (#SCN-MBR-CONV-TOTS (#I2) * 100) / #SCN-MBR-TOTS (#I2)
*
    IF #SCN-CONV-PCT-TOTS (#I2) NE 0
      MOVE EDITED #SCN-CONV-PCT-TOTS (#I2) (EM=ZZ9)
                      TO #SCN-CONV-PCT-TOTS-NNN (#I2)
      COMPRESS #SCN-CONV-PCT-TOTS-NNN (#I2) '%'
                       INTO #SCN-CONV-PCT-TOTS-AREA (#I2) LEAVING NO
    END-IF
  END-FOR
*
  MOVE 'IP' TO #CGA-SCR-COMMAND
  MOVE (AD=I) TO #IP-CV
  INPUT TEXT '1986 AND 1987 ARMY ROTC NUMBERS INCLUDE AIR FORCE NUMBERS'
                                                   USING MAP 'G4501MIP'
*
  IF #IP-CV MODIFIED
    UPDATE
    END TRANSACTION
    MOVE (AD=P) TO #IP-CV
    INPUT TEXT 'COUNTS UPDATED' USING MAP 'G4501MIP'
  END-IF
END-FIND
*
END TRANSACTION
*
FETCH 'G1000PXX'
*
END
