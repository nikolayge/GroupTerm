* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPNOTC    - DETAIL REPORT OF NOTICES
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
* SAG-AB 20060825 Corrected zip code suffix to right justify and add   *
*                 leading zeros                                        *
* YAK  09022009  YK1  Create data for automated e-mail process         *
* RSE  09302009 RE1  Change 600K logic for Unification                 *
* DRW 10/8/2009 DW2 AFFL Unification                                   *
* YAK 03/18/2010 YK2  Bypass Billing Notices (P1) for 'Bill Payer'     *
*                     policies                                         *
* YAK 06/25/2010 YK3  Check for restricted email                       *
* NGG  07/19/2010 NG1 Call subprogram instead of function
* vxt 11/24/2010 vt1 change the layout of the #REPT-EXTRACT structure  *
*                    and source of the name fields and addresses       *
* vxt 07/27/2011 vt2 Add APPEND to the e-mail file definition.         *
* vxt 01/22/2013 vt3 Extended field size /ticket 1736/                 *
* JNT 09/23/2013 JT1 Address normalization (Remove unused fields)      *
* YAK 09/23/2013 YK4 Contact normalization (Remove unused fields)      *
* TMT 06/10/2014 TT1 CONTACT NORMALIZATION - DOB AND SEX               *
************************************************************************
DEFINE DATA
*
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL USING A-IFAFFL        /* PDA used with FUNCTION F-if-AFFL     /* DW2
LOCAL USING GPRA0005        /* Fields to be passed to EmailCreator YK1
LOCAL USING GPRL0001        /* Init properties                     YK1
LOCAL USING GPRA0003        /* Error log
LOCAL USING ADDA0001        /* vt1 pda for the address
LOCAL
1 #FILE-OUT    (A451)                                                      /* vt1 was 250
1 #CNT-2       (P5)
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
*
1 #REPT-EXTRACT          (A549)                                             /* vt1 was 250
1 REDEFINE #REPT-EXTRACT
  2 #RE-SORT-KEY             (A72)                                          /* vt1 was 27
  2 REDEFINE #RE-SORT-KEY
    3 #RE-SECTION           (A1)
    3 #RE-SUBSECTION        (A1)
    3 #RE-NAME              (A70)                                           /* vt1 was 25
  2 #RE-REMAINING-FIELDS    (A477)                                          /* vt1 was 223
  2 REDEFINE #RE-REMAINING-FIELDS
    3 #RE-ADDR1             (A70)                                           /* vt1 was 25 \/
    3 #RE-ADDR2             (A70)
    3 #RE-ADDR3             (A70)
    3 #RE-ADDR4             (A70)                                           /* vt1 was 25 /\
    3 #re-city              (a50)                                           /* vt1
    3 #re-state             (a2)                                            /* vt1
    3 #RE-ZIP-5             (A5)
    3 #RE-DASH              (A1)
    3 #RE-ZIP-LST-4         (A4)
    3 #RE-RANK              (A25)                                            /* vt1 was 3
    3 #RE-STATUS            (A1)
    3 #RE-CN                (A2)
    3 #RE-CN-NUMBER         (A6)
    3 REDEFINE #RE-CN-NUMBER
      4 #RE-CN-NUMBER-N      (N6)
    3 REDEFINE #RE-CN-NUMBER
      4 #RE-CN-NUM-0         (A1)
      4 #RE-CN-NUM-5         (A5)
    3 #RE-FILLER-1          (A1)
    3 #RE-OLD-SECT          (A1)
    3 #RE-NON-MBR-FLAG      (A1)
    3 #RE-REMAINING         (A98)
    3 REDEFINE #RE-REMAINING           /* FPF-DELINQUENT
      4 #RE-FPF-AMT          (N5.2)
      4 #RE-FPF-ISS-DT-YYYY  (A4)
      4 #RE-FPF-ISS-DT-MM    (A2)
      4 #RE-FPF-ISS-DT-DD    (A2)
      4 #RE-FPF-PDTO-DT-YYYY (A4)
      4 #RE-FPF-PDTO-DT-MM   (A2)
      4 #RE-FPF-PDTO-DT-DD   (A2)
      4 #RE-FPF-DUE-DT-YYYY  (A4)
      4 #RE-FPF-DUE-DT-MM    (A2)
      4 #RE-FPF-DUE-DT-DD    (A2)
      4 #RE-FPF-NEW-ADDL     (A1)
      4 #RE-FPF-INTRO-FLAG   (A1)
      4 #RE-FPF-SNG-PREM     (A1)
      4 #RE-FPF-APL-LAPS     (A1)
    3 REDEFINE #RE-REMAINING           /* PREMIUM-INFO
      4 #RE-PRM-DUE-YY       (N2)
      4 #RE-PRM-DUE-MM       (A2)
      4 #RE-PRM-MODE         (A1)
      4 #RE-PRM-SVC-ONLY     (A1)
      4 #RE-PRM-PREMADJ      (N4.2)
      4 #RE-PRM-BILLPREM     (N5.2)
      4 #RE-PRM-BILLPRIN     (N4.2)
      4 #RE-PRM-BILLINTR     (N3.2)
      4 #RE-PRM-APL-LAPS     (A1)
      4 #RE-PRM-PUSW         (A1)
      4 #RE-PRM-APL          (N2)
      4 #RE-PRM-BILLDUES     (N4.2)
      4 #RE-PRM-BILLRIDER    (N4.2)
      4 #RE-PRM-BILL-EXTRA   (N5.2)
      4 #RE-PRM-ISS-DT       (N8)
      4 #RE-PRM-PLAN         (A3)
    3 REDEFINE #RE-REMAINING           /* PAID-UP-INFO
      4 #RE-PDUP-TYPE        (A1)
      4 #RE-PDUP-MM          (A2)
      4 #RE-PDUP-DD          (A2)
      4 #RE-PDUP-YY          (N2)
      4 #RE-PDUP-AMT-BEFORE  (N4.2)
      4 #RE-PDUP-AMT-AFTER   (N4.2)
      4 #RE-PDUP-PLAN        (A3)
      4 #RE-PDUP-EFF-DT-TIME (N4)
      4 #RE-PDUP-EFF-DT-DD   (A2)
    3 REDEFINE #RE-REMAINING           /* ALLOTMENT STOP/START
      4 #RE-ALLT-ALLT-AMT    (N5.2)
      4 #RE-ALLT-DB-CR       (N5.2)
      4 #RE-ALLT-QTR-PREM    (N5.2)  /* prem + dues + fbr)
      4 #RE-ALLT-FIXED-LOAN  (N5.2)
      4 #RE-ALLT-QTR-PRM     (N5.2)
      4 #RE-ALLT-QTR-DUE     (N5.2)
      4 #RE-ALLT-QTR-FBR     (N5.2)
    3 REDEFINE #RE-REMAINING           /* LOAN INTEREST DUE
      4 #RE-INT-POL          (A5)
      4 #RE-INT-TYPE         (A1)
      4 #RE-INT-DUE-MM       (A2)
      4 #RE-INT-DUE-DD       (A2)
      4 #RE-INT-DUE-YY       (N2)
      4 #RE-INT-LOAN-AMT     (N7.2)                          /* vt3 was n5.2)
      4 #RE-INT-TOT-INTR     (N7.2)                          /* vt3 was n5.2)
      4 #RE-INT-PREPAID-INTR (N7.2)                          /* vt3 was n5.2)   
      4 #RE-INT-NET-INTR-DUE (N7.2)                          /* vt3 was n5.2)
      4 #RE-INT-OVER-CV-SW   (A1)
      4 #RE-INT-LAPSE-MONS   (N2)
      4 #RE-INT-ALP-AMT      (N4.2)
      4 #RE-INT-F-V          (A1)
      4 #RE-INT-NUM-LNS      (N1)
      4 #RE-INT-LOAN-DT-YY   (N2)
      4 #RE-INT-LOAN-DT-MM   (A2)
      4 #RE-INT-LOAN-DT-DD   (N2)
      4 #RE-INT-LOAN-RATE    (N2.3)
      4 REDEFINE #RE-INT-LOAN-RATE
        5 #RE-INT-LOAN-RATE-2   (N2)
        5 #RE-INT-LOAN-RATE-3   (N3)
      4 #RE-INT-ISS-DT       (N8)
*
1 #TOTAL-BILL    (P7.2)                                       /* vt3 was p5.2  
1 #TOTAL-E-BILL  (A12)
1 #TYPE          (A20)
1 #INSURED-CN    (A6)
1 #SPS-GEN-CN-X  (A14)
1 #SPS-GEN-NAME-X (A29)
1 #ZIP-CODE-9       (N9)
1 REDEFINE #ZIP-CODE-9
  2 #ZIP-5           (A5)
  2 #ZIP-4           (A4)
*
1 ST-BILL-V VIEW OF A-STATUS
* 1 ST-ADDR-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 member-contact-id                                    /* vt1
  2 spouse-contact-id                                    /* vt1
  2 INTRO-FLAG
*  2 NAME-SP                                             /* vt1
  2 TITLE-SP
  2 MILITARY-STATUS
  2 LINKED-CN (1)
  2 REDEFINE LINKED-CN
    3 LINKED-CN-A (A6/1)
*  2 SEX                                                /* TT1
*  2 RANK                                               /* vt1
*  2 NAME                                               /* vt1
*  2 S-ADDRESS-1                                        /* vt1
*  2 ADDRESS-1                                          /* vt1 \/
*  2 ADDRESS-2
*  2 ADDRESS-3
*  2 ZIP-CODE
*  2 REDEFINE ZIP-CODE
*    3 ZIP-CODE-5 (A5)
*    3 ZIP-CODE-4 (A4)
*  2 ZIP-CODE-DPB    (N2)
*  2 REDEFINE ZIP-CODE-DPB
*    3 ZIP-CODE-DPB-A   (A2)
*  2 REDEFINE ZIP-CODE-DPB
*    3 ZIP-CODE-DPB-TBL (N1/2)                         /* vt1 /\
  2 MILITARY-SERVICE   (A3)
  2 INS-MAIL-CODE
  2 X-ALT-LP-IND (9)
  2 X-ALT-LP-AMT (9)
  2 X-ALT-LP-IND-2 (9)
  2 X-ALT-LP-AMT-2 (9)
  2 PLAN         (9)
  2 MODE         (9)
  2 STATUS       (9)
  2 ISSUE-DATE   (9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC (9)
      4 ISS-DT-YYYY  (N4)
      4 ISS-DT-MM    (N2)
      4 ISS-DT-DD    (N2)
  2 PAID-TO-DATE (9)
  2 PAID-UP-DATE (9)
  2 MONTH-ALLOC-AMT (9)
  2 ANNUAL-PREM     (9)
  2 PAY-STATUS      (9)
  2 BILL-PAYER-FLAG (9)        /* YK2
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT           (N2)
  2 #YY             (N2)
  2 REDEFINE #YY
    3 #YY-A          (A2)
  2 #MM             (N2)
  2 REDEFINE #MM
    3 #MM-A          (A2)
  2 #DD             (N2)
  2 REDEFINE #DD
    3 #DD-A          (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYY           (N4)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A     (A8)
*
1 #TOTAL-MOD-PREM   (N5.2)
1 #MODAL-PREM    (N5.2)
*
1 #ISS-DU-DT        (A10/9)
1 #ALREADY-USED-POL (L/9)
1 #CALC-PD-TO       (N8/9)
1 #CALC-LAP-DT      (A10/9)
1 #MOD-PREM         (A9/9)
1 #MOD-PREM-X     (P5.2/9)
*
1 #SCR-DUE-GRID  (A73)
1 REDEFINE #SCR-DUE-GRID
  2 #SCR-ISS-DT   (A14)
  2 #F2       (A1)
  2 #SCR-LAP-DT   (A15)
  2 #F32      (A4)
  2 #SCR-AMT-DUE  (A11)
*
1 #NUM-POLS    (P2)
1 #RESET-TOT   (L)
1 #HAS-M       (L)
1 #MULT-M      (L)
1 #HAS-MEC-POL   (L)
1 #RT-ISS-DT     (A10)
1 #RT-MODE       (A11)
1 #YYYY-X             (N4)
1 #MM-DD-YY-1         (A10)
1 #MM-DD-YY-2         (A10)
1 #SUSPEND-DATE       (N8)
1 #SUS-DATE-MM-DD-YY  (A10)
*
1 #TOT-DUE-N          (P5.2)
1 #TOT-DUE            (A10)
1 #AMT-4-2            (A8)
1 #AMT-4-2-A          (A8)
1 #AMT-4-2-2X         (A8)
1 #AMT-4-2-3X         (A8)
1 #AMT-5-2-DUES       (A10)
1 #AMT-5-2-RIDER      (A10)
1 #AMT-5-2            (A10)
1 #AMT-UNPAID         (A10)
1 #AMT                (N5.2)
1 #AMT2               (N5.2)
1 #PRIN               (A8)
1 #PDUP-PLAN          (A8)
1 #PRM-TEXT    (A12)
1 #DUES-TEXT   (A42)
*
1 #I1              (I2)
1 #I2              (I2)
1 #I3              (I2)
1 #I4              (I2)
*
1 #ABEND-CODE       (I4)
1 #BNK-CNT     (P5)
1 #DELQ-EMAIL  (L)                               /* YK1
1 #P2-PRM-DELQ (A8) INIT <'PRM-DELQ'>            /* YK1
1 #PREM-DELQ-NOTICE (A3) INIT <'P2'>             /* YK1
1 #BILL-PAYER  (L)                               /* YK2
1 #CNT-BILL-PAYER (N5)                           /* YK2
1 #EMAIL-CN-NUMBER (A6)                          /* YK3
1 #HOME-PHONE        (A11)                       /* YK3
1 #AUTO-DIALER-CONST (A8) INIT <'0001.wav'>      /* YK3
1 #AUTODIALER-MONTHLY         (A) DYNAMIC INIT
  <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/COM/Autodialer_Monthly.txt'>       /* YK3
1 #AUTODIALER-MONTHLY-BACKUP  (A) DYNAMIC INIT
  <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/COM/Autodialer_Monthly_bkup.txt'>  /* YK3
*
1 #contact-id      (n8)                          /* vt1 \/
1 #name-f          (a70)
1 #first-name-f    (a25)
1 #middle-name-f   (a25)
1 #last-name-f     (a25)
1 #suffix-f        (a10)
*
1 Phone view of A-contact-phone
  2 PHONE-ID                (N8)
  2 Type
  2 CONTACT-ID              (N8)
  2 PHONE-TYPE-CODE         (A1)
  2 INTL-ACCESS-CODE       (N3)
  2 INTL-DIAL-NUMBER
  2 AREA-CODE               (N3)
  2 DIAL-NUMBER             (N7)
  2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3   (N3)
    3 DIAL-NUMBER-4   (N4)
1 #e-mail                   (a70)
*                                               /* vt1 /\
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* YK1 Define E-mail information file
DEFINE WORK FILE 3 #EMAIL-MONTHLY-FILE TYPE 'ASCII-COMPRESSED'
ATTRIBUTES 'APPEND'      /* vt2
* YK3 Define Autodialer file (append to existing file)
DEFINE WORK FILE 4 #AUTODIALER-MONTHLY TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
DEFINE WORK FILE 5 #AUTODIALER-MONTHLY-BACKUP TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
FORMAT (2) PS=62 LS=133
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'NOTICES created'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
COMPRESS 'CN'
  #TAB 'TYPE'
  #TAB 'TOT-BILL'
  #TAB 'NAME'
  #TAB 'FOR-NAME'
  #TAB 'FOR-CN'
  #TAB 'ADDR1'
  #TAB 'ADDR2'
  #TAB 'ADDR3'
  #TAB 'ADDR4'
  #TAB 'ZIP'
  INTO #FILE-OUT   LEAVING NO
WRITE WORK FILE 2 #FILE-OUT
*
READ WORK 1 RECORD #REPT-EXTRACT
**                  fix zip-code suffix replacing leading zeros
  IF #RE-ZIP-LST-4 EQ ' '                 /* SAG-AB 20060825 START
    IGNORE
  ELSE
    MOVE RIGHT JUSTIFIED #RE-ZIP-LST-4 TO #RE-ZIP-LST-4
    EXAMINE #RE-ZIP-LST-4 FOR ' ' REPLACE WITH '0'
  END-IF                                  /* SAG-AB 20060825 END
*
* * * * * * * DETERMINE TYPE OF NOTICE
  DECIDE ON FIRST VALUE OF #RE-OLD-SECT
    VALUE 'D' MOVE 'FPF'      TO #TYPE
      MOVE #RE-FPF-AMT TO #TOTAL-BILL
    VALUE 'K' MOVE 'INT'      TO #TYPE
      MOVE #RE-INT-TOT-INTR TO #TOTAL-BILL
    VALUE 'E' MOVE 'PRM-DELQ' TO #TYPE
      COMPUTE #TOTAL-BILL = #RE-PRM-BILLDUES + #RE-PRM-BILLRIDER
        + #RE-PRM-PREMADJ + #RE-PRM-BILLPRIN + #RE-PRM-BILLINTR
        + #RE-PRM-BILLPREM
      #DELQ-EMAIL := TRUE                         /* YK1
    VALUE 'J' MOVE 'FPF-REM'  TO #TYPE
      MOVE #RE-FPF-AMT TO #TOTAL-BILL
    VALUE 'F' MOVE 'STOP'     TO #TYPE
      MOVE #RE-ALLT-ALLT-AMT TO #TOTAL-BILL
    VALUE 'M' MOVE 'STOP'     TO #TYPE
      MOVE #RE-ALLT-ALLT-AMT TO #TOTAL-BILL
    VALUE 'N' MOVE 'START'    TO #TYPE
      MOVE #RE-ALLT-ALLT-AMT TO #TOTAL-BILL
    VALUE 'P' MOVE 'PRM-NOTC' TO #TYPE
      COMPUTE #TOTAL-BILL = #RE-PRM-BILLDUES + #RE-PRM-BILLRIDER
        + #RE-PRM-PREMADJ + #RE-PRM-BILLPRIN + #RE-PRM-BILLINTR
        + #RE-PRM-BILLPREM
    VALUE 'Q' MOVE 'PAID-UP'  TO #TYPE
      MOVE 0 TO #TOTAL-BILL
    NONE      MOVE 'UNKNOWN'   TO #TYPE
      MOVE 0 TO #TOTAL-BILL
  END-DECIDE
* logic below must match athp07p0
*
  MOVE #RE-CN-NUMBER TO #INSURED-CN
  RESET #SPS-GEN-NAME-X #SPS-GEN-CN-X
  FIND (1) ST-BILL-V WITH MEMBER-KEY = #RE-CN-NUMBER
*
* #HAS-3SW Used

    CALLNAT 'PLAN-AFA' ST-BILL-V.ID-NUMBER F-IF-AFFL-DATA /* NG1

*    IF F-IF-AFFL(<ST-BILL-V.ID-NUMBER,F-IF-AFFL-DATA>) /*TEST AFFL Conditions  DW2
*      IGNORE
*    END-IF
*
*   IF   (ST-BILL-V.PLAN (1) = MASK (..'W'))  /* afsc widow client
*       AND (#RE-CN-NUMBER GE '600000')             /* RE1
    IF #HAS-3SW                                                    /* DW2
*                                                                                /* vt1 \/
      move st-bill-v.spouse-contact-id to #contact-id
      #name-f :=  FUNCTION-FRM-NAME(<#CONTACT-ID,#FIRST-NAME-f,#MIDDLE-NAME-f,#LAST-NAME-f,#SUFFIX-f>)
      compress #first-name-f #middle-name-f #last-name-f into #re-name
*
*      MOVE ST-BILL-V.NAME-SP  TO #RE-NAME                                       /* vt1 /\
      MOVE ST-BILL-V.TITLE-SP TO #RE-RANK
    END-IF
* YK3 - save insured info
    RESET #HOME-PHONE
*
    move st-bill-v.member-contact-id to #contact-id                    /* vt1 \/
    perform get-home-phone
*
*     IF ST-BILL-V.TELE-NUMBER > 2000000000
*       MOVE EDITED ST-BILL-V.TELE-NUMBER (EM=Z9999999999) TO #HOME-PHONE
*     END-IF                                                           /* vt1   /\
*
    RESET #INTERNET-ADDR
    IF ST-BILL-V.INS-MAIL-CODE = 'E'
      #INTERNET-ADDR := 'XXX'
    ELSE
      callnat 'emailn01' #contact-id #e-mail                           /* vt1 \/
      if #e-mail ne '999'
        #internet-addr := #e-mail
*      IF ST-BILL-V.INTERNET-ADDR NE '999'
*        #INTERNET-ADDR := ST-BILL-V.INTERNET-ADDR                      /* vt1 \/
      END-IF
    END-IF
*
    IF   (#RE-OLD-SECT = 'E' OR = 'P')  /* added 9/2003
*        AND (#RE-CN-NUMBER-N LT 600000)                     RE1
        AND NOT AFFL-ONLY-CLIENT(<ST-BILL-V.ID-NUMBER>)     /* RE1
      FOR #I1 1 TO 9
        RESET #BILL-PAYER                                                  /* YK2
        IF ST-BILL-V.ISSUE-DATE (#I1) = #RE-PRM-ISS-DT
            AND (ST-BILL-V.PLAN (#I1) = #RE-PRM-PLAN)
          MOVE #RE-CN-NUMBER-N TO #CGA-SCR-ID
          MOVE #I1 TO #CONTACT-SEG
          CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
* Assign EMAIL fields if email to be sent      YK1
          IF #DELQ-EMAIL
            CALLNAT 'GPLNMSXX' EMAIL-INFORMATION.POLICY-NUMBER
              #RE-CN-NUMBER-N #I1                                       /* YK1
            EMAIL-INFORMATION.INSURED-NAME := #RE-NAME                    /* YK1
            EMAIL-INFORMATION.DAL-CN       := #RE-CN-NUMBER               /* YK1
            EMAIL-INFORMATION.OWNER-NAME   := #RE-NAME                    /* YK1
            EMAIL-INFORMATION.OWNER-RANK   := #FULL-RANK                  /* YK1
            EMAIL-INFORMATION.ADDRESS-LINE-1 := #RE-ADDR1                 /* YK3
            EMAIL-INFORMATION.ADDRESS-LINE-2 := #RE-ADDR2                 /* YK3
            EMAIL-INFORMATION.ADDRESS-LINE-3 := #RE-ADDR3                 /* YK3
            COMPRESS #RE-ADDR4 #RE-ZIP-5 #RE-DASH #RE-ZIP-LST-4
              INTO EMAIL-INFORMATION.ZIP-CODE LEAVING NO                  /* YK3
          END-IF                                                          /* YK1
          IF #RE-OLD-SECT = 'P' AND ST-BILL-V.BILL-PAYER-FLAG(#I1) = 'Y'  /* YK2
            #BILL-PAYER := TRUE                                           /* YK2
            ESCAPE BOTTOM                                                 /* YK2
          END-IF                                                          /* YK2
          IF #NUM-OWNERS NE 0
*          
          move st-bill-v.member-contact-id to #add-contact-id      /* vt1 \/
          move 'S' to #add-type
          callnat 'addn0001' #pda-address
*          
*            IF   (ST-BILL-V.S-ADDRESS-1 NE ' ')
             if #add-line-1 ne ' '                                 /* vt1 /\
                AND (#RE-NON-MBR-FLAG = 'N')
              IGNORE
            ELSE
              COMPRESS 'For' #RE-NAME          INTO #SPS-GEN-NAME-X
              COMPRESS 'With CN' #RE-CN-NUMBER INTO #SPS-GEN-CN-X
              MOVE #ID-NUM TO #RE-CN-NUMBER-N
*
*              MOVE #NAME   TO #RE-NAME                             /* vt1
              move #ext-name to #re-name                           /* vt1
*              MOVE #RANK   TO #RE-RANK                             /* vt1
              move #ext-rank to #re-rank                           /* vt1
*
              MOVE #MIL-STATUS TO #RE-STATUS
              MOVE #FIRST-ADDRESS TO #RE-ADDR1
              MOVE #ADDR-2 TO #RE-ADDR2
              MOVE #ADDR-3 TO #RE-ADDR3
              MOVE #ZIP-CODE TO #ZIP-CODE-9
              MOVE #ZIP-CODE TO #ZIP-CODE-9
              RESET #RE-ZIP-LST-4 #RE-DASH
              MOVE #ZIP-5   TO #RE-ZIP-5
              IF #ZIP-4 NE ' '
                MOVE '-'    TO #RE-DASH
                MOVE #ZIP-4 TO #RE-ZIP-LST-4
              END-IF
              IF #DELQ-EMAIL                                                  /* YK1
                EMAIL-INFORMATION.OWNER-NAME     := #RE-NAME                  /* YK1
                EMAIL-INFORMATION.OWNER-RANK     := #FULL-RANK                /* YK1
                EMAIL-INFORMATION.ADDRESS-LINE-1 := #RE-ADDR1                 /* YK3
                EMAIL-INFORMATION.ADDRESS-LINE-2 := #RE-ADDR2                 /* YK3
                EMAIL-INFORMATION.ADDRESS-LINE-3 := #RE-ADDR3                 /* YK3
                COMPRESS #RE-ADDR4 #RE-ZIP-5 #RE-DASH #RE-ZIP-LST-4
                  INTO EMAIL-INFORMATION.ZIP-CODE LEAVING NO                  /* YK3
              END-IF                                                          /* YK1
            END-IF
          END-IF
        END-IF
      END-FOR
    END-IF
  END-FIND
*
  IF #BILL-PAYER                                                              /* YK2
    #CNT-BILL-PAYER := #CNT-BILL-PAYER + 1                                    /* YK2
    WRITE (10) #RE-CN-NUMBER-N ' ' #TOTAL-BILL(EM=Z,ZZZ,ZZ9.99)               /* YK2              /* vt3  was EM= zz,zz9.99
    ESCAPE TOP                                                                /* YK2
  END-IF                                                                      /* YK2
*
  MOVE EDITED #TOTAL-BILL(EM=Z,ZZZ,ZZ9.99) TO #TOTAL-E-BILL                                       /* vt3 was (EM=ZZ,ZZ9.99)
*
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2
    'ID'    #RE-CN-NUMBER-N
*    'LINK CN' #LINK-CN
    'SECT'  #RE-SECTION
    'SUB/SECTION' #RE-SUBSECTION
    'OLD/SECT'    #RE-OLD-SECT
    'TYPE'        #TYPE (EM=XXXXXXXX)
    'AMT/BILLED'  #TOTAL-E-BILL
    'Name'        #RE-NAME (al=32)                                            /* vt1
    'For'         #SPS-GEN-NAME-X
    'With CN'     #SPS-GEN-CN-X
*
  COMPRESS #RE-CN-NUMBER-N
    #TAB #TYPE
    #TAB #TOTAL-E-BILL
    #TAB #RE-NAME
    #TAB #SPS-GEN-NAME-X
    #TAB #SPS-GEN-CN-X
    #TAB #RE-ADDR1
    #TAB #RE-ADDR2
    #TAB #RE-ADDR3
    #TAB #RE-ADDR4
    #TAB #RE-ZIP-5 #RE-DASH #RE-ZIP-LST-4
    INTO #FILE-OUT   LEAVING NO
  WRITE WORK FILE 2 #FILE-OUT
*
  IF #TYPE = #P2-PRM-DELQ                                    /* YK1
    PERFORM CREATE-EMAIL-INFO                                /* YK1
  END-IF                                                     /* YK1
*
END-WORK
*
WRITE (10) 'Number of P1 notices for "Bill Payers"' #CNT-BILL-PAYER /* YK2
WRITE (2)  'Number of P1 notices for "Bill Payers"' #CNT-BILL-PAYER /* YK2
*
***********************************
DEFINE SUBROUTINE CREATE-EMAIL-INFO                          /* YK1
***********************************
*
* Dear {OwnerName}:
* Insured Name: {InsuredName}
* Policy Number: {PolicyNumber}
* {AddressLine1}
* {AddressLine2}
* {AddressLine3}
*
* TEMPLATE-TYPE
* DAL-CN
* POLICY-NUMBER
* OWNER-NAME
* INSURED-NAME
* OWNER-RANK
* OWNER-EMAIL    (Member e-mail in this case)
* ADDRESS-LINE-1
* ADDRESS-LINE-2
* ADDRESS-LINE-3
*
EMAIL-INFORMATION.TEMPLATE-TYPE := #PREM-DELQ-NOTICE
* Check if restricted e-mail
IF #ID-NUM = VAL(#INSURED-CN)
  EMAIL-INFORMATION.OWNER-EMAIL := #INTERNET-ADDR            /* YK3
ELSE
  #EMAIL-CN-NUMBER := #ID-NUM
  EMAIL-INFORMATION.OWNER-EMAIL := FUNCTION-CN-EMAIL(<#EMAIL-CN-NUMBER>)
  IF EMAIL-INFORMATION.OWNER-EMAIL = ' ' AND #EMAIL-CN-NUMBER NE #INSURED-CN
    EMAIL-INFORMATION.OWNER-EMAIL := FUNCTION-MEMBER-EMAIL(<#INSURED-CN>)
    IF EMAIL-INFORMATION.OWNER-EMAIL = ' '
      EMAIL-INFORMATION.OWNER-EMAIL := FUNCTION-CN-EMAIL(<#INSURED-CN>)
    END-IF
  END-IF
END-IF
IF EMAIL-INFORMATION.OWNER-EMAIL NE ' '
  WRITE WORK FILE 3 EMAIL-INFORMATION
ELSE
*  Create file for Autodialer                                        /* YK3 start
  IF #ID-NUM = VAL(#INSURED-CN)
    IGNORE
  ELSE
    RESET #HOME-PHONE
    FIND(1) ST-BILL-V WITH ID-NUMBER = #ID-NUM
      move st-bill-v.member-contact-id to #contact-id                                  /* vt1 \/
      perform get-home-phone
      if #home-phone > '2000000000'
        ignore                                                                         /* vt1 /\
*      IF ST-BILL-V.TELE-NUMBER > 2000000000
*        MOVE EDITED ST-BILL-V.TELE-NUMBER (EM=Z9999999999) TO #HOME-PHONE
      ELSE
        COMPRESS 'Invalid telephone number on file for ' ST-BILL-V.ID-NUMBER
          INTO ERROR-MESSAGE
        ERROR-PROGRAM := *PROGRAM
        CALLNAT 'GPRN0003' ERROR-LOG
      END-IF
    END-FIND
  END-IF
  IF #HOME-PHONE NE ' '
    WRITE WORK FILE 4 VARIABLE #HOME-PHONE ',' #AUTO-DIALER-CONST
    WRITE WORK FILE 5 VARIABLE #HOME-PHONE #TAB #ID-NUM
  END-IF
END-IF                                                               /* YK3 end
RESET #DELQ-EMAIL EMAIL-INFORMATION
*
END-SUBROUTINE
*
define subroutine get-home-phone                                       /* vt1 \/
*
FIND PHONE WITH PHONE.CONTACT-ID = #CONTACT-ID
  if no
    escape bottom
  end-norec
  accept if PHONE.PHONE-TYPE-CODE = 'H'
  IF PHONE.TYPE = 'D'      /* domestic phones
    COMPRESS phone.AREA-CODE  PHONE.DIAL-NUMBER-3  PHONE.DIAL-NUMBER-4  INTO #home-PHONE
      LEAVING NO
  ELSE
    IF PHONE.TYPE = 'I'   /* international
      COMPRESS  PHONE.INTL-ACCESS-CODE  PHONE.INTL-DIAL-NUMBER INTO #home-PHONE
    END-IF
  END-IF
end-find
*
end-subroutine                                                          /* vt1 /\
*
*
END
