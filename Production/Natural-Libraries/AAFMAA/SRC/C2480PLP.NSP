* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2480PLP
*     'LP' COMMAND PROCESSING (NEW LP LETTERS AND MAPS AS OF 3/2002)
********************************************************************
* YAK    12052007  YK1   Remove hardcording for interest rates
*                        (Read GEN-TABLE through OBJNGENT)
* YAK    02052008  YK2   Change code to use FUNCTION-INT-RATE
*                         to get rate from GEN-TABLE
* vxt    06212011  VT1   Fix the calculation of the interest.
* NGG    20140116  NG1   Address normalization
********************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING C2480LLP
LOCAL USING ADDA0001     /* NG1
LOCAL
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID (N8)
* 1 #CURR-VAR-INT-RATE (P2.3) INIT <8.10>
1 #CURR-VAR-INT-RATE (P2.3)                    /* YK1
*
1 #SCR-SEL-CV   (C/18)
1 #SCR-SEL      (A1/18)
1 #SCR-ISS-DT   (A10/18)
1 #SCR-LOAN-AMT (N5.2/18)
1 #SCR-LOAN-DT  (A10/18)
1 #SCR-FIX-VAR  (A8/18)
1 #SCR-X        (I2/18)
1 #SCR-ISS-DATE (A10)
1 #SCR-FX-VR    (A8)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A      (A4)
  2 #MM-A        (A2)
  2 #DD-A        (A2)
*
1 #MMDDYYYY    (N8)
1 REDEFINE #MMDDYYYY
  2 #MM-A1       (A2)
  2 #DD-A1       (A2)
  2 #YYYY-A1     (A4)
*
1 #HOLD-SCR     (A77/34)
1 #SCR-TEXT-77  (A77/34)
1 REDEFINE #SCR-TEXT-77
  2 #SCR-TEXT-OCC (34)
    3 #SCR-ISSUE-DT  (A10)
    3 #SCR-FILL-1    (A8)
    3 #SCR-TYPE      (A1)
    3 #SCR-FILL-2    (A8)
    3 #SCR-LN-DT     (A10)
    3 #SCR-FILL-3    (A2)
    3 #SCR-DOLL-3    (A2)
    3 #SCR-LN-AMOUNT (A10)
    3 #SCR-FILL-5    (A2)
    3 #SCR-DOLL-5    (A2)
    3 #SCR-LN-INT    (A9)
    3 #SCR-FILL-6    (A1)
    3 #SCR-DOLL-6    (A2)
    3 #SCR-TOT-POFF  (A10)
*
1 #SCR-TOTAL      (N8.2)
1 #SCR-TOTAL-PAYOFF (A12)
1 #REFORMAT-DATE  (A10)
1 REDEFINE #REFORMAT-DATE
  2 #REFORM-MM    (A2)
  2 #FILL-1       (A1)
  2 #REFORM-DD    (A2)
  2 #FILL-2       (A1)
  2 #REFORM-YYYY  (A4)
*
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2)
1 #I4    (I2)
1 #LN-AMT         (P6.2)
1 #INT-RT         (P2.3)
1 #ACC-INT        (P5.2)
1 #NXT-YR-INT     (P5.2)
1 #LOAN-DT        (N8)
1 REDEFINE #LOAN-DT
  2 #LN-DT-YYYY    (N4)
  2 #LN-DT-MM      (N2)
  2 REDEFINE #LN-DT-MM
    3 #LN-DT-MM-A   (A2)
  2 #LN-DT-DD      (N2)
  2 REDEFINE #LN-DT-DD
    3 #LN-DT-DD-A   (A2)
*
1 #VALID-DATE     (L) INIT <FALSE>
1 #LAST-NAME    (A25)
1 #FIRST-MID    (A25)
1 #SUFFIX       (A25)
1 #DEL          (A1)
1 #RETIRED      (A9)
1 #HOLD-NAME    (A50)
1 #SCN-LINE1A  (A75) INIT<'Per your request, the above information '
  -'is furnished.'>
1 #SCN-LINE2A  (A75) INIT<'This information is furnished '
  -'at your request.'>
1 #SCN-LINE3A  (A75) INIT<'This correspondence has been prepared '
  -'for you by:'>
1 #SCN-LINE4A  (A78) INIT<'                                         '
  -'                          __________'>
1 #HOLD-DATE-YYYYMMDD (A8)
1 REDEFINE #HOLD-DATE-YYYYMMDD
  2 #HOLD-DATE-YYYYMMDD-N (N8)
*
1 #INDEX        (I2)
1 #MONTH-NUMBER  (A24) INIT<'312831303130313130313031'>
1 REDEFINE #MONTH-NUMBER
  2 #CALENDAR-DAYS (N2/12)
1 #HOLD-EDIT-AMOUNT (A9)
1 #HOLD-DAILY-INT (P3.7)
1 #HOLD-ACC-INT (P5.2)
1 #COMPUTE-DAYS (N3)
1 #SCN-CORR-NAME   (A20)
1 #CLERK-ABB-PASS  (A3)
1 #CLERK-ABB-NAME-PASS  (A20)
1 #INVALID  (L)
END-DEFINE
*
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX
*
GET STATUS-VIEW #CGA-ISN
*
MOVE *DATN TO #HOLD-TODAY-DATE
MOVE '*' TO #CGA-SCR-COMMAND
*
FOR #I1 1 TO 9
  IF X-LOAN-AMOUNT(#I1) = 0 AND X-LOAN-AMOUNT-2 (#I1) = 0
    MOVE #I1 TO #I2
  ELSE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #I2 = 9
  INPUT TEXT 'MUST HAVE A LOAN TO DO AN "LP"' USING MAP 'G1000MXX'
  FETCH 'G1000PXX'
END-IF
*
INPUT MARK *#SCN-ENTER-DATE USING MAP 'C2483MLP'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF #SCN-ENTER-DATE-N NE MASK(MMDDYYYY)
  MOVE FALSE TO #VALID-DATE
  REINPUT 'Invalid Pay-Off date (MMDDYYYY)'
    MARK*#SCN-ENTER-DATE
ELSE
  MOVE TRUE TO #VALID-DATE
END-IF
*
COMPRESS #SED-YYYY-A #SED-MM-A #SED-DD-A INTO #HOLD-DATE-YYYYMMDD
  LEAVING NO
*
IF #HOLD-DATE-YYYYMMDD-N < #HTD-YYYYMMDD
  MOVE FALSE TO #VALID-DATE
  REINPUT 'Payoff date cannot be prior to today'
    MARK *#SCN-ENTER-DATE
ELSE
  MOVE TRUE TO #VALID-DATE
END-IF
*
ADD 1 TO #HOLD-TODAY-YYYY
*
IF #HOLD-DATE-YYYYMMDD-N > #HTD-YYYYMMDD
  MOVE FALSE TO #VALID-DATE
  REINPUT  'Payoff date cannot be more than 1 yr from today'
    MARK *#SCN-ENTER-DATE
ELSE
  MOVE TRUE TO #VALID-DATE
END-IF
*
#CURR-VAR-INT-RATE := FUNCTION-INT-RATE(<'RATE5AB'>)  /* YK2
*
IF #VALID-DATE
  COMPRESS #SED-MM-A '/' #SED-DD-A '/' #SED-YYYY-A INTO #SCN-PAYOFF-DATE
    LEAVING NO
*
  MOVE *DATN TO #HOLD-TODAY-DATE
  COMPRESS #HTD-MM-A '/' #HTD-DD-A '/' #HTD-YYYY-A INTO #SCN-AS-OF-DATE
    LEAVING NO
*
  MOVE '-' TO #ZIP-HYPH
  RESET #CGA-SCR-COMMAND
  RESET #CGA-SCR-ID
  COMPRESS 'CN' ID-NUMBER-A INTO #SCN-CN LEAVING NO SPACE
  MOVE ',' TO #DEL
  SEPARATE NAME INTO #LAST-NAME #FIRST-MID #SUFFIX WITH DELIMITER #DEL
*
  IF MILITARY-STATUS = 'R'
    MOVE ', RETIRED' TO #RETIRED
  END-IF
*
  COMPRESS RANK #FIRST-MID INTO #SCN-NAME LEAVING NO SPACE
  COMPRESS #SCN-NAME #LAST-NAME #SUFFIX INTO #HOLD-NAME
  COMPRESS #HOLD-NAME #RETIRED INTO #SCN-NAME LEAVING NO SPACE
*                                             NG1 Start             
*   MOVE ZIP-CODE TO #ZIP-HOLD
*   MOVE #ZIP-H1 TO #ZIP-C1
*   MOVE #ZIP-H2 TO #ZIP-C2
* *
*   IF ADDRESS-3 = ' '
*     IF ADDRESS-2 = ' '
*       IF ADDRESS-1 = ' '
*         INPUT WITH TEXT 'ADDRESS-ZIP ERROR'
*           USING MAP 'G1000MXX'
*         FETCH 'G1000PXX'
*       ELSE
*         COMPRESS ADDRESS-1 #ZIP-COMBINE INTO #SCN-ADDR1
*       END-IF
*     ELSE
*       COMPRESS ADDRESS-2 #ZIP-COMBINE INTO #SCN-ADDR2
*       MOVE ADDRESS-1 TO #SCN-ADDR1
*     END-IF
*   ELSE
*     COMPRESS ADDRESS-3 #ZIP-COMBINE INTO #SCN-ADDR3
*     MOVE ADDRESS-2 TO #SCN-ADDR2
*     MOVE ADDRESS-1 TO #SCN-ADDR1
*   END-IF
*                                 
  RESET #PDA-ADDRESS 
  MOVE 'P' TO #ADD-TYPE                                   
  #ADD-CONTACT-ID := STATUS-VIEW.MEMBER-CONTACT-ID            
  CALLNAT 'ADDN0001' #PDA-ADDRESS                        
  MOVE #ADD-LINE-1 TO #SCN-ADDR1
  MOVE #ADD-LINE-2 TO #SCN-ADDR2
  MOVE #ADD-LINE-3 TO #SCN-ADDR3
*                                              NG1 End
  MOVE (AD=P) TO #SCR-SEL-CV (*)
*
  RESET #I3
  FOR #I1 1 TO 9
    IF X-LOAN-AMOUNT (#I1) NE 0
      ADD 1 TO #I2
      MOVE 'F'     TO #SCR-TYPE (#I2)
*     MOVE '$'     TO #SCR-DOLL-3(#I2) #SCR-DOLL-5(#I2) #SCR-DOLL-6(#I2)
      MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-ISS-DT (#I2) WITH '/'
      MOVE #SCR-ISS-DT (#I2) TO #SCR-ISSUE-DT (#I2)
*
      MOVE X-LOAN-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-LOAN-DT (#I2) WITH '/'
      MOVE #SCR-LOAN-DT (#I2) TO #SCR-LN-DT (#I2)
*
      move x-loan-date   (#i1) to #loan-dt                    /* vt1
      MOVE X-LOAN-AMOUNT (#I1) TO #LN-AMT
*
      COMPUTE ROUNDED #HOLD-DAILY-INT =
        #LN-AMT * (X-INTEREST-RATE (#I1) / 365) / 100
      PERFORM CALCULATE-NUMBER-OF-DAYS
      COMPUTE ROUNDED #HOLD-ACC-INT =
        (#COMPUTE-DAYS * #HOLD-DAILY-INT)
        + (X-ACCUM-INTEREST (#I1) + X-NXT-YR-ACCUM-INT (#I1))
*
      MOVE EDITED #LN-AMT (EM=ZZZ,ZZ9.99) TO #SCR-LN-AMOUNT(#I2)
      MOVE EDITED #HOLD-ACC-INT (EM=ZZ,ZZZ.99) TO #SCR-LN-INT(#I2)
*
      COMPUTE #HOLD-LOAN-AVAIL = (#LN-AMT + #HOLD-ACC-INT)
      MOVE EDITED #HOLD-LOAN-AVAIL (EM=ZZZ,ZZ9.99) TO #SCR-TOT-POFF(#I2)
*
      ADD #HOLD-LOAN-AVAIL TO #SCR-TOTAL
      ADD 1 TO #I3
      MOVE #SCR-TEXT-77(#I2) TO #HOLD-SCR (#I3)
    END-IF
*
    IF X-LOAN-AMOUNT-2 (#I1) NE 0
      ADD 1 TO #I2
      MOVE 'V'     TO #SCR-TYPE (#I2)
*     MOVE '$'     TO #SCR-DOLL-3(#I2) #SCR-DOLL-5(#I2) #SCR-DOLL-6(#I2)
      MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-ISS-DT (#I2) WITH '/'
      MOVE #SCR-ISS-DT (#I2) TO #SCR-ISSUE-DT (#I2)
*
      MOVE X-LOAN-DATE-2 (#I1) TO #YYYYMMDD
      move x-loan-date-2 (#i1) to #loan-dt                    /* vt1
      COMPRESS #MM-A #DD-A #YYYY-A INTO #SCR-LOAN-DT (#I2) WITH '/'
      MOVE #SCR-LOAN-DT (#I2) TO #SCR-LN-DT (#I2)
*
      MOVE X-LOAN-AMOUNT-2 (#I1) TO #LN-AMT
*
      COMPUTE ROUNDED #HOLD-DAILY-INT =
        #LN-AMT * (#CURR-VAR-INT-RATE / 365) / 100
      PERFORM CALCULATE-NUMBER-OF-DAYS
      COMPUTE ROUNDED #HOLD-ACC-INT =
        (#COMPUTE-DAYS * #HOLD-DAILY-INT)
        + (X-ACCUM-INTEREST-2 (#I1) + X-NXT-YR-ACCUM-INT-2 (#I1))
*
      MOVE EDITED #LN-AMT (EM=ZZZ,ZZ9.99) TO #SCR-LN-AMOUNT(#I2)
      MOVE EDITED #HOLD-ACC-INT (EM=ZZ,ZZZ.99) TO #SCR-LN-INT(#I2)
*
      COMPUTE #HOLD-LOAN-AVAIL = (#LN-AMT + #HOLD-ACC-INT)
      MOVE EDITED #HOLD-LOAN-AVAIL (EM=ZZZ,ZZ9.99) TO #SCR-TOT-POFF(#I2)
*
      ADD #HOLD-LOAN-AVAIL TO #SCR-TOTAL
      ADD 1 TO #I3
      MOVE #SCR-TEXT-77(#I2) TO #HOLD-SCR (#I3)
    END-IF
  END-FOR
*
  MOVE #CGA-CLERK TO #CLERK-ABB-PASS
  MOVE ' ' TO #CLERK-ABB-NAME-PASS
  CALLNAT 'GCKNMSXX' #CLERK-ABB-PASS
    #INVALID
    #CLERK-ABB-NAME-PASS
  MOVE #CLERK-ABB-NAME-PASS TO #SCN-CORR-NAME
  RESET #CGA-SCR-SUFFIX
*
****** WRITE DETAIL LINES ON LETTER  ***********
*
  RESET #SCR-TEXT-77 (*)
  MOVE 1 TO #I4
  FOR #I3 1 TO 34
    IF #HOLD-SCR (#I3) > ' '
      MOVE #HOLD-SCR (#I3) TO #SCR-TEXT-77 (#I4)
      ADD 1 TO #I4
    END-IF
  END-FOR
*
*************  WRITE SUMMARY LINES ON LETTER *************
*
  IF X-LOAN-AMOUNT (*) > 0 OR X-LOAN-AMOUNT-2 (*) > 0
    COMPRESS #SCN-LINE4A INTO #SCR-TEXT-77 (#I4)
    ADD 2 TO #I4
*
    MOVE EDITED #SCR-TOTAL (EM=ZZZ,ZZ9.99) TO #SCR-TOTAL-PAYOFF
    COMPRESS '                                                 Total'
      'Loan Payoff    ' #SCR-TOTAL-PAYOFF INTO #SCR-TEXT-77 (#I4)
    ADD 1 TO #I4
  END-IF
*
  ADD 1 TO #I4
  COMPRESS #SCN-LINE2A INTO #SCR-TEXT-77 (#I4)
  ADD 1 TO #I4
*
  ADD 1 TO #I4
  COMPRESS #SCN-LINE3A #SCN-CORR-NAME INTO #SCR-TEXT-77 (#I4)
*
  INPUT MARK*#CGA-SCR-COMMAND  USING MAP 'C2482MLP'
  IF #I4 > 12
    INPUT MARK*#CGA-SCR-COMMAND  USING MAP 'C2484MLP'
  END-IF
END-IF
*
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALCULATE-NUMBER-OF-DAYS
* * * * * * * * * * * * * * * * * * * * * *
*
IF #SED-MM-N = #HOLD-TODAY-MM
  IF #SED-YYYY-N = #HOLD-TODAY-YYYY
    MOVE #SED-DD-N TO #COMPUTE-DAYS
    IF #SED-MM-N = #LN-DT-MM AND #SED-YYYY-N = #LN-DT-YYYY
      COMPUTE #COMPUTE-DAYS = #COMPUTE-DAYS - #LN-DT-DD
    ELSE
      COMPUTE #COMPUTE-DAYS = #COMPUTE-DAYS - 1
    END-IF
    ESCAPE ROUTINE
  ELSE
    MOVE 364 TO #COMPUTE-DAYS   /*  365 IN A YEAR - 1 FOR CALCS
    ESCAPE ROUTINE
  END-IF
END-IF
COMPUTE #COMPUTE-DAYS = #SED-DD-N - 1
ASSIGN #INDEX = #HOLD-TODAY-MM
*
REPEAT
  COMPUTE #COMPUTE-DAYS = #COMPUTE-DAYS + #CALENDAR-DAYS(#INDEX)
  ADD 1 TO #INDEX
*
  IF #INDEX = 13
    MOVE 1 TO #INDEX
  END-IF
*
  IF #INDEX = #SED-MM-N
    ESCAPE ROUTINE
  END-IF
END-REPEAT
*
END-SUBROUTINE
*
END
