* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* COPYBOOK S2000CNM EDITS FOR NM for SERVICES
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE STANDARD-EDITS
* * * * * * * * * * * * * * * * *
*
IF #DOB NE MASK(MMDDYYYY)
   REINPUT 'Invalid DOB; must be format MMDDYYYY'
   MARK *#DOB
END-IF
*
COMPRESS #DOB-CENT #DOB-YY #DOB-MM #DOB-DD INTO #DOB-YYYYMMDD-A
  LEAVING NO SPACE
*
IF #DOB-YYYYMMDD > #HOLD-TODAY-YYYYMMDD
  REINPUT 'Greater than Today Date'
  MARK *#DOB
END-IF
*
IF #EFF-DT NE MASK(MMDDYYYY)
  REINPUT 'Invalid Eff Date; must be format MMDDYYYY'
  MARK *#EFF-DT
END-IF
*
COMPRESS #EFF-DT-CENT #EFF-DT-YY #EFF-DT-MM #EFF-DT-DD
       INTO #EFF-YYYYMMDD-A LEAVING NO
*
MOVE #HOLD-TODAY-YYYYMMDD TO #WORK-DATE-YYYYMMDD
COMPUTE #WD-MM = #WD-MM - 3
*
IF #WD-MM LT 1
  ADD 12 TO #WD-MM
  SUBTRACT 1 FROM #WD-YYYY
END-IF
*
IF #EFF-YYYYMMDD LT #WORK-DATE-YYYYMMDD
  REINPUT 'Date less than 3 months ago'
  MARK*#EFF-DT
END-IF
*
MOVE #HOLD-TODAY-YYYYMMDD TO #WORK-DATE-YYYYMMDD
MOVE 01 TO #WD-DD
ADD 1 TO #WD-MM
*
IF #WD-MM = 13
  MOVE 1 TO #WD-MM
  ADD 1 TO #WD-YYYY
END-IF
*
IF #EFF-YYYYMMDD GE #WORK-DATE-YYYYMMDD
  REINPUT 'Date greater than this month'
  MARK*#EFF-DT
END-IF
*
IF #DOB-YYYYMMDD + 1200000 < #EFF-YYYYMMDD
  REINPUT 'Exceeds maximum age of 120'
  MARK *#DOB
END-IF
*
IF #DOB-YYYYMMDD + 170000 > #EFF-YYYYMMDD
  REINPUT '17 is minimum MEMBER age'
  MARK *#DOB
END-IF
*
IF #FPF-START NE 0
  IF #FPF-START NE MASK(MMDDYYYY)
    REINPUT 'Invalid FPF Start Date; must be format MMDDYYYY'
    MARK *#FPF-START
  END-IF
END-IF
*
IF #FPF-END NE 0
  IF #FPF-END NE MASK(MMDDYYYY)
    REINPUT 'Invalid FPF End Data; must be format MMDDYYYY'
    MARK *#FPF-END
  END-IF
END-IF
*
IF   (#CGA-SCR-SUFFIX NE 'OVR')
 AND (#APPL-SRC = ' ')
  REINPUT 'Must have APPL SOURCE'
  MARK *#APPL-SRC
END-IF
*
IF NOT (#MODE = 'G' OR = 'A' OR = 'S' OR = 'Q'
                    OR = '1' OR = '2' OR = '4' OR = 'E')
  REINPUT USING HELP
  MARK *#MODE
END-IF
*
IF   (#PLAN = '2SW' OR = '3SW')
 AND (NOT #MODE = 'A' OR = '1')
  REINPUT 'Plan 2SW and 3SW must be Annual Mode'
  MARK *#MODE
END-IF
*
IF #MODE = 'E'
  IF  (#PAD-ABA-NUM = ' ') OR (#PAD-ACCT-NUM = ' ')
    REINPUT 'PAD Info required if mode is "E"'
    MARK *#PAD-ABA-NUM
  END-IF
*
  IF   (#PAD-ABA-NUM NE ' ')
   AND (#PAD-ABA-NUM NE MASK (NNNNNNNNN))
    REINPUT 'PRE-AUTH-ABA-NUMBER must be 9 numeric digits'
    MARK *#PAD-ABA-NUM
  END-IF
*
  IF #PAD-ABA-NUM NE ' '
    COMPUTE #DIGIT-SUM = (#PAD-ABA-NUM-TBL (1) * 3)
                       + (#PAD-ABA-NUM-TBL (2) * 7)
                       + (#PAD-ABA-NUM-TBL (3) * 1)
                       + (#PAD-ABA-NUM-TBL (4) * 3)
                       + (#PAD-ABA-NUM-TBL (5) * 7)
                       + (#PAD-ABA-NUM-TBL (6) * 1)
                       + (#PAD-ABA-NUM-TBL (7) * 3)
                       + (#PAD-ABA-NUM-TBL (8) * 7)
*
    IF #DIG-SUM-1 = 0
      MOVE 0 TO #CHK-DIGIT
    ELSE
      COMPUTE #CHK-DIGIT = 10 - #DIG-SUM-1
    END-IF
*
    IF #CHK-DIGIT NE #PAD-ABA-NUM-TBL (9)
      REINPUT 'PRE-AUTH-ABA-NUMBER has bad Check-Digit'
      MARK *#PAD-ABA-NUM
    END-IF
  END-IF
ELSE
  IF (#PAD-ABA-NUM NE ' ') OR (#PAD-ACCT-NUM NE ' ')
    REINPUT 'PAD INFO invalid if mode is "E"'
    MARK *#PAD-ABA-NUM
  END-IF
END-IF
*
IF #MODE = '1' OR = '2' OR = '4'
  IF (#CC-NUM = ' ') OR (#SCN-CC-EXP-DT = ' ' OR = '000000')
    REINPUT 'CC Info required if MODE is "1/2/4"'
    MARK *#CC-NUM
  END-IF
*
  IF #CC-NUM NE MASK (NNNNNNNN........)
    REINPUT 'CREDIT CARD NUMBER must be at least 8 digits'
    MARK *#CC-NUM
  END-IF
*
  IF  (#SCN-CC-EXP-DT NE MASK(MMYYYY))
   OR (#SCN-CC-EXP-YYYY LT '2000')
   OR (#SCN-CC-EXP-YYYY GT '3000')
    REINPUT  'Date Format is "MMYYYY"'
    MARK *#SCN-CC-EXP-DT
  END-IF
ELSE
  IF (#CC-NUM NE ' ') OR (NOT #SCN-CC-EXP-DT = ' ' OR = '000000')
    REINPUT 'CC Info invalid if MODE is "1/2/4"'
    MARK *#CC-NUM
  END-IF
END-IF
*
*
END-SUBROUTINE
*
* * * * * * *  E N D   O F   C O P Y C O D E   * * * * * * * * * *
