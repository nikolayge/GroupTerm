* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* PROGRAM-ID: ATHPSYN6    PROCESS AFSC SSN EXTRACT FOR OTHER-COMP-ID
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* DRW    05072006          Create Program
************************************************************************
*
DEFINE DATA
LOCAL
*
1 #SSN-EXTRACT
 2 #SE-SSN         (N9)
 2 #SE-CN          (N6)
 2 #SE-OTH-COMP-ID (N6)
 2 #SE-DATA-SHARED-IND (A1)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A   (A6)
 2 PROCESS-IND
 2 SSN
 2 OTHER-COMPANY-ID
 2 DATA-SHARED-IND
 2 ALOT-SW
*
1 #FND-AAFM    (L)
1 #CNT-2      (N5)
1 #LAST-SSN   (N9)
1 #LAST-CN    (N6)
*
END-DEFINE
*
ON ERROR
  WRITE   / '*E R R O R - N R    ' *ERROR-NR
          / '*E R R O R - L I N E' *ERROR-LINE
          / '*P R O G R A M = ATHPSYN1'
END-ERROR
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'Exceptions' 5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT  (2) LS=133 PS=76
WRITE   (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
   'Needs OTH-COMP-ID Update' 5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 #SSN-EXTRACT
END-ALL
*
SORT BY #SE-SSN
    USING #SE-CN #SE-OTH-COMP-ID #SE-DATA-SHARED-IND
*
*  WRITE 'JUST READ' #SE-SSN #SE-CN #SE-OTH-COMP-ID
  IF #SE-SSN = #LAST-SSN
    WRITE 'Ssn=' #SE-SSN 'CN=' #SE-CN '2 Dupl AFSC S(A1)'
    '- Last-cn=' #LAST-CN
  END-IF
*
  MOVE #SE-SSN TO #LAST-SSN
  MOVE #SE-CN  TO #LAST-CN
  RESET #FND-AAFM
  FIND ST-V WITH SSN = #SE-SSN
    IF  (PROCESS-IND = 'S' OR = 'C')
     OR (ST-V.ID-NUMBER LT 1900)
      END TRANSACTION
      ESCAPE TOP
    END-IF
*
    IF #FND-AAFM
      WRITE #SE-SSN #SE-CN #SE-OTH-COMP-ID '2 AAFMAA SSN Finds'
    END-IF
*
    MOVE TRUE TO #FND-AAFM
*
    IF DATA-SHARED-IND NE #SE-DATA-SHARED-IND
      WRITE #SE-SSN #SE-CN #SE-OTH-COMP-ID
       'Data-Shared-Inf mismatch' DATA-SHARED-IND  #SE-DATA-SHARED-IND
    END-IF
*
*
    IF ST-V.ID-NUMBER = #SE-OTH-COMP-ID
*     WRITE #SE-SSN #SE-CN #SE-OTH-COMP-ID 'Other-Company-ID matches'
      END TRANSACTION
      ESCAPE TOP
    ELSE
      ADD 1 TO #CNT-2
      DISPLAY (2) #CNT-2
        'AFSC/CN'            #SE-CN
        'SSN'                #SE-SSN
        'AFSC/Other/Comp/ID' #SE-OTH-COMP-ID
        'AFSC/Shared/Ind'    #SE-DATA-SHARED-IND
        'AAFM/CN'            ST-V.ID-NUMBER
        'AAFM/Other/Comp/ID' ST-V.OTHER-COMPANY-ID
        'AAFM/Shared/Ind'    ST-V.DATA-SHARED-IND
        'NEW/AFSC/Other/Comp/ID' ST-V.ID-NUMBER
        'NEW/AAFMAA/Other/Comp/ID' #SE-CN
      MOVE #SE-CN TO ST-V.OTHER-COMPANY-ID
      MOVE 'A' TO ALOT-SW
      UPDATE (0070)
      END TRANSACTION
    END-IF
  END-FIND
END-SORT
*
END
