* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************
* FUNCTION : Get preferred phone for the contact
* Return the first one if none is preferred
* Re-written for MC2
************************************************
DEFINE FUNCTION F-PREF-PHONE RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #CONTACT-ID    (N8)
  LOCAL USING OBJLCOPH
  LOCAL
  1 #PHONE (A) DYNAMIC
  1 #DEL   (A1) CONST <'-'>
  1 #DIAL-NUMBER (N7)
  1 REDEFINE #DIAL-NUMBER
    2 #DIAL-NUMBER-3 (A3)
    2 #DIAL-NUMBER-4 (A4)
  1 #EXT            (A) DYNAMIC
  END-DEFINE
*
FIND CNT-PHONE-V WITH CONTACT-ID = #CONTACT-ID
  DECIDE FOR EVERY CONDITION
    WHEN PREFERRED-PHONE-IND = 'Y'
      PERFORM FORMAT-PHONE
      ESCAPE ROUTINE
    WHEN F-PREF-PHONE = ' '
      PERFORM FORMAT-PHONE
    WHEN NONE
      IGNORE
  END-DECIDE
END-FIND
*
**============================
DEFINE SUBROUTINE FORMAT-PHONE
**============================
DECIDE FOR FIRST CONDITION
  WHEN TYPE = 'D'
    #DIAL-NUMBER := CNT-PHONE-V.DIAL-NUMBER
    COMPRESS FULL CNT-PHONE-V.AREA-CODE #DIAL-NUMBER-3 #DIAL-NUMBER-4 INTO
      #PHONE WITH DELIMITER #DEL
  WHEN TYPE = 'I'
    #PHONE := CNT-PHONE-V.INTL-DIAL-NUMBER
    #PHONE := *TRIM(#PHONE)
    COMPRESS FULL CNT-PHONE-V.INTL-ACCESS-CODE #PHONE INTO #PHONE WITH DELIMITER #DEL
  WHEN NONE
    IGNORE
END-DECIDE
IF #PHONE <> ' '
  IF CNT-PHONE-V.EXT <> 0
    MOVE EDITED CNT-PHONE-V.EXT (EM=ZZZZZ9) TO #EXT
    COMPRESS FULL #PHONE 'Ext.' *TRIM(#EXT) INTO #PHONE
  END-IF
  F-PREF-PHONE := #PHONE
END-IF
END-SUBROUTINE
END-FUNCTION
END
