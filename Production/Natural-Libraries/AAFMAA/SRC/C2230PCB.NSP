* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 2
* <Natural Source Header
* PGM-ID: C2230PCB      ** AAFMA CORE BENEFITS FIELDS UPDATE
*                       ** PARTS OF DM & DC
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW 08112005 DW1  Add Special-code so AAFMAA can do rank secure
* DRW 09152005 DW2 Dis Allow DEP STAT A/B/J/L/M/Q/R/X to be <18
* SAG 11242005 RH1   SS - Replaced call to assembler program WAASSRT1 with
*                      Natural subprogram SAGSRT2.
* DRW 04212006 DW3 Add FMR SPS INFO & SPS STATUS & DELETE BASD ETC
* DRW 04242006 DW4 CLEAR DATES on not entered
* DRW 05072006 DW5 Remove ALOT-SW logic
* DRW 05112006 DW6 Add Auto move Spouse logic to Former Spouse using '*'
* DRW 08232006 DW7 Add Access-Auth-Code to screen & updating
* DRW 09112007 DW8 Change update access to Retired-date
* DRW 10182007 DW9 Create MD SPS/WID Death on Spouse Death
* DRW 11072007 DW10 Allow Special Code "X" for Recalled to Active Duty
* DRW 07/15/2008 DW11 Add Screen to verify Spouse Death Validity
* DRW 07/21/2008 DW12 Clear out old AFSC fields when scrunching
* RSE 10022009 RE1 Change PROCESS-IND logic for Unification
* RSE 03232010 RE1 Drastic changes applied to this program to accommodate
*                  Project CR222 requirements such as ability to view, update,
*                  add and delete records.
* RSE 04282010 RE2 Display 2 Former Spouses
* RSE 04292010 RE3 Identify Spouse/Dependents with CN with a '*'
* NGG 05042010 NG1 Pass Member dependents data to CRM
* RSE 06082010 RE4 Copy Spouse logic from subroutine COLLECT-DATA
* RSE 08122010 RE5 Add Role Code 'O' for CAO - Casualty Assistance Officer
* RSE 08182010 RE6 Reset Depn Date of Death when Status Code is not 'D'
* RSE 08202010 RE7 Fix former spouse problem
* RSE 08222010 RE8 Change CODDSPL to GPF12SXX
* RSE 08232010 RE9 CR249 Changes
* vxt 10272010 vt1 Change 60606060 to be 99999999 in issue-date of MTD-Detail file.
* RSE 10252010 RE10 Add Role Code 'Z' (Business Partner) and 'H' (Grandparent)
* RSE 11122010 RE11 Update RANK- and SSN-PROTECT fields in Contact record
* RSE 11162010 RE12 When Spouse is change to FS, move and reset Birth-Cert and Marr-Cert Codes
* RSE 11232010 RE13 Move symbol together with Spouse info when changed to FS
* RSE 12212010 RE14 Add Role Codes 'U' and 'W'
* RSE 12222010 RE15 identify Spouse/Dependents with Beneficiaries with a '$'
* RSE 01012011 RE16 Remove check for name of Others Contact; fix DOB and DOD for member/spouse
* RSE 01122011 RE17 Add array for ISN to enable users to delete a duplicate record
* RSE 02232011 RE18 Reset additional spouse fields
* RSE 03152011 RE19 Display message for member
* RSE 03162011 RE20 Fix message concatenation error
* NGG 03212011 NG2  Pass additional parm for Family data synch
* RSE 05192011 RE21 Add Role Codes 'L', 'V', and 'Y'
* RSE 06242011 RE22 Fix msg truncation
* RSE 06272011 RE23 Add PF4 for Member/Spouse on Header
* RSE 07132011 RE24 Change Role Code 'Y' to 'J'; add 'C'
* RSE 07202011 RE25 When OTHER but not organization, put REMARKS instead of ORGANIZATION on map
* RSE 07212011 RE26 Display standard Header for Rank and Name
* RSE 07282011 RE27 Reset message; fix logic to display spouse when status = 'F'
* RSE 09092011 RE28 Fix problem of Fmr Sp DOD going to Sp DOD; get member's/widow's info from Contacts file
* RSE 09292011 RE29 Don't allow updates to record upon exiting the program
* RSE 10132011 RE30 Change function to dislay role description
* RSE 10272011 RE31 Fix issue of spouse not being kept after being made into a fmr spouse;
*                   fmr husband ssn changed to N9
* RSE 11292011 RE32 Add 'FORMER HUSBAND' text
****************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL
1 STATUS VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 WIDOW-FLAG
  2 RANK
  2 NAME
  2 PROCESS-IND
  2 INTRO-FLAG
  2 CHIEF-OF-STAFF-IND
  2 SSN
  2 DATE-OF-BIRTH
  2 DATE-OF-MARRIAGE
  2 DATE-OF-DIVORCE
  2 DATE-OF-DEATH
  2 DATE-RETIRED
  2 LAST-FM-DATE
  2 BIRTH-CERT-CODE
  2 BIRTH-CERT-CODE-SP
  2 MARRIAGE-CERT-CODE
  2 ACCESS-AUTH-CODE
  2 SPOUSE-CONTACT-ID
  2 NAME-SP
  2 SSN-SP
  2 SEX-SP
  2 DATE-OF-BIRTH-SP
  2 STATUS-CODE-SP
  2 FORMER-SPOUSE-CONTACT-ID
  2 FORMER-SPOUSE-NAME
  2 FORMER-SPOUSE-SSN
  2 FORMER-SPOUSE-DT-OF-BIRTH
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 FORMER-SPOUSE-STATUS
  2 FS-BIRTH-CERT-CD
  2 FS-MARR-CERT-CD
  2 FORMER-HUSBAND-CONTACT-ID
  2 FORMER-HUSBAND-NAME
  2 FORMER-HUSBAND-SSN
  2 TITLE-SP                /* RE18
  2 MAILING-FLAG-SP         /* RE18
  2 PLAN(1:9)               /* RE19
  2 MILITARY-SERVICE        /* RE19
  2 NUMBER-MEMBERSHIPS      /* RE19
  2 DTS-DATN
  2 DTS-TIMN
*
1 STATUS-2 VIEW OF A-STATUS
  2 ID-NUMBER
*
1 FIRS VIEW OF A-FIRS
  2 ID-NUMBER
  2 CONTACT-ID-NUMBER
  2 MEMBER-FLAG
  2 DEPENDENT-TABLE(60)
    3 DEPN-CONTACT-ID (N8)  /* Descriptor
    3 DEPN-NAME       (A15)
    3 DEPN-BIRTH-CD   (A1)
    3 DEPN-STATUS-CD  (A1)    /* G- Grand Child All others are Child
    3 DEPN-DATE-OF-BIRTH (N8.0)
    3 DEPN-SSN        (P9.0)
    3 DEPN-REMARKS    (A20)
  2 DEPN-COLL-FUND-TBL (1:60)
    3 DEPN-COLL-FUND (P7)
  2 DEPN-COLL-TYPE-TBL (1:60)
    3 DEPN-COLL-TYPE (A1)
  2 DEPN-COLL-INFO-TBL (1:60)
    3 DEPN-COLL-FROM-YYYY (N4)
    3 DEPN-COLL-THRU-YYYY (N4)
    3 DEPN-COLL-ANN-COST (P6)
  2 DP-LAST-FM-DATE     (N8)
  2 DTS-DATN
  2 DTS-TIMN
*
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 DATE-OF-BIRTH
  2 DATE-OF-DEATH
  2 SSN
  2 GENDER-CD
  2 FIRM-ORGANIZATION-NAME
  2 DELETE-FLAG
  2 RANK-PROTECT      /* RE11
  2 RANK-PROTECT-DATE /* RE11
  2 SSN-PROTECT       /* RE11
  2 SSN-PROTECT-DATE  /* RE11
  2 CONTACT-REMARKS(1) /* RE25
*
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 CONTACT-2-ID
  2 ROLE-1-CD
  2 ROLE-2-CD
  2 ACCESS-AUTH-SVC
  2 ACCESS-AUTH-INS
  2 LAST-USER-UPD
  2 LAST-DATE-UPD
  2 LAST-TIME-UPD
*
1 MBRSHIP-MTD-DET-V VIEW OF A-MBRSHIP-MTD-DETAIL   /* DW9
  2 ID-NUMBER
  2 ENTRY-DATE
  2 REC-ISN
  2 CODE
  2 ISSUE-DATE
  2 NUM-MBRSHIPS
*
1 CONTACT-NOTES VIEW OF A-CONTACT-NOTES
  2 CONTACT-ID
*
1 CNT-ADDRESS-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 STATUS
*
1 CNT-EMAIL-V VIEW OF A-CONTACT-EMAIL
*
1 CNT-PHONE-V VIEW OF A-CONTACT-PHONE
*
1 ##STATUS
  2 ##ID-NUMBER (N6)
  2 ##MEMBER-CONTACT-ID (N8)
  2 ##WIDOW-FLAG (A1)
  2 ##RANK (A3)
  2 ##NAME (A25)
  2 ##PROCESS-IND (A1)
  2 ##INTRO-FLAG (A1)
  2 ##CHIEF-OF-STAFF-IND (A1)
  2 ##SSN (N9)
  2 ##DATE-OF-BIRTH (N8)
  2 ##DATE-OF-MARRIAGE (N8)
  2 ##DATE-OF-DIVORCE (N8)
  2 ##DATE-OF-DEATH (N8)
  2 ##DATE-RETIRED (N8)
  2 ##LAST-FM-DATE (N8)
  2 ##BIRTH-CERT-CODE (A1)
  2 ##BIRTH-CERT-CODE-SP(A1)
  2 ##MARRIAGE-CERT-CODE(A1)
  2 ##ACCESS-AUTH-CODE (A1)
  2 ##SPOUSE-CONTACT-ID (N8)
  2 ##NAME-SP (A25)
  2 ##SSN-SP (N9)
  2 ##SEX-SP (A1)
  2 ##DATE-OF-BIRTH-SP (N8)
  2 ##STATUS-CODE-SP (A1)
  2 ##FORMER-SPOUSE-CONTACT-ID (N8)
  2 ##FORMER-SPOUSE-NAME (A25)
  2 ##FORMER-SPOUSE-SSN (N9)
  2 ##FORMER-SPOUSE-DT-OF-BIRTH (N8)
  2 ##FORMER-SPOUSE-DATE-OF-MARRIAGE (N8)
  2 ##FORMER-SPOUSE-DATE-OF-DIVORCE (N8)
  2 ##FORMER-SPOUSE-STATUS (A1)
  2 ##FS-BIRTH-CERT-CD(A1)
  2 ##FS-MARR-CERT-CD(A1)
  2 ##FORMER-HUSBAND-CONTACT-ID (N8)
  2 ##FORMER-HUSBAND-NAME (A25)
  2 ##FORMER-HUSBAND-SSN (N9)      /* RE31 formerly N8
  2 ##TITLE-SP (A4)                /* RE18
  2 ##MAILING-FLAG-SP (A1)         /* RE18
  2 ##PLAN(A3/1:9)                 /* RE19
  2 ##MILITARY-SERVICE(A3)         /* RE19
  2 ##NUMBER-MEMBERSHIPS(N1)       /* RE19
  2 ##DTS-DATN (P9)
  2 ##DTS-TIMN (P7)
*
1 ##FIRS
  2 ##ID-NUMBER (N6)
  2 ##CONTACT-ID-NUMBER (N8)
  2 ##MEMBER-FLAG (A1)
  2 ##DEPENDENT-TABLE(60)
    3 ##DEPN-CONTACT-ID (N8)  /* Descriptor
    3 ##DEPN-NAME       (A15)
    3 ##DEPN-BIRTH-CD   (A1)
    3 ##DEPN-STATUS-CD  (A1)    /* G- Grand Child All others are Child
    3 ##DEPN-DATE-OF-BIRTH (N8.0)
    3 ##DEPN-SSN        (P9.0)
    3 ##DEPN-REMARKS    (A20)
  2 ##DEPN-COLL-FUND-TBL (1:60)
    3 ##DEPN-COLL-FUND (P7)
  2 ##DEPN-COLL-TYPE-TBL (1:60)
    3 ##DEPN-COLL-TYPE (A1)
  2 ##DEPN-COLL-INFO-TBL (1:60)
    3 ##DEPN-COLL-FROM-YYYY (N4)
    3 ##DEPN-COLL-THRU-YYYY (N4)
    3 ##DEPN-COLL-ANN-COST (P6)
  2 ##DP-LAST-FM-DATE     (N8)
  2 ##DTS-DATN (P9)
  2 ##DTS-TIMN (P7)
*
1 #MAP
  2 #MAIN-DOB           (A8)
  2 #MAIN-DOD           (A8)
  2 #MAIN-SSN           (N9)
  2 #DEPN-CID          (N8/10)
  2 REDEFINE #DEPN-CID           /* RE17
    3 #DEPN-CID-A      (A8/10)   /* RE17
  2 #LINE              (N2/10)
  2 #DEPN-NAME         (A32/10)
  2 #STATUS-CODE       (A1/10)
  2 #ROLE-CODE         (A1/10)
  2 #ROLE-DESC         (A11/10)
  2 #GENDER            (A1/10)
  2 #DOB               (A8/10)
  2 #DOD               (A8/10)
  2 #SSN               (N9/10)
  2 #OTH-CID           (N8/5)
  2 REDEFINE #OTH-CID            /* RE17
    3 #OTH-CID-A       (A8/5)    /* RE17
  2 #OTH-POSN          (U1/5)
  2 #OTH-LINE          (N2/5)
  2 #OTH-POSITION      (A1/5)
  2 #OTH-NAME          (A32/5)
  2 #OTH-ROLE-CODE     (A1/5)
  2 #OTH-ROLE-DESC     (A11/5)
  2 #OTH-GENDER        (A1/5)
  2 #OTH-DOB           (A8/5)
  2 #OTH-DOD           (A8/5)
  2 #OTH-SSN           (N9/5)
  2 #OTH-ORG           (A62/5)
  2 #OTH-ORG-TEXT      (A12/5)
  2 #ACC-AUTH-INS      (A1/10)
  2 #ACC-AUTH-SVC      (A1/10)
  2 #NO-OF-DEPN        (N2)
  2 #NO-OF-OTHERS       (N2)
  2 #SPS-ACC-AUTH-INS  (A1)
  2 #SPS-ACC-AUTH-SVC  (A1)
  2 #FMR-SPS-ACC-AUTH-INS(A1)
  2 #FMR-SPS-ACC-AUTH-SVC(A1)
  2 #MAIN-ACCESS-AUTH-CODE (A1)
  2 #MAIN-DATE-RETIRED (A8)
  2 #MAIN-LST-FM-DM    (A11)
1 #MAP-SPOUSE
  2 #SPOUSE-CID        (N8)
  2 #SPOUSE-NAME       (A62)
  2 #SPOUSE-SUFFIX     (A8)
  2 #SPOUSE-SEX        (A1)
  2 #SPOUSE-DOB        (A8)
  2 #SPOUSE-DOM        (A8)
  2 #SPOUSE-DIV        (A8)
  2 #SPOUSE-DOD        (A8)
  2 #SPOUSE-SSN        (N9)
  2 #FORMER-SPS-CID    (N8)
  2 #FORMER-SPS-NAME   (A62)
  2 #FORMER-SPS-SUFFIX (A8)
  2 #FORMER-SPS-SEX    (A1)
  2 #FORMER-SPS-DOB    (A8)
  2 #FORMER-SPS-DOM    (A8)
  2 #FORMER-SPS-DIV    (A8)
  2 #FORMER-SPS-DOD    (A8)
  2 #FORMER-SPS-SSN    (N9)
*
1 #NAME-LBL            (A6)
1 #MAIN-RANK                (A3)
1 #MAIN-NAME                (A46)   /* RE26 A25 before
1 #SCR-CV                  (C)
1 #RET-DT-CV               (C)
1 #AAC-CV                  (C)
1 #STAT-CD-CV              (C/10)
1 #ROLE-DESC-CV            (C)
1 #OTH-LINE-CV             (C/5)
1 #OTH-ROLE-DESC-CV        (C)
1 #OTH-ORG-CV              (C)
1 #ARRAY
  2 #ARR-LINE (N2/60)
  2 #ARR-ISN (N8/60)        /* RE17
  2 #ARR-DEPN-CID (N8/60)
  2 REDEFINE #ARR-DEPN-CID
    3 #ARR-DEPN-CID-A (A8/60)
  2 #ARR-ROLE-CODE (A1/60)
  2 #ARR-ROLE-DESC (A10/60)
  2 #ARR-STAT-CODE (A1/60)
  2 #ARR-DEPN-NAME (A39/60)
  2 #ARR-SUFFIX (A8/60)
  2 #ARR-DOB (A8/60)
  2 REDEFINE #ARR-DOB
    3 #ARR-DOB-N (N8/60)
  2 #ARR-DOD (A8/60)
  2 REDEFINE #ARR-DOD
    3 #ARR-DOD-N (N8/60)
  2 #ARR-SSN (N9/60)
  2 #ARR-SEX (A1/60)
  2 #ARR-ACC-AUTH-INS(A1/60)
  2 #ARR-ACC-AUTH-SVC(A1/60)
1 #ARRAY-OTHERS
  2 #ARR-OTH-LINE (N2/60)
  2 #ARR-OTH-ISN (P8/60)    /* RE17
  2 #ARR-OTH-CID (N8/60)
  2 REDEFINE #ARR-OTH-CID
    3 #ARR-OTH-CID-A (A8/60)
  2 #ARR-OTH-ROLE-CODE (A1/60)
  2 #ARR-OTH-ROLE-DESC (A11/60)
  2 #ARR-OTH-NAME (A39/60)
  2 #ARR-OTH-DOB (A8/60)
  2 REDEFINE #ARR-OTH-DOB
    3 #ARR-OTH-DOB-N (N8/60)
  2 #ARR-OTH-DOD (A8/60)
  2 REDEFINE #ARR-OTH-DOD
    3 #ARR-OTH-DOD-N (N8/60)
  2 #ARR-OTH-SSN (N9/60)
  2 #ARR-OTH-SEX (A1/60)
  2 #ARR-OTH-ORG (A62/60)
  2 #ARR-ACC-AUTH-INS(A1/60)
  2 #ARR-ACC-AUTH-SVC(A1/60)
1 #MMDDYYYY   (A8)
1 REDEFINE #MMDDYYYY
  2 #MM-X   (A2)
  2 #DD-X   (A2)
  2 #YYYY-X (A4)
1 REDEFINE #MMDDYYYY
  2 #FILL   (A4)
  2 #YYYY-N (N4)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
1 #MONTHS (A36)  INIT
  <'JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
1 #PARENT-CID   (N8)
1 #REL-CID      (N8)
1 REDEFINE #REL-CID
  2 #REL-CID-A  (A8)
1 #I             (N3)
1 #J             (N3)
1 #K             (N3)
1 #M             (N3)
1 #Y-N           (A1)
1 #INVALID         (L)
1 #ERR-MSG         (A60)
1 #EDIT-FLAG       (A1)    INIT<'N'>
1 #HOLD-TIME           (P7)
1 #ST-REC-CHGD         (L)
1 #FIRS-REC-CHGD       (L)
1 #FIRS-FOUND          (L)
1 #CURR-SP-TO-FMR-SP   (L)
1 #PF4                 (L)
1 #UPDATED             (L)
1 #STAY-ON-CURRENT-SCREEN(L)
1 #SPOUSE-EXISTS       (L)
1 #DEPN-SCREEN         (L)
1 #ST-ISN              (P8)
1 #FIRS-ISN            (P8)
1 #SP-ISN              (P8) /* RE17
1 #FS-ISN              (P8) /* RE17
1 #ST-ISN-2            (P8)
1 #HOLD-SP-ST    (A1)    /* DW3
1 #HOLD-C-O-F-IND  (A1)  /* DW3
**1 #HOLD-ACC-AUTH-CD (A1)
*
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (N2)
1 REDEFINE #I3
  2 #I3-1     (N1)
  2 #I3-2     (N1)
1 #ITR1       (I2)              /* USED FOR TRANS REG TRAILERS
1 #ITR2       (I2)              /* USED FOR BUCKETS WITHIN TR REG TRLS
*
1 #SRT-PARM
  2 #SRT-NUM-ENTRIES  (I2) INIT <60>
  2 #SRT-ENTRY-LENGTH (I2) INIT <93>
  2 #SRT-KEY-LENGTH   (I2) INIT <8>
  2 #SRT-KEY-POSITION (I2) INIT <60>  /* DOB ONLY
  2 #SRT-SORT-TYPE    (A1) INIT <'A'>
1 #SPS-NAME-FIRST-MID  (A25)
1 #SPS-NAME-LAST     (A25)
1 #SPS-NAME-SUFFIX   (A25)
1 #NAME-EXTRA        (A25)
1 #MODE              (A1)
1 #MSG               (A72)
1 REDEFINE #MSG
  2 FILLER           (A1)
  2 #MSG-ISN         (N8)
1 #STATUS-CODE-SP    (A1)
1 #FORMER-SPOUSE-STATUS(A1)
1 #USER-NAME         (A20)
1 #CMD               (A2)
1 #X                 (N2)
1 #STAR-SP           (A2)
1 #STAR-FS           (A2)
1 #CONTACT-ID        (N8)
1 #BD-LINKS          (N6/100)
1 #BD-CI             (N8)
1 #DUPES             (N2)
1 #TEXT-40           (A40)
1 REDEFINE #TEXT-40            /* RE22
  2 FILLER           (A37)      /* RE22
  2 #TEXT-40-LAST-3  (A3)       /* RE22
1 #REQUEST           (A) DYNAMIC               /* NG2
1 #SPOUSE            (L)     /* RE25 START
1 #FMR-SPOUSE        (L)
1 #DEPENDENT         (L)
1 #OTHER             (L)
1 #ORGANIZATION      (L)
1 #INDIVIDUAL        (L)     /* RE25 END
1 #ADMIN-SCREEN      (A) DYNAMIC /* RE26
*
END-DEFINE
*
INCLUDE GONERCXX           /* NATURAL ERROR ROUTINE
*
SET KEY PF4 PF12
*
PERFORM COLLECT-DATA
*
REPEAT
  FOR #I 1 60
    ADD 1 TO #J
    IF #ARR-DEPN-NAME(#I) = ' ' AND #J < 11
      RESET #LINE(#J:10)
      MOVE (AD=P) TO #STAT-CD-CV(#J:10)
    ELSE
*     RESET #STAT-CD-CV(*)
      MOVE (AD=U) TO #STAT-CD-CV(#J)
      MOVE #I TO #LINE(#J)
      MOVE #ARR-DEPN-CID(#I) TO #DEPN-CID(#J)
      MOVE #ARR-ROLE-CODE(#I) TO #ROLE-CODE(#J)
      MOVE #ARR-ROLE-DESC(#I) TO #ROLE-DESC(#J)
      MOVE #ARR-STAT-CODE(#I) TO #STATUS-CODE(#J)
      MOVE #ARR-DEPN-NAME(#I) TO #DEPN-NAME(#J)
      MOVE #ARR-DOB(#I) TO #DOB(#J)
      MOVE #ARR-DOD(#I) TO #DOD(#J)
      MOVE #ARR-SSN(#I) TO #SSN(#J)
      MOVE #ARR-SEX(#I) TO #GENDER(#J)
*      IF #CGA-SCR-SUFFIX = 'ACS'
*        MOVE #ARR-ACC-AUTH-INS(#I) TO #ACC-AUTH-INS(#J)
*        MOVE #ARR-ACC-AUTH-SVC(#I) TO #ACC-AUTH-SVC(#J)
*      END-IF
    END-IF
    IF #ARR-DEPN-NAME(#I) = ' ' OR #J = 10
      MOVE '*'              TO #CGA-SCR-COMMAND
      MOVE #CGA-ORIG-ID     TO #CGA-SCR-ID
      IF #CGA-SCR-SUFFIX NE 'ACS'
        RESET #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
      END-IF
      REPEAT
        INPUT WITH TEXT #MSG USING MAP 'C2230MCB'
        RESET #MSG #PF4
        IF #CGA-SCR-COMMAND NE '*'
*          PERFORM WRITE-TRANS-REG               /* RE29
          END TRANSACTION
          FETCH 'G1000PXX'
        END-IF
*
        IF *PF-KEY = 'PF12'
          PERFORM CHECK-CURS-LINE                /* RE9
          IF #REL-CID > 0                         /* RE9
            CALLNAT 'GPF12SXX' #REL-CID           /* RE9
          ELSE
            CALLNAT 'GPF12SXX' STATUS.MEMBER-CONTACT-ID           /* RE8
          END-IF
          ESCAPE TOP
        END-IF
*
        IF *PF-KEY = 'PF4'
          PERFORM CHECK-CURS-LINE
          MOVE 'U' TO #MODE
          CALLNAT 'GUPCISXX' #PARENT-CID #REL-CID #CGA-ORIG-COMMAND
            #CGA-ORIG-ID #CGA-USER-COMPANY #CGA-CLERK #MODE #MSG
          IF #MSG NE ' '
*            IF #MSG = 'FUNC'
*            IF #MSG = 'X'            /* RE27
              RESET #MSG
*            END-IF
            MOVE TRUE TO #STAY-ON-CURRENT-SCREEN
          ELSE
            ESCAPE TOP
          END-IF
          MOVE TRUE TO #PF4
          PERFORM COLLECT-DATA
        END-IF
*
        IF #UPDATED
          RESET #UPDATED
          ESCAPE BOTTOM
        END-IF
*
        IF NOT #PF4
          PERFORM VALIDATE-FIELDS
          PERFORM WRITE-TRANS-REG
          IF #UPDATED
            RESET #CURR-SP-TO-FMR-SP
            PERFORM GET-ISN
            MOVE 'Record(s) successfully updated.' TO #MSG
            ESCAPE TOP
          END-IF
        END-IF
        ESCAPE BOTTOM
      END-REPEAT
*
      RESET #J
      IF #ARR-DEPN-NAME(#I) = ' ' OR #ARR-DEPN-NAME(#I+1) = ' '
        PERFORM COLLECT-DATA
        IF #NO-OF-OTHERS > 0 AND NOT #STAY-ON-CURRENT-SCREEN
          ESCAPE BOTTOM
        ELSE
          RESET #I #STAY-ON-CURRENT-SCREEN
        END-IF
      END-IF
      RESET #DEPN-CID(*) #ROLE-CODE(*) #ROLE-DESC(*) #STATUS-CODE(*) #DEPN-NAME(*)
        #DOB(*) #SSN(*) #GENDER(*) #ACC-AUTH-INS(*) #ACC-AUTH-SVC(*) #DOD(*)
    END-IF
  END-FOR
*
  FOR #I 1 60
    ADD 1 TO #J
*    IF #ARR-OTH-NAME(#I) = ' ' AND #J < 6                           /* RE16
    IF #ARR-OTH-CID(#I) = 0 AND #J < 6                               /* RE16
      RESET #OTH-LINE(#J:5) #OTH-ORG-TEXT(#J:5) #OTH-ORG(#J:5)
      MOVE (AD=P) TO #OTH-LINE-CV(#J:5)
    ELSE
      MOVE #I TO #OTH-LINE(#J)
      MOVE (AD=I) TO #OTH-LINE-CV(#J)
      MOVE #ARR-OTH-CID(#I) TO #OTH-CID(#J)
      MOVE #ARR-OTH-ROLE-CODE(#I) TO #OTH-ROLE-CODE(#J)
      MOVE #ARR-OTH-ROLE-DESC(#I) TO #OTH-ROLE-DESC(#J)
      MOVE #ARR-OTH-NAME(#I) TO #OTH-NAME(#J)
      MOVE #ARR-OTH-DOB(#I) TO #OTH-DOB(#J)
      MOVE #ARR-OTH-DOD(#I) TO #OTH-DOD(#J)
      MOVE #ARR-OTH-SSN(#I) TO #OTH-SSN(#J)
      MOVE #ARR-OTH-SEX(#I) TO #OTH-GENDER(#J)
*      IF #ARR-OTH-ROLE-CODE (#I) = 'P' OR= 'H' OR= 'B'          /* NG3  RE25
*        IGNORE                                                          RE25
      IF FUNCTION-CONTACT-TYPE(<#ARR-OTH-ROLE-CODE (#I)>) = 'INDIVIDUAL' /* RE25
        MOVE 'Remarks' TO #OTH-ORG-TEXT(#J)                              /* RE25
      ELSE
        IF FUNCTION-CONTACT-TYPE(<#ARR-OTH-ROLE-CODE (#I)>) = 'ORGANIZATION' /* RE25
          MOVE 'Organization'  TO #OTH-ORG-TEXT(#J)                      /* RE25
        ELSE
          MOVE 'Unknown'  TO #OTH-ORG-TEXT(#J)                      /* RE25
        END-IF
      END-IF
      MOVE #ARR-OTH-ORG(#I) TO #OTH-ORG(#J)
*      IF #CGA-SCR-SUFFIX = 'ACS'
*        MOVE #ARR-ACC-AUTH-INS(#I) TO #ACC-AUTH-INS(#J)
*        MOVE #ARR-ACC-AUTH-SVC(#I) TO #ACC-AUTH-SVC(#J)
*      END-IF
    END-IF
*    IF #ARR-OTH-NAME(#I) = ' ' OR #J = 5                           /* RE16
    IF #ARR-OTH-CID(#I) = 0 OR #J = 5                              /* RE16
      MOVE '*'              TO #CGA-SCR-COMMAND
      MOVE #CGA-ORIG-ID     TO #CGA-SCR-ID
      IF #CGA-SCR-SUFFIX NE 'ACS'
        RESET #CGA-SCR-SUFFIX #CGA-ORIG-SUFFIX
      END-IF
      REPEAT
        INPUT WITH TEXT #MSG USING MAP 'C2233MCB'
        RESET #MSG #PF4
        IF #CGA-SCR-COMMAND NE '*'
*          PERFORM WRITE-TRANS-REG                           /* RE29
          END TRANSACTION
          FETCH 'G1000PXX'
        END-IF
*
        IF *PF-KEY = 'PF12' AND #CGA-CLERK = 'RE' OR = 'SV' OR = 'LR' OR = 'JC'
          CALLNAT 'GPF12SXX' STATUS.MEMBER-CONTACT-ID
          ESCAPE TOP
        END-IF
*
        IF *PF-KEY = 'PF4'
          PERFORM CHECK-CURS-LINE-OTHERS
          MOVE 'U' TO #MODE
          CALLNAT 'GUPCISXX' #PARENT-CID #REL-CID #CGA-ORIG-COMMAND
            #CGA-ORIG-ID #CGA-USER-COMPANY #CGA-CLERK #MODE #MSG
          IF #MSG NE ' '
*            IF #MSG = 'X'    /* RE27
              RESET #MSG
*            END-IF
            MOVE TRUE TO #STAY-ON-CURRENT-SCREEN
          ELSE
            ESCAPE TOP
          END-IF
          MOVE TRUE TO #PF4
          PERFORM COLLECT-OTHERS-DATA
        END-IF
*
        IF #UPDATED
          RESET #UPDATED
          ESCAPE BOTTOM
        END-IF
*
        IF NOT #PF4
          PERFORM VALIDATE-FIELDS
          PERFORM WRITE-TRANS-REG
          IF #UPDATED
            RESET #CURR-SP-TO-FMR-SP
            PERFORM GET-ISN
            MOVE 'Record(s) successfully updated.' TO #MSG
            ESCAPE TOP
          END-IF
        END-IF
        ESCAPE BOTTOM
      END-REPEAT
*
      RESET #J
*      IF #ARR-OTH-NAME(#I) = ' ' OR #ARR-OTH-NAME(#I+1) = ' '   /* RE16
      IF #ARR-OTH-CID(#I) = 0 OR #ARR-OTH-CID(#I+1) = 0      /* RE16
        PERFORM COLLECT-OTHERS-DATA
        IF #STAY-ON-CURRENT-SCREEN
          RESET #I #STAY-ON-CURRENT-SCREEN
        ELSE
          ESCAPE BOTTOM
        END-IF
      END-IF
      RESET #OTH-CID(*) #OTH-ROLE-CODE(*) #OTH-ROLE-DESC(*) #STATUS-CODE(*) #OTH-NAME(*) #OTH-ORG-TEXT(*)
        #OTH-DOB(*) #OTH-SSN(*) #OTH-GENDER(*) #ACC-AUTH-INS(*) #ACC-AUTH-SVC(*) #OTH-DOD(*) #OTH-ORG(*) #OTH-LINE-CV(*)
    END-IF
  END-FOR
END-REPEAT
******************************
DEFINE SUBROUTINE COLLECT-DATA
******************************
/* When modifying this subroutine, subroutine COLLECT-OTHERS-DATA may need to be modified too
RESET #MAP #ARRAY #DEPN-CID(*) #ROLE-CODE(*) #STATUS-CODE(*) #DEPN-NAME(*)
  #DOB(*) #SSN(*) #GENDER(*) #ACC-AUTH-INS(*) #ACC-AUTH-SVC(*) #DOD(*)
  #ARRAY-OTHERS #OTH-CID(*) #OTH-ROLE-CODE(*) #OTH-NAME(*) #SPOUSE-EXISTS
  #OTH-DOB(*) #OTH-SSN(*) #OTH-GENDER(*) #ACC-AUTH-INS(*) #ACC-AUTH-SVC(*) #OTH-DOD(*)
IF NOT #CURR-SP-TO-FMR-SP
  RESET #MAP-SPOUSE
END-IF
*
PERFORM GET-ISN
*
IF NOT #CURR-SP-TO-FMR-SP
  MOVE STATUS.STATUS-CODE-SP TO #HOLD-SP-ST
  MOVE STATUS.CHIEF-OF-STAFF-IND TO #HOLD-C-O-F-IND   /* DW3
ELSE
  MOVE STATUS.SPOUSE-CONTACT-ID  TO STATUS.FORMER-SPOUSE-CONTACT-ID
  MOVE 'F'                      TO STATUS.FORMER-SPOUSE-STATUS
  MOVE STATUS.NAME-SP            TO STATUS.FORMER-SPOUSE-NAME
  MOVE STATUS.DATE-OF-DIVORCE   TO STATUS.FORMER-SPOUSE-DATE-OF-DIVORCE
  MOVE STATUS.DATE-OF-MARRIAGE  TO STATUS.FORMER-SPOUSE-DATE-OF-MARRIAGE
  MOVE STATUS.DATE-OF-BIRTH-SP  TO STATUS.FORMER-SPOUSE-DT-OF-BIRTH
  MOVE STATUS.SSN-SP            TO STATUS.FORMER-SPOUSE-SSN
  MOVE STATUS.BIRTH-CERT-CODE-SP TO STATUS.FS-BIRTH-CERT-CD               /* RE12
  MOVE STATUS.MARRIAGE-CERT-CODE TO STATUS.FS-MARR-CERT-CD                /* RE12
  RESET STATUS.SPOUSE-CONTACT-ID STATUS.STATUS-CODE-SP STATUS.DATE-OF-BIRTH-SP STATUS.DATE-OF-DIVORCE
    STATUS.SSN-SP STATUS.NAME-SP STATUS.SEX-SP STATUS.BIRTH-CERT-CODE-SP STATUS.MARRIAGE-CERT-CODE /* RE12
    STATUS.DATE-OF-MARRIAGE STATUS.TITLE-SP STATUS.MAILING-FLAG-SP /* RE18
END-IF
*
MOVE (AD=U) TO #RET-DT-CV
IF (NOT #CGA-CLERK = 'DRW' OR = 'VXT' OR = 'YAK'        /* DW8  was 'PAM'
    OR = 'RLB' OR = 'KHB' OR = 'BMG' OR = 'CEB' OR = 'SKS')    /* Not SEL Users
* or (STATUS.process-ind = 'G')                            /* Grandfathered
    OR (STATUS.PROCESS-IND = 'G' OR = 'S')   /* Grandfathered or Combo  RE1
    OR ((STATUS.PROCESS-IND = 'I' OR = 'O' OR = 'D') AND (STATUS.WIDOW-FLAG ='Y')) /* DEAD
  IF NOT STATUS.INTRO-FLAG = 'S' OR = 'K' OR = 'G'  /* SPS/GEN (INS-ONLY)can be updated
    MOVE (AD=P) TO #RET-DT-CV
  END-IF
END-IF
*
MOVE 'SCREEN' TO #ADMIN-SCREEN        /* RE26
IF STATUS.WIDOW-FLAG = 'Y'
  MOVE 'SPOUSE' TO #NAME-LBL
*  RESET            #MAIN-RANK                                                       /* RE26
  FIND CONTACT WITH CONTACT-ID = STATUS.SPOUSE-CONTACT-ID
*    #MAIN-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)  /* RE26
    CALLNAT 'GPRS0CNT' STATUS.SPOUSE-CONTACT-ID #ADMIN-SCREEN                        /* RE26
    #MAIN-NAME := #ADMIN-SCREEN                                                      /* RE26
    MOVE SSN TO #MAIN-SSN
  END-FIND
*  MOVE STATUS.NAME-SP  TO #MAIN-NAME
*  MOVE STATUS.SSN-SP   TO #MAIN-SSN
ELSE
  MOVE 'MEMBER' TO #NAME-LBL
*  MOVE STATUS.RANK TO #MAIN-RANK                                                    /* RE26
  FIND CONTACT WITH CONTACT-ID = STATUS.MEMBER-CONTACT-ID 
*    #MAIN-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)  /* RE26
    CALLNAT 'GPRS0CNT' STATUS.MEMBER-CONTACT-ID #ADMIN-SCREEN                        /* RE26
    #MAIN-NAME := #ADMIN-SCREEN                                                      /* RE26
    MOVE SSN TO #MAIN-SSN
  END-FIND
*  MOVE STATUS.NAME TO #MAIN-NAME
*  MOVE STATUS.SSN  TO #MAIN-SSN
END-IF
*
**MOVE STATUS.ACCESS-AUTH-CODE TO #HOLD-ACC-AUTH-CD   /* DW3
*
IF NOT #CURR-SP-TO-FMR-SP
  MOVE STATUS.STATUS-CODE-SP TO #STATUS-CODE-SP
  IF #STATUS-CODE-SP > ' ' 
    AND #STATUS-CODE-SP NE 'F'      /* RE27 F IS FORMER SPOUSE
    MOVE TRUE TO #SPOUSE-EXISTS
  END-IF
  MOVE FORMER-SPOUSE-STATUS TO #FORMER-SPOUSE-STATUS
END-IF
IF DATE-OF-MARRIAGE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE DATE-OF-MARRIAGE TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOM LEAVING NO
END-IF
IF DATE-OF-DIVORCE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE DATE-OF-DIVORCE TO #YYYYMMDD
  IF STATUS-CODE-SP = 'D'
    COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO
  END-IF
*  COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DIV LEAVING NO
END-IF
IF ACCESS-AUTH-CODE = ' '
  MOVE 'N' TO #MAIN-ACCESS-AUTH-CODE
ELSE
  MOVE ACCESS-AUTH-CODE TO #MAIN-ACCESS-AUTH-CODE
END-IF
IF #CGA-USER-COMPANY = 'S'
  MOVE (AD=P) TO #AAC-CV
ELSE
  MOVE (AD=U) TO #AAC-CV
END-IF
*
IF DATE-RETIRED NE 0
  MOVE DATE-RETIRED TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DATE-RETIRED LEAVING NO
END-IF
**IF STATUS.WIDOW-FLAG = 'Y'                       /* RE28 START
*  IF STATUS.DATE-OF-BIRTH-SP NE 0                 
*    MOVE STATUS.DATE-OF-BIRTH-SP TO #YYYYMMDD
**ELSE
*  IF STATUS.DATE-OF-BIRTH NE 0                    
*    MOVE STATUS.DATE-OF-BIRTH TO #YYYYMMDD
**END-IF
*
IF CONTACT.DATE-OF-BIRTH NE 0                    
  MOVE CONTACT.DATE-OF-BIRTH TO #YYYYMMDD        
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO
END-IF                                            /* RE28 END
*
IF STATUS.WIDOW-FLAG = 'Y'
  IF STATUS.DATE-OF-DIVORCE NE 0 AND STATUS-CODE-SP = 'D'
    MOVE STATUS.DATE-OF-DIVORCE TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
  END-IF
ELSE
*  IF STATUS.DATE-OF-DEATH NE 0                             /* RE28 START
*    MOVE STATUS.DATE-OF-DEATH TO #YYYYMMDD
  IF CONTACT.DATE-OF-DEATH NE 0
    MOVE CONTACT.DATE-OF-DEATH TO #YYYYMMDD                 /* RE28 END
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
  END-IF
END-IF
**WRITE '='STATUS-CODE-SP '='FORMER-SPOUSE-DATE-OF-DIVORCE '='FORMER-SPOUSE-STATUS '='FORMER-SPOUSE-DT-OF-BIRTH
IF FORMER-SPOUSE-DATE-OF-DIVORCE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO #YYYYMMDD
  IF FORMER-SPOUSE-STATUS  = 'D'
    COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOD LEAVING NO
  END-IF
  COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DIV LEAVING NO
END-IF
IF FORMER-SPOUSE-DATE-OF-MARRIAGE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOM LEAVING NO
END-IF
*
IF NOT #CURR-SP-TO-FMR-SP
  RESET #FORMER-SPS-SEX                                         /* RE28
  FIND (1) CONTACT WITH CONTACT-ID = FORMER-SPOUSE-CONTACT-ID
*    IF CONTACT.CONTACT-ID = FORMER-SPOUSE-CONTACT-ID           /* RE28 START
    MOVE GENDER-CD TO #FORMER-SPS-SEX
*    ELSE                                                       /* RE28
*      RESET #FORMER-SPS-SEX                                    /* RE28
*    END-IF                                                     /* RE28
  END-FIND
END-IF
*
IF LAST-FM-DATE = 0
  MOVE '   ?'  TO #MAIN-LST-FM-DM
ELSE
  MOVE STATUS.LAST-FM-DATE TO #YYYYMMDD
  MOVE EDITED #DD (EM=Z9) TO #DD-A
  COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #MAIN-LST-FM-DM WITH '-'
END-IF
*
FIND (1) CONTACT WITH ID-NUMBER = STATUS.ID-NUMBER
  ASSIGN #PARENT-CID = CONTACT.CONTACT-ID
END-FIND
*
RESET #X
FIND CONTACT-REL WITH CONTACT-1-ID = #PARENT-CID         /* RE2
  IF ROLE-2-CD = 'X'
    ADD 1 TO #X
  END-IF
END-FIND
*
RESET #I1 #I2 #STAR-SP #STAR-FS
READ CONTACT-REL WITH CONTACT-1-ID = #PARENT-CID
  IF CONTACT-1-ID NE #PARENT-CID
    ESCAPE BOTTOM
  END-IF
  FIND CONTACT WITH CONTACT-ID = CONTACT-2-ID
    REJECT IF DELETE-FLAG = 'Y'
    IF CONTACT.ID-NUMBER > 0
      FIND STATUS-2 WITH ID-NUMBER = CONTACT.ID-NUMBER
        MOVE *ISN TO #ST-ISN-2
        PERFORM GET-ISN-2       /* GET doesn't work within a FIND. Have to do PERFORM.
      END-FIND
    END-IF
    PERFORM FIND-ROLE-TYPE      /* RE25
    DECIDE FOR FIRST CONDITION
      WHEN #SPOUSE              /* RE25
        IF NOT #CURR-SP-TO-FMR-SP
          MOVE CONTACT.CONTACT-ID TO #SPOUSE-CID #BD-CI
          MOVE *ISN(0862) TO #SP-ISN
          #SPOUSE-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
          IF CONTACT.ID-NUMBER > 0
            MOVE '*' TO #STAR-SP
            PERFORM FIND-BD-LINKS
          ELSE
            FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
            IF *NUMBER(0885) > 0
              MOVE '+' TO #STAR-SP
            END-IF                                                    /* RE9 End
            FIND CNT-ADDRESS-V WITH CONTACT-ID = #SPOUSE-CID
              IF STATUS NE 'D'
                ESCAPE BOTTOM
              END-IF
            END-FIND
            FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
            FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
            IF (*COUNTER(0889) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
                OR *NUMBER(0894) > 0
                OR *NUMBER(0895) > 0
              COMPRESS '@' #STAR-SP INTO #STAR-SP LEAVING NO
            END-IF                                                    /* RE9 End
            PERFORM FIND-BD-LINKS
          END-IF
          ASSIGN #SPOUSE-SSN = SSN
          ASSIGN #SPOUSE-SEX = GENDER-CD
          MOVE TRUE TO #SPOUSE-EXISTS
        END-IF
      WHEN #FMR-SPOUSE             /* RE25
        IF NOT #CURR-SP-TO-FMR-SP
          FIND STATUS-2 WITH ID-NUMBER = #CGA-ORIG-ID                          /* RE2
            IF CONTACT.CONTACT-ID = SPOUSE-CONTACT-ID AND (NOT #SPOUSE-EXISTS OR #X > 1)
              MOVE CONTACT.CONTACT-ID TO #SPOUSE-CID #BD-CI
              MOVE *ISN(0862) TO #FS-ISN
              #SPOUSE-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
              IF CONTACT.ID-NUMBER > 0
                MOVE '*' TO #STAR-SP
                PERFORM FIND-BD-LINKS
              ELSE
                FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                IF *NUMBER(0918) > 0
                  MOVE '+' TO #STAR-SP
                END-IF                                                    /* RE9 End
                FIND NUMBER CNT-ADDRESS-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                IF *NUMBER(0922) > 0 OR *NUMBER(0923) > 0 OR *NUMBER(0924) > 0
                  COMPRESS '@' #STAR-SP INTO #STAR-SP LEAVING NO
                END-IF
                PERFORM FIND-BD-LINKS                                                    /* RE9 End
              END-IF
              ASSIGN #SPOUSE-SSN = SSN
              ASSIGN #SPOUSE-SEX = GENDER-CD
              MOVE TRUE TO #SPOUSE-EXISTS
              SUBTRACT 1 FROM #X
            ELSE
              MOVE CONTACT.CONTACT-ID TO #FORMER-SPS-CID #BD-CI
              MOVE *ISN(0862) TO #FS-ISN
              #FORMER-SPS-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
              IF CONTACT.ID-NUMBER > 0
                MOVE '*' TO #STAR-FS
                PERFORM FIND-BD-LINKS
              ELSE
                FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                IF *NUMBER(0942) > 0
                  MOVE '+' TO #STAR-FS
                END-IF                                                        /* RE9 End
                FIND NUMBER CNT-ADDRESS-V WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                IF *NUMBER(0946) > 0 OR *NUMBER(0947) > 0 OR *NUMBER(0948) > 0
                  COMPRESS '@' #STAR-FS INTO #STAR-FS LEAVING NO
                END-IF
                PERFORM FIND-BD-LINKS                                                    /* RE9 End
              END-IF
*                IF #CGA-SCR-SUFFIX = 'ACS'
*                 ASSIGN #FMR-SPS-ACC-AUTH-INS = ACCESS-AUTH-INS
*                 ASSIGN #FMR-SPS-ACC-AUTH-SVC = ACCESS-AUTH-SVC
*                END-IF
              ASSIGN #FORMER-SPS-SSN = SSN
              ASSIGN #FORMER-SPS-SEX = GENDER-CD
            END-IF                                                            /* RE2
          END-FIND
        END-IF
      WHEN #DEPENDENT             /* RE25
        ADD 1 TO #I1
        MOVE CONTACT.CONTACT-ID TO #ARR-DEPN-CID(#I1) #BD-CI
        #ARR-ISN (#I1) := *ISN(0862)
        #ARR-LINE (#I1) := #I1
        ASSIGN #ARR-ROLE-CODE (#I1) = CONTACT-REL.ROLE-2-CD
*        #ARR-ROLE-DESC(#I1) := FUNCTION-DISP-RL-DESC(<CONTACT-REL.ROLE-2-CD>)         /* RE30
        #ARR-ROLE-DESC(#I1) := FUNCTION-CODE-1-DESC(<'041',CONTACT-REL.ROLE-2-CD>)     /* RE30
        IF CONTACT.ID-NUMBER > 0
          COMPRESS '*' #ARR-ROLE-DESC(#I1) INTO #ARR-ROLE-DESC(#I1) LEAVING NO
          PERFORM FIND-BD-LINKS
        ELSE
          FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #ARR-DEPN-CID(#I1)     /* RE9 Start
          IF *NUMBER(0975) > 0
            COMPRESS FULL '+' #ARR-ROLE-DESC(#I1) INTO #ARR-ROLE-DESC(#I1) LEAVING NO
          END-IF                                                             /* RE9 End
          FIND CNT-ADDRESS-V WITH CONTACT-ID = #ARR-DEPN-CID(#I1)
            IF STATUS NE 'D'
              ESCAPE BOTTOM
            END-IF
          END-FIND
          FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #ARR-DEPN-CID(#I1)   /* RE9 Start
          FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #ARR-DEPN-CID(#I1)   /* RE9 Start
          IF (*COUNTER(0979) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
              OR *NUMBER(0984) > 0
              OR *NUMBER(0985) > 0
            COMPRESS '@' #ARR-ROLE-DESC(#I1) INTO #ARR-ROLE-DESC(#I1) LEAVING NO
          END-IF                                                    /* RE9 End
          PERFORM FIND-BD-LINKS
        END-IF
*        ASSIGN #ARR-ACC-AUTH-INS(#I1) = ACCESS-AUTH-INS
*        ASSIGN #ARR-ACC-AUTH-SVC(#I1) = ACCESS-AUTH-SVC
        #ARR-DEPN-NAME(#I1) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
        IF DATE-OF-BIRTH NE 0
          MOVE DATE-OF-BIRTH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-DOB(#I1) LEAVING NO
        END-IF
        IF DATE-OF-DEATH NE 0
          MOVE DATE-OF-DEATH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-DOD(#I1) LEAVING NO
        END-IF
        ASSIGN #ARR-SSN (#I1) = SSN
        ASSIGN #ARR-SEX (#I1) = GENDER-CD
      WHEN #OTHER  /* RE5      RE10 RE14 RE21 RE24 RE25
        ADD 1 TO #I2
        MOVE CONTACT.CONTACT-ID TO #ARR-OTH-CID(#I2) #BD-CI
        #ARR-OTH-LINE (#I2) := #I2
        #ARR-OTH-ISN (#I2) := *ISN(0862)                  /* RE17
        ASSIGN #ARR-OTH-ROLE-CODE (#I2) = ROLE-2-CD
*        #ARR-OTH-ROLE-DESC(#I2) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)         /* RE30 START
        IF ROLE-2-CD = 'Y'
          #ARR-OTH-ROLE-DESC(#I2) := 'Sp FMR-SPS'
        ELSE
          #ARR-OTH-ROLE-DESC(#I2) := FUNCTION-CODE-1-DESC(<'041',ROLE-2-CD>)   /* RE30 END
        END-IF
        PERFORM CHECK-IF-TRUST
        IF CONTACT.ID-NUMBER > 0
          COMPRESS '*' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          PERFORM FIND-BD-LINKS
        ELSE
          FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #ARR-OTH-CID(#I2)     /* RE9 Start
          IF *NUMBER(1023) > 0
            COMPRESS FULL '+' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          END-IF                                                            /* RE9 End
          FIND CNT-ADDRESS-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)
            IF STATUS NE 'D'
              ESCAPE BOTTOM
            END-IF
          END-FIND
          FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)   /* RE9 Start
          FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)   /* RE9 Start
          IF (*COUNTER(1027) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
              OR *NUMBER(1032) > 0
              OR *NUMBER(1033) > 0
            COMPRESS '@' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          END-IF                                                    /* RE9 End
          PERFORM FIND-BD-LINKS
        END-IF
*        ASSIGN #ARR-ACC-AUTH-INS(#I2) = ACCESS-AUTH-INS
*        ASSIGN #ARR-ACC-AUTH-SVC(#I2) = ACCESS-AUTH-SVC
        #ARR-OTH-NAME(#I2) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
        IF DATE-OF-BIRTH NE 0
          MOVE DATE-OF-BIRTH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOB(#I2) LEAVING NO
        END-IF
        IF DATE-OF-DEATH NE 0
          MOVE DATE-OF-DEATH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOD(#I2) LEAVING NO
        END-IF
        ASSIGN #ARR-OTH-SSN (#I2) = SSN
        ASSIGN #ARR-OTH-SEX (#I2) = GENDER-CD
*        ASSIGN #ARR-OTH-ORG (#I2) = FIRM-ORGANIZATION-NAME   /* RE25
        IF FUNCTION-CONTACT-TYPE(<ROLE-2-CD>) = 'INDIVIDUAL'  /* RE25
          ASSIGN #ARR-OTH-ORG (#I2) = CONTACT-REMARKS(1)      /* RE25
        ELSE
          IF FUNCTION-CONTACT-TYPE(<ROLE-2-CD>) = 'ORGANIZATION'  /* RE25
            ASSIGN #ARR-OTH-ORG (#I2) = FIRM-ORGANIZATION-NAME
          ELSE
            ASSIGN #ARR-OTH-ORG (#I2) = 'UNKNOWN'             /* RE25
          END-IF
        END-IF
      WHEN NONE
        IGNORE
    END-DECIDE
    IF DATE-OF-BIRTH NE 0
      MOVE DATE-OF-BIRTH TO #YYYYMMDD
      IF #SPOUSE AND NOT #CURR-SP-TO-FMR-SP         /* RE25
        COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOB LEAVING NO
      ELSE
        IF #FMR-SPOUSE AND NOT #CURR-SP-TO-FMR-SP   /* RE25
          IF #HOLD-SP-ST = 'F'
            COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOB LEAVING NO
          ELSE
            COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOB LEAVING NO
          END-IF
        ELSE
*          IF ROLE-2-CD = 'K' OR = 'G' OR = 'D'   /* RE25
          IF #DEPENDENT                           /* RE25
            COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-DOB(#I1) LEAVING NO
          ELSE
*           IF ROLE-2-CD = 'A' OR = 'B' OR = 'E' OR = 'F' OR = 'I' OR = 'P' OR = 'R' OR = 'T' OR = 'O' /* RE5
*               OR = 'H' OR = 'Z' OR = 'U' OR = 'W' OR = 'L' OR = 'V' OR = 'J' OR = 'C'   /* RE10 RE14 RE21 RE24 /* RE25
            IF #OTHER  /* RE25
              COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOB(#I2) LEAVING NO
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
    IF DATE-OF-DEATH NE 0
      MOVE DATE-OF-DEATH TO #YYYYMMDD
*      DECIDE ON FIRST VALUE OF ROLE-2-CD /* RE25
      DECIDE FOR FIRST CONDITION          /* RE25
*        VALUE 'S'                        /* RE25
        WHEN #SPOUSE                      /* RE25
          IF NOT #CURR-SP-TO-FMR-SP
            COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO
          END-IF
*        VALUE 'X'                        /* RE25
        WHEN #FMR-SPOUSE                  /* RE25
*          IF #SPOUSE-EXISTS                                           /* RE27  RE28
*            COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO  /* RE27  RE28
*          ELSE                                                                 RE28
            IF NOT #CURR-SP-TO-FMR-SP
              COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOD LEAVING NO
            END-IF
*          END-IF                                                               RE28
*        VALUE 'K','G','D'                /* RE25
        WHEN #DEPENDENT                   /* RE25
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-DOD(#I1) LEAVING NO
*        VALUE 'A','B','E','F','I','P','R','T','O','H','Z','U','W','L','V','J','C'   /* RE5    RE10 RE14 RE21 RE24 RE25
        WHEN #OTHER                       /* RE25
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOD(#I2) LEAVING NO
        WHEN NONE
          IGNORE
      END-DECIDE
    END-IF
  END-FIND
END-READ
*
MOVE #I1 TO #NO-OF-DEPN
MOVE #I2 TO #NO-OF-OTHERS
*
FIND (1) FIRS WITH ID-NUMBER  = STATUS.ID-NUMBER
  MOVE TRUE TO #FIRS-FOUND
  MOVE *ISN TO #FIRS-ISN
END-FIND
*
IF #FIRS-FOUND
  GET FIRS #FIRS-ISN
  FOR #I1 1 TO 60
    IF DEPN-CONTACT-ID (#I1) = 0
      ESCAPE BOTTOM
    END-IF
    FOR #M 1 60
      IF #ARR-DEPN-CID(#M) = 0
        ESCAPE BOTTOM
      END-IF
      IF DEPN-CONTACT-ID (#I1) = #ARR-DEPN-CID(#M)
        ASSIGN #ARR-STAT-CODE (#M) = DEPN-STATUS-CD (#I1)
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FOR
END-IF
*
PERFORM SORT-DEPN
*
END-SUBROUTINE
********************************
DEFINE SUBROUTINE FIND-ROLE-TYPE
********************************
RESET #SPOUSE #FMR-SPOUSE #DEPENDENT #OTHER
DECIDE ON FIRST VALUE OF FUNCTION-ROLE-TYPE(<ROLE-2-CD>)
  VALUE 'SPOUSE'
    MOVE TRUE TO #SPOUSE
  VALUE 'FORMER SPOUSE'
    MOVE TRUE TO #FMR-SPOUSE
  VALUE 'DEPENDENT'
    MOVE TRUE TO #DEPENDENT
  VALUE 'FORMER HUSBAND', 'OTHER'    /* RE32    
    MOVE TRUE TO #OTHER
  NONE
    IGNORE
END-DECIDE
*  
END-SUBROUTINE
*************************************
DEFINE SUBROUTINE COLLECT-OTHERS-DATA
*************************************
RESET #ARRAY-OTHERS #OTH-CID(*) #OTH-ROLE-CODE(*)  #OTH-NAME(*)
  #OTH-DOB(*) #OTH-SSN(*) #OTH-GENDER(*) #ACC-AUTH-INS(*) #ACC-AUTH-SVC(*) #OTH-DOD(*)
*
IF NOT #CURR-SP-TO-FMR-SP
  RESET #MAP-SPOUSE
END-IF
*
PERFORM GET-ISN
*
IF NOT #CURR-SP-TO-FMR-SP
  MOVE STATUS.STATUS-CODE-SP TO #HOLD-SP-ST
  MOVE STATUS.CHIEF-OF-STAFF-IND TO #HOLD-C-O-F-IND   /* DW3
ELSE
  MOVE STATUS.SPOUSE-CONTACT-ID  TO STATUS.FORMER-SPOUSE-CONTACT-ID
  MOVE 'F'                       TO STATUS.FORMER-SPOUSE-STATUS
  MOVE STATUS.NAME-SP            TO STATUS.FORMER-SPOUSE-NAME
  MOVE STATUS.DATE-OF-DIVORCE    TO STATUS.FORMER-SPOUSE-DATE-OF-DIVORCE
  MOVE STATUS.DATE-OF-MARRIAGE   TO STATUS.FORMER-SPOUSE-DATE-OF-MARRIAGE
  MOVE STATUS.SSN-SP             TO STATUS.FORMER-SPOUSE-SSN
  MOVE STATUS.DATE-OF-BIRTH-SP   TO STATUS.FORMER-SPOUSE-DT-OF-BIRTH
  MOVE STATUS.BIRTH-CERT-CODE-SP TO STATUS.FS-BIRTH-CERT-CD               /* RE12
  MOVE STATUS.MARRIAGE-CERT-CODE TO STATUS.FS-MARR-CERT-CD                /* RE12
  RESET STATUS.SPOUSE-CONTACT-ID STATUS.STATUS-CODE-SP STATUS.DATE-OF-BIRTH-SP STATUS.DATE-OF-DIVORCE
    STATUS.SSN-SP STATUS.NAME-SP STATUS.SEX-SP STATUS.BIRTH-CERT-CODE-SP STATUS.MARRIAGE-CERT-CODE  /* RE12
    STATUS.DATE-OF-MARRIAGE STATUS.TITLE-SP STATUS.MAILING-FLAG-SP  /* RE18
END-IF
*
IF (NOT #CGA-CLERK = 'DRW' OR = 'VXT' OR = 'YAK'        /* DW8  vt was 'PAM'
    OR = 'RLB' OR = 'KHB' OR = 'BMG' OR = 'CEB' OR = 'SKS')    /* Not SEL Users
* or (STATUS.process-ind = 'G')                            /* Grandfathered
    OR (STATUS.PROCESS-IND = 'G' OR = 'S')   /* Grandfathered or Combo  RE1
    OR ((STATUS.PROCESS-IND = 'I' OR = 'O' OR = 'D') AND (STATUS.WIDOW-FLAG ='Y')) /* DEAD
  IF NOT STATUS.INTRO-FLAG = 'S' OR = 'K' OR = 'G'  /* SPS/GEN (INS-ONLY)can be updated
    MOVE (AD=P) TO #RET-DT-CV
  END-IF
END-IF
*
MOVE 'SCREEN' TO #ADMIN-SCREEN        /* RE26
IF STATUS.WIDOW-FLAG = 'Y'
  MOVE 'SPOUSE' TO #NAME-LBL
*  RESET            #MAIN-RANK
*  MOVE STATUS.NAME-SP  TO #MAIN-NAME
  CALLNAT 'GPRS0CNT' STATUS.SPOUSE-CONTACT-ID #ADMIN-SCREEN                        /* RE26
  #MAIN-NAME := #ADMIN-SCREEN                                                      /* RE26
  MOVE STATUS.SSN-SP   TO #MAIN-SSN
ELSE
  MOVE 'MEMBER' TO #NAME-LBL
*  MOVE STATUS.RANK TO #MAIN-RANK
*  MOVE STATUS.NAME TO #MAIN-NAME
  CALLNAT 'GPRS0CNT' STATUS.MEMBER-CONTACT-ID #ADMIN-SCREEN                        /* RE26
  #MAIN-NAME := #ADMIN-SCREEN                                                      /* RE26
  MOVE STATUS.SSN  TO #MAIN-SSN
END-IF
*
**MOVE STATUS.ACCESS-AUTH-CODE TO #HOLD-ACC-AUTH-CD   /* DW3
*
IF NOT #CURR-SP-TO-FMR-SP
  MOVE STATUS.STATUS-CODE-SP TO #STATUS-CODE-SP
  IF #STATUS-CODE-SP > ' '                            /* RE4
    AND #STATUS-CODE-SP NE 'F'                        /* RE27 F IS FORMER SPOUSE
    MOVE TRUE TO #SPOUSE-EXISTS                       /* RE4
  END-IF                                              /* RE4
  MOVE FORMER-SPOUSE-STATUS TO #FORMER-SPOUSE-STATUS
END-IF
IF DATE-OF-MARRIAGE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE DATE-OF-MARRIAGE TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOM LEAVING NO
END-IF
IF DATE-OF-DIVORCE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE DATE-OF-DIVORCE TO #YYYYMMDD
  IF STATUS-CODE-SP = 'D'
    COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO
  END-IF
*  COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DIV LEAVING NO
END-IF
IF ACCESS-AUTH-CODE = ' '
  MOVE 'N' TO #MAIN-ACCESS-AUTH-CODE
ELSE
  MOVE ACCESS-AUTH-CODE TO #MAIN-ACCESS-AUTH-CODE
END-IF
IF #CGA-USER-COMPANY = 'S'
  MOVE (AD=P) TO #AAC-CV
END-IF
IF DATE-RETIRED NE 0
  MOVE DATE-RETIRED TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DATE-RETIRED LEAVING NO
END-IF
IF STATUS.WIDOW-FLAG = 'Y'                                 /* RE16 start
  IF STATUS.DATE-OF-BIRTH-SP NE 0
    MOVE STATUS.DATE-OF-BIRTH-SP TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO
  END-IF
ELSE
  IF STATUS.DATE-OF-BIRTH NE 0
    MOVE STATUS.DATE-OF-BIRTH TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO
  END-IF
END-IF
**IF STATUS.DATE-OF-BIRTH NE 0
*  MOVE STATUS.DATE-OF-BIRTH TO #YYYYMMDD
*  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO
**END-IF
IF STATUS.WIDOW-FLAG = 'Y'
  IF STATUS.DATE-OF-DIVORCE NE 0 AND STATUS-CODE-SP = 'D'
    MOVE STATUS.DATE-OF-DIVORCE TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
  END-IF
ELSE
  IF STATUS.DATE-OF-DEATH NE 0
    MOVE STATUS.DATE-OF-DEATH TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
  END-IF
END-IF
**IF STATUS.DATE-OF-DEATH NE 0
*  MOVE STATUS.DATE-OF-DEATH TO #YYYYMMDD
*  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
**END-IF                                                   /* RE16 end
**WRITE '='STATUS-CODE-SP '='FORMER-SPOUSE-DATE-OF-DIVORCE '='FORMER-SPOUSE-STATUS '='FORMER-SPOUSE-DT-OF-BIRTH
IF FORMER-SPOUSE-DATE-OF-DIVORCE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE FORMER-SPOUSE-DATE-OF-DIVORCE TO #YYYYMMDD
  IF FORMER-SPOUSE-STATUS  = 'D'
    COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOD LEAVING NO
  END-IF
  COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DIV LEAVING NO
END-IF
IF FORMER-SPOUSE-DATE-OF-MARRIAGE NE 0 AND NOT #CURR-SP-TO-FMR-SP
  MOVE FORMER-SPOUSE-DATE-OF-MARRIAGE TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOM LEAVING NO
END-IF
*
IF NOT #CURR-SP-TO-FMR-SP
  FIND (1) CONTACT WITH CONTACT-ID = FORMER-SPOUSE-CONTACT-ID
    IF CONTACT.CONTACT-ID = FORMER-SPOUSE-CONTACT-ID
      MOVE GENDER-CD TO #FORMER-SPS-SEX
    ELSE
      RESET #FORMER-SPS-SEX
    END-IF
  END-FIND
END-IF
*
IF LAST-FM-DATE = 0
  MOVE '   ?'  TO #MAIN-LST-FM-DM
ELSE
  MOVE STATUS.LAST-FM-DATE TO #YYYYMMDD
  MOVE EDITED #DD (EM=Z9) TO #DD-A
  COMPRESS #DD-A #MON (#MM) #YYYY-A INTO #MAIN-LST-FM-DM WITH '-'
END-IF
*
FIND (1) CONTACT WITH ID-NUMBER = STATUS.ID-NUMBER
  ASSIGN #PARENT-CID = CONTACT.CONTACT-ID
END-FIND
*
RESET #X
FIND CONTACT-REL WITH CONTACT-1-ID = #PARENT-CID         /* RE2
  IF ROLE-2-CD = 'X'
    ADD 1 TO #X
  END-IF
END-FIND
*
RESET #I2 #SPOUSE-EXISTS #STAR-SP #STAR-FS
READ CONTACT-REL WITH CONTACT-1-ID = #PARENT-CID
  IF CONTACT-1-ID NE #PARENT-CID
    ESCAPE BOTTOM
  END-IF
  FIND CONTACT WITH CONTACT-ID = CONTACT-2-ID
    REJECT IF DELETE-FLAG = 'Y'
    PERFORM FIND-ROLE-TYPE                /* RE25
*    DECIDE ON FIRST VALUE OF ROLE-2-CD
    DECIDE FOR FIRST CONDITION            /* RE25
*      VALUE 'S'                          /* RE25
      WHEN #SPOUSE                        /* RE25
        IF NOT #CURR-SP-TO-FMR-SP
          MOVE CONTACT.CONTACT-ID TO #SPOUSE-CID #BD-CI
          MOVE *ISN(1333) TO #SP-ISN                     /* re17
          #SPOUSE-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
          IF CONTACT.ID-NUMBER > 0
            MOVE '*' TO #STAR-SP
            PERFORM FIND-BD-LINKS
          ELSE
            FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #SPOUSE-CID    /* RE9 Start
            IF *NUMBER(1352) > 0
              MOVE '+' TO #STAR-SP
            END-IF                                                     /* RE9 End
            FIND CNT-ADDRESS-V WITH CONTACT-ID = #SPOUSE-CID
              IF STATUS NE 'D'
                ESCAPE BOTTOM
              END-IF
            END-FIND
            FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
            FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
            IF (*COUNTER(1356) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
                OR *NUMBER(1361) > 0
                OR *NUMBER(1362) > 0
              COMPRESS '@' #STAR-SP INTO #STAR-SP LEAVING NO
            END-IF                                                    /* RE9 End
            PERFORM FIND-BD-LINKS
          END-IF
          ASSIGN #SPOUSE-SSN = SSN
          ASSIGN #SPOUSE-SEX = GENDER-CD
          MOVE TRUE TO #SPOUSE-EXISTS                              /* RE2
        END-IF
*      VALUE 'X'              /* RE25
      WHEN #FMR-SPOUSE        /* RE25                
        IF NOT #CURR-SP-TO-FMR-SP
          FIND STATUS-2 WITH ID-NUMBER = #CGA-ORIG-ID               /* RE2 START
            IF CONTACT.CONTACT-ID = SPOUSE-CONTACT-ID AND (NOT #SPOUSE-EXISTS OR #X > 1)
              MOVE CONTACT.CONTACT-ID TO #SPOUSE-CID #BD-CI
              MOVE *ISN(1333) TO #FS-ISN                           /* RE17
              #SPOUSE-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
              IF CONTACT.ID-NUMBER > 0
                MOVE '*' TO #STAR-SP
                PERFORM FIND-BD-LINKS
              ELSE
                FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                IF *NUMBER(1386) > 0
                  MOVE '+' TO #STAR-SP
                END-IF                                                    /* RE9 End
                FIND CNT-ADDRESS-V WITH CONTACT-ID = #SPOUSE-CID
                  IF STATUS NE 'D'
                    ESCAPE BOTTOM
                  END-IF
                END-FIND
                FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #SPOUSE-CID   /* RE9 Start
                IF (*COUNTER(1390) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
                    OR *NUMBER(1395) > 0
                    OR *NUMBER(1396) > 0
                  COMPRESS '@' #STAR-SP INTO #STAR-SP LEAVING NO
                END-IF                                                    /* RE9 End
                PERFORM FIND-BD-LINKS
              END-IF
              ASSIGN #SPOUSE-SSN = SSN
              ASSIGN #SPOUSE-SEX = GENDER-CD
              MOVE TRUE TO #SPOUSE-EXISTS
            ELSE
              MOVE CONTACT.CONTACT-ID TO #FORMER-SPS-CID #BD-CI
              MOVE *ISN(1333) TO #FS-ISN                           /* RE17
              #FORMER-SPS-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
              IF CONTACT.ID-NUMBER > 0
                MOVE '*' TO #STAR-FS
                PERFORM FIND-BD-LINKS
              ELSE
                FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                IF *NUMBER(1415) > 0
                  MOVE '+' TO #STAR-FS
                END-IF                                                        /* RE9 End
                FIND CNT-ADDRESS-V WITH CONTACT-ID = #FORMER-SPS-CID
                  IF STATUS NE 'D'
                    ESCAPE BOTTOM
                  END-IF
                END-FIND
                FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #FORMER-SPS-CID   /* RE9 Start
                IF (*COUNTER(1419) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
                    OR *NUMBER(1424) > 0
                    OR *NUMBER(1425) > 0
                  COMPRESS '@' #STAR-FS INTO #STAR-FS LEAVING NO
                END-IF                                                    /* RE9 End
                PERFORM FIND-BD-LINKS
              END-IF
*                IF #CGA-SCR-SUFFIX = 'ACS'
*                 ASSIGN #FMR-SPS-ACC-AUTH-INS = ACCESS-AUTH-INS
*                 ASSIGN #FMR-SPS-ACC-AUTH-SVC = ACCESS-AUTH-SVC
*                END-IF
              ASSIGN #FORMER-SPS-SSN = SSN
              ASSIGN #FORMER-SPS-SEX = GENDER-CD
            END-IF                                                  /* RE2 END
          END-FIND
        END-IF
*      VALUE 'A', 'B', 'E', 'F', 'I', 'P', 'R', 'T', 'O', 'H', 'Z', 'U', 'W','L','V','J','C'   /* RE5   RE10 RE14 RE21 RE24 RE25
      WHEN #OTHER     /* RE25
        ADD 1 TO #I2
        MOVE CONTACT.CONTACT-ID TO #ARR-OTH-CID(#I2) #BD-CI
        MOVE *ISN(1333) TO #ARR-OTH-ISN(#I2)
        #ARR-OTH-LINE (#I2) := #I2
        ASSIGN #ARR-OTH-ROLE-CODE (#I2) = ROLE-2-CD
*        #ARR-OTH-ROLE-DESC(#I2) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)         /* RE30
        IF ROLE-2-CD = 'Y'
          #ARR-OTH-ROLE-DESC(#I2) := 'Sp FMR-SPS'
        ELSE
          #ARR-OTH-ROLE-DESC(#I2) := FUNCTION-CODE-1-DESC(<'041',ROLE-2-CD>)   /* RE30 END
        END-IF
        PERFORM CHECK-IF-TRUST
        IF CONTACT.ID-NUMBER > 0
          COMPRESS '*' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          PERFORM FIND-BD-LINKS
        ELSE
          FIND NUMBER CONTACT-NOTES WITH CONTACT-ID = #ARR-OTH-CID(#I2)     /* RE9 Start
          IF *NUMBER(1460) > 0
            COMPRESS FULL '+' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          END-IF                                                            /* RE9 End
          FIND CNT-ADDRESS-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)
            IF STATUS NE 'D'
              ESCAPE BOTTOM
            END-IF
          END-FIND
          FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)   /* RE9 Start
          FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #ARR-OTH-CID(#I2)   /* RE9 Start
          IF (*COUNTER(1464) > 0 AND CNT-ADDRESS-V.STATUS NE 'D')
              OR *NUMBER(1469) > 0
              OR *NUMBER(1470) > 0
            COMPRESS '@' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
          END-IF                                                    /* RE9 End
          PERFORM FIND-BD-LINKS
        END-IF
*        ASSIGN #ARR-ACC-AUTH-INS(#I2) = ACCESS-AUTH-INS
*        ASSIGN #ARR-ACC-AUTH-SVC(#I2) = ACCESS-AUTH-SVC
        #ARR-OTH-NAME(#I2) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
        IF DATE-OF-BIRTH NE 0
          MOVE DATE-OF-BIRTH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOB(#I2) LEAVING NO
        END-IF
        IF DATE-OF-DEATH NE 0
          MOVE DATE-OF-DEATH TO #YYYYMMDD
          COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOD(#I2) LEAVING NO
        END-IF
        ASSIGN #ARR-OTH-SSN (#I2) = SSN
        ASSIGN #ARR-OTH-SEX (#I2) = GENDER-CD
        IF FUNCTION-CONTACT-TYPE(<ROLE-2-CD>) = 'INDIVIDUAL'  /* RE25
          ASSIGN #ARR-OTH-ORG (#I2) = CONTACT-REMARKS(1)      /* RE25
        ELSE
          IF FUNCTION-CONTACT-TYPE(<ROLE-2-CD>) = 'ORGANIZATION'  /* RE25
            ASSIGN #ARR-OTH-ORG (#I2) = FIRM-ORGANIZATION-NAME
          ELSE
            ASSIGN #ARR-OTH-ORG (#I2) = 'UNKNOWN'             /* RE25
          END-IF
        END-IF
      WHEN NONE
        IGNORE
    END-DECIDE
    IF DATE-OF-BIRTH NE 0
      MOVE DATE-OF-BIRTH TO #YYYYMMDD
*      IF ROLE-2-CD = 'S' AND NOT #CURR-SP-TO-FMR-SP  /* RE25
      IF #SPOUSE AND NOT #CURR-SP-TO-FMR-SP           /* RE25
        COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOB LEAVING NO
      ELSE
*        IF ROLE-2-CD = 'X' AND NOT #CURR-SP-TO-FMR-SP /* RE25
        IF #FMR-SPOUSE AND NOT #CURR-SP-TO-FMR-SP      /* RE25
          IF #HOLD-SP-ST = 'F'
            COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOB LEAVING NO
          ELSE
            COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOB LEAVING NO
          END-IF
        ELSE
*          IF ROLE-2-CD = 'A' OR = 'B' OR = 'E' OR = 'F' OR = 'I' OR = 'P' OR = 'R' OR = 'T' OR = 'O' /* RE5   
*              OR = 'H' OR = 'Z' OR = 'U' OR = 'W' OR = 'L' OR = 'V' OR = 'J' OR = 'C'       /* RE10 RE14 RE21 RE24  RE25
          IF #OTHER             /* RE25
            COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOB(#I2) LEAVING NO
          END-IF
        END-IF
      END-IF
    END-IF
    IF DATE-OF-DEATH NE 0
      MOVE DATE-OF-DEATH TO #YYYYMMDD
*      IF ROLE-2-CD = 'S' AND NOT #CURR-SP-TO-FMR-SP    /* RE25
      IF #SPOUSE AND NOT #CURR-SP-TO-FMR-SP             /* RE25    
        COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO
      ELSE
*        IF ROLE-2-CD = 'X' AND NOT #CURR-SP-TO-FMR-SP  /* RE25
        IF #FMR-SPOUSE                                  /* RE25
*          IF #SPOUSE-EXISTS                                           /* RE27  RE28
*            COMPRESS #MM-A #DD-A #YYYY-A INTO #SPOUSE-DOD LEAVING NO  /* RE27  RE28
*          ELSE                                                                 RE28
          IF NOT #CURR-SP-TO-FMR-SP       /* RE25
            COMPRESS #MM-A #DD-A #YYYY-A INTO #FORMER-SPS-DOD LEAVING NO
          END-IF
*          END-IF                                                               RE28
        ELSE
*          IF ROLE-2-CD = 'A' OR = 'B' OR = 'E' OR = 'F' OR = 'I' OR = 'P' OR = 'R' OR = 'T' OR = 'O'  /* RE5
*              OR = 'H' OR = 'Z' OR = 'U' OR = 'W' OR = 'L' OR = 'V' OR = 'J' OR = 'C'    /* RE10 RE14 RE21 RE24 RE25
          IF #OTHER                /* RE25
            COMPRESS #MM-A #DD-A #YYYY-A INTO #ARR-OTH-DOD(#I2) LEAVING NO
          END-IF
        END-IF
      END-IF
    END-IF
  END-FIND
END-READ
*
MOVE #I2 TO #NO-OF-OTHERS
*
END-SUBROUTINE
*************************
DEFINE SUBROUTINE GET-ISN
*************************
GET STATUS #CGA-ISN
MOVE *ISN TO #ST-ISN
*
IF #MSG = ' '     /* RE20
  CALLNAT 'GGRANSXX' USING STATUS.ID-NUMBER STATUS.PROCESS-IND  /* RE19 START
    STATUS.INTRO-FLAG STATUS.MILITARY-SERVICE STATUS.DATE-OF-DEATH
    STATUS.NUMBER-MEMBERSHIPS STATUS.PLAN (1) #TEXT-40
  COMPRESS #MSG #TEXT-40 INTO #MSG
  IF #TEXT-40-LAST-3 = '(Pe'                                  /* RE22
    COMPRESS #MSG 'nding Confirmation)' INTO #MSG LEAVING NO  /* RE22
  END-IF                                                      /* RE22
  IF FUNCTION-SERVICE-PLAN(<STATUS.ID-NUMBER>) = '3SR'
    COMPRESS #MSG ' + Services Member' INTO #MSG LEAVING NO
  END-IF
END-IF                                                          /* RE19 END
*
END-SUBROUTINE
*************************
DEFINE SUBROUTINE GET-ISN-2
*************************
GET STATUS-2 #ST-ISN-2
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE VALIDATE-FIELDS
*********************************
*
IF STATUS.WIDOW-FLAG = 'Y' /* AND #FORMER-SPOUSE-STATUS NE '*'
  IF (#HOLD-SP-ST = 'D') AND (NOT #CGA-CLERK = 'DRW' OR = 'TDM')
      AND (NOT #STATUS-CODE-SP = 'D' OR = 'P')
    REINPUT 'CALL EDS TO UNDEAD A WIDOW'
      MARK *#STATUS-CODE-SP
  END-IF

  CALLNAT 'GWDSTSXX' #STATUS-CODE-SP #INVALID #ERR-MSG
  IF #INVALID
    REINPUT USING HELP
      MARK *#STATUS-CODE-SP
  END-IF
END-IF
*
IF #FORMER-SPOUSE-STATUS = '*' AND #SPOUSE-NAME = ' '
  REINPUT 'Former Spouse status must be "F/D/G"'
    MARK *#FORMER-SPOUSE-STATUS
END-IF
*
IF #FORMER-SPOUSE-STATUS = 'F' OR = 'D' OR = ' ' OR = 'G' /* DW3
    OR (#FORMER-SPOUSE-STATUS = '*' AND #STATUS-CODE-SP NE ' ')
  IGNORE
ELSE
  REINPUT 'Former Spouse status must be "F/D/G"'
    MARK *#FORMER-SPOUSE-STATUS
END-IF
*
IF #FORMER-SPOUSE-STATUS = '*'     /* DW6
    OR #STATUS-CODE-SP = 'F'         /* RE7
  MOVE STATUS.SPOUSE-CONTACT-ID  TO STATUS.FORMER-SPOUSE-CONTACT-ID
  MOVE STATUS.STATUS-CODE-SP     TO STATUS.FORMER-SPOUSE-STATUS
  MOVE STATUS.NAME-SP            TO STATUS.FORMER-SPOUSE-NAME
  MOVE STATUS.DATE-OF-DIVORCE    TO STATUS.FORMER-SPOUSE-DATE-OF-DIVORCE
  MOVE STATUS.DATE-OF-MARRIAGE   TO STATUS.FORMER-SPOUSE-DATE-OF-MARRIAGE
  MOVE STATUS.DATE-OF-BIRTH-SP   TO STATUS.FORMER-SPOUSE-DT-OF-BIRTH
  MOVE STATUS.MARRIAGE-CERT-CODE TO STATUS.FS-MARR-CERT-CD
  MOVE STATUS.BIRTH-CERT-CODE-SP TO STATUS.FS-BIRTH-CERT-CD                /* RE
  MOVE STATUS.SSN-SP             TO STATUS.FORMER-SPOUSE-SSN
  MOVE #SPOUSE-NAME              TO #FORMER-SPS-NAME
  MOVE #SPOUSE-DOB               TO #FORMER-SPS-DOB
  MOVE #SPOUSE-DOM               TO #FORMER-SPS-DOM
  MOVE #SPOUSE-DIV               TO #FORMER-SPS-DIV
  MOVE #SPOUSE-DOD               TO #FORMER-SPS-DOD
  MOVE #SPOUSE-CID               TO #FORMER-SPS-CID
  MOVE #SPOUSE-SSN               TO STATUS.FORMER-SPOUSE-SSN #FORMER-SPS-SSN
  MOVE #SPOUSE-SEX               TO #FORMER-SPS-SEX
  MOVE #STAR-SP                  TO #STAR-FS          /* RE13
  IF NOT (#FORMER-SPOUSE-STATUS = 'F' OR = 'D')   /* MAY BE '*'
*  IF STATUS.STATUS-CODE-SP = 'D'
    IF STATUS.FORMER-SPOUSE-STATUS = 'D'
      MOVE 'D' TO #FORMER-SPOUSE-STATUS
    ELSE
      MOVE 'F' TO STATUS.FORMER-SPOUSE-STATUS #FORMER-SPOUSE-STATUS
    END-IF
  END-IF
*
  RESET STATUS.SPOUSE-CONTACT-ID STATUS.STATUS-CODE-SP STATUS.DATE-OF-BIRTH-SP STATUS.DATE-OF-DIVORCE STATUS.SSN-SP
    STATUS.NAME-SP STATUS.SEX-SP STATUS.MARRIAGE-CERT-CODE STATUS.BIRTH-CERT-CODE-SP
    STATUS.DATE-OF-MARRIAGE STATUS.TITLE-SP MAILING-FLAG-SP      /* RE18
  RESET #STATUS-CODE-SP #SPOUSE-NAME #SPOUSE-SSN #SPOUSE-SEX #SPOUSE-DOB #SPOUSE-DOM #SPOUSE-DIV #SPOUSE-DOD
    #SPOUSE-CID #STAR-SP                            /* RE13
* RESET STATUS.ACCESS-AUTH-CODE
  MOVE TRUE TO #CURR-SP-TO-FMR-SP
END-IF
*
* * * * * * * * * * * * * EDIT SCREEN * * * * * * * * * * * * * *
*
IF #STATUS-CODE-SP = ' ' AND (#SPOUSE-NAME NE ' ' OR #SPOUSE-SSN NE 0 OR #SPOUSE-SEX NE ' '
    OR #SPOUSE-DOB NE ' ' OR #SPOUSE-DOM NE ' ' OR #SPOUSE-DIV NE ' ' OR #SPOUSE-DOD NE ' ')
  REINPUT 'If Spouse info exists, enter Spouse Status Code'
    MARK *#STATUS-CODE-SP
END-IF
*
IF #FORMER-SPOUSE-STATUS = ' ' AND (#FORMER-SPS-NAME NE ' ' OR #FORMER-SPS-DOB NE ' ' OR #FORMER-SPS-DOM NE ' '
    OR #FORMER-SPS-DIV NE ' ' OR #FORMER-SPS-DOD NE ' ' OR #FORMER-SPS-SEX NE ' ' OR #FORMER-SPS-SSN NE 0)
  REINPUT 'If Former Spouse info exists, enter Former Spouse Status Code'
    MARK *#FORMER-SPOUSE-STATUS
END-IF
*
IF #STATUS-CODE-SP = 'N' AND (#SPOUSE-NAME NE ' ' OR #SPOUSE-SSN NE 0 OR #SPOUSE-SEX NE ' '  /* RE
    OR #SPOUSE-DOB NE ' ' OR #SPOUSE-DOM NE ' ' OR #SPOUSE-DIV NE ' ' OR #SPOUSE-DOD NE ' ')
  REINPUT 'Not valid when Spouse info exists. Either delete Spouse or correct Status Code.'
    MARK *#STATUS-CODE-SP
END-IF                                                                                       /* RE
*
IF (#STATUS-CODE-SP = 'D') AND (#HOLD-SP-ST NE 'D')    /* DW11
  MOVE 'N' TO #Y-N
  INPUT USING MAP 'C2231MCB'
*
  IF #Y-N = 'Y'
    IGNORE
  ELSE
    MOVE #HOLD-SP-ST TO #STATUS-CODE-SP
  END-IF
END-IF                                                      /* END DW11
*
MOVE #SPOUSE-DOM TO #MMDDYYYY
IF (#SPOUSE-DOM NE ' ' AND #SPOUSE-DOM NE MASK (MMDDYYYY)) OR
    (#SPOUSE-DOM NE ' ' AND #YYYY-N < 1900)
  REINPUT 'Invalid date format - must be MMDDYYYY'
    MARK *#SPOUSE-DOM
END-IF
*
MOVE #FORMER-SPS-DOM TO #MMDDYYYY
IF (#FORMER-SPS-DOM NE ' ' AND #FORMER-SPS-DOM NE MASK (MMDDYYYY)) OR
    (#FORMER-SPS-DOM NE ' ' AND #YYYY-N < 1900)
  REINPUT 'Invalid date format - must be MMDDYYYY'
    MARK *#FORMER-SPS-DOM
END-IF
*
MOVE #SPOUSE-DIV TO #MMDDYYYY
IF (#SPOUSE-DIV NE ' ' AND #SPOUSE-DIV NE MASK (MMDDYYYY)) OR
    (#SPOUSE-DIV NE ' ' AND #YYYY-N < 1900)
  REINPUT 'Invalid date format - must be MMDDYYYY'
    MARK *#SPOUSE-DIV
END-IF
*
MOVE #FORMER-SPS-DIV TO #MMDDYYYY
IF (#FORMER-SPS-DIV NE ' ' AND #FORMER-SPS-DIV NE MASK (MMDDYYYY)) OR
    (#FORMER-SPS-DIV NE ' ' AND #YYYY-N < 1900)
  REINPUT 'Invalid date format - must be MMDDYYYY'
    MARK *#FORMER-SPS-DIV
END-IF
*
IF #SPOUSE-DOM NE ' '
  MOVE #SPOUSE-DOM TO #MMDDYYYY
  COMPRESS #YYYY-X #MM-X #DD-X INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO DATE-OF-MARRIAGE
ELSE
  RESET DATE-OF-MARRIAGE    /* DW4
END-IF
*
IF #FORMER-SPS-DOM NE ' '
  MOVE #FORMER-SPS-DOM TO #MMDDYYYY
  COMPRESS #YYYY-X #MM-X #DD-X INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO FORMER-SPOUSE-DATE-OF-MARRIAGE
ELSE
  RESET FORMER-SPOUSE-DATE-OF-MARRIAGE    /* DW4
END-IF
*
IF #SPOUSE-DIV NE ' '
  MOVE #SPOUSE-DIV TO #MMDDYYYY
  COMPRESS #YYYY-X #MM-X #DD-X INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO DATE-OF-DIVORCE
ELSE
  RESET DATE-OF-DIVORCE    /* DW4
END-IF
*
IF #FORMER-SPS-DIV NE ' '
  MOVE #FORMER-SPS-DIV TO #MMDDYYYY
  COMPRESS #YYYY-X #MM-X #DD-X INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO FORMER-SPOUSE-DATE-OF-DIVORCE
ELSE
  RESET FORMER-SPOUSE-DATE-OF-DIVORCE   /* DW4
END-IF
*
IF   (#SPOUSE-DIV NE ' ')
* AND (NOT (STATUS.STATUS-CODE-SP = 'F' OR = 'D' OR = 'R'))
    AND (NOT (#STATUS-CODE-SP = 'F' OR = 'D' OR = 'R'))
  REINPUT 'Sps status must be "F/D/R" with DIV/DTH Dt'
    MARK *#STATUS-CODE-SP
END-IF
*
**IF   (STATUS.STATUS-CODE-SP = 'D')
IF   (#STATUS-CODE-SP = 'D')
    AND #SPOUSE-NAME = ' ' /*(STATUS.NAME-SP = ' ')
  REINPUT '"D" invalid without spouse'
    MARK *STATUS.STATUS-CODE-SP
END-IF
*
IF DATE-OF-MARRIAGE GT *DATN
  REINPUT 'Future date invalid.'
    MARK *#SPOUSE-DOM
END-IF
*
IF STATUS.WIDOW-FLAG NE 'Y'
*  CALLNAT 'GSPSTSXX' STATUS.STATUS-CODE-SP #INVALID #ERR-MSG
  CALLNAT 'GSPSTSXX' #STATUS-CODE-SP #INVALID #ERR-MSG
  IF #INVALID
    REINPUT USING HELP
      MARK *#STATUS-CODE-SP
  END-IF
END-IF
*
IF   (STATUS.DATE-OF-MARRIAGE NE 0)
    AND (STATUS.DATE-OF-DIVORCE NE 0)
  IF STATUS.DATE-OF-DIVORCE LE STATUS.DATE-OF-MARRIAGE
    REINPUT 'Marriage must precede divorce.'
      MARK *#SPOUSE-DOM
  END-IF
END-IF
*
IF #FORMER-SPOUSE-STATUS = ' '
  IF STATUS.FORMER-SPOUSE-NAME NE ' '
    MOVE 'F' TO #FORMER-SPOUSE-STATUS
  END-IF
END-IF
*
IF (#STATUS-CODE-SP NE ' '
    OR #SPOUSE-DOM NE ' ' OR #SPOUSE-DIV NE ' ') AND
    #SPOUSE-NAME = ' '
  REINPUT 'If you wish to enter Spouse info, please press PF4.'
    MARK *#STATUS-CODE-SP
END-IF
*
IF (#FORMER-SPOUSE-STATUS NE ' '
    OR #FORMER-SPS-DOM NE ' ' OR #FORMER-SPS-DIV NE ' ') AND
    #FORMER-SPS-NAME = ' '
  REINPUT 'If you wish to enter Former Spouse info, please press PF4.'
    MARK *#FORMER-SPOUSE-STATUS
END-IF
*
FOR #I1 1 TO 10
  CALLNAT 'GDPSTSXX' #STATUS-CODE (#I1) #INVALID #ERR-MSG
  IF #INVALID
    REINPUT USING HELP
      MARK *#STATUS-CODE (#I1)
  END-IF
END-FOR
*
IF NOT (#MAIN-ACCESS-AUTH-CODE = 'N' OR = 'Y')   /* DW7
  REINPUT 'Value must be Y or N'
    MARK *#MAIN-ACCESS-AUTH-CODE
ELSE
  MOVE #MAIN-ACCESS-AUTH-CODE TO ACCESS-AUTH-CODE
END-IF
*
IF #CGA-USER-COMPANY = 'I' AND STATUS.CHIEF-OF-STAFF-IND NE #HOLD-C-O-F-IND    /* DW1/DW3
  IF NOT STATUS.CHIEF-OF-STAFF-IND = ' ' OR = 'R' OR = 'X'               /* DW10
*  CALLNAT 'GSPCDSXX' STATUS.CHIEF-OF-STAFF-IND #INVALID #ERR-MSG /*DW3
*  IF #INVALID
    REINPUT 'AAFMAA can only change Spc Code to "R" or "X"'      /* DW10
      MARK *STATUS.CHIEF-OF-STAFF-IND
  END-IF
END-IF
*
MOVE #MAIN-DATE-RETIRED TO #MMDDYYYY
IF   (#MAIN-DATE-RETIRED NE ' ' AND #MAIN-DATE-RETIRED NE MASK (MMDDYYYY)) OR                  /* DW8
    (#MAIN-DATE-RETIRED NE ' ' AND #YYYY-N < 1900)
  REINPUT 'Invalid date format - must be MMDDYYYY'
    MARK *#MAIN-DATE-RETIRED
END-IF
*
IF #MAIN-DATE-RETIRED NE ' '                   /* DW8
  MOVE #MAIN-DATE-RETIRED TO #MMDDYYYY
  COMPRESS #YYYY-X #MM-X #DD-X INTO #YYYYMMDD-A LEAVING NO
  MOVE #YYYYMMDD TO DATE-RETIRED
ELSE
  RESET DATE-RETIRED
END-IF
*
IF #STAT-CD-CV(*) MODIFIED
  FOR #I1 1 TO 10
    IF #LINE(#I1) = 0
      ESCAPE BOTTOM
    ELSE
      FOR #I2 1 60
        IF #DEPN-CID (#I1) = FIRS.DEPN-CONTACT-ID (#I2)
          MOVE #STATUS-CODE (#I1) TO FIRS.DEPN-STATUS-CD (#I2)
          MOVE #STATUS-CODE (#I1) TO #ARR-STAT-CODE (#I1)
          IF #ROLE-CODE(#I1) = 'K' OR = 'D' AND #STATUS-CODE (#I1) = 'G'             /* RE
            FIND CONTACT WITH CONTACT-ID = #DEPN-CID (#I1)
              MOVE 'G' TO #ROLE-CODE(#I1)
              IF ID-NUMBER > 0
                MOVE '*GRANDCHILD' TO #ROLE-DESC(#I1)
              ELSE
                MOVE ' GRANDCHILD' TO #ROLE-DESC(#I1)
              END-IF
            END-FIND
            FIND CONTACT-REL WITH CONTACT-2-ID = FIRS.DEPN-CONTACT-ID (#I2)
              IF CONTACT-1-ID = MEMBER-CONTACT-ID
                MOVE 'G' TO ROLE-2-CD
                MOVE #CGA-CLERK          TO CONTACT-REL.LAST-USER-UPD
                MOVE *DATN               TO CONTACT-REL.LAST-DATE-UPD
                MOVE *TIMN               TO CONTACT-REL.LAST-TIME-UPD
                UPDATE
                ESCAPE BOTTOM
              END-IF
            END-FIND                                                                /* RE
            ESCAPE BOTTOM
          END-IF
          IF #DOD(#I1) NE ' ' AND #STATUS-CODE (#I1) NE 'D'             /* RE14 Start
            RESET #DOD(#I1)
            FIND CONTACT WITH CONTACT-ID = #DEPN-CID (#I1)
              RESET DATE-OF-DEATH
              MOVE #CGA-CLERK          TO CONTACT-REL.LAST-USER-UPD
              MOVE *DATN               TO CONTACT-REL.LAST-DATE-UPD
              MOVE *TIMN               TO CONTACT-REL.LAST-TIME-UPD
              UPDATE
              ESCAPE BOTTOM
            END-FIND
          END-IF                                                        /* RE14 End
        END-IF
      END-FOR
    END-IF
  END-FOR
END-IF
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE WRITE-TRANS-REG
*********************************
* * * * * * * * * * TRANS REG PROCESSING * * * * * * * *
*
RESET #ST-REC-CHGD #FIRS-REC-CHGD
MOVE #STATUS-CODE-SP TO STATUS-CODE-SP
MOVE #FORMER-SPOUSE-STATUS TO FORMER-SPOUSE-STATUS
MOVE *TIMN TO #HOLD-TIME
*
CALLNAT 'C2231SCB'
  #ST-ISN
  #ST-REC-CHGD
  #FIRS-ISN
  #FIRS-REC-CHGD
  #ITR1
  #ITR2
  STATUS.ID-NUMBER
  STATUS.PROCESS-IND /* PM - 06/2000 - SPLIT COMPANIES
  STATUS.NAME
  FIRS.DP-LAST-FM-DATE
  #CGA-CLERK
  #HOLD-TIME
  #DEPN-CID (*)
  #STATUS-CODE (*)
  STATUS-CODE-SP
  FORMER-SPOUSE-STATUS
  CHIEF-OF-STAFF-IND   /* DW1
  ACCESS-AUTH-CODE     /* DW7
  DATE-RETIRED         /* DW8
* * * * * * * * * * UPDATE RECORDS * * * * * * * * * *
*
IF #ST-REC-CHGD OR #FIRS-REC-CHGD
  PERFORM SET-MD-ENTRY                                   /* DW9
  MOVE *DATN      TO STATUS.LAST-FM-DATE
  MOVE *DATN      TO STATUS.DTS-DATN
  MOVE #HOLD-TIME TO STATUS.DTS-TIMN
  MOVE TRUE TO #UPDATED
*
  MOVE BY POSITION STATUS TO ##STATUS
  GET STATUS #CGA-ISN
  MOVE BY POSITION ##STATUS TO STATUS
  UPDATE (1924)
*
  IF CHIEF-OF-STAFF-IND NE #HOLD-C-O-F-IND              /* RE11 Start
    FIND CONTACT WITH ID-NUMBER = STATUS.ID-NUMBER
      DECIDE ON FIRST VALUE OF CHIEF-OF-STAFF-IND
        VALUE 'R'
          MOVE 'Y' TO RANK-PROTECT
          MOVE *DATN TO RANK-PROTECT-DATE
        VALUE 'S'
          MOVE 'Y' TO SSN-PROTECT
          MOVE *DATN TO SSN-PROTECT-DATE
        VALUE 'Q'
          MOVE 'Y' TO RANK-PROTECT SSN-PROTECT
          MOVE *DATN TO RANK-PROTECT-DATE SSN-PROTECT-DATE
        NONE
          MOVE 'N' TO RANK-PROTECT SSN-PROTECT
          RESET RANK-PROTECT-DATE SSN-PROTECT-DATE
      END-DECIDE
      UPDATE (1929)
    END-FIND
  END-IF                                                /* RE11 End
*
**display #CURR-SP-TO-FMR-SP #HOLD-SP-ST #STATUS-CODE-SP FORMER-SPOUSE-CONTACT-ID
  IF #CURR-SP-TO-FMR-SP OR (#HOLD-SP-ST NE #STATUS-CODE-SP AND #STATUS-CODE-SP = 'F')
    IF #CURR-SP-TO-FMR-SP
      MOVE FORMER-SPOUSE-CONTACT-ID TO #CONTACT-ID
    ELSE
      MOVE SPOUSE-CONTACT-ID TO #CONTACT-ID
    END-IF
    FIND CONTACT-REL WITH CONTACT-1-ID = MEMBER-CONTACT-ID      /* RE31 START
      IF ROLE-2-CD = 'X'
        DELETE
        ESCAPE BOTTOM
      END-IF
    END-FIND                                                    /* RE31 END
    FIND CONTACT-REL WITH CONTACT-2-ID = #CONTACT-ID
      IF CONTACT-1-ID = MEMBER-CONTACT-ID            /* RE
        MOVE 'X'                 TO CONTACT-REL.ROLE-2-CD CONTACT-REL.ROLE-1-CD
        MOVE #CGA-CLERK          TO CONTACT-REL.LAST-USER-UPD
        MOVE *DATN               TO CONTACT-REL.LAST-DATE-UPD
        MOVE *TIMN               TO CONTACT-REL.LAST-TIME-UPD
        UPDATE
        ESCAPE BOTTOM
      END-IF
    END-FIND
  END-IF
  IF #HOLD-SP-ST NE #STATUS-CODE-SP AND #STATUS-CODE-SP = 'S'
    FIND CONTACT-REL WITH CONTACT-2-ID = SPOUSE-CONTACT-ID
      IF CONTACT-1-ID = MEMBER-CONTACT-ID            /* RE
        MOVE 'S'                 TO CONTACT-REL.ROLE-2-CD CONTACT-REL.ROLE-1-CD
        MOVE #CGA-CLERK          TO CONTACT-REL.LAST-USER-UPD
        MOVE *DATN               TO CONTACT-REL.LAST-DATE-UPD
        MOVE *TIMN               TO CONTACT-REL.LAST-TIME-UPD
        UPDATE
        ESCAPE BOTTOM
      END-IF
    END-FIND
  END-IF
END-IF
*
IF #ST-REC-CHGD OR #FIRS-REC-CHGD
  MOVE *DATN      TO FIRS.DP-LAST-FM-DATE
  MOVE *DATN      TO FIRS.DTS-DATN
  MOVE #HOLD-TIME TO FIRS.DTS-TIMN
  IF #FIRS-FOUND
    MOVE BY POSITION FIRS TO ##FIRS
    GET FIRS #FIRS-ISN
    MOVE BY POSITION ##FIRS TO FIRS
    UPDATE RECORD (1992)
  ELSE
    STORE FIRS
  END-IF
END-IF
*
IF #ST-REC-CHGD
  #REQUEST := 'FAMILY'                          /* NG2
  CALLNAT 'MBRCRC' STATUS.MEMBER-CONTACT-ID     /* CRM SYNC PROCESS  NG1
    #REQUEST
END-IF
*
END TRANSACTION                 /* ACTUALLY UPDATE RECORD (S)
*
END-SUBROUTINE
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-MD-ENTRY                         /* DW9
* * * * * * * * * * * * * * * *
*
IF   (STATUS.STATUS-CODE-SP = 'D')     /* IF spouse STAT D ENTERED
    AND (#HOLD-SP-ST NE 'D')            /* THE FIRST TIME
  RESET MBRSHIP-MTD-DET-V
  COMPUTE MBRSHIP-MTD-DET-V.ENTRY-DATE = 99999999 - *DATN
  MOVE #CGA-ISN           TO MBRSHIP-MTD-DET-V.REC-ISN
  MOVE 'W'                TO MBRSHIP-MTD-DET-V.CODE
  MOVE STATUS.ID-NUMBER TO MBRSHIP-MTD-DET-V.ID-NUMBER
*
  IF STATUS.WIDOW-FLAG = 'Y'
    IF STATUS.PROCESS-IND = 'G'                                      /* vt1  \/
      MOVE 99999999 TO MBRSHIP-MTD-DET-V.ISSUE-DATE
    ELSE
      MOVE 60606060 TO MBRSHIP-MTD-DET-V.ISSUE-DATE
    END-IF                                                           /* vt1  /\
  ELSE
    MOVE 88888888 TO MBRSHIP-MTD-DET-V.ISSUE-DATE
  END-IF
*
  MOVE 0          TO MBRSHIP-MTD-DET-V.NUM-MBRSHIPS
  STORE MBRSHIP-MTD-DET-V
END-IF
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE CHECK-CURS-LINE
*********************************
RESET #MSG
MOVE TRUE TO #DEPN-SCREEN           /* RE17
DECIDE ON FIRST VALUE OF *CURS-LINE
  VALUE 2, 3                               /* RE23 START
    IF #NAME-LBL = 'MEMBER'
      MOVE MEMBER-CONTACT-ID TO #REL-CID
    ELSE
      MOVE #SPOUSE-CID TO #REL-CID
    END-IF                                 /* RE23 END
  VALUE 5, 6
    MOVE #SPOUSE-CID TO #REL-CID
    IF #SPOUSE-CID > 0 AND #SPOUSE-CID = #FORMER-SPS-CID
      MOVE 'S' TO #MSG
      MOVE #SP-ISN TO #MSG-ISN     /* RE17
    END-IF
    IF #SPOUSE-CID > 0 AND (#SPOUSE-CID = #DEPN-CID(*) OR = #OTH-CID(*))  /* RE17
      MOVE #SP-ISN TO #MSG-ISN                      /* RE17
    END-IF                                          /* RE17
  VALUE 7, 8
    MOVE #FORMER-SPS-CID TO #REL-CID
    IF #FORMER-SPS-CID > 0 AND #SPOUSE-CID = #FORMER-SPS-CID
      MOVE 'X' TO #MSG
      MOVE #FS-ISN TO #MSG-ISN                         /* RE17
    END-IF
    IF #FORMER-SPS-CID > 0 AND (#FORMER-SPS-CID = #DEPN-CID(*) OR = #OTH-CID(*)) /* RE17
      MOVE #FS-ISN TO #MSG-ISN                         /* RE17
    END-IF                                             /* RE17
  VALUE 11
    MOVE #DEPN-CID(1) TO #REL-CID
  VALUE 12
    MOVE #DEPN-CID(2) TO #REL-CID
  VALUE 13
    MOVE #DEPN-CID(3) TO #REL-CID
  VALUE 14
    MOVE #DEPN-CID(4) TO #REL-CID
  VALUE 15
    MOVE #DEPN-CID(5) TO #REL-CID
  VALUE 16
    MOVE #DEPN-CID(6) TO #REL-CID
  VALUE 17
    MOVE #DEPN-CID(7) TO #REL-CID
  VALUE 18
    MOVE #DEPN-CID(8) TO #REL-CID
  VALUE 19
    MOVE #DEPN-CID(9) TO #REL-CID
  VALUE 20
    MOVE #DEPN-CID(10) TO #REL-CID
  NONE
    RESET #REL-CID
END-DECIDE
*
IF *CURS-LINE = 11 OR = 12 OR = 13 OR = 14 OR = 15 OR = 16 OR = 17 OR = 18 OR = 19 OR = 20 /* RE17 START
  MOVE *CURS-LINE TO #I3
  IF #I3 = 0
    MOVE 10 TO  #I3
  ELSE
    RESET #I3-1
  END-IF
  PERFORM FIND-DUPES
END-IF
*
RESET #DEPN-SCREEN                                                                         /* RE17 END
END-SUBROUTINE
****************************************
DEFINE SUBROUTINE CHECK-CURS-LINE-OTHERS
****************************************
RESET #MSG
DECIDE ON FIRST VALUE OF *CURS-LINE
  VALUE 2, 3                               /* RE23 START
    IF #NAME-LBL = 'MEMBER'
      MOVE MEMBER-CONTACT-ID TO #REL-CID
    ELSE
      MOVE #SPOUSE-CID TO #REL-CID
    END-IF                                 /* RE23 END
  VALUE 5, 6
    MOVE #SPOUSE-CID TO #REL-CID
    IF #SPOUSE-CID > 0 AND #SPOUSE-CID = #FORMER-SPS-CID
      MOVE 'S' TO #MSG
    END-IF
    IF #SPOUSE-CID = #DEPN-CID(*) OR = #OTH-CID(*)  /* RE17
      MOVE #SP-ISN TO #MSG-ISN                      /* RE17
    END-IF                                          /* RE17
  VALUE 7, 8
    MOVE #FORMER-SPS-CID TO #REL-CID
    IF #SPOUSE-CID > 0 AND #SPOUSE-CID = #FORMER-SPS-CID
      MOVE 'X' TO #MSG
    END-IF
    IF #FORMER-SPS-CID = #DEPN-CID(*) OR = #OTH-CID(*)  /* RE17
      MOVE #FS-ISN TO #MSG-ISN                          /* RE17
    END-IF                                              /* RE17
  VALUE 11,12
    MOVE #OTH-CID(1)      TO #REL-CID
    MOVE 1 TO #I3                   /* RE17
  VALUE 13,14
    MOVE #OTH-CID(2) TO #REL-CID
    MOVE 2 TO #I3                   /* RE17
  VALUE 15,16
    MOVE #OTH-CID(3) TO #REL-CID
    MOVE 3 TO #I3                   /* RE17
  VALUE 17,18
    MOVE #OTH-CID(4) TO #REL-CID
    MOVE 4 TO #I3                   /* RE17
  VALUE 19,20
    MOVE #OTH-CID(5) TO #REL-CID
    MOVE 5 TO #I3                   /* RE17
  NONE
    RESET #REL-CID
END-DECIDE
*
IF *CURS-LINE = 11 OR = 12 OR = 13 OR = 14 OR = 15 OR = 16 OR = 17 OR = 18 OR = 19 OR = 20 /* RE17 START
  PERFORM FIND-DUPES
END-IF
*
END-SUBROUTINE
********************************
DEFINE SUBROUTINE CHECK-IF-TRUST
********************************
DECIDE ON FIRST VALUE OF ROLE-2-CD
  VALUE 'L'                                     /* RE21 START
    MOVE 'LIV. TRUST' TO #ARR-OTH-ROLE-DESC(#I2)
  VALUE 'V'
    MOVE 'IRR. TRUST' TO #ARR-OTH-ROLE-DESC(#I2)
  VALUE 'J'                                     /* RE24
    MOVE 'TES. TRUST' TO #ARR-OTH-ROLE-DESC(#I2)
  NONE
    IGNORE
END-DECIDE                                      /* RE21 END
*
END-SUBROUTINE
***************************
DEFINE SUBROUTINE SORT-DEPN
***************************
CALLNAT 'SAGSRT10' #ARR-DEPN-CID(*) #ARR-ISN(*) #ARR-ROLE-CODE(*) #ARR-ROLE-DESC(*) #ARR-DEPN-NAME(*) #ARR-STAT-CODE(*) /* RE17
  #ARR-DOB-N(*) #ARR-DOD-N(*) #ARR-SSN(*) #ARR-SEX(*) #SRT-PARM
FOR #I1 1 60
  IF #ARR-DEPN-CID(#I1) = 0
    ESCAPE BOTTOM
  END-IF
  IF #ARR-DOB-N(#I1) = 0
    RESET #ARR-DOB(#I1)
  ELSE
    MOVE #ARR-DOB-N(#I1) TO #MMDDYYYY
    COMPRESS #YYYY-X #MM-X #DD-X INTO #ARR-DOB(#I1) LEAVING NO
  END-IF
  IF #ARR-DOD-N(#I1) = 0
    RESET #ARR-DOD(#I1)
  ELSE
    MOVE #ARR-DOD-N(#I1) TO #MMDDYYYY
    COMPRESS #YYYY-X #MM-X #DD-X INTO #ARR-DOD(#I1) LEAVING NO
  END-IF
END-FOR
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE FIND-BD-LINKS   /* RE15
*******************************
RESET #BD-LINKS (*)
#BD-LINKS (*) := FUNCTION-BD-LINKS(<#BD-CI>)(*)
IF #BD-LINKS (*) > 0
*  DECIDE ON FIRST VALUE OF ROLE-2-CD                             /* RE23
  DECIDE ON FIRST VALUE OF FUNCTION-ROLE-TYPE(<ROLE-2-CD>)        /* RE23
    VALUE 'SPOUSE'                                                /* RE23
      COMPRESS '$' #STAR-SP INTO #STAR-SP LEAVING NO
    VALUE 'FORMER SPOUSE'                                         /* RE23
      IF CONTACT.CONTACT-ID = SPOUSE-CONTACT-ID AND (NOT #SPOUSE-EXISTS OR #X > 1)
        COMPRESS '$' #STAR-SP INTO #STAR-SP LEAVING NO
      ELSE
        COMPRESS '$' #STAR-FS INTO #STAR-FS LEAVING NO
      END-IF
    VALUE 'DEPENDENT'                                             /* RE23
      COMPRESS '$' #ARR-ROLE-DESC(#I1) INTO #ARR-ROLE-DESC(#I1) LEAVING NO
    VALUE 'FORMER HUSBAND', 'OTHER'  /* RE21                                        /* RE23  RE32
      COMPRESS '$' #ARR-OTH-ROLE-DESC(#I2) INTO #ARR-OTH-ROLE-DESC(#I2) LEAVING NO
    NONE
      IGNORE
  END-DECIDE
END-IF
*
END-SUBROUTINE
***************************
DEFINE SUBROUTINE FIND-DUPES    /* RE17 START
***************************
EXAMINE FULL #DEPN-CID-A(*) FOR #REL-CID-A GIVING NUMBER #DUPES
IF #DUPES = 1 AND #OTH-CID(*) > 0
  EXAMINE FULL #OTH-CID-A(*) FOR #REL-CID-A GIVING NUMBER #DUPES
END-IF
IF #DUPES = 1
  IF #REL-CID = #SPOUSE-CID OR = #FORMER-SPS-CID
    MOVE 2 TO #DUPES
  END-IF
END-IF
IF #DUPES > 1
  IF #DEPN-SCREEN
    MOVE #ARR-ISN(#I3) TO #MSG-ISN
  ELSE
    MOVE #ARR-OTH-ISN(#I3) TO #MSG-ISN
  END-IF
END-IF
*
END-SUBROUTINE                 /* RE17 END
*****************************
DEFINE SUBROUTINE HOLD-RECORD
*****************************
UPDATE(1558)    /* Hold MEMBER relations record that has a CN
UPDATE(1578)    /* Hold MEMBER record
*
END-SUBROUTINE
*
END
