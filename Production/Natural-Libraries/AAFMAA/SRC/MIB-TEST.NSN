* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* MIB-TEST - Process Test cases
* bz 09/12/2016
DEFINE DATA
PARAMETER
1 tst_Case           (A3)
1 FIRST-NAME         (A25)
1 MIDDLE-NAME        (A25)
1 LAST-NAME          (A25)
1 SUFFIX             (A10)
1 v_Occ              (A8)
1 GENDER-CD          (A1)
1 DATE-OF-BIRTH_A    (A8)
1 #ADD-STATE         (A2)
1 p_AT               (N6)   BY VALUE
1 p_SSN              (N9)   BY VALUE
LOCAL
1 tst(A/10) DYNAMIC INIT <'001;Acacia;E;Snmmmmtestcaseqv;;F;19840722;MA',
                          '002;Minerva;R;Snmmmmtestcaseqz;;M;19450903;LA',
                          '008;Harold;H;Snmmmmtestcasehh;;M;20000108;FL',
                          '014;Nadia;N;Snmmmmtestcasenn;;F;20000114;MA',
                          '031;Alice;A;Snmmmmtestcaseba;;F;19250201;NY',
                          '315;Dorian;E;Snmmmmtestcasejd;Jr.;M;19590108;CO',
                          '091;Gail;B;Snmmmmtestcasegb;;F;19320529;AR', 
                          "146;Jean-Paul;;Snmmmmv'Snmmmmtej;;M;19551010;",
                          "150;Mary'Alice;;Snmmmmz-Snmmmmten;;F;19851010;">
1 tst_N     (N2)
END-DEFINE
ON ERROR
    PRINT NOTITLE  
   / 'Testing error'     / 
     'Please notify MSS' //
     *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR /
     'AT:' p_AT  'Test selection' tst_N ///
     'Hit Enter to continue'
     ESCAPE ROUTINE
END-ERROR
    DEFINE WINDOW WIN-TST
      SIZE 19 * 69
      BASE 6 / 7
      TITLE 'MIB test samples'
      CONTROL SCREEN
    FRAMED

 SET KEY PF2
 SET KEY PF3

 tst_N := 0 
 REPEAT UNTIL *PF-KEY = 'PF2'
 INPUT WINDOW='WIN-TST' (AD=M IP=OFF) 
      01/5 'MIB Testing'(I) 3x 'Original: AT' p_AT(AD=O) 'SSN' p_SSN (AD=O)
      02/2 'Enter case number or make a manual entry:' tst_N (AD=MU EM=ZZ)
           'Test Case:' tst_Case(AD=OI)
      03/2 'First:' FIRST-NAME(AD=MI)  'Middle:' MIDDLE-NAME(AD=MI)
      04/2 'Last :' LAST-NAME(AD=MI)   'Suffix:' SUFFIX(AD=MI)
      05/2 'Occupation:' v_Occ(AD=MI)
      06/2 'Sex  M/F:' GENDER-CD(AD=MI)'DOB:' DATE-OF-BIRTH_A(AD=MI) 'State:' #ADD-STATE(AD=MI)
      07/2 '1)' '001; Acacia;  E;  Snmmmmtestcaseqv;;  ' 45T 'New 1'
      08/2 '2)' '002; Minerva; R;  Snmmmmtestcaseqz;;  ' 45T 'New 4 Contest'
      09/2 '3)' '008; Harold;  H;  Snmmmmtestcasehh;;  ' 45T '1 Rec Many codes'
      10/2 '4)' '014; Nadia;   N;  Snmmmmtestcasenn;;  ' 45T '1 Rec 1 code'
      11/2 '5)' '031; Alice;   A;  Snmmmmtestcaseba;;  ' 45T 'Alt Name. Disputed'
      12/2 '6)' '315; Dorian;  E;  Snmmmmtestcasejd;Jr.' 45T 'Name Has Suffix'
      13/2 '7)' '091; Gail;    B;  Snmmmmtestcasegb;;  ' 45T 'Name change note'
      14/2 '8)' "146; Jean-Paul;;  Snmmmmv'Snmmmmtej;; " 45T 'Alt Name, DOB'
      15/2 '9)' "150; Mary'Alice;; Snmmmmz-Snmmmmten;; " 45T 'Alt Name, DOB'
      17/2 'PF2-Run  PF3-Exit' (I)
      IF *PF-KEY = 'PF3' SET WINDOW OFF ESCAPE ROUTINE END-IF
 IF tst_N <> 0
    SEPARATE tst(tst_N) INTO
                        tst_Case
                        FIRST-NAME
                        MIDDLE-NAME 
                        LAST-NAME 
                        SUFFIX
                        GENDER-CD
                        DATE-OF-BIRTH_A
                        #ADD-STATE
         WITH DELIMITER ';'
 END-IF
 END-REPEAT
 SET CONTROL 'N' 
 INPUT (AD=O IP=OFF) 'I"m running now test case:' tst_N(EM=z9) 'Last Name:' LAST-NAME '...'
 END
