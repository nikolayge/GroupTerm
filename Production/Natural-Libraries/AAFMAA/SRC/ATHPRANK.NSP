* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPRANK - LIST MEMBERS BY RANK
* FILL IN BETWEEN * ############## LINES ON THIS SCREEN AND MODIFY VIEW
* AND LOGIC AFTER 'SELECT-PROCESS' AND 'DETAIL-PROCESS'
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  Reset display fields for GRANKSXX                 *
* DRW  09062006 DW2  Increase size of #recs-read
************************************************************************
*
DEFINE DATA
LOCAL USING G4090LNX
LOCAL
1 #PERSON-FOR    (A3) INIT <'JJF'>     /* INITIALS THIS REPORT IS FOR
1 #TITLE (A50) INIT<'ACTIVE DUTY BY RANK                        **'>
1 #NUM-UP        (P3) INIT <01>  /* MUST MATCH NUM OCC OF #FULL-LINE-TAB
1 #FULL-LINE     (A78)           /* KEEP AT (A78)
1 REDEFINE #FULL-LINE
 2 #FULL-LINE-TAB  (1)           /* CANNOT EXCEED 78 TOTAL
  3 #L-ID-NUMBER       (N05)     /* FORMAT LINE HERE AND BELOW
  3 #L-FILL-1          (A01)
  3 #L-RANK            (A03)
  3 #L-FILL-2          (A01)
  3 #L-NAME            (A22)
  3 #L-FILL-3          (A01)
  3 #L-SSN             (A11)
  3 #L-FILL-4          (A01)
  3 #L-MIL-ID          (A03)
  3 #L-FILL-5          (A01)
*
1 #NOTE-NUM      (N6)
1 #NUM-NOTES-STORED  (P3)
1 #MAX-NOTES     (P3) INIT <20>  /* MAXIMUM NUMBER OF NOTE RECORDS
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-FLAG    (A1)
  2 WIDOW-FLAG     (A1)
  2 RANK                (A3)
  2 SSN
  2 MILITARY-STATUS     (A1)
  2 MILITARY-SERVICE    (A3)
  2 DATE-BOOK
  2 NAME                (A25)
*
1 NT-800107-V VIEW OF A-NOTES
 2 ID-NUMBER
 2 NOTE-DATE (N8/1)
 2 NOTE-TEXT (A60/1)
 2 REDEFINE NOTE-TEXT
  3 ANN-MTG-CNT   (N5)
*
1 #COUNTERS
  2 #RECS-READ        (P7)    /* DW2
  2 #RECS-SELECTED    (P5)
  2 #TOTAL-SELECTED-DISP (A7)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I5               (I2)
  2 #I2               (I2)
  2 #ISC              (I2)  /* SCREEN LINE INDEX
  2 #IUP              (I2)  /* COLUMN-UP INDEX
  2 #ITB              (I2)  /* TABLE INDEX IF DETAIL STORED
*
  2 #IND              (A1/120)
  2 #ID               (N5/120)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT          (A2)
  2 #YY            (A2)
  2 #MM            (A2)
  2 #DD            (A2)
*
1 #DATN         (N8)
1 REDEFINE #DATN
 2 #DATN-CC     (A2)
 2 #DATN-YY     (A2)
 2 #DATN-MM     (A2)
 2 #DATN-DD     (A2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
 2 #TIMN-HH     (A2)
 2 REDEFINE #TIMN-HH
  3 #TIMN-HH-N  (N2)
 2 #TIMN-MM     (A2)
 2 #TIMN-SS     (A2)
 2 #TIMN-T      (A1)
*
1 #SCR-LINES      (A78/22)
1 #SPLIT-LINE     (A78)
1 REDEFINE #SPLIT-LINE
 2 #FST-60        (A60)
 2 #LST-18        (A18)
1 #UPDATE         (L)
1 #LAST      (A25)
1 #FIRST-MID (A25)
1 #EXTRA-1   (A25)
1 #EXTRA-2   (A25)
1 #EXTRA-3   (A25)
1 #EXTRA-4   (A25)
1 #COMMAND    (A2) INIT <'BA'>
1 #INVALID    (L)
1 #RANK-DISPL (P2)
1 #RANK-LIT   (A23)
1 #RANK-DISPLAY (A5)
1 #NT-NOT-FND   (L)
1 #ARMY     (P3)
1 #AIRF     (P3)
1 #OTHER    (P3)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X #TITLE
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATN
MOVE *TIMN TO #TIMN
IF #TIMN-HH-N GT 12
  SUBTRACT 12 FROM #TIMN-HH-N
END-IF
*
MOVE #TITLE TO #SCR-LINES (1)
MOVE 2  TO #ISC      /* INDEX OF 22 POSSIBLE LINES ON A SCREEN
RESET #IUP
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
READ ST-V BY MEMBER-KEY FROM '001900' /* MBRS AND UNPURGED WIDOWS BY ID
* READ ST-V BY WIDOW-KEY        /* ONLY WIDOWS BY ID-NUM
* READ ST-V PHYSICAL            /* ALL RECORDS
  IF WIDOW-FLAG = 'Y'   /* bypass deceased members
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #RECS-READ
* ####################################################################
  IF   (MILITARY-STATUS NE 'A')
   OR (MILITARY-SERVICE NE MASK ('F'))
   OR (NOT RANK = 'BG' OR = 'MG' OR = 'LTG' OR = 'GEN')
    ESCAPE TOP
  END-IF
* ####################################################################
* * * * * * * GET CURRENT AND FORMER RANK DISPLACEMENT INDEX * * *
*
  RESET #RANK-DISPL #RANK-LIT #RANK-DISPLAY                       /* PM1
  CALLNAT 'GRANKSXX' #COMMAND RANK MILITARY-SERVICE
            #INVALID #RANK-DISPL #RANK-LIT #RANK-DISPLAY
*
  IF #INVALID OR (#RANK-DISPL = 99)
    MOVE 99 TO #RANK-DISPL
  END-IF
END-ALL
*
SORT BY #RANK-DISPL RANK NAME
      USING MILITARY-STATUS
            MILITARY-SERVICE
            ID-NUMBER
            SSN
  PERFORM DETAIL-PROCESS
  ADD 1 TO #RECS-SELECTED
  DISPLAY #RANK-DISPL RANK ID-NUMBER
END-SORT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
WRITE (2) #FULL-LINE                  /* PRINT THE LAST LINE
MOVE #FULL-LINE TO #SCR-LINES (#ISC)  /* PROCESS LAST NOTE LINE
*
IF #ISC NE 22
  MOVE EDITED #RECS-SELECTED (EM=ZZZ,ZZ9) TO #TOTAL-SELECTED-DISP
  COMPRESS '** END OF LISTING **'
    'ARMY =' #ARMY 'AIRF = '#AIRF 'OTHER =' #OTHER
    'TOTAL =' #TOTAL-SELECTED-DISP INTO #SCR-LINES (22)
END-IF
*
PERFORM BUILD-NOTE-RECORD
PERFORM BUILD-NOTIFY-NOTE-RECORD
*
FIND (1) TRMS-V WITH CLERK-ID = #PERSON-FOR
  MOVE 'Y' TO MSG-JUST-SENT
  UPDATE (0189)
  END TRANSACTION
END-FIND
*
FIND (1) NT-800107-V WITH ID-NUMBER = 800107
  IF NO RECORD FOUND
    MOVE 800107 TO NT-V.ID-NUMBER
    MOVE *DATN TO NOTE-DATE (1)
    RESET NOTE-TEXT (*)
    RESET ANN-MTG-CNT
    MOVE TRUE TO #NT-NOT-FND
  END-NOREC
*
  MOVE #RECS-SELECTED TO ANN-MTG-CNT
*
  IF #NT-NOT-FND
    STORE NT-800107-V
  ELSE
    UPDATE
  END-IF
  END TRANSACTION
END-FIND
*
WRITE (2) 'TOT-RECS-READ        = ' #RECS-READ
WRITE (2) 'TOT-SELECTED         = ' #RECS-SELECTED
WRITE (2) 'TOT-army             = ' #ARMY
WRITE (2) 'TOT-airf             = ' #AIRF
WRITE (2) 'TOT-other            = ' #OTHER
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-NOTE-RECORD
* * * * * * * * * * * * * * * * * *
*
MOVE 950000 TO #NOTE-NUM                /* GET NEXT AVAILABLE NX RECORD
READ (1) NT-V BY ID-NUMBER FROM #NOTE-NUM
  MOVE ID-NUMBER TO #NOTE-NUM
END-READ
*
IF  (#NOTE-NUM LE 950000)
 OR (#NOTE-NUM GT 980000)
  MOVE 980000 TO #NOTE-NUM
END-IF
*
SUBTRACT 1 FROM #NOTE-NUM
*
IF #NUM-NOTES-STORED GT #MAX-NOTES
  ESCAPE ROUTINE
END-IF
*
IF #NUM-NOTES-STORED = #MAX-NOTES
  MOVE
   '* EXCEEDED MAX SCREENS - SEE PRINTED REPORT FOR ADDITIONAL INFO *'
                                               TO #SCR-LINES (22)
END-IF
ADD 1 TO #NUM-NOTES-STORED
*
RESET NT-V
MOVE #NOTE-NUM     TO NT-V.ID-NUMBER
MOVE #DATN         TO NT-V.DTS-DATN
MOVE #TIMN         TO NT-V.DTS-TIMN
MOVE #DATN         TO NT-V.NOTE-DATE (1)
MOVE 'EDS'         TO NT-V.NOTE-CLERK-ID (1)
MOVE 'LST'         TO NT-V.NOTE-CLERK-ID (2)
MOVE 'UNV'         TO NT-V.NOTE-CLERK-ID (3)
MOVE #PERSON-FOR   TO NT-V.NOTE-CLERK-ID (4)
*
MOVE 31 TO #I1       /* INDEX OF 18 CHAR NOTE
MOVE 1  TO #I2       /* INDEX OF SEGMENT WITHIN 18 CHAR NOTE
*
FOR #ISC 1 TO 22
  IF #I2 GT 3
    ADD 1 TO #I1
    MOVE 1 TO #I2
  END-IF
*
  MOVE #SCR-LINES (#ISC) TO #SPLIT-LINE
  MOVE #FST-60 TO NT-V.NOTE-TEXT (#ISC)
  MOVE #LST-18 TO NT-V.NOTE-18   (#I1, #I2)
*
  ADD 1 TO #I2
END-FOR
*
STORE NT-V
END TRANSACTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-NOTIFY-NOTE-RECORD
* * * * * * * * * * * * * * * * * * * * * *
*
MOVE 950000 TO #NOTE-NUM                /* GET NEXT AVAILABLE NX RECORD
READ (1) NT-V BY ID-NUMBER FROM #NOTE-NUM
  MOVE ID-NUMBER TO #NOTE-NUM
END-READ
*
IF  (#NOTE-NUM LE 950000)
 OR (#NOTE-NUM GT 980000)
  MOVE 980000 TO #NOTE-NUM
END-IF
*
RESET NT-V
SUBTRACT 1 FROM #NOTE-NUM
MOVE #NOTE-NUM     TO NT-V.ID-NUMBER
MOVE #DATN         TO NT-V.DTS-DATN
MOVE #TIMN         TO NT-V.DTS-TIMN
MOVE #DATN         TO NT-V.NOTE-DATE (1)
MOVE 'EDS'         TO NT-V.NOTE-CLERK-ID (1)
MOVE #PERSON-FOR   TO NT-V.NOTE-CLERK-ID (2)
MOVE 'UNV'         TO NT-V.NOTE-CLERK-ID (3)
*
COMPRESS 'LIST OF' #TITLE INTO NT-V.NOTE-TEXT (2)
MOVE 'IS AVAILABLE ON NX SCREEN' TO NT-V.NOTE-TEXT (3)
*
STORE NT-V
END TRANSACTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE DETAIL-PROCESS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ADD 1 TO #IUP
*
IF #IUP GT #NUM-UP
  WRITE (2) #FULL-LINE                  /* PRINT A LINE
  MOVE #FULL-LINE TO #SCR-LINES (#ISC)
  RESET #FULL-LINE
  MOVE 1 TO #IUP
  ADD 1 TO #ISC
*
  IF #ISC GT 22
    PERFORM BUILD-NOTE-RECORD
    RESET #SCR-LINES (2:22)
    MOVE 2 TO #ISC              /* TO LET HEADER REMAIN
    MOVE 1 TO #IUP
  END-IF
END-IF
*
* ####################################################################
MOVE ST-V.ID-NUMBER              TO #L-ID-NUMBER (#IUP)
MOVE ST-V.NAME                   TO #L-NAME       (#IUP)
MOVE ST-V.RANK    TO #L-RANK (#IUP)
MOVE ST-V.MILITARY-SERVICE       TO #L-MIL-ID     (#IUP)
MOVE EDITED ST-V.SSN (EM=999-99-999) TO #L-SSN    (#IUP)
* MOVE ST-V.MILITARY-STATUS        TO #L-MIL-STAT   (#IUP)
*
IF ST-V.MILITARY-SERVICE = MASK ('A')
  ADD 1 TO #ARMY
ELSE
  IF ST-V.MILITARY-SERVICE = MASK ('F')
    ADD 1 TO #AIRF
  ELSE
    ADD 1 TO #OTHER
  END-IF
END-IF
* ####################################################################
*
END-SUBROUTINE
*
END
