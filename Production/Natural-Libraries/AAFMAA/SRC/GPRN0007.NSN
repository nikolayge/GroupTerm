* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Write Transaction Log - generic program
* Can be called from any process
*
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* NGG    01312011       Initial Creation
********************************************************
*
DEFINE DATA
PARAMETER
1 #HEADER
  2 #ID-NUMBER         (N6)
  2 #NAME              (A) DYNAMIC
  2 #TR-DATE-LAST-UPDATE (N8)
  2 #CLERK-ID          (A3)
  2 #LOG-DESCRIPTION   (A) DYNAMIC
1 #OLD-DATA-TABLE   (32)
  2 #FIELD-DESCRIPTION (A10)
  2 #OLD-DATA          (A115)
1 #NEW-DATA-TABLE   (32)
  2 #NEW-DATA-TAG      (A3)
  2 #NEW-DATA          (A122)
1 #TR-ISN              (P8)
LOCAL
1 TR-V VIEW OF A-TRANSACTION-REGISTER
  2 TRANSREG
    3 ID-NUMBER        (N6.0)
    3 NAME             (A25)
    3 DATE-LAST-UPDATE (N8.0)
    3 TERMINAL-ID      (A8)
    3 CLERK-ID         (A3)
    3 TRANSACTION-DATN (P9.0)
    3 TRANSACTION-TIMN (P7.0)
  2 CHANGED-DATA-TABLE (1:32)
    3 OLD-DATA         (A125)
    3 NEW-DATA         (A125)
  2 PROCESS-IND        (A1)
LOCAL
1 #I                 (I2)
1 #FIRST-LINE        (A9)
1 #DATX              (D)
1 #DATN              (N8)
1 REDEFINE #DATN
  2 #DATE-A          (A8)
END-DEFINE
*
IF #TR-ISN > 0
  GET TR-V #TR-ISN
  FOR #I = 1 TO 32
COMPRESS FULL #FIELD-DESCRIPTION (#I) #NEW-DATA(#I) INTO TR-V.NEW-DATA (#I) LEAVING NO
  END-FOR
  UPDATE (0055)
ELSE
  TR-V.ID-NUMBER            := #ID-NUMBER
  TR-V.PROCESS-IND          := 'I'
  TR-V.NAME                 := #NAME
  TR-V.DATE-LAST-UPDATE     := #TR-DATE-LAST-UPDATE
  TR-V.CLERK-ID             := #CLERK-ID
  TR-V.TRANSACTION-DATN     := *DATN
  TR-V.TRANSACTION-TIMN     := *TIMN
  IF *DEVICE = 'BATCH'
    TR-V.TERMINAL-ID := 'BATCH'
  ELSE
    TR-V.TERMINAL-ID := *INIT-ID
  END-IF
*
  FOR #I = 1 TO 32
COMPRESS FULL #FIELD-DESCRIPTION (#I) #OLD-DATA(#I) INTO TR-V.OLD-DATA (#I)
      LEAVING NO
    IF #I = 1
COMPRESS #LOG-DESCRIPTION #FIELD-DESCRIPTION (1) INTO #FIRST-LINE LEAVING NO
      #FIELD-DESCRIPTION(1) := #FIRST-LINE
    END-IF
COMPRESS FULL #FIELD-DESCRIPTION (#I) #NEW-DATA (#I) INTO TR-V.NEW-DATA (#I) LEAVING NO
  END-FOR
*
  STL.
  STORE TR-V
  #TR-ISN := *ISN (STL.)
*
END-IF
*
END
