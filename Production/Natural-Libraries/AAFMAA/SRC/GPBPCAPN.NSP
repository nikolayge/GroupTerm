* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
**************************************************************************
* GPBPCAPN  (Clone of G4020PNS but modified to run in batch only)
* CAP Loan Collection REPORT
* RE 02/10/2012 Created
*
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE    TAG  REASON
* bz  06/29/2012 bz1  (OI-1340) bypass notes for AFA status = c
* RSE 07/25/2012 RE1  Change file structure
* JNT 09/27/2013 JT1  Address normalization 
* TMT 06/12/2014 TT1  Contact Normalization - Gender and DOB
* vxt 05/05/2015 vt1  /Ticket 3511/
**************************************************************************
DEFINE DATA
* * * * * * * * * * * * **
*
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING G4020LNS
LOCAL USING A-IFAFFL   /* PDA used with FUNCTION F-if-AFFL     /* DW23
LOCAL USING ADDA0001                   /* JT1
*
LOCAL
*
1 #MEMBER-NAME          (A42)         /* NG2  \/
1 #WIDOW-NAME           (A42)
*                                     /* NG2  /\
1 NT-V VIEW OF A-NOTES   /* DW18
  2 C*NOTE-TABLE
  2 NOTE-DATE     (N8/500)                     /* DW18
  2 REDEFINE NOTE-DATE
    3 NOTE-DATE-OCC   (500)                     /* DW18
      4 NOTE-DATE-8   (N8)
      4 REDEFINE NOTE-DATE-8
        5 ND-YYYY     (A4)
        5 ND-MM       (A2)
        5 ND-DD       (A2)
  2 NOTE-LINE-NUM        (P1/500)             /* DW18
  2 NOTE-CLERK-ID        (A3/500)
  2 USER-COMPANY         (A1/500)                           /* DW20
  2 NOTE-TEXT (A60/500)                       /* DW18
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-OCC     (500)
      4 NOTE-TEXT-60     (A60)
      4 REDEFINE NOTE-TEXT-60
        5 NT-PAYS-LBL      (A14)
        5 NT-PAYS-CN       (A6)
        5 NT-PAYS-REST     (A40)
      4 REDEFINE NOTE-TEXT-60
        5 NT-POL-ISS       (A9)
        5 NT-POL-SPACE     (A1)
        5 NT-POL-ISS-DATE  (A10)
        5 REDEFINE NT-POL-ISS-DATE
          6 NT-POL-ISS-DATE-MM   (N2)
          6 NT-POL-ISS-DATE-1    (A1)
          6 NT-POL-ISS-DATE-DD   (N2)
          6 NT-POL-ISS-DATE-2    (A1)
          6 NT-POL-ISS-DATE-YYYY (N4)
        5 NT-POL-SPACE-2   (A1)
        5 NT-POL-CSV       (A12)
        5 NT-POL-SPACE-3   (A1)
        5 NT-POL-CSV-DATE  (A10)
        5 REDEFINE NT-POL-CSV-DATE
          6 NT-POL-CSV-DATE-MM   (A2)
          6 NT-POL-CSV-DATE-1    (A1)
          6 NT-POL-CSV-DATE-DD   (A2)
          6 NT-POL-CSV-DATE-2    (A1)
          6 NT-POL-CSV-DATE-YYYY (A4)
      4 REDEFINE NOTE-TEXT-60
        5 NT-TU-FILL1       (A21)
        5 NT-TRADE-UP       (A13)
        5 NT-TU-FILL2       (A26)
*
* 1 NN-V VIEW OF A-NOTES    /* note file used for pending beneficiaries
*  2 ID-NUMBER          (N6)    /* let D = bene designation (1-4)
*   2 NOTE-DATE          (N8/60) /* LET P/A = PRINCIPAL/ALTERNATE DESING
*   2 REDEFINE NOTE-DATE         /* let L = linked policies
*     3 BENE-DATES-6-POLS (6)
*     4 BENE-ISS-DT       (N6/7)   /* (D, L)
*       4 BENE-SPLIT-IND    (N1/7)   /* (D, L)
*       4 BENE-LST-UPD      (N8)     /* (D)
*       4 BENE-STRT-PEND    (N8)     /* (D)
*       4 BENE-FOL-UP-IND   (N1)     /* (D)
*       4 BENE-OWN-TRNSF-DT (N8)     /* (D)
*       4 BENE-SUC-OWN      (N1)     /* (d)
*      4 BENE-NEW-LAYOUT   (N1)     /* (d)
*      4 BENE-FIL1         (A4)
*   2 NOTE-CLERK-ID       (A3/60)
*   2 REDEFINE NOTE-CLERK-ID
*     3 BENE-MISC-6-POLS  (6)
*       4 BENE-STATUS      (A1)     /* (D)
*       4 BENE-RESTRICT    (A1)     /* (D)
*       4 BENE-MISC-PRIM-SEC (2)
*         5 BENE-TYPE        (A1)    /* (D, P/A)
*         5 BENE-SHARE-TYP   (A1)    /* (D, P/A)
*         5 BENE-MISC-FIL-2  (A12)
*   2 NOTE-TEXT          (A60/60)
*   2 REDEFINE NOTE-TEXT
*     3 BENE-TEXTS-6-POLS (6)
*       4 BENE-TEXT-PRIM-SEC (2)
*         5 BENE-TEXT-OCC    (5)
*           6 BENE-TEXT        (A60)   /* (D, P/A, text-line)
*           6 REDEFINE BENE-TEXT
*             7 BENE-TEXT-1ST-25   (A25)
*             7 BENE-TEXT-LST-35   (A35)
*             7 REDEFINE BENE-TEXT-LST-35
*               8 BENE-TEXT-LST-35-6  (A6)
*
1 LNK-V VIEW OF A-STATUS
  2 NAME
  2 INTRO-FLAG
*
1 PAYS-V VIEW OF A-STATUS
  2 ALOT-AMT
*
1 AT-V VIEW OF A-APPL-TRACKING                             /* YK1
  2 CAP-IND            (A1)
  2 ID-NUMBER          (N6)
  2 MEMBER-CN          (A6)
  2 CONTACT-CODE       (A1)
*
**1 SVC-MEMBER-V VIEW OF A-SVC-MEMBER-PLAN                    RE1
1 SVC-MEMBER-V VIEW OF A-SVC-MEMBER                        /* RE1   
  2 SVC-ISSUE-DATE
  2 REDEFINE SVC-ISSUE-DATE
    3 #SVC-YYYYMM    (N6)
  2 SVC-PLAN
  2 SVC-TERMINATION-DATE
  2 SVC-STATUS                       /* bz1  
*  2 C*SVC-ACTIVE-SEGMENTS                                 /* RE1
*  2 SVC-ACTIVE-SEGMENTS(20)                               /* RE1
*    3 SVC-ISSUE-DATE
*    3 REDEFINE SVC-ISSUE-DATE
*      4 #SVC-YYYYMM    (N6)
*    3 SVC-PLAN
*    3 SVC-TERMINATION-DATE
*
1 #SEG-X     (I2)             /* DW13
1 #HAS-CONTESTIBILITY  (L)
1 #NO-LONGER-CONTESTABLE   (L)    /* DW14
1 #CONTESTABLE-BUT-PAID    (L)     /* DW17
1 #SA-CARD   (A4)                                          /* YK1
1 #ID-NUMBER (A6)                                          /* YK1
1 #AMT-9     (A9)
1 #TEXT-40        (A40)
1 #LINK-TYPE      (A40)
1 #NAME           (A25)
1 #RANK           (A3)
1 #SCN-MSG        (A60)
1 #SCR-CV         (C/21)
1 #SCR-WIDOW-LBL  (A7)
1 #SCR-WIDOW-NAME (A25)
*
1 #CC-MODE                 (L)     /* DW2
1 #NO-NOTES                (L)
1 #NO-PSEUDO-NOTES         (L)
1 #ALL-NOTES-PROCESSED     (L)
1 #FIRST-SCN               (L)
1 #CRITICAL-NOTE           (L)
1 #FIRST-NON-CRITICAL-NOTE (L)
1 #SVC-ONLY                (L)
* 1 #KATRINA-ZIP             (L)  /* DW7    /* vt1
1 #USE-2ND-ADDR-FOR-BILLING (L)  /* DW9
*
1 #I1         (I2)
1 #I2         (I2)
1 #INDEXT     (I2)
1 #INDEXN     (I2)
1 #INDEX1     (I2)
1 #INDEXB     (I2)
1 #ISCR       (I2)
*
1 #LAST-NAME    (A25)
1 #FIRST-MID    (A25)
1 #SUFFIX       (A25)
1 #SUP-MESSAGE  (A35)
1 #DEL          (A1)
1 #HOLD-NAME    (A50)
1 #LINE-NUM     (N1)
1 #NUM-IN-MSSG  (N1)
1 #TYPE         (A11)
1 #LINKED       (A70)
*
1 #DOD-DISP-GR
  2 #DOD-DISP-MM   (N2)
  2 #DOD-SL1       (A1) INIT <'/'>
  2 #DOD-DISP-DD   (N2)
  2 #DOD-SL2       (A1) INIT <'/'>
  2 #DOD-DISP-YYYY (N4)
1 REDEFINE #DOD-DISP-GR
  2 #DOD-DISP       (A10)
*
1 #DATE           (N8)
1 #DATN           (N8)
1 REDEFINE #DATN
  2 #DATN-YYYY (A4)
  2 #DATN-MM   (A2)
  2 #DATN-DD   (A2)
*
1 #DATE-DISP (A10)
1 #AMT-DISP  (A10)
1 #YYYYMMDD (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY  (N4)
    3 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
*
1 #NT-ID      (N6)
1 #RSTR       (L)
1 #1ST-ACT-POL (I2)
1 #TRADE-UP-DATE (A10)
1 #WORK-ISSUE-DATE-TU (N8)
1 REDEFINE #WORK-ISSUE-DATE-TU
  2 #WORK-DATE-TU-YYYY (N4)
  2 #WORK-DATE-TU-MM (N2)
  2 #WORK-DATE-TU-DD (N2)
1 #WORK-ISSUE-DATE (N8)
1 REDEFINE #WORK-ISSUE-DATE
  2 #WORK-ISS-DATE-YYYY (N4)
  2 #WORK-ISS-DATE-MM (N2)
  2 #WORK-ISS-DATE-DD (N2)
1 #DO-NOT-WAIVE (L)
1 #IS-A-TRADE-UP (L)
1 #LTC-ISSUE-DATE
  2 #LTC-ISS-DATE-MM   (A2)
  2 #LTC-SLASH-1       (A1) INIT <'/'>
  2 #LTC-ISS-DATE-DD   (A2)
  2 #LTC-SLASH-2       (A1) INIT <'/'>
  2 #LTC-ISS-DATE-YYYY (A4)
1 REDEFINE #LTC-ISSUE-DATE
  2 #LTC-ISSUE-DATE-DISP (A10)
*
1 #WORK-TIME           (N7)                                       /* PM4
1 REDEFINE #WORK-TIME
  2 #WORK-TIME-HH       (A2)
  2 #WORK-TIME-MM       (A2)
  2 #WORK-TIME-SS       (A2)
  2 #WORK-TIME-T        (A1)
1 #DISPLAY-DATE         (A10)                                     /* PM4
1 #DISPLAY-TIME         (A8)                                      /* PM4
1 #DATE-X               (A10)
1 #PROJECTED-DATE       (N8)                                      /* PM16
1 REDEFINE #PROJECTED-DATE
  2 #PROJECTED-YYYY      (N4)
  2 REDEFINE #PROJECTED-YYYY
    3 #PROJECTED-YYYY-A   (A4)
  2 #PROJECTED-MM        (N2)
  2 REDEFINE #PROJECTED-MM
    3 #PROJECTED-MM-A     (A2)
  2 #PROJECTED-DD        (N2)
*
1 #BYPASS-IAS      (L)
1 #BYPASS-SAS      (L)                                   /* DW21
1 #COMPANY-MSG  (A10)                      /* DW24
1 #I                    (N2)       /* RE2
1 #J                    (N2)       /* RE2
1 DATE-OF-BIRTH         (N8.0)     /* TT1
END-DEFINE
**************************************************************************
INCLUDE GONERCXX
*
FORMAT (1) LS=121 PS=250
MOVE 'ALL Notes' TO #COMPANY-MSG                                /* DW24
MOVE *DATN TO #DATN
COMPRESS #DATN-MM '/' #DATN-DD '/' #DATN-YYYY INTO #DISPLAY-DATE  /* PM4
  LEAVING NO                                                      /* PM4
*                                                                 /* PM4
MOVE *TIMN TO #WORK-TIME                                          /* PM4
COMPRESS #WORK-TIME-HH ':' #WORK-TIME-MM ':' #WORK-TIME-SS        /* PM4
  INTO #DISPLAY-TIME LEAVING NO                                   /* PM4
*
INPUT #CGA-ISN
GET ST-V #CGA-ISN
CALLNAT 'G4020SNS' ST-V.ID-NUMBER                                 /* NG2
  #MEMBER-NAME #WIDOW-NAME
CALLNAT 'PLAN-AFA' ST-V.ID-NUMBER F-IF-AFFL-DATA /* NG1
*
FOR #I1 1 TO 9
  IF (PLAN (#I1) = 'SO' OR = 'FSO') AND (STATUS (#I1) = 'D')
    MOVE TRUE TO #SVC-ONLY
    ESCAPE BOTTOM
  END-IF
END-FOR
*
RESET #1ST-ACT-POL
FOR #I1 1 TO 9
  IF STATUS (#I1) NE 'D'
    ESCAPE TOP
  END-IF
*
  MOVE #I1 TO #1ST-ACT-POL
  ESCAPE BOTTOM
END-FOR
*
  RESET #PDA-ADDRESS                                                            /* JT1 start
  #ADD-TYPE        := 'P'                                                       
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID                                     
  CALLNAT 'ADDN0001' #PDA-ADDRESS                                               
*
IF  (ACCESS-AUTH-CODE = 'Y' OR = 'S')
*    OR (ADDRESS-CHG-CODE = 'U' OR = 'V')    
*    OR (ADDRESS-1 = ' ')
*    OR (ZIP-CODE = 0)
    OR  (ADDRESS-CHANGE-CODE (< ST-V.MEMBER-CONTACT-ID >) = 'U' OR = 'V')         
    OR (#ADD-LINE-1 = ' ')
    OR (#ADD-ZIP-CODE = 0)                                                      /* JT1 end
    OR (DATE-OF-DEATH NE 0)
    OR (RESTRICT-MBR-SW = 'Y')
    OR (INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q')
    OR #SVC-ONLY
  RESET #NO-PSEUDO-NOTES
ELSE
  MOVE TRUE TO #NO-PSEUDO-NOTES
END-IF
*
MOVE ',' TO #DEL
*
MOVE NAME TO #NAME
MOVE RANK TO #RANK
*
SEPARATE #NAME INTO #LAST-NAME #FIRST-MID #SUFFIX WITH DELIMITER #DEL
*
COMPRESS #RANK #FIRST-MID INTO #HOLD-NAME LEAVING NO SPACE
COMPRESS #HOLD-NAME #LAST-NAME #SUFFIX INTO #SCN-NAME
*
FIND (1) NT-V WITH ID-NUMBER = ST-V.ID-NUMBER   
  IF NO RECORDS FOUND
    MOVE TRUE TO #NO-NOTES
  END-NOREC
*
  RESET #IS-A-TRADE-UP
  FOR #I1 1 TO NT-V.C*NOTE-TABLE                            /* DW18
    IF NT-V.NT-TRADE-UP (#I1) = 'IS A TRADE-UP'
      MOVE TRUE TO #IS-A-TRADE-UP
      ESCAPE BOTTOM
    END-IF
*
    IF NT-V.NOTE-TEXT (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  MOVE TRUE TO #DO-NOT-WAIVE
  IF #IS-A-TRADE-UP
    IF #CGA-ORIG-COMMAND = 'ST'
      MOVE 'T' TO #CGA-MF-SCR-COMMAND
    END-IF
*
    IF TRAD-UP-SENT-DT LT 20031201
      MOVE NT-POL-ISS-DATE-YYYY (#I1) TO #WORK-DATE-TU-YYYY
      MOVE NT-POL-ISS-DATE-MM   (#I1) TO #WORK-DATE-TU-MM
      MOVE NT-POL-ISS-DATE-DD   (#I1) TO #WORK-DATE-TU-DD
      IF #DATN - #WORK-ISSUE-DATE-TU < 20000  /* New Pol < 2 years old
        RESET #DO-NOT-WAIVE
        MOVE EDITED #WORK-DATE-TU-MM (EM=99) TO #LTC-ISS-DATE-MM
        MOVE EDITED #WORK-DATE-TU-DD (EM=99) TO #LTC-ISS-DATE-DD
        MOVE EDITED #WORK-DATE-TU-YYYY (EM=9999) TO #LTC-ISS-DATE-YYYY
        FOR #I2 1 TO NT-V.C*NOTE-TABLE                            /* DW18
          IF NT-V.NOTE-DATE (#I2) = NT-V.NOTE-DATE (#I1)
              AND NT-V.NT-POL-ISS (#I2) = '* POL ISS'
              AND NT-V.NT-POL-CSV (#I2) = "CSV EXC'D ON"
            MOVE NT-POL-ISS-DATE-YYYY (#I2) TO #WORK-ISS-DATE-YYYY
            MOVE NT-POL-ISS-DATE-MM   (#I2) TO #WORK-ISS-DATE-MM
            MOVE NT-POL-ISS-DATE-DD   (#I2) TO #WORK-ISS-DATE-DD
            IF #WORK-ISSUE-DATE-TU - #WORK-ISSUE-DATE < 20000 /* Orig
              MOVE TRUE TO #DO-NOT-WAIVE                   /* pol < 2
              ESCAPE BOTTOM                                /* yrs old
            END-IF                                         /* a tu time
          END-IF
        END-FOR
      END-IF
    END-IF
  END-IF
*
  MOVE NT-V.C*NOTE-TABLE TO #INDEXT  /*LAST FILLED NOTE LINE
  MOVE 1 TO #INDEXN                          /*START OF NOTES
  MOVE TRUE TO #FIRST-SCN
*
  REPEAT UNTIL #ALL-NOTES-PROCESSED
*
    RESET #SCN-UPDATE-ID   (1:21)
    RESET #SCN-UPDATE-TEXT (1:21)
    RESET #SCN-UPDATE-DATE (1:21)
    RESET #SCN-UPDATE-USER-COMPANY (1:21)     /* DW21
    RESET #SCR-CV          (1:21)
    PERFORM FILL-SCREEN
*
    IF #ALL-NOTES-PROCESSED
      IF #FIRST-SCN
        IF #CGA-ORIG-COMMAND = 'ST' OR = 'CO' OR = 'BD'
          COMPRESS 'Press ENTER for "' #CGA-ORIG-COMMAND
            '"' INTO #SCN-MSG LEAVING NO
        ELSE
          IF   #NO-NOTES
              AND #NO-PSEUDO-NOTES
            MOVE 'NO NOTES EXIST' TO #SCN-MSG
          ELSE
            RESET #SCN-MSG
          END-IF
        END-IF
      ELSE
        IF #CGA-ORIG-COMMAND = 'ST' OR = 'CO' OR = 'BD'
          COMPRESS 'PRESS ENTER TO RETURN TO THE "' #CGA-ORIG-COMMAND
            '" SCREEN' INTO #SCN-MSG LEAVING NO
        ELSE
          MOVE 'LAST NOTE SCREEN' TO #SCN-MSG
        END-IF
      END-IF
    ELSE
      MOVE '*' TO #CGA-SCR-COMMAND
      MOVE 'MORE NOTES FOLLOW' TO #SCN-MSG
    END-IF
*
    RESET #FIRST-SCN
    IF   (#CGA-ORIG-COMMAND = 'ST' OR = 'CO' OR = 'BD')
        AND (#ISCR LE 1)
      IGNORE
    ELSE
      IF #SAS-SUSPENDED                             /* DW23
        COMPRESS 'Suspended due to lack of payment' #SCN-MSG
          INTO #SCN-MSG
      END-IF
*
      WRITE (1) NOTITLE USING FORM 'GPBFCAPN'
*
    END-IF
  END-REPEAT
END-FIND
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE FILL-SCREEN
* * * * * * * * * * * * * * *
*
MOVE #INDEXN TO #INDEXB            /* START OF TABLE TO FILL SCRN
MOVE #INDEXN TO #INDEX1            /* START OF FIRST LINE OF FIRST MSG
*
FOR #ISCR 1 TO 21
  IF   #FIRST-SCN
      AND (#ISCR = 1)
    PERFORM BUILD-PSEUDO-NOTES
  END-IF
*
  IF #INDEXN GT NT-V.C*NOTE-TABLE
    MOVE TRUE TO #ALL-NOTES-PROCESSED
    ESCAPE ROUTINE
  END-IF
*
  IF ((NT-V.USER-COMPANY (#INDEXN) = 'S')  /*Bypass SAS notes is not a SAS user DW21
      AND #BYPASS-SAS)
      OR ((NT-V.USER-COMPANY (#INDEXN) = 'I') /*Bypass IAS notes is not a IAS user
      AND #BYPASS-IAS)
*    WRITE 'bypass' NT-V.NOTE-TEXT (#INDEXN)
    SUBTRACT 1 FROM #ISCR
    ADD 1 TO #INDEXN
    ESCAPE TOP
  END-IF                                                           /* END DW21
*
  IF NT-V.NOTE-LINE-NUM (#INDEXN) = 1
    RESET #NUM-IN-MSSG                  /* DETERMINE NUMBER OF LINES
    FOR #I1 #INDEXN TO NT-V.C*NOTE-TABLE       /* IN THIS NOTE    /* DW18
      ADD 1 TO #NUM-IN-MSSG
      IF  (#I1 + 1) GT NT-V.C*NOTE-TABLE                    /* DW18
          OR (NT-V.NOTE-LINE-NUM (#I1 + 1) = 1)
          OR (NT-V.NOTE-TEXT     (#I1 + 1) = ' ')
        ESCAPE BOTTOM
      END-IF
    END-FOR
*
    IF   (NT-V.NOTE-TEXT (#INDEXN) NE MASK ('*'))  /* FIRST NON-
        AND (#CRITICAL-NOTE)                          /* CRITICAL NOTE
      MOVE TRUE TO #FIRST-NON-CRITICAL-NOTE
      ADD 1 TO #NUM-IN-MSSG
    ELSE
      RESET #FIRST-NON-CRITICAL-NOTE
    END-IF
*
    IF #ISCR + #NUM-IN-MSSG GT 21   /* WE"RE DONE IF NOT ENOUGH ROOM
      ESCAPE BOTTOM
    END-IF
*
    IF NT-V.NOTE-TEXT (#INDEXN) = MASK ('*')   /* IMPORTANT NOTE
      MOVE TRUE TO #CRITICAL-NOTE
    ELSE
      RESET #CRITICAL-NOTE
    END-IF
  END-IF
*
  IF   (#CGA-ORIG-COMMAND = 'CO') /* only show critical note in 'CO'
      AND (NT-V.NOTE-TEXT (#INDEXN) = MASK ('*@'))  /* pending action
    SUBTRACT 1 FROM #ISCR
    ADD 1 TO #INDEXN
    ESCAPE TOP
  END-IF
*
  IF #FIRST-NON-CRITICAL-NOTE
    RESET #FIRST-NON-CRITICAL-NOTE
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    MOVE ALL '-' TO #SCN-UPDATE-DATE (#ISCR)
    MOVE ALL '-' TO #SCN-UPDATE-ID   (#ISCR)
    MOVE ALL '-' TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE ALL '-' TO #SCN-UPDATE-USER-COMPANY (#ISCR)      /* DW20
    ADD 1 TO #ISCR
  END-IF
*
  IF NT-V.NOTE-LINE-NUM(#INDEXN) = 1
    MOVE NT-V.NOTE-CLERK-ID(#INDEXN) TO #SCN-UPDATE-ID(#ISCR)
    MOVE NT-V.USER-COMPANY (#INDEXN) TO #SCN-UPDATE-USER-COMPANY(#ISCR) /* DW20
    COMPRESS ND-MM(#INDEXN) '/' ND-DD(#INDEXN) '/' ND-YYYY(#INDEXN)
      INTO #SCN-UPDATE-DATE(#ISCR) LEAVING NO SPACE
  END-IF
*
  IF NT-V.NOTE-TEXT (#INDEXN) = MASK ('*#ALOT:PAYS CN')
    FIND PAYS-V WITH MEMBER-KEY = NT-V.NT-PAYS-CN (#INDEXN)
      MOVE EDITED PAYS-V.ALOT-AMT (EM=ZZ,ZZZ.99) TO #AMT-9
      MOVE LEFT #AMT-9 TO #AMT-9
      COMPRESS NT-V.NOTE-TEXT (#INDEXN) 'Alot Amt=' #AMT-9
        INTO NT-V.NOTE-TEXT (#INDEXN)
    END-FIND
  END-IF
*
  MOVE NT-V.NOTE-TEXT(#INDEXN) TO #SCN-UPDATE-TEXT(#ISCR)
*
  IF #CRITICAL-NOTE
    MOVE (AD=I) TO #SCR-CV (#ISCR)
  END-IF
*
  ADD 1 TO #INDEXN
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE BUILD-PSEUDO-NOTES
* * * * * * * * * * * * * * * * * * *
*
IF DATE-OF-DEATH NE 0                 /* DW13
  RESET #SEG-X
  CALLNAT 'GCONTSXX' ST-V.ID-NUMBER #SEG-X #HAS-CONTESTIBILITY
    #NO-LONGER-CONTESTABLE #CONTESTABLE-BUT-PAID             /* DW14 DW17
*
  IF #HAS-CONTESTIBILITY
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE '*Has a Contestable Death Claim'   /* DW13
      TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
END-IF
*
* CALLNAT 'GKATRSXX' ZIP-CODE #KATRINA-ZIP    /* DW7         /* JT1
* CALLNAT 'GKATRSXX' #ADD-ZIP-CODE #KATRINA-ZIP                /* JT1   /* vt1 \/
*
* IF #KATRINA-ZIP                             /* DW7
*   MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
*   MOVE TRUE TO #CRITICAL-NOTE
*   COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
*     INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
*   MOVE '*Member resides in Katrina Disaster Area'    /* DW7
*     TO #SCN-UPDATE-TEXT (#ISCR)
*   MOVE (AD=I) TO #SCR-CV (#ISCR)
*   ADD 1 TO #ISCR
* END-IF                                                                /* vt1 /\
*
CALLNAT 'GGRANSXX' USING ST-V.ID-NUMBER ST-V.PROCESS-IND
  ST-V.INTRO-FLAG ST-V.MILITARY-SERVICE ST-V.DATE-OF-DEATH
  ST-V.NUMBER-MEMBERSHIPS ST-V.PLAN (1) #TEXT-40
*
MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
MOVE TRUE TO #CRITICAL-NOTE
COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
  INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
*
MOVE #TEXT-40 TO #SCN-UPDATE-TEXT (#ISCR)
*
MOVE *DATN TO #DATE
IF DATE-OF-DEATH NE 0
  MOVE DATE-OF-DEATH TO #DATE
  MOVE 'WIDOW' TO #SCR-WIDOW-LBL
  SEPARATE NAME-SP INTO #LAST-NAME #FIRST-MID #SUFFIX
    WITH DELIMITER #DEL
  COMPRESS #FIRST-MID #LAST-NAME #SUFFIX INTO #SCR-WIDOW-NAME
*
  MOVE DOD-MM TO #DOD-DISP-MM
  MOVE DOD-DD TO #DOD-DISP-DD
  MOVE DOD-YYYY TO #DOD-DISP-YYYY
  COMPRESS  #SCN-UPDATE-TEXT (#ISCR) #DOD-DISP
    INTO #SCN-UPDATE-TEXT (#ISCR)
*
* IF PLAN (1) = '2SW' OR = '3SW' OR = '2DW' OR = '3DW'  /* DW21
*    COMPRESS #SCN-UPDATE-TEXT (#ISCR) '- WIDOW CLIENT'
*      INTO #SCN-UPDATE-TEXT (#ISCR)
*  END-IF
ELSE
  RESET #WIDOW-NAME                                     /* NG2
END-IF
*
IF   (#1ST-ACT-POL) NE 0
    AND (NOT PROCESS-IND = 'G' OR = 'O'
    OR = 'S')               /* DW21
    AND ((ISSUE-DATE (#1ST-ACT-POL) LT 19970101)
    OR   (MONTH-ALLOC-AMT (#1ST-ACT-POL) = 999.99))
  COMPRESS #SCN-UPDATE-TEXT (#ISCR)
    '-no svcs but dues imbedded in prm'
    INTO #SCN-UPDATE-TEXT (#ISCR)
END-IF
*
IF MBR-SINCE NE 0
  IF #SCN-UPDATE-TEXT (#ISCR) = SCAN 'Pending Confirmation'  /* RE3 START
    ADD 1 TO #ISCR
    MOVE TRUE TO #CRITICAL-NOTE
  END-IF                                                     /* RE3 END
  MOVE MBR-SINCE TO #YYYYMM
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '-MBR Since' #MM
    INTO #SCN-UPDATE-TEXT (#ISCR)
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '/' #YYYY
    INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
END-IF
*
IF DT-MBR-CARD-SINCE NE 0
  MOVE DT-MBR-CARD-SINCE TO #YYYYMMDD
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '-MBR Card Sent' #MM
    INTO #SCN-UPDATE-TEXT (#ISCR)
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '/' #DD '/' #YYYY
    INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
END-IF
*
MOVE (AD=I) TO #SCR-CV (#ISCR)
*
PERFORM MESSAGE-IF-AFA-CLIENT  /* RE2
*
MOVE (AD=I) TO #SCR-CV (#ISCR) /* RE2
ADD 1 TO #ISCR
*
IF   (ALOT-CHG-AUTH NE ' ')                    /* DW3
    AND (ALOT-CHG-AUTH NE 'N')                 /* DW3
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  IF ALOT-CHG-AUTH = 'S'
    IF ALOT-CHG-DATE = 0
      MOVE '*Allotment will stop as soon as possible'
        TO #SCN-UPDATE-TEXT (#ISCR)
    ELSE
      MOVE ALOT-CHG-DATE TO #YYYYMMDD
      COMPRESS #MM-A #YYYY-A INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
      COMPRESS '*Allotment will stop on' #SCN-UPDATE-TEXT (#ISCR)
        INTO #SCN-UPDATE-TEXT (#ISCR)
    END-IF
    IF   ALOT-CHG-AUTH-DATE NE 0               /* YK2
      MOVE ALOT-CHG-AUTH-DATE TO #YYYYMMDD   /* YK2
      COMPRESS #MM-A #DD-A #YYYY-A INTO      /* YK2
        #SCN-UPDATE-DATE (#ISCR) WITH '/'     /* YK2
    END-IF                                     /* YK2
  ELSE
    IF ALOT-CHG-AUTH = 'B'              /* DW11
      MOVE ALOT-CHG-DATE TO #YYYYMMDD
      COMPRESS #MM-A #YYYY-A INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
      COMPRESS '*Allotment Bypass for DFAS until' #SCN-UPDATE-TEXT (#ISCR)
        INTO #SCN-UPDATE-TEXT (#ISCR)
      MOVE ALOT-CHG-AUTH-DATE TO #YYYYMMDD    /* YK2
      COMPRESS #MM-A #DD-A #YYYY-A INTO
        #SCN-UPDATE-DATE (#ISCR) WITH '/'
    ELSE
      IF ALOT-CHG-AUTH = 'A'              /* DW12
        COMPRESS '*Allotment Authorization Initiated by AAFMAA'
          INTO #SCN-UPDATE-TEXT (#ISCR)
        MOVE ALOT-CHG-AUTH-DATE TO #YYYYMMDD    /* YK2
        COMPRESS #MM-A #DD-A #YYYY-A INTO
          #SCN-UPDATE-DATE (#ISCR) WITH '/'
      ELSE
        MOVE '*Member has given authority to change allotment'
          TO #SCN-UPDATE-TEXT (#ISCR)
        IF   ALOT-CHG-AUTH-DATE NE 0               /* YK2
          MOVE ALOT-CHG-AUTH-DATE TO #YYYYMMDD     /* YK2
          COMPRESS #MM-A #DD-A #YYYY-A INTO         /* YK2
            #SCN-UPDATE-DATE (#ISCR) WITH '/'     /* YK2
        END-IF                                     /* YK2
      END-IF
    END-IF
  END-IF
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
IF TRAD-UP-SENT-DT NE 0
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  COMPRESS TRADE-UP-MM '/' TRADE-UP-DD '/' TRADE-UP-YYYY
    INTO #TRADE-UP-DATE LEAVING NO
  COMPRESS '*Trade-up letter created on' #TRADE-UP-DATE
    INTO #SCN-UPDATE-TEXT (#ISCR)
*
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
IF INTRO-FLAG = 'S' OR = 'K' OR = 'G' OR = 'F'
  DECIDE ON FIRST VALUE INTRO-FLAG
    VALUE 'S'
      IF ST-V.STATUS-CODE-SP = 'F'          /* DW15
        COMPRESS '*Ex-Spouse' TO #TYPE
      ELSE
        MOVE '*Spouse' TO #TYPE
      END-IF
*
    VALUE 'K' MOVE '*Child'  TO #TYPE
    VALUE 'G' MOVE '*Grandchild' TO #TYPE
    VALUE 'F' IF (PLAN (1) = 'FSO') AND (STATUS (1) = 'D')
        MOVE '*Free "SO"' TO #TYPE
      ELSE
        MOVE '*Orig Free "SO"' TO #TYPE
      END-IF
    NONE IGNORE
  END-DECIDE
*
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  COMPRESS #TYPE 'policy linked to CN' LINKED-CN (1)
    INTO #SCN-UPDATE-TEXT (#ISCR)
*
  FIND (1) LNK-V WITH MEMBER-KEY = LINKED-CN-A (1)
    COMPRESS #SCN-UPDATE-TEXT (#ISCR) LNK-V.NAME
      INTO #SCN-UPDATE-TEXT (#ISCR)
*
  END-FIND
*
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
IF NOT INTRO-FLAG = 'S' OR = 'K' OR = 'G'
  IF #SVC-ONLY AND (INTRO-FLAG NE 'F')
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE '*Service Only Member'  TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  RESET #LINKED
  FOR #I1 1 TO 4
    IF (INTRO-FLAG = 'F') AND (#I1 = 1) /* bypass fso link
      ESCAPE TOP
    END-IF
*
    IF LINKED-CN (#I1) NE 0
      COMPRESS #LINKED LINKED-CN (#I1) INTO #LINKED
      PERFORM SET-LINK-TYPE
    END-IF
  END-FOR
*
  IF #LINKED NE ' '
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Linked to CN' #LINKED INTO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  RESET #LINKED
  FOR #I1 5 TO 9
    IF LINKED-CN (#I1) NE 0
      COMPRESS #LINKED LINKED-CN (#I1) INTO #LINKED
      PERFORM SET-LINK-TYPE
*
    END-IF
  END-FOR
*
  IF #LINKED NE ' '
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*and' #LINKED INTO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  RESET #LINKED
  FOR #I1 10 TO 14
    IF LINKED-CN (#I1) NE 0
      COMPRESS #LINKED LINKED-CN (#I1) INTO #LINKED
      PERFORM SET-LINK-TYPE
    END-IF
  END-FOR
*
  IF #LINKED NE ' '
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*and' #LINKED INTO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  RESET #LINKED
  FOR #I1 15 TO 19
    IF LINKED-CN (#I1) NE 0
      COMPRESS #LINKED LINKED-CN (#I1) INTO #LINKED
      PERFORM SET-LINK-TYPE
    END-IF
  END-FOR
*
  IF #LINKED NE ' '
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*and' #LINKED INTO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
END-IF
*
RESET DATE-OF-BIRTH                             /* TT1
DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<MEMBER-CONTACT-ID>)
*
FOR #I1 1 TO 9
  IF (PLAN (#I1) = 'LT')
      AND (STATUS (#I1) = 'D')
      AND ((SMOKER-FLAG(#I1) = 'N') AND (DATE-OF-BIRTH LE #DATE - 500000)
      OR (SMOKER-FLAG(#I1) = 'S') AND (DATE-OF-BIRTH LE #DATE - 400000))
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE '*Level Term Coverage has been lowered due to age'
      TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
    ESCAPE BOTTOM
  END-IF
*
  MOVE *DATN TO #YYYYMMDD
  IF   (PLAN (#I1) = 'RT')
      AND (STATUS (#I1) = 'D')
*
      AND ((PAID-UP-DATE-YYYYMM (#I1)  LT #YYYYMM)
      OR ((PAID-UP-DATE-YYYYMM (#I1) = #YYYYMM)
      AND (#DD GE ISSUE-DATE-DD (#I1))))
      AND (ISSUE-AGE (#I1) GT '65')
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE '*5-Yr Term Coverage has been expired due to age'
      TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
    ESCAPE BOTTOM
  END-IF
END-FOR
*
* IF ST-V.ADDRESS-CHG-CODE = 'U' OR = 'V'                                /* JT1
IF ADDRESS-CHANGE-CODE (< ST-V.MEMBER-CONTACT-ID >) = 'U' OR = 'V'       /* JT1
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*UNDELIVERABLE ADDRESS' TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
ELSE
*  IF ST-V.ADDRESS-1 = ' '                                               /* JT1
  IF #ADD-LINE-1 = ' '                                                   /* JT1
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE '*NO ADDRESS' TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  ELSE
*    IF ST-V.ZIP-CODE = 0                                                /* JT1
    IF #ADD-ZIP-CODE = 0                                                 /* JT1
      MOVE TRUE TO #CRITICAL-NOTE
      MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
      COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
        INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
      MOVE '*FOREIGN ADDRESS' TO #SCN-UPDATE-TEXT (#ISCR)
      MOVE (AD=I) TO #SCR-CV (#ISCR)
      ADD 1 TO #ISCR
    END-IF
  END-IF
END-IF
*
IF ST-V.ACCESS-AUTH-CODE = 'S'
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*SPOUSE HAS ACCESS TO FILE' TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
ELSE
  IF ST-V.ACCESS-AUTH-CODE = 'Y'
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
*   MOVE '*OK ACCESS TO FILE - SEE "DD" SCREEN'        /* DW10
    MOVE '*OK ACCESS TO FILE - View DAL for details'   /* DW10
      TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
END-IF
*
IF ST-V.RESTRICT-MBR-SW = 'Y'
  MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*SPECIAL CATEGORY MEMBER'  TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
RESET #1ST-ACT-POL
FOR #I1 1 TO 9
  IF STATUS (#I1) NE 'D'
    ESCAPE TOP
  END-IF
*
  IF   (OWNERSHIP-FLAG (#I1) = 'N')  /* DW9
*     AND (S-ADDRESS-1 NE ' ')                                      /* JT1  
    AND SECONDARY-ADDR-EXIST (< ST-V.MEMBER-CONTACT-ID >)           /* JT1
    MOVE TRUE TO #USE-2ND-ADDR-FOR-BILLING
  END-IF
*
  IF   (MODE (#I1) = '0' OR = '1' OR = '2' OR = '4')    /* DW2
      AND (PAY-STATUS (#I1) LT '40')                       /* DW2
    MOVE TRUE TO #CC-MODE                               /* DW2
  END-IF
*
  IF PREM-RATING (#I1) = '1' OR = '2' OR = '3'
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Pol Iss' ISSUE-DATE-MM(#I1) INTO #SCN-UPDATE-TEXT (#ISCR)
*
    COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
      '/' ISSUE-DATE-YYYY (#I1) TO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
*
    COMPRESS #SCN-UPDATE-TEXT(#ISCR) 'Is a' PLAN (#I1) 'rated class'
      PREM-RATING (#I1) 'policy' TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  IF   (DATE-ENTERED (#I1) NE 0)             /* DW8
      AND (DATE-ENTERED (#I1) NE ISSUE-DATE (#I1))
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    MOVE DATE-ENTERED (#I1) TO #YYYYMMDD
    COMPRESS #MM #DD #YYYY INTO #DATE-X WITH '/'
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Pol Iss' ISSUE-DATE-MM(#I1) INTO #SCN-UPDATE-TEXT (#ISCR)
*
    COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
      '/' ISSUE-DATE-YYYY (#I1) TO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
*
    COMPRESS #SCN-UPDATE-TEXT(#ISCR) 'plan' PLAN (#I1)
      'was entered on' #DATE-X TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  IF #1ST-ACT-POL = 0
    MOVE #I1 TO #1ST-ACT-POL
  END-IF
*
  IF (CONV-DURATION (#I1) = 888) AND (#CGA-SCR-ID = 40486)
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Pol Iss 05/28/97 is a 457 DEF COMP Policy'
      'owned by AAFMAA'  TO #SCN-UPDATE-TEXT (#ISCR)
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  IF CONV-DURATION (#I1) = 777 OR = 778 OR = 779 OR = 776
      OR  = 611 OR = 621 OR = 631 OR = 641
      OR  = 612 OR = 622 OR = 632 OR = 642
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Pol Iss' ISSUE-DATE-MM(#I1) INTO #SCN-UPDATE-TEXT (#ISCR)
*
    IF CONV-DURATION (#I1) = 777 OR = 611 OR = 612
      COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
        '/' ISSUE-DATE-YYYY (#I1) ' Initially owned by a Trust'
        INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
    ELSE
      IF CONV-DURATION (#I1) = 778 OR = 621 OR = 622
        COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
          '/' ISSUE-DATE-YYYY (#I1) ' Initially owned by the Insured'
          INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
      ELSE
        IF CONV-DURATION (#I1) = 779 OR = 631 OR = 632
          COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
            '/' ISSUE-DATE-YYYY(#I1) ' Initially owned by Members Spouse'
            INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
        ELSE
          COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
            '/' ISSUE-DATE-YYYY (#I1) ' Initially owned by a Non-Member'
            ' see "BD"'      INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
        END-IF
      END-IF
    END-IF
*
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  IF CONV-DURATION (#I1) = 661 OR = 662
      OR = 611 OR = 621 OR = 631 OR = 641
      OR = 612 OR = 622 OR = 632 OR = 642
    MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    COMPRESS '*Pol Iss' ISSUE-DATE-MM(#I1) INTO #SCN-UPDATE-TEXT (#ISCR)
*
    IF CONV-DURATION (#I1) = 661 OR = 611 OR = 621 OR = 631 OR = 641
      COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
        '/' ISSUE-DATE-YYYY (#I1) ' is regular Level II Policy'
        INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
    ELSE
      COMPRESS #SCN-UPDATE-TEXT(#ISCR) '/' ISSUE-DATE-DD(#I1)
        '/' ISSUE-DATE-YYYY(#I1) ' is College Protection Level II Policy'
        INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
    END-IF
*
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
*
  IF X-ALT-LP-IND (#I1) = 'E'                                           /* PM16
    #PROJECTED-DATE := *DATN                                            /* PM16
    DECIDE FOR FIRST CONDITION                                          /* PM16
      WHEN EZPAY-CAP-LOAN-AMOUNT GT 0                                   /* PM16
        IGNORE                                                          /* PM16
      WHEN PAD-CODE = 'P' OR = 'D' OR = 'L' OR = ' ' OR = 'S' OR = 'R'  /* PM16
        #PROJECTED-DATE := *DATN + 00000100                             /* PM19
*       #PROJECTED-DATE := *DATN + 00000200                             /* PM16 PM19
      WHEN NONE                                                         /* PM16
        #PROJECTED-DATE := *DATN + 00000100                             /* PM16
    END-DECIDE                                                          /* PM16
*
    IF #PROJECTED-MM GT 12                                              /* PM16
      SUBTRACT 12 FROM #PROJECTED-MM                                    /* PM16
      #PROJECTED-YYYY := #PROJECTED-YYYY + 1                            /* PM16
    END-IF
*
    COMPRESS '*' #PROJECTED-MM-A '/18/' #PROJECTED-YYYY-A INTO          /* PM16
      #SCN-UPDATE-TEXT (#ISCR) LEAVING NO                               /* PM16
    COMPRESS #SCN-UPDATE-TEXT (#ISCR) '- CAP loan projected'            /* PM16
      'distribution date' INTO #SCN-UPDATE-TEXT (#ISCR)                 /* PM16
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)                                 /* PM16
    MOVE TRUE TO #CRITICAL-NOTE                                         /* PM16
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY                               /* PM16
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'                            /* PM16
    MOVE (AD=I) TO #SCR-CV (#ISCR)                                      /* PM16
    ADD 1 TO #ISCR                                                      /* PM16
  END-IF                                                                /* PM16
END-FOR
*
IF #USE-2ND-ADDR-FOR-BILLING  /* DW9
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*Uses 2nd Address for Payor Info' TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
IF CHIEF-OF-STAFF-IND = 'C'                  /* DW7
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*Deceased was Posthumously promoted' TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
IF #CC-MODE                                             /* DW2
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY                 /* DW2
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE '*This member pays by Credit Card' TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
MOVE LAST-MBRSHIP-PHONE-DT TO #YYYYMMDD
IF ST-V.INTRO-FLAG = 'C' OR = 'B' OR = 'P' OR = 'Q'
  MOVE 'EDS'               TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  MOVE CAP-ENTRY-DATE TO #YYYYMM
*
  IF #YYYYMM GT 199612 /* nm was done in previous mon after 12/1996
    ADD 1 TO #MM
*
    IF #MM GT 12
      SUBTRACT 12 FROM #MM
      ADD 1 TO #YYYY
    END-IF
  END-IF
*
  COMPRESS '*C.A.P. Mbr as of' #MM-A
    INTO #SCN-UPDATE-TEXT (#ISCR)
  COMPRESS #SCN-UPDATE-TEXT (#ISCR)  '/' #YYYY
    INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  MOVE LAST-MBRSHIP-PHONE-DT TO #YYYYMMDD
*
  IF PC-5 = 'O' OR = 'Q' OR = 'P'
    IF PC-5 = 'O'
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) '- Payoff opts ltr sent'
        INTO #SCN-UPDATE-TEXT (#ISCR)
      COMPRESS #SCN-UPDATE-TEXT(#ISCR) #MM-A INTO #SCN-UPDATE-TEXT(#ISCR)
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) #DD-A #YYYY-A
        INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
    ELSE
      IF (PC-5 = 'P' OR = 'Q')
          AND (#YYYYMMDD GE 20010917)  /* date P/Q was implemented
        COMPRESS #SCN-UPDATE-TEXT (#ISCR) '- Pay Off ltr sent'
          INTO #SCN-UPDATE-TEXT (#ISCR)
        COMPRESS #SCN-UPDATE-TEXT(#ISCR) #MM-A INTO #SCN-UPDATE-TEXT(#ISCR)
        COMPRESS #SCN-UPDATE-TEXT (#ISCR) #DD-A #YYYY-A
          INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
      END-IF
    END-IF
*
  ELSE
    IF PC-4 = 'Y'
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) '- FAS form sent'
        INTO #SCN-UPDATE-TEXT (#ISCR)
      COMPRESS #SCN-UPDATE-TEXT(#ISCR) #MM-A
        INTO #SCN-UPDATE-TEXT (#ISCR)
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) #DD-A #YYYY-A
        INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
    END-IF
  END-IF
  ADD 1 TO #ISCR
ELSE /* other PC options do not apply to CAPS
  IF (PC-1 NE ' ') OR (PC-2 NE ' ')
      OR ((PLAN (1) = 'FSO') AND (STATUS (1) = 'D')
      AND ((PC-3 NE ' ') OR (PC-4 NE ' ') OR (PC-6 NE ' ')))
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE (AD=I) TO #SCR-CV (#ISCR)
*
    IF PC-1 NE ' '
      MOVE '*New Mbr PKT Sent' TO #SCN-UPDATE-TEXT (#ISCR)
    END-IF
*
    IF (PLAN (1) = 'FSO') AND (STATUS (1) = 'D') AND (PC-6 NE ' ')
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) '*FSO 14 Mon Survey sent'
        INTO #SCN-UPDATE-TEXT (#ISCR)
    ELSE
      IF (PLAN (1) = 'FSO') AND (STATUS (1) = 'D') AND (PC-5 NE ' ')
        COMPRESS #SCN-UPDATE-TEXT (#ISCR) '*FSO Conv ltr sent'
          INTO #SCN-UPDATE-TEXT (#ISCR)
      ELSE
        IF (PLAN (1) = 'FSO') AND (STATUS (1) = 'D') AND (PC-4 NE ' ')
          COMPRESS #SCN-UPDATE-TEXT (#ISCR) '*FSO FIR sent'
            INTO #SCN-UPDATE-TEXT (#ISCR)
        ELSE
          IF (PLAN (1) = 'FSO') AND (STATUS (1) = 'D') AND (PC-3 NE ' ')
            COMPRESS #SCN-UPDATE-TEXT (#ISCR)
              '*FSO SS & FAS Info letter sent'
              INTO #SCN-UPDATE-TEXT (#ISCR)
          ELSE
            IF PC-2 = 'Y'
              COMPRESS #SCN-UPDATE-TEXT (#ISCR)
                '*Welcome Call made by Phone'
                INTO #SCN-UPDATE-TEXT (#ISCR)
            END-IF
*
            IF PC-2 = 'E'
              COMPRESS #SCN-UPDATE-TEXT (#ISCR)
                '*Welcome Call sent by e-mail'
                INTO #SCN-UPDATE-TEXT (#ISCR)
            END-IF
*
            IF PC-2 = 'A'
              COMPRESS #SCN-UPDATE-TEXT (#ISCR)
                '*W/C made -left on answering machine'
                INTO #SCN-UPDATE-TEXT (#ISCR)
            END-IF
*
            IF PC-2 = 'N'
              COMPRESS #SCN-UPDATE-TEXT (#ISCR)
                '*Welcome Call - No contact made'
                INTO #SCN-UPDATE-TEXT (#ISCR)
            END-IF
*
            IF PC-2 = 'X'
              COMPRESS #SCN-UPDATE-TEXT (#ISCR)
                '*W/C made - Mbr says not to call again'
                INTO #SCN-UPDATE-TEXT (#ISCR)
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
*
    COMPRESS #SCN-UPDATE-TEXT (#ISCR) #MM-A
      INTO #SCN-UPDATE-TEXT (#ISCR)
    COMPRESS #SCN-UPDATE-TEXT (#ISCR) #DD-A #YYYY-A
      INTO #SCN-UPDATE-TEXT (#ISCR) WITH '/'
    ADD 1 TO #ISCR
  END-IF
END-IF
*
* Signature Authorization Card check                            /* YK1
IF CAP-ENTRY-DATE > 0
  #YYYYMM := CAP-ENTRY-DATE
  IF #MM = 12
    #MM := 1
    #YYYY := #YYYY + 1
  ELSE #MM := #MM + 1
  END-IF
  #DD := 1
  IF #DATN - #YYYYMMDD < 60
    FIND AT-V WITH SSN = ST-V.SSN
      IF NO RECORDS FOUND
        ESCAPE BOTTOM
      END-NOREC
      RESET #SA-CARD #ID-NUMBER
      MOVE #CGA-ID TO #ID-NUMBER
      IF AT-V.CAP-IND = 'Y' AND AT-V.MEMBER-CN = #ID-NUMBER
        IF AT-V.CONTACT-CODE NE 'Y'
          #SA-CARD := 'not'
        END-IF
        COMPRESS '*Signature authorization card has' #SA-CARD 'been received'
          TO #SCN-UPDATE-TEXT(#ISCR)
        MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
        COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
          INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
        MOVE (AD=I) TO #SCR-CV (#ISCR)
        ADD 1 TO #ISCR
        ESCAPE BOTTOM
      END-IF
    END-FIND
  END-IF
END-IF
*
IF   (PLAN (1) = 'FSO') AND (STATUS (1) = 'D')
    AND (ISSUE-DATE (1) LT *DATN - 10000)
  MOVE ISSUE-DATE (1) TO #YYYYMMDD
  ADD 1 TO #YYYY
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-DISP WITH '/'
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  COMPRESS '*FSO Expired' #DATE-DISP INTO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
FOR #I1 1 TO 9
  IF   (ISSUE-DATE (#I1) GE 19970101)
      AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
      AND (LAST-ANN-CASH-VALUE (#I1) NE 0)
    MOVE EDITED LAST-ANN-CASH-VALUE (#I1) (EM=ZZZ,ZZZ.99) TO #AMT-DISP
    MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
    COMPRESS #MM-A #DD-A #YYYY-A INTO #DATE-DISP WITH '/'
    MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
    MOVE TRUE TO #CRITICAL-NOTE
    COMPRESS '*Ltr re incorrect death bene of' #AMT-DISP 'on pol'
      #DATE-DISP INTO #SCN-UPDATE-TEXT (#ISCR)
    COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
      INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
    MOVE (AD=I) TO #SCR-CV (#ISCR)
    ADD 1 TO #ISCR
  END-IF
END-FOR
*
IF STATUS-CODE-SP = 'G'
  MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
  MOVE TRUE TO #CRITICAL-NOTE
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  COMPRESS '*Guardian of child is' NAME-SP
    TO #SCN-UPDATE-TEXT (#ISCR)
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
*
* IF #CGA-USER-COMPANY = 'I' OR = 'B'    /* DW24
IF #DO-NOT-WAIVE
  IGNORE
ELSE
  MOVE TRUE TO #CRITICAL-NOTE
  MOVE 'EDS'                       TO #SCN-UPDATE-ID(#ISCR)
  COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
    INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
  COMPRESS '*LTC waiting period waived on Trade-Up pol iss'
    #LTC-ISSUE-DATE-DISP INTO #SCN-UPDATE-TEXT (#ISCR)
*
  MOVE (AD=I) TO #SCR-CV (#ISCR)
  ADD 1 TO #ISCR
END-IF
* END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SET-LINK-TYPE
* * * * * * * * * * * * * * * *
FIND (1) LNK-V WITH MEMBER-KEY = LINKED-CN-A (#I1)
  MOVE '(M)' TO #LINK-TYPE /* init to member
  IF LNK-V.INTRO-FLAG = 'S' MOVE '(S)' TO #LINK-TYPE END-IF
  IF LNK-V.INTRO-FLAG = 'K' MOVE '(C)' TO #LINK-TYPE END-IF
  IF LNK-V.INTRO-FLAG = 'G' MOVE '(G)' TO #LINK-TYPE END-IF
  COMPRESS #LINKED #LINK-TYPE INTO #LINKED LEAVING NO
END-FIND
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE MESSAGE-IF-AFA-CLIENT   /* RE2
* * * * * * * * * * * * * * * * * * * *
FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = ST-V.ID-NUMBER
*  FOR #I 1 C*SVC-ACTIVE-SEGMENTS          /* RE1
*    IF SVC-TERMINATION-DATE(#I) = 0       /* RE1
    IF SVC-TERMINATION-DATE = 0 AND SVC-STATUS <> 'C' /* RE1 bz1
      ADD 1 TO #ISCR
      MOVE 'EDS' TO #SCN-UPDATE-ID(#ISCR)
      MOVE TRUE TO #CRITICAL-NOTE
      COMPRESS #DATN-MM #DATN-DD #DATN-YYYY
        INTO #SCN-UPDATE-DATE (#ISCR) WITH '/'
*
      COMPRESS '*AM&SB Client with Plan ' TO #SCN-UPDATE-TEXT (#ISCR)        /* TT1
*      ESCAPE BOTTOM                                                               /* RE1 START
    ELSE
      ESCAPE TOP
    END-IF
*  END-FOR                                  
*
*  IF #I > C*SVC-ACTIVE-SEGMENTS
*    ESCAPE BOTTOM
*  END-IF
*
*  FOR #I 1 C*SVC-ACTIVE-SEGMENTS                                                  
*    IF SVC-TERMINATION-DATE(#I) = 0
*   IF SVC-TERMINATION-DATE = 0                               
*      COMPRESS #SCN-UPDATE-TEXT (#ISCR) SVC-PLAN(#I) TO #SCN-UPDATE-TEXT (#ISCR)
      COMPRESS #SCN-UPDATE-TEXT (#ISCR) SVC-PLAN TO #SCN-UPDATE-TEXT (#ISCR)       
*    END-IF
*  END-FOR  
*
*  MOVE #SVC-YYYYMM(1) TO #YYYYMM                       
  MOVE #SVC-YYYYMM TO #YYYYMM                                                      /* RE1 END
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '-MBR Since' #MM
    INTO #SCN-UPDATE-TEXT (#ISCR)
  COMPRESS #SCN-UPDATE-TEXT (#ISCR) '/' #YYYY
    INTO #SCN-UPDATE-TEXT (#ISCR) LEAVING NO
END-FIND
*
END-SUBROUTINE
*
END
