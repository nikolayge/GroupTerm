* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID TBMPGENT
*    Main Gen-Table maintenance program
*
********************************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  12122007      Initial Creation
********************************************************************************
DEFINE DATA
*
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING OBJAGENT
LOCAL USING OBJAUSER
LOCAL  USING GONERLXX
LOCAL
1 #SCREEN-ARRAY (14)
  2 #POINTER         (A1)
  2 #GEN-KEY         (A4)
  2 #GEN-SUB-KEY     (A3)
  2 #ADDL-INFO       (A10)
  2 #GEN-DESCRIPTION (A75)
*
1 #USER-ACTION       (A8)
1 #START-KEY         (A4)
1 #START-SUB-KEY     (A3)
1 #CV-START-KEY      (C)
1 #CV-START-SUB-KEY  (C)
1 #MAX-ENTRY (N2) INIT <14>
1 #HEADER-2          (A20) INIT <'    Summary'>
1 #KEY-START-VALUE   (A16) INIT <'Key Start Value:'>
1 #I  (N2)
1 #I1 (N2)
1 #I2 (N2)
1 #LINE-COUNT (N6)
1 #SCREEN-COUNT (N6)
1 #SCREEN       (N6)
1 #IND          (N6)
1 #TEXT (A78)
1 #FULL-ACCESS     (L)
1 #ADMIN-ACCESS    (L)
1 #ADMIN-FUNCTION  (L)
1 #NEW-DATA        (L)
1 #CV-POINTER (C/14)
1 #FUNCTION-ATTRIBUTES (A68)
1 REDEFINE #FUNCTION-ATTRIBUTES
  2 #PROGRAM           (A8)
  2 #SEP1              (A1)
  2 #FUNCTION-PARM     (A7)
  2 #HEADER-1          (A50)
  2 #SECURITY-CODE     (N2)
*
END-DEFINE
*
* INCLUDE GONERCXX
*
DEFINE WINDOW FIELD-WINDOW
      SIZE 4*78
      BASE 9/1
*
SET KEY PF1 = HELP
SET KEY PF4 NAMED 'Add'
SET KEY PF5 NAMED 'Mod'
SET KEY PF7 NAMED 'Up'
SET KEY PF8 NAMED 'Down'
SET KEY PF12 NAMED 'Quit'
SET KEY PF3 NAMED 'Retrn'
*
IF *DATA NE 0
  INPUT #FUNCTION-ATTRIBUTES
  RELEASE STACK
END-IF
IF #FUNCTION-PARM NE ' '
 #CV-START-KEY := (AD=P)
ELSE
 #ADMIN-FUNCTION := TRUE
END-IF
RESET #ADMIN-ACCESS #FULL-ACCESS
IF FUNCTION-ACCESS(<*USER,#SECURITY-CODE>) = 'A'
   #ADMIN-ACCESS := #FULL-ACCESS := TRUE
ELSE
   IF FUNCTION-ACCESS(<*USER,#SECURITY-CODE>) = 'U'
      #FULL-ACCESS      := TRUE
      #CV-START-KEY     := (AD=NP)
      #CV-START-SUB-KEY := (AD=NP)
   END-IF
END-IF
IF NOT #FULL-ACCESS
  #TEXT := 'User not authorized to perform this function. Browse access only'
  SET KEY PF5=OFF
  SET KEY PF4=OFF
END-IF
*
#HEADER-1 := FUNCTION-HEADER(<#HEADER-1>)
#NEW-DATA := TRUE
REPEAT
IF #NEW-DATA
  RESET #NEW-DATA
  PERFORM POPULATE-ARRAY
END-IF
  INPUT WITH TEXT #TEXT USING MAP 'TBMMGENT'
  RESET #TEXT
  DECIDE ON FIRST VALUE *PF-KEY
    VALUE 'PF3'
      #PROGRAM := 'G1000PUI'
      #FUNCTION-PARM := 'RSUB'
      STACK TOP DATA #FUNCTION-ATTRIBUTES
      FETCH #PROGRAM
    VALUE 'ENTR'
   IF #CV-START-KEY MODIFIED OR #CV-START-SUB-KEY MODIFIED
    PERFORM POPULATE-ARRAY
   ELSE
    PARM-GEN-TABLE.#ACTION := 'BROWSE'
    PERFORM TABLE-CHANGE
   END-IF
    VALUE 'PF12'
      SET KEY OFF
      FETCH 'G1000PXX'
    VALUE 'PF4'
      PARM-GEN-TABLE.#ACTION := 'ADD'
      PERFORM TABLE-CHANGE
    VALUE 'PF5'
      PARM-GEN-TABLE.#ACTION := 'UPDATE'
      PERFORM TABLE-CHANGE
    VALUE 'PF8', 'PF7'
      PERFORM NEXT-SCREEN
   NONE
      IGNORE
  END-DECIDE
END-REPEAT
*
********************************
DEFINE SUBROUTINE POPULATE-ARRAY
********************************
*
  RESET PARM-GEN-TABLE #SCREEN-ARRAY(*)
  PARM-GEN-TABLE.#DATE := *DATN
  IF #FUNCTION-PARM NE ' '
    PARM-GEN-TABLE.#KEY := #START-KEY := SUBSTR(#FUNCTION-PARM,1,4)
    PARM-GEN-TABLE.#SUB-KEY := #START-SUB-KEY := SUBSTR(#FUNCTION-PARM,5,3)
    RESET #FUNCTION-PARM
  ELSE
    PARM-GEN-TABLE.#KEY     := #START-KEY
    PARM-GEN-TABLE.#SUB-KEY := #START-SUB-KEY
  END-IF
  #NUMBER-OF-RECORDS := #MAX-ENTRY
  PARM-GEN-TABLE.#ACTION := 'READ'
  CALLNAT 'OBJNGENT' USING PARM-GEN-TABLE
  IF PARM-GEN-TABLE.#RETURN-CODE NE ' '
    #TEXT := PARM-GEN-TABLE.#RETURN-MESSAGE
    ESCAPE ROUTINE
  END-IF
*
  #SCREEN-COUNT := (#NUMBER-OF-RECORDS / #MAX-ENTRY)
  IF #SCREEN-COUNT * #MAX-ENTRY < #NUMBER-OF-RECORDS
     #SCREEN-COUNT := #SCREEN-COUNT + 1
  END-IF
  IF #SCREEN = 0
     #SCREEN := 1
  END-IF
  PERFORM POPULATE-SCREEN
*
END-SUBROUTINE
*
*********************************
DEFINE SUBROUTINE POPULATE-SCREEN
*********************************
*
  #LINE-COUNT := #NUMBER-OF-RECORDS - (#SCREEN - 1)*#MAX-ENTRY
  #IND        := (#SCREEN - 1)* #MAX-ENTRY
  IF #LINE-COUNT > #MAX-ENTRY
     #LINE-COUNT := #MAX-ENTRY
  END-IF
  RESET #SCREEN-ARRAY(*)
  FOR #I = 1 TO #LINE-COUNT
    #SCREEN-ARRAY.#GEN-DESCRIPTION (#I) := PARM-GEN-TABLE.GEN-DESCRIPTION (#IND + #I)
    #SCREEN-ARRAY.#ADDL-INFO (#I)       := SUBSTR(PARM-GEN-TABLE.GEN-ALPHA-VALUE (#IND + #I),1,10)
    #SCREEN-ARRAY.#GEN-KEY (#I)         := PARM-GEN-TABLE.GEN-KEY (#IND + #I)
    #SCREEN-ARRAY.#GEN-SUB-KEY (#I)     := PARM-GEN-TABLE.GEN-SUB-KEY (#IND + #I)
  END-FOR
  #I1 := #LINE-COUNT + 1
  FOR #I2 = #I1 TO #MAX-ENTRY
   #CV-POINTER(#I2) := (AD=P)
  END-FOR
*
END-SUBROUTINE
*
******************************
DEFINE SUBROUTINE TABLE-CHANGE
******************************
*
#USER-ACTION := PARM-GEN-TABLE.#ACTION
IF PARM-GEN-TABLE.#ACTION = 'ADD'
   RESET PARM-GEN-TABLE.PARM-ARRAY(*)
   #NUMBER-OF-RECORDS := 1
   PARM-GEN-TABLE.GEN-SECURITY-CODE(1) := #SECURITY-CODE
   RESET PARM-GEN-TABLE.#RETURN-CODE PARM-GEN-TABLE.#RETURN-MESSAGE
   CALLNAT 'TBMNGENA' PARM-GEN-TABLE #USER-ACTION
   IF PARM-GEN-TABLE.#RETURN-CODE NE ' '
     #TEXT := PARM-GEN-TABLE.#RETURN-MESSAGE
   END-IF
   #NEW-DATA := TRUE
   ESCAPE ROUTINE
ELSE
*
 EXAMINE #POINTER(*) FOR 'X' INDEX #I
   IF #I = 0
     #I := *CURS-LINE
     #I := #I - 5
   END-IF
   IF #I > 0 AND #I LE #LINE-COUNT
     IGNORE
   ELSE
     IF #FULL-ACCESS
     #TEXT := 'Place Cursor on the record you want to Browse/Modify'
     ELSE
       #TEXT := 'Place Cursor on the record you want to Browse'
     END-IF
     REINPUT FULL WITH TEXT #TEXT
   END-IF
   IF PARM-GEN-TABLE.#ACTION = 'UPDATE' AND NOT #FULL-ACCESS
      PARM-GEN-TABLE.#ACTION := 'BROWSE'
   END-IF
   PARM-GEN-TABLE.#KEY     := #SCREEN-ARRAY.#GEN-KEY (#I)
   PARM-GEN-TABLE.#SUB-KEY := #SCREEN-ARRAY.#GEN-SUB-KEY (#I)
END-IF
IF PARM-GEN-TABLE.#ACTION = 'UPDATE' AND
 #ADMIN-ACCESS AND #ADMIN-FUNCTION
  CALLNAT 'TBMNGENA' PARM-GEN-TABLE #USER-ACTION
ELSE
* Special processing for Life Insurance Premium Rates
  IF PARM-GEN-TABLE.#SUB-KEY = '3AG'
    #CGA-SCR-COMMAND := 'GM'
    #CGA-SCR-SUFFIX  := 'PRM'
    FETCH 'G1100PGM'
  END-IF
  RESET PARM-GEN-TABLE.PARM-ARRAY(*)
  RESET PARM-GEN-TABLE.#RETURN-CODE PARM-GEN-TABLE.#RETURN-MESSAGE
  CALLNAT 'TBMNGENT' PARM-GEN-TABLE #USER-ACTION #HEADER-1
END-IF
IF PARM-GEN-TABLE.#RETURN-CODE NE ' '
  #TEXT := PARM-GEN-TABLE.#RETURN-MESSAGE
END-IF
*
#NEW-DATA := TRUE
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE NEXT-SCREEN
*****************************
IF *PF-KEY = 'PF8'
   IF #SCREEN = #SCREEN-COUNT
     #TEXT := 'Last Screen'
     REINPUT WITH TEXT #TEXT
   ELSE
     #SCREEN := #SCREEN + 1
   END-IF
ELSE
   IF #SCREEN = 1
     #TEXT := 'First Screen'
     REINPUT WITH TEXT #TEXT
   ELSE
     #SCREEN := #SCREEN - 1
   END-IF
END-IF
PERFORM POPULATE-SCREEN
*
END-SUBROUTINE
END
