* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-ACHRCD                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Display ACH rejection reason                          *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
* PAM    08/05/2009  PM1     Expanded to include change codes          *
*                            ACH Phase III                             *
* PAM    12/14/2009  PM2     Added R13 code                            *
************************************************************************
*
DEFINE FUNCTION FUNCTION-ACH-REASON RETURNS (A) DYNAMIC
*
DEFINE DATA
*
PARAMETER
*
1 #REJECTION-CODE          (A3)
1 #REJECTION-CODE-TYPE     (A1)
*
LOCAL
*
1 #REJECTION-TABLE
 2 #R01                    (A54)   INIT <'R01-Insufficient funds                    -NSF      -E'>
 2 #R02                    (A54)   INIT <'R02-Account closed                        -Act Close-S'>
 2 #R03                    (A54)   INIT <'R03-No account/Unable to locate account   -No Accnt -S'>
 2 #R04                    (A54)   INIT <'R04-Invalid account number                -Inv Act #-S'>
 2 #R06                    (A54)   INIT <'R06-Returned by ODFI"s request            -Ret ODFI -S'>
 2 #R07                    (A54)   INIT <'R07-Authorization revoked by customer     -Auth Rev -S'>
 2 #R08                    (A54)   INIT <'R08-Payment stopped on item               -Pay Stop -S'>
 2 #R09                    (A54)   INIT <'R09-Uncollected funds                     -Unc Fund -E'>
 2 #R10                    (A54)   INIT <'R10-Customer advises not authorized       -Not Auth -S'>
 2 #R11                    (A54)   INIT <'R11-Check truncation entry return         -Truncated-S'>
 2 #R12                    (A54)   INIT <'R12-Branch sold to another DFI            -Brn Sold -E'>
 2 #R13                    (A54)   INIT <'R13-RDFI not qualified to participate     -RDFI nq  -S'> /* PM2
 2 #R14                    (A54)   INIT <'R14-Re-presentment payee deceased         -Deceased -S'>
 2 #R15                    (A54)   INIT <'R15-Beneficiary of account holder deceased-Bene Dec -S'>
 2 #R16                    (A54)   INIT <'R16-Account frozen                        -Act Froz -S'>
 2 #C01                    (A54)   INIT <'C01-Incorrect DFI account number          -Inc DFI  - '>
 2 #C02                    (A54)   INIT <'C02-Incorrect routing number              -Inc Route- '> /* PM1
 2 #C03                    (A54)   INIT <'C03-Incorrect routing and DFI numbers     -Inc DF/RT- '> /* PM1
*
1 REDEFINE #REJECTION-TABLE
 2 #REJECTION-TABLE-OCC    (18)    /* PM1 PM2
  3 #REJECTION-TYPE        (A3)
  3 #REJECTION-SPACE-1     (A1)
  3 #REJECTION-DESCRIPTION (A38)
  3 #REJECTION-SPACE-2     (A1)
  3 #REJECTION-DESC-ABBR   (A9)
  3 #REJECTION-SPACE-3     (A1)
  3 #REJECTION-REV-STOP    (A1)
*
1 #I1                      (I2)
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-ACH-REASON
*
FOR #I1 1 TO 18                    /* PM1 PM2
  IF #REJECTION-TYPE (#I1) = #REJECTION-CODE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #I1 LT 19                       /* PM1 PM2
  DECIDE ON FIRST VALUE OF #REJECTION-CODE-TYPE
    VALUE 'A'
      FUNCTION-ACH-REASON := #REJECTION-DESC-ABBR (#I1)
    VALUE 'B'
      FUNCTION-ACH-REASON := #REJECTION-REV-STOP (#I1)
    VALUE 'C'
      FUNCTION-ACH-REASON := #REJECTION-DESCRIPTION (#I1)
    NONE VALUE
      FUNCTION-ACH-REASON := #REJECTION-CODE
  END-DECIDE
ELSE
  FUNCTION-ACH-REASON     := #REJECTION-CODE
END-IF
*
END-FUNCTION
*
END
