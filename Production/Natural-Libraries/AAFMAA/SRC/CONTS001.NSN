* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* SUBPROGRAM-ID: CONTS001 - GETS CONTACT DATA  USING CONTACT-ID        *
*                                                                      *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* TMT  06/09/2014  NONE  INITIAL CREATION                              *
*                                                                      *
************************************************************************
DEFINE DATA
PARAMETER USING CONTPDA1     /* INPUT IS CONTACT-ID
PARAMETER
1 SPOUSE-DATE-OF-BIRTH    (N8.0)      OPTIONAL 
1 SPOUSE-GENDER           (A1)        OPTIONAL 
1 FORMER-SPOUSE-DOB       (N8.0)      OPTIONAL
*
LOCAL USING OBJLCONT         /* A-CONTACTS-DATA
LOCAL
1 #CONTACT-ID           (N8.0)
END-DEFINE
*
IF P-CONTACT-ID = 0 THEN
  ESCAPE ROUTINE
END-IF
*
FIND(1) CT-V WITH CONTACT-ID = P-CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  P-ID-NUMBER       :=  CT-V.ID-NUMBER
  P-GENDER          :=  CT-V.GENDER-CD
  P-DATE-OF-BIRTH   :=  CT-V.DATE-OF-BIRTH
END-FIND
*
IF (SPOUSE-DATE-OF-BIRTH SPECIFIED OR SPOUSE-GENDER SPECIFIED)
  PERFORM GET-SPOUSE-INFO
END-IF
*
IF FORMER-SPOUSE-DOB SPECIFIED
  PERFORM GET-FORMER-SPOUSE-INFO
END-IF
*
**===============================
DEFINE SUBROUTINE GET-SPOUSE-INFO
RESET CONTACT-ID
#CONTACT-ID := FUNCTION-SPOUSE-CI(<P-CONTACT-ID>)  /* CURRENT SPOUSE
*
FIND(1) CT-V WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  SPOUSE-DATE-OF-BIRTH    := DATE-OF-BIRTH
  SPOUSE-GENDER           := GENDER-CD
END-FIND
*
END-SUBROUTINE
**============
*
**===============================
DEFINE SUBROUTINE GET-FORMER-SPOUSE-INFO
RESET CONTACT-ID
#CONTACT-ID := FUNCTION-XSPOUSE-CI(<P-CONTACT-ID>) /* Former SPOUSE
*
FIND(1) CT-V WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  FORMER-SPOUSE-DOB       := DATE-OF-BIRTH
END-FIND
*
END-SUBROUTINE
**============
END
