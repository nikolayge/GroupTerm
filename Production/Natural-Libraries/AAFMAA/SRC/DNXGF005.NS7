* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ID DNXGF002   Defaul Phone
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20101225  None    Program written.                             *
*                                                                      *
************************************************************************
*
DEFINE FUNCTION FUNCTION-DEFAULT-PHONE RETURNS (A) DYNAMIC
DEFINE DATA
PARAMETER
1 #CONTACT-ID         (N8)
LOCAL
* Get Phones Routine
1 p_PhoneN1M    (10)
  2 p_Phone     (A36)
  2 p_Type      (A16)
  2 p_Code      (A1)
  2 p_Preferred (A1)
1 p_Rc          (N3)
1 BLANK         (A1)   CONST <' '>
1 #BUSINESS     (A1/2) CONST <'B','P'>
1 #PERSONAL     (A1/3) CONST <'H','C','M'>
1 #OTHER        (A1)   CONST <'O'>
1 PHONE VIEW OF A-CONTACT-PHONE
  2 PHONE-ID (N8)
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3   (A3)
    3 DIAL-NUMBER-4   (A4)
  2 EXT (N6)
  2 PREFERRED-PHONE-IND (A1)
1 #BUSINESS-PHONE (A) DYNAMIC
1 #OTHER-PHONE    (A) DYNAMIC
1 #J              (I4)
END-DEFINE
*
FUNCTION-DEFAULT-PHONE := BLANK
RESET p_PhoneN1M (*)
CALLNAT 'PHONEN1M'  #CONTACT-ID p_PhoneN1M(*) p_Rc
FOR #J 1 TO 10
  IF p_Preferred (#J) = 'Y'
    IF p_Code (#J) = #PERSONAL (*)
      FUNCTION-DEFAULT-PHONE := p_Phone (#J)
      ESCAPE BOTTOM
    END-IF
    IF p_Code (#J) = #BUSINESS (*)
      #BUSINESS-PHONE := p_Phone (#J)
    END-IF
    IF p_Code (#J) = #OTHER
      #OTHER-PHONE := p_Phone (#J)
    END-IF
  END-IF
END-FOR
IF FUNCTION-DEFAULT-PHONE = BLANK
  FUNCTION-DEFAULT-PHONE := #BUSINESS-PHONE
END-IF
IF FUNCTION-DEFAULT-PHONE = BLANK
  FUNCTION-DEFAULT-PHONE := #OTHER-PHONE
END-IF
*
DEFINE READ-DB
FIND PHONE WITH CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
*
  IF PREFERRED-PHONE-IND = 'Y'
    IF PHONE-TYPE-CODE = #PERSONAL (*)
      p_Code(1) := PHONE-TYPE-CODE
      CALLNAT 'PHONEN1M'  #CONTACT-ID p_PhoneN1M(*) p_Rc
      FUNCTION-DEFAULT-PHONE := p_Phone (1)
      ESCAPE BOTTOM
    END-IF
    IF PHONE-TYPE-CODE = #BUSINESS (*)
      p_Code(1) := PHONE-TYPE-CODE
      CALLNAT 'PHONEN1M'  #CONTACT-ID p_PhoneN1M(*) p_Rc
      #BUSINESS-PHONE := p_Phone (1)
    END-IF
    IF PHONE-TYPE-CODE = #OTHER
      p_Code(1) := PHONE-TYPE-CODE
      CALLNAT 'PHONEN1M'  #CONTACT-ID p_PhoneN1M(*) p_Rc
      #OTHER-PHONE := p_Phone (1)
    END-IF
  END-IF
END-FIND
END-SUBROUTINE
*
END-FUNCTION
END
