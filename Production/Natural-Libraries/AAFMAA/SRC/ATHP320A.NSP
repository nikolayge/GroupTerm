* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP320A     ** BATCH VERSION OF "IG" (C2D50PIG) **
*****************************************************************
**         MODIFICATIONS
** WHO   DATE   TAG      COMMENTS
** SAG 11012005 RH1 AB - Replace CALL to Cobol with CALLNAT to Natural
*****************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GC2V6LXX
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL
*
1 IM-V VIEW OF A-INTRO-MEMBER
 2 ID-NUMBER           (N6)
 2 NAME                (A25)
 2 SEX                 (A1)
 2 MIL-SERVICE         (A3)
 2 ISSUE-AGE           (P3)
 2 ANNUAL-PREMIUM      (P5.2)
 2 SMOKER-FLAG         (A1)
 2 INS-MODE            (A1)
 2 DATE-OF-BIRTH       (N8)
 2 REDEFINE DATE-OF-BIRTH
  3 DATE-OF-BIRTH-YYYY (N4)
  3 DATE-OF-BIRTH-MM   (N2)
  3 DATE-OF-BIRTH-DD   (N2)
 2 PAID-THRU-DATE      (N8)
 2 REDEFINE PAID-THRU-DATE
  3 PAID-THRU-DATE-YYYY (N4)
  3 PAID-THRU-DATE-MM   (N2)
  3 PAID-THRU-DATE-DD   (N2)
 2 ADDRESS-1            (A25)
 2 ADDRESS-2            (A25)
 2 ADDRESS-3            (A25)
 2 ZIP-CODE             (N9)
 2 S-ADDRESS-1          (A25)
 2 S-ADDRESS-2          (A25)
 2 S-ADDRESS-3          (A25)
 2 S-ZIP-CODE           (N9)
*
1 #EXTR-RECORD
 2 #RECORD-1             (A80)    /* USES 65
 2 REDEFINE #RECORD-1
  3 #ID-NUMBER-1          (N6)
  3 #LOAN-IND             (A1)
  3 #PLAN                 (A3)
  3 #F1-02                (A1)
  3 #ISS-DATE             (N8)
  3 #F1-03                (A1)
  3 #ISS-AGE              (A2)
  3 #F1-04                (A1)
  3 #UNITS                (N1)
  3 #F1-05                (A1)
  3 #LAST-DURATION        (N2)
  3 #F1-06                (A1)
  3 #MONTH-IND            (N2)
  3 #F1-07                (A1)
  3 #CONV-DUR             (N2)
  3 #F1-08                (A1)
  3 #PAY-STATUS           (A2)
  3 #F1-09                (A1)
  3 #ANN-PREM             (N3.2)
  3 #F1-10                (A1)
  3 #MODE                 (A1)
  3 #F1-11                (A1)
  3 #SMOKER-FLAG          (A1)
  3 #F1-12                (A1)
  3 #DOB                  (N8)
  3 #F1-13                (A1)
  3 #ACV                  (N7.2)
*
 2 #RECORD-2             (A80)     /* USES 66
 2 REDEFINE #RECORD-2
  3 #ID-NUMBER-2          (N6)
  3 #F2-01                (A1)
  3 #CV                   (N7.2)
  3 #F2-02                (A1)
  3 #MIN-CV               (N5.2)
  3 #F2-03                (A1)
  3 #NXT-YR-MIN-CV        (N5.2)
  3 #F2-04                (A1)
  3 #DB                   (N7.2)
  3 #F2-05                (A1)
  3 #LAST-ANN-CV          (N7.2)
  3 #F2-06                (A1)
  3 #FACE-AMT             (N5.2)
  3 #F2-07                (A1)
  3 #GROWTH-RATE          (N0.5)
*
 2 #RECORD-3             (A80)     /* USES 74
 2 REDEFINE #RECORD-3
  3 #ID-NUMBER-3          (N6)
  3 #F3-01                (A1)
  3 #NAME                 (A15)
  3 #F3-02                (A1)
  3 #MONTHLY-ALLOC-CRED   (N3.2)
  3 #F3-03                (A1)
  3 #MONTHLY-PREM-DEFIC   (N3.2)
  3 #F3-04                (A1)
  3 #NET-PREM-RATIO       (N1.6)
  3 #F3-05                (A1)
  3 #LOAN-AMT             (N5.2)
  3 #F3-06                (A1)
  3 #LOAN-INT-RATE        (N0.5)
  3 #F3-07                (A1)
  3 #SEX                  (A1)
  3 #F3-08                (A1)
  3 #CELL-COUNT           (N5)
  3 #REAL-ISS-DATE        (N8)
  3 #REAL-ISS-AGE         (A2)
*
1 #FIRST-ADDRESS  (A25)
1 #SECOND-ADDRESS (A40)
1 #THIRD-ADDRESS  (A40)
1 #HOLD-ADDRESS-2  (A25)
1 #HOLD-ADDRESS-3  (A25)
1 #HOLD-ZIP-CODE   (N9)
1 #HOLD-EDIT-ZIP   (A10)
1 REDEFINE #HOLD-EDIT-ZIP
  2 #HOLD-ZIP      (A5)
  2 #HOLD-ZIP2     (A5)
*
1 #SCN-1-TIME-COST    (A10)
1 #WHOLE-DOLLARS      (P5)
*
1 #FORMATED-NAME  (A25)
1 #FORMATED-NAME-PERIOD (A26)
1 #HOLD-NAME      (A75)
1 #DEL            (A1) CONST<','>
1 #LAST-NAME    (A25)
1 #FIRST/MID    (A25)
1 REDEFINE #FIRST/MID
 2 #FIRST/MID-1  (A1)
 2 #FIRST/MID-24 (A24)
1 #SUFFIX       (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
1 #AGE         (P3)
1 #CNT         (P5)
*
1 #EFF-DATE        (N8)
1 REDEFINE #EFF-DATE
 2 #EFF-DATE-CENT   (N2)
 2 #EFF-DATE-YYMMDD (N6)
1 #WORK-YYYYMMDD   (N8)
1 REDEFINE #WORK-YYYYMMDD
 2 #WORK-YYYY      (N4)
 2 #WORK-MM        (N2)
 2 #WORK-DD        (N2)
*
1 #TOT-COUNT       (P5)
1 #TBL-COUNT       (P5/20)
1 #TBL-ISS-AGE     (P3/20)
1 #TBL-SNG-PREM    (P5.2/20)
1 #TBL-LST-MIN-CSV (P5.2/20)
1 #TBL-NXT-MIN-CSV (P5.2/20)
1 #IAG             (I2)
1 #I1              (I2)
*
1 #HOLD-ID-N      (N6)
1 REDEFINE #HOLD-ID-N
 2 #HOLD-ID-1 (A1)
 2 #HOLD-ID         (A5)
1 REDEFINE #HOLD-ID-N
 2 #HOLD-ID-A       (A6)
*
1 #ERROR-MESSAGE    (A25)
*
1 #FE-EXTRACT       (A50)
1 #FE-NAME          (A50)
1 #FE-FIRST-ADDR    (A40)
1 #FE-SECOND-ADDR   (A40)
1 #FE-THIRD-ADDR    (A40)
1 #FE-LAST-LINE     (A40)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
INCLUDE GONERCXX         /* NATURAL ERROR ROUTINE
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                  'INTRODUCTORY GIFT MEMBERSHIPS'
                   5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ IM-V BY NAME
* IF   (MIL-SERVICE = 'A  ')                /* ONLY DO THESE
* IF   (MIL-SERVICE = 'F  ')                /* ONLY DO THESE
  IF   (MIL-SERVICE = 'ACR')                    /* ONLY DO THESE
* IF   (MIL-SERVICE = 'FCR')                    /* ONLY DO THESE
* IF   (MIL-SERVICE = 'ACR' OR = 'FCR')         /* ONLY DO THESE
   AND (PAID-THRU-DATE-YYYY = 1992)  /* ONLY IF GRADUATING IN 1992
   AND (INS-MODE = 'N')             /* ONLY IF INSURANCE MODE = NO
   IGNORE
  ELSE
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #CNT
*
  SEPARATE NAME INTO #LAST-NAME #FIRST/MID
    #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER #DEL
  COMPRESS #FIRST/MID-24 #LAST-NAME INTO #HOLD-NAME
*
  IF #SUFFIX NE ' '
    COMPRESS #HOLD-NAME ',' #SUFFIX INTO #HOLD-NAME LEAVING NO
  END-IF
  MOVE #HOLD-NAME TO #FORMATED-NAME
  COMPRESS #HOLD-NAME '.' INTO #FORMATED-NAME-PERIOD LEAVING NO
*
  MOVE ID-NUMBER TO #HOLD-ID-N
*
* IF ADDRESS-1 = ' '     /* USE 2ND ADDRESS
    IF S-ADDRESS-1 = ' '
      WRITE ID-NUMBER ' NO USEABLE ADDRESS'
      ESCAPE TOP
*     RESET #FIRST-ADDRESS
*     RESET #HOLD-ADDRESS-2
*     RESET #HOLD-ADDRESS-3
*     RESET #HOLD-ZIP-CODE
    ELSE
      MOVE S-ADDRESS-1 TO #FIRST-ADDRESS
      MOVE S-ADDRESS-2 TO #HOLD-ADDRESS-2
      MOVE S-ADDRESS-3 TO #HOLD-ADDRESS-3
      MOVE S-ZIP-CODE  TO #HOLD-ZIP-CODE
    END-IF
* ELSE
*   MOVE ADDRESS-1 TO #FIRST-ADDRESS
*   MOVE ADDRESS-2 TO #HOLD-ADDRESS-2
*   MOVE ADDRESS-3 TO #HOLD-ADDRESS-3
*   MOVE ZIP-CODE  TO #HOLD-ZIP-CODE
* END-IF
*
  MOVE EDITED #HOLD-ZIP-CODE (EM=99999-9999) TO #HOLD-EDIT-ZIP
*
  IF #HOLD-ZIP2 = '-0000'
    MOVE '     ' TO #HOLD-ZIP2
  END-IF
*
  IF #HOLD-ADDRESS-3 = ' '
    COMPRESS #HOLD-ADDRESS-2 #HOLD-EDIT-ZIP INTO #SECOND-ADDRESS
    RESET #THIRD-ADDRESS
  ELSE
    MOVE #HOLD-ADDRESS-2 TO #SECOND-ADDRESS
    COMPRESS #HOLD-ADDRESS-3 #HOLD-EDIT-ZIP INTO #THIRD-ADDRESS
  END-IF
*
  MOVE *DATN TO #WORK-YYYYMMDD     /* COMPUTE CURRENT EFFECTIVE DATE
*
  MOVE 09 TO #WORK-MM
  MOVE 01 TO #WORK-DD
  MOVE #WORK-YYYYMMDD TO #EFF-DATE
*
  IF  (#WORK-MM LT DATE-OF-BIRTH-MM)
   OR ((#WORK-MM = DATE-OF-BIRTH-MM) AND (#WORK-DD LT DATE-OF-BIRTH-DD))
    SUBTRACT 1 FROM #WORK-YYYY
  END-IF
*
  COMPUTE #AGE = #WORK-YYYY - DATE-OF-BIRTH-YYYY
*
  IF SEX = 'F'
    SUBTRACT 3 FROM #AGE
  END-IF
*
  IF (#AGE LT 12) OR (#AGE GT 60)
    WRITE ID-NUMBER 'CALCULATED AGE IS LESS THAN THAN 12 OR GTR 60'
    ESCAPE TOP
  END-IF
*
  RESET #L-604-RSV-VA-DIFF
*
  FOR #IAG 1 TO 20         /* HAVE WE ALREADY DONE THIS AGE
    IF #TBL-ISS-AGE (#IAG) = 0
      ADD 1 TO #TBL-COUNT (#IAG)
      ESCAPE BOTTOM
    END-IF
*
    IF #AGE = #TBL-ISS-AGE (#IAG)
      MOVE #TBL-SNG-PREM (#IAG) TO #L-604-RSV-VA-DIFF
      MOVE #TBL-LST-MIN-CSV  (#IAG) TO #L-604-NEW-LST-MCV
      MOVE #TBL-NXT-MIN-CSV  (#IAG) TO #L-604-NEW-NXT-MCV
      ADD 1 TO #TBL-COUNT (#IAG)
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF #L-604-RSV-VA-DIFF = 0
    RESET #L-604-DB
    RESET #L-604-MIN-CSV
    RESET #L-604-RSV-35-DIFF
    RESET #L-604-RSV-VA-DIFF
    RESET #L-604-NEW-PRM
    RESET #L-604-NEW-LST-MCV
    RESET #L-604-NEW-NXT-MCV
    RESET #L-604-NEW-CUR-MCV
    RESET #L-604-NEW-NPR
*
    MOVE 604           TO #L-CI-TRAN
    MOVE 'OL'          TO #L-604-FM-TYPE
    MOVE 5000.00       TO #L-604-FAC-AMT
    MOVE 'SP'          TO #L-604-TO-TYPE
    MOVE SEX           TO #L-604-SEX
    MOVE #AGE          TO #L-604-ISS-AGE
    RESET                 #L-604-OLD-PRM
*   MOVE 1             TO #L-604-MBSHPS
    MOVE #EFF-DATE-YYMMDD TO #L-604-ISS-DATE
    MOVE #EFF-DATE-YYMMDD TO #L-604-TRN-DATE
*
**  CALL 'AOCX2V6B' #L-CI-TRAN       /* SAG 11012005 RH1
    CALLNAT 'AOCX2V6B' #L-PARMS      /* SAG 11012005 RH1
*
    IF #L-604-RSV-VA-DIFF LE 0
      RESET #L-604-RSV-VA-DIFF
    END-IF
*
    MOVE ROUNDED #L-604-RSV-VA-DIFF TO #WHOLE-DOLLARS
    MOVE #WHOLE-DOLLARS             TO #L-604-RSV-VA-DIFF
*
    IF #IAG LE 20
      MOVE #AGE               TO #TBL-ISS-AGE  (#IAG)
      MOVE #L-604-RSV-VA-DIFF TO #TBL-SNG-PREM (#IAG)
      MOVE #L-604-NEW-LST-MCV TO #TBL-LST-MIN-CSV (#IAG)
      MOVE #L-604-NEW-NXT-MCV TO #TBL-NXT-MIN-CSV (#IAG)
    END-IF
  END-IF
*
  MOVE EDITED #L-604-RSV-VA-DIFF (EM=Z,Z99.99) TO #SCN-1-TIME-COST
  COMPRESS '$' #SCN-1-TIME-COST '.' INTO #SCN-1-TIME-COST LEAVING NO
*
  NEWPAGE (3)
  WRITE (3) NOTITLE USING FORM 'C2D50FIG'
  ADD 1 TO #TOT-COUNT
*
  COMPRESS 'YN' #HOLD-ID '$$' INTO #FE-EXTRACT LEAVING NO
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #FORMATED-NAME TO #FE-EXTRACT
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #FIRST-ADDRESS TO #FE-EXTRACT
  WRITE WORK FILE 1 #FE-EXTRACT
  MOVE #SECOND-ADDRESS TO #FE-EXTRACT
  WRITE WORK FILE 1 #FE-EXTRACT
*
  IF #THIRD-ADDRESS NE ' '
    MOVE #THIRD-ADDRESS TO #FE-EXTRACT
    WRITE WORK FILE 1 #FE-EXTRACT
  END-IF
*
  COMPRESS '@@' #LAST-NAME '##' INTO #FE-EXTRACT LEAVING NO
  WRITE WORK FILE 1 #FE-EXTRACT
*
  MOVE 999999                      TO #ID-NUMBER-1
  MOVE ID-NUMBER                   TO #ID-NUMBER-2
                                      #ID-NUMBER-3
  MOVE 'OL'                        TO #PLAN
  MOVE 19920901                    TO #REAL-ISS-DATE
  MOVE ISSUE-AGE                   TO #REAL-ISS-AGE
  MOVE 1                           TO #UNITS
  MOVE 0                           TO #LAST-DURATION
  MOVE 0                           TO #MONTH-IND
  MOVE 0                           TO #CONV-DUR
  MOVE '47'                        TO #PAY-STATUS
  MOVE ANNUAL-PREMIUM              TO #ANN-PREM
  MOVE 'G'                         TO #MODE
  MOVE SEX                         TO #SEX
* MOVE SMOKER-FLAG                 TO #SMOKER-FLAG
  IF SMOKER-FLAG NE 'N'
    WRITE ID-NUMBER 'SMOKER FLAG = ' SMOKER-FLAG ' RESET TO "N"'
  END-IF
*
  MOVE 'N'                         TO #SMOKER-FLAG
  MOVE DATE-OF-BIRTH               TO #DOB
  MOVE #L-604-RSV-VA-DIFF          TO #ACV
  MOVE #L-604-RSV-VA-DIFF          TO #CV
  MOVE #L-604-NEW-LST-MCV          TO #MIN-CV
  MOVE #L-604-NEW-NXT-MCV          TO #NXT-YR-MIN-CV
  MOVE 0                           TO #NXT-YR-MIN-CV
  MOVE NAME                        TO #NAME
  MOVE 5000                        TO #DB
  MOVE 0                           TO #LAST-ANN-CV
  MOVE 5000                        TO #FACE-AMT
  MOVE .8900                       TO #GROWTH-RATE
  MOVE 0                           TO #MONTHLY-ALLOC-CRED
  MOVE 0                           TO #MONTHLY-PREM-DEFIC
  MOVE 0                           TO #NET-PREM-RATIO
  RESET #LOAN-AMT
  MOVE 19920901                    TO #ISS-DATE
  MOVE ISSUE-AGE                   TO #ISS-AGE
*
  WRITE WORK 2 #RECORD-1
  WRITE WORK 2 #RECORD-2
  WRITE WORK 2 #RECORD-3
END-READ /* (0190)
*
MOVE '******' TO #RECORD-1
WRITE WORK 2 #RECORD-1
*
FOR #IAG 1 TO 20
  IF #TBL-ISS-AGE (#IAG) = 0
    ESCAPE BOTTOM
  END-IF
*
  WRITE #TBL-COUNT (#IAG) ' ISSUE-AGE ' #TBL-ISS-AGE (#IAG)
                                          ' = ' #TBL-SNG-PREM (#IAG)
END-FOR
*
WRITE 'TOTAL FORMS = ' #TOT-COUNT
*
END
