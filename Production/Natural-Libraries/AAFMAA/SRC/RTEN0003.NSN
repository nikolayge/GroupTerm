* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram RTEN0003.
/**
/** :author nguentchev
/* TODO Enter your code here
DEFINE DATA
PARAMETER
1 #CONTACT-ID (N8)
1 #NICOTINE   (A1)
1 #GENDER-P   (A1)
1 #AGE        (P3) BY VALUE
1 #DURATION   (P3)
1 IS-MORT-RT  (P11.7)
LOCAL
1 BLANK (A1) CONST <' '>
1 CNT-V VIEW OF A-CONTACTS
  2 GENDER-CD (A1)
1 A-PRODUCT-RATES VIEW OF A-PRODUCT-RATES
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 DURATION (A2)
  2 MORTALITY (N5.4)
  2 MORTALITY-AGE (A3)
  2 MORTALITY-KEY (A6)
  2 MORTALITY-ADD-KEY (A5)
  2 LTII-KEY (A14)
1 #MK
  2 #GENDER     (A1)
  2 #SMOKER     (A1)
  2 #AGE-2      (A2)
  2 #DURATION-2 (A2)
1 REDEFINE #MK
  2 #MORTALITY-KEY (A6)
1 #MAK
  2 #GENDER     (A1)
  2 #SMOKER     (A1)
  2 #AGE-3      (A3)
1 REDEFINE #MAK
  2 #MORTALITY-ADD-KEY (A5)
1 #VAR-N2  (N2)
END-DEFINE
*
IF #GENDER-P = ' '
  IF #CONTACT-ID > 0
    FIND (1) CNT-V WITH CONTACT-ID = #CONTACT-ID
      #MK.#GENDER := CNT-V.GENDER-CD
    END-FIND
  ELSE
    #MK.#GENDER := 'M'
  END-IF
ELSE
  #MK.#GENDER := #GENDER-P
END-IF
/*
IF #NICOTINE = 'S'
  #MK.#SMOKER := 'Y'
ELSE
  #MK.#SMOKER := #NICOTINE
END-IF
IF #AGE > 99
  #VAR-N2 := 99
ELSE
  #VAR-N2 := #AGE
END-IF
MOVE EDITED #VAR-N2 (EM=99) TO #MK.#AGE-2
IF #DURATION > 25
  #AGE := #AGE + #DURATION
  SUBTRACT 1 FROM #AGE
  MOVE EDITED #AGE (EM=999) TO #MAK.#AGE-3
  #MAK.#GENDER := #MK.#GENDER
  #MAK.#SMOKER := #MK.#SMOKER
ELSE
  RESET #MORTALITY-ADD-KEY
  #VAR-N2 := #DURATION
  MOVE EDITED #VAR-N2 (EM=99) TO #MK.#DURATION-2
END-IF
IF #MORTALITY-ADD-KEY NE BLANK
  FIND A-PRODUCT-RATES WITH MORTALITY-ADD-KEY = #MORTALITY-ADD-KEY
    IS-MORT-RT := A-PRODUCT-RATES.MORTALITY
  END-FIND
ELSE
  FIND A-PRODUCT-RATES WITH MORTALITY-KEY = #MORTALITY-KEY
    IS-MORT-RT := A-PRODUCT-RATES.MORTALITY
  END-FIND
END-IF
END
