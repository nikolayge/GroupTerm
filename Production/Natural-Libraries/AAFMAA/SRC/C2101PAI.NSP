* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2101SAI - Create NX message to AFSC when Trade-Up causes
*                  - causes a member to de-grandfather (and Note)
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    03242005  None    Program Written                             *
* PAM    03292005  PM1     Changed NX and Note Text as per Rick and    *
*                          Meredith                                    *
* TDM    04182006  TM1     ADDED SCC TO NX.
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING G3500AAD
LOCAL
1 #APPL-SOURCE          (A7)
1 #INS-ONLY             (A1)
1 #TO-INIT              (A3)
1 #NOTE-TEXT            (A60/10)
1 #INT                  (I2)
1 #SPLIT                (A1)
1 #STREET               (A25)
1 #STREET-2             (A25)
1 #CITY                 (A22)
1 #STATE                (A2)
1 #ERR-MSG              (A60)
1 #HOLD-CLERK           (A3)
1 #ID-NUMBER            (N6)
1 REDEFINE #ID-NUMBER
 2 #ID-NUMBER-A         (A6)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER            (N6)
* 2 TELE-NUMBER          (P11)

*
END-DEFINE
************************************************************************
*
INPUT #APPL-SOURCE #INS-ONLY
*
IF #APPL-SOURCE = 'TU'
 AND #INS-ONLY = '1' OR = '2' OR = '3'
  MOVE #CGA-SCR-ID TO #ID-NUMBER
  FIND ST-V WITH MEMBER-KEY = #ID-NUMBER-A
    IF NO RECORDS FOUND
      ESCAPE ROUTINE
    END-NOREC
*
    MOVE #CGA-CLERK TO #HOLD-CLERK
    MOVE 'EDS' TO #CGA-CLERK
*
    IF #INS-ONLY NE '1'
      RESET #NOTE-TEXT (*)
      COMPRESS #ID-NUMBER-A '- Member' INTO #NOTE-TEXT (1)
      IF #INS-ONLY = '2'
       COMPRESS #NOTE-TEXT (1) 'will contact AFSC by themselves.'
           INTO #NOTE-TEXT (1)
      ELSE
       COMPRESS #NOTE-TEXT (1) 'would like AFSC to send an application.'
           INTO #NOTE-TEXT (1)
      END-IF
*
      MOVE 'MAS' TO #TO-INIT
      MOVE '*NX' TO #CGA-SCR-SUFFIX
      FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
*
      MOVE 'SCC' TO #TO-INIT                                      /* TM1
      MOVE '*NX' TO #CGA-SCR-SUFFIX                               /* TM1
      FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
      RESET #CGA-SCR-SUFFIX
    END-IF
*
    RESET #NOTE-TEXT (*)
    DECIDE FOR FIRST CONDITION
      WHEN #INS-ONLY = '1'
        MOVE 'Member declined AFSC services when Trading Up.'
          TO #NOTE-TEXT (1)
      WHEN #INS-ONLY = '2'                                        /* PM1
        MOVE 'Member will contact AFSC by themselves.'            /* PM1
          TO #NOTE-TEXT (1)                                       /* PM1
      WHEN #INS-ONLY = '3'                                        /* PM1
        MOVE 'Member would like AFSC to send an application.'     /* PM1
          TO #NOTE-TEXT (1)                                       /* PM1
      WHEN NONE                                                   /* PM1
        IGNORE                                                    /* PM1
    END-DECIDE
    MOVE 1 TO #INT
    CALLNAT 'E4205SDM' ST-V.ID-NUMBER #INT #CGA-CLERK #NOTE-TEXT (1:10)
*
    MOVE #HOLD-CLERK TO #CGA-CLERK
  END-FIND
ELSE
  ESCAPE ROUTINE
END-IF
*
END
