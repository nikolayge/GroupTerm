* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MANF0005.
/**
/** :author nguentchev
/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Gross Amount: Sum of all Monthly Payment in the calendar year + Initial Payment + 13th Check
/*  Net Amount:   Gross Amount - Taxable Amount
/* Long Term Care Settlement
/*  Gross Amount: Sum of (each Monthly Payment - 20) in the calendar year

DEFINE FUNCTION GET-AZ-GROSS-PAY RETURNS (N7.2)
  DEFINE DATA
  PARAMETER
  1 #ISN      (P8)
  1 #DATE     (N8) BY VALUE
  1 REDEFINE #DATE
    2 PARM-YYYY (N4)
    2 PARM-MM   (N2)
    2 PARM-DD   (N2)
  LOCAL
  1 ANNUITANTS VIEW OF A-ANNUITANTS
    2 POLICY-NUMBER     (A16)
    2 SETTLEMENT-OPTION (A2)
    2 MONTHLY-PENSION   (N7.2)
    2 WITHDRAWALS       (N7.2)
    2 INITIAL-PAYMENT   (N7.2)
    2 DEPOSIT           (N7.2)
    2 T13TH-CHECK       (N7.2)
    2 TAXABLE           (N7.2)
    2 START-DATE (N8.0) 2 REDEFINE START-DATE
      3 START-YYYY (N4)
    2 WITHDRAWAL-DATE   (N8)
    2 REDEFINE WITHDRAWAL-DATE
      3 WD-YYYY (N4)
      3 WD-MM   (N2)
      3 WD-DD   (N2)
  1 #INTEREST-ONLY (A2) CONST <'IO'>
  1 #PRO-RATE-X    (N7.2)
  1 A-ANNU-HISTORY VIEW OF A-ANNU-HISTORY
    2 ADD-DATE          (N8)
    2 MONTHLY-PENSION   (N7.2)
  1 #TODAY (N8) 1 REDEFINE #TODAY
    2 #YYYY  (N4)
    2 #MM    (N2)
    2 #DD    (N2)
  END-DEFINE
/*
  RESET GET-AZ-GROSS-PAY
  GET ANNUITANTS #ISN
  #TODAY := *DATN
IF PARM-YYYY = START-YYYY
  GET-AZ-GROSS-PAY := ANNUITANTS.INITIAL-PAYMENT
END-IF
GET-AZ-GROSS-PAY := GET-AZ-GROSS-PAY
  + GET-AZ-MONTHLY-PAY (< #ISN, #DATE >)

ADD 1 TO #DATE.PARM-MM
REPEAT UNTIL #DATE.PARM-MM > 12
    OR #DATE.PARM-MM = #TODAY.#MM
  GET-AZ-GROSS-PAY := GET-AZ-GROSS-PAY
    + GET-AZ-MONTHLY-PAY (< #ISN, #DATE >)
  ADD 1 TO #DATE.PARM-MM
END-REPEAT
END-FUNCTION
END
