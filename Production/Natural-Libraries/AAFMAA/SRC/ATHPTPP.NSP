* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPTPP  TPP / CSV Comparisons
*
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  04014005 DW1  Reset VW Premium Errors
* DRW  05152005 DW2  Add face Amt & TPP linkage to CVALUSXX
* DRW  06202005 DW3  Include New pols this mon in reports
* PAM  08162005 PM4  Exclude policies with partial terminations
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII
* DRW  03082006 DW5  If today is > 24 use next month day 1 to calc date
* DRW  03272006 DW6  Chg format of issue-date from n to a for compare
* RSE  09302009 RE1  Change 600K logic for Unification
* YAK  10102012 YK1 Use function to identify Level Term II plans
* TMT  06102014 TT1 CONTACT NORMALIZATION - DOB AND SEX
*******************************************
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A   (A6)
  2 MEMBER-CONTACT-ID                     /* TT1
  2 INTRO-FLAG
  2 ONLINE-ACTION-GN
  2 DEBIT-CREDIT-AMT
  2 RESTRICT-MBR-SW
*  2 DATE-OF-BIRTH                       /* TT1
  2 MTD-ACV-PREM-ADJ (9)
  2 ONLINE-ACTION (9)
  2 ISSUE-AGE    (9)
  2 STATUS       (9)
  2 MODE         (9)
  2 FACE-AMOUNT  (9)
  2 PAY-STATUS   (9)
  2 PAID-TO-DATE (9)
  2 ANNUAL-PREM  (9)
  2 ISSUE-DATE   (9)
  2 REDEFINE ISSUE-DATE       /* PM4
    3 ISSUE-DATE-OCC (9)
      4 ISSUE-YYYY    (A4)   /* DW6
      4 ISSUE-MM      (A2)
      4 ISSUE-DD      (A2)
  2 CASH-VALUE   (9)
  2 COST-BASIS   (9)
  2 X-FIRST-PLACE-FEE (9)
  2 X-BILL-ADJUST (9)
  2 MONTH-ALLOC-AMT  (9)
  2 CONV-DURATION  (9)
  2 TOT-PREMIUMS-PAID (9)
  2 PREM-RATING       (9)
  2 MEC-IND           (9)
  2 PLAN         (9)
  2 REDEFINE PLAN
    3 PLAN-OCC    (9)
      4 PLAN-A1  (A1)
      4 PLAN-A2  (A2)
*
1 VA-V VIEW OF A-VA-HISTORY
  2 YEAR-MONTH-M        (P7/13)
  2 PREMIUM-M           (13)
  2 ACCUM-CASH-VALUE-M  (13)
  2 INTEREST-M          (13)
  2 COST-OF-INSURANCE-M (13)
  2 PRORAT-LOAN-BAL-M   (13)
  2 PRORAT-LN-ACC-INT-M (13)
*
1 NT-V VIEW OF A-NOTES       /* PM4
  2 NOTE-TABLE       (60)
    3 NOTE-TEXT       (A60)
    3 REDEFINE NOTE-TEXT       /* Partial termination
      4 NT-PT-1        (A5)     /* '* 200'
      4 NT-PT-K1       (A5)     /* 'K OF '
      4 NT-PT-2        (A3)     /* '400'
      4 NT-PT-K2       (A13)    /* 'K ON POL ISS '
      4 NT-PT-ISS-MM   (A2)    /* DW6
      4 NT-PT-ISS-/1   (A1)
      4 NT-PT-ISS-DD   (A2)
      4 NT-PT-ISS-/2   (A1)
      4 NT-PT-ISS-YYYY (A4)
      4 NT-PT-REST     (A24)
*
1 #PARTIAL-TERM     (L)      /* PM4
1 #EXTRACT   (A35)
1 #VA-KEY    (N15)
1 REDEFINE #VA-KEY
  2 #VA-KEY-CN      (N6)
  2 #VA-KEY-ISS-DT  (N8)
  2 #VA-KEY-SPL-IND (N1)
1 #TOT-VA-PREM-PD   (N7.2)
1 #MON-B4-ISS-DT    (N6)
1 #TPP-DIFF         (N8.2)
1 REDEFINE #TPP-DIFF
  2 #TPP-DIFF-FILL    (A3)
  2 #TPP-DIFF-A       (A7)
1 #MONS-OFF     (N5.2)
1 REDEFINE #MONS-OFF
  2 #QUOT    (N5)
  2 #REM     (N2)
1 #DIFF-EQ-MON    (P3)
1 #DIFF-EQ-MON-X  (P3)
1 #PCT       (N5.3)
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #SUFFIX    (A3)
1 #I1        (I2)
1 #COI       (N7.2)
1 #COI-EXCEEDS-INT (L)
1 #I2        (I2)
1 #I3        (I2)
1 #CNT-2     (N5)
1 #CNT-3     (N5)
1 #CNT-4     (N5)
1 #CNT-5     (N5)
1 #CNT-6     (N5)
1 #CNT-7     (N5)
1 #CNT-8     (N5)
1 #AS-OF-DT   (N8)
1 #LOAN-BAL   (N7.2/9)
1 #LOAN-BAL-2 (N7.2/9)
1 #ACC-INT    (N7.2/9)
1 #ACC-INT-2  (N7.2/9)
1 #CSV        (N7.2/9)
1 #CVALUE     (N7.2/9)
1 #LVALUE     (N7.2/9)
1 #MCSV       (N7.2/9)
1 #DB         (N7.2/9)
1 #ANN-PREMIUM (N7.2/9)
1 #FACE-AMT    (N7.2/9)  /* DW2
1 #TPP         (N7.2/9)  /* DW2
1 #MON-PREM    (N5.2)
1 REDEFINE #MON-PREM
  2 #MON-PREM-A  (A7)
1 #CSV-DIFF     (N7.2)
1 #TOT-CSV-DIFF (N9.2)
1 #CAP-CSV-DIFF     (N7.2)
1 #CAP-TOT-CSV-DIFF (N9.2)
1 #1-YEAR-AGO       (N8)
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM (N6)
  2 REDEFINE #YYYYMM
    3 #YYYY   (N4)
    3 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A   (A4)
  2 REDEFINE #YYYY-A
    3 #CENT-A   (A2)
    3 #YY-A      (A2)
  2 #MM-A     (A2)
  2 #DD-A     (A2)
*
1 #DATE-X   (A12)
1 #ACCUM-ACV      (P7.2)
1 #NEW-ACV        (P7.2)
1 #NEW-REAL-ACV   (P7.2)  /* DW1
1 #ADJUSTED-PREM  (N7.2)  /* DW1
1 REDEFINE #ADJUSTED-PREM
  2 #ADJ-PREM-1ST-4 (N4)
  2 #ADJ-PREM-3     (N3.2)
1 #PREV-ACV       (P7.2)
1 #ACV-DIFF       (P7.2)
1 #ACV-DIFF-REAL  (P7.2)  /* DW1
1 #REAL-PREM      (N6.2)
*
* BELOW for CALC-PREMS-PAID
1 #PP-DATE-OF-BIRTH  (N8)
1 #PP-ANNUAL-PREM    (N5.2)
1 #PP-PAY-STATUS     (A2)
1 #PP-TOT-PREMIUMS-PAID (N7.2)
1 #PP-PAID-TO-DATE   (N8)
1 #PP-ISSUE-DATE     (N8)
1 #PP-MODE           (A1)
1 #PP-PLAN           (A3)
1 #PP-COST-BASIS      (N7.2)
1 #PP-MONTH-ALLOC-AMT (N7.2)          /*TT1
1 #PP-INTRO-FLAG     (A1)
1 #PP-FIRST-PLACE-FEE (N7.2)
1 #PREMS-PAID         (P7.2)
1 #SUB-PREMS-PAID     (P7.2)
1 #PP-BADJ            (P5.2)
* ABOVE for CALC-PREMS-PAID
*
1 DATE-OF-BIRTH       (N8.0)
END-DEFINE
* * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Exceptional Conditions'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Exceptional NON-CAP TPP exceeds CSV'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'RPU WITH TPP EXCEEDING CSV'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'CAP POLS WITH TPP EXCEEDING CSV'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=133 PS=76
WRITE  (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'TPP Exceeds CSV by ar least 25% or DIFF gtr $1000'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (6) LS=133 PS=76
WRITE  (6) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'TPP does not = CSV prems on less than 1 year policies'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (7) LS=133 PS=76
WRITE  (7) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'PD-UP Mec"s w/o Cost basis or PP with Cost-basis'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (8) LS=133 PS=76
WRITE  (8) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Corrected VW Premiums'
  5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #YYYYMMDD
*
IF #DD GE 25   /* use next mon day 1 as date    /* DW5
  MOVE 1 TO #DD /* to handle condition of running athptpp after
  ADD 1 TO #MM  /* running M/e and before actual system date of next mon
  IF #MM GT 12
    SUBTRACT 12 FROM #MM
    ADD 1 TO #YYYY
  END-IF
END-IF
*
SUBTRACT 1 FROM #YYYY
MOVE #YYYYMMDD TO #1-YEAR-AGO
*
READ ST-V BY MEMBER-KEY FROM '001900'
* READ ST-V BY MEMBER-KEY FROM '060079'
*  IF ST-V.ID-NUMBER GE 600000               RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)   /* RE1
    ESCAPE TOP                            /* RE1
* IF ST-V.ID-NUMBER GT 107073
*    ESCAPE BOTTOM                           RE1
  END-IF
*
  IF ST-V.RESTRICT-MBR-SW = 'Y'
    ESCAPE TOP
  END-IF
*
  MOVE *DATN TO #AS-OF-DT
  MOVE 'NTP' TO #SUFFIX
  MOVE FACE-AMOUNT (*) TO #FACE-AMT (*)
  CALLNAT 'CVALUSXX'
    ST-V.ID-NUMBER
    #SUFFIX
    #AS-OF-DT
    #LOAN-BAL   (1:9)
    #LOAN-BAL-2 (1:9)
    #ACC-INT    (1:9)
    #ACC-INT-2  (1:9)
    #CSV        (1:9)
    #CVALUE     (1:9)
    #LVALUE     (1:9)
    #MCSV       (1:9)
    #DB         (1:9)
    #ANN-PREMIUM  (1:9)
    #FACE-AMT   (1:9)   /* DW2
    #TPP        (1:9)   /* DW2
*
  FOR #I1 1 TO 9
    IF (STATUS (#I1) NE 'D')
*    OR (PLAN (#I1) = 'RT' OR = 'LT' OR = 'SO')                     /* YK1
*    OR ((PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z'))         /* YK1
        OR (PLAN (#I1) = 'SO')                                         /* YK1
        OR FUNCTION-TERM-POLICY(<PLAN(#I1)>)                           /* YK1
*    OR (ONLINE-ACTION-GN = '2')     /* REMOVED  6/20/2005  /* DW3
*    OR (ONLINE-ACTION (#I1) = '1')   /* REMOVED  6/20/2005  /* DW3
*    OR (CONV-DURATION (#I1) = 999)
      ESCAPE TOP
    END-IF
*
    RESET #PARTIAL-TERM      /* PM4
    FIND NT-V WITH NT-V.ID-NUMBER = ST-V.ID-NUMBER
      FOR #I2 1 TO 60
        IF NT-V.NOTE-TEXT (#I2) = ' '
          ESCAPE BOTTOM
        END-IF
*
        IF NT-V.NT-PT-K1 (#I2) = 'K OF '
            AND NT-V.NT-PT-K2 (#I2) = 'K ON POL ISS '
            AND NT-V.NT-PT-ISS-MM (#I2) = ST-V.ISSUE-MM (#I1)
            AND NT-V.NT-PT-ISS-DD (#I2) = ST-V.ISSUE-DD (#I1)
            AND NT-V.NT-PT-ISS-YYYY (#I2) = ST-V.ISSUE-YYYY (#I1)
          MOVE TRUE TO #PARTIAL-TERM
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
*
    IF #PARTIAL-TERM
      ESCAPE TOP
    END-IF                    /* PM4
*
    IF  ((PAY-STATUS (#I1) GT '40') AND (COST-BASIS (#I1) = 0)
        AND (MEC-IND (#I1) = 'Y'))
        OR ((PAY-STATUS (#I1) LT '40') AND (COST-BASIS (#I1) NE 0)
        AND (PLAN (#I1) NE MASK (..'F'))
        AND (PLAN (#I1) NE MASK (.'F')))
      ADD 1 TO #CNT-7
      DISPLAY (7) #CNT-7 ST-V.ID-NUMBER ST-V.ISSUE-DATE (#I1)
        'Pay/Stat' PAY-STATUS (#I1)
        'Pln'      PLAN (#I1)
        'Cost/Basis' COST-BASIS (#I1)
        'Mec/Ind' MEC-IND (#I1)
    END-IF
*
    PERFORM CALC-PREMS-PAID-SETUP
*
    IF ISSUE-DATE (#I1) GE #1-YEAR-AGO
      RESET #VA-KEY #TOT-VA-PREM-PD
*
      IF MTD-ACV-PREM-ADJ (#I1) NE 9999999.99
        ADD MTD-ACV-PREM-ADJ (#I1) TO #TOT-VA-PREM-PD
      END-IF
*
      MOVE ST-V.ID-NUMBER TO #VA-KEY-CN
      MOVE ST-V.ISSUE-DATE (#I1) TO #VA-KEY-ISS-DT
*
      FIND (1) VA-V WITH RECORD-KEY = #VA-KEY
        MOVE ST-V.ISSUE-DATE (#I1) TO #YYYYMMDD
        SUBTRACT 1 FROM #MM
        IF #MM LT 1
          ADD 12 TO #MM
          SUBTRACT 1 FROM #YYYY
        END-IF
        MOVE #YYYYMM TO #MON-B4-ISS-DT
*
        FOR #I2 1 TO 13
          IF VA-V.YEAR-MONTH-M (#I2) NE 0
            IF #I2 GT 1
              MOVE VA-V.ACCUM-CASH-VALUE-M (#I2 - 1) TO #PREV-ACV
            ELSE
              RESET #PREV-ACV
            END-IF
*
            COMPUTE #NEW-ACV = #PREV-ACV + VA-V.PREMIUM-M   (#I2)
              + VA-V.INTEREST-M          (#I2)
              - VA-V.COST-OF-INSURANCE-M (#I2)
*
            IF   (#I1 LE 9)
                AND (ISSUE-DATE (#I1) GE 19970101) /* 1997 pol
                AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
              SUBTRACT VA-V.PRORAT-LOAN-BAL-M   (#I2) FROM #NEW-ACV
              SUBTRACT VA-V.PRORAT-LN-ACC-INT-M (#I2) FROM #NEW-ACV
            END-IF
*
            IF #NEW-ACV GT VA-V.ACCUM-CASH-VALUE-M (#I2)
              COMPUTE #ACV-DIFF =
                #NEW-ACV - VA-V.ACCUM-CASH-VALUE-M (#I2)
            ELSE
              COMPUTE #ACV-DIFF =
                VA-V.ACCUM-CASH-VALUE-M (#I2) - #NEW-ACV
            END-IF
*
            MOVE VA-V.PREMIUM-M (#I2) TO #REAL-PREM
            IF #ACV-DIFF NE 0
                AND ((#I2 NE 1)   /* ADDED 12/98 FOR ALC
                OR  (VA-V.YEAR-MONTH-M (#I2) = #MON-B4-ISS-DT))
              PERFORM CALC-REAL-PREM
              COMPUTE #NEW-REAL-ACV = #PREV-ACV + #REAL-PREM      /* DW1
                + VA-V.INTEREST-M          (#I2)
                - VA-V.COST-OF-INSURANCE-M (#I2)
*
              IF   (#I1 LE 9)                                    /* DW1
                  AND (ISSUE-DATE (#I1) GE 19970101) /* 1997 pol
                  AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
                SUBTRACT VA-V.PRORAT-LOAN-BAL-M  (#I2) FROM #NEW-REAL-ACV
                SUBTRACT VA-V.PRORAT-LN-ACC-INT-M(#I2) FROM #NEW-REAL-ACV
              END-IF
*
              IF #NEW-REAL-ACV NE VA-V.ACCUM-CASH-VALUE-M (#I2)  /* DW1
                IF #NEW-REAL-ACV GT VA-V.ACCUM-CASH-VALUE-M(#I2) /* DW1
                  COMPUTE #ACV-DIFF-REAL =
                    #NEW-REAL-ACV - VA-V.ACCUM-CASH-VALUE-M (#I2)
                ELSE
                  COMPUTE #ACV-DIFF-REAL =
                    VA-V.ACCUM-CASH-VALUE-M (#I2) - #NEW-REAL-ACV
                END-IF
*
                MOVE #REAL-PREM TO #ADJUSTED-PREM     /* DW1
                ADD #ACV-DIFF-REAL TO #ADJUSTED-PREM
                ADD 1 TO #CNT-8
*                 DISPLAY (8) #CNT-8 'CN' ST-V.ID-NUMBER           /* DW1
*                    'ISSUE/DATE' ST-V.ISSUE-DATE (#I1)
* *                   #I2
*                     'VW/DATE' VA-V.YEAR-MONTH-M (#I2)
* *                   'ACV/DIFF' #ACV-DIFF
* *                   #NEW-ACV
*                     'NEW/REAL/ACV' #NEW-REAL-ACV
*                     'AMT TO/ADJ/PREMIUM' #ACV-DIFF-REAL
* *                   'VA/PREM/BEFORE' VA-V.PREMIUM-M (#I2)
*                     'REAL/PREM' #REAL-PREM
*                     'VW/NEW/adj/PREM' #ADJ-PREM-3
              END-IF
            END-IF
*
            ADD #REAL-PREM TO #TOT-VA-PREM-PD
          END-IF
        END-FOR
      END-FIND
*
      IF #PREMS-PAID NE #TOT-VA-PREM-PD
        COMPUTE #TPP-DIFF = (#PREMS-PAID - #TOT-VA-PREM-PD)
        COMPUTE #MON-PREM = ANNUAL-PREM (#I1) / 12
*
        RESET #MONS-OFF
        COMPUTE #MONS-OFF = #TPP-DIFF / #MON-PREM
*
        IF #REM NE 0
          RESET #QUOT
        END-IF
*
        IF #QUOT GT 0
          ADD 1 TO #DIFF-EQ-MON
          PERFORM ADJUST-ACV
        END-IF
*
        IF #QUOT LT 0
          ADD 1 TO #DIFF-EQ-MON-X
        END-IF
*
        ADD 1 TO #CNT-6
        DISPLAY (6) 'CNT6' #CNT-6 (EM=ZZ9)
          'CN' ST-V.ID-NUMBER
          'ISS/Date' ST-V.ISSUE-DATE (#I1)
          'VA/CSV/Prems' #TOT-VA-PREM-PD (EM=ZZZZZZ.99)
          'TPP'       #PREMS-PAID        (EM=ZZZZZZ.99)
          'Diff'      #TPP-DIFF          (EM=-ZZZZZ.99)
          'DB/CR'     DEBIT-CREDIT-AMT   (EM=ZZ,ZZZ.99)
          'MON/PREM'  #MON-PREM          (EM=ZZZZ.99)
          'NM/MNS'    #QUOT              (EM=Z9)
          'Pln'       PLAN (#I1)
*         'MD'      MODE (#I1)
*         'ACV/PRM/ADJ' MTD-ACV-PREM-ADJ (#I1)
          'VW-xtr/PREM/PD' TOT-PREMIUMS-PAID  (#I1) (EM=ZZZZZZ.99)
          'FPF'       X-FIRST-PLACE-FEE (#I1)   (EM=-ZZZZ.99)
          'BADJ'      X-BILL-ADJUST (#I1)       (EM=-ZZZZ.99)
      END-IF
    END-IF
*
    IF #PREMS-PAID GT #CSV (#I1)    /* TPP exceeds CSV
*   IF #PREMS-PAID GT CASH-VALUE (#I1) /* TPP exceeds Cv
      IF PAY-STATUS (#I1) = '45'    /* RPU's bypass TPP exceeds test
        ADD 1 TO #CNT-3
        DISPLAY (3) #CNT-3 ST-V.ID-NUMBER
          'Iss-DT' ISSUE-DATE (#I1)
          'Rate'    PREM-RATING (#I1)
          'CSV' #CSV (#I1)
          'TPP' #PREMS-PAID
          'Mode' MODE (#I1)
          'Face' FACE-AMOUNT  (#I1)
      ELSE
        IF CONV-DURATION (#I1) = 999  /* CAPS after 1/1/1999
*         IF ISSUE-DATE (#I1) LT 19990101
          COMPUTE #CAP-CSV-DIFF = #PREMS-PAID - #CSV (#I1)
          ADD #CAP-CSV-DIFF TO #CAP-TOT-CSV-DIFF
          ADD 1 TO #CNT-4
          DISPLAY (4) #CNT-4 ST-V.ID-NUMBER
            'Iss-DT' ISSUE-DATE (#I1)
            'Rate'    PREM-RATING (#I1)
            'CSV' #CSV (#I1)
            'TPP' #PREMS-PAID
            'Mode' MODE (#I1)
            'Pay/Stat' PAY-STATUS (#I1)
            'Face' FACE-AMOUNT  (#I1)
*         END-IF
        ELSE      /* all else (use TPP)
          RESET #COI-EXCEEDS-INT #COI
          RESET #VA-KEY #TOT-VA-PREM-PD
          MOVE ST-V.ID-NUMBER TO #VA-KEY-CN
          MOVE ST-V.ISSUE-DATE (#I1) TO #VA-KEY-ISS-DT
          FIND (1) VA-V WITH RECORD-KEY = #VA-KEY
            MOVE VA-V.COST-OF-INSURANCE-M (13) TO #COI
*
            IF   (ISSUE-DATE (#I1) GE 19970101) /* 1997 pol
                AND (MONTH-ALLOC-AMT (#I1) NE 999.99)
              ADD VA-V.PRORAT-LOAN-BAL-M   (13) TO #COI
              ADD VA-V.PRORAT-LN-ACC-INT-M (13) TO #COI
            END-IF
*
            IF   (#COI GT VA-V.INTEREST-M (13))
                AND (VA-V.INTEREST-M (13) NE 0)
              MOVE TRUE TO #COI-EXCEEDS-INT
*             IF ISSUE-DATE (#I1) LT #1-YEAR-AGO
*               WRITE ST-V.ID-NUMBER ST-V.ISSUE-DATE (#I1)
*                    'COI exceeds INT' #COI VA-V.INTEREST-M (13)
*             END-IF
            END-IF
          END-FIND
*
          COMPUTE #CSV-DIFF = #PREMS-PAID - #CSV (#I1)
*
          IF #CSV (#I1) LT 0
            MOVE 999.99 TO #PCT
          ELSE
            COMPUTE ROUNDED #PCT =
              ((#PREMS-PAID / #CSV (#I1)) * 100) - 100
          END-IF
*
          ADD #CSV-DIFF TO #TOT-CSV-DIFF
*      DISPLAY #COI VA-V.INTEREST-M (13) #PREMS-PAID #CSV(#I1) #PCT
*
          IF   (NOT #COI-EXCEEDS-INT)
              AND ((ISSUE-DATE (#I1) LT #1-YEAR-AGO) OR (#PCT GT 50))
              AND ((ISSUE-AGE  (#I1) LT '60') OR (#PCT GT 15))
              AND ((ISSUE-AGE  (#I1) GT '20') OR (#PCT GT 15))
            ADD 1 TO #CNT-2
            DISPLAY (2) #CNT-2 (EM=ZZZ9)
              'CN' ST-V.ID-NUMBER
              'Iss/DT' ISSUE-DATE (#I1)
              'Iss/AG' ISSUE-AGE (#I1)
              'RT'      PREM-RATING (#I1)
              'CSV' #CSV (#I1)   (EM=ZZZZ,ZZZ.99-)
*             'CV'  CASH-VALUE (#I1)
              'TPP' #PREMS-PAID (EM=ZZZZ,ZZZ.99)
              'PCT' #PCT    (EM=ZZZ.99)
              'Dif' #CSV-DIFF   (EM=ZZZZ.99)
              'MD'  MODE (#I1)
              'Pay/Stat' PAY-STATUS (#I1)
              'Face' FACE-AMOUNT  (#I1)
              'Conv/Dur' CONV-DURATION  (#I1)
*
            IF  (#PCT GT 25)
                OR (#CSV-DIFF GT 1000)
              ADD 1 TO #CNT-5
              DISPLAY (5) #CNT-5 (EM=ZZZ9)
                'CN' ST-V.ID-NUMBER
                'Iss-DT' ISSUE-DATE (#I1)
                'Iss/AG' ISSUE-AGE (#I1)
                'Rate'    PREM-RATING (#I1)
                'CSV' #CSV (#I1)
*               'CV'  CASH-VALUE (#I1)
                'TPP' #PREMS-PAID
                'PCT' #PCT
                'Dif' #CSV-DIFF
                'Mode' MODE (#I1)
                'Pay/Stat' PAY-STATUS (#I1)
                'Face' FACE-AMOUNT  (#I1)
                'Conv/Dur' CONV-DURATION  (#I1)
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-FOR
END-READ
WRITE (2) /// 'tot-csv diff=' #TOT-CSV-DIFF
WRITE (4) /// 'tot-csv diff=' #CAP-TOT-CSV-DIFF
WRITE (6) 'tot-mon equal difference  =' #DIFF-EQ-MON
WRITE (6) 'tot-mon equal diff inverse=' #DIFF-EQ-MON-X
*
* * * * *
DEFINE CALC-PREMS-PAID-SETUP
* * * * *
*
RESET #PREMS-PAID DATE-OF-BIRTH                                        /* TT1
DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)           /* TT1
*
MOVE DATE-OF-BIRTH           TO #PP-DATE-OF-BIRTH
MOVE ANNUAL-PREM (#I1)       TO #PP-ANNUAL-PREM
MOVE PAY-STATUS (#I1)        TO #PP-PAY-STATUS
MOVE TOT-PREMIUMS-PAID (#I1) TO #PP-TOT-PREMIUMS-PAID
MOVE PAID-TO-DATE (#I1)      TO #PP-PAID-TO-DATE
MOVE ISSUE-DATE (#I1)        TO #PP-ISSUE-DATE
MOVE MODE (#I1)              TO #PP-MODE
MOVE PLAN (#I1)              TO #PP-PLAN
MOVE COST-BASIS (#I1)        TO #PP-COST-BASIS
MOVE MONTH-ALLOC-AMT (#I1)   TO #PP-MONTH-ALLOC-AMT
MOVE INTRO-FLAG              TO #PP-INTRO-FLAG
MOVE X-FIRST-PLACE-FEE (#I1) TO #PP-FIRST-PLACE-FEE
MOVE X-BILL-ADJUST     (#I1) TO #PP-BADJ
*
CALLNAT 'GPRPDSCO'
  #PP-DATE-OF-BIRTH
  #PP-ANNUAL-PREM
  #PP-PAY-STATUS
  #PP-TOT-PREMIUMS-PAID
  #PP-PAID-TO-DATE
  #PP-ISSUE-DATE
  #PP-MODE
  #PP-PLAN
  #PP-COST-BASIS
  #PP-MONTH-ALLOC-AMT
  #PP-INTRO-FLAG
  #PP-FIRST-PLACE-FEE
  #PREMS-PAID
  #SUB-PREMS-PAID
  #PP-BADJ
*
END-SUBROUTINE
*
*
**********************************
DEFINE SUBROUTINE CALC-REAL-PREM
**********************************
*
FOR #I3 1 TO 800
  IF   (#ACV-DIFF GT ((#I3 * 1000) - 10))
      AND (#ACV-DIFF LT ((#I3 * 1000) + 10))
    IF #NEW-ACV LT VA-V.ACCUM-CASH-VALUE-M (#I2)
      ADD (#I3 * 1000) TO #REAL-PREM
    ELSE
      SUBTRACT (#I3 * 1000) FROM #REAL-PREM
    END-IF
*
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE ADJUST-ACV
* * * * * * * * * * * * * *
*
MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
IF #MM = 2
  MOVE 29 TO #DD
ELSE
  IF #MM = 9 OR = 4 OR = 6 OR = 11
    MOVE 30 TO #DD
  ELSE
    MOVE 31 TO #DD
  END-IF
END-IF
*
COMPRESS #MM-A #DD-A #YY-A INTO #DATE-X WITH '/'
*
COMPRESS ST-V.ID-NUMBER-A
  #TAB  ISSUE-DATE (#I1)
  #TAB  #TPP-DIFF-A
  #TAB  #DATE-X
  INTO #EXTRACT LEAVING NO
* WRITE WORK FILE 2 #EXTRACT
*
END-SUBROUTINE
*
END
