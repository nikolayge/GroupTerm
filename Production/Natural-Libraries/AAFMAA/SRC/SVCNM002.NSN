* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
DEFINE DATA
PARAMETER USING OBJAMB02
LOCAL
1 i           (I4)
1 j           (I4)

1 #MAP-FIELDS
  2 #SELECTION-BOX (A1/1:15)
  2 #SERVICE-NAMES (A60/1:15)
  
1 #CV (C/15)
  
END-DEFINE
*
SET KEY PF3  NAMED 'Exit'
SET KEY ENTR
*
#CV(*) := (AD=P)
*
PERFORM POPULATE-MAP-FIELDS
*

REPEAT
  INPUT USING MAP 'SVCMM003'

  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE ROUTINE
    VALUE 'ENTR'
      PERFORM SELECT-SERVICE-PLAN
    NONE
      IGNORE
  END-DECIDE

END-REPEAT

**===================================
DEFINE SUBROUTINE POPULATE-MAP-FIELDS
**===================================
FOR i 1 TO #SVC-COUNT
  #SERVICE-NAMES(i) := GET-PLAN-INFO(<SVC-PLAN(i)>)(1)
  RESET #CV(i)
END-FOR
**============
END-SUBROUTINE
**============

**===================================
DEFINE SUBROUTINE SELECT-SERVICE-PLAN
**===================================
EXAMINE #SELECTION-BOX(*) FOR 'X' INDEX j
RESET #SELECTION-BOX(*)
IF j = 0      /* If nothing selected pick the first service plan on the list
  j := 1
END-IF

CALLNAT 'SVCNM001' SVC-ID-NUMBER(1) SVC-PLAN(j)
**============
END-SUBROUTINE
**============
END
