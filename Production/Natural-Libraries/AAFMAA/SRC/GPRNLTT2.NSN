* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* SUBPROGRAM-ID: GPRNLTT2 - Subprogram to get rates
*************************************************************
*        MODIFICATION LOG
*************************************************************
* USER   DATE      TAG REASON
* NGG    20121107      Initial Creation
*************************************************************
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
LOCAL
1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
  2 ##GENDER          (A1)
  2 ##LTII-TYPE       (A2)
  2 ##NICOTINE        (A1)
  2 ##AGE             (A2)
  2 ##EFFECTIVE-DATE  (A8)
*
1 LTII-V VIEW OF A-PRODUCT-RATES
  2 VAR-TERM-RATE (N3.2/65)
  2 EFFECTIVE-DATE (A8)
  2 GENDER (A1)
  2 LTII-TYPE (A2)
  2 NICOTINE (A1)
  2 AGE (A2)
  2 RIDER-OPTION (A1)
  2 LTII-KEY     (A14)
*
1 RATE-ARRAY     (A6/20,4)
1 TERM-ARRAY     (A2/20,4)
1 AGE-ARRAY      (A2/20,4)
1 page-is-full   (l)
1 PAGE-INDEX     (I4)
1 RECORD-INDEX   (N2)
1 J              (I4)
1 TERM           (N2)
1 L              (I4)
END-DEFINE
*
RESET PAGE-INDEX PAGE-IS-FULL
RECORD-INDEX := 1 L:= 1
INPUT USING MAP 'GPREMM11'
REPEAT
  PERFORM FILL-IN
  INPUT USING MAP 'GPREMM11'
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
  RESET PAGE-INDEX PAGE-IS-FULL
    RATE-ARRAY (*,*) TERM-ARRAY (*,*) AGE-ARRAY (*,*)
  L := 1
  IF TERM >= 5 AND TERM <= 69
    RECORD-INDEX := TERM - 4
  END-IF
END-REPEAT
MOVE 'GM' TO #CGA-SCR-COMMAND
FETCH 'G1000PXX'
*
DEFINE FILL-IN
/*     =======
RL1.
READ LTII-V BY LTII-KEY STARTING FROM #LTII-KEY
  IF SUBSTR ( LTII-KEY,1,4) NE SUBSTR ( #LTII-KEY,1,4) /* Gender+Type+Nicotine
    #LTII-KEY := LTII-KEY
    IF ##GENDER = 'G'
      RESET #LTII-KEY
    END-IF
    RECORD-INDEX := 1
    ESCAPE BOTTOM
  END-IF
  REPEAT UNTIL RECORD-INDEX > 65
    PERFORM ADD-TO-CURRENT-PAGE
    IF PAGE-IS-FULL
      #LTII-KEY := LTII-KEY
      ESCAPE BOTTOM (RL1.)
    END-IF
  END-REPEAT
  RECORD-INDEX := 1
END-READ
END-SUBROUTINE
/*
DEFINE ADD-TO-CURRENT-PAGE
/*     ===================
IF PAGE-INDEX > 20
    AND L >= 4
  MOVE TRUE TO PAGE-IS-FULL
  ESCAPE ROUTINE
END-IF
FOR J=RECORD-INDEX TO 65
  IF VAR-TERM-RATE (J) <= 0
    RECORD-INDEX := 66
    ESCAPE ROUTINE
  END-IF
  ADD 1 TO PAGE-INDEX
  IF PAGE-INDEX > 20
    ADD 1 TO L
    IF L > 4
      PAGE-IS-FULL := TRUE
      ESCAPE BOTTOM
    END-IF
    PAGE-INDEX := 1
  END-IF
  AGE-ARRAY ( PAGE-INDEX, L ) := AGE
  TERM := J + 4
  MOVE EDITED TERM (EM=99) TO TERM-ARRAY ( PAGE-INDEX, L )
  MOVE EDITED VAR-TERM-RATE (J) (EM=ZZ9.99) TO RATE-ARRAY ( PAGE-INDEX, L )
END-FOR
RECORD-INDEX := J
TERM := RECORD-INDEX + 4
IF TERM > 65
  TERM := 5
END-IF
END-SUBROUTINE
*
END
