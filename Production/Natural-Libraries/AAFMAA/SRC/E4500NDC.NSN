* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* E4500NDC
* NGG 06/16/2014   NG2  Contact Normalization - DOB and Sex
define data
parameter
1 #status-isn    (P8)
1 #NAME-LBL      (A6)
1 #MAIN-RANK     (A3)
1 #MAIN-NAME     (A25)
1 #MAIN-SSN      (A9)
1 #MAIN-DOB      (A8)
1 #MAIN-DOD      (A8)

local
1 ST-V VIEW OF A-STATUS
  2 WIDOW-FLAG
  2 RANK
  2 NAME
  2 SSN
  2 MEMBER-CONTACT-ID
  2 SPOUSE-CONTACT-ID
  2 NAME-SP
  2 SSN-SP
*   2 DATE-OF-BIRTH                      /* TT1
  2 DATE-OF-DEATH

1 CONTACT VIEW OF A-CONTACTS
  2 FIRST-NAME              (A25)
  2 MIDDLE-NAME             (A25)
  2 LAST-NAME               (A25)
  2 SUFFIX                  (A10)
  2 DATE-OF-BIRTH           (N8)
  2 DATE-OF-DEATH           (N8)
  2 SSN                     (N9)

1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-A (A8)
end-define

GET ST-V #status-isn

IF ST-V.WIDOW-FLAG = 'Y'
  MOVE 'SPOUSE' TO #NAME-LBL
  RESET            #MAIN-RANK
  MOVE ST-V.NAME-SP  TO #MAIN-NAME
  MOVE EDITED ST-V.SSN-SP (EM=Z(9)) TO #MAIN-SSN
ELSE
  MOVE 'MEMBER'  TO #NAME-LBL
  MOVE ST-V.RANK TO #MAIN-RANK
  MOVE ST-V.NAME TO #MAIN-NAME
  MOVE EDITED ST-V.SSN (EM=Z(9)) TO #MAIN-SSN
END-IF
* IF ST-V.DATE-OF-BIRTH NE 0                                     /* TT1
*   MOVE ST-V.DATE-OF-BIRTH TO #YYYYMMDD                         /* TT1
*   COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO       /* TT1
* END-IF                                                         /* TT1
IF ST-V.DATE-OF-DEATH NE 0
  MOVE ST-V.DATE-OF-DEATH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
END-IF

IF ST-V.WIDOW-FLAG = 'Y'
  FIND CONTACT WITH CONTACT-ID = SPOUSE-CONTACT-ID
    PERFORM GET-CONTACT-DATA
  END-FIND
ELSE
  FIND CONTACT WITH CONTACT-ID = MEMBER-CONTACT-ID
    PERFORM GET-CONTACT-DATA
  END-FIND
END-IF

DEFINE GET-CONTACT-DATA
IF CONTACT.DATE-OF-BIRTH NE 0
  MOVE CONTACT.DATE-OF-BIRTH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOB LEAVING NO
ELSE
  RESET #MAIN-DOB
END-IF
IF CONTACT.DATE-OF-DEATH NE 0
  MOVE CONTACT.DATE-OF-DEATH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #MAIN-DOD LEAVING NO
ELSE
  RESET #MAIN-DOD
END-IF
MOVE EDITED CONTACT.SSN (EM=Z(9)) TO #MAIN-SSN
#MAIN-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
END-SUBROUTINE

END
