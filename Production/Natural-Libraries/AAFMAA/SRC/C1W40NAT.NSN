* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* C1W40NAT data etry for Status W4
* Created 2016-01-03 BZ
*
DEFINE DATA  
PARAMETER  
1 #CGA-ISN (P8)  BY VALUE
LOCAL 
1 AT-V VIEW OF A-APPL-TRACKING
  2 W4-OPTIONS
    3 W4-REASON
    3 W4-DISCLOSE
    3 W4-EXAM-BASED
    3 W4-MIB
    3 W4-NOTE

1 w4_Choice  (A1/15)
1 l_w4_App   (L)
1 l_w4_Med   (L) 
1 l_w4_MIB   (L) 
1 Note       (A25)  
1 Msg        (A30)
1 #i         (I1)
1 #j         (I1)
1 text       (A30) INIT <'line for short note'>
END-DEFINE
*
DEFINE WINDOW W4-WIN
      SIZE 23 * 31
      BASE 2/48
      TITLE 'Primary Reason (W4)'
      CONTROL SCREEN
      FRAMED  
*
GAT.
GET AT-V #CGA-ISN
IF W4-REASON <> 0 
   w4_Choice(W4-REASON) := 'X'
   IF W4-DISCLOSE    = 'Y' l_w4_App := TRUE END-IF
   IF W4-EXAM-BASED  = 'Y' l_w4_Med := TRUE END-IF
   IF W4-MIB         = 'Y' l_w4_MIB := TRUE END-IF
   Note := W4-NOTE
END-IF
SET KEY PF3
REPEAT
* IF NOTE = ' ' NOTE := text END-IF
 INPUT WINDOW='W4-WIN' (IP=OFF AD=MU)
     1x w4_Choice(01) 'Abnormal Labs'           /
     1x w4_Choice(02) 'Alcohol/Drug Abuse'      /  
     1x w4_Choice(03) 'Auto-immune Syndrome'    /
     1x w4_Choice(04) 'Blood Pressure'          /
     1x w4_Choice(05) 'Brain (Tumor/CVA/TBI)'   /
     1x w4_Choice(06) 'Build'                   /
     1x w4_Choice(07) 'Cancer'                  /
     1x w4_Choice(08) 'Cardiac'                 / 
     1x w4_Choice(09) 'Diabetes'                /  
     1x w4_Choice(10) 'Mental/Mood/Cognitive'   /
     1x w4_Choice(11) 'Pending Surgery/Tests'   /
     1x w4_Choice(12) 'PTSD'                    /
     1x w4_Choice(13) 'Respiratory'             / 
     1x w4_Choice(14) 'HIV'                     / 
     1x w4_Choice(15) 'Other'                   //
     1x l_w4_App (AD=MI EM=N/Y) 'Disclosed on App'          /
     1x l_w4_Med (AD=MI EM=N/Y) 'Decision Based on Paramed' /
     1x l_w4_MIB (AD=MI EM=N/Y) 'MIB Discovery'             /
     1x 'Note' Note(AD=WU)                                  /
     Msg(AD=IO)
 IF *PF-KEY = 'PF3' ESCAPE ROUTINE END-IF
 Msg := ' ' #j := 0     
 FOR #i = 1 TO 15
  IF w4_Choice(#i) <> ' ' #j := #j + 1 W4-REASON := #i END-IF
 END-FOR    
 
 IF #j > 1 Msg := 'Choose one reason only' END-IF
 IF #j = 0 Msg := 'One reason is mandatory' END-IF
 IF Msg = ' ' ESCAPE BOTTOM END-IF
END-REPEAT
 IF Note <> text W4-NOTE := Note END-IF 
 MOVE EDITED l_w4_App(EM=N/Y) TO W4-DISCLOSE
 MOVE EDITED l_w4_Med(EM=N/Y) TO W4-EXAM-BASED
 MOVE EDITED l_w4_MIB(EM=N/Y) TO W4-MIB
 UPDATE (GAT.)
END       
