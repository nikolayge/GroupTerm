* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Policies teminated this year
* BZ O5/20/2013  OI-2001
*
DEFINE DATA LOCAL
1 VA-V VIEW OF A-VA-HISTORY
  2 RECORD-KEY          (N15)
  2 REDEFINE RECORD-KEY
    3 v_CN              (N6)
 2 TERMINATION-DATE     (N7)
 2 TERMINATION-CODE     (A3)
 2 ISSUE-PLAN           (A3)   
*
 1 CNT-V VIEW OF A-CONTACTS
   2 CONTACT-ID      
   2 ID-NUMBER 
   2 FULL-NAME                                     
     3 FIRST-NAME        
     3 MIDDLE-NAME       
     3 LAST-NAME         
     3 SUFFIX            
 1  v_Name                (A)  DYNAMIC
 1  v_OUT                 (A100) 
 1  c_OUT                 (N6)
 1  dm                    (A1)     INIT <H'09'>
 1  f_Rep       (A) DYNAMIC INIT <'/usr/SAG/AAFMAA/batch/workfiles/WORK/TERMINATED_THIS_YEAR.TXT'>
 1  v_Date     (N8)
 1  REDEFINE   v_Date
    2 v_YYYY  (N4)
 1 v_Key      (N7)   
END-DEFINE
ON ERROR
  WRITE NOTITLE  *PROGRAM "Err-ln:" *ERROR-LINE "Err:" *ERROR-NR  / 'CN:' v_CN
END-ERROR

 IF *DEVICE <> 'BATCH'
    DEFINE WORK FILE 1 f_Rep TYPE 'ASCII-COMPRESSED'
 ELSE
    DEFINE WORK FILE 1 TYPE 'ASCII-COMPRESSED'
 END-IF
 v_Date := *DATN
 v_Key  := v_YYYY * 100

SETA. SETTIME
*
READ VA-V BY RECORD-KEY 
    WHERE TERMINATION-DATE > v_Key
  v_OUT := ' '
  FIND CNT-V WITH ID-NUMBER  = v_CN 
  v_NAME := Edit_Name_LFMS (<FULL-NAME>)
  END-FIND
  PERFORM sbr_OUT
*  PRINT v_CN v_Name TERMINATION-DATE TERMINATION-CODE ISSUE-PLAN   
END-READ
*
 CLOSE WORK FILE 1

  PRINT(0) *DATX *TIMX *PROGRAM  'Env:' F-IS-DEV(<>) (EM=Production/Development) 'Elapsed' *TIMD(SETA.) (EM=ZZh:99min:99sec:9) /
  'Output:' c_OUT 
  
* = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = 
DEFINE sbr_Out
    c_OUT  := c_OUT + 1
    IF c_OUT = 1
       COMPRESS 'Policies terminated This year before last ME.  Executed at' *DAT4U INTO v_OUT
       WRITE WORK FILE 1 v_OUT
       COMPRESS 'CN' 'Name' 'Term Date' 'Term Code' 'Plan'
                                          INTO v_OUT WITH DELIMITERS dm
        WRITE WORK FILE 1 v_OUT
    END-IF 
    COMPRESS v_CN 
          dm v_NAME
          dm TERMINATION-DATE
          dm TERMINATION-CODE
          dm ISSUE-PLAN
       INTO v_OUT 
    WRITE WORK FILE 1 v_OUT
END-SUBROUTINE
END
