* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: G1300PPF  ** AFSC PDF PROCESSING **
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* TDM    06222005  TM1     CREATED WITH CCD AND BEN OPTIONS            *
* NGG    20130930  NG1  Address normalization
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX        /* WORK FIELDS FOR 'GONERCXX'
LOCAL USING ADDA0001        /* NG1
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 MEMBER-CONTACT-ID       /* NG1
 2 NAME
 2 SSN
*  2 ADDRESS-1
*  2 ADDRESS-2
*  2 ADDRESS-3
*  2 ZIP-CODE
 2 ALOT-AMT
 2 RANK
 2 CC-NUM
 2 REDEFINE CC-NUM
  3 #FIRST-12   (N12)
  3 #LST4DIGITS (A4)
 2 CC-EXP-DT
 2 REDEFINE CC-EXP-DT
  3 CC-YYYY     (A4)
  3 CC-MM       (A2)
  3 CC-DD       (A2)
*
1 #FORMAT-CCDT   (A10)
1 #FILL-BLNKS (A78/24)
1 #AS-OF-DATE   (A10)
1 #YYYYMMDD     (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY    (N4)
 2 #MM      (N2)
 2 #DD      (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A  (A4)
 2 #MM-A    (A2)
 2 #DD-A    (A2)
*
1 #NAME      (A25)
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #ERR-MSG   (A60)
1 #MI        (A1)
*
1 #SPLIT    (A1)
1 #STREET   (A25)
1 #STREET-2 (A25)
1 #CITY     (A22)
1 #STATE    (A2)
1 #ZIP         (N9)
1 REDEFINE #ZIP
 2 #ZIP-CODE-5-A  (A5)
 2 #ZIP-CODE-4-A  (A4)
1 REDEFINE #ZIP
 2 #ZIP-CODE-5    (N5)
 2 #ZIP-CODE-4    (N4)
1 #ZIP-A       (A10)
*
1 #DONE      (L)
1 #CAP-PAYOFF-DT (N8)
1 #REMAINDER (P5.2)
1 #DUES      (N3.2)
1 #PREM-W-DUES  (P5.2)
1 #I1        (I2)
1 #CAP-X     (I2)
1 #SUBTYPE    (A1)
* 1 #USER       (A14)
1 #USER       (A15)
1 #SSN        (A11)
1 #CITYST     (A35)
1 #ADDR       (A50)
1 #DNAME      (A30)
*
1 #CRCD-INFO      (A1)
1 #BENE           (A1)
1 #INS-CLAIM      (A1)
*
1 #AMT-12   (A12)
*
1 #MONTH-TABLE
 2 #JAN           (A6) INIT <'01 Jan'>
 2 #FEB           (A6) INIT <'02 Feb'>
 2 #MAR           (A6) INIT <'03 Mar'>
 2 #APR           (A6) INIT <'04 Apr'>
 2 #MAY           (A6) INIT <'05 May'>
 2 #JUNE          (A6) INIT <'06 Jun'>
 2 #JULY          (A6) INIT <'07 Jul'>
 2 #AUG           (A6) INIT <'08 Aug'>
 2 #SEPT          (A6) INIT <'09 Sep'>
 2 #OCT           (A6) INIT <'10 Oct'>
 2 #NOV           (A6) INIT <'11 Nov'>
 2 #DEC           (A6) INIT <'12 Dec'>
*
1 REDEFINE #MONTH-TABLE
 2 MONTH-OCC      (12)
  3 #MONTH-NUM    (N2)
  3 #MONTH-SPACE  (A1)
  3 #MONTH-LIT    (A3)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX            /* NATURAL ERROR ROUTINE
*
GET ST-V #CGA-ISN
*
IF NOT #CGA-SCR-SUFFIX = 'CCD' OR = 'BEN' OR = 'INS'
  MOVE *DATN TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #AS-OF-DATE WITH '/'
*
  MOVE '*' TO #CGA-SCR-COMMAND
  INPUT MARK *#CRCD-INFO USING MAP 'G1300MPF'
*
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
END-IF
*
IF (#CRCD-INFO NE ' ') OR (#CGA-SCR-SUFFIX = 'CCD')
  PERFORM CRCD-INFO-LTR
END-IF
*
IF (#BENE NE ' ') OR (#CGA-SCR-SUFFIX = 'BEN')
  MOVE 'BD' TO #CGA-SCR-COMMAND   /* Will end up in E470XSBD
  MOVE 'PDF' TO #CGA-SCR-SUFFIX
  FETCH 'G1000PXX'
END-IF
*
IF (#INS-CLAIM NE ' ') OR (#CGA-SCR-SUFFIX = 'INS')
  PERFORM INS-CLAIM
END-IF
*
MOVE 'PF' TO #CGA-SCR-COMMAND
RESET        #CGA-SCR-SUFFIX
FETCH 'G1000PXX'
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE INS-CLAIM
* * * * * * * * * * * * * *
MOVE EDITED SSN (EM=999-99-9999) TO #SSN
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #USER WITH '/'
COMPRESS #CGA-CLERK #USER INTO #USER
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER ')>>'
         INTO #FILL-BLNKS (2) LEAVING NO
COMPRESS '<</T(CN)/V(' ST-V.ID-NUMBER ')>>'
         '<</T(NAME)/V(' NAME ')>>'
   INTO #FILL-BLNKS (3) LEAVING NO
COMPRESS '<</T(SSN)/V(' #SSN ')>>'
   INTO #FILL-BLNKS (4) LEAVING NO
*
MOVE '] /F (H:\PDF/INSURANCE CLAIM.PDF)>> >>'  TO #FILL-BLNKS (5)
MOVE 'endobj'                             TO #FILL-BLNKS (6)
MOVE'trailer <</Root 1 0 R >> %%EOF'     TO #FILL-BLNKS  (7)
INPUT USING MAP 'GPDFXMXX'
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CRCD-INFO-LTR
* * * * * * * * * * * * * * * *
*
PERFORM FORMAT-NM-ADDR
*
MOVE *DATN TO #YYYYMMDD
PERFORM LOAD-MONTH-LITERAL
COMPRESS #DD-A #MONTH-LIT (#I1) #YYYY-A INTO #USER WITH '-'
COMPRESS #CGA-CLERK #USER INTO #USER
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER ')>>'
         INTO #FILL-BLNKS (2) LEAVING NO
*
COMPRESS '<</T(CN)/V(' ST-V.ID-NUMBER ')>>'
         '<</T(NAME)/V(' #NAME ')>>'
   INTO #FILL-BLNKS (3) LEAVING NO
*
COMPRESS '<</T(ADDR)/V(' #ADDR ')>>'
   INTO #FILL-BLNKS (4) LEAVING NO
*
COMPRESS '<</T(CITYST)/V(' #CITYST ')>>'
   INTO #FILL-BLNKS (5) LEAVING NO
*
COMPRESS CC-MM '/' CC-YYYY INTO #FORMAT-CCDT LEAVING NO
COMPRESS '<</T(LST4)/V(' #LST4DIGITS ')>>'
         '<</T(EXPDT)/V(' #FORMAT-CCDT ')>>'
   INTO #FILL-BLNKS (6) LEAVING NO
*
COMPRESS '<</T(DNAME)/V(' #DNAME ')>>'
   INTO #FILL-BLNKS (7) LEAVING NO
*
MOVE '] /F (H:\PDF/CREDIT CARD LETTER.PDF)>> >>'  TO #FILL-BLNKS (8)
MOVE 'endobj'                         TO #FILL-BLNKS (9)
MOVE'trailer <</Root 1 0 R >> %%EOF'  TO #FILL-BLNKS  (10)
INPUT USING MAP 'GPDFXMXX'
*
END-SUBROUTINE
*
* * * * * * * * * * *
DEFINE FORMAT-NM-ADDR
* * * * * * * * * * *
*
MOVE ST-V.NAME TO #NAME
RESET #LAST #MI #FIRST-20
CALLNAT 'GFMLNSXX' /* split name
 #NAME
 #FIRST-20
 #MI
 #LAST
 #ERR-MSG
COMPRESS RANK #FIRST-20 #MI #LAST INTO #NAME
*                                                 NG1 Start
* SEPARATE CITY/STATE
* MOVE 'S' TO #SPLIT
* RESET #STREET #STREET-2 #CITY #STATE
* CALLNAT 'GCTSTSXX'  /* SPLIT CITY/STATE
*  #SPLIT
*  ADDRESS-1
*  ADDRESS-2
*  ADDRESS-3
*  #STREET
*  #STREET-2
*  #CITY
*  #STATE
*  #ERR-MSG
* MOVE ZIP-CODE TO #ZIP
* COMPRESS #ZIP-CODE-5-A '-' #ZIP-CODE-4-A
*    INTO #ZIP-A LEAVING NO
* COMPRESS #STREET ' ' #STREET-2  INTO #ADDR LEAVING
* COMPRESS #CITY ',' INTO #CITYST LEAVING NO
* COMPRESS #CITYST #STATE #ZIP-A INTO #CITYST LEAVING
*
RESET #PDA-ADDRESS
#ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
#ADD-TYPE       := 'P'
CALLNAT 'ADDN0001' #PDA-ADDRESS
#ADDR   := #ADD-LINE-1
#CITYST := #ADD-LINE-2
*                                                NG1 End
COMPRESS RANK INTO #DNAME LEAVING NO
COMPRESS #DNAME ' ' #LAST INTO #DNAME LEAVING
COMPRESS #DNAME ',' INTO #DNAME LEAVING NO
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE LOAD-MONTH-LITERAL
* * * * * * * * * * * * * * * * * * *
*
FOR #I1 1 TO 12
  IF #MONTH-NUM (#I1) = #MM
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
