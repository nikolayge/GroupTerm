* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Program MANPSRCH.
/**
/** :author nguentchev
/* Search is based on search functionality in AUC ( User Center)
* NGG  12/24/2015  NG1
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING MANASRCH
LOCAL /* USING AUCASIND
1 SearchIndividual /*SEQ
  2 ErrorCode (I4) /*unsignedShort
  2 ErrorDescription (A) DYNAMIC /*string
  2 ActionType (I4) /*unsignedShort
  2 SearchId (A) DYNAMIC /*string
  2 TotalFound (I4) /*unsignedInt
  2 FirstName (A) DYNAMIC /*string
  2 LastName (A) DYNAMIC /*string
  2 DateOfBirth (N8) /*unsignedLong
  2 DateOfDeath (N8)
  2 SSN (A) DYNAMIC /*unsignedLong
  2 Phone (N20) /*unsignedShort
  2 PhoneType (A) DYNAMIC
  2 Email (A) DYNAMIC /*string
  2 City (A) DYNAMIC /*string
  2 State (A) DYNAMIC /*string
  2 ZIP (N9) /*unsignedInt
  2 MembersOnly (L)
  2 GenerateReport (L)
  2 FILENAME (A) DYNAMIC
1 PageIndividual /*SEQ
  2 ErrorCode (I4) /*unsignedShort
  2 ErrorDescription (A) DYNAMIC /*string
  2 SearchId (A) DYNAMIC /*string
  2 PageNumber (I4) /*unsignedShort
  2 LinesPerPage (I4) /*unsignedShort
  2 TotalFound (I4) /*unsignedShort
  2 SortedBy (A) DYNAMIC /*string
  2 SortOrder (A) DYNAMIC /*string
  2 IndList /*SEQ
    3 C@Individual (I4)
    3 Individual (1:*) /*SEQ
      4 CN (N6) /*unsignedInt
      4 ContactID (N8)
      4 FormatedName (A) DYNAMIC /*string
      4 SSN (A9) /*unsignedLong
      4 DateOfBirth (N8) /*unsignedLong
      4 DateOfDeath (N8)
      4 PhoneNumber (A) DYNAMIC
      4 PhoneType (A) DYNAMIC /*string
      4 Email (A) DYNAMIC /*string
      4 Address1 (A) DYNAMIC /*string
      4 Address2 (A) DYNAMIC /*string
      4 Address3 (A) DYNAMIC /*string
LOCAL
1 SEARCH-ISNS                  (P10/1:*)
1 ADDRESS-FLAGS                (A1/1:*)
1 EMAIL-FLAGS                  (N8/1:*)
1 PHONE-FLAGS                  (N8/1:*)
1 #MAX  (N7) CONST <500>
1 BLANK (A1) CONST <' '>
1 #LINES-PER-PAGE     (N7) CONST <15>
1 #MAX-PAGE#          (N7)
1 #FL                 (N7)
1 J                   (N7)
1 K                   (N7)
1 L                   (N7)
1 N                   (N7)
1 BLANK-EXIST         (L)
1 FILL-EXIST          (L)
*
1 #NAMES              (A25/#MAX)
1 #SOCIAL             (A9/#MAX)
1 #BDATE              (A8/#MAX)
1 #SELECT             (A1/#MAX)
1 #CN                 (N6/#MAX)
1 #AFLAG              (A1/#MAX)
1 #POLICY-ID          (A16/#MAX)
1 #CONTACT-ID         (N8/#MAX)
*
1 #PAGE#              (N7)
1 POLICY-ID           (A16)
1 POLICY-OWNER-CN     (N6)
*
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER
*
1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-TYPE     (A1)
  2 ID-NUMBER        (N6)
  2 CONTACT-ID       (N8)
  2 LAST-NAME        (A25)
  2 FIRST-NAME       (A25)
  2 DATE-OF-BIRTH    (N8)
  2 SSN              (N9)
  2 FIRM-ORGANIZATION-NAME (A75)
*
END-DEFINE
*
SET KEY PF3 NAMED "Exit"
SET KEY PF7 NAMED 'PgUp'
SET KEY PF8 NAMED 'PgDw'
*
INCLUDE GONERCXX
*
#CGA-ORIG-ID := #CGA-SCR-ID
#OWNERCN := #CGA-ORIG-ID
#CGA-SCR-COMMAND := '*'
*
IF #OWNERCN > 0
*
*  Search for Owner dependants
*
  RESET C@Individual
  FIND (1) CNT WITH ID-NUMBER = #OWNERCN
  END-FIND
  FIND CONTACT-REL WITH CONTACT-1-ID = CNT.CONTACT-ID
    ADD 1 TO C@Individual
    FIND CNT WITH CONTACT-ID = CONTACT-2-ID
      IF CNT.CONTACT-TYPE NE 'O'
        COMPRESS CNT.LAST-NAME CNT.FIRST-NAME INTO #NAMES(C@Individual)
          WITH DELIMITER ','
      ELSE
        #NAMES(C@Individual) := CNT.FIRM-ORGANIZATION-NAME
      END-IF
      #SOCIAL(C@Individual) := CNT.SSN
      #BDATE(C@Individual) := CNT.DATE-OF-BIRTH
      #CN(C@Individual) := CNT.ID-NUMBER
      #CONTACT-ID (C@Individual) := CONTACT-2-ID
      FIND ANNUITANTS WITH CONTACT-ID = CONTACT-2-ID
        IF NO RECORDS FOUND
          RESET #AFLAG (C@Individual)
          ESCAPE BOTTOM
        END-NOREC
        #AFLAG (C@Individual) := 'Y'
        #POLICY-ID(C@Individual) := POLICY-NUMBER
      END-FIND
    END-FIND
  END-FIND
  SearchIndividual.TotalFound := C@Individual
  IF SearchIndividual.TotalFound > 0
    PERFORM BROWSE-NAMES
  END-IF
  ESCAPE ROUTINE
END-IF
*
RP1. REPEAT
  INPUT USING MAP 'MANMSRCH'
* 
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RP1.)
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      PERFORM SEARCH-BY-NAME
      IF SearchIndividual.TotalFound > 0
        PERFORM BROWSE-NAMES
      END-IF
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
FETCH 'G1000PXX'
*
* LOOK FOR NAMES BASED ON THE SEARCH CRITERIA
*
DEFINE SEARCH-BY-NAME
*      --------------
IF #OWNERCN > 0
*
*  Search for Owner dependants
*
  RESET C@Individual
  FIND (1) CNT WITH ID-NUMBER = #OWNERCN
  END-FIND
  FIND CONTACT-REL WITH CONTACT-1-ID = CNT.CONTACT-ID
    ADD 1 TO C@Individual
    FIND CNT WITH CONTACT-ID = CONTACT-2-ID
      IF CNT.CONTACT-TYPE NE 'O'
        COMPRESS CNT.LAST-NAME CNT.FIRST-NAME INTO #NAMES(C@Individual)
          WITH DELIMITER ','
      ELSE
        #NAMES(C@Individual) := CNT.FIRM-ORGANIZATION-NAME
      END-IF
      #SOCIAL(C@Individual) := CNT.SSN
      #BDATE(C@Individual) := CNT.DATE-OF-BIRTH
      #CN(C@Individual) := CNT.ID-NUMBER
      #CONTACT-ID (C@Individual) := CONTACT-2-ID
      FIND ANNUITANTS WITH CONTACT-ID = CONTACT-2-ID
        IF NO RECORDS FOUND
          RESET #AFLAG (C@Individual)
          ESCAPE BOTTOM
        END-NOREC
        #AFLAG (C@Individual) := 'Y'
        #POLICY-ID(C@Individual) := POLICY-NUMBER
      END-FIND
    END-FIND
  END-FIND
  SearchIndividual.TotalFound := C@Individual
  ESCAPE ROUTINE
END-IF
*
* Search for Contacts match
*
RESET SearchIndividual #CGA-ORIG-ID
SearchIndividual.LastName := #LAST-NAME
SearchIndividual.FirstName := #FIRST-NAME
SearchIndividual.DateOfBirth := #DOB
SearchIndividual.SSN := #SSN
SearchIndividual.Phone := #PHONE
SearchIndividual.Email := #EMAIL
*
CALLNAT 'AUCNSIG1' SearchIndividual
  SEARCH-ISNS(*)
  ADDRESS-FLAGS(*)
  EMAIL-FLAGS(*)
  PHONE-FLAGS(*)
*
IF SearchIndividual.ErrorCode = 0
  IF SearchIndividual.TotalFound > #MAX
    C@Individual := #MAX
  ELSE
    C@Individual := SearchIndividual.TotalFound
  END-IF
  PageIndividual.LinesPerPage := C@Individual
  PageIndividual.TotalFound := SearchIndividual.TotalFound
  IF C@Individual > 0
    EXPAND ARRAY Individual TO (1 : C@Individual)
    CALLNAT 'AUCNSIR1' PageIndividual
      SEARCH-ISNS(*)
      ADDRESS-FLAGS(*)
      EMAIL-FLAGS(*)
      PHONE-FLAGS(*)
    #NAMES(1 : C@Individual) := FormatedName(1 : C@Individual)
    #SOCIAL(1 : C@Individual) := PageIndividual.SSN(1 : C@Individual)
    #BDATE(1 : C@Individual) :=
      PageIndividual.DateOfBirth(1 : C@Individual)
    #CN(1 : C@Individual) := PageIndividual.CN(1 : C@Individual)
    #CONTACT-ID (1:C@Individual) :=
      PageIndividual.ContactID(1 : C@Individual)
*   Get Annuitant related data
    FOR J 1 TO C@Individual
      FIND ANNUITANTS WITH CONTACT-ID = ContactID (J)
        IF NO RECORDS FOUND
          RESET #AFLAG (J)
          ESCAPE BOTTOM
        END-NOREC
        #AFLAG (J) := 'Y'
        #POLICY-ID(J) := POLICY-NUMBER
      END-FIND
    END-FOR
  END-IF
ELSE
  PRINT SearchIndividual.ErrorDescription
END-IF
END-SUBROUTINE
* NAVIGATE THE PAGES OF THE OUTPUT SCREEN
DEFINE BROWSE-NAMES
/*     ------------
#FL := 1
#PAGE# := 1
#MAX-PAGE# := SearchIndividual.TotalFound / #LINES-PER-PAGE
RPL.
REPEAT
  INPUT USING MAP 'MANMSRCD'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPL.)
    VALUE 'PF8'
      IF #PAGE# < #MAX-PAGE#
        ADD 1 TO #PAGE#
        #FL := ( #PAGE# - 1) * #LINES-PER-PAGE + 1
      END-IF
    VALUE 'PF7'
      IF #PAGE# > 1
        SUBTRACT 1 FROM #PAGE#
        #FL := ( #PAGE# ) * #LINES-PER-PAGE - 14
      END-IF
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      #FL := ( #PAGE# - 1) * #LINES-PER-PAGE + 1
      FOR J 1 TO #LINES-PER-PAGE
        IF #SELECT (#FL) NE BLANK
          #CGA-SCR-ID := #CONTACT-ID (#FL)                 /* NG1
          IF #CGA-ORIG-ID = 0
*            #CGA-ORIG-ID := #CN (#FL)
            IF #CGA-ORIG-ID = 0
              #CGA-ORIG-ID := #OWNERCN
            END-IF
          END-IF
          FETCH RETURN 'MANPDTL'
        END-IF
        ADD 1 TO #FL
      END-FOR
    NONE VALUE
      IGNORE
  END-DECIDE
  UNTIL *PF-KEY = 'PF3'
END-REPEAT
FETCH 'MANPSRCH'
END-SUBROUTINE
*
*
END
