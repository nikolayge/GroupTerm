* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
**************************************************************************
* SUBPROGRAM-ID: GOEMN001
* DESCRIPTION: Add/Update/Del of Contact Email
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
*
DEFINE DATA
PARAMETER USING GOEMA001
PARAMETER
1 #CLERK-ID  (A3)
1 #MSG       (A) DYNAMIC
1 #UPDATED   (L)
*
LOCAL
1 EMAIL VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-TYPE
  2 EMAIL-ADDRESS
  2 EMAIL-PROTECTION-IND
  2 EMAIL-UNDELIV
  2 EMAIL-PREFERRED-IND
  2 STATUS
  2 STATUS-DATE
  2 START-DATE
  2 END-DATE
  2 REMARKS                   (A70/3)
  2 LAST-DATE-UPD             (N8)
  2 LAST-TIME-UPD             (N7)
  2 LAST-USER-UPD             (A8)
*
END-DEFINE
*
* #EMAIL.EMAIL-PREFERRED-IND := 'Y'
*
IF #EMAIL.EMAIL-ID = 0
  RESET EMAIL
  CALLNAT 'ADRNT002' #EMAIL.EMAIL-ID
  MOVE BY NAME #EMAIL TO EMAIL
  EMAIL.LAST-DATE-UPD  := *DATN
  EMAIL.LAST-TIME-UPD  := *TIMN
  EMAIL.LAST-USER-UPD  := #CLERK-ID
  STORE EMAIL
ELSE
  IF #EMAIL.EMAIL-PREFERRED-IND = 'Y'
    FIND EMAIL WITH CONTACT-ID = #EMAIL.CONTACT-ID
      RESET EMAIL.EMAIL-PREFERRED-IND
      UPDATE
    END-FIND
  END-IF
  FIND (1) EMAIL WITH EMAIL-ID = #EMAIL.EMAIL-ID
    MOVE BY NAME #EMAIL TO EMAIL
    EMAIL.LAST-DATE-UPD  := *DATN
    EMAIL.LAST-TIME-UPD  := *TIMN
    EMAIL.LAST-USER-UPD  := #CLERK-ID
    UPDATE
  END-FIND
END-IF
*
#UPDATED := TRUE
*
ON ERROR
  COMPRESS 'Email add:' *ERROR-NR *ERROR-LINE *PROGRAM *DATN *TIME INTO #MSG
    WITH DELIMITER ':'
  ESCAPE ROUTINE
END-ERROR
*
END
