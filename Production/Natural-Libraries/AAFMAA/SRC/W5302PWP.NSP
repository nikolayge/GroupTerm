* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* * * * * * * * * * * * * * * * * * * * *
* PROGRAM-ID: W5302PWP                  *
* WP  -  WIDOW INSURANCE ADD NEW RECORD *
* * * * * * * * * * * * * * * * * * * * *
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX    /* WORK FIELDS FOR GONERCXX - ERROR ROUTINE
LOCAL USING W5301LWP
LOCAL
*
1 #AAFMAA-1-OF-GR
 2 #AAFMAA-1         (A2)
 2 #AAFMAA-OF        (A4) INIT <' OF '>
 2 #AAFMAA-X         (A2)
1 REDEFINE #AAFMAA-1-OF-GR
 2 #AAFMAA-1-OF-X     (A8)
*
1 #FIELD-UPDATED     (L)        INIT<FALSE>
1 #UPDATE-DATE       (L)        INIT<FALSE>
1 #NOW-SETTLED         (L)
1 #NOW-UNSETTLED       (L)
1 #INVALID             (L)
1 #ERR-MSG             (A60)
*
********************************************************************
*                    SCREEN FIELDS
********************************************************************
*
1 #SCR-MESSAGE    (A35)
1 #SCR-NAME       (A25)
1 #SCR-VAR-TITLE  (A9)
1 #SCR-DOB        (A10)
1 #SCR-MDOD       (A10)
1 #SCR-PNAME      (A19)
1 #SCR-PNAME-CV   (C)
1 #SCR-PNUM       (A14)
1 #SCR-PCODE      (A1)
1 #SCR-W-STATUS   (A1)
1 #SCR-FACE-AMT   (P7.0)
1 #SCR-AMT-PD     (P7.2)
1 #SCR-ISS-DATE   (A8)
1 #SCR-DT-FIL     (A8)
1 #SCR-DT-SET     (A8)
1 #SCR-DT-STW     (A8)
1 #SCR-DT-STW-HDR (A18) INIT <'DATE-SENT-TO-WIDOW'>
1 #SCR-712        (A1)
1 #SCR-TYPE       (A4)
1 #SCR-MODE       (A1)
1 #SCR-INSTAL     (P7.2)
1 #SCR-BENE       (A20)
1 #SCR-BENE2      (A20)
1 #SCR-BENE3      (A20)
1 #SCR-REMARKS    (A21)
1 #SCR-AAFMAA-M   (A24)  INIT <'AAFMAA INSURANCE SUMMARY'>
1 #SCR-CK-NUM-M   (A14)  INIT <'CHECK NUMBER #'>
1 #SCR-CHECK-NUM  (N6)
1 #SCR-BEN-PD-M   (A12)  INIT <'BENEFIT PAID'>
1 #SCR-BEN-PD     (P7.2)
1 #SCR-DT-PD-M    (A9)  INIT <'DATE-PAID'>
1 #SCR-DT-PD      (A6)
1 #SCR-TOT-M      (A9)  INIT <'TOTAL-INS'>
1 #SCR-TOT        (P7.2)
1 #SCR-PD-BY-M    (A7)  INIT <'PAID-BY'>
1 #SCR-PD-BY      (A4)
1 #SCR-LST-UPDT   (A10)
1 #SCR-AAFMAA-HDR-CV  (C)
1 #SCR-AAFMAA-CV      (C)
1 #SCR-DT-STW-CV      (C)
1 #SCR-CONTESTABLE  (A1)
*
1 #A-X          (I2)
1 #B-X          (I2)
********************************************************************
*                    WORK AREA
********************************************************************
*
1 #ST-X              (I2)
1 #SC-X              (I2)
1 #NUM-POLS          (I2)
1 #PASS-ISN          (P8)
1 #POL-ISN           (P8)
1 #A-AMAA-TOT        (N7.2)
1 #HOLD-SCR-ID       (N9)
1 #PASS-DTH-BEN      (N7.2)
1 #H-ISN             (P8)
1 #HOLD-SCR-COMM     (A2)
*
1 #YYYYMMDD
  2 #YYYY             (N4)
  2 #MM               (N2)
  2 #DD               (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-N       (N8)
*
1 #MMDDYYYY
  2 #MM               (N2)
  2 #DD               (N2)
  2 #YYYY             (N4)
1 REDEFINE #MMDDYYYY
  2 #MMDDYYYY-A       (A8)
*
1 #WYYYYMMDD          (N8)
1 REDEFINE #WYYYYMMDD
  2 #WYYYY            (A4)
  2 #WMM              (A2)
  2 #WDD              (A2)
1 REDEFINE #WYYYYMMDD
  2 #WYYYYMMDD-N      (N8)
1 #DATE-OF-BIRTH-SP   (N8)
*
********************************************************************
*             TRLOG VIEW OF TRANSACTION-REGISTER FILE
********************************************************************
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER        (N6)
  2 PROCESS-IND      (A1)  /* PM - 06/2000 - SPLIT COMPANIES
  2 NAME             (A25)
  2 DATE-LAST-UPDATE (N8)
  2 TERMINAL-ID      (A8)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (P9)
  2 TRANSACTION-TIMN (P7)
  2 OLD-DATA         (A125/1)
  2 NEW-DATA         (A125/1)
END-DEFINE
***********************************************************************
INCLUDE GONERCXX      /* NATURAL ERROR ROUTINE
*
MOVE *DATN TO #WYYYYMMDD
COMPRESS #WMM #WDD #WYYYY INTO #SCR-LST-UPDT WITH '/'
*
INPUT #HOLD-SCR-ID #SC-X #PASS-ISN
      #A-AMAA-TOT #NUM-POLS
*
GET STATUS-V #CGA-ISN
*
RESET #CGA-SCR-COMMAND #CGA-SCR-ID
PERFORM HEADER-INFO
MOVE 'AMOUNT-PD'              TO #SCR-VAR-TITLE
MOVE (AD=N)                   TO #SCR-AAFMAA-HDR-CV
MOVE (AD=NP)                   TO #SCR-AAFMAA-CV
PERFORM MAIN-LOOP
****************************
DEFINE SUBROUTINE MAIN-LOOP
****************************
*
ADD 1 TO #NUM-POLS
*
IF #NUM-POLS GT 45
  MOVE 'MAXIMUM POLICIES REACHED' TO #SCR-MESSAGE
  MOVE 'DP' TO #CGA-SCR-COMMAND
  MOVE #HOLD-SCR-ID TO #CGA-SCR-ID
  INPUT WITH TEXT #SCR-MESSAGE USING MAP 'W5301MWP'
  END TRANSACTION                        /* 09/15/88
  FETCH 'G1000PXX'
END-IF
*
FIND (1) FIRS-V WITH WIDOW-KEY = #CGA-ID-G
 IF NO RECORDS FOUND
   MOVE 'NO FIRS RECORD ON FILE' TO #SCR-MESSAGE
   ESCAPE BOTTOM
 END-NOREC
*
  MOVE *ISN TO #POL-ISN
  RESET #SCR-PNAME
        #SCR-PNUM
        #SCR-PCODE
        #SCR-FACE-AMT
        #SCR-AMT-PD
        #SCR-ISS-DATE
        #SCR-DT-FIL
        #SCR-DT-SET
        #SCR-DT-STW
        #SCR-712
        #SCR-TYPE
        #SCR-MODE
        #SCR-INSTAL
        #SCR-BENE
        #SCR-REMARKS
  MOVE #NUM-POLS TO #SC-X
  INPUT MARK *#SCR-PCODE USING MAP 'W5301MWP'
*
  IF #CGA-SCR-COMMAND NE '  ' AND #CGA-SCR-COMMAND NE '*'
     END TRANSACTION               /* 09/15/88
     FETCH 'G1000PXX'
  ELSE
    MOVE '* ' TO #CGA-SCR-COMMAND
  END-IF
*
  PERFORM DO-EDITS
  PERFORM PREPARE-RECORD
*
* * * * * * * * * * * DETERMINE IF POLICIES UNSETTLED STATUS CHANGED * *
*
  RESET #NOW-UNSETTLED
        #NOW-SETTLED
*
  IF   (FIRS-V.POL-DATE-CLM-FILED   (#SC-X) NE 0)  /* NOW UNSETTLED
   AND (FIRS-V.POL-DATE-CLM-SETTLED (#SC-X) =  0)
    MOVE TRUE TO #NOW-UNSETTLED
  ELSE
    MOVE TRUE TO #NOW-SETTLED
  END-IF
*
  IF #NOW-UNSETTLED                      /* 1ST TIME UNSETTLED
    RESET MBRSHIP-MTD-DET-V
    MOVE FIRS-V.POL-DATE-CLM-FILED (#SC-X)
                            TO MBRSHIP-MTD-DET-V.ENTRY-DATE
    MOVE #POL-ISN           TO MBRSHIP-MTD-DET-V.REC-ISN
*
    IF FIRS-V.POL-NAME (#SC-X) = 'AMAA' OR = 'AAFMAA'
      MOVE 'A'              TO MBRSHIP-MTD-DET-V.CODE
    ELSE
      MOVE 'P'              TO MBRSHIP-MTD-DET-V.CODE
    END-IF
*
    MOVE #CGA-ISN           TO MBRSHIP-MTD-DET-V.ISSUE-DATE
    MOVE #CGA-ID            TO MBRSHIP-MTD-DET-V.ID-NUMBER
    STORE MBRSHIP-MTD-DET-V
  END-IF
*
  IF #FIELD-UPDATED
    MOVE STATUS-V.ID-NUMBER    TO TRLOG.ID-NUMBER
    MOVE STATUS-V.PROCESS-IND  TO TRLOG.PROCESS-IND /* - PM - 06/2000
    MOVE STATUS-V.NAME-SP      TO TRLOG.NAME
    MOVE INS-LAST-FM-DATE  TO TRLOG.DATE-LAST-UPDATE
*
    MOVE #CGA-CLERK            TO TRLOG.CLERK-ID
    MOVE *INIT-ID              TO TRLOG.TERMINAL-ID
    MOVE *DATN                 TO TRLOG.TRANSACTION-DATN
    MOVE *TIMN                 TO TRLOG.TRANSACTION-TIMN
*
    COMPRESS 'DP-NEW INS POL' POL-NAME (#SC-X) POL-NUMBER (#SC-X)
             POL-TYPE (#SC-X) POL-AMOUNT (#SC-X) POL-REMARKS (#SC-X)
               INTO NEW-DATA (1)
*
    STORE TRLOG
*
    MOVE *DATN TO FIRS-V.INS-LAST-FM-DATE
    UPDATE RECORD (0164)
    MOVE 'FIRS UPDATE SUCCESSFUL' TO #SCR-MESSAGE
  ELSE
    MOVE 'NO UPDATES REQUESTED' TO #SCR-MESSAGE
  END-IF
*
  END TRANSACTION
*
  MOVE #HOLD-SCR-ID TO #CGA-SCR-ID
  INPUT WITH TEXT #SCR-MESSAGE USING MAP 'W5301MWP'
*
  IF #CGA-SCR-COMMAND NE '* '
    FETCH 'G1000PXX'
  ELSE
    PERFORM MAIN-LOOP
  END-IF
*
END-FIND
END-SUBROUTINE /* (0150)
*
*****************************
DEFINE SUBROUTINE HEADER-INFO
*****************************
*
MOVE STATUS-V.NAME-SP        TO #SCR-NAME
MOVE STATUS-V.STATUS-CODE-SP TO #SCR-W-STATUS
*
#DATE-OF-BIRTH-SP := GET-DATE-OF-BIRTH (< STATUS-V.SPOUSE-CONTACT-ID >)
MOVE #DATE-OF-BIRTH-SP TO #WYYYYMMDD
COMPRESS #WMM #WDD #WYYYY INTO #SCR-DOB WITH '/'
MOVE STATUS-V.DATE-OF-DEATH           TO #WYYYYMMDD
COMPRESS #WMM #WDD #WYYYY INTO #SCR-MDOD WITH '/'
*
END-SUBROUTINE /* (0267)
*
**************************
DEFINE SUBROUTINE DO-EDITS
**************************
*
CALLNAT 'GPLCDSXX' #SCR-PCODE #INVALID #ERR-MSG
IF #INVALID
  REINPUT USING HELP
  MARK *#SCR-PCODE
END-IF
*
IF #SCR-PCODE NE ' '
  IF #SCR-PCODE NE '*' AND #SCR-PCODE NE 'X' AND #SCR-PCODE NE '&'
    REINPUT WITH 'MUST BE *, X, &, OR BLANK'
    MARK *#SCR-PCODE
  ELSE
    MOVE TRUE TO #FIELD-UPDATED
  END-IF
END-IF
*
**IF #SCR-PNAME EQ 'AAFMAA             ' OR       /* 09/15/88
*   #SCR-PNAME EQ 'AMAA               '
*  REINPUT WITH 'CAN NOT ADD AMAA OR AAFMAA POLICY'
*  MARK *#SCR-NAME
**ELSE
  IF #SCR-PNAME NE '                   '
    MOVE TRUE TO #FIELD-UPDATED
  END-IF
**END-IF
IF #SCR-PNUM NE '              '
  IF #SCR-PNAME EQ '                   '
    REINPUT WITH 'MUST ENTER COMPANY NAME'
    MARK *#SCR-PNAME
  ELSE
    MOVE TRUE TO #FIELD-UPDATED
  END-IF
END-IF
*
IF #SCR-FACE-AMT NE 0
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-AMT-PD NE 0
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-ISS-DATE NE '        '
  IF #SCR-ISS-DATE NE MASK(MMDDYYYY)
    REINPUT 'Must be (MMDDYYYY) format'
    MARK *#SCR-ISS-DATE
  ELSE
    MOVE #SCR-ISS-DATE TO #MMDDYYYY-A
    PERFORM FLIP-DATE-TO-YYYYMMDD
    IF #YYYYMMDD-N GT *DATN
      REINPUT 'Greater than today"s date'
      MARK *#SCR-ISS-DATE
    END-IF
  END-IF
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-DT-STW NE '        '
  IF #SCR-DT-STW NE MASK(MMDDYYYY)
    REINPUT 'Must be (MMDDYYYY) format'
    MARK *#SCR-DT-STW
  ELSE
    MOVE #SCR-DT-STW TO #MMDDYYYY-A
    PERFORM FLIP-DATE-TO-YYYYMMDD
    IF #YYYYMMDD-N GT *DATN
      REINPUT WITH 'Greater than today"s date'
      MARK *#SCR-DT-STW
    END-IF
  END-IF
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-DT-SET NE '        '
  IF #SCR-DT-SET NE MASK(MMDDYYYY)
    REINPUT 'Must be (MMDDYYYY) format'
    MARK *#SCR-DT-SET
  ELSE
    MOVE #SCR-DT-SET TO #MMDDYYYY-A
    PERFORM FLIP-DATE-TO-YYYYMMDD
    IF #YYYYMMDD-N GT *DATN
      REINPUT WITH 'Greater than today"s date'
      MARK *#SCR-DT-SET
    END-IF
  END-IF
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-DT-FIL NE '        '
  IF #SCR-DT-FIL NE MASK(MMDDYYYY)
    REINPUT 'Must be (MMDDYYYY) format'
    MARK *#SCR-DT-FIL
  ELSE
    MOVE #SCR-DT-FIL TO #MMDDYYYY-A
    PERFORM FLIP-DATE-TO-YYYYMMDD
    IF #YYYYMMDD-N GT *DATN
      REINPUT WITH 'Greater than today"s date'
      MARK *#SCR-DT-FIL
    END-IF
  END-IF
  MOVE TRUE TO #FIELD-UPDATED
END-IF
*
IF #SCR-712 NE ' '
  IF #SCR-712 NE 'Y' AND #SCR-712 NE 'N'
    REINPUT WITH 'MUST BE Y, N, OR BLANK'
    MARK *#SCR-712
  ELSE
    MOVE TRUE TO #FIELD-UPDATED
  END-IF
END-IF
*
IF #SCR-MODE NE ' '
  IF #SCR-MODE NE 'A' AND #SCR-MODE NE 'S' AND
     #SCR-MODE NE 'Q' AND #SCR-MODE NE 'M'
    REINPUT WITH 'MUST BE A, S, Q, M, OR BLANK'
    MARK *#SCR-MODE
  ELSE
    MOVE TRUE TO #FIELD-UPDATED
  END-IF
END-IF
*
END-SUBROUTINE  /* (0282)
*
********************************
DEFINE SUBROUTINE PREPARE-RECORD
********************************
*
MOVE #SCR-PNAME                            TO FIRS-V.POL-NAME(#SC-X)
MOVE #SCR-PNUM                             TO FIRS-V.POL-NUMBER(#SC-X)
MOVE #SCR-PCODE                            TO FIRS-V.POL-CODE(#SC-X)
MOVE #SCR-FACE-AMT                         TO FIRS-V.POL-AMOUNT(#SC-X)
MOVE #SCR-AMT-PD                           TO FIRS-V.POL-AMT-PAID(#SC-X)
*
IF #SCR-ISS-DATE = '        '
  RESET FIRS-V.POL-DATE(#SC-X)
ELSE
  MOVE #SCR-ISS-DATE TO #MMDDYYYY-A
  PERFORM FLIP-DATE-TO-YYYYMMDD
  MOVE #YYYYMMDD-N TO FIRS-V.POL-DATE (#SC-X)
END-IF
*
IF #SCR-DT-FIL = '        '
  RESET FIRS-V.POL-DATE-CLM-FILED(#SC-X)
ELSE
  MOVE #SCR-DT-FIL TO #MMDDYYYY-A
  PERFORM FLIP-DATE-TO-YYYYMMDD
  MOVE #YYYYMMDD-N TO FIRS-V.POL-DATE-CLM-FILED (#SC-X)
END-IF
*
IF #SCR-DT-SET = '        '
  RESET FIRS-V.POL-DATE-CLM-SETTLED(#SC-X)
ELSE
  MOVE #SCR-DT-SET TO #MMDDYYYY-A
  PERFORM FLIP-DATE-TO-YYYYMMDD
  MOVE #YYYYMMDD-N TO FIRS-V.POL-DATE-CLM-SETTLED (#SC-X)
END-IF
*
IF #SCR-DT-STW = '        '
  RESET FIRS-V.POL-DATE-SENT-TO-WIDOW(#SC-X)
ELSE
  MOVE #SCR-DT-STW TO #MMDDYYYY-A
  PERFORM FLIP-DATE-TO-YYYYMMDD
  MOVE #YYYYMMDD-N TO FIRS-V.POL-DATE-SENT-TO-WIDOW (#SC-X)
END-IF
*
MOVE #SCR-712     TO FIRS-V.POL-712     (#SC-X)
MOVE #SCR-TYPE    TO FIRS-V.POL-TYPE(#SC-X)
MOVE #SCR-MODE    TO FIRS-V.POL-MODE(#SC-X)
MOVE #SCR-INSTAL  TO FIRS-V.POL-INSTAL(#SC-X)
MOVE 1 TO #B-X
MOVE #SCR-BENE    TO FIRS-V.POL-BENEFIC(#SC-X,#B-X)
MOVE #SCR-BENE2   TO FIRS-V.POL-BENEFIC(#SC-X,#B-X+1)
MOVE #SCR-BENE3   TO FIRS-V.POL-BENEFIC(#SC-X,#B-X+2)
MOVE #SCR-REMARKS TO FIRS-V.POL-REMARKS(#SC-X)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FLIP-DATE-TO-YYYYMMDD
* * * * * * * * * * * * * * * * * * * *
*
IF #MMDDYYYY-A = '00000000'
  RESET #YYYYMMDD-N
ELSE
  MOVE BY NAME #MMDDYYYY TO #YYYYMMDD
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FLIP-DATE-TO-MMDDYYYY
* * * * * * * * * * * * * * * * * * * *
*
MOVE BY NAME #YYYYMMDD TO #MMDDYYYY
*
END-SUBROUTINE
*
END
