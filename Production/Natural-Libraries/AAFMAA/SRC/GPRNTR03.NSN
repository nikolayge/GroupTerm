* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************
* SUBPROGRAM-ID: GPRNTR03 - TRANSACTION LOG GENERIC MODULE 4
*                           Paperless transactions
***************************************************************
*        MODIFICATION LOG
***************************************************************
* USER   DATE      TAG  REASON
* YAK    11142013       Initial Creation
* YAK    04012015  YK1  Add Annual Report Delivery Flag
* YAK    08012016  YK2  Add DV Upload transaction logic
***************************************************************
*
DEFINE DATA
PARAMETER
1 #FUNCTION         (A) DYNAMIC
PARAMETER USING GPRATR03
LOCAL USING GPRA0004       /* PDA to pass data to TR log subprogram
LOCAL USING GPRL0004
LOCAL
1 #LOG-PAPERLESS-TRAN (A) DYNAMIC CONST <'PaperlessOptions'>
1 #CG-CHANGE          (A) DYNAMIC CONST <'CG-DelOpt'>
1 #LOG-DV-UPLOAD-TRAN (A) DYNAMIC CONST <'DVUpload'>           /* YK2
1 #DD-CHANGE          (A) DYNAMIC CONST <'DD-AddDoc'>          /* YK2
1 TR_DESCRIPTION      (A) DYNAMIC
1 #I  (I2)
1 #II (I2)
*
END-DEFINE
*
DECIDE ON FIRST VALUE #FUNCTION
  VALUE #LOG-PAPERLESS-TRAN
    TR_DESCRIPTION := #CG-CHANGE
    PERFORM LOG-PO-TRAN
* YK2 >
  VALUE #LOG-DV-UPLOAD-TRAN
    TR_DESCRIPTION := #DD-CHANGE
    PERFORM LOG-DV-UPLOAD-TRAN
* < YK2
  NONE VALUE
    IGNORE
END-DECIDE
*
PERFORM WRITE-TRAN-LOG
*
********************************
DEFINE SUBROUTINE WRITE-TRAN-LOG
********************************
IF #I NE 0
  #TR-NAME               := TR_NAME
  #TR-ID-NUMBER          := TR_ID_NUMBER
  #LOG-DESCRIPTION       := TR_DESCRIPTION
  #TR-CLERK-ID           := TR_CLERK
  #TR-DATE-LAST-UPDATE   := BEFORE_IMAGE.DATE-LAST-UPDATE
  #NEW-DATA-TAG(1)       := TR_DESCRIPTION
  CALLNAT 'GPRN0004'
    #TR-FIELDS #SYNC-FIELD-TABLE(*)
    #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
END-IF
END-SUBROUTINE
*
*****************************
DEFINE SUBROUTINE LOG-PO-TRAN
*****************************
*
IF BEFORE_IMAGE.ANNS-DELIVERY-FLAG <> AFTER_IMAGE.ANNS-DELIVERY-FLAG
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CG-CHANGE
  COMPRESS FULL 'Annual Statement Delivery ' BEFORE_IMAGE.ANNS-DELIVERY-FLAG TO #OLD-DATA(#I) LEAVING NO
  COMPRESS FULL 'Annual Statement Delivery ' AFTER_IMAGE.ANNS-DELIVERY-FLAG  TO #NEW-DATA(#I) LEAVING NO
END-IF
IF BEFORE_IMAGE.BILL-DELIVERY-FLAG <> AFTER_IMAGE.BILL-DELIVERY-FLAG
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CG-CHANGE
  COMPRESS FULL 'Billing Statement Delivery ' BEFORE_IMAGE.BILL-DELIVERY-FLAG TO #OLD-DATA(#I) LEAVING NO
  COMPRESS FULL 'Billing Statement Delivery ' AFTER_IMAGE.BILL-DELIVERY-FLAG  TO #NEW-DATA(#I) LEAVING NO
END-IF
IF BEFORE_IMAGE.FIRMBA-DELIVERY-FLAG <> AFTER_IMAGE.FIRMBA-DELIVERY-FLAG
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CG-CHANGE
  COMPRESS FULL 'FIR/MBA Delivery ' BEFORE_IMAGE.FIRMBA-DELIVERY-FLAG TO #OLD-DATA(#I) LEAVING NO
  COMPRESS FULL 'FIR/MBA Delivery ' AFTER_IMAGE.FIRMBA-DELIVERY-FLAG  TO #NEW-DATA(#I) LEAVING NO
END-IF
* YK1 >
IF BEFORE_IMAGE.ANNUAL-RPT-FLAG <> AFTER_IMAGE.ANNUAL-RPT-FLAG
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #CG-CHANGE
  COMPRESS FULL 'Annual Report Delivery ' BEFORE_IMAGE.ANNUAL-RPT-FLAG TO #OLD-DATA(#I) LEAVING NO
  COMPRESS FULL 'Annual Report Delivery ' AFTER_IMAGE.ANNUAL-RPT-FLAG  TO #NEW-DATA(#I) LEAVING NO
END-IF
* > YK1
END-SUBROUTINE
*
************************************
DEFINE SUBROUTINE LOG-DV-UPLOAD-TRAN  /* YK2
************************************
*
* BIRTH-CERT-CD
* MARRIAGE-CERT-CD
* MARRIAGE-DATE
* DOCUMENT-NAME
*
IF BEFORE_IMAGE.DOCUMENT-NAME <> AFTER_IMAGE.DOCUMENT-NAME
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DD-CHANGE
  #NEW-DATA(#I) := AFTER_IMAGE.DOCUMENT-NAME
END-IF
*
IF BEFORE_IMAGE.BIRTH-CERT-CD NE AFTER_IMAGE.BIRTH-CERT-CD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DD-CHANGE
  #OLD-DATA(#I) := FUNCTION-CODE-A-DESC(<'033',BEFORE_IMAGE.BIRTH-CERT-CD>)
  COMPRESS BEFORE_IMAGE.DOCUMENT-NAME #OLD-DATA(#I) INTO #OLD-DATA(#I)
  #NEW-DATA(#I) := FUNCTION-CODE-A-DESC(<'033',AFTER_IMAGE.BIRTH-CERT-CD>)
  COMPRESS AFTER_IMAGE.DOCUMENT-NAME #NEW-DATA(#I) INTO #NEW-DATA(#I)
END-IF
*
IF BEFORE_IMAGE.MARRIAGE-DATE NE AFTER_IMAGE.MARRIAGE-DATE
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DD-CHANGE
  #OLD-DATA(#I) := BEFORE_IMAGE.MARRIAGE-DATE
  COMPRESS BEFORE_IMAGE.DOCUMENT-NAME #OLD-DATA(#I) INTO #OLD-DATA(#I)
  #NEW-DATA(#I) := AFTER_IMAGE.MARRIAGE-DATE
  COMPRESS AFTER_IMAGE.DOCUMENT-NAME #NEW-DATA(#I) INTO #NEW-DATA(#I)
END-IF

IF BEFORE_IMAGE.MARRIAGE-CERT-CD NE AFTER_IMAGE.MARRIAGE-CERT-CD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := #DD-CHANGE
  #OLD-DATA(#I) := FUNCTION-CODE-A-DESC(<'033',BEFORE_IMAGE.MARRIAGE-CERT-CD>)
  COMPRESS BEFORE_IMAGE.DOCUMENT-NAME #OLD-DATA(#I) INTO #OLD-DATA(#I)
  #NEW-DATA(#I) := FUNCTION-CODE-A-DESC(<'033',AFTER_IMAGE.MARRIAGE-CERT-CD>)
  COMPRESS AFTER_IMAGE.DOCUMENT-NAME #NEW-DATA(#I) INTO #NEW-DATA(#I)
END-IF
END-SUBROUTINE
*
END
