* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram MSSNL005.
/**
/** :author nguentchev
/* TODO Enter your code here
/*
/*            Create product termination records
/*  Input parms are: CN, MD-CODE, USER COMPANY
/*  InOut: #ISSUE-DATE
/*
DEFINE DATA
PARAMETER
1 #CN                 (N6)
1 #CODE               (A1)
1 #USER-COMPANY       (A1)
1 #ISSUE-DATE-O       (N8)
1 #PROGRAM            (A8)
*
LOCAL
1 #CADET    (A3/4) CONST <'FC','FCR','AC','ACR'>
1 #RESERVE  (A3/8) CONST <'FR','FRA','AR','ARA','CGA','CGR','MR','NRA'>
1 #NGUARD   (A3/4) CONST <'FG','FGA','AGA','AG'>
1 #VETERAN  (A3/2) CONST <'P','V'>
1 #CAP-PAID-OFF         (L)
1 #MD-PLAN              (A3)
1 #I1                   (I2)
1 #E-DATE               (N8)
*
1 PS-V VIEW OF A-PRODUCT-STATUS
  2 ADD-PROG (A8)
  2 PRODUCT_ID (A16) /*
  2 FACE_AMOUNT (N7.2)
  2 DEATH_BENEFIT (N7.2)
  2 PRODUCT_CODE (A3)
  2 ID-NUMBER (N6.0) /*
  2 MEMBER_TYPE (A1)
  2 STATUS_CODE (A16)
  2 STATUS_TIME (A15) 2 REDEFINE STATUS_TIME
    3 #YYYYMMDD   (A8)
    3 #HHIISST    (A7)
  2 ACTIVE_CODE (A1)
  2 MIL-BRANCH (A1)
  2 MIL-STATUS (A1)
  2 AGE (N3.0)
  2 PAY-GRADE (A3)
  2 LIVE-CHANGE (N1)
1 CNT-V VIEW OF A-CONTACTS
  2 DATE-OF-DEATH
  2 DATE-OF-BIRTH
  2 RANK-ID
1 RR-V VIEW OF A-RANK-REFERENCE
  2 PAY-GRADE  (A3)
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 SPOUSE-CONTACT-ID
  2 ID-NUMBER
  2 MILITARY-SERVICE (A3)
  2 MILITARY-STATUS  (A1)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 NUMBER-MEMBERSHIPS
  2 C*ACTIVE-SEGMENTS (N3)
  2 PLAN          (9)
  2 STATUS        (9)
  2 ISSUE-DATE    (9)
  2 CONV-DURATION (9)
  2 DEATH-BENEFIT (9)
  2 FACE-AMOUNT   (9)
1 ACTIVE          (A1) CONST <'D'>
1 CLAIM           (A1) CONST <'E'>
*
END-DEFINE
*
RESET #CAP-PAID-OFF
  PS-V
FIND (1) ST-V WITH ID-NUMBER = #CN
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  FIND (1) CNT-V WITH ID-NUMBER = #CN
    IF CNT-V.DATE-OF-DEATH = 0 OR= 88888888 OR= 99999999
      MOVE EDITED *DATX (EM=YYYYMMDD) TO #YYYYMMDD
    ELSE
      MOVE EDITED CNT-V.DATE-OF-DEATH (EM=99999999) TO #YYYYMMDD
    END-IF
    PS-V.AGE := GET-AGE-2 (< CNT-V.DATE-OF-BIRTH >)
  END-FIND
  IF ST-V.INTRO-FLAG = 'S' OR= 'K' OR= 'G'
    PS-V.MEMBER_TYPE := ST-V.INTRO-FLAG
  ELSE
    PS-V.MEMBER_TYPE := 'M'
  END-IF
  DECIDE FOR FIRST CONDITION
    WHEN MILITARY-STATUS = 'C' AND MILITARY-SERVICE = 'V'
      PS-V.MIL-STATUS := 'N'  /* NON MILITARY
    WHEN MILITARY-STATUS = 'R'
      PS-V.MIL-STATUS := 'R'  /* RETIRED
    WHEN MILITARY-STATUS = 'C' AND NOT MILITARY-SERVICE = #VETERAN (*)
      PS-V.MIL-STATUS := 'V'  /* VETERAN
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #NGUARD (*)
      PS-V.MIL-STATUS := 'G'  /* NATIONAL GUARD
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #RESERVE (*)
      PS-V.MIL-STATUS := 'Z'  /* RESERVE
    WHEN MILITARY-STATUS = 'A' AND MILITARY-SERVICE = #CADET (*)
      PS-V.MIL-STATUS := 'C'  /* CADET
    WHEN NONE
      PS-V.MIL-STATUS := 'A'  /* Active
  END-DECIDE
  PS-V.MIL-BRANCH := MILITARY-SERVICE
  FIND (1) RR-V WITH RANK-ID = CNT-V.RANK-ID
    PS-V.PAY-GRADE := RR-V.PAY-GRADE
  END-FIND
*
  FOR #I1 1 TO C*ACTIVE-SEGMENTS   /* in case units changed by corr
    IF ST-V.STATUS (#I1) = ACTIVE
/*    OR= CLAIM
      PS-V.FACE_AMOUNT   := - ST-V.FACE-AMOUNT (#I1)
      PS-V.DEATH_BENEFIT := - ST-V.DEATH-BENEFIT (#I1)
      MOVE ST-V.PLAN          (#I1) TO #MD-PLAN
      CALLNAT 'GPLNMSXX' PS-V.PRODUCT_ID  #CN #I1
      MOVE EDITED *TIMX (EM=HHIISST) TO #HHIISST
      IF #MD-PLAN = ' '
        #MD-PLAN := '*'
      END-IF
      DECIDE FOR FIRST CONDITION
        WHEN #MD-PLAN = MASK (N'PF')
            OR #MD-PLAN =MASK (NN'F')
          #MD-PLAN := 'FLX'
        WHEN F-LT2PLUS-TRUE (< #MD-PLAN >)
          #MD-PLAN := 'LT+'
        WHEN F-LT2L-TRUE (< #MD-PLAN >)
          #MD-PLAN := 'LT2'
        WHEN NONE
          IGNORE
      END-DECIDE
      PS-V.PRODUCT_CODE  := #MD-PLAN
      PS-V.ADD-PROG := #PROGRAM
      PS-V.ID-NUMBER := #CN
      MOVE 'DEATH' TO PS-V.STATUS_CODE
      ACTIVE_CODE := #CODE
      SL.
      STORE PS-V
    END-IF
  END-FOR
END-FIND
*
RESET #I1
FIND PS-V WITH ID-NUMBER = #CN
  ACCEPT IF PS-V.STATUS_CODE = 'DEATH'
  ADD 1 TO #I1
  IF #I1 = 1
    PS-V.LIVE-CHANGE := -1
  ELSE
    RESET PS-V.LIVE-CHANGE
  END-IF
  UPDATE
END-FIND
*
END
