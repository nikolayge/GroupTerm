* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRNEM01
* DESCRIPTION: Add/Update/Del of Contact Email
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  08/27/2010  None Creation
* RSE  03/15/2010  RE1  Fix last user date issue
* RSE  03/25/2010  RE2  Standardize TR Call
* RSE  05/15/2011  RE3  Call module to sync with CRM
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID       (N8)
1 #MEMBER-ID-NUMBER (N6)  /* RE2
1 #EMAIL-ID         (N8)
1 #CLERK-ID         (A3)
1 #MODE             (A1)
1 #MSG              (A72)
LOCAL USING GPRATR00      /* RE2         
LOCAL USING A-CTDATA
**LOCAL USING GPRA0004                /* Parameters passed to write transaction log   RE2
LOCAL
1 CNT-EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-TYPE
  2 EMAIL-ADDRESS
  2 EMAIL-PROTECTION-IND
  2 EMAIL-UNDELIV
  2 EMAIL-PREFERRED-IND
  2 STATUS
  2 STATUS-DATE
  2 REDEFINE STATUS-DATE
    3 #SD-YY                  (N4)
    3 #SD-MM                  (N2)
    3 #SD-DD                  (N2)
  2 REDEFINE STATUS-DATE
    3 #STATUS-DATE-A          (A8)
  2 START-DATE
  2 REDEFINE START-DATE
    3 #ESD-YY                 (N4)
    3 #ESD-MM                 (N2)
    3 #ESD-DD                 (N2)
  2 REDEFINE START-DATE
    3 #START-DATE-A           (A8)
  2 END-DATE
  2 REDEFINE END-DATE
    3 #EED-YY                 (N4)
    3 #EED-MM                 (N2)
    3 #EED-DD                 (N2)
  2 REDEFINE END-DATE
    3 #END-DATE-A             (A8)
  2 LAST-DATE-UPD             (N8)
  2 REDEFINE LAST-DATE-UPD
    3 #L-YY                   (A4)
    3 #L-MM                   (A2)
    3 #L-DD                   (A2)
  2 REMARKS                   (A70/3)
  2 LAST-TIME-UPD             (N7)
  2 LAST-USER-UPD             (A8)
*
1 ST-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID     /* RE2
*  2 INTERNET-ADDR        /* TT1
  2 INS-MAIL-CODE
*
1 TERM-V VIEW OF A-TERMINALS
  2 COMMENTS
  2 REDEFINE COMMENTS
    3 #TERM-EMAIL-ID          (N8)
*
1 #MAP-CV                      (C)
1 #EML-UNDLV-CV                (C)
1 #EMAIL-ADDRESS               (A62)
1 #START-DATE                  (A8)
1 REDEFINE #START-DATE
  2 #START-DATE-N              (N8)
1 REDEFINE #START-DATE
  2 #S-MM                      (N2)
  2 #S-DD                      (N2)
  2 #S-YY                      (N4)
1 #END-DATE                    (A8)
1 REDEFINE #END-DATE
  2 #END-DATE-N                (N8)
1 REDEFINE #END-DATE
  2 #E-MM                      (N2)
  2 #E-DD                      (N2)
  2 #E-YY                      (N4)
1 #LAST-DATE-UPD               (A10)
1 #LAST-USER-UPD               (A20)
1 #CN-TEXT                     (A2)
1 #CI-TEXT                     (A2)
1 #DEL-TEXT                    (A3)
1 #ADD-TEXT                    (A3)
1 #CONFIRM-DELETE              (A1)
1 #ADDR-POOL-ID                (N8)
1 #FMTD-NAME                   (A33)
1 #SSN                         (A11)
1 #DOB                         (A10)
1 #DOD                         (A10)
1 #PHONE-ID                    (N8)
1 #HEADER1                     (A78)
1 #HEADER2                     (A78)
1 #UPDATE-FILES                (L)
1 #REMARKS                     (A70/3)
1 #STATUS                      (A1)
1 #STATUS-DATE                 (A8)
1 REDEFINE #STATUS-DATE
  2 #STATUS-DATE-N             (N8)
1 REDEFINE #STATUS-DATE
  2 #S2-MM                     (N2)
  2 #S2-DD                     (N2)
  2 #S2-YY                     (N4)
1 #POS                         (N2)
1 #N                           (N2)
1 #END                         (N2)
1 #CODES
  2  #ADDR-INTL        (A1)     /* 001
  2  #ADDR-LOCATION    (A1)     /* 002
  2  #ADDR-OVERRD      (A1)     /* 003   Y/N
  2  #ADDR-PROTECT     (A1)     /* 004
  2  #ADDR-RETURNED    (A1)     /* 005   Y/N
  2  #ADDR-TYPE        (A1)     /* 006
  2  #ADDR-VALID       (A1)     /* 007
  2  #EML-PREF         (A1)     /* 008   Y/N
  2  #EML-PROTECT      (A1)     /* 009   Y/N
  2  #EML-TYPE         (A1)     /* 010
  2  #EML-UNDLV        (A1)     /* 011   Y/N
  2  #PH-INTL          (A1)     /* 012
  2  #PH-PREF          (A1)     /* 013
  2  #PH-TIME          (A1)     /* 014
  2  #PH-TYPE          (A1)     /* 015
  2  #PH-VALID         (A1)     /* 016
  2  #ADDR-SOURCE      (A1)     /* 017
  2  #ADDR-STATUS      (A1)     /* 018
  2  #XXX              (A1)
  2  #EML-STATUS       (A1)     /* 020
1 #CODE-NAMES
  2  #ADDR-INTL-TXT        (A20)
  2  #ADDR-LOCATION-TXT    (A20)
  2  #ADDR-OVERRD-TXT      (A20)
  2  #ADDR-PROTECT-TXT     (A20)
  2  #ADDR-RETURNED-TXT    (A20)
  2  #ADDR-TYPE-TXT        (A20)
  2  #ADDR-VALID-TXT       (A20)
  2  #EML-PREF-TXT         (A20)
  2  #EML-PROTECT-TXT      (A20)
  2  #EML-TYPE-TXT         (A20)
  2  #EML-UNDLV-TXT        (A20)
  2  #PH-INTL-TXT          (A20)
  2  #PH-PREF-TXT          (A20)
  2  #PH-TIME-TXT          (A20)
  2  #PH-TYPE-TXT          (A20)
  2  #PH-VALID-TXT         (A20)
  2  #ADDR-SOURCE-TXT      (A20)
  2  #ADDR-STATUS-TXT      (A20)
  2  #XXX                  (A20)
  2  #EML-STATUS-TXT       (A20)

1 XML_SERIALZE_OUTPUT (A) DYNAMIC
1 #CSV-LINE           (A) DYNAMIC
1 #CRM-Result         (A) DYNAMIC
1 #Contact            (I4) CONST <2>
1 #PREF-EML-EXISTS         (L)
1 #SAVE-EMAIL              (A62)
1 #TEXT               (A67/1) /* RE2
1 #EMAIL              (A) DYNAMIC INIT<'EMAIL'>
1 #ISN                (P8)
1 #SYNCHRONIZE        (L)     /* RE3
*
END-DEFINE
*
DEFINE WINDOW Confirm-Delete
  SIZE 8 * 65
  BASE 13/2
  FRAMED ON POSITION SYMBOL AUTO
*
IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>)
  IGNORE
END-IF
*
SET KEY ENTR NAMED 'Save'
SET KEY PF1=HELP
SET KEY PF3 NAMED 'Exit'
SET KEY PF12 NAMED 'Reln'
*
EXPAND ARRAY BEFORE_IMAGE.EMAIL_GROUP TO (1:5)       /* RE2
EXPAND ARRAY AFTER_IMAGE.EMAIL_GROUP TO (1:5)        /* RE2
*
IF #EMAIL-ID > 0
  FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-ID = #EMAIL-ID
    MOVE *ISN TO #ISN
*    CALLNAT 'ADRNE022' CNT-EMAIL-V.CONTACT-ID #ISN #TEXT(*)   /* RE2
*    MOVE #TEXT(1) TO BEFORE_IMAGE.EMAIL-ADDRESS(1)                 /* RE2
    MOVE CNT-EMAIL-V.EMAIL-ADDRESS TO #EMAIL-ADDRESS      BEFORE_IMAGE.EMAIL-ADDRESS(1)  /* RE2 START
    MOVE CNT-EMAIL-V.EMAIL-TYPE TO #EML-TYPE              BEFORE_IMAGE.EMAIL-TYPE(1) 
    MOVE CNT-EMAIL-V.EMAIL-PROTECTION-IND TO #EML-PROTECT BEFORE_IMAGE.EMAIL-PROTECTION-IND(1)
    MOVE CNT-EMAIL-V.EMAIL-UNDELIV TO #EML-UNDLV          BEFORE_IMAGE.EMAIL-UNDELIV(1) 
    MOVE CNT-EMAIL-V.EMAIL-PREFERRED-IND TO #EML-PREF     BEFORE_IMAGE.EMAIL-PREFERRED-IND(1)  
    MOVE STATUS TO #STATUS                                BEFORE_IMAGE.EMAIL-STATUS(1)        /* RE2 END
    BEFORE_IMAGE.DATE-LAST-UPDATE := CNT-EMAIL-V.LAST-DATE-UPD
    IF CNT-EMAIL-V.STATUS-DATE > 0
      MOVE #SD-MM TO #S2-MM
      MOVE #SD-DD TO #S2-DD
      MOVE #SD-YY TO #S2-YY
    END-IF
    IF CNT-EMAIL-V.START-DATE > 0
      MOVE #ESD-MM TO #S-MM
      MOVE #ESD-DD TO #S-DD
      MOVE #ESD-YY TO #S-YY
    END-IF
    IF CNT-EMAIL-V.END-DATE > 0
      MOVE #EED-MM TO #E-MM
      MOVE #EED-DD TO #E-DD
      MOVE #EED-YY TO #E-YY
    END-IF
    COMPRESS #L-MM '/' #L-DD '/' #L-YY INTO #LAST-DATE-UPD LEAVING NO
    MOVE REMARKS(*) TO #REMARKS(*)
    FIND (1) TERM-V WITH CLERK-ID = LAST-USER-UPD
      IF NO RECORD FOUND                                    /* RE1 START
        FIND (1) TERM-V WITH TERMINAL-ID = LAST-USER-UPD
          MOVE COMMENTS TO #LAST-USER-UPD
        END-FIND
        ESCAPE BOTTOM
      END-NOREC                                             /* RE1 END
      MOVE COMMENTS TO #LAST-USER-UPD
    END-FIND
    IF #MODE = 'V'
      MOVE (AD=P) TO #MAP-CV #EML-UNDLV-CV
    ELSE
      MOVE (AD=U) TO #MAP-CV #EML-UNDLV-CV
      MOVE 'Del' TO #DEL-TEXT
      SET KEY PF4 NAMED 'Add'
      SET KEY PF11 NAMED 'Del'
    END-IF
  END-FIND
ELSE
  SET KEY PF4=OFF PF11=OFF
  MOVE (AD=U) TO #MAP-CV #EML-UNDLV-CV
END-IF
*
CALLNAT 'ADRN0004' #CONTACT-ID #HEADER1 #HEADER2
*
IF #CONT-ID-NUMBER > 0
  MOVE 'CN' TO #CN-TEXT
END-IF
MOVE 'CI' TO #CI-TEXT

*
REPEAT
  CALLNAT 'ADRN0003'
    #CONTACT-ID
    #ADDR-POOL-ID   /* 0
    #PHONE-ID       /* 0
    #EMAIL-ID
    #CODES #CODE-NAMES
*
  FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-ID = #EMAIL-ID
    IF CNT-EMAIL-V.STATUS-DATE > 0
      MOVE #SD-MM TO #S2-MM
      MOVE #SD-DD TO #S2-DD
      MOVE #SD-YY TO #S2-YY
    END-IF
    IF CNT-EMAIL-V.START-DATE > 0
      MOVE #ESD-MM TO #S-MM
      MOVE #ESD-DD TO #S-DD
      MOVE #ESD-YY TO #S-YY
    END-IF
    IF CNT-EMAIL-V.END-DATE > 0
      MOVE #EED-MM TO #E-MM
      MOVE #EED-DD TO #E-DD
      MOVE #EED-YY TO #E-YY
    END-IF
    FIND (1) TERM-V WITH CLERK-ID = LAST-USER-UPD
      IF NO RECORD FOUND                                    /* RE1 START
        FIND (1) TERM-V WITH TERMINAL-ID = LAST-USER-UPD
          MOVE COMMENTS TO #LAST-USER-UPD
        END-FIND
        ESCAPE BOTTOM
      END-NOREC                                             /* RE1 END
      MOVE COMMENTS TO #LAST-USER-UPD
    END-FIND
  END-FIND
*
  MOVE 'Y' TO #EML-PREF
  FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
    MOVE CNT-EMAIL-V.EMAIL-ADDRESS TO #SAVE-EMAIL
    IF #EMAIL-ID = 0
      MOVE 'N' TO #EML-PREF
    END-IF
    IF EMAIL-ID = #EMAIL-ID
      MOVE CNT-EMAIL-V.EMAIL-PREFERRED-IND TO #EML-PREF
      ESCAPE BOTTOM
    END-IF
  END-FIND
*
  INPUT WITH TEXT #MSG USING MAP 'ADRMEM01'
*
  RESET #MSG
  IF *PF-KEY = 'PF3'
    SET WINDOW OFF
    PERFORM SYNC-WITH-CRM
    ESCAPE ROUTINE
  END-IF
*
  IF *PF-KEY = 'PF11' AND #MODE = 'U'
    IF #EML-PREF = 'Y'
      FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
      IF *NUMBER(0316) > 1
        REINPUT 'Designate another preferred email address before deleting.'
          MARK *#EML-PREF
      END-IF
    END-IF
    PERFORM DELETE-RECORD
    PERFORM UPDATE-STATUS/TRANS-REG
    PERFORM SYNC-WITH-CRM
    ESCAPE ROUTINE
  END-IF
*
  IF *PF-KEY = 'PF4'
    RESET #EMAIL-ID #EMAIL-ADDRESS #EML-PROTECT #EML-TYPE #EML-UNDLV #STATUS-DATE
      #START-DATE #END-DATE #DEL-TEXT #ADD-TEXT #LAST-DATE-UPD #LAST-USER-UPD #STATUS
      #REMARKS(*) 
      BEFORE_IMAGE.EMAIL-ADDRESS(1)       /* RE2
    MOVE 'N' TO #EML-PREF
    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
      IF NO RECORD FOUND
        MOVE 'Y' TO #EML-PREF
        ESCAPE BOTTOM
      END-NOREC
    END-FIND
    MOVE (AD=P) TO #EML-UNDLV-CV
    SET KEY PF4 NAMED OFF PF11 NAMED OFF
    SET KEY PF4=OFF PF11=OFF
    ESCAPE TOP
  END-IF
*
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #CONTACT-ID
    ESCAPE TOP
  END-IF
*
  IF #MAP-CV NOT MODIFIED AND #EML-UNDLV-CV NOT MODIFIED
    REINPUT 'No changes made. Press PF3 to exit.'
  ELSE
    PERFORM VALIDATE-FIELDS
    PERFORM ADD/UPDATE-RECORD
  END-IF
END-REPEAT
*********************************
DEFINE SUBROUTINE VALIDATE-FIELDS
*********************************
CALLNAT 'ADRN0005' #CODES #CODE-NAMES
*
IF #EMAIL-ADDRESS NE SCAN('@')
  REINPUT 'Invalid Email Address format (missing "@")'
    MARK 1
END-IF
*
EXAMINE #EMAIL-ADDRESS FOR '@' GIVING POSITION #POS
COMPUTE #END = 62 - #POS
EXAMINE SUBSTRING(#EMAIL-ADDRESS,#POS,#END) FOR '.' GIVING NUMBER #N
IF #N = 0
  REINPUT 'Invalid Email Address format (missing "." after "@")'
    MARK 1
END-IF
*
IF #EML-PREF = ' '
  REINPUT 'Enter Preferred code'
    MARK *#EML-PREF
END-IF
*
IF #EML-PREF NE ' ' AND NOT (#EML-PREF = 'Y' OR = 'N')
  REINPUT 'Invalid Preferred code'
    MARK *#EML-PREF
END-IF
*
IF #EML-PREF = 'N'
  FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
  IF *NUMBER(0387) > 2                                               
    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID WHERE EMAIL-ID = #EMAIL-ID AND CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
      REINPUT 'You must have one preferred email address.'
        MARK *#EML-PREF
    END-FIND
  END-IF
  IF #EMAIL-ID = 0 OR *NUMBER(0387) = 1
*    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-PREFERRED-IND = 'Y'   /* RE3 doesn't stow anymore
    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID WHERE CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
      IF NO RECORD FOUND
        REINPUT 'You must have one preferred email address.'
          MARK *#EML-PREF
      END-NOREC
      IF *NUMBER(0387) = 1 AND CNT-EMAIL-V.EMAIL-ADDRESS = #EMAIL-ADDRESS
        REINPUT 'You must have one preferred email address.'
          MARK *#EML-PREF
      END-IF
    END-FIND
  END-IF
END-IF
*
IF #EML-TYPE NE ' ' AND NOT (#EML-TYPE = 'P' OR = 'B' OR = 'O' OR = 'M')
  REINPUT 'Invalid Type'
    MARK *#EML-TYPE
END-IF
*
IF #EML-PROTECT NE ' '  AND NOT (#EML-PROTECT = 'Y' OR = 'N')
  REINPUT 'Invalid Protected code'
    MARK *#EML-PROTECT
END-IF
*
IF #EML-UNDLV NE ' ' AND NOT (#EML-UNDLV = 'Y' OR = 'N')
  REINPUT 'Invalid Undeliverable code'
    MARK *#EML-UNDLV
END-IF
*
IF NOT (#EML-STATUS = ' ' OR = 'E' OR = 'S')
  REINPUT 'Invalid Status'
    MARK *#EML-STATUS
END-IF
*
IF #EML-STATUS = ' ' AND #STATUS-DATE NE ' '
  REINPUT 'Status is missing'
    MARK *#EML-STATUS
END-IF
*
IF #STATUS-DATE NE ' ' AND #STATUS-DATE NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#STATUS-DATE
END-IF
*
IF #START-DATE > ' ' AND #START-DATE-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#START-DATE
END-IF
*
IF #END-DATE > ' ' AND #END-DATE-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#END-DATE
END-IF
*
IF #START-DATE > #END-DATE
  REINPUT 'Start Date must precede End Date'
    MARK *#START-DATE
END-IF
*
END-SUBROUTINE
***********************************
DEFINE SUBROUTINE ADD/UPDATE-RECORD
**********************************
RESET #UPDATE-FILES
IF #CONT-ID-NUMBER > 0 AND #EML-PREF = 'Y' AND
    (CNT-EMAIL-V.EMAIL-ADDRESS NE #EMAIL-ADDRESS OR STATUS NE #EML-STATUS)
  MOVE CNT-EMAIL-V.EMAIL-ADDRESS TO #SAVE-EMAIL
  MOVE TRUE TO #UPDATE-FILES
END-IF
*
IF #EMAIL-ID = 0
  RESET #SAVE-EMAIL
  FIND (1) TERM-V WITH TERMINAL-ID = 'EMAILID'
    MOVE #TERM-EMAIL-ID TO #EMAIL-ID
    ADD 1 TO #TERM-EMAIL-ID
    UPDATE
  END-FIND
  IF #EML-PREF = 'Y'
    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
      IF CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
        MOVE 'N' TO CNT-EMAIL-V.EMAIL-PREFERRED-IND
        UPDATE
        ESCAPE BOTTOM
      END-IF
    END-FIND
  END-IF
  MOVE #CONTACT-ID TO CNT-EMAIL-V.CONTACT-ID
  MOVE #EMAIL-ID   TO CNT-EMAIL-V.EMAIL-ID
  CNT-EMAIL-V.EMAIL-ADDRESS  := *TRANSLATE(#EMAIL-ADDRESS,LOWER)
  MOVE #EML-TYPE TO CNT-EMAIL-V.EMAIL-TYPE
  MOVE #EML-PROTECT TO CNT-EMAIL-V.EMAIL-PROTECTION-IND
  MOVE #EML-UNDLV TO CNT-EMAIL-V.EMAIL-UNDELIV
  MOVE #EML-PREF TO CNT-EMAIL-V.EMAIL-PREFERRED-IND
  MOVE #EML-STATUS TO STATUS
  IF #EML-STATUS NE ' ' AND #STATUS-DATE-N = 0
    MOVE *DATN TO CNT-EMAIL-V.STATUS-DATE
  ELSE
    IF #STATUS-DATE = ' '
      RESET CNT-EMAIL-V.STATUS-DATE
    ELSE
      MOVE #S2-MM TO #SD-MM
      MOVE #S2-DD TO #SD-DD
      MOVE #S2-YY TO #SD-YY
    END-IF
  END-IF
  MOVE #S-MM TO #ESD-MM
  MOVE #S-DD TO #ESD-DD
  MOVE #S-YY TO #ESD-YY
  MOVE #E-MM TO #EED-MM
  MOVE #E-DD TO #EED-DD
  MOVE #E-YY TO #EED-YY
  MOVE #REMARKS(*) TO REMARKS(*)
  MOVE *DATN TO LAST-DATE-UPD
  MOVE *TIMN TO LAST-TIME-UPD
  MOVE #CLERK-ID TO LAST-USER-UPD
  STORE CNT-EMAIL-V
  MOVE 'Record successfully added.' TO #MSG
  MOVE 'Add' TO #ADD-TEXT
  MOVE 'Del' TO #DEL-TEXT
  COMPRESS #L-MM '/' #L-DD '/' #L-YY INTO #LAST-DATE-UPD LEAVING NO
  SET KEY PF4 PF11
  FIND (1) TERM-V WITH CLERK-ID = #CLERK-ID
    IF NO RECORD FOUND     /* RE1 START
      FIND (1) TERM-V WITH TERMINAL-ID = LAST-USER-UPD
        MOVE COMMENTS TO #LAST-USER-UPD
      END-FIND
      ESCAPE BOTTOM
    END-NOREC              /* RE1 END
    MOVE COMMENTS TO #LAST-USER-UPD
  END-FIND
ELSE
  IF #EML-PREF = 'Y'
    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
      IF EMAIL-ID NE #EMAIL-ID AND CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
        MOVE 'N' TO CNT-EMAIL-V.EMAIL-PREFERRED-IND
        UPDATE
        ESCAPE BOTTOM
      END-IF
    END-FIND
  ELSE
    FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
    IF *NUMBER(0535) < 3
      FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
        IF EMAIL-ID NE #EMAIL-ID
          MOVE 'Y' TO CNT-EMAIL-V.EMAIL-PREFERRED-IND
          UPDATE
          ESCAPE BOTTOM
        END-IF
      END-FIND
    END-IF
  END-IF
  FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID
    ACCEPT IF EMAIL-ID = #EMAIL-ID
    MOVE #EMAIL-ADDRESS TO CNT-EMAIL-V.EMAIL-ADDRESS
    MOVE #EML-TYPE TO CNT-EMAIL-V.EMAIL-TYPE
    MOVE #EML-PROTECT TO CNT-EMAIL-V.EMAIL-PROTECTION-IND
    MOVE #EML-UNDLV TO CNT-EMAIL-V.EMAIL-UNDELIV
    MOVE #EML-PREF TO CNT-EMAIL-V.EMAIL-PREFERRED-IND
    MOVE #EML-STATUS TO STATUS
    IF #EML-STATUS NE ' ' AND #STATUS-DATE-N = 0
      MOVE *DATN TO CNT-EMAIL-V.STATUS-DATE
    ELSE
      IF #STATUS-DATE = ' '
        RESET CNT-EMAIL-V.STATUS-DATE
      ELSE
        MOVE #S2-MM TO #SD-MM
        MOVE #S2-DD TO #SD-DD
        MOVE #S2-YY TO #SD-YY
      END-IF
    END-IF
    MOVE #S-MM TO #ESD-MM
    MOVE #S-DD TO #ESD-DD
    MOVE #S-YY TO #ESD-YY
    MOVE #E-MM TO #EED-MM
    MOVE #E-DD TO #EED-DD
    MOVE #E-YY TO #EED-YY
    MOVE #REMARKS(*) TO REMARKS(*)
    MOVE *DATN TO LAST-DATE-UPD
    MOVE *TIMN TO LAST-TIME-UPD
    MOVE #CLERK-ID TO LAST-USER-UPD
    UPDATE
  END-FIND
  MOVE 'Record successfully updated.' TO #MSG
END-IF
*
PERFORM UPDATE-STATUS/TRANS-REG
*
END TRANSACTION
*
MOVE TRUE TO #SYNCHRONIZE            /* RE3
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE DELETE-RECORD
*******************************
RESET #CONFIRM-DELETE
SET WINDOW 'Confirm-Delete'
REPEAT
  INPUT WINDOW='Confirm-Delete' USING MAP 'ADRMEM02'
  IF *PF-KEY = 'PF3' OR #CONFIRM-DELETE = 'N'
    ESCAPE BOTTOM
  END-IF
  IF #CONFIRM-DELETE = 'Y'
    MOVE #EMAIL-ADDRESS TO #SAVE-EMAIL
    FIND (1) CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID AND EMAIL-ID = #EMAIL-ID
      DELETE
      END TRANSACTION
      MOVE TRUE TO #SYNCHRONIZE           /* RE3
      MOVE 'Record successfully deleted.' TO #MSG
    END-FIND
    ESCAPE BOTTOM
  END-IF
END-REPEAT
SET WINDOW OFF
*
END-SUBROUTINE
*****************************************
DEFINE SUBROUTINE UPDATE-STATUS/TRANS-REG
*****************************************
FIND ST-V WITH ID-NUMBER = #CONT-ID-NUMBER
  IF #CONFIRM-DELETE = 'Y'
*    #OLD-DATA(1)  := #SAVE-EMAIL              /* RE2   /* TT1 BEGIN
*    IF INTERNET-ADDR = #SAVE-EMAIL
      RESET INS-MAIL-CODE       
*    END-IF
  ELSE
*    FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID WHERE CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
*  DISPLAY EMAIL-ID #EMAIL-ID EMAIL-ADDRESS(AL=15) #SAVE-EMAIL(AL=15) EMAIL-PREFERRED-IND /* INTERNET-ADDR(AL=10)
*      IF EMAIL-ID = #EMAIL-ID AND (CNT-EMAIL-V.EMAIL-ADDRESS NE #SAVE-EMAIL OR #EML-PREF = 'Y')
*        #OLD-DATA(1)  := #SAVE-EMAIL           /* RE2
*        INTERNET-ADDR := #EMAIL-ADDRESS
*      END-IF
*    END-FIND                                            /* TT1 END
    MOVE #EML-STATUS TO INS-MAIL-CODE
  END-IF
  UPDATE
END-FIND
*
FIND CNT-EMAIL-V WITH CONTACT-ID = #CONTACT-ID /* WHERE CNT-EMAIL-V.EMAIL-PREFERRED-IND = 'Y'  /* RE2
  AND EMAIL-ID = #EMAIL-ID                     /* RE2  
  IF NO RECORD FOUND
    IGNORE
  END-NOREC
  MOVE *ISN TO #ISN                                                            /* RE2 START
*  CALLNAT 'ADRNE022' CNT-EMAIL-V.CONTACT-ID #ISN #TEXT(*)
*  MOVE #TEXT(1) TO AFTER_IMAGE.EMAIL-ADDRESS(1)
  IF #CONT-ID-NUMBER > 0
    MOVE #CONT-ID-NUMBER TO TR_ID_NUMBER
  ELSE
    MOVE #MEMBER-ID-NUMBER TO TR_ID_NUMBER      
    FIND ST-V WITH ID-NUMBER = #MEMBER-ID-NUMBER
      IF F-CONTACT-INFO(<ST-V.MEMBER-CONTACT-ID,F-CONTACT-DATA>)
        IGNORE
      END-IF
    END-FIND
  END-IF
  MOVE #CONT-FMTD-NAME TO TR_NAME    
  MOVE #CLERK-ID       TO TR_CLERK 
  MOVE #EMAIL-ADDRESS  TO AFTER_IMAGE.EMAIL-ADDRESS(1)  
  MOVE #EML-TYPE       TO AFTER_IMAGE.EMAIL-TYPE(1) 
  MOVE #EML-PROTECT    TO AFTER_IMAGE.EMAIL-PROTECTION-IND(1)
  MOVE #EML-UNDLV      TO AFTER_IMAGE.EMAIL-UNDELIV(1) 
  MOVE #EML-PREF       TO AFTER_IMAGE.EMAIL-PREFERRED-IND(1)  
  MOVE #STATUS         TO AFTER_IMAGE.EMAIL-STATUS(1) 
*  print '=' before_IMAGE.EMAIL-ADDRESS(1)  '='AFTER_IMAGE.EMAIL-ADDRESS(1)  
*   '='before_IMAGE.EMAIL-PREFERRED-IND(1) '='AFTER_IMAGE.EMAIL-PREFERRED-IND(1) 
*   '='before_IMAGE.EMAIL-protection-IND(1) '='AFTER_IMAGE.EMAIL-protection-IND(1) 
* PRINT   *PROGRAM *LINE 'Callnat'

  CALLNAT 'GPRNTR00' #EMAIL TRAN_LOG_HEADER BEFORE_IMAGE AFTER_IMAGE            /* RE2   END
*  IF EMAIL-ADDRESS NE #SAVE-EMAIL OR #EML-PREF = 'Y' OR #CONFIRM-DELETE = 'Y'
*    #FIELD-DESCRIPTION(1) := 'EMAIL-ID'
*    #SYNC-FIELD-TABLE(1)  := 15
*    IF #CONFIRM-DELETE = 'Y'
**      RESET  #NEW-DATA(1) RESET #EMAIL-ADDRESS
*    ELSE
*      #NEW-DATA(1)        := #EMAIL-ADDRESS
*    END-IF
*    #TR-NAME              := #CONT-FMTD-NAME
*    #TR-ID-NUMBER         := #CONT-ID-NUMBER
*    #LOG-DESCRIPTION      := 'CG-EMAIL-ID'
*    #TR-CLERK-ID          := #CLERK-ID
*    #TR-DATE-LAST-UPDATE  := *DATN
*    CALLNAT 'GPRN0004'
*      #TR-FIELDS #SYNC-FIELD-TABLE(*)
*      #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
*  END-IF
END-FIND
*
END-SUBROUTINE
*******************************
DEFINE SUBROUTINE SYNC-WITH-CRM
*******************************
IF #SYNCHRONIZE           /* RE3 START
  CALLNAT 'MBRCNTC'
    #CONTACT-ID
    XML_SERIALZE_OUTPUT
    #CSV-LINE
    #CRM-Result
    #Contact
END-IF                    /* RE3 END
*
END-SUBROUTINE
*
END
