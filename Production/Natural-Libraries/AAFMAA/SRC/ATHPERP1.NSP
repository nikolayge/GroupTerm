* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPERP1 - exception report for Policy services
* Allotment authorization 'Y' or 'S' where MODE is not G
***********************************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  07262007 Initial creation
* DRW 06/06/2008 DW1 Add ALOT-CHG-AUTH "A" checking
* RSE 09/22/2009 RE1 Change PROCESS-IND/600k logic for Unification
* JNT 09/17/2013 JT1 Address normalization
***********************************************************************
*
DEFINE DATA
LOCAL
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-FLAG    (A1)
  2 INTRO-FLAG
  2 PROCESS-IND
  2 ALOT-CHG-AUTH
  2 NAME         (A25)
*   2 ADDRESS-1    (A25)                         /* JT1
  2 LAST-FM-DATE
  2 ISSUE-DATE          (N8/9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC    (9)
      4 ID-YYYYMM   (N6)
      4 REDEFINE ID-YYYYMM
        5 ID-YYYY    (N4)
        5 ID-MM      (N2)
      4 ID-DD       (N2)
  2 PAY-STATUS          (9)
  2 X-BILL-ADJUST       (9)
  2 PLAN                (9)
  2 STATUS              (9)
  2 MODE                (9)
*
1 #ID-NUMBER                (N6)
1 #RECS-READ                (N6)
1 #RECS-WRITTEN             (N6)
1 #I                        (N2)
1 #REPORT-RECORD            (A125)
1 #STATUS                   (A8) INIT <'ACTIVE'>
1 #TAB                      (A1) INIT <H'09'>
* * * * * * * * * * * * * * * * * * * * * * *
END-DEFINE
*
FORMAT LS=133 PS=76
WRITE TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
  'Program Messages' 5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
COMPRESS ' ' #TAB 'PROGRAM: ' *PROGRAM #TAB 'DATE: ' *DATN #TAB 'TIME: ' *TIMX #TAB
  INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
COMPRESS ' ' #TAB 'ALLOTMENT AUTHORIZATION RECORDS WITH NON "G" PAYMENT MODE' #TAB
  INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
*
COMPRESS 'CN' #TAB 'NAME' #TAB 'ALLOTMENT' #TAB 'POLICY' #TAB 'PAYMENT' #TAB
  'ISSUE-DATE' #TAB INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
COMPRESS ' ' #TAB ' ' #TAB 'AUTHORIZATION' #TAB 'STATUS' #TAB 'MODE' #TAB
  INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
*
READ ST-V BY MEMBER-KEY FROM '001900'
*
*  IF PROCESS-IND = 'S'   /* OR = 'C'  - include pending members      RE1
  IF AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)                            /* RE1 
    ESCAPE TOP
  END-IF
*  IF ST-V.ID-NUMBER GE 600000                                     /* RE1
*    ESCAPE BOTTOM                                                 /* RE1
*  END-IF                                                          /* RE1
*
  RESET #REPORT-RECORD
  ADD 1 TO #RECS-READ
*
  IF ALOT-CHG-AUTH = 'Y' OR = 'S' or = 'A'     /* DW1
    FOR #I = 1 TO 9
      IF ISSUE-DATE(#I) = 0
        ESCAPE BOTTOM
      ELSE
        IF STATUS(#I) = 'D' AND MODE(#I) NE 'G'
          #ID-NUMBER := ID-NUMBER
          #RECS-WRITTEN := #RECS-WRITTEN + 1
          COMPRESS ID-NUMBER #TAB NAME #TAB ALOT-CHG-AUTH #TAB #STATUS #TAB
            MODE(#I) #TAB ISSUE-DATE(#I) #TAB INTO #REPORT-RECORD
          WRITE WORK FILE 1 #REPORT-RECORD
        END-IF
      END-IF
    END-FOR
  END-IF
*
END-READ
*
RESET #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
COMPRESS ' ' #TAB 'RECORDS WITH NON "G" MODE' #TAB #RECS-WRITTEN #TAB
  INTO #REPORT-RECORD
WRITE WORK FILE 1 #REPORT-RECORD
*
END
