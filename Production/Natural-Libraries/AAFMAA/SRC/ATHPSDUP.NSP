* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPSDUP - DUPLICATE SSNS ON DATABASE
*
*
DEFINE DATA
LOCAL
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER                  (N6)
 2 WIDOW-FLAG                 (A1)
 2 LINKED-CN                  (N6/10)
 2 INTRO-FLAG                 (A1)
 2 NAME                       (A25)
 2 NAME-SP                    (A25)
 2 FORMER-SPOUSE-NAME         (A25)
 2 FORMER-HUSBAND-NAME        (A25)
 2 SSN                        (N9)
 2 SSN-SP                     (N9)
 2 FORMER-SPOUSE-SSN          (N9)
 2 FORMER-HUSBAND-SSN         (N9)
*
1 FR-V VIEW OF A-FIRS
 2 ID-NUMBER                  (N6)
 2 DEPN-SSN                   (P9/14)
 2 DEPN-NAME                  (A15/14)
*
1 #CNT          (N5)
1 #EXT-REC        (A42)
1 REDEFINE #EXT-REC
 2 #EXT-SSN       (N9)
 2 #EXT-CN        (N6)
 2 REDEFINE #EXT-CN
  3 #EXT-CN-A     (A6)
 2 #EXT-TYPE      (A1)
 2 #EXT-INT-FL    (A1)
 2 #EXT-NAME      (A25)
*
1 #FND-LNKED-CN      (L)
1 #FND-LNKED-CN-X    (L)
1 #XXX               (A3)
1 #I1                (I2)
1 #LAST-SSN          (N9)
1 #LAST-CN           (N6)
1 REDEFINE #LAST-CN
 2 #LAST-CN-A        (A6)
1 #LAST-INT-FL       (A1)
1 #TYPE-X            (A9)
1 #LAST-INT-FL-X     (A8)
1 #INT-FL-X          (A8)
1 #LAST-TYPE         (A1)
1 #LAST-TYPE-X       (A9)
1 #LAST-NAME         (A25)
*
1 #TODAY             (N8)
1 REDEFINE #TODAY
 2 #TODAY-YYYY       (N4)
 2 #TODAY-MM         (N2)
 2 #TODAY-DD         (N2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
        'EXCEPTIONS'
        5X 'DATE:' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
        'DUPLICATE SSNS WITHIN DATABASE'
           5X 'DATE:' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
        'LINKED SPS/GEN POLICIES'
           5X 'DATE:' *DAT4U 5X 'TIME: ' *TIMX
MOVE *DATN TO #TODAY
*
READ ST-V PHYSICAL
  IF ID-NUMBER LT 1900
    ESCAPE TOP
  END-IF
*
  IF SSN NE 0
    MOVE SSN        TO #EXT-SSN
    MOVE ID-NUMBER  TO #EXT-CN
    MOVE INTRO-FLAG TO #EXT-INT-FL
    MOVE NAME       TO #EXT-NAME
*
    IF WIDOW-FLAG = 'Y'
      MOVE '2' TO #EXT-TYPE
    ELSE
      MOVE '1' TO #EXT-TYPE
    END-IF
*
    WRITE WORK 2 #EXT-REC
  END-IF
*
  IF SSN-SP NE 0
    MOVE SSN-SP     TO #EXT-SSN
    MOVE ID-NUMBER  TO #EXT-CN
    MOVE INTRO-FLAG TO #EXT-INT-FL
    MOVE NAME-SP    TO #EXT-NAME
*
    IF WIDOW-FLAG = 'Y'
      MOVE '4' TO #EXT-TYPE
    ELSE
      MOVE '3' TO #EXT-TYPE
    END-IF
*
    WRITE WORK 2 #EXT-REC
  END-IF
*
  IF FORMER-SPOUSE-SSN NE 0
    MOVE FORMER-SPOUSE-SSN  TO #EXT-SSN
    MOVE ID-NUMBER          TO #EXT-CN
    MOVE INTRO-FLAG         TO #EXT-INT-FL
    MOVE FORMER-SPOUSE-NAME TO #EXT-NAME
    MOVE '5' TO #EXT-TYPE
    WRITE WORK 2 #EXT-REC
  END-IF
*
  IF FORMER-HUSBAND-SSN NE 0
    MOVE FORMER-HUSBAND-SSN  TO #EXT-SSN
    MOVE ID-NUMBER           TO #EXT-CN
    MOVE INTRO-FLAG          TO #EXT-INT-FL
    MOVE FORMER-HUSBAND-NAME TO #EXT-NAME
    MOVE '6' TO #EXT-TYPE
    WRITE WORK 2 #EXT-REC
  END-IF
END-READ
*
READ FR-V PHYSICAL
  IF FR-V.ID-NUMBER LT 1900
    ESCAPE TOP
  END-IF
*
  FOR #I1 1 TO 14
    IF DEPN-SSN (#I1) NE 0
      MOVE DEPN-SSN (#I1)  TO #EXT-SSN
      MOVE ID-NUMBER       TO #EXT-CN
      MOVE INTRO-FLAG      TO #EXT-INT-FL
      MOVE DEPN-NAME (#I1) TO #EXT-NAME
      MOVE '7' TO #EXT-TYPE
      WRITE WORK 2 #EXT-REC
    END-IF
  END-FOR
END-READ
*
* * * * * * READ EXTRACT FILE *
*
REPEAT
  READ WORK 2 ONCE RECORD #EXT-REC
    AT END
      ESCAPE BOTTOM
    END-ENDFILE
END-ALL
*
SORT BY #EXT-SSN #EXT-CN #EXT-TYPE
  USING #EXT-INT-FL #EXT-NAME
*
  IF #EXT-SSN NE #LAST-SSN
    MOVE #EXT-SSN    TO #LAST-SSN
    MOVE #EXT-CN     TO #LAST-CN
    MOVE #EXT-TYPE   TO #LAST-TYPE
    MOVE #EXT-INT-FL TO #LAST-INT-FL
    MOVE #EXT-NAME   TO #LAST-NAME
    ESCAPE TOP
  END-IF
*
  RESET #FND-LNKED-CN #FND-LNKED-CN-X
  IF   (#LAST-TYPE = '3' OR = '4' OR = '5' OR = '7')   /* sps/depn
   AND (#EXT-INT-FL = 'S' OR = 'K' OR = 'G') /* sps-gen
    FIND (1) ST-V WITH MEMBER-KEY = #EXT-CN-A
      FOR #I1 1 TO 10
        IF LINKED-CN (#I1) = #LAST-CN
          MOVE TRUE TO #FND-LNKED-CN
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
*
    FIND (1) ST-V WITH MEMBER-KEY = #LAST-CN-A
      FOR #I1 1 TO 10
        IF LINKED-CN (#I1) = #EXT-CN
          MOVE TRUE TO #FND-LNKED-CN-X
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
  END-IF
*
  RESET #XXX #LAST-INT-FL-X #INT-FL-X
*
  IF #EXT-CN = #LAST-CN
    MOVE '***' TO #XXX
  END-IF
*
  ADD 1 TO #CNT
*
  DECIDE ON FIRST VALUE OF #LAST-TYPE
    VALUE '1' MOVE 'MBR/INS'   TO #LAST-TYPE-X
    VALUE '2' MOVE 'DEC MBR'   TO #LAST-TYPE-X
    VALUE '3' MOVE 'SPOUSE'    TO #LAST-TYPE-X
    VALUE '4' MOVE 'WIDOW/ER'  TO #LAST-TYPE-X
    VALUE '5' MOVE 'FMR SPS'   TO #LAST-TYPE-X
    VALUE '6' MOVE 'FMR HUSB'  TO #LAST-TYPE-X
    VALUE '7' MOVE 'DEPENDANT' TO #LAST-TYPE-X
    NONE      MOVE 'UNKNOWN'   TO #LAST-TYPE-X
  END-DECIDE
*
  DECIDE ON FIRST VALUE OF #EXT-TYPE
    VALUE '1' MOVE 'MBR/INS'   TO #TYPE-X
    VALUE '2' MOVE 'DEC MBR'   TO #TYPE-X
    VALUE '3' MOVE 'SPOUSE'    TO #TYPE-X
    VALUE '4' MOVE 'WIDOW/ER'  TO #TYPE-X
    VALUE '5' MOVE 'FMR SPS'   TO #TYPE-X
    VALUE '6' MOVE 'FMR HUSB'  TO #TYPE-X
    VALUE '7' MOVE 'DEPENDANT' TO #TYPE-X
    NONE      MOVE 'UNKNOWN'   TO #TYPE-X
  END-DECIDE
*
  DECIDE ON FIRST VALUE OF #LAST-INT-FL
    VALUE 'S' MOVE 'SPOUSE'   TO #LAST-INT-FL-X
    VALUE 'K' MOVE 'CHILD'    TO #LAST-INT-FL-X
    VALUE 'G' MOVE 'GRNDCHLD' TO #LAST-INT-FL-X
    NONE      IGNORE
  END-DECIDE
*
  DECIDE ON FIRST VALUE OF #EXT-INT-FL
    VALUE 'S' MOVE 'SPOUSE'   TO #INT-FL-X
    VALUE 'K' MOVE 'CHILD'    TO #INT-FL-X
    VALUE 'G' MOVE 'GRNDCHLD' TO #INT-FL-X
    NONE      IGNORE
  END-DECIDE
*
  IF #FND-LNKED-CN
    IF NOT #FND-LNKED-CN-X
      WRITE 'Linked #' #EXT-CN 'not backwards linked to' #LAST-CN
    END-IF
*
    DISPLAY (3) 'SSN'            #EXT-SSN   (EM=999-99-9999)
                'MAIN/CN'        #LAST-CN
                'LINKED/CN'      #EXT-CN
                'MAIN/TYPE'      #LAST-TYPE-X
                'LINKED/INSURED' #INT-FL-X
                'MAIN-NAME'      #LAST-NAME
                'LINKED-NAME'    #EXT-NAME
  ELSE
    DISPLAY (2) 'SSN'             #EXT-SSN   (EM=999-99-9999)
                'XXX'             #XXX
                'MAIN/CN'         #LAST-CN
                'MATCHED/CN'      #EXT-CN
                'MAIN/TYPE'       #LAST-TYPE-X
                'MATCHED/TYPE'    #TYPE-X
                'MAIN/INSURED'    #LAST-INT-FL-X
                'MATCHED/INSURED' #INT-FL-X
                'MAIN-NAME'       #LAST-NAME
                'MATCHED-NAME'    #EXT-NAME
  END-IF
END-SORT
*
END
