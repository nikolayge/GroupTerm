* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVPIM - EXTRACT INTRO MBR INFO FOR "VP"
*
*********************************************************************  
**           MODIFICATIONS
** WHO   DATE   TAG      COMMENTS
** SAG 11012005 RH1 AB - Replace CALL to Cobol with CALLNAT to Natural
*********************************************************************
DEFINE DATA
LOCAL USING GC2V6LXX
LOCAL
*
1 #JCL-PARM          (A20)
1 REDEFINE #JCL-PARM
 2 #JCL-FIL-1        (A12) /* 'TODAYS DATE='  (ASSUMES USING CURR FILE)
 2 #JCL-CURR-YYYY    (N4)
 2 #JCL-CURR-MMDD    (N4)
 2 REDEFINE #JCL-CURR-MMDD
  3 #JCL-CURR-MM     (N2)
  3 #JCL-CURR-DD     (N2)
*
1 IM-V VIEW OF A-INTRO-MEMBER
 2 ID-NUMBER            (N6)
 2 RANK                 (A3)
 2 NAME                 (A25)
 2 SEX                  (A1)
 2 SMOKER-FLAG          (A1)
*  2 S-ADDRESS-1          (A25)
*  2 S-ADDRESS-2          (A25)
*  2 S-ADDRESS-3          (A25)
*  2 S-ZIP-CODE           (N9)
 2 DATE-OF-BIRTH        (N8)
 2 EFF-DATE             (N8)
 2 ISSUE-AGE            (P3)
 2 ANNUAL-PREMIUM       (P5.2)
 2 PAID-THRU-DATE       (N8)
*
1 #EXTR-RECORD
 2 #RECORD-1             (A80)    /* USES 67
 2 REDEFINE #RECORD-1
  3 #ID-NUMBER-1          (N6)
  3 #LOAN-IND             (A1)
  3 #PLAN                 (A3)
  3 #F1-02                (A1)
  3 #ISS-DATE             (N8)
  3 #F1-03                (A1)
  3 #ISS-AGE              (A2)
  3 #F1-04                (A1)
  3 #UNITS                (N1)
  3 #F1-05                (A1)
  3 #LAST-DURATION        (N2)
  3 #F1-06                (A1)
  3 #MONTH-IND            (N2)
  3 #F1-07                (A1)
  3 #CONV-DUR             (N2)
  3 #F1-08                (A1)
  3 #PAY-STATUS           (A2)
  3 #F1-09                (A1)
  3 #ANN-PREM             (N5.2)
  3 #F1-10                (A1)
  3 #MODE                 (A1)
  3 #F1-11                (A1)
  3 #SMOKER-FLAG          (A1)
  3 #F1-12                (A1)
  3 #DOB                  (N8)
  3 #F1-13                (A1)
  3 #ACV                  (N7.2)
*
 2 #RECORD-2             (A80)     /* USES 66
 2 REDEFINE #RECORD-2
  3 #ID-NUMBER-2          (N6)
  3 #F2-01                (A1)
  3 #CV                   (N7.2)
  3 #F2-02                (A1)
  3 #MIN-CV               (N5.2)
  3 #F2-03                (A1)
  3 #NXT-YR-MIN-CV        (N5.2)
  3 #F2-04                (A1)
  3 #DB                   (N7.2)
  3 #F2-05                (A1)
  3 #LAST-ANN-CV          (N7.2)
  3 #F2-06                (A1)
  3 #FACE-AMT             (N5.2)
  3 #F2-07                (A1)
  3 #GROWTH-RATE          (N0.5)
*
 2 #RECORD-3             (A80)     /* USES 74
 2 REDEFINE #RECORD-3
  3 #ID-NUMBER-3          (N6)
  3 #F3-01                (A1)
  3 #NAME                 (A15)
  3 #F3-02                (A1)
  3 #MONTHLY-ALLOC-CRED   (N3.2)
  3 #F3-03                (A1)
  3 #MONTHLY-PREM-DEFIC   (N3.2)
  3 #F3-04                (A1)
  3 #NET-PREM-RATIO       (N1.6)
  3 #F3-05                (A1)
  3 #LOAN-AMT             (N5.2)
  3 #F3-06                (A1)
  3 #LOAN-INT-RATE        (N0.5)
  3 #F3-07                (A1)
  3 #SEX                  (A1)
  3 #F3-08                (A1)
  3 #CELL-COUNT           (N5)
  3 #REAL-ISS-DATE        (N8)
  3 #REAL-ISS-AGE         (A2)
*
1 #COUNTERS
  2 #RECS-READ        (P5)
  2 #RECS-WRITTEN     (P5)
  2 #CNT              (P5)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #WHOLE-DOLLARS    (P5)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INPUT #JCL-PARM           /* GET CURRENT PROCESS DATE
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
        'INTRO MEMBER EXTRACT FOR YEAR-END VALUE-ADDED PROJECTIONS'
               5X 'DATE: '*DATX 5X 'TIME: ' *TIMX
*
READ IM-V
  ADD 1 TO #RECS-READ
*
  RESET #L-604-DB
  RESET #L-604-MIN-CSV
  RESET #L-604-RSV-35-DIFF
  RESET #L-604-RSV-VA-DIFF
  RESET #L-604-NEW-PRM
  RESET #L-604-NEW-LST-MCV
  RESET #L-604-NEW-NXT-MCV
  RESET #L-604-NEW-CUR-MCV
  RESET #L-604-NEW-NPR
*
  MOVE 604            TO #L-CI-TRAN
  MOVE 'OL'           TO #L-604-FM-TYPE
  MOVE 5000           TO #L-604-FAC-AMT
  MOVE 'SP'           TO #L-604-TO-TYPE
  MOVE SEX            TO #L-604-SEX
  MOVE ISSUE-AGE      TO #L-604-ISS-AGE
  MOVE ANNUAL-PREMIUM TO #L-604-OLD-PRM
* MOVE 1              TO #L-604-MBSHPS   /* REMOVED 8/2005
  MOVE 920901         TO #L-604-ISS-DATE
  MOVE 920901         TO #L-604-TRN-DATE
  DISPLAY ID-NUMBER ISSUE-AGE ANNUAL-PREMIUM
*
**CALL 'AOCX2V6B' #L-CI-TRAN       /* SAG 11012005 RH1
  CALLNAT 'AOCX2V6B' #L-PARMS      /* SAG 11012005 RH1
*
  IF #L-604-RSV-VA-DIFF LE 0
    WRITE 'INVALID SINGLE PREMIUM AMOUNT'
  END-IF
*
  DISPLAY ID-NUMBER #WHOLE-DOLLARS ISSUE-AGE
  MOVE ROUNDED #L-604-RSV-VA-DIFF TO #WHOLE-DOLLARS
*
  MOVE ID-NUMBER                   TO #ID-NUMBER-1
                                      #ID-NUMBER-2
                                      #ID-NUMBER-3
  MOVE 'OL'                        TO #PLAN
  MOVE 19920901                    TO #REAL-ISS-DATE
  MOVE ISSUE-AGE                   TO #REAL-ISS-AGE
  MOVE 1                           TO #UNITS
  MOVE 0                           TO #LAST-DURATION
  MOVE 0                           TO #MONTH-IND
  MOVE 0                           TO #CONV-DUR
  MOVE '47'                        TO #PAY-STATUS
  MOVE ANNUAL-PREMIUM              TO #ANN-PREM
  MOVE 'G'                         TO #MODE
  MOVE SEX                         TO #SEX
  MOVE SMOKER-FLAG                 TO #SMOKER-FLAG
  MOVE DATE-OF-BIRTH               TO #DOB
  MOVE #WHOLE-DOLLARS              TO #ACV
  MOVE #WHOLE-DOLLARS              TO #CV
  MOVE 0                           TO #MIN-CV
  MOVE 0                           TO #NXT-YR-MIN-CV
  MOVE NAME                        TO #NAME
  MOVE 0                           TO #DB
  MOVE 0                           TO #LAST-ANN-CV
  MOVE 0                           TO #FACE-AMT
  MOVE .8900                       TO #GROWTH-RATE
  MOVE 0                           TO #MONTHLY-ALLOC-CRED
  MOVE 0                           TO #MONTHLY-PREM-DEFIC
  MOVE 0                           TO #NET-PREM-RATIO
  RESET #LOAN-AMT
  MOVE EFF-DATE                    TO #ISS-DATE
  MOVE ISSUE-AGE                   TO #ISS-AGE
*
* WRITE WORK 2 #EXTR-RECORD
  WRITE WORK 2 #RECORD-1
  WRITE WORK 2 #RECORD-2
  WRITE WORK 2 #RECORD-3
  ADD 1 TO #RECS-WRITTEN
END-READ
*
WRITE  'TOT-RECS-READ     = ' #RECS-READ
WRITE  'TOT-RECS-WRITTEN  = ' #RECS-WRITTEN
*
END
