* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID HELPDD01
*   Help screen to display any drop downs from GEN-TABLE
*
********************************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* YAK  07202011 YK1  Initial Creation
********************************************************
DEFINE DATA
PARAMETER
1 #DATA-ARRAY (A/*) Dynamic
1 #HEADER     (A) Dynamic
1 #SEL-VALUE  (A) Dynamic
1 #WIN-DIR    (A50)
*
LOCAL
1 #NUM-FILES  (N6)
1 #MAX-SCREEN (N2)
1 #MAX-LINES  (N2) INIT <10> 
1 #WIN-LENGTH (N2) INIT <60>
1 #WIN-ROW    (N2) 
1 #I          (N6)
1 #I1         (N6)
1 #TEXT       (A70)
1 #SCREEN     (N2) INIT <0>
1 #SEL        (A1/10)
1 #SCR-DESCRIPTION (A60/10)
1 v_MSG        (A78)
1 #IN-LINE     (A200)
END-DEFINE
*
ON ERROR
  SET WINDOW OFF
END-ERROR
#NUM-FILES := *OCCURRENCE(#DATA-ARRAY)
#MAX-SCREEN := #NUM-FILES / #MAX-LINES
IF #MAX-SCREEN * #MAX-LINES < #NUM-FILES
  #MAX-SCREEN := #MAX-SCREEN + 1
END-IF
*
#WIN-LENGTH := 60
#WIN-ROW    := #NUM-FILES + 10 
IF #WIN-ROW > #MAX-LINES 
   #WIN-ROW := #MAX-LINES + 8
END-IF
*     
DEFINE WINDOW HELPWINDOW
 SIZE #WIN-ROW*#WIN-LENGTH
 BASE 7/5
 TITLE #HEADER
*
SET KEY PF7 NAMED 'Up'
SET KEY PF8 NAMED 'Down'
SET KEY PF12 NAMED 'Quit'
SET KEY PF3 NAMED 'Retrn'
SET WINDOW 'HELPWINDOW'
*
PERFORM POPULATE-SCREEN
REPEAT
  INPUT WITH TEXT #TEXT USING MAP 'GPRMDD02'
    IF *PF-KEY = 'PF12' OR *PF-KEY = 'PF3'
     SET WINDOW OFF
     ESCAPE ROUTINE
    END-IF
 IF *PF-KEY = 'ENTR'    
     FOR #I = 1 TO #MAX-LINES
       IF #SEL(#I) <> ' ' ESCAPE BOTTOM END-IF
     END-FOR  
     IF #I = 0 OR #I = #MAX-LINES + 1
       #I := *CURS-LINE
       #I := #I - 1 
     END-IF  
     #SEL-VALUE := #SCR-DESCRIPTION(#I)
     SET WINDOW OFF
     ESCAPE ROUTINE
 ELSE
  IF *PF-KEY = 'PF8'
     IF #SCREEN = #MAX-SCREEN - 1
        #TEXT := 'Last screen'
        ESCAPE TOP
   ELSE
     #SCREEN := #SCREEN + 1
     PERFORM POPULATE-SCREEN
     ESCAPE TOP
   END-IF
  END-IF
  IF *PF-KEY = 'PF7'
     IF #SCREEN = 0
      #TEXT := 'First screen'
    ESCAPE TOP
     ELSE
       #SCREEN := #SCREEN - 1
       PERFORM POPULATE-SCREEN
       ESCAPE TOP
     END-IF
  END-IF
 END-IF
END-REPEAT
SET WINDOW OFF
*
DEFINE SUBROUTINE POPULATE-SCREEN
*
  #TEXT := 'Select with cursor and hit "Enter"'
  RESET #SCR-DESCRIPTION(*)
  FOR #I1 = 1 TO #MAX-LINES
   #I := #SCREEN * #MAX-LINES + #I1
   IF #I > #NUM-FILES
     ESCAPE BOTTOM
   ELSE
     #SCR-DESCRIPTION(#I1) := #DATA-ARRAY(#I)
   END-IF
  END-FOR
END-SUBROUTINE
*
END
