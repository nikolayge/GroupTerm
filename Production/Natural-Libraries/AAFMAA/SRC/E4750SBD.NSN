* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
DEFINE DATA
************************************************************************
*                       MODIFICATION LOG
* USER   DATE      TAG  REASON
* DRW    12102004  DW1  hide ssn on ann stmts
* DRW    01032005  DW2  show bene labels on alternate if not on prim*
* DRW    02012006  DW3  Left justify Name label for Ann STM word merge
* DRW    10112006  DW4  CHG trust explanation
* DRW   05/30/2008 DW5  Add a sentence to Bene descriprion and add 'Owner or'
* DRW  08/21/2008  DW6  Stop replacing Bene Number when counting Benes
* DRW  09/25/2008  DW7  Change type 'D' per stirpes text
* YAK  09/30/2008  YK1  Add logic for Policy print
*                       Pass beneficiary information back to calling program
*                       and further to the batch driver through STACK DATA
*                       instead of writing to 800117 record on the Note file
* DRW 01/15/2009 DW8 Make 'bene be paid' sentence show for Non-mbr owner
* DRW 01/27/2009 DW9 Add new-bene-processing indicator to linkage
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* vxt 09/11/2014 vt1  Allign the text in the shown text/Ticket 3054/
* NGG 03/30/2016 TICKET 4391
************************************************************************
PARAMETER
1 #CGA-SCR-COMMAND (A2)
1 #CGA-SCR-ID      (N9)
1 #CGA-SCR-SUFFIX  (A3)
1 REDEFINE #CGA-SCR-SUFFIX
  2 #CGA-SCR-SUFFIX-2  (A2)
  2 #CGA-SCR-SUFFIX-SHX (N1)
1 #CGA-CLERK       (A3)
1 #CGA-SECURITY-CODE (N2)
*
1 #ISS-DT-OCC     (9)    /* from status file
  2 #ISS-DT         (N6)
  2 #ISS-DT-DISP    (A10)
  2 #SPLIT-IND      (N1)
  2 #STATUS         (A1)
  2 #POL-NUM        (A15)
  2 #POL-BENE-FOUND (L)
  2 #POL-BENE-SEL   (A1)
*
1 #SCR-PRIM-SEC   (2)    /* (prim/alt)
  2 #SCR-DESIG    (A60/5)
  2 REDEFINE #SCR-DESIG
    3 #SCR-DESIG-OCC   (5)
      4 #SCR-DESIG-2   (A60)
      4 REDEFINE #SCR-DESIG-2
        5 #SCR-BN-NAME     (A25)
        5 #SCR-BN-SSN      (N9)
        5 REDEFINE #SCR-BN-SSN
          6 #SCR-BN-SSN-A    (A9)    /* DW1
        5 #SCR-BN-REL      (A1)
        5 #SCR-BN-SHARE    (A10)
        5 #SCR-BN-SET-OPT  (A1)
        5 #SCR-BN-RESTRICT (A1)
        5 #SCR-BN-DOB      (A8)
        5 #SCR-BN-FILL     (A5)
  2 #SCR-TYPE     (A1)
  2 #SCR-SHARE-TYP (A1)
*
1 #DES-SEL         (I2)
1 #SCR-NAME       (A25)
1 #SCR-OWNER      (A35)
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-1A  (A1)
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-6A  (A6)
  2 REDEFINE #SCR-OWNER-6A
    3 #SCR-OWNER-6N (N6)
  2 #SCR-OWNER-AST (A1) /* TO SHOW OWNER IS IN DESIG
1 REDEFINE #SCR-OWNER
  2 #SCR-OWNER-OCC (5)
    3 #SCR-OWNER-6A-XA (A6)
    3 REDEFINE #SCR-OWNER-6A-XA
      4 #SCR-OWNER-6A-XN (N6)
    3 #SCR-OWNER-SEP    (A1)
*
1 #SCR-OWN-TRNSF-DT-DISP (A10)
1 #DESIG-CNT      (P2)
1 #LST-UPD        (N8)
1 REDEFINE #LST-UPD
  2 #LST-UPD-YYYY  (N4)
  2 #LST-UPD-MM    (N2)
  2 #LST-UPD-DD    (N2)
1 #SCR-BENE-RESTRICT (A1)
1 #SCR-BENE-STATUS   (A12)
1 #TEXT              (A75)
1 #CGA-MF-SCR-COMMAND (A2)
1 REDEFINE #CGA-MF-SCR-COMMAND
  2 #TRNSF-IND    (N1)
  2 #SUCC-FORM    (N1)
1 #ZIP-CODE-PASSED  (N9)
1 #INTRO-FLAG-PASSED  (A1)
1 #LINKED-CN     (N6)
1 REDEFINE #LINKED-CN
  2 #LINKED-CN-A   (A6)
1 #SD-SUFF        (A15)
1 #CONT-ENT   (A25/5)
1 #EDS-DATA-VAL   (A60)
1 #NEW-LAYOUT     (L)
1 #SCR-COM-DIS    (N2)
1 #SCR-PER-STR    (A1)
1 #POLICY-PRINT   (L) /* YK1
* View below was moved from LOCAL to PARAMETERS
1 NT-V                /* YK1 VIEW OF A-NOTES   /* rec 800117 used to send info to ATHPANNP
  2 ID-NUMBER  (N6)
  2 NOTE-DATE  (N8/60)
  2 NOTE-TEXT  (A60/60)
  2 REDEFINE NOTE-TEXT /* below layout must match ATHPANNS
    3 NT-CN     (N6)
    3 NT-ISS-DT (N8)
    3 NT-PLAN   (A3)
    3 NT-TYPE   (A1)     /* bene-type of primary bene
    3 NT-NEW-LAYOUT (A1) /* new layout indicator of primar
    3 NT-NUM-LINES (N3)  /* # of lines entered below
   3 NT-BENE-SHO  (A79/40)   /* 79 to handle raw bene indicator in pos 1
*
1 #NEW-BENE-PROCESSING  (L)                    /* DW99
*
LOCAL USING G3500AAD
LOCAL
*
1 #CGA-MASTER
  2 #CGA-SCR-COMMAND-X              (A2)
  2 #CGA-SCR-ID-X                   (N9)
  2 #CGA-SCR-SUFFIX-X               (A3)
  2 #CGA-ORIG-COMMAND-X             (A2)
  2 #CGA-ORIG-ID-X                  (N6)
  2 #CGA-ORIG-SUFFIX-X              (A3)
  2 #CGA-CLERK-X                    (A3)
  2 #CGA-SECURITY-CODE-X            (N2)
  2 #CGA-USER-COMPANY               (A1)
  2 #CGA-ID-G                       (A6)
  2 #CGA-ISN                        (P8)
  2 #CGA-GOOD-TAS                   (L)
  2 #CGA-GOOD-TAS-VS                (L)
  2 #CGA-MF-SCR-COMMAND-X           (A2)
  2 #CGA-MF-SCR-PARM-1              (A9)
  2 #CGA-MF-SCR-PARM-2              (A5)
  2 #CGA-MF-ORIG-COMMAND            (A2)
  2 #CGA-MF-ORIG-PARM-1             (A9)
  2 #CGA-MF-ORIG-PARM-2             (A5)
  2 #POLICY-NUMBER                  (A16)
*
1 ST-V VIEW OF A-STATUS
  2 WIDOW-FLAG
  2 NAME
  2 NUMBER-MEMBERSHIPS
  2 ISSUE-DATE    (9)
  2 PLAN          (9)
*
1 #CN-N9   (N9)
1 REDEFINE #CN-N9
  2 #CN-FILL   (A3)
  2 #CN-A6     (A6)
1 #NUM-BENES   (P3)
1 #UNEQUAL     (L)
1 #LAST-SHARE    (A10)
1 #NUM-ALT-BENES   (P3)
1 #ALT-DONE        (L)
1 #HAS-BENE-LBLS   (L) /* DW9
1 #INVALID  (L)
**1 #REL-DISP  (A15)
1 #Relation  (A) DYNAMIC
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #MI        (A1)
1 #FIRST-MID  (A25)
1 #SUFF1      (A25)
1 #SUFF2      (A25)
1 #SUFF3      (A25)
1 #ERR-MSG      (A60)
*
1 #NEW-FORMAT-DATA (L/57)    /* to indicate raw new format
1 #SCR-LINES-CV   (C/57)
1 #SCR-LINES      (A78/57)
1 REDEFINE #SCR-LINES
  2 #SCR-LINES-57-OCC    (57)
    3 #SCR-LINES-78    (A78)
    3 REDEFINE #SCR-LINES-78
      4 #SCR-LINES-NAME        (A25)
      4 #SCR-LINES-FIL-A       (A1)
      4 #SCR-LINES-SSN         (A11)
      4 #SCR-LINES-FIL-B       (A1)
      4 #SCR-LINES-RELATION    (A13)
      4 #SCR-LINES-FIL-C       (A1)
      4 #SCR-LINES-SHARE       (A10)
      4 #SCR-LINES-FIL-D       (A2)
      4 #SCR-LINES-SETTL       (A1)
      4 #SCR-LINES-FIL-E       (A6)
      4 #SCR-LINES-RESTRICT    (A3)
      4 #SCR-LINES-FIL-F       (A4)
1 REDEFINE #SCR-LINES
  2 #SCR-LINES-OCC     (57)
    3 #SCR-LINES-FIL    (A7)
    3 #SCR-LINES-INDENT (A71)
    3 REDEFINE #SCR-LINES-INDENT
      4 #SCR-LINES-FIL-2       (A39)
      4 #SCR-LINES-SIGNATURE   (A32)
    3 REDEFINE #SCR-LINES-INDENT
      4 #SCR-LINES-FIL-3       (A65)
      4 #SCR-LINES-LST-6       (A6)
1 #SCR-POLS       (A60)
1 #SCR-POLS-2     (A30)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY       (N4)
  2 #MM         (N2)
  2 #DD         (N2)
*
1 #TODAY-MMDDYYYY-DISP  (A10)
*
1 #MONTHS (A36)  INIT
  <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
*
1 #POL        (A14)
1 #CNT        (P2)
1 #SCR-X      (I2)
1 #OWN-X      (I2)
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #I4         (I2)
1 #HOLD-ID     (N9)
1 #HOLD-SUFFIX (A3)
1 #MANUAL      (L)
1 #TRUSTEEZ   (A10)
1 #OWNERZ     (A8)
1 #I  (A2)  /* or we
1 #IC (A2)  /* or We
1 #AM (A3)  /* or are
1 #MY (A3)  /* or our
1 #ME (A3)  /* or us
1 #WP         (L)
1 #SIGNATURE  (A20)
1 REDEFINE #SIGNATURE
  2 #SIGNATURE-FIL (A13)
  2 #SIGNATURE-SIG (A7)
1 #SIG-LINES (P3)
1 #DEF-CMP   (L)
1 #SCR-MBR   (A25)
*
1 #BENE-TEXT   (A60/10)
1 #NEW-LAYOUT-YN      (A1)
1 #FND-SSN            (L)
1 #FND-SSN-NEW-LAYOUT (L)
1 #HIDE-SSN           (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
* WRITE 'top of e4750sbd' #CGA-SCR-ID #CGA-SCR-SUFFIX 'new=' #NEW-LAYOUT
*   #SCR-DESIG (1, 1)
IF   (#CGA-SCR-SUFFIX-2 = 'SH') /* coming from ATHPANNP    DW1
    AND (*DEVICE = 'BATCH')
  IF NOT #POLICY-PRINT               /* YK1
    MOVE TRUE TO #HIDE-SSN
  END-IF
  FOR #I2 1 TO 2                     /* DW1
    FOR #I3 1 TO 5
      IF #I2 = 1
        MOVE #I3 TO #I4
      ELSE
        COMPUTE #I4 = #I3 + 5
      END-IF
*
      MOVE #SCR-DESIG (#I2, #I3) TO #BENE-TEXT (#I4)
    END-FOR
  END-FOR
*
  CALLNAT 'ATHSBDSS' #BENE-TEXT (1:10)     /* DW1
    #NEW-LAYOUT
    #FND-SSN
    #FND-SSN-NEW-LAYOUT
    #HIDE-SSN
*
  IF #FND-SSN OR #FND-SSN-NEW-LAYOUT
    FOR #I2 1 TO 2
      FOR #I3 1 TO 5
        IF #I2 = 1
          MOVE #I3 TO #I4
        ELSE
          COMPUTE #I4 = #I3 + 5
        END-IF
*
        MOVE #BENE-TEXT (#I4) TO #SCR-DESIG (#I2, #I3)
      END-FOR
    END-FOR
  END-IF
END-IF
*
MOVE #CGA-SCR-ID TO #CN-N9
MOVE #CN-A6 TO #CGA-ID-G
MOVE #CGA-SCR-ID TO #CGA-SCR-ID-X
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM #DD #YYYY INTO #TODAY-MMDDYYYY-DISP WITH '/'
*
IF #CGA-SCR-SUFFIX = 'WP'   /* requesting link to word perfect scn prt
  MOVE TRUE TO #WP
  ADD 1 TO #SCR-X
END-IF
*
IF  (#SCR-TYPE (1) NE ' ')   /* bene has been entered
    AND (NOT #WP)
    AND ((*DEVICE NE 'BATCH')
    OR (#CGA-SCR-SUFFIX = 'BND' OR = 'BNA' OR = 'BNC' OR = 'FIR'))
  ADD 1 TO #SCR-X
  IF #SCR-BENE-STATUS NE 'PENDING'
    MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
   COMPRESS '* This is a copy of the Beneficiary Information we have on'
      'file *' INTO #SCR-LINES (#SCR-X)
  END-IF
*
  IF #EDS-DATA-VAL NE ' '
    ADD 1 TO #SCR-X
    MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
    MOVE #EDS-DATA-VAL TO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
IF   (*DEVICE NE 'BATCH') AND (NOT #WP)
    AND ((#SCR-BENE-STATUS = 'PENDING') OR (#SCR-TYPE (1) = ' '))
  IF #SCR-BENE-STATUS = 'PENDING'
    ADD 1 TO #SCR-X
    MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
    MOVE 'This Designation is in a pending status' TO #SCR-LINES(#SCR-X)
  END-IF
*
  IF #SCR-TYPE (1) = ' '
    ADD 1 TO #SCR-X
    MOVE 'This designation not yet converted' TO #SCR-LINES (#SCR-X)
    MOVE (AD=I) TO #SCR-LINES-CV (10)
    MOVE '*** THIS DESIGNATION NOT YET CONVERTED TO EDS SCREEN ***'
      TO #SCR-LINES (10)
  END-IF
END-IF
*
IF  (*DEVICE NE 'BATCH') AND (#SCR-BENE-RESTRICT = 'Y') AND (NOT #WP)
  ADD 1 TO #SCR-X
  MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
  MOVE '***** Changes are restricted *****' TO #SCR-LINES (#SCR-X)
END-IF
*
FOR #I1 1 TO 9
  IF #POL-BENE-SEL (#I1) = 'X'
    ADD 1 TO #CNT
  END-IF
END-FOR
*
RESET #SCR-POLS-2 #I2          /* YK2
FOR #I1 1 TO 9
  IF #POL-BENE-SEL (#I1) = 'X'
    IF (#CGA-SCR-ID = 40486) AND (#ISS-DT (#I1) = 970528)
      MOVE TRUE TO #DEF-CMP   /* jjf 457 def comp policy
    END-IF
*
    ADD 1 TO #I2
*
    IF #I2 NE 1
      IF #I2 GE 6
        IF #CNT = #I2
          COMPRESS #SCR-POLS-2 '&' INTO #SCR-POLS-2
        ELSE
          COMPRESS #SCR-POLS-2 ',' INTO #SCR-POLS-2 LEAVING NO
        END-IF
      ELSE
        IF #CNT = #I2
          COMPRESS #SCR-POLS '&' INTO #SCR-POLS
        ELSE
          COMPRESS #SCR-POLS ',' INTO #SCR-POLS LEAVING NO
        END-IF
      END-IF
    END-IF
*
    MOVE LEFT #POL-NUM (#I1) TO #POL
*
    IF #I2 GE 5
      COMPRESS #SCR-POLS-2 #POL INTO #SCR-POLS-2
    ELSE
      COMPRESS #SCR-POLS #POL INTO #SCR-POLS
    END-IF
  END-IF
END-FOR
*
IF   (#SCR-BENE-STATUS NE 'PENDING')
    AND (#SCR-TYPE (1) NE ' ')   /* bene has been entered
*  AND  (((#CGA-SCR-SUFFIX NE 'BNB') AND (*DEVICE NE 'BATCH'))
*  OR  (#CGA-SCR-SUFFIX =  'BNF') OR #WP)
  ADD 1 TO #SCR-X
  IF #LST-UPD-MM GT 0 AND #LST-UPD-MM LE 12
    COMPRESS 'Date last update:' #LST-UPD-DD #MON(#LST-UPD-MM)
      #LST-UPD-YYYY        INTO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
ADD 1 TO #SCR-X
COMPRESS 'Insured:' #SCR-NAME INTO #SCR-LINES (#SCR-X)
MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
*
RESET #SCR-MBR
IF #INTRO-FLAG-PASSED = 'S' OR = 'K' OR ='G'
  FIND (1) ST-V WITH MEMBER-KEY = #LINKED-CN-A
    IF NO RECORDS FOUND
      FIND (1) ST-V WITH WIDOW-KEY = #LINKED-CN-A
        IF NO RECORDS FOUND
          MOVE 'Member has been purged' TO #SCR-MBR
        END-NOREC
      END-FIND
    END-NOREC
  END-FIND
*
  IF #SCR-MBR = ' '
    IF WIDOW-FLAG = 'Y'
      MOVE 'Deceased' TO #SCR-MBR
    ELSE
      IF NUMBER-MEMBERSHIPS = 0
        MOVE 'Inactive' TO #SCR-MBR
      ELSE
        SEPARATE ST-V.NAME LEFT INTO #LAST #FIRST-MID
          #SUFF1 #SUFF2 #SUFF3      WITH DELIMITER ','
        COMPRESS #FIRST-MID #LAST #SUFF1 #SUFF2 #SUFF3 INTO #SCR-MBR
      END-IF
    END-IF
  END-IF
END-IF
*
RESET #OWN-X
IF #SCR-OWNER = ' '
  IF #INTRO-FLAG-PASSED = 'S' OR = 'K' OR ='G'
    ADD 1 TO #SCR-X
*   COMPRESS 'Owner:' #SCR-MBR INTO #SCR-LINES (#SCR-X)
    COMPRESS 'Owner:' #SCR-NAME INTO #SCR-LINES (#SCR-X)
    COMPRESS #SCR-LINES (#SCR-X) #SD-SUFF
      INTO #SCR-LINES (#SCR-X) LEAVING NO
  END-IF
ELSE
  ADD 1 TO #SCR-X
  IF  ((*DEVICE = 'BATCH') OR #WP)
      AND (#CGA-SCR-SUFFIX-2 NE 'SH')
    ADD 1 TO #SCR-X
  END-IF
*
  MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
  IF  (#SCR-OWNER  = '*')
      OR (#SCR-OWNER-AST = '*')
    IF   (#SCR-TYPE (1) = 'K' OR = 'P')
        AND (#SCR-DESIG (1, 3) NE ' ')
      COMPRESS 'Owner:' #SCR-DESIG  (1, 3) INTO #SCR-LINES (#SCR-X)
      MOVE #SCR-X TO #OWN-X   /* SAVE INDEX OF OWNER LINE
      IF #SCR-DESIG (1, 4) NE ' '
        ADD 1 TO #SCR-X
        MOVE #SCR-DESIG (1, 4) TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
    ELSE
      COMPRESS 'Owner:' #SCR-DESIG  (1, 1) INTO #SCR-LINES (#SCR-X)
      MOVE #SCR-X TO #OWN-X   /* SAVE INDEX OF OWNER LINE
      IF #SCR-DESIG (1, 2) NE ' '
        ADD 1 TO #SCR-X
        MOVE #SCR-DESIG (1, 2) TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
    END-IF
  ELSE
    IF #SCR-OWNER = 'X'  /* never after 7/25/2002
      COMPRESS 'Owner:' #SCR-NAME INTO #SCR-LINES (#SCR-X)
      COMPRESS #SCR-LINES (#SCR-X) #SD-SUFF
        INTO #SCR-LINES (#SCR-X) LEAVING NO
    ELSE
      IF #SCR-OWNER-6A = MASK (999999)
        COMPRESS 'Owner:' #CONT-ENT (1) INTO #SCR-LINES (#SCR-X)
        MOVE #SCR-OWNER-6A TO #SCR-LINES-LST-6 (#SCR-X)
*
        FOR #I4 2 TO 5
          IF #SCR-OWNER-6A-XA (#I4) = MASK (999999)
            ADD 1 TO #SCR-X
            COMPRESS 'Owner:' #CONT-ENT (#I4) INTO #SCR-LINES (#SCR-X)
            MOVE #SCR-OWNER-6A-XN (#I4) TO #SCR-LINES-LST-6 (#SCR-X)
          END-IF
        END-FOR
      ELSE
        COMPRESS 'Owner:' #SCR-OWNER INTO #SCR-LINES (#SCR-X)
      END-IF
    END-IF
  END-IF
*
  IF #SCR-OWN-TRNSF-DT-DISP NE ' '
    ADD 1 TO #SCR-X
    COMPRESS  '  - As of' #SCR-OWN-TRNSF-DT-DISP
      INTO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
IF #INTRO-FLAG-PASSED = 'S' OR = 'K' OR ='G'
  ADD 1 TO #SCR-X
  IF  ((*DEVICE = 'BATCH') OR #WP)
      AND (#CGA-SCR-SUFFIX-2 NE 'SH')
    ADD 1 TO #SCR-X
  END-IF
  COMPRESS 'Linked Member:' #SCR-MBR INTO #SCR-LINES (#SCR-X)
END-IF
*
IF   ((*DEVICE = 'BATCH') OR #WP)
    AND (#CGA-SCR-SUFFIX-2 NE 'SH')
  ADD 1 TO #SCR-X  /* was 2
  MOVE     'TO SECRETARY, AAFMAA:'
    TO #SCR-LINES (#SCR-X)
  ADD 1 TO #SCR-X
END-IF
*
MOVE 'I'  TO #I
MOVE 'I'  TO #IC
MOVE 'am' TO #AM
MOVE 'my' TO #MY
MOVE 'me' TO #ME
*
IF   ((*DEVICE = 'BATCH') OR #WP)
    AND (#CGA-SCR-SUFFIX-2 NE 'SH')
  ADD 1 TO #SCR-X
  IF #SCR-OWNER = ' '
    COMPRESS 'I, a member of the AAFMAA'
      ', holding' INTO #SCR-LINES (#SCR-X) LEAVING NO
  ELSE
    IF   (#SCR-OWNER-AST NE '*')  /* assumes entered owner is single
        AND (#SCR-OWNER NE '*')
      MOVE 'I,' TO #SCR-LINES (#SCR-X)
      MOVE ', owner' TO #TRUSTEEZ
      MOVE ', owner' TO #OWNERZ
    ELSE
      IF  (#SCR-SHARE-TYP (1) = 'S')    /* single
        MOVE 'I,' TO #SCR-LINES (#SCR-X)
        MOVE ', trustee' TO #TRUSTEEZ
        MOVE ', owner' TO #OWNERZ
      ELSE
        MOVE 'We,' TO #SCR-LINES (#SCR-X)
        MOVE ', trustees' TO #TRUSTEEZ
        MOVE ', owners' TO #OWNERZ
        MOVE 'we'  TO #I
        MOVE 'We'  TO #IC
        MOVE 'are' TO #AM
        MOVE 'our' TO #MY
        MOVE 'us'  TO #ME
      END-IF
    END-IF
*
    IF  (#SCR-OWNER-AST = '*')
        OR (#SCR-OWNER = '*')
      COMPRESS #SCR-LINES (#SCR-X) #SCR-DESIG  (1,1)
        INTO #SCR-LINES (#SCR-X)
      IF #SCR-DESIG (1, 2) NE ' '
        ADD 1 TO #SCR-X
        MOVE #SCR-DESIG (1, 2) TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
    ELSE
      IF #SCR-OWNER = 'X'
        COMPRESS #SCR-LINES (#SCR-X) #SCR-NAME INTO #SCR-LINES (#SCR-X)
      ELSE
        IF #SCR-OWNER-6A = MASK (999999)
          COMPRESS #SCR-LINES(#SCR-X) #CONT-ENT (1)
            INTO #SCR-LINES(#SCR-X)
        ELSE
          COMPRESS #SCR-LINES(#SCR-X) #SCR-OWNER INTO #SCR-LINES(#SCR-X)
        END-IF
      END-IF
    END-IF
*
    IF (#SCR-TYPE (1) = 'K' OR = 'P' OR = 'X')
        AND ((#SCR-OWNER-AST = '*')
        OR  (#SCR-OWNER = '*'))
      COMPRESS #SCR-LINES (#SCR-X) #TRUSTEEZ
        INTO #SCR-LINES(#SCR-X) LEAVING NO
*
      ADD 1 TO #SCR-X
      COMPRESS 'of the' #SCR-DESIG (1,3) INTO #SCR-LINES (#SCR-X)
*
      IF #SCR-DESIG (1, 4) NE ' '
        ADD 1 TO #SCR-X
        MOVE #SCR-DESIG (1,4) TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
*
      IF #SCR-DESIG (1, 5) NE ' '
        ADD 1 TO #SCR-X
        MOVE #SCR-DESIG (1,5) TO #SCR-LINES-INDENT (#SCR-X)
      END-IF
    END-IF
*
    COMPRESS #SCR-LINES (#SCR-X) #OWNERZ
      INTO #SCR-LINES (#SCR-X) LEAVING NO
    ADD 1 TO #SCR-X
    MOVE 'of the life insurance benefits arising under'
      TO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
ADD 1 TO #SCR-X
MOVE     'Certificate' TO #SCR-LINES-INDENT (#SCR-X)
*
IF #CNT GT 1
  COMPRESS #SCR-LINES (#SCR-X) '(s)' INTO #SCR-LINES (#SCR-X) LEAVING NO
END-IF
*
COMPRESS #SCR-LINES (#SCR-X) 'No.' #SCR-POLS INTO #SCR-LINES (#SCR-X)
*
IF #CNT GE 5
  ADD 1 TO #SCR-X
  MOVE #SCR-POLS-2 TO #SCR-LINES-INDENT (#SCR-X)
END-IF
*
IF #INTRO-FLAG-PASSED = 'S' OR = 'K' OR = 'G'
  IF #CNT GT 1
    ADD 1 TO #SCR-X
  END-IF
*
  COMPRESS #SCR-LINES (#SCR-X) 'on the life of' #SCR-NAME
    INTO #SCR-LINES (#SCR-X)
END-IF
*
IF   ((*DEVICE = 'BATCH') OR #WP)
    AND (#CGA-SCR-SUFFIX-2 NE 'SH')
  ADD 1 TO #SCR-X
  COMPRESS 'certify that' #I #AM 'under no legal obligation to'
    'continue' #MY 'present desig-' INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  COMPRESS 'nation of beneficiary to receive the AAFMAA'
    ' ' INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  COMPRESS 'insurance.'#IC 'hereby cancel all designations of'
    'beneficiaries heretofore made'    INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  COMPRESS 'by' #ME 'and direct that the benefit that will become'
    'payable by said Association'        INTO #SCR-LINES (#SCR-X)
*
  IF #SCR-OWNER = ' '
    ADD 1 TO #SCR-X
    COMPRESS 'in the event of the Insured"s death, shall'
      'be paid as shown below.' INTO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
FOR #I1 1 TO 2
  IF #CGA-SCR-SUFFIX = 'BNB'
    ADD 3 TO #SCR-X
    IF #I1 = 1
      MOVE 'PRINCIPAL(S):' TO #SCR-LINES (#SCR-X)
    ELSE
      MOVE 'ALTERNATE(S):' TO #SCR-LINES (#SCR-X)
    END-IF
*
    COMPRESS #SCR-LINES (#SCR-X) '___________________________________'
      '__________________________' INTO #SCR-LINES (#SCR-X) LEAVING NO
*
    ADD 1 TO #SCR-X
    MOVE '           (Name and relationship of named beneficiary(ies))'
      TO #SCR-LINES (#SCR-X)
    ADD 3 TO #SCR-X
    COMPRESS '_______________________________________________________'
      '___________________' INTO #SCR-LINES (#SCR-X) LEAVING NO
    ADD 1 TO #SCR-X
*
    IF #I1 = 1
      COMPRESS 'if said beneficiary(ies) shall survive the insured.'
        INTO #SCR-LINES (#SCR-X)
    ELSE
      COMPRESS 'if said alternate beneficiary(ies) shall survive the'
        'insured.' INTO #SCR-LINES (#SCR-X)
      ADD 2 TO #SCR-X
      COMPRESS '  If a married woman, please state her own first name'
        'and surname above.' INTO #SCR-LINES (#SCR-X)
      ADD 2 TO #SCR-X
      COMPRESS '  If multiple beneficiaries are named, the benefit is'
        'to be paid in equal' INTO #SCR-LINES (#SCR-X)
      ADD 1 TO #SCR-X
      COMPRESS 'shares to the beneficiary(ies) who survive the'
        'insured.'      INTO #SCR-LINES (#SCR-X)
      ADD 2 TO #SCR-X
      COMPRESS '  If the beneficiary designation is cancelled and no'
        'designated beneficiary' INTO #SCR-LINES (#SCR-X)
      ADD 1 TO #SCR-X
      COMPRESS 'lives to receive the benefit, the benefit is to be'
        'paid to the Owner' INTO #SCR-LINES (#SCR-X)   /* DW5
      ADD 1 TO #SCR-X
      MOVE     'or the Owner"s ESTATE.' TO #SCR-LINES (#SCR-X)   /* DW5
    END-IF
*
    ESCAPE TOP
  END-IF
*
  IF #SCR-TYPE (#I1) = ' '
    ESCAPE TOP
  END-IF
*
  IF #SCR-TYPE (#I1) = 'M'
    MOVE TRUE TO #MANUAL
  END-IF
*
  IF #I1 = 1
    ADD 1 TO #SCR-X
*
    COMPRESS 'Upon the death of the named insured, I direct'
      'that the benefit be paid to'       TO #SCR-LINES (#SCR-X)
*
    IF #SCR-TYPE (1) = 'F'
      ADD 1 TO #SCR-X
  COMPRESS #SCR-LINES (#SCR-X) 'the Owner or the Owner"s ESTATE.' /* DW5
        INTO #SCR-LINES (#SCR-X)
    ELSE
      COMPRESS #SCR-LINES (#SCR-X) ':' TO #SCR-LINES (#SCR-X) LEAVING NO
    END-IF
  ELSE
    IF #SCR-TYPE (#I1) NE 'C' /* c = continuation
      ADD 1 TO #SCR-X
      IF   ((*DEVICE = 'BATCH') OR #WP)
          AND (#CGA-SCR-SUFFIX-2 NE 'SH')
        ADD 1 TO #SCR-X
      END-IF
*
      MOVE 'Otherwise as alternate beneficiary(ies), I hereby name:'
        TO #SCR-LINES (#SCR-X)
    END-IF
  END-IF
*
  IF #SCR-TYPE (#I1) = 'T'
    ADD 1 TO #SCR-X
    COMPRESS '     the TRUSTEE of the Trust established unde'
      'r terms of my Last'
      INTO #SCR-LINES (#SCR-X) LEAVING NO
    ADD 1 TO #SCR-X
    COMPRESS '     Will and Testament duly admitted to probate'
      'and recorded as such' INTO #SCR-LINES (#SCR-X)
  ELSE
    FOR #I2 1 TO 2
      IF    (#SCR-DESIG (#I1, #I2) NE ' ')
          OR ((#SCR-TYPE (#I1) = 'A') AND (#I1 = 2) AND #NEW-LAYOUT)
        ADD 1 TO #SCR-X
*
        IF   #NEW-LAYOUT
          AND (NOT #SCR-TYPE (#I1) = 'I' OR = 'P' OR = 'C') /* free form
          IF ((#I1 = 1) AND (#I2 = 1))
              OR ((#I1 = 2) AND (#I2 = 1) AND NOT #HAS-BENE-LBLS) /* DW2
*           COMPRESS '    NAME                     SSN       RELATION'
            COMPRESS 'NAME                         SSN       RELATION'
             'SHIP SHARE     SETL RESTRICTED' /* DW3 above chg 4 ann stm
              INTO #SCR-LINES (#SCR-X) LEAVING NO
            MOVE TRUE TO #HAS-BENE-LBLS  /* DW2
            ADD 1 TO #SCR-X
          END-IF
          PERFORM NEW-LAYOUT
        ELSE
          MOVE #SCR-DESIG (#I1, #I2) TO #SCR-LINES-INDENT (#SCR-X)
        END-IF
        MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
      END-IF
    END-FOR
*
    IF #SCR-TYPE (#I1) =  'K'
      ADD 1 TO #SCR-X
      MOVE 'or it"s successor(s), as trustee(s) of the:'
        TO #SCR-LINES (#SCR-X)
    END-IF
*
    IF #SCR-TYPE (#I1) =  'P' OR = 'X'
      ADD 1 TO #SCR-X
      MOVE 'or successor(s) as trustee(s) of:' TO #SCR-LINES (#SCR-X)
    END-IF
*
    FOR #I2 3 TO 5
      IF  (#SCR-DESIG (#I1, #I2) NE ' ')
          OR ((#SCR-TYPE (#I1) = 'A') AND (#I1 = 2) AND #NEW-LAYOUT)
        ADD 1 TO #SCR-X
        IF    #NEW-LAYOUT
          AND (NOT #SCR-TYPE (#I1) = 'I' OR = 'P' OR = 'C') /* free form
          PERFORM NEW-LAYOUT
        ELSE
          MOVE #SCR-DESIG (#I1, #I2) TO #SCR-LINES-INDENT (#SCR-X)
        END-IF
*
        MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
      END-IF
    END-FOR
  END-IF
*
  RESET #UNEQUAL #LAST-SHARE #NUM-BENES
  IF #NEW-LAYOUT
    IF (#SCR-TYPE (#I1) = 'I' OR = 'C')
        AND((#I1 = 2) AND (#SCR-PER-STR = 'Y'))
      MOVE 'S' TO #SCR-TYPE (#I1)
    END-IF
*
    IF (#SCR-TYPE (#I1) = 'B')
       AND((#I1 = 1) AND (#SCR-PER-STR = 'Y') AND (#SCR-TYPE (2) = ' '))
      MOVE 'S' TO #SCR-TYPE (#I1)
    END-IF
*
    IF (#SCR-TYPE (#I1) = 'B' OR = 'S')
      FOR #I2 1 TO 5
        IF #SCR-BN-NAME (#I1, #I2) = ' '
          ESCAPE BOTTOM
        END-IF
*
        ADD 1 TO #NUM-BENES
        IF   (#I2 NE 1)
            AND (#SCR-BN-SHARE (#I1, #I2) NE #LAST-SHARE)
          MOVE TRUE TO #UNEQUAL
        END-IF
*
        MOVE #SCR-BN-SHARE (#I1, #I2) TO #LAST-SHARE
      END-FOR
*
*     IF #NUM-BENES = 1               /* Removed 8/21/2008 as per LCE DW6
*        write 'make type "S" #num-benes =' #num-benes
*        MOVE 'S' TO #SCR-SHARE-TYP (#I1)
*      ELSE
*        IF #NUM-BENES = 2
*          MOVE 'D' TO #SCR-SHARE-TYP (#I1)
*        ELSE
*          IF #NUM-BENES GT 2
*            MOVE 'M' TO #SCR-SHARE-TYP (#I1)
*          END-IF
*        END-IF
*      END-IF
    END-IF
  END-IF
*
  IF (#SCR-TYPE (#I1) =  'B' OR = 'I' OR = 'C' OR = 'A')
      AND NOT #UNEQUAL
      AND ((#SCR-PER-STR NE 'Y') OR (#I1 = 1))
    IF #SCR-SHARE-TYP (#I1)  = 'S'
      ADD 1 TO #SCR-X
      COMPRESS 'if said beneficiary shall survive the insured.'
        INTO #SCR-LINES (#SCR-X)
* Below removed 1/15/2009 replaced later in DW8
*     if   (#SCR-OWNER ne ' ') /* Will be done later if non ins owner /* DW5
*       and (#i1 = 2)                                          /* DW99
*        ADD 1 TO #SCR-X
*        COMPRESS 'If no designated beneficiary lives to receive the benefit,'
*                'I direct that'     INTO #SCR-LINES (#SCR-X)
*        ADD 1 TO #SCR-X
*        move 'the benefit be paid to the Owner or the Owner"s ESTATE.' /* DW5
*                     TO #SCR-LINES (#SCR-X)
*      end-if
    ELSE
      IF #SCR-SHARE-TYP (#I1)  = 'D'
        ADD 1 TO #SCR-X
        MOVE 'to share equally or all to the survivor.'
          TO #SCR-LINES (#SCR-X)
      ELSE
        IF #SCR-SHARE-TYP (#I1)  = 'M'
          ADD 1 TO #SCR-X
          COMPRESS 'to share equally or all to the survivors or surviv'
            'or.'                 INTO #SCR-LINES (#SCR-X) LEAVING NO
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF (#SCR-TYPE (#I1) =  'S')
      OR ((#I1 = 2) AND (#SCR-PER-STR = 'Y'))
    IF   (#SCR-SHARE-TYP (#I1)  = 'S')
     OR ((#I1 = 2) AND (#SCR-PER-STR = 'Y') AND (#SCR-TYPE (#I1) = 'A'))
      IF #NEW-LAYOUT
        ADD 1 TO #SCR-X
      END-IF
      COMPRESS #SCR-LINES (#SCR-X) 'per stirpes.' TO #SCR-LINES (#SCR-X)
    ELSE
      IF #SCR-SHARE-TYP (#I1)  = 'D'        /* DW7
        ADD 1 TO #SCR-X
        COMPRESS 'to share equally or to their issue (born or adopte'
          'd), then living, per' INTO #SCR-LINES (#SCR-X) LEAVING NO
        ADD 1 TO #SCR-X
COMPRESS 'stirpes. If either of the above named beneficiaries predecease'
          'me or die' INTO #SCR-LINES (#SCR-X)
        ADD 1 TO #SCR-X
COMPRESS 'before payment is made without leaving issue, their share of the'
          'benefit'  INTO #SCR-LINES (#SCR-X)
        ADD 1 TO #SCR-X
COMPRESS 'shall be paid to the named beneficiary surviving, then living,'
          'per stirpes.'      INTO #SCR-LINES (#SCR-X)   /* END DW7
      ELSE
        IF #SCR-SHARE-TYP (#I1)  = 'M'
          ADD 1 TO #SCR-X
          COMPRESS 'to share equally or to their issue (born or adopte'
            'd), then living, per' INTO #SCR-LINES (#SCR-X) LEAVING NO
          ADD 1 TO #SCR-X
          COMPRESS 'stirpes. If any of the above named beneficiaries p'
         'redecease me or die before' INTO #SCR-LINES(#SCR-X) LEAVING NO
          ADD 1 TO #SCR-X
          COMPRESS 'payment is made without leaving issue, their share'
            'of the benefit shall be'  INTO #SCR-LINES (#SCR-X)
          ADD 1 TO #SCR-X
          COMPRESS 'paid equally to those beneficiaries surviving or'
            'to their issue, then living,'    INTO #SCR-LINES (#SCR-X)
          ADD 1 TO #SCR-X
          MOVE 'per stirpes.' TO #SCR-LINES (#SCR-X)
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF ((#SCR-TYPE (#I1) =  'U') OR #UNEQUAL)
      AND ((#SCR-PER-STR NE 'Y') OR NOT #NEW-LAYOUT)
    ADD 1 TO #SCR-X
    COMPRESS 'If any of the above named beneficiaries predecease'
      ' me or die before payment' INTO #SCR-LINES (#SCR-X) LEAVING NO
    ADD 1 TO #SCR-X
    COMPRESS 'is made, their share of the benefit shall be paid '
      ' equally to those bene-' INTO #SCR-LINES (#SCR-X) LEAVING NO
    ADD 1 TO #SCR-X
    MOVE     'ficiaries surviving.' TO #SCR-LINES (#SCR-X)
  END-IF
*
  IF #SCR-TYPE (#I1) = 'K' OR = 'P' OR = 'T' OR = 'X'
    COMPRESS #SCR-LINES (#SCR-X) '.' INTO #SCR-LINES(#SCR-X) LEAVING NO
  END-IF
END-FOR
*
IF  ((#SCR-TYPE (1) = 'K' OR = 'P' OR = 'T')  /* DO NOT INCLUDE TYPE "X"
    AND (#SCR-TYPE (2) = 'K' OR = 'P' OR = 'T' OR =' '))   /* DW4
    OR (#SCR-TYPE (2) = 'K' OR = 'P' OR = 'T')
  ADD 1 TO #SCR-X
*
  COMPRESS 'In the event the aforementioned Trust is not in'
    'effect at the time of death' INTO #SCR-LINES (#SCR-X)   /* vt1
  ADD 1 TO #SCR-X
  COMPRESS 'and no alternate or subsequent designation shall'
    'have been made by me,'       INTO #SCR-LINES (#SCR-X)    /* vt1
  ADD 1 TO #SCR-X
COMPRESS 'I direct that the benefit be paid to the Owner or The Owner"s ESTATE.'  /* vt1
    INTO #SCR-LINES (#SCR-X)
*   ADD 1 TO #SCR-X                                            /* vt1
*   COMPRESS 'Owner"s ESTATE.'                                 /* vt1
*                                      TO #SCR-LINES (#SCR-X)  /* vt1
ELSE
  IF   (NOT #SCR-TYPE (1) = 'F' OR = ' ')
*  AND (NOT #INTRO-FLAG-PASSED = 'S' OR = 'K' OR = 'G') /* 4/2003 khb
*  AND (#SCR-OWNER = ' ')       /* Removed 1/15/2009    DW8
      AND (NOT #DEF-CMP)   /* JJF 457 DEF-COMP POLICY
    ADD 1 TO #SCR-X
*
    COMPRESS 'If no designated beneficiary lives to receive the'
      'benefit, I direct that' INTO #SCR-LINES (#SCR-X)
    ADD 1 TO #SCR-X
  MOVE 'the benefit be paid to the Owner or the Owner"s ESTATE.'  /* DW5
      TO #SCR-LINES (#SCR-X)
  END-IF
END-IF
*
IF #SCR-COM-DIS NE 0
  ADD 1 TO #SCR-X
  COMPRESS 'Common Disaster' #SCR-COM-DIS 'days'
    INTO #SCR-LINES (#SCR-X)
END-IF
*
IF ((*DEVICE = 'BATCH') OR #WP)
    AND (NOT #CGA-SCR-SUFFIX = 'BND' OR = 'BNA' OR = 'BNC' OR = 'FIR')
    AND (#CGA-SCR-SUFFIX-2 NE 'SH')
  MOVE 'Signature of Owner' TO #SIGNATURE
*
  IF #SCR-OWNER NE ' '
    IF #SCR-TYPE (1) = 'P' OR = 'K' OR = 'X'
      MOVE 'Trustee' TO #SIGNATURE-SIG
    ELSE
      MOVE 'Owner' TO #SIGNATURE-SIG
    END-IF
  END-IF
*
  ADD 1 TO #SCR-X
*
  MOVE 1 TO #SIG-LINES
  IF (#SCR-OWNER NE ' ') AND (#SCR-TYPE (1) = 'P' OR = 'B' OR = 'X')
    IF #SCR-SHARE-TYP (1) = 'D'
      MOVE 2 TO #SIG-LINES
    ELSE
      IF #SCR-SHARE-TYP (1) = 'M'
        MOVE 4 TO #SIG-LINES
      END-IF
    END-IF
  END-IF
*
  FOR #I1 1 TO #SIG-LINES
    IF #SCR-SHARE-TYP (1) = 'M'
      ADD 1 TO #SCR-X
    ELSE
      ADD 2 TO #SCR-X
    END-IF
*
    COMPRESS ' Date ______________ __________________________________'
      '_______________' INTO #SCR-LINES (#SCR-X) LEAVING NO
*
    IF #SCR-SHARE-TYP (1) = 'M'
      ADD 1 TO #SCR-X
    ELSE
      ADD 2 TO #SCR-X
    END-IF
*
    MOVE #SIGNATURE  TO #SCR-LINES-SIGNATURE (#SCR-X)
  END-FOR
*
  ADD 2 TO #SCR-X
  MOVE 'Approved by authority of the Board of Directors'
    TO #SCR-LINES (#SCR-X)
*
  ADD 2 TO #SCR-X
  COMPRESS '                       Secretary _____________________'
    '_________________' INTO #SCR-LINES (#SCR-X) LEAVING NO
END-IF
*
IF ((*DEVICE = 'BATCH') OR #WP)
    AND (#CGA-SCR-SUFFIX = 'BND' OR = 'FIR')
    AND (#CGA-SCR-SUFFIX-2 NE 'SH')
  ADD 2 TO #SCR-X
  COMPRESS '  In the event you wish to make a change in this'
    'designation, please call' INTO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  COMPRESS 'Lin Blakley using our toll free number. A new form will'
    'be prepared and sent' TO #SCR-LINES (#SCR-X)
*
  ADD 1 TO #SCR-X
  MOVE     'to you for your signature.' TO #SCR-LINES (#SCR-X)
END-IF
*
IF   (*DEVICE NE 'BATCH') AND NOT #WP
    AND (#SCR-BENE-STATUS = 'PENDING')
  ADD 2 TO #SCR-X
  MOVE (AD=I) TO #SCR-LINES-CV (#SCR-X)
  MOVE     '***  THIS IS NOT AN ENDORSED DESIGNATION - PENDING'-
    ' OWNER"S SIGNATURE ***'    TO #SCR-LINES (#SCR-X) #TEXT
END-IF
*
IF #MANUAL
  COMPRESS 'Designations with type "M" (manual) are incomplete'
    '- see original' INTO #TEXT
END-IF
*
* IF *DEVICE = 'BATCH' OR #POLICY-PRINT
IF *DEVICE = 'BATCH' OR #NEW-BENE-PROCESSING           /* DW9

  IF #CGA-SCR-SUFFIX = MASK ('SH')   /* coming from ATHPANNP
    FIND (1) ST-V WITH MEMBER-KEY = #CGA-ID-G
      IF NO RECORDS FOUND
        WRITE #CGA-ID-G 'SH record not found'
      END-NOREC
    END-FIND
*
* YK1 changes starts
*
*
*    FIND NT-V WITH ID-NUMBER = 800117
    MOVE *DATN             TO NOTE-DATE(1)
    MOVE #CGA-SCR-ID       TO NT-CN
    MOVE ST-V.ISSUE-DATE (#CGA-SCR-SUFFIX-SHX)  TO NT-ISS-DT
    MOVE ST-V.PLAN (#CGA-SCR-SUFFIX-SHX)        TO NT-PLAN
    MOVE #SCR-TYPE (1) TO NT-TYPE
    IF   #NEW-LAYOUT
        AND (NOT #SCR-TYPE (1) = 'I' OR = 'P' OR = 'C') /* free form
      MOVE 'Y' TO NT-NEW-LAYOUT
    ELSE
      RESET NT-NEW-LAYOUT
    END-IF
*
    RESET #I2
    FOR #I1 1 TO 40
      ADD 1 TO #I2
      MOVE #SCR-LINES (#I1) TO NT-BENE-SHO (#I2)
*
      IF #NEW-FORMAT-DATA (#I1) = TRUE
        COMPRESS '~' NT-BENE-SHO(#I2) INTO NT-BENE-SHO(#I2) LEAVING NO
      END-IF
    END-FOR
*
    IF #I1 GT 40
      MOVE 40 TO NT-NUM-LINES
    ELSE
      MOVE #I1 TO NT-NUM-LINES
    END-IF
*
*      UPDATE (1058)
*      END TRANSACTION
*    END-FIND
*
    ESCAPE ROUTINE
  END-IF
*
  MOVE #CGA-SCR-ID     TO #HOLD-ID
  MOVE #CGA-SCR-SUFFIX TO #HOLD-SUFFIX
  RESET #CGA-SCR-ID
*
  IF #CGA-SCR-SUFFIX = 'FIR'  /* bnd with fir/fin
    MOVE 'SNG' TO #CGA-SCR-SUFFIX
    NEWPAGE
    WRITE NOTITLE USING FORM 'E4751FBD'  /* letterhead with firs
    ADD 1 TO #CGA-SECURITY-CODE
  ELSE
    IF #CGA-SCR-SUFFIX = 'BND' OR = 'BNA' OR = 'BNC'
      RESET #CGA-SCR-SUFFIX
      IF #ZIP-CODE-PASSED = 0
        NEWPAGE (6)
        WRITE (6) NOTITLE USING FORM 'E4751FBD' /* letterhead
      ELSE
        NEWPAGE (4)
        WRITE (4) NOTITLE USING FORM 'E4751FBD' /* letterhead
      END-IF
    ELSE
      RESET #CGA-SCR-SUFFIX
      IF #ZIP-CODE-PASSED = 0
        NEWPAGE (6)    /* was 7
        WRITE (6) NOTITLE USING FORM 'E4750FBD' /* plain white
      ELSE
        NEWPAGE (4)    /* was 5
        WRITE (4) NOTITLE USING FORM 'E4750FBD' /* plain white
      END-IF
    END-IF
  END-IF
*
  MOVE #HOLD-ID     TO #CGA-SCR-ID
  MOVE #HOLD-SUFFIX TO #CGA-SCR-SUFFIX
ELSE
  REPEAT
    INPUT TEXT #TEXT USING MAP 'E4750MBD'
*
    IF (NOT #WP) OR (#CGA-SCR-COMMAND NE '*')
      ESCAPE BOTTOM
    END-IF
*
    IF #WP AND (#SCR-X GT 22)
      SUBTRACT 22 FROM #SCR-X
      FOR #I1 23 TO 57
        MOVE #SCR-LINES-CV (#I1) TO #SCR-LINES-CV (#I1 - 21)
        MOVE #SCR-LINES    (#I1) TO #SCR-LINES    (#I1 - 21)
      END-FOR
*
      RESET #SCR-LINES-CV (37:57)
      RESET #SCR-LINES    (37:57)
      ESCAPE TOP
    END-IF
    ESCAPE BOTTOM
  END-REPEAT
END-IF
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE NEW-LAYOUT
* * * * * * * * * * * * * *
*
IF (#I1 = 2) AND (#SCR-TYPE (#I1) = 'A')
  IF #SCR-DESIG (#I1, #I2) NE  ' '
    ADD 1 TO #NUM-ALT-BENES  /* use new layout for entered benes
  ELSE    /* 1st blank line
    IF #ALT-DONE
      SUBTRACT 1 FROM #SCR-X
      ESCAPE ROUTINE
    END-IF
*
    MOVE TRUE TO #ALT-DONE
*
    IF #NUM-ALT-BENES NE 0
      MOVE 'and all' TO #SCR-LINES (#SCR-X)
    ELSE
      MOVE 'All' TO #SCR-LINES (#SCR-X)
    END-IF
*
    COMPRESS #SCR-LINES (#SCR-X)
      'children of the insured (born or adopted)'
      INTO #SCR-LINES (#SCR-X)
*
    IF   (#SCR-SHARE-TYP (#I1) = ' ')
        AND (#SCR-PER-STR = ' ')
      COMPRESS #SCR-LINES (#SCR-X) '.'
        INTO #SCR-LINES (#SCR-X) LEAVING NO
    END-IF
*
    ESCAPE ROUTINE
  END-IF
END-IF
*
MOVE #SCR-BN-NAME (#I1, #I2) TO #NAME
RESET #LAST #MI #FIRST-20
CALLNAT 'GFMLNSXX' /* split name
  #NAME
  #FIRST-20
  #MI
  #LAST
  #ERR-MSG
*
COMPRESS #FIRST-20 #MI #LAST INTO #SCR-LINES-NAME (#SCR-X)
*    NG1 Start
* CALLNAT 'GRELASXX' #SCR-BN-REL (#I1, #I2)
*    #REL-DISP
*    #INVALID
*    #ERR-MSG
#Relation := FUNCTION-DISP-RL-DESC(<#SCR-BN-REL (#I1, #I2)>)
#Relation := EDIT_NAME_LOWCASE (< #Relation >)
IF *LENGTH ( #Relation ) > 13
  MOVE SUBSTR (#Relation,1,13) TO #SCR-LINES-RELATION (#SCR-X)
ELSE
  MOVE #Relation TO #SCR-LINES-RELATION (#SCR-X)
END-IF
*      NG1 End
IF   (#CGA-SCR-SUFFIX-2 = 'SH') /* coming from ATHPANNP /*DW1
    AND (*DEVICE = 'BATCH')
    AND (#SCR-BN-SSN-A (#I1, #I2) = 'SSN ON FL')             /*DW1
  MOVE 'SSN ON FILE' TO #SCR-LINES-SSN (#SCR-X)           /*DW1
ELSE
  IF   (#SCR-BN-SSN (#I1, #I2) = MASK (NNNNNNNNN))
      AND (#SCR-BN-SSN (#I1, #I2) NE 0)
    MOVE EDITED #SCR-BN-SSN(#I1, #I2) (EM=999-99-9999)
      TO #SCR-LINES-SSN      (#SCR-X)
  END-IF
END-IF
MOVE #SCR-BN-SHARE   (#I1, #I2) TO #SCR-LINES-SHARE    (#SCR-X)
MOVE #SCR-BN-SET-OPT (#I1, #I2) TO #SCR-LINES-SETTL    (#SCR-X)
IF #SCR-BN-RESTRICT(#I1, #I2) = 'Y'
  MOVE 'YES' TO #SCR-LINES-RESTRICT (#SCR-X)
ELSE
  MOVE ' NO' TO #SCR-LINES-RESTRICT (#SCR-X)
END-IF
*
IF   (#CGA-SCR-SUFFIX-2 = 'SH')   /* coming from ATHPANNP
    AND (*DEVICE = 'BATCH')
  MOVE TRUE TO #NEW-FORMAT-DATA (#SCR-X) /* to indicate raw new format
END-IF
*
END-SUBROUTINE
*
END
