* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNICG1.
/**
/** :author nguentchev
/* TODO Enter your code here
* vxt 06/15/2016 vt1 /Ticket 4544 - fix SSN to be 9 digits/
* bz-Name 07/07/2017     Fine Names  
DEFINE DATA
PARAMETER USING AUCAIC01
LOCAL USING ERROR_L
LOCAL
1 CONT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD
*
1 CNT-V VIEW OF A-CONTACTS
  2 CONTACT-TYPE    (A1)
  2 CONTACT-ID
  2 ID-NUMBER       (N6)
  2 DATE-OF-BIRTH   (N8)
  2 SSN             (N9)
  2 LAST-NAME
1 K    (I2)
1 L    (I2)
1 M    (I2)
1 #INSURED-CN (N6)
*
1 MEMBER VIEW OF A-STATUS
  2 LINKED-CN      (N6/30)
END-DEFINE
/**
IF InsuredContactID = 0
  ErrorDescription := 'Insured CI is required'
ELSE
  EXPAND ARRAY ContactRelation TO (1:99)
  PERFORM GET-RELATION
END-IF

DEFINE GET-RELATION
/*     ------------
RESET K
ADD 1 TO K
#INSURED-CN      := F-GET-CN(<InsuredContactID>)
ContactID (K)    := InsuredContactID
Relationship (K) := 'Self'
FIND CONT-REL-V WITH CONTACT-1-ID = InsuredContactID
  ADD 1 TO K
  ContactID (K) := CONTACT-2-ID
  Relationship (K) :=
    FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
END-FIND
* FIND CONT-REL-V WITH CONTACT-2-ID = InsuredContactID
*   ADD 1 TO K
*   ContactID (K) := CONTACT-1-ID
*   Relationship (K) :=
*     FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
* END-FIND
C@ContactRelation := K
FOR K 1 TO C@ContactRelation
  FIND (1) CNT-V WITH CONTACT-ID = ContactID (K)
    CN (K) := CNT-V.ID-NUMBER
*   ContactName (K) := FUNCTION-CONTACT-NAME-2(<ContactID (K)>)
    ContactName (K) := /* EDIT_NAME_LOWCASE (<                      /* bz-Name
      FUNCTION-CONTACT-NAME(< ContactID (K) >)  /*  >)              /* bz-Name
    DOB (K) := CNT-V.DATE-OF-BIRTH
    MOVE EDITED CNT-V.SSN (EM=9(9)) TO ContactRelationsList.SSN (K)            /* vt1 was z(9)
    ContactType (K) := CONTACT-TYPE
  END-FIND
END-FOR
*
IF K > 1
  SUBTRACT 1 FROM K
END-IF
*
THIS.
FIND (1) MEMBER WITH ID-NUMBER = #INSURED-CN
  FOR M 1 TO 30
    IF MEMBER.LINKED-CN(M) = CN(*)
      ESCAPE BOTTOM(THIS.)
    END-IF
    IF MEMBER.LINKED-CN (M) > 0
      FIND (1) CNT-V WITH CNT-V.ID-NUMBER = MEMBER.LINKED-CN (M)
        IF NO RECORDS FOUND
          ESCAPE BOTTOM
        END-NOREC
        ADD 1 TO K
        C@ContactRelation := K
        Relationship (K) := 'Linked CN'
        CN (K) := CNT-V.ID-NUMBER
        ContactID (K) := CNT-V.CONTACT-ID
*       ContactName (K) := FUNCTION-CONTACT-NAME-2(<CNT-V.CONTACT-ID>)

        ContactName (K) := /* EDIT_NAME_LOWCASE (<                /* bz-Name
          FUNCTION-CONTACT-NAME(< CNT-V.CONTACT-ID >) /* >)       /* bz-Name

        DOB (K) := CNT-V.DATE-OF-BIRTH
        MOVE EDITED CNT-V.SSN (EM=9(9)) TO ContactRelationsList.SSN (K)          /* vt1 was z(9)
        ContactType (K) := CNT-V.CONTACT-TYPE
      END-FIND
*       ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
END-SUBROUTINE
END
