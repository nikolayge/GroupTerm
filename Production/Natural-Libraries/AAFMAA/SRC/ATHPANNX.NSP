* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPANNM - LIST attenders at the annual meeting
*                        used prior to 1997 fron "NS" record
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  09062006 DW1  Increase size of #recs-read
* NGG  20121023 NG1  CN Expansion Project -
*                    New beneficiary database file increase CN up to 500000
*******************************************
DEFINE DATA
LOCAL
*
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
   3 ID-NUMBER-A    (A6)
  2 NOTE-TEXT     (A60/60)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 RANK
 2 MILITARY-SERVICE
 2 MILITARY-STATUS
 2 NAME
*
1 #COUNTERS
 2 #RECS-READ      (P7)   /* Dw1
 2 #RECS-SELECTED  (P5)
 2 #CNT            (P5)
 2 #I1             (I2)
 2 #I2             (I2)
 2 #CNT-2          (P5)
 2 #CNT-3          (P5)
 2 #FOUND-MATCH   (L)
 2 #A-F-ELSE      (P5/3)
 2 #A-R-C         (P5/3)
*
END-DEFINE
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Unsorted selected notes'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Sorted selected notes'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
READ NT-V BY ID-NUMBER FROM 00200
* IF NT-V.ID-NUMBER GT 199999
  IF NT-V.ID-NUMBER GT 500000   /* NG1
    ESCAPE BOTTOM
  END-IF
*
  ADD 1 TO #RECS-READ
*
  RESET #FOUND-MATCH
  FOR #I1 1 TO 60
    IF (NOTE-TEXT (#I1) = MASK ('CALLED TO RESERVE FOR'))
      ADD 1 TO #RECS-SELECTED
      ADD 1 TO #CNT-2
      DISPLAY (2) #CNT-2 ID-NUMBER #I1 NOTE-TEXT (#I1)
      MOVE TRUE TO #FOUND-MATCH
      ESCAPE BOTTOM
    END-IF
  END-FOR
*
  IF NOT #FOUND-MATCH
    ESCAPE TOP
  END-IF
*
  FIND (1) ST-V WITH MEMBER-KEY = NT-V.ID-NUMBER-A
  END-FIND
END-ALL
*
SORT BY RANK NAME
  USING MILITARY-SERVICE
        MILITARY-STATUS
*
  AT BREAK RANK
    NEWPAGE (3)
    RESET #CNT-3
  END-BREAK
*
  ADD 1 TO #CNT-3
  DISPLAY (3) #CNT-3 RANK NAME MILITARY-STATUS MILITARY-SERVICE
*
  IF MILITARY-STATUS = 'A'
    ADD 1 TO #A-R-C (1)
  ELSE
    IF MILITARY-STATUS = 'R'
      ADD 1 TO #A-R-C (2)
    ELSE
      ADD 1 TO #A-R-C (3)
    END-IF
  END-IF
*
  IF MILITARY-SERVICE = MASK ('A')
    ADD 1 TO #A-F-ELSE (1)
  ELSE
    IF MILITARY-SERVICE = MASK ('F')
      ADD 1 TO #A-F-ELSE (2)
    ELSE
      ADD 1 TO #A-F-ELSE (3)
    END-IF
  END-IF
END-SORT
*
WRITE (2) 'TOT-RECS-SELECTED    = ' #RECS-SELECTED
*
NEWPAGE (3)
WRITE (3) // '  ACTIVE/RETIRED/CIVILIAN'
WRITE (3) #A-R-C (*)
WRITE (3) // '  ARMY  /AIRFORC/ALL ELSE'
WRITE (3) #A-F-ELSE (*)
*
END
