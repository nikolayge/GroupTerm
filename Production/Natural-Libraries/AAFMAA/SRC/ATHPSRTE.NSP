* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPSRTE - Sort EZ PAY RECORDS
*
DEFINE DATA
LOCAL
*
1 PAD-FILE    (A94)
1 REDEFINE PAD-FILE
 2 PAD-REC-TYPE      (A1)
 2 PAD-TRAN-CODE     (N2)
 2 PAD-ABA-NUM       (N9)
 2 PAD-ACCT-NUM      (A17)
 2 PAD-AMT           (N8.2)
 2 PAD-CN            (N6)
 2 PAD-FILL          (A9)
 2 PAD-NAME          (A22)
 2 PAD-DISC-DATA     (A2)
 2 PAD-ADD-REC-IND   (N1)
 2 PAD-TRACE-NUM-ABA (N8)
 2 PAD-TRACE-NUM     (N7)
*
1 #NUM-CHECKING   (P5)
1 #AMT-CHECKING   (P9.2)
1 #NUM-SAVINGS    (P5)
1 #AMT-SAVINGS    (P9.2)
1 #NUM-PRENOTES   (P5)
1 #TOT-NUM-PAD-DEBITS (P9.2)
1 #TOT-AMT-PAD-DEBITS (P9.2)
1 #TOT-PAD-AMT   (P9.2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'EZ-PAY SORT'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD PAD-FILE
  IF PAD-REC-TYPE NE '6'
    ESCAPE TOP
  END-IF
*
END-ALL
*
SORT BY PAD-CN
  USING PAD-FILE
  WRITE WORK 3 PAD-FILE
*
  IF PAD-TRAN-CODE = 27 OR = 37
    ADD PAD-AMT TO #TOT-PAD-AMT
    WRITE 'Accum pad file' PAD-CN PAD-AMT #TOT-PAD-AMT
  END-IF
*
  IF PAD-TRAN-CODE = 27
    ADD 1 TO #NUM-CHECKING
    ADD 1 TO #TOT-NUM-PAD-DEBITS
    ADD PAD-AMT TO #AMT-CHECKING
    ADD PAD-AMT TO #TOT-AMT-PAD-DEBITS
  END-IF
*
  IF PAD-TRAN-CODE = 37
    ADD 1 TO #NUM-SAVINGS
    ADD 1 TO #TOT-NUM-PAD-DEBITS
    ADD PAD-AMT TO #AMT-SAVINGS
    ADD PAD-AMT TO #TOT-AMT-PAD-DEBITS
  END-IF
*
  IF PAD-TRAN-CODE = 28 OR = 38
    ADD 1 TO #NUM-PRENOTES
  END-IF
END-SORT
*
WRITE 'Total prenotes         =' #NUM-PRENOTES       (EM=ZZZ,ZZ9)
WRITE 'Number checking debits =' #NUM-CHECKING       (EM=ZZZ,ZZ9)
WRITE 'Number savings  debits =' #NUM-SAVINGS        (EM=ZZZ,ZZ9)
WRITE 'Total number debits    =' #TOT-NUM-PAD-DEBITS (EM=ZZZ,ZZ9)
WRITE 'Amount checking debits =' #AMT-CHECKING       (EM=Z,ZZZ,ZZZ.99)
WRITE 'Amount savings  debits =' #AMT-SAVINGS        (EM=Z,ZZZ,ZZZ.99)
WRITE 'Total debits           =' #TOT-AMT-PAD-DEBITS (EM=Z,ZZZ,ZZZ.99)
*
END
