* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Created to be used by AFA to track FAS changes from
* Member center
********************************************************
*        MODIFICATION LOG
********************************************************
* USER   DATE      TAG  REASON
* YAK    04202012       Initial Creation
********************************************************
*
DEFINE DATA
LOCAL USING OBJLGENT
LOCAL
1 #SEC-KEY       (A7)
1 REDEFINE #SEC-KEY
  2 #GEN-KEY     (A4)
  2 #GEN-SUB-KEY (A3)
1 #FAS-FILE      (A) Dynamic INIT <'/usr/SAG/MC/FAS_REPORT.TXT'>
1 #FAS-REPORT    (A) Dynamic INIT <'/usr/SAG/AAFMAA/batch/workfiles/PERMDA/COM/fas_change_report.txt'>
1 #OUTPUT-RECORD    (A500)
1 #INPUT-RECORD     (A100)
1 #DATN             (N8)
1 REDEFINE #DATN
  2 #DATN_A         (A8)
1 #DATE             (A) DYNAMIC
1 #TAB              (A1) INIT <H'09'>
1 #CN               (A) DYNAMIC
1 #FUNCTION         (A) DYNAMIC
1 #ACTION-TYPE      (A) DYNAMIC
1 #INV-CATEGORY     (A) DYNAMIC
1 #INV-TYPE         (A) DYNAMIC
1 #INV-TYPE-CODE    (A) DYNAMIC
1 #INV-CATEGORY-CODE(A) DYNAMIC
1 #USER             (A) DYNAMIC 

END-DEFINE
*
#DATN := *DATN
*
DEFINE WORK FILE 1 #FAS-FILE TYPE 'ASCII-COMPRESSED'
DEFINE WORK FILE 2 #FAS-REPORT TYPE 'ASCII-COMPRESSED'
COMPRESS 'CN' 'Action' 'Investment Category' 'Investment Type' 'Updated By'
  INTO #OUTPUT-RECORD WITH DELIMITER #TAB
WRITE WORK FILE 2 #OUTPUT-RECORD
*
READ WORK FILE 1 #INPUT-RECORD
  RESET #CN #FUNCTION #DATE #ACTION-TYPE #INV-CATEGORY #INV-TYPE-CODE #INV-CATEGORY-CODE #INV-TYPE
  SEPARATE #INPUT-RECORD INTO #CN #FUNCTION #DATE #ACTION-TYPE #INV-CATEGORY-CODE #INV-TYPE-CODE #USER IGNORE
    WITH DELIMITER #TAB
*
  #DATE := *TRIM(#DATE)
  IF #DATE = #DATN_A
    #CN := *TRIM(#CN)
    #ACTION-TYPE := *TRIM(#ACTION-TYPE)
    #INV-CATEGORY-CODE := *TRIM(#INV-CATEGORY-CODE)
    #GEN-KEY := 'CODE'
    DECIDE ON FIRST VALUE #INV-CATEGORY-CODE
      VALUE '1'
        #GEN-SUB-KEY := 'D01'
      VALUE '2'
        #GEN-SUB-KEY := 'D02'
      VALUE '3'
        #GEN-SUB-KEY := 'D03'
      VALUE '4'
        #GEN-SUB-KEY := 'D04'
      VALUE '5'
        #GEN-SUB-KEY := 'D05'
      VALUE '6'
        #GEN-SUB-KEY := 'D06'
      VALUE '7'
        #GEN-SUB-KEY := 'D07'
      VALUE '7A'
        #GEN-SUB-KEY := 'DA7'
      VALUE '8'
        #GEN-SUB-KEY := 'D08'
      NONE
        IGNORE
    END-DECIDE  
*
  IF #INV-TYPE-CODE <> ' '
    FIND GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
     ACCEPT IF GEN-TABLE-V.GEN-ALPHA-VALUE = *TRIM(#INV-TYPE-CODE)
     #INV-CATEGORY := GEN-TABLE-V.GEN-DESCRIPTION
     #INV-TYPE     := GEN-TABLE-V.ADDL-INFO
    END-FIND
  ELSE
    FIND(1) GEN-TABLE-V WITH SEC-KEY = #SEC-KEY
     #INV-CATEGORY := GEN-TABLE-V.GEN-DESCRIPTION
    END-FIND
  END-IF  
  COMPRESS #CN #TAB #ACTION-TYPE #TAB #INV-CATEGORY #TAB #INV-TYPE #TAB #USER INTO #OUTPUT-RECORD
  WRITE WORK FILE 2 #OUTPUT-RECORD
  END-IF
END-WORK
*
END

