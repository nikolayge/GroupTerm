* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: G3700PML
* THIS PROGRAM PROCESSES THE 'ML' MAILING REQUEST LIST SCREEN
*
*  MODIFICATION LOG
* DRW 10/01/2007 DW14 Delete AFSC Suffix's( BA/DI/EDU/FAS/FIN/FIR/IFR/MS/MS2/SSA/DOP)
* YAK 10/15/2012 YK1  Include new Level Term II plans ('S','R','Q')
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX     /* WORK FIELDS FOR GONERCXX - WORK FIELDS
LOCAL
1 #SCR-ID-NO       (N06/20)
1 #SCR-MEMBER-NAME (A20/20)
1 #SCR-TYPE        (A24/20)
1 REDEFINE #SCR-TYPE
 2 #SCR-TYPE-OCC   (20)
  3 #SCR-TYPE-XXX  (A4/6)
1 #SCR-CLERK       (A03/20)
1 #SCR-REQ-DATE    (A05/20)
1 #SCR-ASSUM-RET-DT-OCC (20)
 2 #SCR-ASSUM-RET-DT (A8/3)
 2 REDEFINE #SCR-ASSUM-RET-DT
  3 #SCR-ASSUM-RET-DT-OCC-3 (3)
   4 #SCR-ASSUM-RET-DT-1    (A3)
   4 #SCR-ASSUM-RET-DT-FIL1 (A1)
   4 #SCR-ASSUM-RET-DT-2    (A3)
   4 #SCR-ASSUM-RET-DT-AST (A1)
*
1 #BNP-TBL    /* this tbl is also in g3600pmr
 2 #BNP-1    (A20) INIT <'PRIN Bene By law'>
 2 #BNP-2    (A20) INIT <'ALT Bene By law'>
 2 #BNP-3    (A20) INIT <'Full Name Needed'>
 2 #BNP-4    (A20) INIT <'Rel of Bene Needed'>
 2 #BNP-5    (A20) INIT <'Exact Intentions'>
 2 #BNP-6    (A20) INIT <'Unequal Distribution'>
 2 #BNP-7    (A20) INIT <'No Beneficiary'>
 2 #BNP-8    (A20) INIT <'In Accord W Tel Conv'>
 2 #BNP-9    (A20) INIT <'Owner Signature Reqd'>
 2 #BNP-10   (A20) INIT <'Successor Owner Reqd'>
 2 #BNP-11   (A20) INIT <'Own Sig & Succ Owner'>
 2 #BNP-REST1 (A20/11)
1 REDEFINE #BNP-TBL
 2 #BNP-DESC (20)
  3 #BNP-DESC-1 (A8)
  3 #BNP-DESC-2 (A8)
  3 #BNP-DESC-3 (A4)
*
1 #TRNSF-TBL
 2 #TRN-1    (A5) INIT <'His'>
 2 #TRN-2    (A5) INIT <'Her'>
 2 #TRN-3    (A5) INIT <'Their'>
 2 #TRN-4    (A5) INIT <'Its'>
1 REDEFINE #TRNSF-TBL
 2 #TRN-DESC (A5/4)
*
1 #PLAN            (A3)
1 #PLAN-VP         (N2)
1 #FAC             (N3)
1 I                (I1)
1 #I1              (I1)
1 #I2              (I1)
1 #I3              (I1)
1 #HOLD-TYPE       (A4)
1 #EOF-MSG         (A60)
1 #STARTING-ID     (N6)
*
1 #TOT-RQSTS       (P4)
1 #TOT-RQSTS-A     (A4)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A         (A4)
 2 #MM-A           (A2)
 2 #DD-A           (A2)
*
1 ML-R01 VIEW OF REQUEST
 2 ID-NUMBER        (N6)
 2 MEMBER-NAME      (A20)
 2 CLERK-ID         (A3)
 2 ZIP-CODE         (N5)
 2 REDEFINE ZIP-CODE
  3 FIR-IND         (N1)
  3 MSG-IND         (N1)
  3 RET-IND         (N1)
  3 FIN-IND         (N1)
  3 FAS-IND         (N1)
 2 RQST-TABLE       (A10)
 2 REDEFINE RQST-TABLE
  3 BEN-IND         (A1)
  3 MAL-IND         (A1)
  3 BEN-SHO-IND     (A1)
  3 ACC-IND         (A1)
  3 CAP-IND         (A1)
  3 APP-IND         (A1)
  3 PIL-IND         (A1)
  3 CO-IND          (A1)
  3 VP-IND          (A1)
  3 MS-IND          (A1)
 2 MESSAGE          (A60/20)
 2 REQUEST-DATE     (N4)
 2 ASSUM-RET-DATE   (N8)
 2 REDEFINE ASSUM-RET-DATE
  3 ASSUM-RET-DATE-A (A8)
 2 REDEFINE ASSUM-RET-DATE
  3 ASSUM-RET-DATE-X (N2/4)
 2 ASSUM-RET-DATE-2 (N8)
*
 2 REDEFINE ASSUM-RET-DATE-2
  3 ASSUM-RET-DATE-2-A (A8)
 2 REDEFINE ASSUM-RET-DATE-2
  3 ASSUM-RET-DATE-2-X (N1/8)
 2 REDEFINE ASSUM-RET-DATE-2
  3 ASSUM-RET-DATE-2-2X (N2/4)
*
 2 ASSUM-RET-DATE-3 (N8)
 2 REDEFINE ASSUM-RET-DATE-3
  3 ASSUM-RET-DATE-3-X (N1/8)
 2 REDEFINE ASSUM-RET-DATE-3
  3 ASSUM-RET-DATE-3-2X (N2/4)
*
 2 INFO       (A50)   /* only used by co to store 1035 company name now
 2 CO-RQSTS      (A3/10)
 2 REDEFINE CO-RQSTS
  3 CO-RQSTS-OCC  (10)
   4 CO-CERT-SEQ  (N1)
   4 CO-CONV-TYPE (A2)
*
1 #MR-MSG-NOTE-NUM   (P6)
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT  (A60/3)
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
*
MOVE '*' TO #CGA-SCR-COMMAND
COMPUTE #STARTING-ID = #CGA-ORIG-ID - 1  /* IF AN INITIAL # WAS ENTERED
*
REPEAT
  IF #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  RESET I
*
  READ ML-R01 LOGICAL WITH ID-NUMBER STARTING FROM #STARTING-ID
    IF   (ZIP-CODE = 0)   /* NO REQUESTS
     AND (RQST-TABLE = ' ')
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'MSG')
     AND (MSG-IND = 0)              /* ONLY PROCESS MSG RQST
      ESCAPE TOP
    END-IF
*
    IF  ((#CGA-SCR-SUFFIX = 'BEN') OR (#CGA-SCR-SUFFIX = MASK ('BN')))
     AND (BEN-IND = ' ')             /* ONLY PROCESS BEN RQST
     AND (BEN-SHO-IND = ' ')
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'PUP')
     AND (APP-IND NE 'P')             /* ONLY PROCESS PUP RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'ACB' OR = 'ACS' OR = 'ACC')
     AND (ACC-IND = ' ')             /* ONLY PROCESS access RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'CAP')
     AND (CAP-IND NE '1')             /* ONLY PROCESS CAP RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'APP')
     AND (APP-IND = ' ')             /* ONLY PROCESS APP RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'PIL')
     AND (PIL-IND = ' ')             /* ONLY PROCESS PIL RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'CO')
     AND (CO-IND = ' ')             /* ONLY PROCESS CO RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'VP')
     AND (VP-IND = ' ')             /* ONLY PROCESS VP RQST
      ESCAPE TOP
    END-IF
*
    IF   (#CGA-SCR-SUFFIX = 'APL' OR = 'FAL'
     OR = 'RFS' OR = 'RFC' OR = 'RFZ' OR = 'LPF' OR = 'LPV')
     AND (MAL-IND = ' ')             /* ONLY PROCESS mal RQST
      ESCAPE TOP
    END-IF
*
    ADD 1 TO I
    ADD 1 TO #TOT-RQSTS
    ASSIGN #SCR-ID-NO       (I) = ID-NUMBER
    ASSIGN #SCR-MEMBER-NAME (I) = MEMBER-NAME
*
    IF ML-R01.FIR-IND = 1 OR = 5
      MOVE 'FIR'   TO #SCR-TYPE-XXX (I, 1)
    END-IF
*
    IF ML-R01.FIN-IND = 1 OR = 4
      MOVE 'FIN' TO #SCR-TYPE-XXX (I, 2)
    END-IF
*
    IF ML-R01.APP-IND NE ' '
      IF  ASSUM-RET-DATE NE 0
        RESET #SCR-ASSUM-RET-DT (I, 1)
        FOR #I1 1 TO 3
          IF ASSUM-RET-DATE-X (#I1) NE 0
            IF #I1 GT 2
              MOVE ASSUM-RET-DATE-A TO #SCR-ASSUM-RET-DT (I, 1)
              ESCAPE BOTTOM
            END-IF
*
            COMPUTE #FAC = ASSUM-RET-DATE-X (#I1) * 5
            COMPRESS #SCR-ASSUM-RET-DT (I, 1) #FAC 'K'
                INTO #SCR-ASSUM-RET-DT (I, 1) LEAVING NO
          END-IF
        END-FOR
      END-IF
*
      IF  ASSUM-RET-DATE-2 NE 0
        RESET #SCR-ASSUM-RET-DT (I, 2) #I2
        FOR #I1 1 TO 8
          IF ASSUM-RET-DATE-2-X (#I1) NE 0
            ADD 1 TO #I2
            IF #I2 GT 3
              MOVE ASSUM-RET-DATE-2-A TO #SCR-ASSUM-RET-DT (I, 2)
              ESCAPE BOTTOM
            END-IF
*
            IF #I1 = 1 MOVE 'OL' TO #PLAN END-IF
            IF #I1 = 2 MOVE '30' TO #PLAN END-IF
            IF #I1 = 3 MOVE '20' TO #PLAN END-IF
            IF #I1 = 4 MOVE '7P' TO #PLAN END-IF
            IF #I1 = 5 MOVE 'SP' TO #PLAN END-IF
            IF #I1 = 6 MOVE 'RT' TO #PLAN END-IF
            IF #I1 = 7 MOVE 'LT' TO #PLAN END-IF
            IF #I1 = 8
              IF ASSUM-RET-DATE-2-X (8) = 1 MOVE 'U' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 2 MOVE 'V' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 3 MOVE 'W' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 4 MOVE 'X' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 5 MOVE 'Y' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 6 MOVE 'Z' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 7 MOVE '*' TO #PLAN END-IF
              IF ASSUM-RET-DATE-2-X (8) = 8 MOVE '#' TO #PLAN END-IF
              COMPRESS #PLAN ASSUM-RET-DATE-3-2X (2)
                   INTO #PLAN LEAVING NO
            END-IF
*
            IF #I2 = 1
              MOVE #PLAN TO #SCR-ASSUM-RET-DT (I, 2)
            ELSE
              COMPRESS #SCR-ASSUM-RET-DT (I, 2) '/' #PLAN
                 INTO #SCR-ASSUM-RET-DT (I, 2) LEAVING NO
            END-IF
          END-IF
        END-FOR
      END-IF
    END-IF
*
    IF ML-R01.VP-IND NE ' '
      IF ML-R01.VP-IND = '1'
        MOVE 'VP ' TO #HOLD-TYPE   /* VP ADDS ONLY
      ELSE
        IF ML-R01.VP-IND = '2'
          MOVE 'VC ' TO #HOLD-TYPE  /* VP CURRENT & OPTIONAL ADDS
        ELSE
          IF ML-R01.VP-IND = '3'
            MOVE 'VAP' TO #HOLD-TYPE /* VP ADDS ONLY WITH APPLICATIONS
          ELSE
            MOVE 'VCA' TO #HOLD-TYPE /* VP CURR & OPT ADDS & APPLICS
          END-IF
        END-IF
      END-IF
      PERFORM INSERT-TYPE
*
      IF  ASSUM-RET-DATE NE 0
        RESET  #I2
        RESET #SCR-ASSUM-RET-DT (I, *)
        FOR #I1 1 TO 3
          IF ASSUM-RET-DATE-X (#I1) NE 0
            ADD 1 TO #I2
            IF #I1 GT 2
              MOVE ASSUM-RET-DATE-A TO #SCR-ASSUM-RET-DT (I, 1)
            ELSE
              COMPUTE #FAC = ASSUM-RET-DATE-X (#I1) * 5
              COMPRESS #SCR-ASSUM-RET-DT (I, 1) #FAC 'K'
                  INTO #SCR-ASSUM-RET-DT (I, 1) LEAVING NO
            END-IF
*
            MOVE ASSUM-RET-DATE-2-2X (#I1) TO #PLAN-VP
*
            IF #I2 = 1
              MOVE #PLAN-VP TO #SCR-ASSUM-RET-DT (I, 2)
            ELSE
              COMPRESS #SCR-ASSUM-RET-DT (I, 2) '/' #PLAN-VP
                 INTO #SCR-ASSUM-RET-DT (I, 2) LEAVING NO
            END-IF
*
            IF ASSUM-RET-DATE-3-X (#I1) = 1 MOVE 'N' TO #PLAN END-IF
            IF ASSUM-RET-DATE-3-X (#I1) = 2 MOVE 'S' TO #PLAN END-IF
*
            IF #I2 = 1
              MOVE #PLAN TO #SCR-ASSUM-RET-DT (I, 3)
            ELSE
              COMPRESS #SCR-ASSUM-RET-DT (I, 3) #PLAN
                   INTO #SCR-ASSUM-RET-DT (I, 3) LEAVING NO
            END-IF
          END-IF
        END-FOR
*
        RESET  #I2
        FOR #I1 1 TO 3
          IF ASSUM-RET-DATE-X (#I1) NE 0
            ADD 1 TO #I2
*
            RESET #PLAN
            IF ASSUM-RET-DATE-3-X (#I1 + 4) = 0 MOVE 'N' TO #PLAN END-IF
            IF ASSUM-RET-DATE-3-X (#I1 + 4) = 1 MOVE 'A' TO #PLAN END-IF
            IF ASSUM-RET-DATE-3-X (#I1 + 4) = 2 MOVE 'D' TO #PLAN END-IF
*
            IF #I2 = 1
              COMPRESS #SCR-ASSUM-RET-DT (I, 3) '/' #PLAN
                   INTO #SCR-ASSUM-RET-DT (I, 3) LEAVING NO
            ELSE
              COMPRESS #SCR-ASSUM-RET-DT (I, 3) #PLAN
                   INTO #SCR-ASSUM-RET-DT (I, 3) LEAVING NO
            END-IF
          END-IF
        END-FOR
      END-IF
    END-IF
*
    IF ML-R01.BEN-IND NE ' '
      IF ML-R01.BEN-IND = '1'
        MOVE 'BNF' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '2'
        MOVE 'BNB' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '3'
        MOVE 'BNA' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '4'
        MOVE 'BNC' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '5'
        MOVE 'BNP' TO #SCR-TYPE-XXX (I,5)
        MOVE #BNP-DESC-1 (ASSUM-RET-DATE-3) TO #SCR-ASSUM-RET-DT (I, 1)
        MOVE #BNP-DESC-2 (ASSUM-RET-DATE-3) TO #SCR-ASSUM-RET-DT (I, 2)
        MOVE #BNP-DESC-3 (ASSUM-RET-DATE-3) TO #SCR-ASSUM-RET-DT (I, 3)
      END-IF
*
      IF ML-R01.BEN-IND = '6'
        MOVE 'BNT' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '7'
        MOVE 'BNL' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '8'
        MOVE 'BNN' TO #SCR-TYPE-XXX (I,5)
      END-IF
*
      IF ML-R01.BEN-IND = '6' OR = '7' OR = '8'
        MOVE #TRN-DESC (ASSUM-RET-DATE-3) TO #SCR-ASSUM-RET-DT (I, 1)
        IF ASSUM-RET-DATE-2 = 1
          MOVE 'Succ Own' TO #SCR-ASSUM-RET-DT (I,2)
        END-IF
      END-IF
    END-IF
*
    IF ML-R01.BEN-SHO-IND NE ' '
      MOVE 'BND' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.CAP-IND NE ' '
      MOVE 'CAP' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.APP-IND NE ' '
      DECIDE ON FIRST VALUE ML-R01.APP-IND
        VALUE 'P' MOVE 'PUP' TO #HOLD-TYPE
        VALUE 'A' MOVE 'APP' TO #HOLD-TYPE
        VALUE 'B' MOVE 'APN' TO #HOLD-TYPE
        VALUE 'C' MOVE 'APT' TO #HOLD-TYPE
        VALUE 'D' MOVE 'AS ' TO #HOLD-TYPE
        VALUE 'E' MOVE 'ASN' TO #HOLD-TYPE
        VALUE 'F' MOVE 'AST' TO #HOLD-TYPE
        VALUE 'G' MOVE 'ASG' TO #HOLD-TYPE
        VALUE 'H' MOVE 'AMS' TO #HOLD-TYPE
        VALUE '1' MOVE 'AG1' TO #HOLD-TYPE
        VALUE '2' MOVE 'AG2' TO #HOLD-TYPE
        VALUE '3' MOVE 'AG3' TO #HOLD-TYPE
        VALUE '4' MOVE 'AG4' TO #HOLD-TYPE
        VALUE '5' MOVE 'AG5' TO #HOLD-TYPE
        VALUE '6' MOVE 'AG6' TO #HOLD-TYPE
        NONE      MOVE '???' TO #HOLD-TYPE
      END-DECIDE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.PIL-IND = '1'
      MOVE 'PIL' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.CO-IND NE ' '
      MOVE 'CO' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
*
      IF #SCR-ASSUM-RET-DT (I, 1) = ' '
        RESET #I2 #I3
        FOR #I1 1 TO 10
          IF CO-RQSTS (#I1) NE ' '
            ADD 1 TO #I2
            IF #I2 = 1
              ADD 1 TO #I3
*
              IF #I3 GT 3
                MOVE '*' TO #SCR-ASSUM-RET-DT-AST (I, 3)
                ESCAPE BOTTOM
              END-IF
*
              MOVE CO-RQSTS (#I1) TO #SCR-ASSUM-RET-DT (I, #I3)
            ELSE
              COMPRESS #SCR-ASSUM-RET-DT (I, #I3) '/' CO-RQSTS (#I1)
                       INTO #SCR-ASSUM-RET-DT (I, #I3) LEAVING NO
               RESET #I2
            END-IF
          END-IF
        END-FOR
      END-IF
    END-IF
*
    IF ML-R01.MAL-IND = '2'
      MOVE 'APL' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '3'
      MOVE 'FAL' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '4'
      MOVE 'RFS' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '5'
      MOVE 'RFC' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '6'
      MOVE 'RFZ' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '7'
      MOVE 'LPF' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '8'
      MOVE 'LPV' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MAL-IND = '9'
      MOVE 'LPB' TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.ACC-IND NE ' '
      IF ML-R01.ACC-IND = '1'
        MOVE 'ACB' TO #HOLD-TYPE
      ELSE
        MOVE 'ACS' TO #HOLD-TYPE
      END-IF
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MSG-IND = 1
      MOVE 'MSG' TO #HOLD-TYPE
*
      IF (MESSAGE (2) = MASK ('ENCLOSED IS A FORM TO SEND TO'))
        MOVE 'SSA' TO #HOLD-TYPE
      ELSE
        IF (MESSAGE (3) = MASK ('CONGRATULATIONS ON YOUR PROMOTION'))
          MOVE 'DOP' TO #HOLD-TYPE
        END-IF
      END-IF
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MSG-IND = 2
      MOVE 'MSB'  TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    IF ML-R01.MSG-IND = 3
      MOVE 'MSG'  TO #HOLD-TYPE
      PERFORM INSERT-TYPE
      MOVE 'MSB'  TO #HOLD-TYPE
      PERFORM INSERT-TYPE
    END-IF
*
    ASSIGN #SCR-CLERK       (I) = CLERK-ID
    MOVE EDITED REQUEST-DATE (EM=99/99) TO #SCR-REQ-DATE (I)
*
    IF I GE 20
      ESCAPE BOTTOM
    END-IF
  END-READ /* (0151)
*
  IF I LT 20
    MOVE EDITED #TOT-RQSTS (EM=ZZZZ) TO #TOT-RQSTS-A
    COMPRESS 'ALL' #TOT-RQSTS-A 'Requests displayed' INTO #EOF-MSG
    RESET #TOT-RQSTS
    MOVE 'ML' TO #CGA-SCR-COMMAND
    RESET #CGA-SCR-SUFFIX
    INPUT TEXT #EOF-MSG USING MAP 'G3700MML'
    FETCH 'G1000PXX'
  END-IF
*
  MOVE EDITED #TOT-RQSTS (EM=ZZZZ) TO #TOT-RQSTS-A
  COMPRESS  #TOT-RQSTS-A 'Requests displayed' INTO #EOF-MSG
  COMPUTE #STARTING-ID = #SCR-ID-NO (20) + 1
*
  IF #CGA-SCR-SUFFIX NE 'CNT'
    INPUT TEXT #EOF-MSG USING MAP 'G3700MML'
  END-IF
*
  RESET #SCR-ID-NO       (*)
  RESET #SCR-MEMBER-NAME (*)
  RESET #SCR-CLERK       (*)
  RESET #SCR-TYPE        (*)
  RESET #SCR-REQ-DATE    (*)
  RESET #SCR-ASSUM-RET-DT (*, *)
END-REPEAT /* (0144)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE INSERT-TYPE
*
FOR #I1 6 TO 1 STEP -1
  IF #SCR-TYPE-XXX (I, #I1) = ' '
    MOVE #HOLD-TYPE TO #SCR-TYPE-XXX (I, #I1)
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
