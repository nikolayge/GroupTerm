* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: C1000PAT
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    11152005  None    Program written.                            *
* PAM    04142009  PM1     Reset counter when 599999 reached           *   
************************************************************************
DEFINE DATA
*
PARAMETER
*
1 #AT-NUMBER       (N6)
*
LOCAL
*
1 NM-T01 VIEW OF A-TERMINALS
 2 TERMINAL-ID     (A8)
 2 COMMENTS        (A20)
 2 REDEFINE COMMENTS
  3 LAST-AT        (N6)
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER       (N6)
*
1 #I1              (I2)
1 #AVAILABLE       (L)
*
END-DEFINE
************************************************************************
*
FOR #I1 1 TO 10000
  FIND (1) NM-T01 WITH TERMINAL-ID = 'ATMASTER'
    MOVE LAST-AT TO #AT-NUMBER
    IF #AT-NUMBER = 599999               /* PM1
      LAST-AT := 500000                  /* PM1
    ELSE                                 /* PM1
      ADD 1 TO LAST-AT
    END-IF                               /* PM1
    UPDATE RECORD (0038)
  END-FIND
*
  RESET #AVAILABLE
  FIND (1) AT-V WITH AT-V.ID-NUMBER = #AT-NUMBER
    IF NO RECORDS FOUND
      MOVE TRUE TO #AVAILABLE
      ESCAPE BOTTOM
    END-NOREC
  END-FIND
*
  IF #AVAILABLE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END
