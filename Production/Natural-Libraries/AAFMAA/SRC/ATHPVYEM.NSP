* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPVYEM - Merge Status & VA-History
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* DRW  12082005      Create                                            *  
* YAK  10112012 YK1  Use function to identify Level Term II plans      *
* YAK  11092012 YK2  Add No Medical Term plan  (EZ)                    *
* vxt  12122012  vt1 extend field size
* vxt  12302013 vt2  Extend the size of face-amt field
************************************************************************
DEFINE DATA
LOCAL
*
1 #STAT-IN
 2 #STAT-ID-NUMBER        (N6)
 2 #STAT-ISSUE-DATE       (N8)
 2 #STAT-IN-INFO  (A235)  /* (235 - 14) = 221
 2 REDEFINE #STAT-IN-INFO
  3 #STAT-PAY-STATUS      (A2)    /* Prior to 1997 y/e
  3 #STAT-HIPS            (N1)    /* P1
  3 #STAT-FACE-AMT        (N7.2)  /* P5.2                    /* vt2 was n6.2
  3 #STAT-ANN-PREM             (N5.2)  /* P5.2
  3 #STAT-MIN-CV               (N7.2)  /* P5.2               /* vt1 was n6.2
  3 #STAT-NXT-YR-MIN-CV        (N7.2)  /* P5.2               /* vt1 was n6.2
  3 #STAT-INT-RATE             (N2.3)  /* P2.3
  3 #STAT-MODE                 (A1)     /*      = 54 CHARS
*       below added 1997 y/e
  3 #STAT-INTRO-FLAG           (A1)
  3 #STAT-PLAN            (A3)
  3 REDEFINE #STAT-PLAN
   4 #STAT-PLAN-A1    (A1)
   4 #STAT-PLAN-N2    (N2)
  3 #STAT-DOB             (N8)
  3 #STAT-ACV                  (N7.2)
  3 #STAT-CV                   (N7.2)
  3 #STAT-CONT-FUND            (N7.2)  /* beg-cash-value in database
  3 #STAT-CURR-ACQ             (N4.2)  /* month-alloc-amt in database
  3 #STAT-MON-AMORT-ACQ        (N3.2)
  3 STAT-#DB                   (N7.2)
  3 #STAT-FIXED-LOAN           (N7.2)
  3 #STAT-FIXED-ACC-INT        (N5.2)
  3 #STAT-VAR-LOAN             (N7.2)
  3 #STAT-VAR-ACC-INT          (N5.2)
  3 #STAT-DB-CR                (N7.2)
  3 #STAT-PAID-TO-DT           (N8)
  3 #STAT-PAID-UP-DT           (N8)     /* + 116 = 170 chars  a/o 1/1998
  3 #STAT-5-5-DEATH-BENEFIT   (N7.2)   /* + 125 = 179 chars  a/o  4/1998
* below added 12/1998
  3 #STAT-BOTH-LOANS           (N7.2)
  3 #STAT-CAP-POL-FLAG         (A1)
  3 #STAT-SMOKER-FLAG          (A1)
  3 #STAT-USES-1996-FLAG       (A1)
  3 #STAT-DUES-FLAG            (A1)     /* + 138 = 192 chars  a/o  12/98
  3 #STAT-NXT-MON-CASH-VALUE   (N7.2)
  3 #STAT-NXT-MON-DEATH-BENEFIT (N7.2)
  3 #STAT-YTD-DUES-PAID        (N5.2)   /* + 163 = 217 chars  a/o  12/98
* BELOW ADDED 12/1999
  3 #STAT-RID-PLAN             (A2)
  3 #STAT-RID-EFF-DT           (N8)
  3 #STAT-RID-PDTO-DT          (N8)     /* + 18 = 235 chars  a/o  12/99
*
1 #VA-IN
 2 #VA-ID-NUMBER         (N6)
 2 #VA-ISSUE-DATE        (N8)
 2 #VA-IN-INFO   (A104)   /* (118 - 14) = 104
 2 REDEFINE #VA-IN-INFO
  3 #VA-PLAN             (A3)
  3 REDEFINE #VA-PLAN
   4 #VA-PLAN-A1     (A1)
   4 #VA-PLAN-A2     (A2)
  3 #VA-ISSUE-AGE         (A2)
  3 #VA-DB                   (N7.2)
  3 #VA-ACV                  (N7.2)
  3 #VA-CV                   (N7.2)
  3 #VA-YTD-PREMIUMS-PAID    (N7.2)
  3 #VA-YTD-INTEREST-PAID    (N5.2)
  3 #VA-YTD-COI-PAID         (N5.2)
  3 #VA-YTD-ACQ-PAID         (N4.2)
  3 #VA-YTD-MNT-PAID         (N4.2)
  3 #VA-TERM-CODE         (A3)
  3 #VA-TERM-DATE            (N6)
  3 #VA-PROR-LOAN-AMT        (N5.2)
  3 #VA-SMOKER-FLAG          (A1)
  3 #VA-USES-1996-PREMS      (A1)  /* Y if month-alloc-amt is 999.99
  3 #VA-CAP-POLICY           (A1)  /* Y if conv-dur is 999
  3 #VA-BEG-OF-YR-ACV        (N7.2)
  3 #VA-BEG-OF-YR-DB         (N7.2)
*
1 #COMBINED-OUT
 2 #ID-NUMBER            (N6)     /* from v/a history rec
 2 #ISSUE-DATE           (N8)
 2 #VA-OUT-INFO   (A104)   /* (118 - 14) = 104
 2 REDEFINE #VA-OUT-INFO
  3 #PLAN-HIST            (A3)
  3 REDEFINE #PLAN-HIST
   4 #PLAN-HIST-A1    (A1)
   4 #PLAN-HIST-A2    (A2)
  3 #ISSUE-AGE            (A2)
  3 #DB-HIST              (N7.2)
  3 #ACV-HIST             (N7.2)
  3 #CV-HIST              (N7.2)
  3 #YTD-PREMIUMS-PAID    (N7.2)
  3 #YTD-INTEREST-PAID    (N5.2)
  3 #YTD-COI-PAID         (N5.2)
  3 #YTD-ACQ-PAID         (N4.2)
  3 #YTD-MNT-PAID         (N4.2)
  3 #TERM-CODE            (A3)
  3 #TERM-DATE            (N6)
  3 REDEFINE #TERM-DATE
   4 #TERM-DATE-YYYY      (N4)
   4 #TERM-DATE-MM        (N2)
  3 #PROR-LOAN-AMT        (N5.2)
  3 #SMOKER-FLAG-HIST     (A1)
  3 #USES-1996-PREMS      (A1)  /* Y if month-alloc-amt is 999.99
  3 #CAP-POLICY           (A1)  /* Y if conv-dur is 999
  3 #BEG-OF-YR-ACV        (N7.2)
  3 #BEG-OF-YR-DB         (N7.2)
************     above = 118 chars
 2 #STAT-OUT-INFO  (A224)  /* (235 - 14) = 221      /* vt1 was a221   /* vt2 was a223
 2 REDEFINE #STAT-OUT-INFO
  3 #PAY-STATUS           (A2)       /* from status record
  3 #NUM-SHIPS            (N1)
  3 #FACE-AMT             (N7.2)                       /* vt2 was n6.2 
  3 #ANN-PREM             (N5.2)
  3 #MIN-CV               (N7.2)                       /* vt1 was n6.2
  3 #NXT-YR-MIN-CV        (N7.2)                       /* vt1 was n6.2
  3 #INT-RATE             (N2.3)
  3 #MODE                 (A1)
*       below added 1997 y/e
  3 #INTRO-FLAG           (A1)
  3 #PLAN                 (A3)
  3 REDEFINE #PLAN
   4 #PLAN-A1    (A1)
   4 #PLAN-N2    (N2)
  3 #DOB                  (N8)
  3 #ACV                  (N7.2)
  3 #CSV                  (N7.2)
  3 #CONT-FUND            (N7.2)
  3 #CURR-ACQ             (N4.2)
  3 #MON-AMORT-ACQ        (N3.2)
  3 #DB                   (N7.2)
  3 #FIXED-LOAN           (N7.2)
  3 #FIXED-ACC-INT        (N5.2)
  3 #VAR-LOAN             (N7.2)
  3 #VAR-ACC-INT          (N5.2)
  3 #DB-CR                (N7.2)
  3 #PAID-TO-DT           (N8)
  3 #PAID-UP-DT           (N8)     /* a/o 1/1998
  3 #5-5-DEATH-BENEFIT    (N7.2)   /* a/o 4/1998
* below added 12/1998
  3 #PRORATED-LOAN        (N7.2)
  3 #CAP-POL-FLAG         (A1)
  3 #SMOKER-FLAG          (A1)
  3 #USES-1996-FLAG       (A1)
  3 #DUES-FLAG            (A1)
  3 #NXT-MON-CASH-VALUE   (N7.2)
  3 #NXT-MON-DEATH-BENEFIT (N7.2)
  3 #YTD-DUES-PAID        (N5.2)   /* + 203 = 321 chars  a/o  12/98
*       BELOW ADDED 1999 Y/E
  3 #RID-PLAN             (A2)
  3 REDEFINE #RID-PLAN
   4 #RID-PLAN-1          (A1)
   4 #RID-PLAN-2          (N1)
  3 #RID-EFF-DT           (N8)
  3 #RID-PD-TO            (N8)     /* + 18  = 339 CHARS  A/O  12/1999
*
****************** ABOVE = 221 CHARS
****************** TOTAL = 339 CHARS
*
1 #NUM-STAT-IN   (N6)
1 #NUM-VA-IN     (N6)
1 #NUM-STAT-WO-VA  (N6)
1 #NUM-VA-WO-STAT  (N6)
1 #NUM-STAT-AND-VA (N6)
1 #RECS-OUT        (N6)
1 #REINSTATES    (N6)
*
1 #CURR-STAT  (A17)
1 REDEFINE #CURR-STAT
 2 #CURR-STAT-CN     (N6)
 2 #CURR-STAT-ISS-DT (N8)
 2 #CURR-STAT-PLAN   (A3)
*
1 #CURR-VA  (A17)
1 REDEFINE #CURR-VA
 2 #CURR-VA-CN     (N6)
 2 #CURR-VA-ISS-DT (N8)
 2 #CURR-VA-PLAN     (A3)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (N4)
 2 REDEFINE #YYYY
  3 #CENT  (N2)
  3 #YY-A  (A2)
 2 #MM     (N2)
 2 #DD     (N2)
*
1 #COUNTERS
  2 #RECS-READ        (P7)
  2 #RECS-WRITTEN     (P7)
  2 #CNT              (P7)
  2 #I1               (I2)
  2 #I2               (I2)
  2 #I3               (I2)
  2 #CNT-5            (P5)
  2 #CNT-6            (P5)
*
END-DEFINE
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM:' *PROGRAM 3X
   'Exceptions'
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 5X 'PAGE:' *PAGE-NUMBER /
*
FORMAT (2) LS=170 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM 3X
   'Merge Status & V/A History'
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 5X 'PAGE:' *PAGE-NUMBER (2) /
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM:' *PROGRAM 3X
   'CASH-VALUE GREATER THAN ACV FOR '
   3X 'DATE:' *DAT4U 2X 'TIME:' *TIMX 5X 'PAGE:' *PAGE-NUMBER (4) /
*
PERFORM READ-STATUS
PERFORM READ-VA
*
REPEAT UNTIL (#CURR-STAT = '99999999999999999')
         AND (#CURR-VA   = '99999999999999999')
*
  RESET #COMBINED-OUT
*
  IF #CURR-STAT LT #CURR-VA   /* Status with no V/A History
    ADD 1 TO #NUM-STAT-WO-VA
*   WRITE #CURR-STAT ' Status w/o V/A History'
    RESET  #DB-HIST
    PERFORM MOVE-STAT-INFO
    RESET  #ACV-HIST #CV-HIST #YTD-PREMIUMS-PAID
      #YTD-INTEREST-PAID #YTD-COI-PAID #YTD-ACQ-PAID #YTD-MNT-PAID
      #PROR-LOAN-AMT #BEG-OF-YR-ACV #BEG-OF-YR-DB
*
    MOVE #STAT-ISSUE-DATE TO #YYYYMMDD   /* maybe else where
    SUBTRACT #STAT-DOB FROM #YYYYMMDD
    MOVE #YY-A TO #ISSUE-AGE
*
    WRITE WORK 3 #COMBINED-OUT
    ADD 1 TO #RECS-OUT
*
    PERFORM READ-STATUS
    ESCAPE TOP
  END-IF
*
  IF #CURR-VA LT #CURR-STAT  /* V/a with no Status
    ADD 1 TO #NUM-VA-WO-STAT
*   WRITE #CURR-VA ' V/A History w/o status'
    PERFORM MOVE-VA-INFO
    RESET #FACE-AMT #ANN-PREM #MIN-CV #NXT-YR-MIN-CV #INT-RATE
      #DOB #ACV #CSV #CONT-FUND #CURR-ACQ #MON-AMORT-ACQ #DB #FIXED-LOAN
      #FIXED-ACC-INT #VAR-LOAN #VAR-ACC-INT #DB-CR #PAID-TO-DT
      #PAID-UP-DT #5-5-DEATH-BENEFIT #PRORATED-LOAN #NXT-MON-CASH-VALUE
      #NXT-MON-DEATH-BENEFIT #YTD-DUES-PAID #RID-EFF-DT #RID-PD-TO
      #NUM-SHIPS
    MOVE '00' TO #RID-PLAN
*
    WRITE WORK 3 #COMBINED-OUT
    ADD 1 TO #RECS-OUT
*
    PERFORM READ-VA
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #NUM-STAT-AND-VA             /* Status = V/A
* WRITE #CURR-STAT ' Status = V/A History'
*
  PERFORM MOVE-VA-INFO
  PERFORM MOVE-STAT-INFO   /* Must follow MOVE-VA-INFO
*
  IF #TERM-CODE = 'REI'
    ADD 1 TO #REINSTATES   /* Reinstatement here is ok
  ELSE
    IF #TERM-CODE NE ' '
      WRITE #VA-ID-NUMBER #VA-ISSUE-DATE 'Stat = Hist with Term Code of'
         #VA-TERM-CODE
      RESET #TERM-CODE #TERM-DATE
    END-IF
  END-IF
*
  WRITE WORK 3 #COMBINED-OUT
*
  PERFORM READ-STATUS
  PERFORM READ-VA
END-REPEAT
*
WRITE #REINSTATES      '= Reinstatements'
WRITE #NUM-VA-WO-STAT  '= VA without Status'
WRITE #NUM-STAT-WO-VA  '= Status without VA'
WRITE #NUM-STAT-AND-VA '= Status and VA match'
WRITE #RECS-OUT        '= Records Out'
*
DEFINE SUBROUTINE MOVE-STAT-INFO
*
MOVE #STAT-ID-NUMBER  TO #ID-NUMBER
MOVE #STAT-ISSUE-DATE TO #ISSUE-DATE
MOVE #STAT-PLAN       TO #PLAN-HIST
MOVE #STAT-IN-INFO    TO #STAT-OUT-INFO
*
PERFORM RESET-PLAN
*
END-SUBROUTINE
*
DEFINE SUBROUTINE MOVE-VA-INFO
*
MOVE #VA-ID-NUMBER TO  #ID-NUMBER
MOVE #VA-ISSUE-DATE TO #ISSUE-DATE
MOVE #VA-IN-INFO    TO #VA-OUT-INFO
PERFORM RESET-PLAN
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE RESET-PLAN
* * * * * * * * * * * * * * *
*
* IF (#PLAN-HIST-A1 GE 'U') AND (#PLAN-HIST-A1 LE 'Z')     /* YK1
IF F-LT2-TRUE(<#PLAN-HIST>)                                /* YK1
  MOVE 'LV2' TO #PLAN-HIST
END-IF
*
IF  (#PLAN-HIST = MASK (.'F'.))
 OR (#PLAN-HIST = MASK (..'F'))
  MOVE  'FLX' TO #PLAN-HIST
END-IF
*
IF #PAY-STATUS = '44'
  MOVE ' ET' TO #PLAN-HIST
END-IF
*
IF #PAY-STATUS = '45'
  MOVE ' RP' TO #PLAN-HIST
END-IF
*
IF #PLAN-HIST = 'RT' OR = 'LT' OR = 'LV2' OR = 'EZ'     /* YK2
  MOVE #FACE-AMT TO #DB-HIST
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-STATUS
* * * * * * * * * * * * * * * * *
*
IF #CURR-STAT = '99999999999999999'
  ESCAPE ROUTINE
END-IF
*
READ WORK 1 ONCE RECORD #STAT-IN
  AT END
    MOVE '99999999999999999' TO #CURR-STAT
    ESCAPE ROUTINE
  END-ENDFILE
*
ADD 1 TO #NUM-STAT-IN
MOVE #STAT-IN.#STAT-ID-NUMBER  TO #CURR-STAT-CN
MOVE #STAT-IN.#STAT-ISSUE-DATE TO #CURR-STAT-ISS-DT
MOVE #STAT-IN.#STAT-PLAN       TO #CURR-STAT-PLAN
* WRITE 'just read status' #CURR-STAT
*
END-SUBROUTINE
*
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-VA
* * * * * * * * * * * * * * * * *
*
IF #CURR-VA = '99999999999999999'
  ESCAPE ROUTINE
END-IF
*
READ WORK 2 ONCE RECORD #VA-IN
  AT END
    MOVE '99999999999999999' TO #CURR-VA
    ESCAPE ROUTINE
  END-ENDFILE
*
ADD 1 TO #NUM-VA-IN
MOVE #VA-IN.#VA-ID-NUMBER  TO #CURR-VA-CN
MOVE #VA-IN.#VA-ISSUE-DATE TO #CURR-VA-ISS-DT
MOVE #VA-IN.#VA-PLAN       TO #CURR-VA-PLAN
* WRITE 'just read va' #CURR-VA
*
END-SUBROUTINE
*
END
