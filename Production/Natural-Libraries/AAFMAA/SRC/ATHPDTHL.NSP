* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: athpdthl - list of death with length of mbrship
*
DEFINE DATA
LOCAL
*
1 VA-V VIEW OF A-VA-HISTORY
 2 RECORD-KEY           (N15)
 2 REDEFINE RECORD-KEY
  3 ID-NUMBER           (N6)
  3 REDEFINE ID-NUMBER
   4 ID-NUMBER-A        (A6)
  3 ISSUE-DATE       (N8)
  3 REDEFINE ISSUE-DATE
   4 ISS-DT-YYYY    (N4)
   4 ISS-DT-MM      (N2)
   4 ISS-DT-DD      (N2)
  3 SPLIT-IND           (N1)
 2 TERMINATION-DATE     (P7)
 2 TERMINATION-CODE     (A3)
 2 DEATH-BENEFIT-M      (P7.2/1)
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 WIDOW-FLAG     (A1)
  2 NAME           (A25)
  2 DATE-OF-DEATH  (N8)
  2 REDEFINE DATE-OF-DEATH
   3 DOD-YYYY      (N4)
   3 DOD-MM        (N2)
   3 DOD-DD        (N2)
*
1 #COUNTERS
  2 #RECS-READ        (P5)
  2 #CNT              (P5)
  2 #CNT-2            (P5)
  2 #CNT-3            (P5)
  2 #CNT-4            (P5)
  2 #CNT-5            (P5)
  2 #CNT-6            (P5)
  2 #I1               (I2)
*
1 #STAT-NOT-FND   (L)
1 #TERM-DT     (N6)
1 REDEFINE #TERM-DT
 2 #TERM-DT-YYYY (N4)
 2 #TERM-DT-MM   (N2)
*
1 #SSN               (N9)
1 REDEFINE #SSN
 2 #SSN-1ST-3        (N3)
 2 #SSN-MID-2        (N2)
 2 #SSN-QTD          (N4)
*
1 #WYYYYMMDD-N     (N8)
1 REDEFINE #WYYYYMMDD-N
 2 #WYYYY    (N4)
 2 #WMM      (N2)
 2 #WDD      (N2)
1 #LEN-MBRSHIP    (A16)
*
END-DEFINE
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Deaths in 1993 with length of service'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Deaths in 1994 with length of service'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE  (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
     'Deaths in 1995 with length of service'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
READ VA-V BY RECORD-KEY
  ADD +1 TO #RECS-READ
  MOVE VA-V.TERMINATION-DATE TO #TERM-DT
*
  IF  (NOT (#TERM-DT-YYYY = 1993 OR = 1994 OR = 1995))
   OR (VA-V.TERMINATION-CODE NE 'DTH')  /* IF DEAD AS OF Y/E
    ESCAPE TOP
  END-IF
*
  IF VA-V.TERMINATION-CODE NE 'DTH'   /* IF DEAD AS OF Y/E
    ESCAPE TOP
  END-IF
*
  RESET #STAT-NOT-FND
  FIND (1) ST-V WITH WIDOW-KEY = VA-V.ID-NUMBER-A
    IF NO RECORDS FOUND
      WRITE 'STAT REC NOT FOUND FOR ' VA-V.ID-NUMBER
            ' Issue date = ' ISSUE-DATE
            ' Term code  = ' TERMINATION-CODE
            ' Term date  = ' TERMINATION-DATE
            ' Death Bene = ' DEATH-BENEFIT-M (1)
      MOVE TRUE TO #STAT-NOT-FND
    END-NOREC
  END-FIND
*
  IF #STAT-NOT-FND
    ESCAPE TOP
  END-IF
*
  IF  (DATE-OF-DEATH LT 19930000)
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #CNT
*
  MOVE DATE-OF-DEATH TO #WYYYYMMDD-N
  IF ISS-DT-DD  GT #WDD
    ADD 31 TO #WDD
    SUBTRACT 1 FROM #WMM
  END-IF
*
  IF ISS-DT-MM GT #WMM
    ADD 12 TO #WMM
    SUBTRACT 1 FROM #WYYYY
  END-IF
*
  SUBTRACT ISSUE-DATE FROM #WYYYYMMDD-N
  COMPRESS #WYYYY 'yrs' #WMM 'mons' INTO #LEN-MBRSHIP
*
  IF DATE-OF-DEATH LT 19940000
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ID-NUMBER NAME #LEN-MBRSHIP
                                         DATE-OF-DEATH ISSUE-DATE
  ELSE
    IF DATE-OF-DEATH LT 19950000
      ADD 1 TO #CNT-3
      DISPLAY (3) #CNT-3 ID-NUMBER NAME #LEN-MBRSHIP
                                         DATE-OF-DEATH ISSUE-DATE
    ELSE
      ADD 1 TO #CNT-4
      DISPLAY (4) #CNT-4 ID-NUMBER NAME #LEN-MBRSHIP
                                         DATE-OF-DEATH ISSUE-DATE
    END-IF
  END-IF
END-READ
*
* WRITE (2) 'mbr deaths             ' #CNT
*
END
