* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* ATHPLAPC - create lapse cap download file.
*
* DRW  08182005 DW2  LOAN SENT TO COLLECTION PROCESSING             
* PAM  11102008 PM3  Check issue date also to ensure we have correct
*                    CAP policy                                     
* bz  01-29-2014     redesign output, change data source to contact (OI-2505)
* vxt 03/23/2015 vt1 /Ticket 3418/
* bz-HD-3047 03/22/2017
*********************************************************************************
*
DEFINE DATA
LOCAL USING ADDA0001                                   /* JT1
LOCAL
1 #JCL-PARM       (A10)
1 REDEFINE #JCL-PARM
 2 #JCL-MM           (N2)
 2 #JCL-FILL-1       (A1)
 2 #JCL-DD           (N2)
 2 #JCL-FILL-2       (A1)
 2 #JCL-YYYY         (N4)
*
1 MD-V VIEW OF A-MBRSHIP-MTD-DETAIL
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
   3 ID-NUMBER-A     (A6)
 2 ENTRY-DATE
 2 REDEFINE ENTRY-DATE
  3 ENTRY-DATE-YYYYMM (N6)
  3 ENTRY-DATE-DD     (N2)
 2 CODE
 2 USER
 2 REC-ISN
 2 ISSUE-DATE
 2 NUM-MBRSHIPS
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-TEXT          (A60/60)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 DEBIT-CREDIT-AMT
 2 MEMBER-CONTACT-ID                           /* JT1 end
 2 TERM-LOAN-PRIN
 2 TERM-ACC-INT
 2 X-LOAN-AMOUNT      (9)
 2 ONLINE-ACTION      (9)
 2 FACE-AMOUNT        (9)
 2 ANNUAL-PREM        (9)
 2 PAY-STATUS         (9)
 2 PAID-TO-DATE       (9)
 2 ISSUE-DATE            (9)
 2 CONV-DURATION        (9)
*
1 CNT-V VIEW OF A-CONTACTS
   2  FIRST-NAME      
   2  MIDDLE-NAME     
   2  LAST-NAME
   2  SUFFIX       
   2  DATE-OF-BIRTH  
   2  SSN
*
1 #ISSUE-DATE    (N8)
1 REDEFINE #ISSUE-DATE
 2 #ISSUE-DATE-YYYY (N4)
 2 #ISSUE-DATE-MM  (N2)
 2 #ISSUE-DATE-DD  (N2)
*
1 #PAID-TO-DATE  (N8)
1 #FACE-AMOUNT      (N6.2)
1 #SSN-DISP    (A11)
1 #LOAN-PRIN-DISP (A12)                     /* vt1 was a9
1 #ACC-INT-DISP   (A8)
1 #TOT-LOAN-INT   (N6.2)
1 #TOT-LOAN-INT-DISP (A9)
1 #FACE-AMOUNT-DISP (A10)
*
1 #ZIP-CODE    (N9)
1 REDEFINE #ZIP-CODE
 2 #ZIP-CODE-5-A  (A5)
 2 #ZIP-CODE-4-A  (A4)
1 REDEFINE #ZIP-CODE
 2 #ZIP-CODE-5    (N5)
 2 #ZIP-CODE-4    (N4)
1 #FIRST-ADDRESS  (A25)
1 #SECOND-ADDRESS (A40)
1 #THIRD-ADDRESS  (A40)
*
1 #NUM-RECS          (P6)
1 #FOUND-LAPC   (L)

1 #COUNTERS
 2 #LAPC-CNT         (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
*
1 #INVERSE-ENTRY-DATE  (N8)
1 REDEFINE #INVERSE-ENTRY-DATE
 2 #INVERSE-ENTRY-DATE-YYYYMM    (N6)
 2 #INVERSE-ENTRY-DATE-DD        (N2)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #CENT-A        (A2)
  2 #YY-A          (A2)
  2 #MM-A          (A2)
  2 #DD-A          (A2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM        (N6)
  2 REDEFINE #YYYYMM
   3 #YYYY         (N4)
   3 #MM           (N2)
  2 #DD            (N2)
*
1 #BANKRUPT-IND        (A8)
1 #NOT-FND             (L)
*
1 v_PH_Home            (A) DYNAMIC 
1 v_PH_Work            (A) DYNAMIC
1 v_PH_Cell            (A) DYNAMIC  
1 v_A                  (A) DYNAMIC
*
1 dm                   (A1) INIT <H'09'>
1 A1000                (A1000)  
1 PH-V VIEW OF A-CONTACT-PHONE
  2 CONTACT-ID (N8)
  2 PHONE-TYPE-CODE (A1)
  2 TYPE (A1)
  2 INTL-ACCESS-CODE (N3)
  2 INTL-DIAL-NUMBER (N20)
  2 AREA-CODE (N3)
  2 DIAL-NUMBER (N7)
  2 REDEFINE DIAL-NUMBER
    3 DIAL-NUMBER-3   (A3)  /* bz1 
    3 DIAL-NUMBER-4   (A4)  /* bz1
  2 EXT (N6)
1 A10    (A10)
*  
  1 f_1     (A80) INIT <"/usr/SAG/AAFMAA/batch/workfiles/WORK/Lapc_1.txt"> 
  1 f_2     (A80) INIT <"/usr/SAG/AAFMAA/batch/workfiles/WORK/Lapc_2.txt"> 
  1 v_PN            (I1)
  1 v_RC            (N3)
END-DEFINE
 IF *DEVICE <> 'BATCH'
   v_PN := 1 v_RC := PRT_FN_PUT(<v_PN, f_1>)                     
   v_PN := 2 v_RC := PRT_FN_PUT(<v_PN, f_2>)                     
   DEFINE WORK FILE 1 '/usr/SAG/AAFMAA/batch/workfiles/ATHLB/PARMRUND.TXT'
   DEFINE WORK FILE 2 '/usr/SAG/AAFMAA/batch/workfiles/WORK/LAPC.TXT' TYPE 'ASCII-COMPRESSED'
 ELSE
   DEFINE WORK FILE 2 TYPE 'ASCII-COMPRESSED'
 END-IF
COMPRESS 
    'Account/CN#' 
  dm  'First Name'
  dm  'Middle Name'            /* bz-HD-3047               
  dm  'Last Name'
  dm  'Suffix'                 /* bz-HD-3047 
  dm  'DOB'
  dm  'SSN'
  dm  'Address 1'
  dm  'Address 2 (Apartment#, Lot#, ect)'
  dm  'City'
  dm  'St'
  dm  'Zip Code'
  dm  'Mail Return Y =Yes or N=No'
  dm  'Home Phone'
  dm  'Cell Phone'
  dm  'Work Phone'
  dm  'Principal'
  dm  'Pre Accrued Interest'
  dm  'Date of Service and/or Last charge date "lapsed/Resign Date"' 
  dm  'Date of Delinquency and/or Lapse/Resign Date'
  dm  'Date of Last Payment including Insurance'
  dm  'Date Policy was issued/comments'
  dm  'Debt Description Face Value of Ins:'
  dm  'Bankrupt ind.'
    INTO A1000 
WRITE WORK FILE 2 A1000    

FORMAT  LS=133 PS=76
WRITE    *PROGRAM 'v.1.1' 5X 'Exceptions' 5X *DAT4U 5X *TIMX *DEVICE 
FORMAT (2) LS=133 PS=76
WRITE(2) *PROGRAM 'v.1.1' 5X 'LAP"s this month' 5X *DAT4U 5X *TIMX *DEVICE
*
READ WORK 1 ONCE RECORD #JCL-PARM
WRITE 'jcl-parm = ' #JCL-PARM
*
MOVE #JCL-MM TO #MM
MOVE #JCL-DD TO #DD
MOVE #JCL-YYYY TO #YYYY
SUBTRACT 1 FROM #MM
IF #MM LT 1
  ADD 12 TO #MM
  SUBTRACT 1 FROM #YYYY
END-IF
*
READ MD-V BY ENTRY-DATE FROM 5000000
  COMPUTE #INVERSE-ENTRY-DATE = 99999999 - ENTRY-DATE
  IF  (#INVERSE-ENTRY-DATE-YYYYMM = #YYYYMM)
   AND (CODE = 'V')   /* lapc
    RESET #FOUND-LAPC
    RESET #NOT-FND
    FIND ST-V WITH MEMBER-KEY = MD-V.ID-NUMBER-A
      IF NO RECORDS FOUND
        FIND ST-V WITH WIDOW-KEY = MD-V.ID-NUMBER-A
          IF NO RECORDS FOUND
            MOVE TRUE TO #NOT-FND
            WRITE 'ST NOT FOUND USING MD ID-NUMBER'  MD-V.ID-NUMBER
            WRITE   'MD CODE=' MD-V.CODE
            WRITE   'MD USER=' MD-V.USER
            WRITE   'MD ENTRY DATE=' MD-V.ENTRY-DATE
            WRITE   'MD REC-ISN=' MD-V.REC-ISN
          END-NOREC
        END-FIND
      END-NOREC
    END-FIND
    FIND (1) CNT-V WITH CNT-V.ID-NUMBER = ST-V.ID-NUMBER
      IF NO RECORD FOUND 
         WRITE 'Cn Contact data found for' ST-V.ID-NUMBER   
         #NOT-FND := TRUE
      END-NOREC 
*       COMPRESS FIRST-NAME MIDDLE-NAME INTO FIRST-NAME
    END-FIND
*
    IF #NOT-FND
      ESCAPE TOP
    END-IF
*
    PERFORM PROCESS-INFO
  END-IF
END-READ
*
WRITE (2) 'NUMBER RECORDS  = ' #NUM-RECS
WRITE (2) 'Num LAPC"s      = ' #LAPC-CNT
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-INFO
* * * * * * * * * * * * * * * *
FOR #I1 1 TO 9
  IF  ((ONLINE-ACTION (#I1) = 'S')     /* lapc
   OR (CONV-DURATION (#I1) = 999        /* Loan must have been     DW2
     AND X-LOAN-AMOUNT (#I1) = 0))      /* sent to Collections
*
    IF MD-V.ISSUE-DATE NE ST-V.ISSUE-DATE (#I1)                 /* PM3
      ESCAPE TOP                                                /* PM3
    END-IF                                                      /* PM3
    MOVE TRUE TO #FOUND-LAPC
    MOVE ST-V.ISSUE-DATE   (#I1) TO #ISSUE-DATE
    MOVE PAID-TO-DATE (#I1)      TO #PAID-TO-DATE
    MOVE FACE-AMOUNT  (#I1)      TO #FACE-AMOUNT
*
    RESET #BANKRUPT-IND
    FIND (1) NT-V WITH ID-NUMBER = ST-V.ID-NUMBER
      FOR #I2 1 TO 60
        IF NOTE-TEXT (#I2) = MASK ('*Mbr in bnkrptcy')
          MOVE 'Bankrupt' TO #BANKRUPT-IND
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-FIND
*
    PERFORM sbr_Build_Output
    WRITE WORK FILE 2 A1000
    ADD 1 TO #LAPC-CNT
    ADD 1 TO #CNT-2
    DISPLAY (2) #CNT-2 ST-V.ID-NUMBER #ISSUE-DATE
    ESCAPE BOTTOM                                              /* PM3
  END-IF
END-FOR
*
END-SUBROUTINE
*
* ------------------------------------------------------------------
DEFINE SUBROUTINE sbr_Build_Output
* * * * * * * * * * * * * * * *
*
*
  RESET #PDA-ADDRESS                                                  /* JT1 start 
  MOVE 'P' TO #ADD-TYPE                                   
  #ADD-CONTACT-ID := ST-V.MEMBER-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS  
  MOVE #ADD-ZIP-CODE  TO #ZIP-CODE     
*
IF #ZIP-CODE-4 <> 0
  COMPRESS  #ZIP-CODE-5-A '-' #ZIP-CODE-4-A  INTO A10 LEAVING NO
ELSE
  A10 := #ZIP-CODE-5-A
END-IF

IF #ADD-RETURNED-MAIL <> 'Y' #ADD-RETURNED-MAIL := ' ' END-IF

MOVE EDITED CNT-V.SSN (EM=999-99-9999) TO #SSN-DISP
MOVE EDITED ST-V.TERM-LOAN-PRIN (EM=Z,ZZZ,ZZ9.99) TO #LOAN-PRIN-DISP                 /* vt1 was (EM=ZZ,ZZ9.99)
MOVE LEFT   #LOAN-PRIN-DISP TO #LOAN-PRIN-DISP
MOVE EDITED ST-V.TERM-ACC-INT (EM=Z,ZZ9.99) TO #ACC-INT-DISP
MOVE LEFT   #ACC-INT-DISP TO #ACC-INT-DISP
* COMPUTE     #TOT-LOAN-INT = ST-V.TERM-LOAN-PRIN + ST-V.TERM-ACC-INT
MOVE EDITED #TOT-LOAN-INT (EM=ZZ,ZZ9.99) TO #TOT-LOAN-INT-DISP
MOVE LEFT   #TOT-LOAN-INT-DISP TO #TOT-LOAN-INT-DISP
MOVE EDITED #FACE-AMOUNT (EM=ZZZ,ZZ9.99) TO #FACE-AMOUNT-DISP
MOVE LEFT   #FACE-AMOUNT-DISP TO #FACE-AMOUNT-DISP
*
PERFORM sbr_Get_CHB_Phones
COMPRESS
/* '^' IS THE LOWER CASE OF THE KEY TO THE RIGHT OF 'P'
/* '' IS THE upper CASE OF THE '6' (cris calls this a carrot)
       ST-V.ID-NUMBER                        /*  1
    dm   FIRST-NAME                          /*  2 
    dm   MIDDLE-NAME                         /*  3  /* bz-HD-3047
    dm   LAST-NAME                           /*  4
    dm   SUFFIX                              /*  5  /* bz-HD-3047
    dm   DATE-OF-BIRTH                       /*  6
    dm   #SSN-DISP                           /*  7
    dm   #ADD-STREET                         /*  8 
    dm   #ADD-UNIT                           /*  9 
    dm   #ADD-CITY                           /* 10
    dm   #ADD-STATE                          /* 11 
    dm   A10                                 /* 12
    dm   #ADD-RETURNED-MAIL                  /* 13 
    dm   v_PH_Home                           /* 14 
    dm   v_PH_Cell                           /* 15
    dm   v_PH_Work                           /* 16
    dm   #LOAN-PRIN-DISP                     /* 17
    dm   #ACC-INT-DISP                       /* 18
    dm   #INVERSE-ENTRY-DATE                 /* 19
    dm    #INVERSE-ENTRY-DATE                /* 20
    dm   #PAID-TO-DATE                       /* 21  
    dm   #ISSUE-DATE                         /* 22
    dm   #FACE-AMOUNT-DISP                   /* 23
    dm   #BANKRUPT-IND                       /* 24
                    INTO A1000
*
* ----------------------------------------------------------------------
END-SUBROUTINE

DEFINE sbr_Get_CHB_Phones
     v_PH_Cell := ' ' v_PH_Home := ' ' v_PH_Work := ' ' v_A := ' '
     FIND PH-V WITH PH-V.CONTACT-ID = MEMBER-CONTACT-ID
          WHERE PHONE-TYPE-CODE = 'H' OR = 'B' OR = 'C'
        IF NO RECORD FOUND ESCAPE BOTTOM END-NOREC
          IF PH-V.TYPE = 'D'      /* domestic phones
             COMPRESS '(' PH-V.AREA-CODE ')' PH-V.DIAL-NUMBER-3 '-' PH-V.DIAL-NUMBER-4 INTO v_A
             LEAVING NO SPACE
             IF EXT <> 0 
                COMPRESS v_A 'Ext' PH-V.EXT INTO  v_A
             END-IF
         END-IF
         IF PH-V.TYPE = 'I'        /* international
             COMPRESS '(' PH-V.INTL-ACCESS-CODE ')' PH-V.INTL-DIAL-NUMBER INTO v_A
         END-IF
      v_A := *TRIM(v_A) 
      DECIDE ON FIRST VALUE PHONE-TYPE-CODE 
      VALUE 'H'
            v_PH_Home := v_A 
      VALUE 'B'
            v_PH_Work := v_A 
      VALUE 'C'
            v_PH_Cell := v_A 
      NONE  IGNORE      
      END-DECIDE
    END-FIND
END-SUBROUTINE
END
