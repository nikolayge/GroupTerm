* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: DNXVN002
* DESCRIPTION: Assemble VA Information
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  12-10-2010  RE1  Creation
**************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 STATUS-V VIEW OF A-STATUS
  2 VA-DISABILITY-PCT         (P1.2)
  2 VA-DISABLE-DATE           (N8)
  2 REDEFINE VA-DISABLE-DATE
    3 #DIS-DATE-YY (A4)
    3 #DIS-DATE-MM (A2)
    3 #DIS-DATE-DD (A2)
*  2 VA-CLAIM-NUMBER            (A12)
  2 MIL-SBP-OPT               (A1)
  2 MIL-SBP-AMT               (P5)
*
1 #DIS       (A10) CONST <'VA-Dis %:'>
1 #DIS-DATE  (A12) CONST <'VA-Dis-Date:'>
1 #SBP       (A8)  CONST <'SBP-Opt:'>
1 #BASE-AMT  (A12) CONST <'SBP-Amount:'>
1 #NODATA    (A1)  CONST <'?'>
1 BLANK      (A1)  CONST <' '>
1 #DATE      (A10)
END-DEFINE
*
RESET #TEXT (*)
*
FIND STATUS-V WITH MEMBER-CONTACT-ID = #CONTACT-ID
  DECIDE FOR EVERY CONDITION
    WHEN MIL-SBP-OPT EQ BLANK
      COMPRESS #TEXT (1) #SBP #NODATA INTO #TEXT (1)
    WHEN MIL-SBP-OPT NE BLANK
      COMPRESS #TEXT (1) #SBP MIL-SBP-OPT INTO #TEXT (1)
    WHEN MIL-SBP-AMT EQ 0
      COMPRESS #TEXT (1) #BASE-AMT #NODATA INTO #TEXT (1)
    WHEN MIL-SBP-AMT NE 0
      COMPRESS #TEXT (1) #BASE-AMT MIL-SBP-AMT INTO #TEXT (1)
    WHEN VA-DISABLE-DATE = 0
      COMPRESS #TEXT (1) #DIS-DATE #NODATA INTO #TEXT (1)
    WHEN VA-DISABLE-DATE NE 0
      COMPRESS #DIS-DATE-MM '/' #DIS-DATE-DD '/' #DIS-DATE-YY INTO #DATE LEAVING NO
      COMPRESS #TEXT (1) #DIS-DATE #DATE INTO #TEXT (1)
    WHEN VA-DISABILITY-PCT = 0
      COMPRESS #TEXT (1) #DIS #NODATA INTO #TEXT (1)
    WHEN VA-DISABILITY-PCT NE 0
      COMPRESS #TEXT (1) #DIS VA-DISABILITY-PCT INTO #TEXT (1)
    WHEN NONE IGNORE
  END-DECIDE
END-FIND
*
ON ERROR
  COMPRESS 'Application error' *ERROR-NR *ERROR-LINE *PROGRAM
    INTO #TEXT  (1)
  ESCAPE ROUTINE
END-ERROR

END
