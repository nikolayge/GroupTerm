* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNASG1.
/**
/** :author nguentchev
/* TODO: Enter your code here
DEFINE DATA
PARAMETER
1 #CONTACT-ID          (N8)
1 #ADDRESS-POOL-ID     (N8)
1 #XML-OUTPUT          (A) DYNAMIC
LOCAL USING AUCAAR01
LOCAL USING AUCAAD01
LOCAL USING AUCACR01
LOCAL
1 #XML-TEMP            (A) DYNAMIC
1 #GET                 (I4) CONST <0>
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID (N8)
  2 ADDRESS-POOL-ID (N8)
  2 STATUS (A1)
1 CONTACT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID (N8)
  2 ROLE-1-CD    (A1)
  2 CONTACT-2-ID (N8)
  2 ROLE-2-CD    (A1)
*
END-DEFINE
*
#XML-OUTPUT := ' '
COMPRESS #XML-OUTPUT '<?xml version="1.0" encoding="' *CODEPAGE '"?>'
  INTO #XML-OUTPUT LEAVING NO
COMPRESS FULL #XML-OUTPUT  '<AddressShare'
  '>' INTO #XML-OUTPUT LEAVING NO
PERFORM ADDRESS-XML
COMPRESS #XML-OUTPUT '<AddressContacts>' INTO #XML-OUTPUT
PERFORM ADDR-CONTACT-RELATIONS-XML
COMPRESS FULL #XML-OUTPUT  '</AddressContacts'
  '>' INTO #XML-OUTPUT LEAVING NO
COMPRESS FULL #XML-OUTPUT  '</AddressShare'
  '>' INTO #XML-OUTPUT LEAVING NO
*
DEFINE ADDR-CONTACT-RELATIONS-XML
/*     ==========================
FIND CONTACT-REL-V WITH CONTACT-1-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  COMPRESS #XML-OUTPUT '<AddressContact'
    '>' INTO #XML-OUTPUT LEAVING NO
  COMPRESS #XML-OUTPUT '<AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
  RESET AddressRelation
  AddressRelation.AddressPoolID := #ADDRESS-POOL-ID
  AddressRelation.ContactID     := CONTACT-REL-V.CONTACT-2-ID
  FIND ADDR-REL-V WITH ADDRESS-POOL-ID = #ADDRESS-POOL-ID
      AND CONTACT-ID = CONTACT-REL-V.CONTACT-2-ID
    ACCEPT IF ADDR-REL-V.STATUS NE 'D'
    AddressRelation.RelationID    := *ISN (0059)
    AddressRelation.ActionType    := #GET
    CALLNAT 'AUCNARM1' AddressRelation
    ESCAPE BOTTOM
  END-FIND
  CALLNAT 'AUCNARS1' #XML-TEMP AddressRelation
  COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
  COMPRESS #XML-OUTPUT  '</AddressRelation'
    '>' INTO #XML-OUTPUT LEAVING NO
  RESET ContactRelation
  IF ROLE-1-CD = 'S' OR= 'X'
    ContactRelation.RelationType  := 'M'  /* Dependents = Right Contact ID
  ELSE
    ContactRelation.RelationType  := 'D'  /* Dependents = Right Contact ID
  END-IF
  ContactRelation.RelationID    := *ISN (0048)
  ContactRelation.ActionType    := #GET
  CALLNAT 'AUCNCRM1' ContactRelation 
  CALLNAT 'AUCNCRS1' #XML-TEMP ContactRelation
  COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
  COMPRESS FULL #XML-OUTPUT  '</AddressContact'
    '>' INTO #XML-OUTPUT LEAVING NO
END-FIND
END-SUBROUTINE
*
DEFINE ADDRESS-XML
/*     ===========
RESET Address
Address.AddressPoolID := #ADDRESS-POOL-ID
Address.ContactID     := #CONTACT-ID
Address.ActionType    := #GET
CALLNAT 'AUCNADM1' Address
RESET #XML-TEMP
CALLNAT 'AUCNADS1' #XML-TEMP Address
COMPRESS #XML-OUTPUT #XML-TEMP INTO #XML-OUTPUT
END-SUBROUTINE
*
END
