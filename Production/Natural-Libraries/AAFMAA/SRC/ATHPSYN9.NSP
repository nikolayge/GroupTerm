* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPSYN9 - Compare 2 sync files
*************************************************************
* NAME  DATE    TAG   DESCRIPTION
* SAG 20060803 SAG-AB EBCDIC hex 05 changed to ASCII hex 09
*
*************************************************************
*
DEFINE DATA
LOCAL
* below for party file
1 #PARTY-FILE     /* 525
 2 #PAR-75-1   (A75)
 2 REDEFINE #PAR-75-1
  3 #PAR-75-1-X  (A1/75)
 2 #PAR-75-2   (A75)
 2 #PAR-75-3   (A75)
 2 #PAR-75-4   (A75)
 2 #PAR-75-5   (A75)
 2 #PAR-75-6   (A75)
 2 #PAR-75-7   (A75)
1 #PARTY-FILE-B
 2 #PAR-75-1-B   (A75)
 2 REDEFINE #PAR-75-1-B
  3 #PAR-75-1-B-X  (A1/75)
 2 #PAR-75-2-B   (A75)
 2 #PAR-75-3-B   (A75)
 2 #PAR-75-4-B   (A75)
 2 #PAR-75-5-B   (A75)
 2 #PAR-75-6-B   (A75)
 2 #PAR-75-7-B   (A75)
*
* below for policy-file
1 #POLICY-FILE   /* 375
 2 #POL-75-1  (A75)   /* Only 52 is needed
 2 REDEFINE #POL-75-1
  3 #POL-75-1-X  (A1/75)
 2 #POL-75-2  (A75)
 2 #POL-75-3  (A75)
 2 #POL-75-4  (A75)
 2 #POL-75-5  (A75)
1 #POLICY-FILE-B
 2 #POL-75-1-B  (A75)
 2 REDEFINE #POL-75-1-B
  3 #POL-75-1-B-X  (A1/75)
 2 #POL-75-2-B  (A75)
 2 #POL-75-3-B  (A75)
 2 #POL-75-4-B  (A75)
 2 #POL-75-5-B  (A75)
*
* below for relations-file    /* 150
1 #RELATIONS-FILE       /* 150
 2 #REL-75-1    (A75)
 2 REDEFINE #REL-75-1
  3 #REL-75-1-X  (A1/75)
 2 #REL-75-2    (A75)
1 #RELATIONS-FILE-B   /* 150
 2 #REL-75-1-B  (A75)
 2 REDEFINE #REL-75-1-B
  3 #REL-75-1-B-X (A1/75)
 2 #REL-75-2-B  (A75)
*
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2)
*
**1 #TAB       (A1) INIT <H'05'>   /* SAG-AB 20060803
1 #TAB       (A1) INIT <H'09'>     /* SAG-AB 20060803
1 #BYP-1   (L)
1 #BYP-B   (L)
*
1 #CN-1    (N6)
1 REDEFINE #CN-1
 2 #CN-1-X (A1/6)
*
1 #CN-1-B    (N6)
1 REDEFINE #CN-1-B
 2 #CN-1-B-X (A1/6)
*
1 #PARTYS-CHECKED (N7)
1 #POLS-CHECKED   (N7)
1 #RELS-CHECKED   (N7)
*
END-DEFINE
*
* * * * * * * REPORT HEADER * * * * * *
FORMAT  LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
              'SYNC Extract Compare Exceptions'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'SYNC Mismatched Records'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'SYNC Mismatched Relation'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (4) LS=133 PS=76
WRITE (4) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'SYNC Mismatched policy'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (5) LS=133 PS=76
WRITE (5) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X 'Sync NOT Found on Extract'
               5X 'DATE: '*DAT4U 5X 'TIME: ' *TIMX
*
WRITE (2) '****** Compare RELATION Files *************'
*
READ WORK 6 RECORD #RELATIONS-FILE
* ESCAPE BOTTOM      /* <<<<<<<<<<<<<<<<<<<<<<<<<<<
  ADD 1 TO #RELS-CHECKED
  RESET #CN-1
  FOR #I1 1 TO 20    /* Parse out Cn Number from Relation File 1
    IF #REL-75-1-X (#I1) = '-'
      SUBTRACT 1 FROM #I1
      MOVE 7 TO #I3
      FOR #I2 #I1 TO 1 STEP -1
        SUBTRACT 1 FROM #I3
        MOVE #REL-75-1-X (#I2) TO #CN-1-X (#I3)
      END-FOR
      ESCAPE BOTTOM
    END-IF
  END-FOR
* WRITE 'just read rel-1'  #CN-1
*
  IF #BYP-B
*   WRITE 'Just bypassed read-work-7'
    IGNORE
  ELSE
    PERFORM READ-WORK-7
  END-IF
*
  PERFORM CHECK-REL-FILES
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-REL-FILES
* * * * * * * * * * * * * * * * *
* ITE 'COMPARE REL FLs' #REL-75-1 (EM=XXXXXXXX) #REL-75-1-B (EM=XXXXXXX)
IF  (#REL-75-1 NE #REL-75-1-B)
  IF #CN-1 NE #CN-1-B
    IF #CN-1 LT #CN-1-B
      WRITE (5) 'REL Missing from File B' #REL-75-1
    ELSE
      WRITE (5) 'REL Missing from File A' #REL-75-1-B
    END-IF
  ELSE
    WRITE (2) 'Mismatched Relation file'
    WRITE (2) #REL-75-1
    WRITE (2) #REL-75-1-B
  END-IF
END-IF
*
RESET #BYP-1 #BYP-B
IF #CN-1 LT #CN-1-B
  MOVE TRUE TO #BYP-B
* WRITE 'CN-1 LT CN-1-b' #CN-1 #CN-1-B
END-IF
*
IF #CN-1 GT #CN-1-B
  MOVE TRUE TO #BYP-1
* WRITE 'CN-1 GT CN-1-b' #CN-1 #CN-1-B
END-IF
*
END-SUBROUTINE
*
  REPEAT UNTIL NOT #BYP-1
*   WRITE 'REPEAT - READ extra File 7' #CN-1 #CN-1-B
*
    PERFORM READ-WORK-7
*
    PERFORM CHECK-REL-FILES
  END-REPEAT
END-WORK
*
WRITE (2) #RELS-CHECKED   'Relations Records Checked'
*
WRITE (2) '****** Compare POLICY Files *************'
*
* STOP
RESET #BYP-1 #BYP-B
READ WORK 4 RECORD #POLICY-FILE
* ESCAPE BOTTOM      /* <<<<<<<<<<<<<<<<<<<<<<<<<<<
  ADD 1 TO #POLS-CHECKED
  RESET #CN-1
  FOR #I1 1 TO 40    /* Parse out Cn Number from POLICY File 1
    IF #POL-75-1-X (#I1) NE #TAB
      ESCAPE TOP
    END-IF
*     Found 1st tab
    ADD 1 TO #I1
    FOR #I1 #I1 TO 20
      IF #POL-75-1-X (#I1) NE #TAB
        ESCAPE TOP
      END-IF
*
      SUBTRACT 1 FROM #I1
      MOVE 7 TO #I3
      FOR #I2 #I1 TO 1 STEP -1
        IF #POL-75-1-X (#I2) = #TAB
          ESCAPE BOTTOM
        END-IF
*
        SUBTRACT 1 FROM #I3
        MOVE #POL-75-1-X (#I2) TO #CN-1-X (#I3)
      END-FOR
      ESCAPE BOTTOM
    END-FOR
    ESCAPE BOTTOM
  END-FOR
* WRITE 'just read pol-1'  #CN-1
*
  IF #BYP-B
*   WRITE 'Just bypassed read-work-5'
    IGNORE
  ELSE
    PERFORM READ-WORK-5
  END-IF
*
  PERFORM CHECK-POL-FILES
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-POL-FILES
* * * * * * * * * * * * * * * * *
* ITE 'COMPARE POL FLs' #POL-75-1 (EM=XXXXXXXX) #POL-75-1-B (EM=XXXXXXX)
IF  (#POL-75-1 NE #POL-75-1-B)
  IF #CN-1 NE #CN-1-B
    IF #CN-1 LT #CN-1-B
      WRITE (5) 'POL Missing from File B' #POL-75-1
    ELSE
      WRITE (5) 'POL Missing from File A' #POL-75-1-B
    END-IF
  ELSE
    WRITE (2) 'Mismatched POLICY file'
    WRITE (2) #POL-75-1
    WRITE (2) #POL-75-1-B
  END-IF
END-IF
*
RESET #BYP-1 #BYP-B
IF #CN-1 LT #CN-1-B
  MOVE TRUE TO #BYP-B
* WRITE 'CN-1 LT CN-1-b' #CN-1 #CN-1-B
END-IF
*
IF #CN-1 GT #CN-1-B
  MOVE TRUE TO #BYP-1
* WRITE 'CN-1 GT CN-1-b' #CN-1 #CN-1-B
END-IF
*
END-SUBROUTINE
*
  REPEAT UNTIL NOT #BYP-1
*   WRITE 'REPEAT - READ extra File 5' #CN-1 #CN-1-B
*
    PERFORM READ-WORK-5
*
    PERFORM CHECK-POL-FILES
  END-REPEAT
END-WORK
*
WRITE (2) #POLS-CHECKED   'POLICY Records Checked'
*
WRITE (2) '****** Compare PARTY Files *************'
*
* COMPARE PARTY FILES
*
* STOP
RESET #BYP-1 #BYP-B
READ WORK 2 RECORD #PARTY-FILE
* ESCAPE BOTTOM      /* <<<<<<<<<<<<<<<<<<<<<<<<<<<
  ADD 1 TO #PARTYS-CHECKED
  RESET #CN-1
  FOR #I1 1 TO 7    /* Parse out Cn Number from Policy File 1
    IF #PAR-75-1-X (#I1) = #TAB
      SUBTRACT 1 FROM #I1
      MOVE 7 TO #I3
      FOR #I2 #I1 TO 1 STEP -1
        SUBTRACT 1 FROM #I3
        MOVE #PAR-75-1-X (#I2) TO #CN-1-X (#I3)
      END-FOR
      ESCAPE BOTTOM
    END-IF
  END-FOR
* WRITE 'just read par-1'  #CN-1
*
  IF #BYP-B
*   WRITE 'Just bypassed read-work-2'
    IGNORE
  ELSE
    PERFORM READ-WORK-3
  END-IF
*
  PERFORM CHECK-PAR-FILES
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-PAR-FILES
* * * * * * * * * * * * * * * * *
* ITE 'COMPARE PAR FLs' #PAR-75-1 (EM=XXXXXXXX) #PAR-75-1-B (EM=XXXXXXX)
*
IF  (#PAR-75-1 NE #PAR-75-1-B)
 OR (#PAR-75-2 NE #PAR-75-2-B)
 OR (#PAR-75-3 NE #PAR-75-3-B)
 OR (#PAR-75-4 NE #PAR-75-4-B)
 OR (#PAR-75-5 NE #PAR-75-5-B)
 OR (#PAR-75-6 NE #PAR-75-6-B)
 OR (#PAR-75-7 NE #PAR-75-7-B)
  IF #CN-1 NE #CN-1-B
    IF #CN-1 LT #CN-1-B
      WRITE (5) 'PAR Missing from File B' #PAR-75-1
    ELSE
      WRITE (5) 'PAR Missing from File A' #PAR-75-1-B
    END-IF
  ELSE
    WRITE (2) 'Mismatched Party file'
    WRITE (2) #PAR-75-1
    WRITE (2) #PAR-75-1-B
    WRITE (2) #PAR-75-2
    WRITE (2) #PAR-75-2-B
    WRITE (2) #PAR-75-3
    WRITE (2) #PAR-75-3-B
    WRITE (2) #PAR-75-4
    WRITE (2) #PAR-75-4-B
    WRITE (2) #PAR-75-5
    WRITE (2) #PAR-75-5-B
    WRITE (2) #PAR-75-6
    WRITE (2) #PAR-75-6-B
    WRITE (2) #PAR-75-7
    WRITE (2) #PAR-75-7-B
  END-IF
END-IF
*
RESET #BYP-1 #BYP-B
IF #CN-1 LT #CN-1-B
  MOVE TRUE TO #BYP-B
* WRITE 'CN-1 LT CN-1-b' #CN-1 #CN-1-B
END-IF
*
IF #CN-1 GT #CN-1-B
  MOVE TRUE TO #BYP-1
* WRITE 'CN-1 GT CN-1-b' #CN-1 #CN-1-B
END-IF
*
END-SUBROUTINE
*
  REPEAT UNTIL NOT #BYP-1
*   WRITE 'REPEAT - READ extra File 3' #CN-1 #CN-1-B
*
    PERFORM READ-WORK-3
*
    PERFORM CHECK-PAR-FILES
  END-REPEAT
END-WORK
*
WRITE (2) #PARTYS-CHECKED 'PARTY Records Checked'
*
WRITE (2) '****** PARTY Files Compare Complete  *************'
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-WORK-3
* * * * * * * * * * * * * * *
*
READ WORK 3 ONCE RECORD #PARTY-FILE-B
  AT END OF FILE
    MOVE 999999 TO #CN-1-B
    ESCAPE ROUTINE
  END-ENDFILE
*
RESET #CN-1-B
FOR #I1 1 TO 7    /* Parse out Cn Number from Party File 1-B
  IF #PAR-75-1-B-X (#I1) = #TAB
    SUBTRACT 1 FROM #I1
    MOVE 7 TO #I3
    FOR #I2 #I1 TO 1 STEP -1
      SUBTRACT 1 FROM #I3
      MOVE #PAR-75-1-B-X (#I2) TO #CN-1-B-X (#I3)
    END-FOR
    ESCAPE BOTTOM
  END-IF
END-FOR
* WRITE 'just read par-b'  #CN-1-B
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-WORK-5
* * * * * * * * * * * * * * *
*
READ WORK 5 ONCE RECORD #POLICY-FILE-B
  AT END OF FILE
    MOVE 999999 TO #CN-1-B
    ESCAPE ROUTINE
  END-ENDFILE
*
RESET #CN-1-B
FOR #I1 1 TO 40    /* Parse out Cn Number from POLICY File 1
  IF #POL-75-1-B-X (#I1) NE #TAB
    ESCAPE TOP
  END-IF
*     Found 1st tab
  ADD 1 TO #I1
  FOR #I1 #I1 TO 20
    IF #POL-75-1-B-X (#I1) NE #TAB
      ESCAPE TOP
    END-IF
*
    SUBTRACT 1 FROM #I1
    MOVE 7 TO #I3
    FOR #I2 #I1 TO 1 STEP -1
      IF #POL-75-1-B-X (#I2) = #TAB
        ESCAPE BOTTOM
      END-IF
*
      SUBTRACT 1 FROM #I3
      MOVE #POL-75-1-B-X (#I2) TO #CN-1-B-X (#I3)
    END-FOR
    ESCAPE BOTTOM
  END-FOR
  ESCAPE BOTTOM
END-FOR
* WRITE 'just read pol-b'  #CN-1-B
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE READ-WORK-7
* * * * * * * * * * * * * * *
*
READ WORK 7 ONCE RECORD #RELATIONS-FILE-B
  AT END OF FILE
    MOVE 999999 TO #CN-1-B
    ESCAPE ROUTINE
  END-ENDFILE
*
RESET #CN-1-B
FOR #I1 1 TO 20    /* Parse out Cn Number from Relation File 1-B
  IF #REL-75-1-B-X (#I1) = '-'
    SUBTRACT 1 FROM #I1
    MOVE 7 TO #I3
    FOR #I2 #I1 TO 1 STEP -1
      SUBTRACT 1 FROM #I3
      MOVE #REL-75-1-B-X (#I2) TO #CN-1-B-X (#I3)
    END-FOR
    ESCAPE BOTTOM
  END-IF
END-FOR
* WRITE 'just read rel-b'  #CN-1-B
*
END-SUBROUTINE
*
END
