* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PROGRAM-ID: AOAP005A - AAFMAA MSG/FAS/PUP/CAP/APP/ACC LTRS
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PM2 - 06/2000 - MAKES CHANGES TO PRINT IN DALLAS                     *
* DRW  12/18/2004 DW2  Increase VAR LOAN to handle 999999.00,          *
* PM3  02/10/2005  Increase #LUMP-SUM from 5.2 to 6.2.                 *
* PAM  03112005 PM1  Reset display fields for GRANKSXX                 *
* DRW  04292005 DW2  Add DOB processing to GMAXSXX
* DRW  02222007 DW3  Remove IM 7xxxxx logic
* DRW 10/02/2009 DW4 Process-ind Changes
* vxt 11/26/2010 vt1 change the source of the address fields.
* PAM 04/12/2011 PM5 Fix for form                                      *
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* NG  10/08/2013  ADDRESS NORMALIZATION
* vt  12/04/2013 vt2  0936 error for #nu-ann-prem field
* TMT 07/15/2014 TT1  Contact Normalization DOB and Sex                *
************************************************************************
*
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING GC2V6LXX
LOCAL USING CONTPDA1                         /* TT1
LOCAL USING ADDA0001                         /* vt1 pda for the address
LOCAL
*
1 TERM-V VIEW OF A-TERMINALS
  2 CLERK-ID
  2 SECURITY-CODE
*
1 #FORM-IND          (A3)   /* PM2 ADDED FOR DALLAS PRINT
1 #AFSC-MSG          (L)
1 #COMP-NAME         (A46) /* AAFMAA OR AFSC
1 #CURR-GROWTH-RATE  (P2.3) INIT <8.25>
1 #GROWTH-RATE-DISP  (A5)
1 #GR-RATE-DISP-PER  (A6)
1 #VP-GROWTH-RATE    (P2.3) INIT <7.50>
1 #VP-RATE-DISP      (A4)
1 #YEAR-DISP         (A4)
1 #YEAR-DISP-PER     (A5)
1 #AGE               (N2)
1 #APRM              (N5.2)
1 #ISSUE-DATE        (N8)
1 #SMOKER-FLAG       (A1)
1 #FACE-AMT          (N7.2)
1 #TO-PLAN    (A3)
1 #OUTPUT-CALC-AMT     (P7.2)     /* passed to c1601sco for results
1 #OUTPUT-OLD-PDUP-AMT (P7.2) /* passed to c1601sco for results
1 #PRORATE-LOAN        (L)
1 #RPU-ACV             (P7.2)
1 #NU-ANN-PREM         (N7.2)                 /* vt2 was n6.2
1 #LUMP-SUM            (N6.2)                                     /* PM3
1 #OUTPUT-PREM-DIFF    (N6.2)
1 #TOT-LOAN-AMOUNT     (P6.2)   /* DW2
1 #TOT-CURR-ACCUM-INT  (P5.2)
*
1 #FIRST-TIME (L) INIT <TRUE>
1 #IFL   (I2)
1 #I1    (I2)
1 #TODAY-YYYYMMDD-N  (N8)
1 REDEFINE #TODAY-YYYYMMDD-N
  2 #TODAY-CENT (N2)
  2 #TODAY-YY   (N2)
  2 #TODAY-MM   (N2)
  2 #TODAY-DD   (N2)
*
1 #ACC-BLK2    (A42) INIT <'__________________________________________'>
1 #ACC-BLK3    (A42) INIT <'__________________________________________'>
1 #ACC-REL2    (A32) INIT <'________________________________'>
1 #ACC-REL3    (A32) INIT <'________________________________'>
1 #ACC-DATE    (A22) INIT <'______________________'>
1 #ACC-NAME      (A70)                                        /* vt1 was 42
1 #ACC-REL       (A10)                                        /* vt1 was 32
1 #PARM-INPUT    (A36)
1 #PARM-CHOICE   (A12)
1 #PARM-CLERK    (A3)
1 #SCR-TBL    (A78/40)
1 REDEFINE #SCR-TBL
  2 #SCR-TBL-OCC   (40)
    3 #FST-50       (A50)
    3 #SCR-TITLE    (A28)
1 REDEFINE #SCR-TBL
  2 #SCR-TBL-OCC-1 (40)
    3 #FST-4        (A4)
    3 #SCR-TBL-LST  (A74)
*
1 #ATHPBLTR-PARM         (A27)
1 REDEFINE #ATHPBLTR-PARM
  2 #ATHPBLTR-PARM-TYP (A3)
  2 #ATHPBLTR-PARM-CN  (N6)
*
* * * *   N O T E   F I L E   * * * * * * *
1 #NOTE-REC-NOT-FOUND (L)
1 #NOTE-ISN           (P8)
1 NT-V VIEW OF A-NOTES
  2 ID-NUMBER            (N6)
  2 NOTE-TEXT         (A60/8)
*
* * * *   M A N U A L   R E Q U E S T   F I L E   * * * * * * *
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
    3 FIR-IND       (N1)
    3 MSG-IND       (N1)
    3 RET-IND       (N1)
    3 FIN-IND       (N1)
    3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
    3 BEN-IND       (A1)
    3 MAL-IND       (A1)
    3 BEN-SHO-IND   (A1)
    3 ACC-IND       (A1)
    3 CAP-IND       (A1)
    3 APP-IND       (A1)
    3 PIL-IND       (A1)
    3 CO-IND        (A1)
    3 VP-IND        (A1)
    3 MS-IND        (A1)
  2 MESSAGE        (A60/20)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REDEFINE REQUEST-MM
      4 REQUEST-MM-N (N2)
    3 REQUEST-DD   (A2)
  2 INFO          (A50)
  2 CO-RQSTS      (A3/10)
  2 ASSUM-RET-DATE
  2 ASSUM-RET-DATE-2
  2 ASSUM-RET-DATE-3
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 CI-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-CONTACT-ID                                            /* vt1
  2 WIDOW-FLAG     (A1)
  2 PROCESS-IND
  2 CHIEF-OF-STAFF-IND(A1)
  2 INTRO-FLAG        (A1)
  2 RESTRICT-MBR-SW   (A1)
  2 SUSPEND-CODE      (A1)
  2 STATUS-CODE-SP   (A1)
  2 SSN              (N9)
  2 REDEFINE SSN
    3 SSN-1          (N3)
    3 SSN-2          (N2)
    3 SSN-3          (N4)
*   2 SEX              (A1)
*   2 DATE-OF-BIRTH    (N8)
  2 RANK             (A3)
  2 MIL-SBP-OPT      (A1)
  2 MILITARY-STATUS  (A1)
  2 MILITARY-SERVICE (A3)
  2 VA-DISABILITY-PCT (P1.2)
  2 DATE-RETIRED     (N8)
  2 NAME
  2 NAME-SP
  2 TITLE-SP
*  2 ADDRESS-CHG-CODE (A1)        /* NG1 COMMENTED OUT
*  2 ADDRESS-1      (A25)                                /* vt1 \/
*  2 ADDRESS-2      (A25)
*  2 ADDRESS-3      (A25)                                /* vt1 /\
*
*   2 ZIP-CODE-DPB    (N2)
*   2 REDEFINE ZIP-CODE-DPB
*    3 ZIP-CODE-DPB-A (A2)
*   2 REDEFINE ZIP-CODE-DPB
*    3 ZIP-CODE-DPB-TBL (N1/2)
*   2 ZIP-CODE         (N9)
*   2 REDEFINE ZIP-CODE
*     3 ZIP-CODE-1-5   (N5)
  2 NUMBER-MEMBERSHIPS
  2 PLAN             (9)
  2 STATUS           (9)
  2 ONLINE-ACTION    (9)
  2 ISSUE-AGE        (9)
  2 REDEFINE ISSUE-AGE
    3 ISSUE-AGE-OCC   (9)
      4 ISSUE-AGE-N  (N2)
  2 ISSUE-DATE       (9)
  2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-OCC  (9)
      4 ISSUE-DATE-CENT   (N2)
      4 ISSUE-DATE-YYMMDD (N6)
  2 PAID-TO-DATE     (9)
  2 MODE             (9)
  2 SMOKER-FLAG      (9)
  2 CONVERSION-FLAG  (9)
  2 PAY-STATUS       (9)
  2 FACE-AMOUNT      (9)
  2 CASH-VALUE       (9)
  2 ACC-CASH-VALUE   (9)
  2 NUM-MBRSHIPS     (9)
  2 DURATION         (9)
  2 EXPIRY-DAY       (9)
  2 MIN-CASH-VALUE   (9)
  2 DEATH-BENEFIT    (9)
  2 ANNUAL-PREM      (9)
  2 LAST-DURATION    (9)
  2 MONTH-IND        (9)
  2 NXT-YR-MIN-CASH-VALUE (9)
  2 NXT-MON-CASH-VALUE    (9)
  2 NXT-MON-DEATH-BENEFIT (9)
  2 CONV-DURATION         (9)
  2 MONTH-ALLOC-AMT       (9)
  2 PREM-DEFICIENCY       (9)
  2 BEG-CASH-VALUE        (9)
  2 NET-PR-RATIO          (9)
*
* 1 IM-V VIEW OF A-INTRO-MEMBER     /* DW3
*   2 ID-NUMBER      (N6)
*   2 REDEFINE ID-NUMBER
*     3 ID-NBR       (A6)
*   2 SEX              (A1)
*   2 RANK             (A3)
*   2 NAME
*   2 ADDRESS-1      (A25)
*   2 ADDRESS-2      (A25)
*   2 ADDRESS-3      (A25)
*
*   2 ZIP-CODE         (N9)
*
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 INTRO-FLAG
  2 CONTACT-ID       (N8)  /* NG1  START
  2 SEX
  2 RANK             (A3)
  2 NAME
*   2 ADDRESS-1      (A25)
*   2 ADDRESS-2      (A25)
*   2 ADDRESS-3      (A25)
  2 REFERING-ID-NUMBER (N6/2)
  2 REDEFINE REFERING-ID-NUMBER
    3 REF-ID-A        (A6/2)
*  2 ZIP-CODE         (N9)            /* NG1 END
* * * *   REPORT FIELDS           * * * * * * * * * * * * * * *
1 #REPORT-FIELDS   (3)
  2 #RPT-CN          (A8)
  2 #RPT-NAME        (A20)
  2 #RPT-CLERK       (A3)
  2 #RPT-TYPE        (A5)
  2 #RPT-DATE        (A5)
*
1 #AGE-59-1-2 (N8)
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (A2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (A2)
    3 REDEFINE #MM
      4 #MM-N  (N2)
    3 #DD     (A2)
*
1 #COUNTERS
  2 #FAS-RQST-CNT   (P5)
  2 #IFR-RQST-CNT   (P5)
  2 #MSG-RQST-CNT   (P5)
  2 #MSG-RQST-CNT-TRI (P5)
  2 #BEN-RQST-CNT   (P5)
  2 #ACC-RQST-CNT   (P5)
  2 #CAP-RQST-CNT   (P5)
  2 #APP-RQST-CNT   (P5)
  2 #PIL-RQST-CNT   (P5)
  2 #MAL-RQST-CNT   (P5)
  2 #CO-RQST-CNT    (P5)
  2 #VP-RQST-CNT    (P5)
  2 #MS-RQST-CNT    (P5)
*
  2 #MEMBER-CNT     (P5)
  2 #LOOP-CNT       (I2)
*
1 #FST-BEN  (L) INIT <TRUE>
1 #TAMRA            (L)
1 #PAY-UP           (L)
1 #BYPASS           (L)
1 #I2               (I2)
1 #ISN              (P8)
*
1 #MIL-STATUS     (A1)
1 #MIL-SERVICE    (A3)
1 #EXT-FORMATED-NAME  (A70)                               /* vt1 was #formated-name (a45)
1 #FORMATED-LAST-NAME (A30)
1 #FIRST-ADDRESS  (A70)                                   /* vt1 was 25
1 #SECOND-ADDRESS (A70)                                   /* vt1 was 40
1 #THIRD-ADDRESS  (A70)                                   /* vt1 was 40
1 #FORMAT-CN      (A8)
1 #FORMAT-MEMSHIP (A2)
1 #SPS-NAME     (A25)
1 #LAST-NAME    (A25)
1 #FIRST/MID    (A25)
1 #SUFFIX       (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
*
1 #EDIT-INVALID (L)
1 #RANK-LITERAL (A23)
1 #RANK-DISPLAY (A5)
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #DISPL-SSN-F  (A11)
1 REDEFINE #DISPL-SSN-F
  2 #SSN-1      (N3)
  2 #SPACE1     (A1)
  2 #SSN-2      (N2)
  2 #SPACE2     (A1)
  2 #SSN-3      (N4)
1 #HIDE-SSN     (A11)   INIT <'SSN ON FILE'>
1 #DASH         (A1)   INIT <'-'>
1 #HOLD-NAMES      (A78)
1 #HOLD-ZIP-CODE   (N9)
1 #HOLD-EDIT-ZIP   (A10)
1 REDEFINE #HOLD-EDIT-ZIP
  2 #HOLD-ZIP      (A5)
  2 REDEFINE #HOLD-ZIP
    3 #HOLD-ZIP-5-TBL (N1/5)
  2 #HOLD-ZIP2     (A5)
  2 REDEFINE #HOLD-ZIP2
    3 #HOLD-ZIP2-DSH   (A1)
    3 #HOLD-ZIP-LAST-4 (A4)
  2 REDEFINE #HOLD-ZIP2
    3 #HOLD-ZIP2-DASH  (A1)
    3 #HOLD-ZIP-LAST-4-TBL (N1/4)
*
1 #SCR-ZIP-BAR-CODE  (A14)   /* NOT USED ANY MORE
1 #ZIP-SUM           (N3)
1 REDEFINE #ZIP-SUM
  2 #ZIP-SUM-2        (N2)
  2 #ZIP-SUM-1        (A1)
*
1 #HOLD-NAME       (A70)                                       /* PM5 was 45
1 #ID-NUMBER       (N6)
1 #NAME            (A25)
1 #FULL-RANK       (A23)
1 #HOLD-ADDRESS-2  (A70)                                       /* vt1 was 25
1 #HOLD-ADDRESS-3  (A70)                                       /* vt1 was 25
1 #ADDRESS-1      (A70)                                        /* vt1 was 25
1 #ADDRESS-2      (A70)                                        /* vt1 was 25
1 #ADDRESS-3      (A70)                                        /* vt1 was 25
1 #ZIP-CODE       (N9)
1 #MR-MSG-NOTE-NUM  (N6)
1 #NUM-POLS       (P2)
1 #AMT-TO-CHG-MBR (P5.2)
1 #AMT-TO-ADD-TO-ACV (P5.2)
1 #WHOLE-DOLLARS  (P5)
1 #AMT            (P5.2)
1 #DOLLARS-DISP   (A6)
1 #TOT-REM        (P2)
1 #PUSP           (L)
1 #WORK-LAST-DURATION (P3)
1 #PREMS-YET-TO-PAY  (P7.2)
1 #NUM-MONTHS        (P5)
1 #CLERK-ABB-PASS  (A3)
1 #INVALID   (L)
1 #CLERK-ABB-NAME-PASS (A20)
*
1 #F-M-L (A108) INIT<'January  February March    April    May      '
  -'June     July     August   SeptemberOctober  November December '>
1 REDEFINE #F-M-L            2 #F-MON-LIT (A9/12)
1 #CORR-MSG (A50) CONST<'This correspondence has been prepared for '
  -'you by:'>
*
1 #MAX-FACE-TOTAL           (P7.2)
1 #MAX-FACE-TERM            (P7.2)
1 #OVERRIDE-MAX-FACE-TOTAL  (P7.2)
1 #OVERRIDE-MAX-FACE-TERM   (P7.2)
1 #TOT-FACE   (P6)
1 #REM-FACE   (P6)
1 #REM-FACE-DISP   (A7)
1 #MR-CNT     (P3)
1 #MR-LIST    (A60)
1 #VLT-FIR    (A11)
1 #INT        (I2)
1 #NOTE-TEXT  (A60/10)
1 #DUES-DIFF  (N5.2)
1 #DATE-OF-BIRTH                (N8.0)         /* TT1
1 #GENDER                       (A1)           /* TT1
*
END-DEFINE
*
* * * * * * * * * * * * * * *
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'AAFMAA MSG/BEN/PUP/APP/ACC/ LETTER DETAIL'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
FORMAT (3) LS=133 PS=66
WRITE (3) TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'Multiple MR list'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
INPUT #PARM-INPUT                    /* TYPE OF MAIL-OUTS
MOVE #PARM-INPUT TO #PARM-CHOICE
*
IF #PARM-CHOICE = 'RQST'
  INPUT #PARM-CLERK                    /* CLERK
END-IF
*
INPUT #PARM-INPUT                    /* MEMBER DATE RANGE
INPUT #PARM-INPUT                    /* MEMBER AGE 62 DATE RANGE
INPUT #PARM-INPUT                    /* WIDOW DATE RANGE
*
IF #PARM-CHOICE = 'MBR-RQST-WID' OR = 'MBR-RQST' OR = 'RQST'
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
* * * * * * * * * * * * * * * *
* LETTER REQUEST PROCESSING
* * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
  IF   (ZIP-CODE = 0)       /* NO REQUESTS
      AND (RQST-TABLE = ' ')
    ESCAPE TOP
  END-IF
*
  IF (#PARM-CLERK = '***') AND (REQUEST-MM-N LT 50) /* bypass if
    ESCAPE TOP                              /* entered after job started
  END-IF
END-ALL  /* (0435)
*
SORT BY XX-R01.ID-NUMBER   /* NORMAL WAY
    USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE RQST-TABLE MESSAGE(*)
*
  FIND (1) XX-R01 WITH ID-NUMBER = XX-R01.ID-NUMBER
    IF NO RECORDS FOUND
      WRITE (3) '*** no record found for cn' XX-R01.ID-NUMBER
    END-NOREC
  END-FIND
*
  FIND (1) TERM-V WITH CLERK-ID = XX-R01.CLERK-ID
    IF NO RECORDS FOUND
      WRITE XX-R01.CLERK-ID 'user not found in AOAP005F'
      MOVE 'B' TO #CGA-USER-COMPANY
      ESCAPE BOTTOM
    END-NOREC
*
    IF TERM-V.SECURITY-CODE = 1 OR = 2 OR = 97
      MOVE 'S' TO #CGA-USER-COMPANY
    ELSE
      MOVE 'I' TO #CGA-USER-COMPANY
    END-IF
  END-FIND
*** PM - 05/2000
*
  RESET #AFSC-MSG
  IF MSG-IND = 1 OR = 3 /*REG OR REG/BEN MSG
    IF  MESSAGE(3)= MASK('CONGRATULATIONS ON YOUR PROMOTION! ') /*DOP
      MOVE 'DOP' TO #FORM-IND
      MOVE TRUE TO #AFSC-MSG
    ELSE
      IF MESSAGE(2)= MASK('ENCLOSED IS A FORM TO SEND TO THE S') /*SSA
        MOVE 'SSA' TO #FORM-IND
        MOVE TRUE TO #AFSC-MSG
      ELSE
        MOVE 'MSG' TO #FORM-IND
      END-IF
    END-IF
  END-IF
*
  PERFORM PROCESS-ADDR /* needed for next line zip code test
  IF #ISN = 0 /* so nothing prints if id not found
    ESCAPE TOP
  END-IF
*
  IF #ZIP-CODE = 0
    WRITE (3) // XX-R01.ID-NUMBER '**** FOREIGN RQST' #MR-LIST
  END-IF
*
  IF  (XX-R01.BEN-IND NE ' ')
      OR (XX-R01.BEN-SHO-IND NE ' ')
    ADD +1 TO #BEN-RQST-CNT
  END-IF
*
  IF (XX-R01.MSG-IND = 1 OR = 3)
      AND (#CGA-USER-COMPANY NE 'S') AND NOT #AFSC-MSG
    ADD +1 TO #MSG-RQST-CNT
  END-IF
*
  IF (XX-R01.ACC-IND NE ' ')
*   AND (NOT CI-V.PROCESS-IND = 'G' OR = 'S')    /* DW4
    ADD +1 TO #ACC-RQST-CNT
  END-IF
*
  IF XX-R01.CAP-IND NE ' '
    ADD +1 TO #CAP-RQST-CNT
  END-IF
*
  IF XX-R01.APP-IND NE ' '
    ADD +1 TO #APP-RQST-CNT
  END-IF
*
  IF XX-R01.PIL-IND NE ' '
    ADD +1 TO #PIL-RQST-CNT
  END-IF
*
  IF   (XX-R01.MAL-IND NE ' ')
*   AND (CI-V.PROCESS-IND NE 'S')   /* DW4
    ADD +1 TO #MAL-RQST-CNT
  END-IF
*
  IF (XX-R01.CO-IND NE ' ')
    ADD +1 TO #CO-RQST-CNT
  END-IF
*
  IF XX-R01.VP-IND NE ' '
    ADD +1 TO #VP-RQST-CNT
  END-IF
*
  RESET #MR-CNT #MR-LIST
  DECIDE FOR EVERY CONDITION
    WHEN (XX-R01.MSG-IND = 1 OR = 3)
        AND (#CGA-USER-COMPANY NE 'S') AND NOT #AFSC-MSG
      ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/MSG' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND = 'P'  ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/PUP' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'A' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/APP' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'B' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/APN' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'C' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/APT' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'D' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/AS' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'E' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/ASN' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'F' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/AST' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'G' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/ASG' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  'H' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/AMS' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.APP-IND =  '1' OR = '2' OR = '3' OR = '4' OR = '5'
        OR = '6'          ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/AGX' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.BEN-SHO-IND NE ' ' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/BND' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.ACC-IND NE ' '
*     AND (NOT CI-V.PROCESS-IND = 'G' OR = 'S')    /* DW4
      ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/ACC' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.CAP-IND NE ' ' ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/CAP' INTO #MR-LIST LEAVING NO
    WHEN (XX-R01.PIL-IND NE ' ')
      ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/PIL' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.CO-IND NE  ' '  ADD 1 TO #MR-CNT
      COMPRESS #MR-LIST '/CO' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.VP-IND NE  ' '  ADD 2 TO #MR-CNT   /* includes cover ltr
      COMPRESS #MR-LIST '/VP' INTO #MR-LIST LEAVING NO
    WHEN XX-R01.MAL-IND NE ' '
*     AND (CI-V.PROCESS-IND NE 'S')     /8 DW4
      ADD 1 TO #MR-CNT
      DECIDE ON FIRST VALUE XX-R01.MAL-IND
        VALUE '2' COMPRESS #MR-LIST '/APL' INTO #MR-LIST LEAVING NO
        VALUE '3' COMPRESS #MR-LIST '/FAL' INTO #MR-LIST LEAVING NO
        VALUE '4' COMPRESS #MR-LIST '/RFS' INTO #MR-LIST LEAVING NO
        VALUE '5' COMPRESS #MR-LIST '/RFC' INTO #MR-LIST LEAVING NO
        VALUE '6' COMPRESS #MR-LIST '/RFZ' INTO #MR-LIST LEAVING NO
        VALUE '7' COMPRESS #MR-LIST '/LPF' INTO #MR-LIST LEAVING NO
        VALUE '8' COMPRESS #MR-LIST '/LPV' INTO #MR-LIST LEAVING NO
        VALUE '9' COMPRESS #MR-LIST '/LPB' INTO #MR-LIST LEAVING NO
        NONE IGNORE
      END-DECIDE
*
    WHEN NONE IGNORE
  END-DECIDE
*
  IF #MR-CNT GT 1
    WRITE (3) // XX-R01.ID-NUMBER 'MULTIPLE MAILINGS'
      #MR-LIST
  END-IF
*
  IF XX-R01.BEN-IND NE ' '
    DECIDE ON FIRST VALUE XX-R01.BEN-IND
      VALUE  '1' COMPRESS #MR-LIST '/BNF' INTO #MR-LIST LEAVING NO
      VALUE  '2' COMPRESS #MR-LIST '/BNB' INTO #MR-LIST LEAVING NO
      VALUE  '3' COMPRESS #MR-LIST '/BNA' INTO #MR-LIST LEAVING NO
      VALUE  '4' COMPRESS #MR-LIST '/BNC' INTO #MR-LIST LEAVING NO
      VALUE  '5' COMPRESS #MR-LIST '/BNP' INTO #MR-LIST LEAVING NO
      VALUE  '6' COMPRESS #MR-LIST '/BNT' INTO #MR-LIST LEAVING NO
      VALUE  '7' COMPRESS #MR-LIST '/BNL' INTO #MR-LIST LEAVING NO
      VALUE  '8' COMPRESS #MR-LIST '/BNN' INTO #MR-LIST LEAVING NO
      NONE IGNORE
    END-DECIDE
  END-IF
*
  RESET #NOTE-TEXT (*)
*   below removed 2/30/2003 as per sll/khb
* IF (XX-R01.MAL-IND = '4' OR = '5' OR = '6' OR = '7' OR = '8' OR = '9')
*  AND (CI-V.PROCESS-IND NE 'S')
*   MOVE *DATN TO #DATN
*   ADD 2 TO #MM-N
*
*   IF #MM-N GT 12
*     SUBTRACT 12 FROM #MM-N
*     ADD 1 TO #YYYY
*   END-IF
*
*   COMPRESS #MM #DD #YYYY INTO #NOTE-TEXT (1) WITH '/'
*   COMPRESS '*@' #NOTE-TEXT (1) 'MR Sent' #MR-LIST INTO #NOTE-TEXT (1)
* ELSE
  IF #MR-LIST NE ' '
    COMPRESS 'MR Sent' #MR-LIST INTO #NOTE-TEXT (1)
  END-IF
* END-IF
*
  IF   (#NOTE-TEXT (1) NE ' ')
*   AND (CLERK-ID NE 'DRW')    /* temporary (keep it here)
    MOVE 1 TO #INT      /* create note
    CALLNAT 'E4205SDM' XX-R01.ID-NUMBER #INT CLERK-ID #NOTE-TEXT(1:10)
  END-IF
*
  IF XX-R01.VP-IND NE  ' '
    MOVE EDITED #CURR-GROWTH-RATE (EM=Z.99) TO #GROWTH-RATE-DISP
    COMPRESS #GROWTH-RATE-DISP '%' INTO #GROWTH-RATE-DISP LEAVING NO
    COMPRESS #GROWTH-RATE-DISP '.' INTO #GR-RATE-DISP-PER LEAVING NO
    MOVE EDITED #VP-GROWTH-RATE (EM=Z.9) TO #VP-RATE-DISP
    COMPRESS #VP-RATE-DISP '%' INTO #VP-RATE-DISP LEAVING NO
    MOVE *DATN TO #DATN
    MOVE #YYYY TO #YEAR-DISP
    COMPRESS #YYYY '.' INTO #YEAR-DISP-PER LEAVING NO
    RESET #I2
    PERFORM CORR-MSG-FORMAT
    WRITE (8) NOTITLE USING FORM 'AOAF005F' /* VP COVER LETTER
  END-IF
*
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
*
  IF  (XX-R01.MSG-IND = 1 OR = 3)      /* regular MR MESSAGE
      AND (#CGA-USER-COMPANY NE 'S') AND NOT #AFSC-MSG
    NEWPAGE (8)
    WRITE (8) NOTITLE USING FORM 'AOAF005C'
  END-IF
*
  IF   (XX-R01.ACC-IND NE ' ')
*   AND (NOT CI-V.PROCESS-IND = 'G' OR = 'S')   /* DW4
    IF XX-R01.ACC-IND = '1'
      MOVE '__________________________________________' TO #ACC-NAME
      MOVE '________________________________' TO #ACC-REL
    ELSE
      SEPARATE NAME-SP LEFT INTO #LAST-NAME #FIRST/MID #SUFFIX
        #EXTRA1 #EXTRA2 WITH DELIMITER ','
      COMPRESS #FIRST/MID #LAST-NAME #SUFFIX #EXTRA1 #EXTRA2
        INTO #ACC-NAME
      MOVE 'SPOUSE'     TO #ACC-REL
    END-IF
*
*  IF PROCESS-IND = 'I' OR = 'O' OR = 'D'   /* DW4
*    MOVE 'the AAFMAA:' TO #COMP-NAME
*  ELSE
*    IF PROCESS-IND = 'S'
    MOVE 'the Armed Forces Services Corporation:' TO #COMP-NAME
*    ELSE
*      MOVE 'AAFMAA and AFSC:' TO #COMP-NAME   /* GRANDFATHERED
*    END-IF
*  END-IF
*
    MOVE 'ACS' TO #FORM-IND   /* PM2
    NEWPAGE (7)
    WRITE (7) NOTITLE USING FORM 'AOAF005E' /* PM2 - NEED THIS FOR INS
*   WRITE (7) NOTITLE USING FORM 'AOAF005I' /* PM2 - CHG TO 9 X 6 ENV
  END-IF
*
  IF XX-R01.CAP-IND NE ' '
    MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
    MOVE XX-R01.ID-NUMBER TO #CGA-ID
    MOVE 'CP*' TO #CGA-SCR-SUFFIX
    FETCH RETURN 'ATHPCAPL'
  END-IF
*
  IF XX-R01.APP-IND = 'P'   /* paid-up LETTER
    RESET *ISN #ISN
    FIND (1) CI-V WITH MEMBER-KEY = ID-NUMBER-A
      IF NO RECORDS FOUND
        FIND (1) CI-V WITH WIDOW-KEY = ID-NUMBER-A
          IF NO RECORDS FOUND
            WRITE (2) 'MBR NOT FOUND' ID-NUMBER-A
            ESCAPE BOTTOM
          END-NOREC
*
          MOVE *ISN TO #ISN
        END-FIND
      END-NOREC
*
      IF #ISN = 0
        MOVE *ISN TO #ISN
      END-IF
    END-FIND
*
    IF #ISN = 0
      PERFORM RESET-MR-REC
      ESCAPE TOP
    END-IF
*
    RESET #TOT-FACE
    FOR #IFL 1 TO 9
      IF CI-V.STATUS (#IFL) = 'D'
        IF CI-V.PAY-STATUS (#IFL) = '44' OR = '45'
          IF CI-V.ISSUE-DATE (#IFL) LT 19941201
            COMPUTE #TOT-FACE =
              #TOT-FACE + (CI-V.NUM-MBRSHIPS(#IFL) * 5000)
          ELSE
            COMPUTE #TOT-FACE =
              #TOT-FACE + (CI-V.EXPIRY-DAY (#IFL) * 5000)
          END-IF
        ELSE
          ADD CI-V.FACE-AMOUNT (#IFL) TO #TOT-FACE
        END-IF
      END-IF
    END-FOR
*
    MOVE #ISN TO #CGA-ISN
    MOVE XX-R01.ID-NUMBER TO #CGA-ID
    MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
*
    PERFORM PUP-PROCESS
  END-IF
*
  IF   (XX-R01.APP-IND NE ' ')
      AND (XX-R01.APP-IND NE 'P')  /* ALL APPL'S
    RESET #DISPL #RANK-LITERAL #RANK-DISPLAY                      /* PM1
    CALLNAT 'GRANKSXX' #PASS-COMMAND CI-V.RANK CI-V.MILITARY-SERVICE
      #EDIT-INVALID #DISPL #RANK-LITERAL #RANK-DISPLAY
    MOVE #RANK-LITERAL TO #FULL-RANK
*
    RESET #SCR-TBL (*)
*
    IF #FULL-RANK = ' '
      COMPRESS 'Dear' #EXT-FORMATED-NAME INTO #SCR-TBL (2)                               /* vt1
    ELSE
      COMPRESS 'Dear' #FULL-RANK #LAST-NAME INTO #SCR-TBL (2)
    END-IF
*
    COMPRESS #SCR-TBL (2) ':' INTO #SCR-TBL (2) LEAVING NO
*
    IF XX-R01.APP-IND = 'A' OR = 'B' OR = 'C'
      COMPRESS 'Thank you for requesting this application for'
        'additional insurance.' INTO #SCR-TBL (4)
    END-IF
*
    IF XX-R01.APP-IND = 'D' OR = 'E' OR = 'F'
      COMPRESS 'Thank you for requesting this application for'
        'insurance for your spouse.' INTO #SCR-TBL (4)
    END-IF
*
    IF XX-R01.APP-IND = 'G'
      COMPRESS 'Thank you for requesting these applications for'
        'insurance for your' INTO #SCR-TBL (4)
      MOVE 'spouse and generational insurance.' TO #SCR-TBL (5)
    END-IF
*
    IF XX-R01.APP-IND = 'H'
      COMPRESS 'Thank you for requesting these applications for'
        'insurance for you' INTO #SCR-TBL (4)
      MOVE 'and your spouse.' TO #SCR-TBL (5)
    END-IF
*
    IF XX-R01.APP-IND = '1' OR = '2' OR = '3' OR = '4' OR = '5' OR = '6'
      COMPRESS 'Thank you for requesting information on'
        'generational insurance.' INTO #SCR-TBL (4)
      COMPRESS 'Enclosed find' APP-IND 'application(s) for'
        'generational insurance.' INTO #SCR-TBL (5)
    END-IF
*
    COMPRESS 'We are pleased to offer an array of life insurance'
      'protection for our'   INTO #SCR-TBL (6)
    COMPRESS 'members and their families.' INTO #SCR-TBL (7)
    COMPRESS 'We appreciate your support and look forward to being'
      'of good service'         TO #SCR-TBL (9)
    MOVE     'to you in the years to come.' TO #SCR-TBL (10)
    COMPRESS 'If you have questions call our membership department'
      ' - 1-800-522-5221'  TO #SCR-TBL (12)
    MOVE     'EXT. 103.' TO #SCR-TBL (13)
    MOVE     'Thanks for being a member.' TO #SCR-TBL (15)
*
    NEWPAGE (8)
    WRITE (8) NOTITLE USING FORM 'AOAF005P'  /* application
*
    IF NOT XX-R01.APP-IND = '1' OR = '2' OR = '3' OR = '4' OR = '5'
        OR = '6'
      MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
      MOVE 'MR' TO #CGA-SCR-COMMAND /* to indicate check mr trigger
      RESET #CGA-SCR-SUFFIX
      DECIDE ON FIRST VALUE XX-R01.APP-IND
        VALUE 'A' MOVE 'APP' TO #CGA-SCR-SUFFIX
        VALUE 'B' MOVE 'APN' TO #CGA-SCR-SUFFIX
        VALUE 'C' MOVE 'APT' TO #CGA-SCR-SUFFIX
        VALUE 'D' MOVE 'AS ' TO #CGA-SCR-SUFFIX
        VALUE 'E' MOVE 'ASN' TO #CGA-SCR-SUFFIX
        VALUE 'F' MOVE 'AST' TO #CGA-SCR-SUFFIX
        VALUE 'G' MOVE 'AS ' TO #CGA-SCR-SUFFIX
        VALUE 'H' MOVE 'APP' TO #CGA-SCR-SUFFIX
        NONE IGNORE
      END-DECIDE
*
      FETCH RETURN 'C2150PPO'
*
      IF XX-R01.APP-IND = 'H'   /* mbr & sps
        MOVE 'MR' TO #CGA-SCR-COMMAND /* to indicate check mr trigger
        MOVE 'AS ' TO #CGA-SCR-SUFFIX
        FETCH RETURN 'C2150PPO'    /* <<<<<<<<<<<<<<<<<<<<<<<
      END-IF
    END-IF
  END-IF
*
  PERFORM RESET-MR-REC
END-SORT  /* (0446)
*
WRITE (2) /  12T 'TOTAL BEN LTR REQUESTS' #BEN-RQST-CNT
WRITE (2) /  12T 'TOTAL CO  LTR REQUESTS' #CO-RQST-CNT
WRITE (2) /  '***************'
WRITE (2) /  12T 'TOTAL MSG LTR REQUESTS' #MSG-RQST-CNT
WRITE (2) /  12T 'TOTAL ACC LTR REQUESTS' #ACC-RQST-CNT
WRITE (2) /  12T 'TOTAL VP  LTR REQUESTS' #VP-RQST-CNT
WRITE (2) /  12T 'TOTAL PIL     REQUESTS' #PIL-RQST-CNT
WRITE (2) /  '***************'
WRITE (2) /  12T 'TOTAL CAP LTR REQUESTS' #CAP-RQST-CNT
WRITE (2) // 12T 'TOTAL APP/PUP REQUESTS' #APP-RQST-CNT
WRITE (2) /  '***************'
WRITE (2) // 12T 'TOTAL MAL     REQUESTS' #MAL-RQST-CNT
WRITE (2) /  '***************'
*
* * * * * * * * * * * * * * * *
DEFINE SUBROUTINE RESET-MR-REC
* * * * * * * * * * * * * * * *
FIND (1) XX-R01 WITH ID-NUMBER = XX-R01.ID-NUMBER
  IF NO RECORDS FOUND
    WRITE (3) '*** no MR record found for cn' XX-R01.ID-NUMBER
  END-NOREC
*
  IF XX-R01.BEN-IND NE ' '
    RESET XX-R01.BEN-IND
    RESET MESSAGE (11:14)
  END-IF
*
  IF XX-R01.BEN-SHO-IND NE ' '
    RESET XX-R01.BEN-SHO-IND
    RESET MESSAGE (11:14)
  END-IF
*
  IF ((XX-R01.MSG-IND = 1 OR = 3)
      AND (#CGA-USER-COMPANY NE 'S') AND NOT #AFSC-MSG)
      OR (XX-R01.MSG-IND = 2)
    RESET XX-R01.MSG-IND
    RESET MESSAGE (*)
  END-IF
*
  IF (XX-R01.ACC-IND NE ' ')
*   AND (NOT CI-V.PROCESS-IND = 'G' OR = 'S')   /* DW4
    RESET XX-R01.ACC-IND
  END-IF
*
  IF XX-R01.CAP-IND NE ' '
    RESET XX-R01.CAP-IND
  END-IF
*
  IF XX-R01.APP-IND NE ' '
    RESET XX-R01.APP-IND
    RESET ASSUM-RET-DATE ASSUM-RET-DATE-2 ASSUM-RET-DATE-3
  END-IF
*
  IF XX-R01.PIL-IND NE ' '
    ADD +1 TO #PIL-RQST-CNT
    RESET XX-R01.PIL-IND
  END-IF
*
  IF   (XX-R01.MAL-IND NE ' ')
*   AND (CI-V.PROCESS-IND NE 'S')    /* DW4
    RESET XX-R01.MAL-IND
  END-IF
*
  IF (XX-R01.CO-IND NE ' ')
    RESET XX-R01.CO-IND
    RESET XX-R01.CO-RQSTS (*) INFO
  END-IF
*
  IF XX-R01.VP-IND NE ' '
    RESET XX-R01.VP-IND
    RESET ASSUM-RET-DATE ASSUM-RET-DATE-2 ASSUM-RET-DATE-3
  END-IF
*
  IF REQUEST-MM-N GE 50
    SUBTRACT 50 FROM REQUEST-MM-N   /* restore date
  END-IF
*
  UPDATE (0852)
*
END-FIND
*
END TRANSACTION
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-ADDR
* * * * * * * * * * * * * * *
*
RESET #ISN
*
* IF   (XX-R01.ID-NUMBER GT 700000)    /* DW3
*  AND (XX-R01.ID-NUMBER LE 799999)
*   FIND (1) IM-V WITH ID-NUMBER = XX-R01.ID-NUMBER
*     IF NO RECORDS FOUND
*       WRITE (2) XX-R01.ID-NUMBER 'not found'
*     END-NOREC
*     MOVE *ISN TO #ISN
*
*     MOVE IM-V.ID-NUMBER TO #ID-NUMBER
*     MOVE IM-V.RANK TO #RANK-DISPLAY CI-V.RANK
*     MOVE IM-V.NAME TO #NAME
*     MOVE IM-V.ADDRESS-1 TO #ADDRESS-1
*     MOVE IM-V.ADDRESS-2 TO #ADDRESS-2
*     MOVE IM-V.ADDRESS-3 TO #ADDRESS-3
*     MOVE IM-V.ZIP-CODE  TO #ZIP-CODE
*     RESET #MIL-STATUS
*     RESET #MIL-SERVICE
*   END-FIND
* ELSE
IF   (XX-R01.ID-NUMBER GT 500000)
    AND (XX-R01.ID-NUMBER LE 599999)
  FIND (1) AT-V WITH ID-NUMBER = XX-R01.ID-NUMBER
    IF NO RECORDS FOUND
      WRITE (2) XX-R01.ID-NUMBER 'not found'
    END-NOREC
    MOVE *ISN TO #ISN
*
    MOVE AT-V.ID-NUMBER TO #ID-NUMBER
    MOVE AT-V.RANK TO #RANK-DISPLAY CI-V.RANK
    MOVE AT-V.NAME TO #NAME
*     MOVE AT-V.ADDRESS-1 TO #ADDRESS-1     NG1   Start
*     MOVE AT-V.ADDRESS-2 TO #ADDRESS-2
*     MOVE AT-V.ADDRESS-3 TO #ADDRESS-3
*     MOVE AT-V.ZIP-CODE  TO #ZIP-CODE
    MOVE AT-V.CONTACT-ID TO #ADD-CONTACT-ID
    MOVE 'P' TO #ADD-TYPE
    CALLNAT 'ADDN0001' #PDA-ADDRESS
    MOVE #ADD-LINE-1 TO #ADDRESS-1
    MOVE #ADD-LINE-2 TO #ADDRESS-2
    MOVE #ADD-LINE-3 TO #ADDRESS-3
    MOVE #ADD-ZIP-CODE TO #ZIP-CODE
*                                           NG1 End
    RESET #MIL-STATUS
    RESET #MIL-SERVICE
  END-FIND
*
  IF AT-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    FIND (1) CI-V WITH MEMBER-KEY = AT-V.REF-ID-A (1)
      MOVE CI-V.NAME TO #NAME
      MOVE CI-V.RANK TO #RANK-DISPLAY
    END-FIND
  END-IF
ELSE
  FIND (1) CI-V WITH MEMBER-KEY = ID-NUMBER-A
    IF NO RECORDS FOUND
      FIND (1) CI-V WITH WIDOW-KEY = ID-NUMBER-A
        IF NO RECORDS FOUND
          WRITE (2) XX-R01.ID-NUMBER 'not found'
        END-NOREC
        MOVE *ISN TO #ISN  /* load widow isn
*
      END-FIND
    END-NOREC
*
    IF #ISN = 0           /* load mbr isn
      MOVE *ISN TO #ISN
    END-IF
  END-FIND
*
  IF #ISN = 0
    WRITE (2) 'cn not found' XX-R01.ID-NUMBER
    RESET #FORMAT-CN #EXT-FORMATED-NAME #FIRST-ADDRESS #SECOND-ADDRESS                   /* vt1
      #THIRD-ADDRESS
    ESCAPE ROUTINE
  END-IF
*
  MOVE CI-V.ID-NUMBER TO #ID-NUMBER
  IF CI-V.WIDOW-FLAG = 'Y'
    MOVE CI-V.NAME-SP TO #NAME
    MOVE TITLE-SP     TO #RANK-DISPLAY
    RESET #MIL-STATUS /* to prevent (,ret)
  ELSE
    MOVE CI-V.NAME      TO #NAME
*
    MOVE CI-V.MILITARY-STATUS  TO #MIL-STATUS
    MOVE CI-V.MILITARY-SERVICE TO #MIL-SERVICE
    RESET #DISPL #RANK-LITERAL #RANK-DISPLAY                    /* PM1
    CALLNAT 'GRANKSXX' #PASS-COMMAND CI-V.RANK CI-V.MILITARY-SERVICE
      #EDIT-INVALID #DISPL #RANK-LITERAL #RANK-DISPLAY
*
    IF CI-V.CHIEF-OF-STAFF-IND = 'R' OR = 'Q' /*INDS RANK SECURITY
        OR CI-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      RESET #RANK-DISPLAY
*       IF CI-V.SEX = 'F'
*         IF CI-V.NAME-SP = ' '
*           MOVE 'MS' TO #RANK-DISPLAY
*         ELSE
*           MOVE 'MRS' TO #RANK-DISPLAY
*         END-IF
*       ELSE
*         MOVE 'MR' TO #RANK-DISPLAY
*       END-IF
    END-IF
  END-IF
*
  MOVE CI-V.MEMBER-CONTACT-ID TO #ADD-CONTACT-ID                      /* vt1 \/
  MOVE 'P' TO #ADD-TYPE
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  MOVE #ADD-LINE-1 TO #ADDRESS-1
  MOVE #ADD-LINE-2 TO #ADDRESS-2
  MOVE #ADD-LINE-3 TO #ADDRESS-3
  MOVE #ADD-ZIP-CODE TO #ZIP-CODE
*
*    MOVE CI-V.ADDRESS-1 TO #ADDRESS-1
*    MOVE CI-V.ADDRESS-2 TO #ADDRESS-2
*    MOVE CI-V.ADDRESS-3 TO #ADDRESS-3
*    MOVE CI-V.ZIP-CODE  TO #ZIP-CODE
*                                                                  /* vt1 /\
END-IF
*
IF CI-V.CHIEF-OF-STAFF-IND = 'S' OR = 'Q' /*INDS SSN SECURITY
  MOVE #HIDE-SSN TO #DISPL-SSN-F
ELSE
  MOVE SSN-1 TO #SSN-1
  MOVE SSN-2 TO #SSN-2
  MOVE SSN-3 TO #SSN-3
  MOVE #DASH TO #SPACE1 #SPACE2
END-IF
*
SEPARATE #NAME LEFT INTO #LAST-NAME #FIRST/MID #SUFFIX
  #EXTRA1 #EXTRA2 WITH DELIMITER ','
COMPRESS #FIRST/MID #LAST-NAME #SUFFIX #EXTRA1 #EXTRA2 INTO #HOLD-NAME
COMPRESS #RANK-DISPLAY #HOLD-NAME INTO #EXT-FORMATED-NAME                              /* vt1
*
IF #RANK-DISPLAY = ' '
  MOVE #HOLD-NAME TO #FORMATED-LAST-NAME
ELSE
  COMPRESS #RANK-DISPLAY #LAST-NAME INTO #FORMATED-LAST-NAME
END-IF
COMPRESS #FORMATED-LAST-NAME ':' INTO #FORMATED-LAST-NAME LEAVING NO
*
IF #MIL-STATUS = 'R'
  COMPRESS #EXT-FORMATED-NAME ', RET' INTO #EXT-FORMATED-NAME LEAVING NO                   /* vt1
END-IF
*
COMPRESS 'CN' #ID-NUMBER INTO #FORMAT-CN LEAVING NO
*
MOVE #ADDRESS-1 TO #FIRST-ADDRESS
MOVE #ADDRESS-2 TO #HOLD-ADDRESS-2
MOVE #ADDRESS-3 TO #HOLD-ADDRESS-3
MOVE #ZIP-CODE  TO #HOLD-ZIP-CODE
*
MOVE EDITED #HOLD-ZIP-CODE (EM=99999-9999) TO #HOLD-EDIT-ZIP
*
RESET #SCR-ZIP-BAR-CODE
RESET #ZIP-SUM
* IF #HOLD-ZIP NE '00000'                         NG1 Start
*   RESET #ZIP-SUM
*   FOR #I1 1 TO 5
*     ADD #HOLD-ZIP-5-TBL (#I1) TO #ZIP-SUM
*   END-FOR
* *
*   IF #HOLD-ZIP-LAST-4 NE '0000'
*     FOR #I1 1 TO 4
*       ADD #HOLD-ZIP-LAST-4-TBL (#I1) TO #ZIP-SUM
*     END-FOR
* *
*     FOR #I1 1 TO 2
*       ADD ZIP-CODE-DPB-TBL (#I1) TO #ZIP-SUM
*     END-FOR
*   END-IF
* *
*   DIVIDE 10 INTO #ZIP-SUM REMAINDER #ZIP-SUM
*   COMPUTE #ZIP-SUM = 10 - #ZIP-SUM
* *
*   IF #HOLD-ZIP-LAST-4 = '0000'
*     COMPRESS '!' #HOLD-ZIP #ZIP-SUM-1 '!'
*       INTO #SCR-ZIP-BAR-CODE LEAVING NO
*   ELSE
*     COMPRESS '!' #HOLD-ZIP #HOLD-ZIP-LAST-4 ZIP-CODE-DPB-A
*       #ZIP-SUM-1 '!' INTO #SCR-ZIP-BAR-CODE LEAVING NO
*   END-IF
* END-IF
*
IF #HOLD-ZIP2 = '-0000'
  MOVE '     ' TO #HOLD-ZIP2
END-IF
*
IF #HOLD-ADDRESS-3 = ' '
  COMPRESS #HOLD-ADDRESS-2 #HOLD-EDIT-ZIP INTO #SECOND-ADDRESS
  RESET #THIRD-ADDRESS
ELSE
  MOVE #HOLD-ADDRESS-2 TO #SECOND-ADDRESS
  COMPRESS #HOLD-ADDRESS-3 #HOLD-EDIT-ZIP INTO #THIRD-ADDRESS
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE PUP-PROCESS
* * * * * * * * * * * * * * *
*
MOVE *DATN TO #TODAY-YYYYMMDD-N
*
FOR #IFL 1 TO 9
  IF  (CI-V.STATUS (#IFL) NE 'D')
      OR (CI-V.PAY-STATUS (#IFL) GE '42')
    ESCAPE TOP
  END-IF
*
  ADD 1 TO #NUM-POLS
END-FOR
*
MOVE *DATN TO #DATN   /* age 59 1/2
SUBTRACT 59 FROM #YYYY
SUBTRACT 6 FROM #MM-N
IF #MM-N LT 1
  ADD 12 TO #MM-N
  SUBTRACT 1 FROM #YYYY
END-IF
MOVE #DATN TO #AGE-59-1-2
*
RESET #DISPL #RANK-LITERAL #RANK-DISPLAY                          /* PM1
CALLNAT 'GRANKSXX' #PASS-COMMAND CI-V.RANK CI-V.MILITARY-SERVICE
  #EDIT-INVALID #DISPL #RANK-LITERAL #RANK-DISPLAY
*
MOVE #RANK-LITERAL TO #FULL-RANK
*
RESET #I2 #SCR-TBL (*)
ADD 1 TO #I2
*
IF #FULL-RANK = ' '
  COMPRESS 'Dear' #EXT-FORMATED-NAME INTO #SCR-TBL (#I2)                              /* vt1
ELSE
  COMPRESS 'Dear' #FULL-RANK #LAST-NAME INTO #SCR-TBL (#I2)
END-IF
*
COMPRESS #SCR-TBL (#I2) ':' INTO #SCR-TBL (#I2) LEAVING NO
ADD 2 TO #I2
*
ADD 1 TO #I2
COMPRESS '  The following is furnished in response to your recent'
  'inquiry:' TO #SCR-TBL (#I2)
ADD 1 TO #I2
*
RESET #TAMRA #PAY-UP PDA-CONTACT #DATE-OF-BIRTH #GENDER       /* TT1
P-CONTACT-ID := CI-V.MEMBER-CONTACT-ID                        /* TT1
CALLNAT 'CONTS001' PDA-CONTACT                                /* TT1
#DATE-OF-BIRTH   := P-DATE-OF-BIRTH                           /* TT1
#GENDER          := P-GENDER                                  /* TT1
*
FOR #IFL 1 TO 9
  IF   (CI-V.ISSUE-DATE (#IFL) GE 19880621)
      AND (*DATN LT ISSUE-DATE (#IFL) + 70000)
      AND (#DATE-OF-BIRTH GT #AGE-59-1-2)   /* less than 59 1/2
    MOVE TRUE TO #TAMRA
  END-IF
*
  IF  (CI-V.STATUS (#IFL) NE 'D')
      OR (CI-V.PAY-STATUS (#IFL) GE '42')
    ESCAPE TOP
  END-IF
*
  MOVE 'AP' TO #TO-PLAN
  MOVE TRUE TO #PAY-UP
  PERFORM CALL-AOCX2V6A
*
  IF   (CI-V.ISSUE-DATE (#IFL) GE 19970101)
      AND (CI-V.MONTH-ALLOC-AMT (#IFL) NE 999.99)
    ADD #OUTPUT-PREM-DIFF TO #OUTPUT-CALC-AMT
    ADD #DUES-DIFF        TO #OUTPUT-CALC-AMT
  END-IF
*
  MOVE ROUNDED #OUTPUT-CALC-AMT TO #WHOLE-DOLLARS
  MOVE EDITED #WHOLE-DOLLARS (EM=ZZ,ZZ9) TO #DOLLARS-DISP
*
  ADD 1 TO #I2
  MOVE CI-V.ISSUE-DATE (#IFL) TO #DATN
  COMPRESS 'The cost to pay up the policy issued' #MM
    INTO #SCR-TBL (#I2)
  COMPRESS #SCR-TBL (#I2) #DD #YY INTO #SCR-TBL (#I2) WITH '/'
  COMPRESS #SCR-TBL (#I2) ' is $' #DOLLARS-DISP '.'
    INTO #SCR-TBL (#I2) LEAVING NO
END-FOR
*
IF #PAY-UP
  ADD 2 TO #I2
*
  MOVE *DATN TO #DATN
  COMPRESS '  The above payoff information is valid through the'
    'end of' #F-MON-LIT (#MM-N) #YY      INTO #SCR-TBL (#I2)
  COMPRESS #SCR-TBL (#I2) '.' INTO #SCR-TBL (#I2) LEAVING NO
  ADD 1 TO #I2
  COMPRESS 'For any payoff action after the above date, send'
    'the amount listed and we'  INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'will bill or refund any difference. You may also'
    'call our Financial'  INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'Management Department for the current cost prior to'
    'mailing your check.'                   INTO #SCR-TBL (#I2)
END-IF
*
CALLNAT 'GMAXSXX'
  #CGA-ORIG-COMMAND
  #MAX-FACE-TOTAL
  #MAX-FACE-TERM
  #OVERRIDE-MAX-FACE-TOTAL
  #OVERRIDE-MAX-FACE-TERM
  CI-V.INTRO-FLAG
  #DATE-OF-BIRTH    /* DW2                        /* TT1
*
COMPUTE #REM-FACE = #MAX-FACE-TOTAL - #TOT-FACE
MOVE EDITED #REM-FACE (EM=ZZZ,ZZZ) TO #REM-FACE-DISP
*
IF  (#TOT-FACE GT (#MAX-FACE-TOTAL - 5000))
    OR (#DATE-OF-BIRTH LE (*DATN - 660000))       /* TT1
    OR (CI-V.RESTRICT-MBR-SW = 'Y')
    OR (CI-V.SUSPEND-CODE = '5' OR = '6' OR = '9')
  ADD 2 TO #I2
  MOVE 'You have the maximum available coverage.' TO #SCR-TBL (#I2)
*
  IF #NUM-POLS = 0
    IF #PAY-UP
      ADD 2 TO #I2
      COMPRESS 'Please write' #FORMAT-CN 'on the check and make'
        'payable to AAFMAA.'           TO #SCR-TBL (#I2)
    END-IF
*
    COMPRESS #SCR-TBL (#I2) 'All policies are paid up.'
      INTO #SCR-TBL (#I2)
    ESCAPE ROUTINE
  END-IF
ELSE
  ADD 2 TO #I2
  COMPRESS '  You may apply for $' #REM-FACE-DISP
    ' added coverage. Complete & return the attached'
    INTO #SCR-TBL (#I2) LEAVING NO
*
  ADD 1 TO #I2
  COMPRESS 'attached application for this or other additional'
    'coverage.'       INTO #SCR-TBL (#I2)
END-IF
*
ADD 2 TO #I2
COMPRESS 'Please write' #FORMAT-CN 'on the check and make payable'
  'to AAFMAA.'           TO #SCR-TBL (#I2)
*
IF #TAMRA   /* below TAMRA TEXT is alse in g3520pad ***********
  ADD 2 TO #I2
  COMPRESS 'PROVISIONS OF THE TECHNICAL AND MISCELLANEOUS REVENUE'
    'ACT OF 1988 (TAMRA)'   INTO #SCR-TBL (#I2)
  ADD 2 TO #I2
  COMPRESS '  This act imposes tax implications on loans taken'
    'from or cash surrenders'  INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'made of policies issued on or after 21 June 1988'
    'and paid up within less'     INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'than seven years from date of issue.'
    'The IRS priority sequence for with-' INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'drawals is taxable gains first and then cost basis'
    '(premiums paid in).'  INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'Taxable gains withdrawn before age 59 1/2, unless'
    'the owner is disabled,'  INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'are subject to a 10% penalty tax, as well as ordinary'
    'income tax. Cost'     INTO #SCR-TBL (#I2)
  ADD 1 TO #I2
  COMPRESS 'Basis (premiums paid in) is withdrawn without taxation'
    'and penalty.'         INTO #SCR-TBL (#I2)
END-IF
*
PERFORM CORR-MSG-FORMAT
*
NEWPAGE (8)
WRITE (8) NOTITLE USING FORM 'AOAF005P'  /* paid-up ltr
* PERFORM PRINT-APPL  /* rem-v 03/2004 pre DRW - apps created at AAFMAA
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-APPL
* * * * * * * * * * * * * * * * * *
IF  (CI-V.NUMBER-MEMBERSHIPS = 0)       /* IS A TERMINATED MEMBER
    OR (#TOT-FACE GT (#MAX-FACE-TOTAL - 5000)) /* HAS MAXIMUM COVERAGE
    OR (CI-V.SUSPEND-CODE = '5' OR = '6' OR = '9') /* INS REJ/DEFERRED INS
    OR (CI-V.RESTRICT-MBR-SW = 'Y')        /* RESTRICTED (SPC CATEGORY)
    OR ((*DATN - 710000) GT #DATE-OF-BIRTH)  /* PAST AGE 71      /* TT1
  IGNORE
ELSE
  MOVE XX-R01.ID-NUMBER TO #CGA-SCR-ID
  RESET                    #CGA-SCR-SUFFIX
  FETCH RETURN 'C2150PPO'    /* <<<<<<<<<<<<<<<<<<<<<<<
END-IF
*
END-SUBROUTINE
*
*********************************
DEFINE SUBROUTINE CORR-MSG-FORMAT
*********************************
*
MOVE XX-R01.CLERK-ID TO #CLERK-ABB-PASS
MOVE ' ' TO #CLERK-ABB-NAME-PASS
CALLNAT 'GCKNMSXX' #CLERK-ABB-PASS
  #INVALID
  #CLERK-ABB-NAME-PASS
*
ADD 2 TO #I2
COMPRESS #CORR-MSG #CLERK-ABB-NAME-PASS INTO #SCR-TBL (#I2)
*
ADD 1 TO #I2
DECIDE ON FIRST #CLERK-ABB-PASS
  VALUE 'BJS' MOVE 'PRESIDENT' TO #SCR-TITLE (#I2)
  VALUE 'JJF' MOVE 'SECRETARY' TO #SCR-TITLE (#I2)
  VALUE 'RCC' MOVE 'TREASURER' TO #SCR-TITLE (#I2)
  VALUE 'RRF' MOVE 'ASST SECRETARY' TO #SCR-TITLE (#I2)
  VALUE 'WJW' MOVE 'ASST SECRETARY' TO #SCR-TITLE (#I2)
  VALUE 'ANK' MOVE 'ASST SECRETARY' TO #SCR-TITLE (#I2)
  VALUE 'WEM' MOVE 'ASST TREASURER' TO #SCR-TITLE (#I2)
  VALUE 'WRL' MOVE 'ASST TREASURER' TO #SCR-TITLE (#I2)
  VALUE 'DRW' MOVE 'PROGRAMMER'     TO #SCR-TITLE (#I2)
  NONE VALUE IGNORE
END-DECIDE
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CALL-AOCX2V6A
* * * * * * * * * * * * * * * * *
*
CALLNAT 'C1601SCO'
  #CGA-ORIG-COMMAND
  #CGA-ORIG-ID
  #CGA-ID
  #CGA-SCR-SUFFIX
  #TO-PLAN
  #GENDER                       /* TT1
  #IFL
  #TODAY-YYYYMMDD-N
  STATUS          (1:9)
  PLAN            (1:9)
  ONLINE-ACTION   (1:9)
  MODE            (1:9)
  CONVERSION-FLAG (1:9)
  SMOKER-FLAG     (1:9)
  ISSUE-AGE       (1:9)
  PAY-STATUS      (1:9)
  ISSUE-DATE      (1:9)
  PAID-TO-DATE    (1:9)
  FACE-AMOUNT     (1:9)
  ANNUAL-PREM     (1:9)
  LAST-DURATION   (1:9)
  CASH-VALUE      (1:9)
  DEATH-BENEFIT   (1:9)
  NUM-MBRSHIPS    (1:9)
  DURATION        (1:9)
  MONTH-IND       (1:9)
  MIN-CASH-VALUE  (1:9)
  NXT-YR-MIN-CASH-VALUE (1:9)
  NXT-MON-CASH-VALUE    (1:9)
  NXT-MON-DEATH-BENEFIT (1:9)
  ACC-CASH-VALUE        (1:9)
  CONV-DURATION         (1:9)
  MONTH-ALLOC-AMT       (1:9)
  PREM-DEFICIENCY       (1:9)
  BEG-CASH-VALUE        (1:9)
  NET-PR-RATIO          (1:9)
  #OUTPUT-CALC-AMT
  #OUTPUT-OLD-PDUP-AMT
  #PRORATE-LOAN
  #RPU-ACV
  #TOT-LOAN-AMOUNT
  #TOT-CURR-ACCUM-INT
  #NU-ANN-PREM
  #OUTPUT-PREM-DIFF
  #DUES-DIFF
  #LUMP-SUM
*
END-SUBROUTINE /* (1356)
*
END
