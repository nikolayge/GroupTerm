* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-LTII-RENEW-RATES (BENEF001)                                               *
* DESCRIPTION  : LTII+ renewable rates based on Age
************************************************************************
*                       MODIFICATION LOG
* USER   DATE        TAG     REASON
* NGG   11/24/2012          Program Written
************************************************************************
*
DEFINE FUNCTION F-LTII-RENEW-RATES RETURNS (N3.2) BY VALUE
*
  DEFINE DATA
  PARAMETER
  1 #AGE              (N2)
*  LOCAL USING GRTL0004
  LOCAL
  1 #LTII-KEY  (A14) 1 REDEFINE #LTII-KEY
    2 ##GENDER          (A1)
    2 ##LTII-TYPE       (A2)
    2 ##NICOTINE        (A1)
    2 ##AGE             (A2)
    2 ##EFFECTIVE-DATE  (A8)
  1 LTII-V VIEW OF A-PRODUCT-RATES
    2 VAR-TERM-RATE (N3.2/100)
    2 EFFECTIVE-DATE (A8)
    2 GENDER (A1)
    2 LTII-TYPE (A2)
    2 NICOTINE (A1)
    2 AGE (A2)
    2 RIDER-OPTION (A1)
    2 LTII-KEY     (A14)
  1 J  (N3)
  END-DEFINE
*
RESET F-LTII-RENEW-RATES
ASSIGN J = #AGE + 1
IF J < 1 J:= 1
ELSE
  IF J > 100 J := 100
  END-IF END-IF
##GENDER    := 'X'
##LTII-TYPE := 'CR'   /* CURRENT KEY=XCRX99
##NICOTINE  := 'X'
##AGE       := 99
MOVE EDITED *DATX (EM=99999999) TO ##EFFECTIVE-DATE
READ LTII-V DESCENDING BY LTII-KEY STARTING FROM #LTII-KEY
  IF SUBSTR(LTII-KEY,1,6) NE SUBSTR(#LTII-KEY,1,6)
    ESCAPE BOTTOM
  END-IF
  IF EFFECTIVE-DATE > ##EFFECTIVE-DATE
    ESCAPE TOP
  END-IF
  F-LTII-RENEW-RATES := VAR-TERM-RATE ( J )
  ESCAPE BOTTOM
END-READ
END-FUNCTION
*
END
