* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GNOTESXX - search for expected notes by member
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* PAM    06232004  PM1     Program Written.                            *
************************************************************************
*
DEFINE DATA PARAMETER
1 #NOTE-FIND                           (A3)
1 #ID-NUMBER                           (N6)
1 #ISSUE-DATE                          (N8) /* YYYYMMDD format
1 REDEFINE #ISSUE-DATE
 2 #ISSUE-DATE-YYYY                    (N4)
 2 #ISSUE-DATE-MM                      (N2)
 2 #ISSUE-DATE-DD                      (N2)
1 #NOTE-FOUND                          (L)
*
LOCAL
*
1 NT-V VIEW OF A-NOTES
 2 NOTE-CLERK-ID                       (A3/60)
 2 NOTE-LINE-NUM                       (N1/60)
 2 NOTE-DATE                           (N8/60)
 2 REDEFINE NOTE-DATE
  3 NOTE-DATE-OCC                      (60)
   4 NOTE-DATE-YYYY                    (N4)
   4 NOTE-DATE-MMDD                    (N4)
 2 NOTE-TEXT                           (A60/60)
 2 REDEFINE NOTE-TEXT
  3 NOTE-TEXT-OCC                      (60)
   4 NOTE-TEXT-60                      (A60)
   4 REDEFINE NOTE-TEXT-60
    5 NOTE-POL-ISS                     (A9)
    5 NOTE-POL-ISS-SPACE-1             (A1)
    5 NOTE-POL-ISS-ISSUE-DATE          (A10)
    5 REDEFINE NOTE-POL-ISS-ISSUE-DATE
     6 NOTE-POL-ISS-ISSUE-DATE-MM      (N2)
     6 NOTE-POL-ISS-ISSUE-DATE-S-1     (A1)
     6 NOTE-POL-ISS-ISSUE-DATE-DD      (N2)
     6 NOTE-POL-ISS-ISSUE-DATE-S-2     (A1)
     6 NOTE-POL-ISS-ISSUE-DATE-YYYY    (N4)
    5 NOTE-POL-ISS-SPACE-2             (A1)
    5 NOTE-POL-ISS-LTCSO               (A5)
    5 NOTE-POL-ISS-SPACE-3             (A1)
    5 NOTE-POL-ISS-DB-TITLE            (A14)
    5 NOTE-POL-ISS-DB-$                (A1)
    5 NOTE-POL-ISS-DB                  (A10)
    5 NOTE-POL-ISS-REST                (A8)
*
1 #I1                                  (I2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
RESET #NOTE-FOUND
*
FIND (1) NT-V WITH ID-NUMBER = #ID-NUMBER
  DECIDE FOR FIRST CONDITION
    WHEN #NOTE-FIND = 'LTC'
      PERFORM CHECK-FOR-LONG-TERM-CARE-NOTE
    WHEN NONE
      ESCAPE ROUTINE
  END-DECIDE
END-FIND
*
* * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-FOR-LONG-TERM-CARE-NOTE
* * * * * * * * * * * * * * * * * * * * * * * *
*
FOR #I1 1 TO 60
  IF NOTE-TEXT (#I1) = ' '
    ESCAPE BOTTOM
  END-IF
*
  IF NOTE-POL-ISS (#I1)                   = '* POL ISS'
   AND NOTE-POL-ISS-LTCSO (#I1)           = 'LTCSO'
   AND NOTE-POL-ISS-ISSUE-DATE-MM (#I1)   = #ISSUE-DATE-MM
   AND NOTE-POL-ISS-ISSUE-DATE-DD (#I1)   = #ISSUE-DATE-DD
   AND NOTE-POL-ISS-ISSUE-DATE-YYYY (#I1) = #ISSUE-DATE-YYYY
    MOVE TRUE TO #NOTE-FOUND
    ESCAPE BOTTOM
  END-IF
END-FOR
*
END-SUBROUTINE
*
END
