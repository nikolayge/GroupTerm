* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-CALCIN                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Calculate accumulated interest                        *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE FUNCTION FUNCTION-CALCULATE-INTEREST RETURNS (P5.2) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #INTEREST-RATE         (P2.3)
1 #LOAN-AMOUNT           (P7.2)
1 #EFFECTIVE-DATE        (N8)
1 #LOAN-DATE             (N8)
1 REDEFINE #LOAN-DATE
 2 #LOAN-DATE-YYYY       (N4)
 2 #LOAN-DATE-MM         (N2)
 2 #LOAN-DATE-DD         (N2)
1 #CURR-ACCUM-INTEREST   (P5.2)
1 #NXT-YR-ACCUM-INT      (P5.2)
*
LOCAL
*
1 #INTEREST              (P5.2)
1 #WORK-EFFECTIVE-DATE   (N8)
1 REDEFINE #WORK-EFFECTIVE-DATE
 2 #WORK-YYYY            (N4)
 2 #WORK-MM              (N2)
 2 #WORK-DD              (N2)
1 #SCR-DAILY-INT         (P3.7)
*
1 #FROM-DATE             (N8)
1 #REQUEST               (A1)  INIT <'D'>
*
END-DEFINE
************************************************************************
*
IF #EFFECTIVE-DATE = MASK (YYYYMMDD)
  #WORK-EFFECTIVE-DATE := #EFFECTIVE-DATE
ELSE
  CALLNAT 'GDATESXX'
    #EFFECTIVE-DATE
    #FROM-DATE
    #WORK-EFFECTIVE-DATE
    #REQUEST
END-IF
*
COMPUTE ROUNDED #SCR-DAILY-INT = #LOAN-AMOUNT * (#INTEREST-RATE / 365) / 100
*
IF   (#LOAN-DATE-MM = #WORK-MM )
 AND (#LOAN-DATE-YYYY = #WORK-YYYY )
  COMPUTE ROUNDED #INTEREST = ((#WORK-DD - #LOAN-DATE-DD) * #SCR-DAILY-INT)
    + #CURR-ACCUM-INTEREST + #NXT-YR-ACCUM-INT
ELSE
  COMPUTE ROUNDED #INTEREST = ((#WORK-DD - 1) * #SCR-DAILY-INT)
    + #CURR-ACCUM-INTEREST + #NXT-YR-ACCUM-INT
END-IF

FUNCTION-CALCULATE-INTEREST := #INTEREST
*
END-FUNCTION
*
END
