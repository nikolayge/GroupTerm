* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C232XSRE       PDF Screen for 712 FORM
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* DRW    02022005  DW1    add aafmaa address & pol type to PDF         *
* DRW    09202005  DW2    Use DB/CR & PREM ADJ if f1-23 amount         *
* DRW    06122006  DW3    remove Earned-int from Final DB              *
* DRW    09142007  DW4    Reset Invalid Characters for PDF to '*'      *
* PAM    07292009  PM5    Use actual policy segment                    *
* PAM  08/16/2010  PM15   Added date parameter to death benefit calc   *
* YAK    10032012  YK1    Use function to identify Level II Term plan  *
* vxt    03232015  vt1    /Ticket 3418/
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND    (A2)
1 #CGA-SCR-ID         (N9)
1 #CGA-SCR-SUFFIX     (A3)
1 #CGA-CLERK          (A3)
1 #SEG-I              (I2)
1 #NAME               (A25)
1 #OWNER              (A25)
1 #SEG                (I2)           /* PM5
1 #DATE               (N8)           /* PM15
*
LOCAL
*
*  below is linkage to GOWNRSXX
*
1 #PDF-CHARS-100    (A100)           /* DW3
1 #TYPE-POL    (A10)                 /* DW1
1 #AAFMAA-ADDR (A52) INIT            /* DW1
        <'AAFMAA, 102 Sheridan Avenue, Fort Myer VA 22211-1110'>
1 #MULTIPLE-OWNER   (L)
1 #NON-INS-OWNER    (L)
1 #OWNER-OCC-9 (9)
 2 #OWNER-FLAG (A1)
 2 #OWNER-CN   (N6/10)
 2 REDEFINE #OWNER-CN
  3 #OWNER-CN-A (A6/10)
 2 #OWNER-CN-BY-POL (N6)
 2 #OWNER-CN-SEG    (I2/9)
 2 #ISS-DATE        (N8/9)
 2 #ISS-DATE-DISP   (A10/9)
*    beneficiary info output
 2 #BENE-TYPE       (A1/2)
 2 #BENE-TEXT       (A60/10)
 2 REDEFINE #BENE-TEXT
  3 #BENE-TEXT-OCC   (10)
   4 #BN-NAME        (A25)
   4 #BN-SSN         (N9)
   4 #BN-REL         (A1)
   4 #BN-SHARE       (A10)
   4 #BN-SET-OPT     (A1)
   4 #BN-RESTRICTION (A1)
   4 #BN-DOB         (A8)
   4 #BN-FILL-1      (A5)
*  above is linkage to GOWNRSXX
*
1 ST-V VIEW OF A-STATUS
 2 NAME
 2 SSN
 2 DATE-OF-DEATH
 2 ISSUE-DATE   (9)
 2 ANNUAL-PREM  (9)
 2 FACE-AMOUNT  (9)
 2 PLAN         (9)
 2 REDEFINE PLAN
  3 PLAN-OCC (9)
   4 PLAN-A1   (A1)
   4 PLAN-A2   (A2)
*
1 FR-V VIEW OF A-FIRS
 2 DEAD-GROSS-DB    (9)
 2 DEAD-LOAN-PRIN   (9)
 2 DEAD-LOAN-INT    (9)
 2 DEAD-PREM-ADJ    (9)
 2 DEAD-DB-CR       (9)
*
1 #CN    (N6)
1 REDEFINE #CN
 2 #CN-A   (A6)
*
1 #TOT-ADJ  (P7.2)   /* DW2
1 #USER     (A14)
1 #ADDL-INS      (P9.2)
1 #ADDL-INS-DISP (A12)
1 #NET-DB        (P9.2)
1 #FINAL-DB-DISP (A12)
1 #NET-PREM-W-O-DB-CR  (P5.2)
1 #NET-PREM-DISP  (A13)   /* only if positive
1 #LOAN-PRIN-DISP (A10)
1 #LOAN-INT-DISP (A10)
1 #GROSS-DB      (P9.2)
1 #EARNED-INT    (P9.2)
1 #FINAL-DB      (P9.2)
1 #FACE-AMT-DISP (A10)
1 #ANN-PREM-DISP (A10)
1 #EARNED-INT-DISP (A10)
1 #DOD-DISP      (A10)
1 #ISS-DT-DISP   (A10)
1 #INSURED-SSN   (A12)
1 #BENEFICIARY   (A60/2)
1 #LOAN-PRIN-5   (P7.2)              /* vt1 was p5.2
1 #FULL-NAME (A25)
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #MI        (A1)
1 #FIRST-MI  (A25)
1 #ERR-MSG      (A60)
1 #FILL-BLNKS (A78/24)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY       (N4)
 2 #MM         (N2)
 2 #DD         (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A     (A4)
 2 #MM-A       (A2)
 2 #DD-A       (A2)
*
1 #POL        (A16)
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 #I4         (I2)
1 #I5         (I2)
1 #I6         (I2)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
* WRITE 'top of c232XSRE'
* WRITE '#SEG-I=' #SEG-I 'OWNER=' #OWNER 'NAME' #NAME
*
MOVE #CGA-SCR-ID TO #CN
CALLNAT 'GOWNRSXX' #CN   /* bet beneficiaries
 #NON-INS-OWNER
 #MULTIPLE-OWNER
 #OWNER-OCC-9 (1:9)
*
* MOVE #SEG-I TO #I1                                                      /* PM5
#I1 := #SEG                                                               /* PM5
* WRITE 'test owner on' #I1 #CN #BENE-TYPE (#I1, 1) #BN-NAME (#I1, 1)
IF #BN-NAME (#I1, 1) NE ' '
  FOR #I2 1 TO 2
    IF #BENE-TYPE (#I1, 1) = 'B' OR = 'S'
      MOVE #BN-NAME (#I1, #I2) TO #FULL-NAME
      RESET #LAST #MI #FIRST-20
      CALLNAT 'GFMLNSXX' /* split name
       #FULL-NAME
       #FIRST-20
       #MI
       #LAST
       #ERR-MSG
*
      COMPRESS #FIRST-20 #MI #LAST INTO #BENEFICIARY (#I2)
    ELSE
      IF #I2 = 1   /* only room for 1 line
        IF #BENE-TYPE (#I1, 1) = 'P'
          MOVE #BENE-TEXT (#I1, 3) TO #PDF-CHARS-100                       /* DW4
          CALLNAT 'GPDFRSXX' #PDF-CHARS-100
          MOVE #PDF-CHARS-100 TO #BENEFICIARY (#I2)  /* trust name
        ELSE
          MOVE #BENE-TEXT (#I1, #I2) TO #PDF-CHARS-100                     /* DW4
          CALLNAT 'GPDFRSXX' #PDF-CHARS-100
          MOVE #PDF-CHARS-100 TO #BENEFICIARY (#I2) /* 1st line
        END-IF
      END-IF
    END-IF
  END-FOR
END-IF
*
MOVE #CGA-SCR-ID TO #CN
*
FIND ST-V WITH MEMBER-KEY = #CN-A
  CALLNAT 'GPLNMSXX' #POL #CN #I1
  MOVE ST-V.NAME TO #NAME
  RESET #LAST #MI #FIRST-20
  CALLNAT 'GFMLNSXX' /* split name
   #NAME
   #FIRST-20
   #MI
   #LAST
   #ERR-MSG
  COMPRESS #FIRST-20 #MI INTO #FIRST-MI
  MOVE EDITED ST-V.SSN (EM=999-99-9999) TO #INSURED-SSN
  MOVE DATE-OF-DEATH TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #DOD-DISP WITH '/'
  MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
  COMPRESS #MM-A #DD-A #YYYY-A INTO #ISS-DT-DISP WITH '/'
  MOVE EDITED ANNUAL-PREM (#I1) (EM=ZZZ,ZZ9.99) TO #ANN-PREM-DISP
  MOVE EDITED FACE-AMOUNT (#I1) (EM=ZZZ,ZZ9.99) TO #FACE-AMT-DISP
  MOVE LEFT #ANN-PREM-DISP TO #ANN-PREM-DISP
  MOVE LEFT #FACE-AMT-DISP TO #FACE-AMT-DISP
*
* IF  (PLAN (#I1) = 'LT' OR = 'RT')                        /* DW1     /* YK1
*  OR ((PLAN-A1 (#I1) GE 'U') AND (PLAN-A1 (#I1) LE 'Z'))  /* DW1     /* YK1
  IF FUNCTION-TERM-POLICY(<PLAN(#I1)>)                                /* YK1
    MOVE 'Term' TO #TYPE-POL                               /* DW1
  ELSE                                                     /* DW1
    MOVE 'Whole Life' TO #TYPE-POL                         /* DW1
  END-IF
END-FIND
*
FIND (1) FR-V WITH WIDOW-KEY = #CN-A
  COMPUTE #NET-DB = DEAD-GROSS-DB (#I1) + DEAD-PREM-ADJ (#I1)
    + DEAD-DB-CR (#I1) + DEAD-LOAN-PRIN (#I1) - DEAD-LOAN-INT (#I1)
END-FIND
*
MOVE DEAD-LOAN-PRIN (#I1) TO #LOAN-PRIN-5
MOVE DEAD-PREM-ADJ  (#I1) TO #NET-PREM-W-O-DB-CR
CALLNAT 'C2321SCI' #CN-A #SEG-I
     DATE-OF-DEATH
     #NET-PREM-W-O-DB-CR
     DEAD-DB-CR (#I1)
     #LOAN-PRIN-5
     DEAD-LOAN-INT (#I1)
     #GROSS-DB
     #NET-DB
     #EARNED-INT
     #FINAL-DB
     #DATE                                    /* PM15
*   WRITE 'calced int=' #EARNED-INT DATE-OF-DEATH #GROSS-DB
*
SUBTRACT #EARNED-INT FROM #FINAL-DB           /* DW3
RESET #EARNED-INT                             /* DW3
COMPUTE #ADDL-INS = DEAD-GROSS-DB (#I1) - FACE-AMOUNT (#I1)
MOVE EDITED #ADDL-INS  (EM=Z,ZZZ,ZZ9.99) TO #ADDL-INS-DISP
MOVE EDITED #FINAL-DB    (EM=Z,ZZZ,ZZ9.99) TO #FINAL-DB-DISP
*
IF DEAD-LOAN-PRIN (#I1) NE 0
  MOVE EDITED DEAD-LOAN-PRIN (#I1) (EM=ZZZ,ZZ9.99) TO #LOAN-PRIN-DISP
END-IF
*
IF DEAD-LOAN-INT  (#I1) NE 0
  MOVE EDITED DEAD-LOAN-INT  (#I1) (EM=ZZZ,ZZ9.99) TO #LOAN-INT-DISP
END-IF
*
IF #EARNED-INT NE 0
  MOVE EDITED #EARNED-INT (EM=ZZ,ZZ9.99) TO #EARNED-INT-DISP
END-IF
*
COMPUTE #TOT-ADJ = DEAD-PREM-ADJ (#I1) + DEAD-DB-CR (#I1)  /* DW2
MOVE EDITED #TOT-ADJ (EM=-Z,ZZZ,ZZ9.99) TO #NET-PREM-DISP
IF DEAD-PREM-ADJ (#I1) GT 0
  MOVE EDITED DEAD-PREM-ADJ (#I1) (EM=Z,ZZZ,ZZ9.99) TO #NET-PREM-DISP
END-IF
*
MOVE LEFT #ADDL-INS-DISP   TO #ADDL-INS-DISP
MOVE LEFT #FINAL-DB-DISP   TO #FINAL-DB-DISP
MOVE LEFT #LOAN-PRIN-DISP  TO #LOAN-PRIN-DISP
MOVE LEFT #LOAN-INT-DISP   TO #LOAN-INT-DISP
MOVE LEFT #EARNED-INT-DISP TO #EARNED-INT-DISP
MOVE LEFT #NET-PREM-DISP   TO #NET-PREM-DISP
*
MOVE *DATN TO #YYYYMMDD
COMPRESS #MM-A #DD-A #YYYY-A INTO #USER WITH '/'
COMPRESS #CGA-CLERK #USER INTO #USER
*
MOVE '%FDF-1.2' TO #FILL-BLNKS (1)
MOVE '1 0 obj << /FDF << /Fields [' TO #FILL-BLNKS (2)
COMPRESS #FILL-BLNKS (2) ' <</T(ID)/V(' #USER ')>>'
         INTO #FILL-BLNKS (2) LEAVING NO
COMPRESS '<</T(f1-1)/V(' #FIRST-MI ')>>'
         '<</T(f1-2)/V(' #LAST ')>>'
      INTO #FILL-BLNKS (3) LEAVING NO
COMPRESS '<</T(f1-3)/V(' #INSURED-SSN ')>>'
         '<</T(f1-4)/V(' #DOD-DISP ')>>'
      INTO #FILL-BLNKS (4) LEAVING NO
COMPRESS '<</T(f1-5)/V(' #AAFMAA-ADDR ')>>'
      INTO #FILL-BLNKS (5) LEAVING NO
COMPRESS '<</T(f1-7)/V(' #POL ')>>'
         '<</T(f1-8)/V(' #OWNER ')>>'
      INTO #FILL-BLNKS (6) LEAVING NO
COMPRESS '<</T(f1-9)/V(' #ISS-DT-DISP ')>>'
         '<</T(f1-13)/V(' #ANN-PREM-DISP ')>>'
      INTO #FILL-BLNKS (7) LEAVING NO
COMPRESS '<</T(f1-14)/V(' #BENEFICIARY (1)   /* join bene 1 & 2
      INTO #FILL-BLNKS (8) LEAVING NO
COMPRESS #FILL-BLNKS (8) #BENEFICIARY (2)
      INTO #FILL-BLNKS (8)
COMPRESS #FILL-BLNKS (8) ')>>' INTO #FILL-BLNKS (8) LEAVING NO
COMPRESS '<</T(f1-15)/V(' #FACE-AMT-DISP ')>>'
         '<</T(f1-17)/V(' #ADDL-INS-DISP ')>>'
      INTO #FILL-BLNKS (9) LEAVING NO
COMPRESS '<</T(f1-18)/V(' #EARNED-INT-DISP ')>>'
         '<</T(f1-19)/V(' #LOAN-PRIN-DISP ')>>'
      INTO #FILL-BLNKS (10) LEAVING NO
COMPRESS '<</T(f1-20)/V(' #LOAN-INT-DISP ')>>'
         '<</T(f1-23)/V(' #NET-PREM-DISP ')>>'
      INTO #FILL-BLNKS (11) LEAVING NO
COMPRESS '<</T(f1-24)/V(' #FINAL-DB-DISP ')>>'
         '<</T(f1-6)/V(' #TYPE-POL ')>>'
      INTO #FILL-BLNKS (12) LEAVING NO
*
MOVE '] /F (F:\IRS Form 712.PDF)>> >> endobj'
      TO #FILL-BLNKS (13)
MOVE'trailer <</Root 1 0 R >> %%EOF'  TO #FILL-BLNKS  (14)
*
INPUT USING MAP 'GPDFXMXX'
*
END
