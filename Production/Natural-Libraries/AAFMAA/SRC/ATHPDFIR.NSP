* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPDFIR
* <<<<<   see line 3230 to un create dallas file <<<<<<<<<<<<<<,
* remove all '*' when we know plane is out
*
*  THIS PROGRAM FORMATS THE ANNUAL FIR BATCH OUTPUT AND REDIRECT
*  IT TO THE DALLAS AREA PRINT CENTER.
*  INSERT#
*    1    - AFSC #9 BRE
*    2    - GENERATIONS BUCK SLIP
*    3    - SGLI BUCK SLIP
*    4    - NEW MEMBER BROCHURE
*    5    - NEW MEMBER QUESTIONNAIRE
*    6    - FAS INPUT FORM
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11292005 SS
LOCAL
*
*  INPUT WORK FILE - SCREEN IMAGES
*
1 #LAST-SRI       (A80)
1 SCREEN-REC-IN             (A80)
1 REDEFINE SCREEN-REC-IN
 2 SRI-CARRIAGE-CTL         (A1)
 2 SRI-DATA                 (A79)
 2 REDEFINE SRI-DATA
  3 SRI-FONT-CODE           (A1)
  3 SRI-COMMAND             (A2)
  3 SRI-FILL2               (A1)
  3 SRI-ID-NUMBER           (A6)
  3 REDEFINE SRI-ID-NUMBER
   4 SRI-ID-NUMBER-5        (A5)
   4 SRI-ID-NUMBER-LAST     (A1)
  3 SRI-FILL3               (A4)
  3 SRI-SUFFIX              (A3)
  3 REDEFINE SRI-SUFFIX
   4 SRI-SUFFIX-1           (N1)
  3 SRI-FILL4               (A4)
  3 SRI-FORM-CHECK          (A3)
  3 SRI-FILL5               (A1)
  3 SRI-REQ-FORM-CHECK      (A3)
  3 SRI-FILL6               (A17)
  3 SRI-PAGE-CNT            (N2)
 2 REDEFINE SRI-DATA
  3 SRI-POSTAGE-CHECK-1     (A1)
  3 SRI-POSTAGE-CHECK-2     (A7)
  3 SRI-FILL7               (A37)
  3 SRI-PAGE-AREA           (A3)
 2 REDEFINE SRI-DATA
  3 SRI-NO-RET-CHECK        (A14)
 2 REDEFINE SRI-DATA
  3 SRI-DATA-X              (A1/79)
*
*  OUTPUT WORK FILE - SCREEN IMAGES
*
1 SCREEN-REC-OUT            (A80)
1 REDEFINE SCREEN-REC-OUT
 2 SRO-CARRIAGE-CTL         (A1)
 2 SRO-DATA                 (A79)
 2 REDEFINE SRO-DATA
  3 SRO-FONT-CODE           (A1)
  3 SRO-COMMAND             (A2)
  3 SRO-FILL2               (A1)
  3 SRO-ID-NUMBER           (A6)
  3 SRO-FILL3               (A4)
  3 SRO-SUFFIX              (A3)
 2 REDEFINE SRO-DATA
  3 SRO-FILL4               (A38)
  3 SRO-OMR-CODE            (A3)
  3 SRO-FILL5               (A38)
 2 REDEFINE SRO-DATA
  3 SRO-FILL6               (A17)
  3 SRO-MCI-FORM            (A25)
  3 SRO-FILL7               (A37)
*
1 #CHECK-VARIABLES
  2 #ALL-ASTERICS           (A1) INIT <'*'>
*
1 #REPORT-SECTION
 2 #ANN-FIR-SENTENCE        (A29) INIT <'TOTAL ANNUAL FIRS PRINTED  = '>
 2 #WID-FIR-SENTENCE        (A29) INIT <'TOTAL WIDOW FIRS PRINTED   = '>
 2 #REQ-FIR-SENTENCE        (A29) INIT <'TOTAL REQ FIRS PRINTED     = '>
 2 #REQ-FIR-SSA-SENTENCE    (A29) INIT <'TOTAL REQ FIRS-SSA PRINTED = '>
 2 #REQ-FIR-DOP-SENTENCE    (A29) INIT <'TOTAL REQ FIRS-DOP PRINTED = '>
 2 #REQ-FAS-SENTENCE        (A29) INIT <'TOTAL REQ FAS PRINTED      = '>
 2 #REQ-CAP-FAS-SENTENCE    (A29) INIT <'TOTAL REQ CAP FAS PRINTED  = '>
 2 #REQ-MSG-SENTENCE        (A29) INIT <'TOTAL REQ MSG PRINTED      = '>
 2 #REQ-SSA-SENTENCE        (A29) INIT <'TOTAL REQ SSA MSG PRINTED  = '>
 2 #REQ-DOP-SENTENCE        (A29) INIT <'TOTAL REQ DOP MSG PRINTED  = '>
 2 #REQ-ACS-SENTENCE        (A29) INIT <'TOTAL REQ ACS PRINTED      = '>
 2 #REQ-MS2-SENTENCE        (A29) INIT <'TOTAL REQ MS2 PRINTED      = '>
 2 #REQ-PIL-SENTENCE        (A29) INIT <'TOTAL REQ PIL PRINTED      = '>
 2 #REQ-FAL-SENTENCE        (A29) INIT <'TOTAL REQ FAL PRINTED      = '>
 2 #REQ-RFS-SENTENCE        (A29) INIT <'TOTAL REQ RFS PRINTED      = '>
 2 #REQ-RFC-SENTENCE        (A29) INIT <'TOTAL REQ RFC PRINTED      = '>
 2 #REQ-RFZ-SENTENCE        (A29) INIT <'TOTAL REQ RFZ PRINTED      = '>
 2 #UND-ADD-SENTENCE        (A29) INIT <'TOTAL UNDELIV ADD LETTERS  = '>
 2 #NEW-MBR-SENTENCE        (A29) INIT <'TOTAL NEW MEMBER PACKETS   = '>
 2 #NEW-COM-SENTENCE        (A29) INIT <'TOTAL NEW AT COMBO LETTERS = '>
 2 #UNKNOWN-SENTENCE        (A29) INIT <'TOTAL UNKNOWNS PRINTED     = '>
 2 #TOTAL-SENTENCE          (A29) INIT <'TOTAL MAILINGS             = '>
 2 #ANN-FIR-CNT             (P5)
 2 #GEN-BUCK-SLIP           (P5)
 2 #SGLI-BUCK-SLIP          (P5)
 2 #WID-FIR-CNT             (P5)
 2 #REQ-FIR-CNT             (P5)
 2 #REQ-FIR-SSA-CNT         (P5)
 2 #REQ-FIR-DOP-CNT         (P5)
 2 #REQ-FAS-CNT             (P5)
 2 #REQ-CAP-FAS-CNT         (P5)
 2 #REQ-MSG-CNT             (P5)
 2 #REQ-SSA-CNT             (P5)
 2 #REQ-DOP-CNT             (P5)
 2 #REQ-ACS-CNT             (P5)
 2 #REQ-MS2-CNT             (P5)
 2 #REQ-PIL-CNT             (P5)
 2 #REQ-FAL-CNT             (P5)
 2 #REQ-RFS-CNT             (P5)
 2 #REQ-RFC-CNT             (P5)
 2 #REQ-RFZ-CNT             (P5)
 2 #UND-ADD-CNT             (P5)
 2 #NEW-MBR-CNT             (P5)
 2 #NEW-COM-CNT             (P5)
 2 #UNKNOWN-CNT             (P5)
 2 #TOTAL-MAILING-CNT       (N5)
 2 REDEFINE #TOTAL-MAILING-CNT
  3 #TOTAL-MAILING-CNT-A    (A5)
*
1 #CONSTANTS
 2 #UNKNOWN                 (L)
 2 #NO-RET-PACKETS          (A14) INIT <'NO RET PACKETS'>
 2 #DASHES                   (A3)  INIT <'___'>
*
1 #MCI-PAGE-CNT      (N3)
1 #PAGE-CNT        (N2)
1 REDEFINE #PAGE-CNT
 2 #PAGE-CNT-A     (A2)
1 #INSERT-TBL      (A7)
1 REDEFINE #INSERT-TBL
 2 #INSERT-IND (A1/7)
*
1 #INSERT-TABLE
 2 #INSERT1                 (A3)
 2 #INSERT2                 (A3)
 2 #INSERT3                 (A3)
 2 #INSERT4                 (A3)
 2 #INSERT5                 (A3)
 2 #INSERT6                 (A3)
 2 #INSERT7                 (A3)
 2 #INSERT8                 (A3)
*
1 #I1         (I2)
1 #LAST-FONT-CODE (A1)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR     /* SAG RH1 11292005 SS
*             / '*E R R O R - L I N E' *ERROR-LINE   /* SAG RH1 11292005 SS
INCLUDE GABNDCX3                                     /* SAG RH1 11292005 SS
END-ERROR
*
FORMAT LS=133 PS=66
*
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM 2X 'TOTAL MAILINGS CHECK PAGE'
          5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
* below for troy spacing
RESET SCREEN-REC-OUT
MOVE '1' TO SRO-CARRIAGE-CTL
WRITE WORK 2 SCREEN-REC-OUT
*
READ WORK 1 RECORD SCREEN-REC-IN
  IF SRI-POSTAGE-CHECK-1 = #ALL-ASTERICS
   OR SRI-POSTAGE-CHECK-2 = 'POSTAGE'
    ESCAPE TOP
  END-IF
*
  IF SRI-FONT-CODE = '1'        /* replaced EDS postal bar-code line
    MOVE '2' TO SRI-FONT-CODE   /* with TROY postal bar-code line
  END-IF
*
  IF #LAST-FONT-CODE = '2'    /* EDS postal bar code
    MOVE '1' TO SRI-FONT-CODE /* usually on addr line(to reset bar code)
    MOVE '1' TO #LAST-FONT-CODE
  END-IF
*
  IF #LAST-FONT-CODE = '1'    /* just did TROY postal bar code
   AND (SRI-DATA = ' ')       /* blank line under address
    MOVE '4' TO SRI-FONT-CODE /* return to regular fomt
    RESET #LAST-FONT-CODE
  END-IF
*
  IF SRI-FONT-CODE = '2'     /* TROY postal bar-code line
    MOVE SRI-FONT-CODE TO #LAST-FONT-CODE
    FOR #I1 1 TO 79
      IF SRI-DATA-X (#I1) = '!'   /* replace delimiter with "*"
        MOVE '*' TO SRI-DATA-X (#I1)
      END-IF
    END-FOR
  END-IF
*
  IF SRI-CARRIAGE-CTL = '1'                        /* top of page
    IF  (SRI-COMMAND = ' ' AND SRI-ID-NUMBER = ' ')
     OR (SRI-NO-RET-CHECK = #NO-RET-PACKETS)
      ESCAPE TOP
    END-IF
*
    IF   (#TOTAL-MAILING-CNT NE 0)  /* bypass on 1st page
     AND (NOT #UNKNOWN)             /* if going to TROY
      IF   (SRI-COMMAND = 'AD') /* to bump to new sheet at end of set
       AND (#MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
                       OR = 11 OR = 13 OR = 15 OR = 17 OR = 19)
        RESET SCREEN-REC-OUT    /* to force a blank page w no logo
        MOVE '1' TO SRO-CARRIAGE-CTL
        MOVE 'DJDE FORM=BLANKP,END;' TO SRO-DATA
        WRITE WORK 2 SCREEN-REC-OUT
        RESET SCREEN-REC-OUT
        MOVE '1' TO SRO-CARRIAGE-CTL
        WRITE WORK 2 SCREEN-REC-OUT
        RESET SCREEN-REC-OUT
        MOVE '1' TO SRO-CARRIAGE-CTL  /* force a blank page
        MOVE 'DJDE FORM=AFSC01,END;' TO SRO-DATA /* to return to logo
        WRITE WORK 2 SCREEN-REC-OUT
      END-IF
    END-IF
*
    IF SRI-COMMAND = 'AD'             /* 1st page of a mailing
      MOVE SCREEN-REC-IN TO #LAST-SRI
      RESET #PAGE-CNT #UNKNOWN #INSERT-TABLE #MCI-PAGE-CNT
      ADD 1 TO #TOTAL-MAILING-CNT
*
      DECIDE FOR FIRST CONDITION
        WHEN SRI-FORM-CHECK = 'AFR'  /* ANNUAL-FIR
          ADD +1 TO #ANN-FIR-CNT
*
          MOVE #DASHES TO #INSERT1
          IF SRI-REQ-FORM-CHECK = 'GEN'
            MOVE #DASHES TO #INSERT2  /* generations buck slip
            ADD +1 TO #GEN-BUCK-SLIP
          ELSE
            MOVE #DASHES TO #INSERT3  /* SGLI buck slip
            ADD +1 TO #SGLI-BUCK-SLIP
          END-IF
*
          MOVE SRI-PAGE-CNT TO #PAGE-CNT
          RESET SRI-PAGE-AREA
*
        WHEN SRI-FORM-CHECK = 'WFR'   /* #WIDOW-FIR
          ADD +1 TO #WID-FIR-CNT
          MOVE SRI-PAGE-CNT TO #PAGE-CNT
          RESET SRI-PAGE-AREA
*
        WHEN SRI-FORM-CHECK = 'RFR'   /* #REQUEST-FIR
          IF SRI-REQ-FORM-CHECK = 'L'   /* #FIRST-YEAR-FIR
            MOVE #DASHES TO #INSERT1
            ADD +1 TO #REQ-FIR-CNT
          END-IF
*
          IF SRI-REQ-FORM-CHECK = ' ' OR = 'MSG'
            ADD +1 TO #REQ-FIR-CNT
          END-IF
*
          IF SRI-REQ-FORM-CHECK = 'SSA'
            MOVE TRUE TO #UNKNOWN
            ADD +1 TO #UNKNOWN-CNT
            ADD +1 TO #REQ-FIR-SSA-CNT
          END-IF
*
          IF SRI-REQ-FORM-CHECK = 'DOP'
            MOVE #DASHES TO #INSERT1
            ADD +1 TO #REQ-FIR-DOP-CNT
          END-IF
*
          MOVE SRI-PAGE-CNT TO #PAGE-CNT
          RESET SRI-PAGE-AREA
*
        WHEN SRI-FORM-CHECK = 'FAS'
          MOVE #DASHES TO #INSERT1 #INSERT6
          IF SRI-REQ-FORM-CHECK = 'C'   /* #CAP-FAS (no-longer)
            ADD +1 TO #REQ-CAP-FAS-CNT
          ELSE
            ADD +1 TO #REQ-FAS-CNT
          END-IF
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'MSG'
          ADD +1 TO #REQ-MSG-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'SSA'
          MOVE TRUE TO #UNKNOWN
          ADD +1 TO #UNKNOWN-CNT
          ADD +1 TO #REQ-SSA-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'DOP'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-DOP-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'ACS'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-ACS-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'MS2'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-MS2-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'PIL'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-PIL-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'FAL'
          ADD +1 TO #REQ-FAL-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'RFS'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-RFS-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'RFC'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-RFC-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'RFZ'
          MOVE #DASHES TO #INSERT1
          ADD +1 TO #REQ-RFZ-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'UND'
          MOVE TRUE TO #UNKNOWN
          ADD +1 TO #UNKNOWN-CNT
          ADD +1 TO #UND-ADD-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN SRI-FORM-CHECK = 'NMP'   /* NEW MBR PACKET
          MOVE #DASHES TO #INSERT1 #INSERT4 #INSERT5
          ADD +1 TO #NEW-MBR-CNT
          MOVE SRI-SUFFIX-1 TO #PAGE-CNT   /* stored in aoap001d
          IF SRI-REQ-FORM-CHECK = 'FAS'
            MOVE #DASHES TO #INSERT6
          END-IF
*
        WHEN SRI-FORM-CHECK = 'COM' /* NEW AT CAP COMBO Ltr(ATHPATUP)
          MOVE #DASHES TO #INSERT4
          IF SRI-REQ-FORM-CHECK = 'BRE'   /* include BRE if A/S/Q
            MOVE #DASHES TO #INSERT1
          END-IF
*
          ADD +1 TO #NEW-COM-CNT
          MOVE 1 TO #PAGE-CNT
*
        WHEN NONE
          MOVE TRUE TO #UNKNOWN
          ADD +1 TO #UNKNOWN-CNT
      END-DECIDE
    END-IF
*
*   MOVE TRUE TO #UNKNOWN    /* remove whwn MCI ready
    ADD 1 TO #MCI-PAGE-CNT
  END-IF
*
  IF SRI-CARRIAGE-CTL = '1'
   AND NOT #UNKNOWN
    PERFORM PROCESS-OMR-CODES
  END-IF
*
  MOVE SCREEN-REC-IN TO SCREEN-REC-OUT
*
  IF #UNKNOWN
    WRITE WORK 3 SCREEN-REC-OUT
  ELSE
    WRITE WORK 2 SCREEN-REC-OUT
  END-IF
END-WORK
*
IF (NOT #UNKNOWN)             /* if going to mci-worldcom
 AND (#MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
                OR = 11 OR = 13 OR = 15 OR = 17 OR = 19)
  RESET SCREEN-REC-OUT    /* to force a blank page w no logo
  MOVE '1' TO SRO-CARRIAGE-CTL
  MOVE 'DJDE FORM=BLANKP,END;' TO SRO-DATA
  WRITE WORK 2 SCREEN-REC-OUT
END-IF
*
MOVE '10### Use AFSC 6 X 9 1/2 outside envelope' TO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
COMPRESS ' 0### Inserts 1=#9 AFSC BRE envl   2= Generations Buck Slip'
         '3=SGLI Buck Slip'    INTO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
COMPRESS ' 0### Inserts 4=Mew Member Brochure'
 '5= New MBR Questionaire  6=FAS Form' INTO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
COMPRESS ' 0### Total Mailings =' #TOTAL-MAILING-CNT
                                       INTO SCREEN-REC-OUT
WRITE WORK 2 SCREEN-REC-OUT
*
WRITE (2) #TOTAL-SENTENCE #TOTAL-MAILING-CNT
*
WRITE #REQ-FIR-SENTENCE #REQ-FIR-CNT
WRITE #REQ-FIR-SSA-SENTENCE #REQ-FIR-SSA-CNT
WRITE #REQ-FIR-DOP-SENTENCE #REQ-FIR-DOP-CNT
WRITE #REQ-FAS-SENTENCE #REQ-FAS-CNT
WRITE #REQ-CAP-FAS-SENTENCE #REQ-CAP-FAS-CNT
WRITE #REQ-MSG-SENTENCE #REQ-MSG-CNT
WRITE #REQ-SSA-SENTENCE #REQ-SSA-CNT
WRITE #REQ-DOP-SENTENCE #REQ-DOP-CNT
WRITE #REQ-ACS-SENTENCE #REQ-ACS-CNT
WRITE #REQ-MS2-SENTENCE #REQ-MS2-CNT
WRITE #REQ-PIL-SENTENCE #REQ-PIL-CNT
WRITE #REQ-FAL-SENTENCE #REQ-FAL-CNT
WRITE #REQ-RFS-SENTENCE #REQ-RFS-CNT
WRITE #REQ-RFC-SENTENCE #REQ-RFC-CNT
WRITE #REQ-RFZ-SENTENCE #REQ-RFZ-CNT
WRITE #UND-ADD-SENTENCE #UND-ADD-CNT
WRITE #NEW-MBR-SENTENCE #NEW-MBR-CNT
WRITE #NEW-COM-SENTENCE #NEW-COM-CNT
WRITE #ANN-FIR-SENTENCE #ANN-FIR-CNT
WRITE 'GEN buck slips'  #GEN-BUCK-SLIP
WRITE 'SGLI buck slips' #SGLI-BUCK-SLIP
WRITE #WID-FIR-SENTENCE #WID-FIR-CNT
WRITE #UNKNOWN-SENTENCE #UNKNOWN-CNT
*
* * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-OMR-CODES  /* at top of pages
* * * * * * * * * * * * * * * * * *
*
IF (#MCI-PAGE-CNT =  1 OR =  3 OR =  5 OR =  7 OR =  9
               OR = 11 OR = 13 OR = 15 OR = 17 OR = 19)
  RESET SCREEN-REC-OUT
  MOVE '1' TO SRO-CARRIAGE-CTL       /* line 1
  MOVE '3' TO SRO-FONT-CODE
  MOVE #DASHES TO SRO-OMR-CODE   /* gate/read verify
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 2
  MOVE '3' TO SRO-FONT-CODE
*
  IF #MCI-PAGE-CNT NE 1
    MOVE #DASHES TO SRO-OMR-CODE /* demand feed (only non pg 1)
  END-IF
*
  WRITE WORK 2 SCREEN-REC-OUT
  RESET SRI-CARRIAGE-CTL   /* to put first AD line on same page
  MOVE '4' TO SRI-FONT-CODE   /* text font code
ELSE
  RESET SCREEN-REC-OUT
  MOVE '1' TO SRO-CARRIAGE-CTL  /* space down on non page 1 to get
  WRITE WORK 2 SCREEN-REC-OUT   /*   below logo
END-IF
*
IF #MCI-PAGE-CNT = 1
  RESET SCREEN-REC-OUT               /* line 3
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT1 TO SRO-OMR-CODE     /* insert 1
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 4
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT2 TO SRO-OMR-CODE     /* insert 2
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 5
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT3  TO SRO-OMR-CODE     /* insert 3
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 6
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT4  TO SRO-OMR-CODE     /* insert 4
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 7
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT5  TO SRO-OMR-CODE     /* insert 5
  WRITE WORK 2 SCREEN-REC-OUT
*
  RESET SCREEN-REC-OUT               /* line 8
  MOVE '3' TO SRO-FONT-CODE
  MOVE #INSERT6  TO SRO-OMR-CODE     /* insert 6
  WRITE WORK 2 SCREEN-REC-OUT
  MOVE '4' TO SRI-FONT-CODE   /* text font code
  RESET SRI-CARRIAGE-CTL   /* to put first AD line on same page
ELSE
  RESET SCREEN-REC-OUT
  MOVE '-' TO SRO-CARRIAGE-CTL   /* space down on non page 1 to get
  WRITE WORK 2 SCREEN-REC-OUT    /*   below logo
  MOVE '-' TO SRO-CARRIAGE-CTL
  WRITE WORK 2 SCREEN-REC-OUT
  MOVE '0' TO SRI-CARRIAGE-CTL
  MOVE '4' TO SRI-FONT-CODE     /* text font code
END-IF
*
END-SUBROUTINE
*
END
