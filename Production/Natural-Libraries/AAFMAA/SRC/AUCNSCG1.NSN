* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNSCG1.
/**
/** :author nguentchev
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON                                         *
/** NGG 07/18/2012 NG1 Limit number of found contacts for Org name
DEFINE DATA
PARAMETER USING AUCAPI01
PARAMETER
1 #XML_OUTPUT          (A) DYNAMIC
LOCAL
1 CNT-V VIEW OF A-CONTACTS
  2 ID-NUMBER
  2 CONTACT-ID
  2 CONTACT-TYPE
  2 DATE-OF-BIRTH
  2 FIRST-NAME
  2 LAST-NAME
  2 MIDDLE-NAME
  2 SUFFIX
  2 PREFIX-TITLE
  2 SSN
  2 FIRM-ORGANIZATION-NAME
1 BLANK                (A1) CONST <' '>
1 #GET                 (I4) CONST <0>
1 #FOUND               (I4)
1 #XML-TEMP            (A) DYNAMIC
1 #LastName            (A) DYNAMIC
END-DEFINE
*
RESET #FOUND
COMPRESS FULL '<?xml version="1.0" encoding="' *CODEPAGE '"?>'
  INTO #XML_OUTPUT LEAVING NO
COMPRESS FULL #XML_OUTPUT  '<SearchResultContacts'
  '>' INTO #XML_OUTPUT LEAVING NO
*
DECIDE FOR FIRST CONDITION
  WHEN PersonalInfo.CN NE 0
    PERFORM SEARCH-BY-CN
  WHEN PersonalInfo.ContactId NE 0
    PERFORM SEARCH-BY-CI
  WHEN PersonalInfo.SSN NE 0
    PERFORM SEARCH-BY-SSN
  WHEN PersonalInfo.OrganizationName NE BLANK
    PERFORM SEARCH-BY-ORG
  WHEN PersonalInfo.DateOfBirth NE 0
    PERFORM SEARCH-BY-DOB
  WHEN NONE IGNORE
END-DECIDE
*
IF #FOUND <= 0
  COMPRESS FULL #XML_OUTPUT
    '<ErrorCode'
    '>' INTO #XML_OUTPUT LEAVING NO
  COMPRESS NUMERIC #XML_OUTPUT
    '1'
    '</ErrorCode>' INTO #XML_OUTPUT LEAVING NO
  COMPRESS FULL #XML_OUTPUT
    '<ErrorDescription'
    '>' INTO #XML_OUTPUT LEAVING NO
  COMPRESS FULL #XML_OUTPUT
    'No Contacts found'
    '</ErrorDescription>' INTO #XML_OUTPUT LEAVING NO
END-IF
COMPRESS FULL #XML_OUTPUT  '</SearchResultContacts'
  '>' INTO #XML_OUTPUT LEAVING NO
/*
DEFINE SEARCH-BY-CN
/*     ============
FIND CNT-V WITH ID-NUMBER = PersonalInfo.CN
  RESET PersonalInfo
  PersonalInfo.ContactID := CNT-V.CONTACT-ID
  PersonalInfo.ActionType := #GET
  CALLNAT 'AUCNPIM1' PersonalInfo
  RESET #XML-TEMP
  CALLNAT 'AUCNPIS1' #XML-TEMP PersonalInfo
  COMPRESS  #XML_OUTPUT #XML-TEMP INTO #XML_OUTPUT
  ADD 1 TO #FOUND
END-FIND
END-SUBROUTINE
/*
/*
DEFINE SEARCH-BY-CI
/*     ============
FIND CNT-V WITH CONTACT-ID = PersonalInfo.ContactId
  RESET PersonalInfo
  PersonalInfo.ContactID := CNT-V.CONTACT-ID
  PersonalInfo.ActionType := #GET
  CALLNAT 'AUCNPIM1' PersonalInfo
  RESET #XML-TEMP
  CALLNAT 'AUCNPIS1' #XML-TEMP PersonalInfo
  COMPRESS  #XML_OUTPUT #XML-TEMP INTO #XML_OUTPUT
  ADD 1 TO #FOUND
END-FIND
END-SUBROUTINE
/*
/*
DEFINE SEARCH-BY-SSN
/*     =============
FIND CNT-V WITH SSN = PersonalInfo.SSN
  RESET PersonalInfo
  PersonalInfo.ContactID := CNT-V.CONTACT-ID
  PersonalInfo.ActionType := #GET
  CALLNAT 'AUCNPIM1' PersonalInfo
  RESET #XML-TEMP
  CALLNAT 'AUCNPIS1' #XML-TEMP PersonalInfo
  COMPRESS  #XML_OUTPUT #XML-TEMP INTO #XML_OUTPUT
  ADD 1 TO #FOUND
END-FIND
END-SUBROUTINE
/*
/*
DEFINE SEARCH-BY-ORG
/*     =============
FIND CNT-V WITH FIRM-ORGANIZATION-NAME = PersonalInfo.OrganizationName
  RESET PersonalInfo
  PersonalInfo.ContactID := CNT-V.CONTACT-ID
  PersonalInfo.ActionType := #GET
  CALLNAT 'AUCNPIM1' PersonalInfo
  RESET #XML-TEMP
  CALLNAT 'AUCNPIS1' #XML-TEMP PersonalInfo
  COMPRESS  #XML_OUTPUT #XML-TEMP INTO #XML_OUTPUT
  ADD 1 TO #FOUND
  IF #FOUND > 10      /* NG1
    ESCAPE BOTTOM
  END-IF
END-FIND
END-SUBROUTINE
/*
DEFINE SEARCH-BY-DOB
/*     =============
IF PersonalInfo.LastName NE BLANK
  #LastName := PersonalInfo.LastName
ELSE
  #LastName := '*'
END-IF
FIND CNT-V WITH DATE-OF-BIRTH = PersonalInfo.DateOfBirth
  IF #LastName NE '*' AND #LastName NE CNT-V.LAST-NAME
    ESCAPE TOP
  END-IF
  RESET PersonalInfo
  PersonalInfo.ContactID := CNT-V.CONTACT-ID
  PersonalInfo.ActionType := #GET
  CALLNAT 'AUCNPIM1' PersonalInfo
  RESET #XML-TEMP
  CALLNAT 'AUCNPIS1' #XML-TEMP PersonalInfo
  COMPRESS  #XML_OUTPUT #XML-TEMP INTO #XML_OUTPUT
  ADD 1 TO #FOUND
END-FIND
END-SUBROUTINE
/*
END

