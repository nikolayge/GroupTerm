* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPCCUP    ** UPDATE COMMAND ACCESS COUNTS ON "NX" REC  **
DEFINE DATA
 LOCAL  USING ATHLCCUP
 LOCAL  USING GONERLXX
 LOCAL
*
1 #JCL-PARM       (A32)
1 REDEFINE #JCL-PARM
 2 #JCL-DAILY-MONTHLY (A7)     /* 'DAILY  ' OR 'MONTHLY'
1 REDEFINE #JCL-PARM
 2 #JCL-MM            (N2)   /* DATE PARM 03011990123456789N ETC
 2 #JCL-DD            (N2)
 2 #JCL-YYYY          (N4)
*
1 #DAILY-MONTHLY (A7)
1 #LAST-MM       (N2)
1 #I1            (I2)
1 #I2            (I2)
*
1 #TOT-ACCESSES (P7)
1 #TOT-MTD-ACCESSES (P7)
1 #TOT-ACCESSES-SVC (P7)     /* PM - 06/2000 - SPLIT COMPANIES
1 #TOT-MTD-ACCESSES-SVC (P7) /* PM - 06/2000 - SPLIT COMPANIES
*
1 #DATN         (N8)
1 REDEFINE #DATN
 2 #DATN-CC     (N2)
 2 #DATN-YY     (N2)
 2 #DATN-MM     (N2)
 2 #DATN-DD     (N2)
*
1 #TIMN         (N7)
1 REDEFINE #TIMN
 2 #TIMN-HH     (A2)
 2 REDEFINE #TIMN-HH
  3 #TIMN-HH-N  (N2)
 2 #TIMN-MM     (A2)
 2 #TIMN-SS     (A2)
 2 #TIMN-T      (A1)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT LS=133 PS=76
WRITE  TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                  'UPDATE CC COUNTS ON "NX" RECORD - INS'
                  5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
                      'UPDATE CC COUNTS ON "NX" RECORD - SVC'
                      5X 'DATE: ' *DATN 5X 'TIME: ' *TIMX
*
MOVE *DATN TO #DATN
*
IF *TIMN LT 0600000       /* BEFORE 6 AM
  SUBTRACT 1 FROM #DATN-DD
  IF #DATN-DD LT 1
    MOVE 31 TO #DATN-DD
    SUBTRACT 1 FROM #DATN-MM
    IF #DATN-MM LT 1
      SUBTRACT 1 FROM #DATN-YY
    END-IF
  END-IF
END-IF
*
READ WORK 1 ONCE RECORD #JCL-PARM
MOVE #JCL-DAILY-MONTHLY TO #DAILY-MONTHLY
READ WORK 1 ONCE RECORD #JCL-PARM
READ WORK 1 ONCE RECORD #JCL-PARM
COMPUTE #LAST-MM = #JCL-MM - 1
*
IF #LAST-MM LT 1
  MOVE 12 TO #LAST-MM
END-IF
*
READ CM-V BY COMMAND
  IF #DAILY-MONTHLY = 'DAILY'
    ADD NUM-ACCESSES     TO NUM-ACCESSES-MTD
    WRITE COMMAND NUM-ACCESSES NUM-ACCESSES-MTD
    ADD NUM-ACCESSES     TO #TOT-ACCESSES
    ADD NUM-ACCESSES-MTD TO #TOT-MTD-ACCESSES
    RESET NUM-ACCESSES
*
* PM - 06/2000 - SPLIT COMPANIES
*
    ADD NUM-ACCESSES-SVC     TO NUM-ACCESSES-MTD-SVC
    WRITE (2) COMMAND NUM-ACCESSES-SVC NUM-ACCESSES-MTD-SVC
    ADD NUM-ACCESSES-SVC     TO #TOT-ACCESSES-SVC
    ADD NUM-ACCESSES-MTD-SVC TO #TOT-MTD-ACCESSES-SVC
    RESET NUM-ACCESSES-SVC
  ELSE
    IF #DAILY-MONTHLY = 'MONTHLY'
      ADD NUM-ACCESSES     TO NUM-ACCESSES-MTD
      WRITE COMMAND NUM-ACCESSES NUM-ACCESSES-MTD
      ADD NUM-ACCESSES     TO #TOT-ACCESSES
      ADD NUM-ACCESSES-MTD TO #TOT-MTD-ACCESSES
      RESET NUM-ACCESSES
      RESET NUM-ACCESSES-MTD
*
* PM - 06/2000 - SPLIT COMPANIES
*
      ADD NUM-ACCESSES-SVC     TO NUM-ACCESSES-MTD-SVC
      WRITE (2) COMMAND NUM-ACCESSES-SVC NUM-ACCESSES-MTD-SVC
      ADD NUM-ACCESSES-SVC     TO #TOT-ACCESSES-SVC
      ADD NUM-ACCESSES-MTD-SVC TO #TOT-MTD-ACCESSES-SVC
      RESET NUM-ACCESSES-SVC
      RESET NUM-ACCESSES-MTD-SVC
    END-IF
  END-IF
*
  UPDATE (0082)
  END TRANSACTION
END-READ
WRITE 'TOTALS' #TOT-ACCESSES #TOT-MTD-ACCESSES
WRITE (2) 'TOTALS' #TOT-ACCESSES-SVC #TOT-MTD-ACCESSES-SVC
*
FIND (1) NT-V WITH ID-NUMBER = 800002
  FOR #I1 1 TO 31
    IF  ((NOTE-DATE-MM (1) NE #DATN-MM)   /* MONTH-END OVERLAP
     AND ((#I1 GT NOTE-DATE-DD (1))            /* PAST LAST WORKING-DAY
      OR (#I1 LT #DATN-DD))                    /* BEFORE TODAY
     OR                                   /* WITHIN CURRENT MONTH
        ((#I1 GT NOTE-DATE-DD (1))   /* BETWEEN LAST WORKING DAY
     AND (#I1 LT #DATN-DD)))       /* TODAY
      IF #I1 LE 15
        RESET NT-V.COMMAND-CNTS (1, #I1)
      ELSE
        IF #I1 LE 30
          RESET NT-V.COMMAND-CNTS (2, #I1 - 15)
        ELSE
          RESET NT-V.COMMAND-CNTS (3, 1)
        END-IF
      END-IF
    END-IF
*
* PM - 06/2000 - SPLIT COMPANIES
*
    IF  ((NOTE-DATE-MM (5) NE #DATN-MM)   /* MONTH-END OVERLAP
     AND ((#I1 GT NOTE-DATE-DD (5))            /* PAST LAST WORKING-DAY
      OR (#I1 LT #DATN-DD))                    /* BEFORE TODAY
     OR                                   /* WITHIN CURRENT MONTH
        ((#I1 GT NOTE-DATE-DD (5))   /* BETWEEN LAST WORKING DAY
     AND (#I1 LT #DATN-DD)))       /* TODAY
      IF #I1 LE 15
        RESET NT-V.COMMAND-CNTS (5, #I1)
      ELSE
        IF #I1 LE 30
          RESET NT-V.COMMAND-CNTS (6, #I1 - 15)
        ELSE
          RESET NT-V.COMMAND-CNTS (7, 1)
        END-IF
      END-IF
    END-IF
  END-FOR
*
  MOVE #DATN TO NOTE-DATE (1)   /* UPDATE CURRENT WORKING-DATE
  MOVE #DATN TO NOTE-DATE (5)   /* PM - SPLIT FOR SERVICES
  IF #DAILY-MONTHLY = 'DAILY'
    IF #DATN-DD LE 15
      MOVE #TOT-ACCESSES TO NT-V.COMMAND-CNTS (1, #DATN-DD)
      MOVE #TOT-ACCESSES-SVC TO NT-V.COMMAND-CNTS (5, #DATN-DD) /* PM
    ELSE
      IF #DATN-DD LE 30
       MOVE #TOT-ACCESSES TO NT-V.COMMAND-CNTS (2, #DATN-DD - 15)
       MOVE #TOT-ACCESSES-SVC TO NT-V.COMMAND-CNTS (6, #DATN-DD - 15) /*
      ELSE
        MOVE #TOT-ACCESSES TO NT-V.COMMAND-CNTS (3, 1)
        MOVE #TOT-ACCESSES-SVC TO NT-V.COMMAND-CNTS (7, 1) /* PM
      END-IF
    END-IF
  ELSE
    IF #DAILY-MONTHLY = 'MONTHLY'
      MOVE #TOT-MTD-ACCESSES TO NT-V.COMMAND-CNTS (4, #LAST-MM)
      MOVE #TOT-MTD-ACCESSES-SVC TO NT-V.COMMAND-CNTS (8, #LAST-MM) /*PM
    END-IF
  END-IF
*
  UPDATE (0123)
  END TRANSACTION
END-FIND
*
END
