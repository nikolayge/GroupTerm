* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPBILL  *** Convert Billing by line
DEFINE DATA
* CHG line 4320 to reset old fields <<<<<<<<<<<<<<<<<<
* CHG line 3140 & 3670 to fill debit-credit amt<<<<<<<<<<<<<<
* bz  06/09/2013  Rebranding (Search for AAFMAA)
* TMT 06/10/2014 TT1  CONTACT NORMALIZATION - Gender and Birthday
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL  USING GONERLXX
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-A  (A6)
 2 PROCESS-IND
 2 INTRO-FLAG
 2 NAME
 2 MILITARY-STATUS
* 2 DATE-OF-BIRTH            /* TT1
 2 X-BILL-ADJUST     (9)
 2 X-FIRST-PLACE-FEE (9)
 2 X-BILL-DOLLAR     (9)
 2 X-RID-FPF         (9)
 2 X-RID-BALANCE     (9)
 2 STATUS        (9)
 2 PLAN          (9)
 2 ISSUE-DATE    (9)
 2 SPLIT-IND     (9)
 2 MODE          (9)
 2 PAY-STATUS    (9)
 2 LAST-DURATION (9)
 2 ANNUAL-PREM   (9)
 2 RID-FPF
 2 RID-BALANCE
 2 RID-PLAN      (9)
 2 FIRST-PLACE-FEE
 2 BILL-ADJUST
 2 BILL-DOLLAR
 2 CAP-ENTRY-DATE
 2 DEBIT-CREDIT-AMT
 2 PAD-CODE
*
1 #CNT-2        (N6)
1 #CNT-3        (N6)
1 #CNT-4        (N6)
1 #CNT-5        (N6)
1 #CNT-6        (N6)
1 #CNT-7        (N6)
1 #CNT-8        (N6)
1 #CNT-9        (N6)
1 #CNT-10       (N6)
1 #CNT-11       (N6)
1 #I1           (I2)
1 #I2           (I2)
1 #I3           (I2)
1 #SEP          (A1)
1 #NUM-PP-ASQM  (P1)
1 #NUM-POLS     (P1)
1 #BADJ-W-1-ASQM (P5)
1 #FPF-W-1-POL   (P5)
1 #MOD           (A1)
1 #ASQ-CNT       (P3)
1 #UPDATE        (L)
1 #HTR-FPF       (L)
1 #HTR-BADJ      (L)
1 #MULT-ASQ-CNT  (P5)
1 #MULT-A-CNT  (P5)
1 #MULT-S-CNT  (P5)
1 #MULT-Q-CNT  (P5)
1 #MULT-M-CNT  (P5)
1 #EXTRA-A-CNT  (P5)
1 #EXTRA-S-CNT  (P5)
1 #EXTRA-Q-CNT  (P5)
1 #EXTRA-M-CNT  (P5)
1 #SING-ASQ-CNT (P5)
1 #FST-PP-ASQ-X (I2)
1 #LST-PP-ASQ-X (I2)
1 #LST-PP-X     (I2)
1 #FPF-X          (I2)
1 #RT-X           (I2)
1 #BADJ-X         (I2)
1 #ASQ-X          (I2)
1 #QUOTIENT  (N5.7)
1 REDEFINE #QUOTIENT
 2  #QUOTIENT-5   (N5)
 2  #QUOTIENT-7   (N7)
*
1 #ISSUEDT-MODE-PAYSTAT    (A78)
1 REDEFINE #ISSUEDT-MODE-PAYSTAT
 2 #IMP    (5)
  3 #ISS-DT (A9)
  3 #MODE   (A1)
  3 #PAY-ST (A2)
  3 #PLAN   (A3)
*
END-DEFINE
* * * * * * * *
INCLUDE GONERCXX
* * * * * * * *
FORMAT    PS=66  LS=151
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM 5X 'DATE:' *DAT4U
                  5X 'TIME:' *TIMX
*
FORMAT (1) PS=66  LS=151
WRITE  (1) TITLE LEFT 'PROGRAM:' *PROGRAM 5X 'DATE:' *DAT4U
                  5X 'TIME:' *TIMX
*
FORMAT(2) PS=66 LS=151
WRITE (2) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(2) /
                     45T 'MBRS with hard to resolve BILL ADJUSTMENT'
*
FORMAT(3) PS=66 LS=151
WRITE (3) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(3) /
          40T 'Inactive or no PP pols MBRS with FPF/BADJ moved to DB/CR'
*
FORMAT(4) PS=66 LS=151
WRITE (4) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(4) /
                     43T 'MBRS with hard to resolve FPF'
*
FORMAT(5) PS=66 LS=151
WRITE (5) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(5) /
                     40T 'MBRS with RIDER BAL or RIDER FPF'
*
FORMAT(6) PS=66 LS=151
WRITE (6) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'MBRS with 2 pols w same Issue Date'
*
FORMAT(7) PS=66 LS=151
WRITE (7) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'MBRS with both BILL-ADJ and DEBIT-CREDIT'
*
FORMAT(8) PS=66 LS=151
WRITE (8) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'MBRS with multiple A/S/Q/M policies'
*
FORMAT(9) PS=66 LS=151
WRITE (9) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'Conversion Listing'
*
FORMAT(10) PS=66 LS=151
WRITE (10) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'Hard to Resolve BADJ Conversion Listing'
*
FORMAT(11) PS=66 LS=151
WRITE (11) TITLE LEFT 40T 'AAFMAA'
                     91T 'PAGE' 96T *PAGE-NUMBER(6) /
                     40T 'Hard to Resolve FPF Conversion Listing'
*
READ ST-V BY MEMBER-KEY STARTING FROM '001900'
* IF ST-V.ID-NUMBER GT 80945
*   ESCAPE BOTTOM
* END-IF
*
*  IF ID-NUMBER GE 600000
*    END TRANSACTION
*    ESCAPE BOTTOM
*  END-IF
*
   RESET X-BILL-ADJUST     (*)
         X-FIRST-PLACE-FEE (*)
         X-BILL-DOLLAR     (*)
         X-RID-FPF         (*)
         X-RID-BALANCE     (*)
  IF PROCESS-IND = 'C'
    PERFORM CLEAR-OLD-FLDS
    UPDATE (0163)
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  FOR #I1 1 TO 8  /* ALL BUT LAST
    IF STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    COMPUTE #I3 = #I1 + 1
    FOR #I2 #I3 TO 9
      IF   (ISSUE-DATE (#I1) = ISSUE-DATE (#I2))
*      AND (MODE (#I1) = 'A' OR = 'S' OR = 'Q' OR = 'M')
*       AND (PAY-STATUS (#I1) LT '40')
        AND (NOT PLAN (#I1) = 'LT' OR = 'RT')
        AND (NOT PLAN (#I2) = 'LT' OR = 'RT')
        ADD 1 TO #CNT-6
        DISPLAY (6) #CNT-6    /* duplicate issue dates
         'CN' ST-V.ID-NUMBER
         'LAST/ISS-DT' ISSUE-DATE (#I1) 'LAST/SPL' SPLIT-IND (#I1)
         'LAST/PLAN' PLAN (#I1) 'LAST/MODE' MODE (#I1)
         'LAST/PAY/STAT' PAY-STATUS (#I1)
         '*' #SEP
         'SPL/IND' SPLIT-IND (#I2)
          PLAN(#I2) MODE(#I2) 'PAY/STAT' PAY-STATUS (#I2)
      END-IF
    END-FOR
  END-FOR
*
  RESET #ASQ-CNT #MOD #FPF-X #BADJ-X #UPDATE #FST-PP-ASQ-X #LST-PP-ASQ-X
        #RT-X #HTR-FPF #HTR-BADJ #LST-PP-X
  FOR #I1 1 TO 9
    IF (FIRST-PLACE-FEE NE 0) AND (ANNUAL-PREM (#I1) NE 0)
     AND (STATUS (#I1) = 'D')
      DIVIDE (ST-V.ANNUAL-PREM (#I1) / 12) INTO FIRST-PLACE-FEE
           GIVING #QUOTIENT
*
      IF #QUOTIENT-7 = 0
        WRITE ST-V.ID-NUMBER #I1 'FPF mult of mon prem'
           'FPF=' FIRST-PLACE-FEE 'Ann-Prem=' ANNUAL-PREM (#I1)
           'Quot=' #QUOTIENT
        MOVE #I1 TO #FPF-X
      END-IF
    END-IF
*
    IF   (PAY-STATUS (#I1) LT '40')
     AND (STATUS (#I1) = 'D')
      MOVE #I1 TO #LST-PP-X
    END-IF
*
    IF   (FIRST-PLACE-FEE NE 0)
     AND (#FPF-X = 0)
     AND (LAST-DURATION (#I1) = 0)
     AND (PAY-STATUS (#I1) = '47')
     AND (STATUS (#I1) = 'D')
      WRITE ST-V.ID-NUMBER #I1 'FPF on new PUSP'
           'FPF=' FIRST-PLACE-FEE
      MOVE #I1 TO #FPF-X
    END-IF
*
    IF   (MODE (#I1) = 'A' OR = 'S' OR = 'Q' OR = 'M')
     AND (PAY-STATUS (#I1) LT '40')
     AND (STATUS (#I1) = 'D')
      MOVE MODE (#I1) TO #MOD
      MOVE #I1 TO #ASQ-X
      MOVE #I1 TO #LST-PP-ASQ-X
*
      IF #FST-PP-ASQ-X = 0
        MOVE #I1 TO #FST-PP-ASQ-X
      END-IF
*
      ADD 1 TO #ASQ-CNT
*
      IF (BILL-ADJUST NE 0) AND (ANNUAL-PREM (#I1) NE 0)
        DIVIDE (ST-V.ANNUAL-PREM (#I1) / 12) INTO BILL-ADJUST
             GIVING #QUOTIENT
*
        IF #QUOTIENT-7 = 0
          WRITE ST-V.ID-NUMBER #I1 'BADJ mult of prem'
             'Badj=' BILL-ADJUST 'Ann-Prem=' ANNUAL-PREM (#I1)
             'Quot=' #QUOTIENT
          MOVE #I1 TO #BADJ-X
        END-IF
*
        IF (#BADJ-X = 0) AND (PLAN (#I1) = 'RT')
         AND (ISSUE-DATE (#I1) LT 20000101)
          WRITE ST-V.ID-NUMBER #I1 'RT bill-adjust found'
             'Badj=' BILL-ADJUST 'Ann-Prem=' ANNUAL-PREM (#I1)
          MOVE #I1 TO #RT-X
        END-IF
      END-IF
    END-IF
  END-FOR
*
  IF #ASQ-CNT GT 1
    ADD 1 TO #MULT-ASQ-CNT
    IF #MOD = 'A'
      ADD 1 TO #MULT-A-CNT
      COMPUTE #EXTRA-A-CNT = #EXTRA-A-CNT + (#ASQ-CNT - 1)
    END-IF
    IF #MOD = 'Q'
      ADD 1 TO #MULT-Q-CNT
      COMPUTE #EXTRA-Q-CNT = #EXTRA-Q-CNT + (#ASQ-CNT - 1)
    END-IF
    IF #MOD = 'S'
      ADD 1 TO #MULT-S-CNT
      COMPUTE #EXTRA-S-CNT = #EXTRA-A-CNT + (#ASQ-CNT - 1)
    END-IF
    IF #MOD = 'M'
      ADD 1 TO #MULT-M-CNT
      COMPUTE #EXTRA-M-CNT = #EXTRA-M-CNT + (#ASQ-CNT - 1)
    END-IF
*
    ADD 1 TO #CNT-8
    DISPLAY (8) #CNT-8
      'CN'     ST-V.ID-NUMBER
      'mode'   #MOD
      '# bills' #ASQ-CNT
  ELSE
    IF #ASQ-CNT = 1
      ADD 1 TO #SING-ASQ-CNT
    END-IF
  END-IF
*
  IF FIRST-PLACE-FEE = 0
   AND BILL-ADJUST = 0
   AND RID-FPF = 0
   AND RID-BALANCE = 0
    PERFORM CLEAR-OLD-FLDS
    UPDATE (0163)
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  RESET #ISSUEDT-MODE-PAYSTAT
        #NUM-PP-ASQM #NUM-POLS #I2
  FOR #I1 1 TO 9   /* build tbl of issue dates
    IF STATUS (#I1) NE 'D'
      ESCAPE TOP
    END-IF
*
    ADD 1 TO #NUM-POLS
    ADD 1 TO #I2
*
    IF  (MODE (#I1) = 'A' OR = 'S' OR = 'Q' OR = 'M')
     AND (PAY-STATUS (#I1) LT '40')
      ADD 1 TO #NUM-PP-ASQM
    END-IF
*
    IF #I2 GT 5
      ESCAPE TOP
    END-IF
*
    MOVE ISSUE-DATE(#I1) TO #ISS-DT (#I2)
    MOVE MODE(#I1)       TO #MODE   (#I2)
    MOVE PAY-STATUS(#I1) TO #PAY-ST (#I2)
    MOVE PLAN      (#I1) TO #PLAN   (#I2)
  END-FOR
*
  IF #NUM-POLS GT 5
    WRITE ST-V.ID-NUMBER 'has more than 5 policies'
  END-IF
*
  IF (BILL-ADJUST NE 0) AND (DEBIT-CREDIT-AMT NE 0)
    ADD 1 TO #CNT-7
    DISPLAY (7) #CNT-7
     'CN' ST-V.ID-NUMBER
     'BILL/ADJ'   BILL-ADJUST
     'DEBIT/CREDIT' DEBIT-CREDIT-AMT
     'Num/Pols'     #NUM-POLS
     'Num/PP/ASQM'  #NUM-PP-ASQM
     'PAD/CD'       PAD-CODE
     'List of Pols' #ISSUEDT-MODE-PAYSTAT
  END-IF
*
  IF BILL-ADJUST NE 0
    MOVE TRUE TO #UPDATE
    IF (#NUM-POLS = 0) OR (#NUM-PP-ASQM = 0)
      ADD BILL-ADJUST TO DEBIT-CREDIT-AMT     /* <<<<<<<<<<<<<<<<
      ADD 1 TO #CNT-3
      DISPLAY (3) #CNT-3
        ID-NUMBER
        FIRST-PLACE-FEE
        BILL-ADJUST
        #NUM-PP-ASQM
*
      MOVE 9 TO #I1
      PERFORM SHOW-CONVERSION
    ELSE
      IF #NUM-PP-ASQM = 1
        ADD 1 TO #BADJ-W-1-ASQM
        MOVE #ASQ-X TO #I1
        ADD BILL-ADJUST TO X-BILL-ADJUST (#I1)
        PERFORM SHOW-CONVERSION
      ELSE
        MOVE TRUE TO #HTR-BADJ
        IF #BADJ-X NE 0
          MOVE #BADJ-X TO #I1
          ADD BILL-ADJUST TO X-BILL-ADJUST (#I1)
          PERFORM SHOW-CONVERSION
        ELSE
          IF #RT-X NE 0
            MOVE #RT-X TO #I1
            ADD BILL-ADJUST TO X-BILL-ADJUST (#I1)
            PERFORM SHOW-CONVERSION
          ELSE
            MOVE #FST-PP-ASQ-X TO #I1
            ADD BILL-ADJUST TO X-BILL-ADJUST (#I1)
            PERFORM SHOW-CONVERSION
*
            ADD 1 TO #CNT-2
            DISPLAY (2) #CNT-2
             'CN'           ID-NUMBER
             'BILL/ADJ'     BILL-ADJUST
             'Num/Pols'     #NUM-POLS
             'Num/PP/ASQM'  #NUM-PP-ASQM
             'PAD/CD'       PAD-CODE
             'List of Pols' #ISSUEDT-MODE-PAYSTAT
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
*
  IF FIRST-PLACE-FEE NE 0
    MOVE TRUE TO #UPDATE
    IF #NUM-POLS = 1
      ADD 1 TO #FPF-W-1-POL
      MOVE #ASQ-X TO #I1
      ADD FIRST-PLACE-FEE TO X-FIRST-PLACE-FEE (#I1)
      PERFORM SHOW-CONVERSION
    ELSE
      IF #FPF-X NE 0
        MOVE TRUE TO #HTR-FPF
        MOVE #FPF-X TO #I1
        ADD FIRST-PLACE-FEE TO X-FIRST-PLACE-FEE (#I1)
        PERFORM SHOW-CONVERSION
      ELSE
*       IF #NUM-PP-ASQM = 0
        IF #LST-PP-X = 0 /* no active PP pols
          MOVE TRUE TO #HTR-FPF
          ADD FIRST-PLACE-FEE TO DEBIT-CREDIT-AMT   /* <<<<<<<<<<<<<<
          ADD 1 TO #CNT-3
          DISPLAY (3) #CNT-3
            ID-NUMBER
             FIRST-PLACE-FEE
             BILL-ADJUST
             #NUM-PP-ASQM
*
          MOVE 9 TO #I1
          PERFORM SHOW-CONVERSION
        ELSE
          MOVE TRUE TO #HTR-FPF
          MOVE #LST-PP-X TO #I1
          ADD FIRST-PLACE-FEE TO X-FIRST-PLACE-FEE (#I1)
          PERFORM SHOW-CONVERSION
*
          ADD 1 TO #CNT-4
          DISPLAY (4) #CNT-4
            'CN'           ID-NUMBER
             'FPF'          FIRST-PLACE-FEE
             'SEG'          #I1
             'Num/Pols'     #NUM-POLS
             'PAD/CD'       PAD-CODE
             'List of Pols' #ISSUEDT-MODE-PAYSTAT
        END-IF
      END-IF
    END-IF
  END-IF
*
  FOR #I1 1 TO 9
    IF (RID-PLAN (#I1) NE ' ')
     AND ((RID-FPF NE 0) OR (RID-BALANCE NE 0))
      MOVE TRUE TO #UPDATE
      IF ST-V.ID-NUMBER = 103070
        COMPUTE X-RID-FPF     (#I1) = RID-FPF     / 2
        COMPUTE X-RID-BALANCE (#I1) = RID-BALANCE / 2
      ELSE
        MOVE RID-FPF     TO X-RID-FPF     (#I1)
        MOVE RID-BALANCE TO X-RID-BALANCE (#I1)
      END-IF
      PERFORM SHOW-CONVERSION
*
      ADD 1 TO #CNT-5
      DISPLAY (5) #CNT-5
        'CN'       ID-NUMBER #I1
        'RID/FPF'  RID-FPF
        'RID-BAL'  RID-BALANCE
        'RID-PLAN' RID-PLAN (#I1)
        'ISS-DT'   #ISS-DT (#I1)
        'MD'       #MODE   (#I1)
        'PAY-STST' #PAY-ST (#I1)
        'Num/Pols' #NUM-POLS
*       'List of Pols' #ISSUEDT-MODE-PAYSTAT
    END-IF
  END-FOR
*
  PERFORM CLEAR-OLD-FLDS
*
  UPDATE (0163)
*
  END TRANSACTION
END-READ
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE CLEAR-OLD-FLDS
* * * * * * * * * * * * * *
*
ESCAPE ROUTINE    /* remove later
*
RESET FIRST-PLACE-FEE
      BILL-ADJUST
      BILL-DOLLAR
      RID-FPF
      RID-BALANCE
*
END-SUBROUTINE
*
* * * * * * * * * * * * * *
DEFINE SUBROUTINE SHOW-CONVERSION
* * * * * * * * * * * * * *
*
ADD 1 TO #CNT-9
DISPLAY (9) #CNT-9   (EM=ZZ9)
  'CN'            ST-V.ID-NUMBER
  'ISSUE/DATE'    ISSUE-DATE (#I1)
  'SEG'           #I1    (EM=9)
  'NUM/PP/ASQ'    #NUM-PP-ASQM
  'FPF'           FIRST-PLACE-FEE
  'BADJ'          BILL-ADJUST
  'RID/FPF'       RID-FPF
  'RID/BAL'       RID-BALANCE
  'NEW/FPF'       X-FIRST-PLACE-FEE (#I1)
  'NEW/BADJ'      X-BILL-ADJUST (#I1)
  'NEW/RID-FPF'   X-RID-FPF     (#I1)
  'NEW/RID-BAL'   X-RID-BALANCE (#I1)
  'DB-CR'         DEBIT-CREDIT-AMT
*
IF #HTR-BADJ  /* HTR=Hard to resolve
  ADD 1 TO #CNT-10
  DISPLAY (10) 'CNT/10' #CNT-10   (EM=ZZ9)
    'CN'            ST-V.ID-NUMBER
    'ISSUE/DATE'    ISSUE-DATE (#I1)
    'SEG'           #I1    (EM=9)
    'NUM/PP/ASQ'    #NUM-PP-ASQM
    'BADJ'          BILL-ADJUST
    'NEW/BADJ'      X-BILL-ADJUST (#I1)
    'DB-CR'         DEBIT-CREDIT-AMT
END-IF
*
IF #HTR-FPF  /* HTR=Hard to resolve
  ADD 1 TO #CNT-11
  DISPLAY (11) 'CNT/11' #CNT-11   (EM=ZZ9)
    'CN'            ST-V.ID-NUMBER
    'ISSUE/DATE'    ISSUE-DATE (#I1)
    'SEG'           #I1    (EM=9)
    'NUM/PP/ASQ'    #NUM-PP-ASQM
    'FPF'           FIRST-PLACE-FEE
    'NEW/FPF'       X-FIRST-PLACE-FEE (#I1)
    'DB-CR'         DEBIT-CREDIT-AMT
END-IF
*
END-SUBROUTINE
*
WRITE (2) // 'Tot MBRS w BADJ and 1 PP ASQM =' #BADJ-W-1-ASQM
WRITE (4) // 'Tot MBRS w FPF  and 1 POL     =' #FPF-W-1-POL
WRITE     // 'Tot Single   pp A/S/Q/M Mbrs  =' #SING-ASQ-CNT
WRITE     // 'Tot Multiple pp A/S/Q/M Mbrs  =' #MULT-ASQ-CNT
WRITE     // 'Tot Multiple pp A       Mbrs  =' #MULT-A-CNT
WRITE     // 'Tot Multiple pp S       Mbrs  =' #MULT-S-CNT
WRITE     // 'Tot Multiple pp Q       Mbrs  =' #MULT-Q-CNT
WRITE     // 'Tot Multiple pp M       Mbrs  =' #MULT-M-CNT
WRITE     // 'Tot Extra    pp A       Mbrs  =' #EXTRA-A-CNT
WRITE     // 'Tot Extra    pp S       Mbrs  =' #EXTRA-S-CNT
WRITE     // 'Tot Extra    pp Q       Mbrs  =' #EXTRA-Q-CNT
WRITE     // 'Tot Extra    pp M       Mbrs  =' #EXTRA-M-CNT
*
END
