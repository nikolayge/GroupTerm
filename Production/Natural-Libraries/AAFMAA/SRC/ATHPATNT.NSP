* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPATNT   ** DOWNLOAD AT NOTES INFO FILE TO AAFMAA
*
* SAG  12012005 RH1  SS - Replaced the HEC constant '05' (Tab position)*
*                    EBCDIC to HEX code '09' ASCII                     *
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 AT-V VIEW OF A-APPL-TRACKING
  2 ID-NUMBER
  2 NOTES (50)
*
1 #WORK-NOTE                (A75)
1 REDEFINE #WORK-NOTE
  2 #1RST-BYTE               (A1)
  2 #USER-ID                 (A3)
  2 #DASH                    (A1)
  2 #DATE                    (A10)
  2 #SPACE                   (A1)
  2 #NOTE                    (A59)
*
1 #NEXT-NOTE                (A75)
1 REDEFINE #NEXT-NOTE
  2 #NXT-1RST-BYTE           (A1)
*
1 #REC-OVERFLOW             (A250)
1 REDEFINE #REC-OVERFLOW
  2 #REC-OVF-1ST-125         (A125)
  2 #REC-OVF-2ND-125         (A125)
*
1 #EXTRACT-750
  2 #EXT-125-1               (A125)
  2 #EXT-125-2               (A125)
  2 #EXT-125-3               (A125)
  2 #EXT-125-4               (A125)
  2 #EXT-125-5               (A125)
  2 #EXT-125-6               (A125)
*
1 #EXTRACT-USER-ID          (A3)
1 #EXTRACT-DATE             (A10)
1 #EXTRACT-NOTE             (A75)
*
1 #EXTRACTS                 (P6)
1 #NUM-RECS                 (P5)
*
1 #I1                       (I2)
1 #I2                       (I2)
*
1 #YYYYMMDD                 (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY                    (N4)
  2 #MM                      (N2)
  2 #DD                      (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A                  (A4)
  2 #MM-A                    (A2)
  2 #DD-A                    (A2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=62
WRITE      TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
COMPRESS 'ATNum'
  #TAB  'User'
  #TAB  'Date'
  #TAB  'Note'
  INTO #REC-OVERFLOW LEAVING NO
*
PERFORM CHECK-OVER-125
PERFORM BUILD-FIXED-FILE   /* to get last chunk
ADD 1 TO #NUM-RECS
WRITE WORK FILE 1 #EXTRACT-750
RESET #EXT-125-1
RESET #EXT-125-2
RESET #EXT-125-3
RESET #EXT-125-4
RESET #EXT-125-5
RESET #EXT-125-6
RESET #REC-OVERFLOW
*
READ AT-V PHYSICAL
  IF ID-NUMBER = 0
    ESCAPE TOP
  END-IF
*
* DISPLAY ID-NUMBER NAME SSN
  FOR #I1 1 TO 50
    IF NOTES (#I1) = ' '
      ESCAPE BOTTOM
    END-IF
*
    MOVE NOTES(#I1) TO #WORK-NOTE
    IF #1RST-BYTE = '*'
*
      IF #SPACE = ' '
        IGNORE
      ELSE
        COMPRESS #SPACE #NOTE INTO #NOTE LEAVING NO
      END-IF
*
      COMPRESS AT-V.ID-NUMBER
        #TAB #USER-ID
        #TAB #DATE
        #TAB #NOTE
        INTO #REC-OVERFLOW LEAVING NO
*
    ELSE
      COMPRESS #REC-OVERFLOW
        #WORK-NOTE
        INTO #REC-OVERFLOW
*
      PERFORM CHECK-OVER-125
*
    END-IF
*
    MOVE NOTES(#I1+1) TO #NEXT-NOTE
    IF #NXT-1RST-BYTE = '*'
      PERFORM BUILD-FIXED-FILE   /* to get last chunk
      ADD 1 TO #NUM-RECS
      WRITE WORK FILE 1 #EXTRACT-750
      RESET #EXT-125-1
      RESET #EXT-125-2
      RESET #EXT-125-3
      RESET #EXT-125-4
      RESET #EXT-125-5
      RESET #EXT-125-6
      RESET #REC-OVERFLOW
    END-IF
  END-FOR
*
  IF #REC-OVERFLOW NE ' '
    PERFORM BUILD-FIXED-FILE   /* to get last chunk
    ADD 1 TO #NUM-RECS
    WRITE WORK FILE 1 #EXTRACT-750
    RESET #EXT-125-1
    RESET #EXT-125-2
    RESET #EXT-125-3
    RESET #EXT-125-4
    RESET #EXT-125-5
    RESET #EXT-125-6
    RESET #REC-OVERFLOW
  END-IF
*
END-READ /* (0052)
*
WRITE (2) 'NUMBER RECORDS   = ' #NUM-RECS
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CHECK-OVER-125
* * * * * * * * * * * * * * * * *
*
IF #REC-OVF-2ND-125 NE ' '
  PERFORM BUILD-FIXED-FILE
  MOVE #REC-OVF-2ND-125 TO #REC-OVF-1ST-125
  RESET #REC-OVF-2ND-125
END-IF
*
END-SUBROUTINE
*
* * * * * * * * * * * *
DEFINE BUILD-FIXED-FILE
* * * * * * * * * * * *
*
IF #EXT-125-1 = ' '
  MOVE #REC-OVF-1ST-125 TO #EXT-125-1
ELSE
  IF #EXT-125-2 = ' '
    MOVE #REC-OVF-1ST-125 TO #EXT-125-2
  ELSE
    IF #EXT-125-3 = ' '
      MOVE #REC-OVF-1ST-125 TO #EXT-125-3
    ELSE
      IF #EXT-125-4 = ' '
        MOVE #REC-OVF-1ST-125 TO #EXT-125-4
      ELSE
        IF #EXT-125-5 = ' '
          MOVE #REC-OVF-1ST-125 TO #EXT-125-5
        ELSE
          MOVE #REC-OVF-1ST-125 TO #EXT-125-6
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
*
END-SUBROUTINE
*
END
