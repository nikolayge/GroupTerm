* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram AUCNPHM1.
/**
/** :author nguentchev
/*
DEFINE DATA
PARAMETER USING AUCAPH01
LOCAL USING OBJLCPHN
LOCAL USING OBJADBTS
LOCAL
1 #GET    (I4) CONST <0>
1 #ADD    (I4) CONST <1>
1 #UPDATE (I4) CONST <2>
1 #DELETE (I4) CONST <3>
1 BLANK   (A1) CONST <' '>
*
1 #UNSUPPORTED (A) DYNAMIC CONST <'UNSUPPORTED REQUEST'>
1 #REQUIRED    (A) DYNAMIC CONST <'CONTACT ID REQUIRED'>
1 #UPDATECONFLICT (A) DYNAMIC CONST <'UPDATE CONFLICT'>
END-DEFINE
*
RESET ErrorCode
  ErrorDescription
*
#DB.#FILE-NAME := 'A-CONTACT-PHONE'
*
IF ActionType NE #ADD  /* New phone
  FIND PHONE-V WITH PHONE-ID = Phone.PhoneID
    IF NO RECORDS FOUND
      ErrorCode := 1
      COMPRESS *PROGRAM #REQUIRED INTO ErrorDescription
      ESCAPE ROUTINE
    END-NOREC
    MOVE *ISN TO #DB.#FILE-REC-ISN
    #DB.#REQUEST   := #GET
    CALLNAT 'OBJNDBTS' #DB  /* Get last update date/time
  END-FIND
END-IF
*
DECIDE ON FIRST VALUE OF ActionType
  VALUE #GET    PERFORM GET-RECORD
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  ANY VALUE
    PERFORM SYNC-OPERATIONS
  NONE VALUE
    ErrorCode := 2
    ErrorDescription := #UNSUPPORTED
END-DECIDE
*
DEFINE GET-RECORD
/*     ==========
GET PHONE-V #DB.#FILE-REC-ISN
LastUpdatedDate := #DB.LAST-DATE-UPD
LastUpdatedTime := #DB.LAST-TIME-UPD
LastUpdatedUser := #DB.LUU-NAME
Phone.ContactID := PHONE-V.CONTACT-ID
Phone.CN        := F-GET-CN (< Phone.ContactID >)
Phone.AreaCode  := PHONE-V.AREA-CODE
Phone.PhoneType := PHONE-V.PHONE-TYPE-CODE
Phone.Type      := PHONE-V.TYPE
Phone.Preferred := PHONE-V.PREFERRED-PHONE-IND
Phone.Extension := PHONE-V.EXT
Phone.InvalidInd := PHONE-V.INVALID-IND
Phone.BestCallTime := PHONE-V.BEST-TIME-TO-CALL
IF Phone.AreaCode > 0
  Phone.DialNumber := PHONE-V.DIAL-NUMBER
ELSE
  Phone.DialNumber := PHONE-V.INTL-DIAL-NUMBER
END-IF
COMPRESS PHONE-V.REMARKS (*) INTO Phone.Remarks
END-SUBROUTINE
*
DEFINE ADD-RECORD
/*     ==========
IF Phone.PhoneID = 0
  RESET PHONE-V
  CALLNAT 'ADRNT003' PHONE-V.PHONE-ID
  PHONE-V.CONTACT-ID := Phone.ContactID
  STORE-R.
  STORE PHONE-V
  MOVE *ISN (STORE-R.) TO #DB.#FILE-REC-ISN
  #DB.#REQUEST   := #ADD
  CALLNAT 'OBJNDBTS' #DB
  LastUpdatedDate := #DB.LAST-DATE-UPD
  LastUpdatedTime := #DB.LAST-TIME-UPD
END-IF
END-SUBROUTINE
*
DEFINE UPDATE-RECORD
/*     =============
IF #DB.LAST-DATE-UPD = LastUpdatedDate
    AND #DB.LAST-TIME-UPD = LastUpdatedTime
  UPDATE-R.
  GET PHONE-V #DB.#FILE-REC-ISN
  UPDATE (UPDATE-R.)
  #DB.#REQUEST   := #UPDATE
  CALLNAT 'OBJNDBTS' #DB
  LastUpdatedDate := #DB.LAST-DATE-UPD
  LastUpdatedTime := #DB.LAST-TIME-UPD
ELSE
  ErrorCode := 99
  COMPRESS *PROGRAM #UPDATECONFLICT INTO ErrorDescription
END-IF
*
END-SUBROUTINE
*
DEFINE DELETE-RECORD
/*     =============
IF ActionType NE #GET
  IF #DB.LAST-DATE-UPD = LastUpdatedDate
      AND #DB.LAST-TIME-UPD = LastUpdatedTime
    GET PHONE-V #DB.#FILE-REC-ISN
    DELETE
  END-IF
END-IF
END-SUBROUTINE
*
DEFINE SYNC-OPERATIONS
/*     ===============
ErrorCode := 3
COMPRESS *PROGRAM 'SYNC-OPERATIONS are NOT IMPLEMENTED'
  INTO ErrorDescription
END-SUBROUTINE
*
END
