* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GPDTOSXX    - COMPUTE PAiD-TO-DATE
*
DEFINE DATA
PARAMETER
1 #ISSUE-DATE        (N8)
1 REDEFINE #ISSUE-DATE
 2 #ISS-DT-YYYY   (N4)
 2 #ISS-DT-MM     (N2)
 2 #ISS-DT-DD     (N2)
1 #MONTH-ALLOC-AMT   (P7.2)        /*TT1
1 #PAID-TO-DATE      (N8)
1 #CALC-PAID-TO-DATE   (N8)
*
LOCAL
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY           (N4)
 2 #MM             (N2)
 2 #DD             (N2)
*
1 #I1              (I2)
1 #I2              (I2)
*
*
END-DEFINE
*
MOVE #PAID-TO-DATE TO #YYYYMMDD
*
IF  (#ISSUE-DATE LT 19970101)    /* old or new
 OR (#MONTH-ALLOC-AMT = 999.99)  /*   processing
 OR (#ISS-DT-DD  = 1) /* NXT  or ISS DAY = 1
  IF #MM = 02
    MOVE 28 TO #DD
  ELSE
    IF #MM = 4 OR = 6 OR = 9 OR = 11
      MOVE 30 TO #DD
    ELSE
      MOVE 31 TO #DD
    END-IF
  END-IF
ELSE
  IF ((#MM = 2)
        AND (#ISS-DT-DD = 28 OR = 29))  /* LAST DAY OF MONTH
   OR ((#MM = 4 OR = 6 OR = 9 OR = 11)
          AND (#ISS-DT-DD = 30))
   OR ((#MM = 1 OR = 3 OR = 5 OR = 7 OR = 8 OR = 10 OR = 12)
          AND (#ISS-DT-DD = 31))
    IF #MM = 02
      MOVE 27 TO #DD /* nxt to last day of month
    ELSE
      IF #MM = 4 OR = 6 OR = 9 OR = 11
        MOVE 29 TO #DD
      ELSE
        MOVE 30 TO #DD
      END-IF
    END-IF
  ELSE
    COMPUTE #DD = #ISS-DT-DD - 1
*
    IF (#MM = 2) AND (#DD GE 27)
     MOVE 26 TO #DD /* nxt to nxt to last day of month
    END-IF
  END-IF
END-IF
MOVE #YYYYMMDD TO #CALC-PAID-TO-DATE
*
END
