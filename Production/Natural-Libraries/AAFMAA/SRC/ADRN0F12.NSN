* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM ID: ADRN0F12
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG   20100225  None    Program written.                             *
*                                                                      *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE      TAG  REASON
* NGG  20100225         None Creation
* NGG  20110524    NG1  Synch with CRM shared addresses
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID   (N8)
1 #ADDR-ID      (N8)
1 #MODE         (A1) /* V - VIEW,  U - UPDATE
LOCAL
1 #H1                 (A78)
1 #H2                 (A78) 1 REDEFINE #H2
  2 #M-NAME      (A40)
1 #ADD-CONTACT-ID(N8)
1 #CI            (N6)
1 RID            (I4)
1 #PAGE          (N2)
1 #TOT-PAGES     (N2)
1 #TOT-REL       (N3)
1 #L             (N7)  /* Used in MAP
1 #RELATIONS
  2 #RID            (N8/150)
  2 #ROLE-DESC      (A11/150)
  2 #RNAME          (A24/150)
  2 #CN             (N6/150)
  2 #RDOB           (A10/150)
  2 #ADDR-TYPE      (A1/150)
  2 #ADDR-PROTECT   (A1/150)
  2 #LINK           (A4/150)
  2 #ADDR-SHARE     (A1/150)

1 #CONTACT-NAME  (A25)
1 #CODE-NAME     (A20)
1 #D             (D)
1 #SSN           (N9)
1 #DOB           (A10)
1 #DOD           (A4)
1 #ID-NUMBER     (A6)
1 #ADDR-SHARE-TXT   (A20)
1 #ISN              (P8)
1 #TEXT             (A67/1:3)
1 #SHARED           (A1)

1 #RNAME-CV         (C/150)
1 #RDOB-CV          (C/150)
1 #ROLE-DESC-CV     (C/150)
1 #ADDR-SHARE-CV    (C/150)

1 ADDRESS-POOL VIEW OF A-ADDRESS-POOL

1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DATE-OF-BIRTH-A (A8)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DATE-OF-DEATH-A (A8)
  2 SSN

1 A-ADD-REL VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID        (N8)
  2 ADDRESS-POOL-ID   (N8)
  2 ATTENTION-LINE    (A50)
  2 ADDR-LOCATION     (A1)
  2 ADDR-TYPE-CD      (A1)
  2 RETURNED-MAIL-IND (A1)
  2 PREVENT-OVER-IND  (A1)
  2 ADDRESS-SOURCE    (A1)
  2 STATUS            (A1)
  2 STATUS-DATE       (N8) 2 REDEFINE STATUS-DATE
    3 STATUS-DATE-A   (A8)
  2 LAST-DATE-UPD     (N8) 2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A     (A8)
  2 LAST-TIME-UPD     (N7) 2 REDEFINE LAST-TIME-UPD
    3 LAST-TIME-UPD-A     (A7)
  2 LAST-USER-UPD     (A8)

1 CONTACT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-1-CD
  2 CONTACT-2-ID
  2 ROLE-2-CD

1 STATUS VIEW OF A-STATUS
  2 C*LINKED-CN
  2 LINKED-CN (N6/30)

1 STATUS-2 VIEW OF A-STATUS
  2 INTRO-FLAG

1 CNT-EMAIL-V VIEW OF A-CONTACT-EMAIL

1 CNT-PHONE-V VIEW OF A-CONTACT-PHONE

1 #K                 (I4)
1 #J                 (I4)
1 #MSG               (A72)
1 #POS               (I4)
1 #ERRORS            (L)
1 #UPDATED           (L)
1 BLANK              (A1) CONST <' '>

END-DEFINE

SET KEY PF1 = Help
SET KEY PF3  NAMED 'Exit'
SET KEY PF7  NAMED 'PgUp'
SET KEY PF8  NAMED 'PgDn'

IF #CONTACT-ID <= 0
    or #ADDR-ID <= 0
  ESCAPE ROUTINE
END-IF

IF #MODE = 'U'
  SET KEY ENTR NAMED 'Save'
END-IF

MOVE (AD=P) TO #ADDR-SHARE-CV(*)

FIND ADDRESS-POOL WITH ADDRESS-POOL-ID = #ADDR-ID
  IF NO RECORDS FOUND
    ESCAPE ROUTINE
  END-NOREC
  #ISN := *ISN (0140)
  CALLNAT 'ADRNE002' #CONTACT-ID
    #ISN
    #TEXT (*)
    #SHARED
END-FIND

CALLNAT 'ADRN0004' #CONTACT-ID #H1 #H2

FIND CONTACT WITH CONTACT-ID = #CONTACT-ID
  IF CONTACT.ID-NUMBER > 0
    #ID-NUMBER := CONTACT.ID-NUMBER
    FIND STATUS WITH ID-NUMBER = CONTACT.ID-NUMBER
    END-FIND
  ELSE
    RESET #ID-NUMBER
  END-IF
END-FIND

IF #MODE = 'U'
  MOVE (AD=U) TO #ADDR-SHARE-CV ( * )
END-IF
REPEAT
  PERFORM COLLECT-DATA
  REPEAT
    IF #MODE = 'U'
      INPUT WITH TEXT #MSG MARK #POS USING MAP 'ADRM0F12'
    ELSE
      INPUT USING MAP 'ADRMPF12'
    END-IF
    DECIDE ON FIRST VALUE OF *PF-KEY
      VALUE 'ENTR'
        IF #MODE = 'U'
*        AND #ID-NUMBER NE BLANK
          CALLNAT 'ADRN0007'
            #RID (#L:#L+9)
            #ADDR-SHARE (*)
            #CONTACT-ID
            #ADDR-ID
            #MSG #POS #ERRORS #UPDATED
        END-IF
      VALUE 'PF7'
        IF #PAGE > 1
          SUBTRACT 1 FROM #PAGE
          SUBTRACT 10 FROM #L
        END-IF
        ESCAPE TOP
      VALUE 'PF8'
        IF #TOT-REL >= #L + 10
          ADD 1 TO #PAGE
          ADD 10 TO #L
        END-IF
        ESCAPE TOP
      NONE VALUE IGNORE
    END-DECIDE
    UNTIL NOT #ERRORS
  END-REPEAT
  IF #UPDATED
    #MSG := 'Transaction complete'
    CALLNAT 'MBRADR' #ADDR-ID       /* NG1
  ELSE
    #MSG := 'No updates'
  END-IF
  UNTIL *PF-KEY = 'PF3'
END-REPEAT

DEFINE COLLECT-DATA
/*     ============
RESET RID #RELATIONS

* CHILD-PARENT BASED ON #CONTACT-ID
FIND CONTACT-REL WITH CONTACT-2-ID = #CONTACT-ID
  #ADD-CONTACT-ID := CONTACT-REL.CONTACT-1-ID
  PERFORM COLLECT-LINE-DATA
END-FIND
* PARENT-CHILD BASED ON #CONTACT-ID
FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID
  #ADD-CONTACT-ID := CONTACT-REL.CONTACT-2-ID
  PERFORM COLLECT-LINE-DATA
END-FIND
* ADDR-RELATIONS BASED ON #ADDR-ID
FIND A-ADD-REL WITH ADDRESS-POOL-ID = #ADDR-ID
  IF A-ADD-REL.CONTACT-ID = #CONTACT-ID
    ESCAPE TOP
  END-IF
  #ADD-CONTACT-ID := A-ADD-REL.CONTACT-ID
  PERFORM COLLECT-LINE-DATA
END-FIND
* LINKED-CN BASED ON #ID-NUMBER
FOR #K 1 TO C*LINKED-CN
  IF LINKED-CN ( #K ) GE 500000   /* AT Number
    ESCAPE TOP
  END-IF
  FOR #J 1 TO RID
    IF LINKED-CN ( #K ) = #CN ( #J )
      MOVE 'Link' TO #LINK ( #J )
      ESCAPE BOTTOM
    END-IF
  END-FOR
  IF #J <= RID
    ESCAPE TOP
  END-IF
  FIND (1) CONTACT WITH ID-NUMBER = LINKED-CN ( #K )
    #ADD-CONTACT-ID := CONTACT.CONTACT-ID
    PERFORM COLLECT-LINE-DATA
  END-FIND
END-FOR

#TOT-REL := RID + 1
DIVIDE 10 INTO #TOT-REL GIVING #TOT-PAGES REMAINDER #PAGE
IF #PAGE > 0
  ADD 1 TO #TOT-PAGES
END-IF
#PAGE := 1
#L := 1

END-SUBROUTINE

DEFINE COLLECT-LINE-DATA
/*     =================

FOR #J 1 TO RID
  IF #ADD-CONTACT-ID = #RID ( #J )
    ESCAPE BOTTOM
  END-IF
END-FOR
IF #J <= RID       /* Already in the list
  ESCAPE ROUTINE
END-IF

ADD 1 TO RID
#RID ( RID ) := #ADD-CONTACT-ID
FIND CONTACT WITH CONTACT-ID = #ADD-CONTACT-ID
  #RNAME ( RID ) := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  IF DATE-OF-BIRTH > 0
    MOVE EDITED DATE-OF-BIRTH-A TO #D (EM=YYYYMMDD)
    MOVE EDITED #D (EM=MM/DD/YYYY) TO #RDOB ( RID )
  ELSE
    RESET #RDOB ( RID )
  END-IF
  IF CONTACT.ID-NUMBER > 0
    #CN ( RID ) := CONTACT.ID-NUMBER
  ELSE
    RESET #CN ( RID )
  END-IF
END-FIND

FIND CONTACT-REL WITH CONTACT-1-ID = #CONTACT-ID  /* Parent
    AND CONTACT-2-ID = #ADD-CONTACT-ID
  #ROLE-DESC ( RID ) := FUNCTION-DISP-RL-DESC(<ROLE-2-CD>)
END-FIND

IF #ROLE-DESC ( RID ) = BLANK
  FIND CONTACT-REL WITH CONTACT-2-ID = #CONTACT-ID /* Child
      AND CONTACT-1-ID = #ADD-CONTACT-ID
    #ROLE-DESC ( RID ) := FUNCTION-DISP-RL-DESC(<ROLE-1-CD>)
  END-FIND
END-IF
IF #ROLE-DESC ( RID ) = BLANK
  #ROLE-DESC ( RID ) := 'UNKNOWN'
END-IF

FIND A-ADD-REL WITH CONTACT-ID = #ADD-CONTACT-ID AND ADDRESS-POOL-ID = #ADDR-ID
  IF A-ADD-REL.STATUS NE 'D'
    #ADDR-TYPE    ( RID ) := A-ADD-REL.ADDR-TYPE-CD
    #ADDR-PROTECT ( RID ) := A-ADD-REL.PREVENT-OVER-IND
  END-IF
END-FIND

IF #CN ( RID ) > 0
  FOR #J 1 TO C*LINKED-CN
    IF LINKED-CN ( #J ) = #CN ( RID )
      #LINK ( RID ) := 'Link'
      escape bottom
    END-IF
  END-FOR
END-IF

FIND NUMBER CNT-EMAIL-V WITH CONTACT-ID = #ADD-CONTACT-ID
FIND NUMBER CNT-PHONE-V WITH CONTACT-ID = #ADD-CONTACT-ID
IF (*COUNTER(0306) > 0 AND A-ADD-REL.STATUS NE 'D')
    OR *NUMBER(0322) > 0
    OR *NUMBER(0323) > 0
  COMPRESS '@'  #ROLE-DESC ( RID ) INTO  #ROLE-DESC ( RID ) LEAVING NO
END-IF

END-SUBROUTINE

END
