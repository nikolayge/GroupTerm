* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE DATA
LOCAL USING OBJLMB01
LOCAL
1 #CV      (C)
1 #UPDATE  (A1)
1 #MSG     (A) DYNAMIC
1 BLANK    (A1) CONST <' '>
1 YES      (A1) CONST <'Y'>
END-DEFINE
*
SET KEY PF3
SET KEY PF4 NAMED 'Add'
SET KEY PF5 NAMED 'Upd'
SET KEY PF7 NAMED 'Del'

MOVE (AD=U) TO #CV
INPUT USING MAP 'OBJMMB01'
IF SVC-ID-NUMBER > 0
  PERFORM BY-ID-NUMBER
  IF #UPDATE NE BLANK
    INPUT   /////
      / #MSG  (AL=70 AD=O)
      / 'Do you like to save changes?' #UPDATE  (AD=M)
    IF #UPDATE = YES
      END TRANSACTION
    ELSE
      BACKOUT TRANSACTION
    END-IF
  END-IF
ELSE
  READ SVC-MEMBER-V DESCENDING BY SVC-PLAN
    INPUT USING MAP 'OBJMMB01'
    IF *PF-KEY = 'PF3'
      ESCAPE BOTTOM
    END-IF
  END-READ
END-IF
*
DEFINE BY-ID-NUMBER
/*     ============
REPEAT UNTIL *PF-KEY = 'PF3'
  FIND SVC-MEMBER-V WITH SVC-ID-NUMBER = SVC-ID-NUMBER
    IF NO RECORDS FOUND
      COMPRESS SVC-ID-NUMBER 'record not found'
        INTO #MSG
      ESCAPE ROUTINE
    END-NOREC
    INPUT USING MAP 'OBJMMB01'
    DECIDE ON FIRST VALUE OF *PF-KEY
      VALUE 'PF3'
        ESCAPE BOTTOM
      VALUE 'PF4'
        #MSG := 'Add record is not implemented'
        ESCAPE ROUTINE
      VALUE 'PF5'
        UPDATE
        COMPRESS SVC-ID-NUMBER 'record updated'
          INTO #MSG
        #UPDATE := 'N'
        ESCAPE ROUTINE
      VALUE 'PF7'
        DELETE
        COMPRESS SVC-ID-NUMBER 'record deleted'
          INTO #MSG
        #UPDATE := 'N'
        ESCAPE ROUTINE
      NONE VALUE IGNORE
    END-DECIDE
  END-FIND
END-REPEAT
END-SUBROUTINE
*
END
