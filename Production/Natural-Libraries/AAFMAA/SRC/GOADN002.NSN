* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GOADN002 - Address access
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    03/28/2011  None    Program written                           *
*
************************************************************************
DEFINE DATA
PARAMETER USING GOADA001
PARAMETER USING GOADA002
LOCAL
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID (N8)
  2 ADDRESS-POOL-ID (N8)
  2 ADDR-TYPE-CD (A1)
  2 ADDR-LOCATION (A1)
  2 ATTENTION-LINE (A50)
  2 RETURNED-MAIL-IND (A1)
  2 RETURNED-MAIL-DATE (N8)
  2 PREVENT-OVER-IND (A1)
  2 PREVENT-OVER-DATE (N8)
  2 ADDRESS-SOURCE (A1)
  2 STATUS (A1)
  2 STATUS-DATE (N8)
  2 LAST-DATE-UPD (N8)
  2 LAST-TIME-UPD (N7)
  2 LAST-USER-UPD (A8)
*
1 AD-V VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID
  2 STREET
  2 UNIT
  2 CITY
  2 STATE-CODE
  2 ZIP-CODE 2 REDEFINE ZIP-CODE
    3 ZIP-5  (N5)
    3 ZIP-4  (N4)
  2 COUNTRY-CODE
  2 URBANIZATION
  2 INTL-REGION (A50)
  2 POSTAL-CODE (A25)
  2 ADDR-FORMAT-TYPE-CD
  2 ADDR-VALID-IND
  2 ADDR-VALID-DATE
  2 LAST-DATE-UPD 2 REDEFINE LAST-DATE-UPD
    3 LAST-DATE-UPD-A (A8)
  2 LAST-TIME-UPD
  2 LAST-USER-UPD
*
1 #CONTACT-ID          (N8)
1 #ADDR-TYPE           (A1)
1 #ZIP-5               (A5)
1 #ZIP-4               (A4)
1 YES                  (A1)  CONST <'Y'>
1 NEW                  (A1)  CONST <'N'>
1 BLANK                (A1)  CONST <' '>
1 #SECURED             (A6)  CONST <'Secure'>
1 #PROTECTED           (A9)  CONST <'Protected'>
1 #SHARED-TEXT         (A6)  CONST <'Shared'>
1 PRIMARY              (A1)  CONST <'P'>
1 SECONDARY            (A1)  CONST <'S'>
1 OTHER                (A1)  CONST <'O'>
1 USA                  (A2)  CONST <'US'>
1 PUERTORICO           (A2)  CONST <'PR'>
1 #DATE                (N8)
1 REDEFINE #DATE
  2 #YYYY     (A4)
  2 #MM       (A2)
  2 #DD       (A2)
END-DEFINE
*
RESET #ADDRESS-UI #MSG
#ADDR-TYPE := #ADDR-REL.ADDR-TYPE-CD
IF #ADDR-TYPE = BLANK
  #ADDR-TYPE := PRIMARY
END-IF
#CONTACT-ID := #ADDR-REL.CONTACT-ID
IF #CONTACT-ID = 0
  #MSG := 'No contact ID'
  ESCAPE ROUTINE
END-IF
IF #ADDR-POOL.ADDRESS-POOL-ID > 0
  FIND AD-V WITH ADDRESS-POOL-ID = #ADDR-POOL.ADDRESS-POOL-ID
    MOVE BY NAME AD-V TO #ADDR-POOL
  END-FIND
  FIND ADDR-REL-V WITH CONTACT-ID = #ADDR-REL.CONTACT-ID
      AND ADDRESS-POOL-ID = #ADDR-POOL.ADDRESS-POOL-ID
    MOVE BY NAME ADDR-REL-V TO #ADDR-REL
  END-FIND
ELSE
  FIND ADDR-REL-V WITH CONTACT-ID = #CONTACT-ID
    IF ADDR-REL-V.STATUS = 'D'
      ESCAPE TOP
    END-IF
    IF #ADDR-TYPE = PRIMARY OR= SECONDARY OR= OTHER
      IF ADDR-REL-V.ADDR-TYPE-CD = #ADDR-TYPE
        MOVE BY NAME ADDR-REL-V TO #ADDR-REL
        ESCAPE BOTTOM
      END-IF
    ELSE
      IF ADDR-REL-V.ADDR-TYPE-CD = PRIMARY
        MOVE BY NAME ADDR-REL-V TO #ADDR-REL
      END-IF
      IF ADDR-REL-V.ADDR-TYPE-CD = SECONDARY
          AND #ADDR-REL.ADDRESS-POOL-ID = 0
        MOVE BY NAME ADDR-REL-V TO #ADDR-REL
      END-IF
    END-IF
  END-FIND
  IF #ADDR-REL.ADDRESS-POOL-ID > 0
    FIND AD-V WITH ADDRESS-POOL-ID = #ADDR-REL.ADDRESS-POOL-ID
      MOVE BY NAME AD-V TO #ADDR-POOL
    END-FIND
  END-IF
END-IF
IF #ADDR-POOL.ADDRESS-POOL-ID > 0
  FIND (1) AD-V WITH ADDRESS-POOL-ID = #ADDR-POOL.ADDRESS-POOL-ID
    IF AD-V.COUNTRY-CODE = USA OR= PUERTORICO
        AND AD-V.ZIP-5 > 0
      MOVE EDITED AD-V.ZIP-5 (EM=99999) TO #ZIP-5
      MOVE EDITED AD-V.ZIP-4 (EM=9999)  TO #ZIP-4
      IF #ZIP-4 NE BLANK
        COMPRESS #ZIP-5 #ZIP-4 INTO #ZIP WITH DELIMITER '-'
      END-IF
    END-IF
  END-FIND
ELSE
  #MSG := 'Not exist'
END-IF
IF #ADDR-REL.STATUS-DATE > 0
  #DATE := #ADDR-REL.STATUS-DATE
  COMPRESS #MM '/' #DD '/' #YYYY INTO #STATUS-DATE LEAVING NO
END-IF
*
END
