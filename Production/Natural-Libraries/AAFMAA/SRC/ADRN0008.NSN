* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: ADRN0008
* DESCRIPTION: RANK MAINTENANCE
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  08/27/2010  None Creation
* RSE  02/11/2011  RE1  Call subprogram to validate Branch/Rank
* RSE  04/07/2011  RE2  Use function to check authorized users
* YAK  05/01/2011  YK2  Remove business rules - call validation subprogram
* RSE  05/05/2011  RE3  Call module to sync with CRM
* YAK  05/06/2011  YK1  Changes for transaction log
* RSE  06/02/2011  RE4  Add PREFIX-TITLE
* RSE  06/14/2011  RE5  If Branch is 'V' don't require Rank and Mil Status
* NGG  07/11/2011  NG1  Fixes
* RSE  07/27/2011  RE6  Fix incorrect display of map for non-members
* RSE  09/23/2011  RE7  Pass ID-NUMBER to validation program
* RSE  05/29/2011  RE8  Increase validation array size
**************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID       (N8)
1 #MEMBER-ID-NUMBER (N6)
1 #RANK-ID          (N6)
1 #CLERK-ID         (A3)
1 #MODE             (A1)
1 #MSG              (A72)
LOCAL USING A-CTDATA
LOCAL USING GPRAMINF
LOCAL USING GPRATR00                /* Common transaction logic subroutine
LOCAL USING GPRA0004                /* Parameters passed to write transaction log
LOCAL
1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 PREFIX-TITLE               /* RE4
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
  2 RANK-ID
  2 RANK-PROTECT
  2 RANK-PROTECT-DATE
  2 REDEFINE RANK-PROTECT-DATE
    3 #PROTECT-DATE-YY         (A4)
    3 #PROTECT-DATE-MM         (A2)
    3 #PROTECT-DATE-DD         (A2)
  2 MILITARY-STATUS
  2 LAST-RANK-DATE-UPD
  2 LAST-RANK-USER-UPD
  2 PREV-RANK-ID
  2 SPECIAL-PAY-IND
  2 LAST-USER-UPD
  2 LAST-DATE-UPD
  2 LAST-TIME-UPD
*
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 RANK-ID
  2 PAY-GRADE
  2 MILITARY-BRANCH
  2 MILITARY-ID
  2 RANK-DESCRIPTION
  2 SPECIAL-PAY-IND
  2 RANK-ABBR
  2 RANK-IN-SAS
  2 MESSAGE-NBR
*
1 STATUS-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
  2 DATE-RETIRED
  2 REDEFINE DATE-RETIRED
    3 #DATE-RET-YY             (A4)
    3 #DATE-RET-MM             (A2)
    3 #DATE-RET-DD             (A2)
  2 REDEFINE DATE-RETIRED
    3 #DATE-RETIRED-A          (A8)
  2 DATE-OF-RANK
  2 REDEFINE DATE-OF-RANK
    3 #DATE-RANK-YY            (A4)
    3 #DATE-RANK-MM            (A2)
    3 #DATE-RANK-DD            (A2)
  2 REDEFINE DATE-OF-RANK
    3 #DATE-OF-RANK-A          (A8)
  2 PRIOR-DATE-OF-RANK
  2 REDEFINE PRIOR-DATE-OF-RANK
    3 #PDATE-RANK-YY           (A4)
    3 #PDATE-RANK-MM           (A2)
    3 #PDATE-RANK-DD           (A2)
  2 REDEFINE PRIOR-DATE-OF-RANK
    3 #PRIOR-DATE-OF-RANK-A    (A8)
  2 RANK
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
  2 RETIRED-PAY-AMT
  2 MIL-SBP-OPT
  2 CHIEF-OF-STAFF-IND
*
1 CODES VIEW OF A-GEN-TABLE
  2 GEN-ALPHA-VALUE
  2 GEN-NUMBER-VALUE
  2 ADDL-INFO
  2 REDEFINE ADDL-INFO
    3 #ADDL-INFO-1             (A50)
    3 #ADDL-INFO-2             (A50)
    3 #ADDL-INFO-3             (A50)
*
1 TERM-V VIEW OF A-TERMINALS
  2 COMMENTS
*
1 CONT-REL-V VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID
  2 ROLE-2-CD
*
1 #MAP-CV                      (C)
1 #RANK-MIL-ID-CV              (C)
1 #RANK-ABBR-CV                (C)
1 #PREV-RANK-ABBR-CV           (C)
1 #PREF-CV                     (C)
1 #DOP                         (A8)
1 REDEFINE #DOP
  2 #DOP-N                     (N8)
1 REDEFINE #DOP
  2 #DOP-MM                    (A2)
  2 #DOP-DD                    (A2)
  2 #DOP-YY                    (A4)
1 #PREV-DOP                    (A8)
1 REDEFINE #PREV-DOP
  2 #PREV-DOP-N                (N8)
1 REDEFINE #PREV-DOP
  2 #PREV-DOP-MM               (A2)
  2 #PREV-DOP-DD               (A2)
  2 #PREV-DOP-YY               (A4)
1 #YYYYMMDD                    (A8)
1 REDEFINE #YYYYMMDD
  2 #YYYYMMDD-N                (N8)
1 #YYYYMMDD-2                  (A8)
1 REDEFINE #YYYYMMDD-2
  2 #YYYYMMDD-2-N              (N8)
1 #H1                          (A78)
1 #H2                          (A78)
1 #MILITARY-BRANCH             (A50)
1 #MILITARY-STATUS-DESC        (A20)
1 #PAY-GRADE                   (A3)
1 #PREV-RANK-DATE              (A8)
1 #PREV-PAY-GRADE              (A3)
1 #RANK-DESC                   (A45)
1 #RANK-PROTECTION-DESC        (A20)
1 #RANK-PROTECT-DATE           (A8)
1 #DATE-RETIRED                (A8)
1 REDEFINE #DATE-RETIRED
  2 #DATE-RETIRED-N            (N8)
1 REDEFINE #DATE-RETIRED
  2 #DATE-RETIRED-MM           (A2)
  2 #DATE-RETIRED-DD           (A2)
  2 #DATE-RETIRED-YY           (A4)
1 #SPC-PAY-IND                 (A1)
1 #MESSAGE-NBR                 (N3)
1 #PREFIX-TITLE                (A25)      /* RE4
1 REDEFINE #PREFIX-TITLE
  2 #PREFIX-TITLE-PARM         (A10)
1 #PREV-RANK-ABBR              (A10)
1 #PREV-RANK-DESC              (A45)
1 #RANK-ID-2                   (A10)
1 #PREV-RANK-ID                (N6)
1 #PREV-RANK-ID-2              (A6)
1 #RANK-ABBR-2                 (A10)
1 #LAST-DATE-UPD               (A10)
1 REDEFINE #LAST-DATE-UPD
  2 #LAST-DATE-UPD-YY          (A4)
  2 #LAST-DATE-UPD-MM          (A2)
  2 #LAST-DATE-UPD-DD          (A2)
1 #LAST-USER-UPD               (A20)
1 #CN-TEXT                     (A2)
1 #CI-TEXT                     (A2)
1 #DEL-TEXT                    (A3)
1 #ADD-TEXT                    (A3)
1 #SPC-PAY-IND-TXT             (A3)
1 #FMTD-NAME                   (A33)
1 #SSN                         (A11)
1 #DOB                         (A10)
1 #DOD                         (A10)
1 #UPDATE-FILES                (L)
1 #REMARKS                     (A70/3)
* 1 #POS                         (N2)    /* YK2
1 #N                           (N2)
1 #END                         (N2)
1 #SECURED                     (A1)
1 #VALID                       (L)
1 #BYPASS                      (L)
1 #RANK-LOWERED                (L)
1 #FIRST-DISPLAY               (L)
1 #SECOND-DISPLAY              (L)
1 #SAVE-RANK-MIL-ID            (A3)
1 #SAVE-RANK-ABBR              (A10)
1 #SAVE-RANK-ID                (N6)
1 #SAVE-PAY-GRADE              (A3)
1 #SAVE-RANK-PROTECTION        (A1)
1 #SAVE-RANK-STATUS            (A1)
1 #SAVE-DATE-RETIRED           (A8)
1 #SAVE-DOP                    (A8)
1 #SAVE-PREV-RANK-ID           (N6)
1 #SAVE-PDOP                   (A8)
1 #SAVE-PREV-RANK-ABBR         (A10)
1 #SAVE-PREV-PAY-GRADE         (A3)
1 #SAVE-PREFIX-TITLE           (A10)   /* RE4
1 #NEW-RANK-ID                 (N6)
1 #RANK-IN-SAS                 (A3)
1 #TEXT-1                      (A50)
1 #TEXT-2                      (A50)
1 #TEXT-3                      (A50)
1 #RESPONSE                    (A1)
1 #DATN                        (N8)
1 REDEFINE #DATN
  2 #DATN-YY                   (A4)
  2 #DATN-MM                   (A2)
  2 #DATN-DD                   (A2)
1 #CODES
  2  #RANK-MIL-ID              (A3)
  2  #RANK-STATUS              (A1)
  2  #RANK-ABBR                (A10)
  2  #RANK-PROTECTION          (A1)
1 #CODE-NAMES
  2  #RANK-MIL-ID-TXT          (A50)
  2  #RANK-STATUS-TXT          (A64)
  2  #RANK-ABBR-TXT            (A20)
  2  #RANK-PROTECTION-TXT      (A20)
*
1 XML_SERIALZE_OUTPUT (A) DYNAMIC
1 #CSV-LINE           (A) DYNAMIC
1 #CRM-Result         (A) DYNAMIC
1 #Contact            (I4) CONST <2>
1 #SPECIAL-GUY        (L)
1 #PARM1              (A) DYNAMIC
1 #PARM2              (A10)
1 #RETURN-FLAG        (L)                 /* RE1
1 #BRANCH-POS         (L)                 /* RE1
1 #RANK-POS           (L)                 /* RE1
1 #ERROR-MSG          (A60)               /* RE1
1 #CGA-ISN            (P8)                /* RE3
1 #XML                (A) DYNAMIC         /* RE3
1 #LINE               (A) DYNAMIC         /* RE3
1 #RESPONSE-2           (A) DYNAMIC       /* RE3
1 #CRM-REQUEST        (I4)                /* RE3
1 #RESPONSE-DSPL      (A16)               /* RE3
1 #SYNCHRONIZE        (L)                 /* RE3
1 #PREFIX-TITLE-TEXT  (A16)               /* RE4
1 #PREFIX-TITLE-DESC  (A40)               /* RE4
1 #PREFIX-TITLE-DESC-MSG(A36)             /* RE4
1 #MIL-ABBR-KEY       (A26)               /* YK2
1 REDEFINE #MIL-ABBR-KEY                  /* YK2
  2 #KEY-MILITARY-ID (A1)                 /* YK2
1 #KEY-RANK-ABBR     (A25)                /* YK2
1 #TABLE-RANK-ABBR   (A25)                /* YK2
1 #UNIX               (A) DYNAMIC CONST <'UNIX'>        /* YK2
1 FUNCTION_CODE       (A) DYNAMIC CONST <'MILITARY'>    /* YK1
1 #I                  (I2)                              /* YK1
1 #ST-MIL-ID          (A3)                /* RE4
1 #ID-NUMBER          (N6)                /* NG1 only for help routine
*
END-DEFINE
*
DEFINE WINDOW SPI-Text
  SIZE 11 * 53
  BASE 10/3
  FRAMED ON POSITION SYMBOL AUTO
*
DEFINE WINDOW Contact
  SIZE 20 * 74
  BASE 4/3
  FRAMED ON POSITION SYMBOL AUTO
*
**IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>) /* RE6
*  IGNORE
**END-IF
*
SET KEY ENTR NAMED 'Save'
SET KEY PF1=HELP
SET KEY PF3 NAMED 'Exit'
SET KEY PF12 NAMED 'Reln'
*
CALLNAT 'ADRN0004' #CONTACT-ID #H1 #H2
*
IF #CONT-ID-NUMBER > 0
  MOVE 'CN' TO #CN-TEXT
END-IF
MOVE 'CI' TO #CI-TEXT
*
EXPAND ARRAY #ERROR-CODE    TO (*:10)
EXPAND ARRAY #VALIDATE-FLAG TO (*:50)                /* RE8 formerly 20
*
REPEAT
  IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>)    /* RE6 
    IGNORE                                           /* RE6
  END-IF                                             /* RE6 
*
  IF NOT #BYPASS
    PERFORM COLLECT-DATA
*    IF #CONT-ID-NUMBER > 0
    CALLNAT 'ADRN0009'
      #CONTACT-ID
      #RANK-ID
      CONTACT-V.ID-NUMBER
      #PAY-GRADE
      #SPC-PAY-IND
      #CODES #CODE-NAMES
*    END-IF
    IF #SPC-PAY-IND = 'Y'
      MOVE 'Yes' TO #SPC-PAY-IND-TXT
    ELSE
      MOVE 'No' TO #SPC-PAY-IND-TXT
    END-IF
    IF #ST-MIL-ID NE ' '
      MOVE #ST-MIL-ID TO #SAVE-RANK-MIL-ID              /* RE4
    ELSE
      MOVE #RANK-MIL-ID TO #SAVE-RANK-MIL-ID            /* RE4
    END-IF
    IF #RANK-ID = 9999
      RESET #RANK-ID
    END-IF
    MOVE #RANK-ID TO #SAVE-RANK-ID
    MOVE #RANK-ABBR TO #SAVE-RANK-ABBR
    MOVE #PAY-GRADE TO #SAVE-PAY-GRADE
    MOVE #RANK-PROTECTION TO #SAVE-RANK-PROTECTION
    MOVE #RANK-STATUS TO #SAVE-RANK-STATUS
    MOVE #DATE-RETIRED TO #SAVE-DATE-RETIRED
    MOVE #DOP TO #SAVE-DOP
    MOVE #PREV-DOP TO #SAVE-PDOP
    MOVE #PREV-RANK-ABBR TO #SAVE-PREV-RANK-ABBR
    MOVE #PREV-RANK-ID TO #SAVE-PREV-RANK-ID
    MOVE #PREFIX-TITLE TO #SAVE-PREFIX-TITLE    /* RE4
  END-IF
*
  IF #CONT-ID-NUMBER > 0
    INPUT WITH TEXT #MSG USING MAP 'ADRM0008'
  ELSE
    INPUT WITH TEXT #MSG USING MAP 'ADRM0009'
  END-IF
*
  RESET #MSG
*
  IF *PF-KEY = 'PF3'
    SET WINDOW OFF
    IF #SYNCHRONIZE
      ASSIGN #CRM-REQUEST = 2   /* RE3 CRM SYNC PROCESS  Start
      CALLNAT 'MBRDATA'
        #CGA-ISN                /* Status record ISN
        #XML
        #LINE
        #RESPONSE-2
        #CRM-REQUEST
    END-IF                      /* RE3 CRM SYNC PROCESS  End
    ESCAPE ROUTINE
  END-IF
*
  IF #RETURN-FLAG                     /* RE1 START
    IF *CURS-LINE = 6              /* RE4
      REINPUT USING HELP
        MARK *#RANK-ABBR
    ELSE
      REINPUT USING HELP            /* RE4
        MARK *#PREV-RANK-ABBR       /* RE4
    END-IF
  END-IF                             /* RE1 END
*
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #CONTACT-ID
    ESCAPE TOP
  END-IF
*
  IF #MAP-CV NOT MODIFIED AND
      #PREF-CV NOT MODIFIED AND                /* RE4
      #RANK-MIL-ID-CV NOT MODIFIED AND
      #RANK-ABBR-CV NOT MODIFIED AND
      #PREV-RANK-ABBR-CV NOT MODIFIED AND
      #PAY-GRADE = #SAVE-PAY-GRADE AND
      NOT #BYPASS
    REINPUT 'No changes made. Press PF3 to exit.'
  ELSE
    PERFORM VALIDATE-FIELDS
    IF #BYPASS
      SET CONTROL 'N'
      ESCAPE TOP
    END-IF
    PERFORM ADD/UPDATE-RECORD
  END-IF
END-REPEAT
******************************
DEFINE SUBROUTINE COLLECT-DATA
******************************
MOVE (AD=U) TO #MAP-CV #RANK-MIL-ID-CV #RANK-ABBR-CV #PREV-RANK-ABBR-CV
RESET #SPECIAL-GUY #RANK-ID-2
FIND CONTACT-V WITH CONTACT-ID = #CONTACT-ID
  MOVE PREV-RANK-ID TO #PREV-RANK-ID
  FIND RANK-V WITH RANK-ID = PREV-RANK-ID
    MOVE RANK-V.RANK-ABBR TO #PREV-RANK-ABBR
    MOVE RANK-V.PAY-GRADE TO #PREV-PAY-GRADE
    MOVE RANK-V.RANK-DESCRIPTION TO #PREV-RANK-DESC
*    IF *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA' /* RE2 START
*      COMPRESS '{' RANK-ID '}' INTO #PREV-RANK-ID-2 LEAVING NO
*    END-IF
    IF FUNCTION-AUTHORIZED(<*INIT-USER>)
      COMPRESS '{' RANK-V.RANK-ID '}' INTO #PREV-RANK-ID-2 LEAVING NO
    END-IF
  END-FIND                                                                           /* RE2 END
  FIND RANK-V WITH RANK-ID = #RANK-ID
    IF NO RECORD FOUND                                      /* RE5 START
      IF #RANK-MIL-ID = 'V'
        RESET #RANK-DESC #PAY-GRADE #RANK-ABBR #MESSAGE-NBR
      END-IF
      ESCAPE BOTTOM
    END-NOREC                                               /* RE5 END
    MOVE MILITARY-ID TO #RANK-MIL-ID
    MOVE RANK-V.MILITARY-BRANCH TO #RANK-MIL-ID-TXT
    MOVE RANK-V.RANK-ABBR TO #RANK-ABBR
    MOVE PAY-GRADE TO #PAY-GRADE
    MOVE RANK-DESCRIPTION TO #RANK-DESC
    MOVE MESSAGE-NBR TO #MESSAGE-NBR
*    IF *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA' /* RE2 START
*      COMPRESS '{' RANK-ID INTO #RANK-ID-2 LEAVING NO
*    END-IF
    IF FUNCTION-AUTHORIZED(<*INIT-USER>)
      COMPRESS '{' RANK-V.RANK-ID INTO #RANK-ID-2 LEAVING NO
    END-IF                                                                           /* RE2 END
  END-FIND
  MOVE RANK-PROTECT TO #RANK-PROTECTION
  IF RANK-PROTECT-DATE > 0
    COMPRESS #PROTECT-DATE-MM #PROTECT-DATE-DD #PROTECT-DATE-YY INTO #RANK-PROTECT-DATE LEAVING NO
  ELSE
    RESET #RANK-PROTECT-DATE
  END-IF
  MOVE CONTACT-V.PREFIX-TITLE TO #PREFIX-TITLE                /* RE4
  IF RANK-PROTECT = 'Y'         /* RE4 START
    MOVE 'Prefix         :' TO #PREFIX-TITLE-TEXT
    MOVE (AD=U) TO #PREF-CV
    IF CONTACT-V.PREFIX-TITLE = ' '
      #PREFIX-TITLE := F-DEFAULT-PREFIX(<CONTACT-V.CONTACT-ID>)
      #PREFIX-TITLE-DESC := FUNCTION-PREFIX-DESC(<#PREFIX-TITLE>)
    ELSE
      #PREFIX-TITLE-DESC := FUNCTION-PREFIX-DESC(<CONTACT-V.PREFIX-TITLE>)
    END-IF
    MOVE '(Prefix used when Rank is protected)' TO #PREFIX-TITLE-DESC-MSG
  ELSE
    RESET #PREFIX-TITLE-TEXT #PREFIX-TITLE-DESC #PREFIX-TITLE-DESC-MSG
    MOVE (AD=N) TO #PREF-CV
  END-IF                        /* RE4 END
  MOVE CONTACT-V.MILITARY-STATUS TO #RANK-STATUS
  IF SPECIAL-PAY-IND = ' '
    MOVE 'N' TO #SPC-PAY-IND
  ELSE
    MOVE SPECIAL-PAY-IND TO #SPC-PAY-IND
  END-IF
*   FIND (1) TERM-V WITH CLERK-ID = LAST-USER-UPD           /* YK4 Start
*     IF NO RECORD FOUND                                    /* RE1 START
*       FIND (1) TERM-V WITH TERMINAL-ID = LAST-USER-UPD
*         MOVE COMMENTS TO #LAST-USER-UPD
*       END-FIND
*       ESCAPE BOTTOM
*     END-NOREC                                             /* RE1 END
*     MOVE COMMENTS TO #LAST-USER-UPD
*   END-FIND
  #LAST-USER-UPD := FUNCTION-NAME(< CONTACT-V.LAST-USER-UPD >)          /* YK4 End
  MOVE LAST-DATE-UPD TO #LAST-DATE-UPD
  COMPRESS #LAST-DATE-UPD-MM '/' #LAST-DATE-UPD-DD '/' #LAST-DATE-UPD-YY INTO #LAST-DATE-UPD LEAVING NO
  FIND STATUS-V WITH ID-NUMBER = CONTACT-V.ID-NUMBER
    IF NO RECORD FOUND
      COMPRESS #RANK-ID-2 '}' INTO #RANK-ID-2 LEAVING NO
      ESCAPE BOTTOM
    END-NOREC
    MOVE STATUS-V.MILITARY-SERVICE TO #RANK-MIL-ID #ST-MIL-ID
    IF STATUS-V.DATE-RETIRED > 0
      COMPRESS #DATE-RET-MM #DATE-RET-DD #DATE-RET-YY INTO #DATE-RETIRED LEAVING NO
    END-IF
    IF STATUS-V.DATE-OF-RANK > 0
      COMPRESS #DATE-RANK-MM #DATE-RANK-DD #DATE-RANK-YY INTO #DOP LEAVING NO
    END-IF
    IF STATUS-V.PRIOR-DATE-OF-RANK > 0
      COMPRESS #PDATE-RANK-MM #PDATE-RANK-DD #PDATE-RANK-YY INTO #PREV-DOP LEAVING NO
    END-IF
*    IF *INIT-USER = 'guentch' OR= 'rasekhi' OR= 'XZBSL7' OR= 'RESTABI' OR= 'SVERMA' /* RE2 START
*      COMPRESS #RANK-ID-2 RANK INTO #RANK-ID-2
*      COMPRESS #RANK-ID-2 '}' INTO #RANK-ID-2 LEAVING NO
*    END-IF
    IF FUNCTION-AUTHORIZED(<*INIT-USER>)
      COMPRESS #RANK-ID-2 STATUS-V.RANK INTO #RANK-ID-2
      IF #RANK-ID-2 NE ' '
        COMPRESS #RANK-ID-2 '}' INTO #RANK-ID-2 LEAVING NO
      END-IF
    END-IF                                                                           /* RE2 END
  END-FIND
END-FIND
MOVE #RANK-ID     TO BEFORE_IMAGE.RANK             /* RE2
MOVE #RANK-STATUS TO BEFORE_IMAGE.MILITARY-STATUS  /* RE2
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE VALIDATE-FIELDS
*********************************
*
RESET #VALID #MESSAGE-NBR
  #MILITARY-INFO #OTHERS #ERROR-MSG
#MILITARY-INFO.ID-NUMBER           := CONTACT-V.ID-NUMBER  /* RE7
#MILITARY-INFO.MILITARY-STATUS     := #RANK-STATUS
#MILITARY-INFO.MILITARY-SERVICE    := #RANK-MIL-ID
#MILITARY-INFO.RANK-ABBR           := #RANK-ABBR
#MILITARY-INFO.DATE-OF-RANK        := FUNCTION-DATE(<#DOP-N>)
#MILITARY-INFO.PRIOR-DATE-OF-RANK  := FUNCTION-DATE(<#PREV-DOP-N>)
#MILITARY-INFO.DATE-RETIRED        := FUNCTION-DATE(<#DATE-RETIRED-N>)
*
IF #RANK-MIL-ID = 'V' AND #RANK-ABBR = ' '
  MOVE 'Non Mil' TO #RANK-ABBR
END-IF
* Validate Branch/Rank
MOVE 'Y' TO #VALIDATE-FLAG(2)
* #VAL-FIELD := POS(#RANK-ABBR)
PERFORM CALL-VALIDATION
*
#KEY-MILITARY-ID := SUBSTR(#RANK-MIL-ID,1,1)                                 /* YK2
#KEY-RANK-ABBR   := *TRANSLATE(#RANK-ABBR,UPPER)                             /* YK2
* READ RANK-V                                                                /* YK2
READ RANK-V WITH MIL-ABBR-KEY = #KEY-MILITARY-ID                             /* YK2
  IF SUBSTR(RANK-V.MILITARY-ID,1,1) <> #KEY-MILITARY-ID                      /* YK2
    ESCAPE BOTTOM                                                            /* YK2
  END-IF                                                                     /* YK2
  #TABLE-RANK-ABBR := RANK-V.RANK-ABBR                                       /* YK2
  #TABLE-RANK-ABBR := *TRANSLATE(#TABLE-RANK-ABBR,UPPER)                     /* YK2
  IF #TABLE-RANK-ABBR = #KEY-RANK-ABBR                                       /* YK2
    MOVE RANK-V.RANK-ID TO #NEW-RANK-ID
    MOVE MESSAGE-NBR TO #MESSAGE-NBR
    ESCAPE BOTTOM
  END-IF
END-READ
*
IF #CONT-ID-NUMBER > 0
  RESET #FIRST-DISPLAY
  IF #MESSAGE-NBR > 0 AND (#RANK-MIL-ID-CV MODIFIED OR #PAY-GRADE NE #SAVE-PAY-GRADE)
      AND NOT #BYPASS
    MOVE TRUE TO #FIRST-DISPLAY
    PERFORM DISPLAY-SPI-TEXT
    IF *PF-KEY = 'PF3'
      IGNORE
    ELSE
      MOVE TRUE TO #BYPASS
      ESCAPE ROUTINE
    END-IF
  END-IF
END-IF
*
IF #CONT-GENDER-CD = 'F' AND *TRANSLATE(#RANK-ABBR,UPPER) = 'MR'
  REINPUT 'SEX and RANK mismatch'
    MARK *#RANK-ABBR
ELSE
  IF #CONT-GENDER-CD = 'M'
      AND (*TRANSLATE(#RANK-ABBR,UPPER) = 'MIS' OR = 'MRS' OR = 'MS')
    REINPUT 'SEX and RANK mismatch'
      MARK *#RANK-ABBR
  END-IF
END-IF
*
IF #RANK-ABBR NE #SAVE-RANK-ABBR OR #PAY-GRADE NE #SAVE-PAY-GRADE
  IF #MESSAGE-NBR > 0 AND #PAY-GRADE NE #SAVE-PAY-GRADE
    MOVE #RANK-ID TO #NEW-RANK-ID
    IF #MESSAGE-NBR = 100
      MOVE TRUE TO #SPECIAL-GUY
    END-IF
  ELSE
    #KEY-MILITARY-ID := SUBSTR(#RANK-MIL-ID,1,1)                               /* YK2
    #KEY-RANK-ABBR   := *TRANSLATE(#RANK-ABBR,UPPER)                           /* YK2
*    READ RANK-V                                                               /* YK2
    READ RANK-V WITH MIL-ABBR-KEY = #KEY-MILITARY-ID                           /* YK2
      IF SUBSTR(RANK-V.MILITARY-ID,1,1) <> #KEY-MILITARY-ID                    /* YK2
        ESCAPE BOTTOM                                                          /* YK2
      END-IF                                                                   /* YK2
      #TABLE-RANK-ABBR := RANK-V.RANK-ABBR                                     /* YK2
      #TABLE-RANK-ABBR := *TRANSLATE(#TABLE-RANK-ABBR,UPPER)                   /* YK2
      IF #TABLE-RANK-ABBR = #KEY-RANK-ABBR                                     /* YK2
        MOVE RANK-V.RANK-ID TO #NEW-RANK-ID
        ESCAPE BOTTOM
      END-IF
    END-READ
  END-IF
END-IF
*
IF #CONT-ID-NUMBER = 0          /* When member has no CN, then below fields don't apply
  RESET #BYPASS
  ESCAPE ROUTINE
END-IF
*
IF #DOP > ' ' AND #DOP-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#DOP
END-IF
* Validate #DOP                           /* YK2
MOVE 'Y' TO #VALIDATE-FLAG(6)
* #VAL-FIELD := POS(#DOP)
PERFORM CALL-VALIDATION
*
IF #RANK-PROTECTION = ' '
  REINPUT 'Enter Rank Protection'
    MARK *#RANK-PROTECTION
END-IF
*
#MSG := FUNCTION-CV-CHECK (<'026',#RANK-PROTECTION>)
IF #MSG NE ' '
  REINPUT #MSG
    MARK *#RANK-PROTECTION
END-IF
*
IF #RANK-PROTECTION NE #SAVE-RANK-PROTECTION
  COMPRESS #DATN-MM #DATN-DD #DATN-YY INTO #RANK-PROTECT-DATE
    LEAVING NO
END-IF
*
* Validate Military Status
MOVE 'Y' TO #VALIDATE-FLAG(1)
* #VAL-FIELD := POS(#RANK-STATUS)
PERFORM CALL-VALIDATION
*
IF #DATE-RETIRED > ' ' AND #DATE-RETIRED-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#DATE-RETIRED
END-IF
* Validate #DATE-RETIRED
MOVE 'Y' TO #VALIDATE-FLAG(9)
* #VAL-FIELD := POS(#DATE-RETIRED)
PERFORM CALL-VALIDATION
*
IF (#PREV-RANK-ABBR NE ' ' OR #RANK-ABBR NE ' ')
    AND #PREV-RANK-ABBR = #RANK-ABBR
    AND (#PREV-RANK-ABBR NE 'Non Mil' AND #RANK-ABBR NE 'Non Mil')
  REINPUT 'Current Rank must be different from Previous Rank'
    MARK *#RANK-ABBR
END-IF
*
IF #PREV-RANK-ABBR = ' '
  RESET #PREV-RANK-ID #PREV-RANK-DESC #PREV-PAY-GRADE #PREV-RANK-ID-2
END-IF
*     Previous Rank check
IF #PREV-RANK-ABBR NE ' ' AND #PREV-RANK-ABBR NE #SAVE-PREV-RANK-ABBR
  RESET #VALID #MESSAGE-NBR
  #KEY-MILITARY-ID := SUBSTR(#RANK-MIL-ID,1,1)                               /* YK2
  #KEY-RANK-ABBR   := *TRANSLATE(#PREV-RANK-ABBR,UPPER)                      /* YK2
* READ RANK-V                                                                /* YK2
  READ RANK-V WITH MIL-ABBR-KEY = #KEY-MILITARY-ID                           /* YK2
    IF SUBSTR(RANK-V.MILITARY-ID,1,1) <> #KEY-MILITARY-ID                    /* YK2
      ESCAPE BOTTOM                                                          /* YK2
    END-IF                                                                   /* YK2
*   IF SUBSTR(MILITARY-ID,1,1) = SUBSTR(#RANK-MIL-ID,1,1) AND                /* YK2
*     *TRANSLATE(#PREV-RANK-ABBR,UPPER) = *TRANSLATE(RANK-V.RANK-ABBR,UPPER) /* YK2
    #TABLE-RANK-ABBR := RANK-V.RANK-ABBR                                     /* YK2
    #TABLE-RANK-ABBR := *TRANSLATE(#TABLE-RANK-ABBR,UPPER)                   /* YK2
    IF #TABLE-RANK-ABBR = #KEY-RANK-ABBR                                     /* YK2
      MOVE TRUE TO #VALID
      MOVE MESSAGE-NBR TO #MESSAGE-NBR
      ESCAPE BOTTOM
    END-IF
  END-READ
  IF NOT #VALID
    REINPUT 'Invalid Rank for this Branch. Please use look-up.'
      MARK *#PREV-RANK-ABBR
  END-IF
*
  RESET #SECOND-DISPLAY
  IF #MESSAGE-NBR > 0 AND (#RANK-MIL-ID-CV MODIFIED OR #PREV-PAY-GRADE NE #SAVE-PREV-PAY-GRADE)
      AND NOT #BYPASS
    MOVE TRUE TO #SECOND-DISPLAY
    PERFORM DISPLAY-SPI-TEXT
    IF *PF-KEY = 'PF3'
      IGNORE
    ELSE
      MOVE TRUE TO #BYPASS
      ESCAPE ROUTINE
    END-IF
  END-IF
*
  IF #PREV-RANK-ABBR NE ' ' AND
      (#PREV-RANK-ABBR NE #SAVE-PREV-RANK-ABBR OR #PREV-PAY-GRADE NE #SAVE-PREV-PAY-GRADE)
    IF #MESSAGE-NBR > 0 AND #PREV-PAY-GRADE NE #SAVE-PREV-PAY-GRADE
      MOVE #RANK-ID TO #PREV-RANK-ID
    ELSE
      #KEY-MILITARY-ID := SUBSTR(#RANK-MIL-ID,1,1)                               /* YK2
      #KEY-RANK-ABBR   := *TRANSLATE(#PREV-RANK-ABBR,UPPER)                      /* YK2
*      READ RANK-V                                                               /* YK2
      READ RANK-V WITH MIL-ABBR-KEY = #KEY-MILITARY-ID                           /* YK2
        IF SUBSTR(RANK-V.MILITARY-ID,1,1) <> #KEY-MILITARY-ID                    /* YK2
          ESCAPE BOTTOM                                                          /* YK2
        END-IF                                                                   /* YK2
*       IF SUBSTR(MILITARY-ID,1,1) = SUBSTR(#RANK-MIL-ID,1,1) AND                /* YK2
*         *TRANSLATE(RANK-V.RANK-ABBR,UPPER) = *TRANSLATE(#PREV-RANK-ABBR,UPPER) /* YK2
        #TABLE-RANK-ABBR := RANK-V.RANK-ABBR                                     /* YK2
        #TABLE-RANK-ABBR := *TRANSLATE(#TABLE-RANK-ABBR,UPPER)                   /* YK2
        IF #TABLE-RANK-ABBR = #KEY-RANK-ABBR                                     /* YK2
          MOVE RANK-V.RANK-ID TO #PREV-RANK-ID
          ESCAPE BOTTOM
        END-IF
      END-READ
    END-IF
  END-IF
END-IF
*
IF #PREV-DOP > ' ' AND #PREV-DOP-N NE MASK(MMDDYYYY)
  REINPUT 'Invalid date format'
    MARK *#PREV-DOP
END-IF
* Validate Previous DOP
MOVE 'Y' TO #VALIDATE-FLAG(8)
* #VAL-FIELD := POS(#PREV-DOP)
PERFORM CALL-VALIDATION
*
IF #RANK-PROTECTION = 'Y'
  #PREFIX-TITLE-DESC := FUNCTION-PREFIX-DESC(<#PREFIX-TITLE>)
  IF #PREFIX-TITLE-DESC = ' '
    REINPUT 'Prefix invalid. Please use look-up.'
      MARK *#PREFIX-TITLE
  END-IF
END-IF
*
RESET #BYPASS
*
END-SUBROUTINE
*********************************
DEFINE SUBROUTINE CALL-VALIDATION                 /* YK2 Start
*********************************
#SYSTEM := 'UNIX'
CALLNAT 'GPRNMINF' #MILITARY-INFO #OTHERS
IF #ERROR-CODE(1) NE ' '
  #ERROR-MSG := F-ERROR-DESCRIPTION(<#ERROR-CODE(1)>)
  DECIDE ON FIRST VALUE #VAL-FIELD
    VALUE 'BRANCH'
      #POS := POS(#RANK-MIL-ID)
    VALUE 'RANK'
      #POS := POS(#RANK-ABBR)
    VALUE 'DOP'
      #POS := POS(#DOP)
    VALUE 'MIL-STATUS'
      #POS := POS(#RANK-STATUS)
    VALUE 'RETD'
      #POS := POS(#DATE-RETIRED)
    NONE
      IGNORE
  END-DECIDE
  REINPUT #ERROR-MSG MARK #POS
END-IF
RESET  #VALIDATE-FLAG(*) #VAL-FIELD
END-SUBROUTINE                                    /* YK2 end
**********************************
DEFINE SUBROUTINE DISPLAY-SPI-TEXT
**********************************
RESET #TEXT-1 #TEXT-2 #TEXT-3
MOVE 'N' TO #RESPONSE
FIND CODES WITH SEC-KEY = 'CODE027'
  IF GEN-NUMBER-VALUE = #MESSAGE-NBR
    MOVE #ADDL-INFO-1 TO #TEXT-1
    MOVE #ADDL-INFO-2 TO #TEXT-2
    MOVE #ADDL-INFO-3 TO #TEXT-3
    ESCAPE BOTTOM
  END-IF
END-FIND
*
SET WINDOW 'SPI-Text'
INPUT WINDOW='SPI-Text' USING MAP 'ADRM0010'
*
IF *PF-KEY = 'PF3'
  IF #FIRST-DISPLAY
    MOVE #SAVE-RANK-ID TO #NEW-RANK-ID
    MOVE #SAVE-RANK-ABBR TO #RANK-ABBR
  ELSE
    MOVE #SAVE-PREV-RANK-ID TO #PREV-RANK-ID
    MOVE #SAVE-PREV-RANK-ABBR TO #PREV-RANK-ABBR
  END-IF
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF
*
IF NOT (#RESPONSE = 'Y' OR = 'N' OR = 'U')
  REINPUT 'Invalid response'
    MARK *#RESPONSE
END-IF
*
IF #FIRST-DISPLAY
  MOVE #RANK-ABBR TO #RANK-ABBR-2
ELSE
  MOVE #PREV-RANK-ABBR TO #RANK-ABBR-2
END-IF
#KEY-MILITARY-ID := SUBSTR(#RANK-MIL-ID,1,1)                                 /* YK2
#KEY-RANK-ABBR   := *TRANSLATE(#RANK-ABBR-2,UPPER)                           /* YK2
* READ RANK-V
READ RANK-V WITH MIL-ABBR-KEY = #KEY-MILITARY-ID                             /* YK2
  IF SUBSTR(RANK-V.MILITARY-ID,1,1) <> #KEY-MILITARY-ID                      /* YK2
    ESCAPE BOTTOM                                                            /* YK2
  END-IF                                                                     /* YK2
*   IF SUBSTR(MILITARY-ID,1,1) = SUBSTR(#RANK-MIL-ID,1,1) AND
*       *TRANSLATE(RANK-V.RANK-ABBR,UPPER) = *TRANSLATE(#RANK-ABBR-2,UPPER)
  #TABLE-RANK-ABBR := RANK-V.RANK-ABBR                                       /* YK2
  #TABLE-RANK-ABBR := *TRANSLATE(#TABLE-RANK-ABBR,UPPER)                     /* YK2
  IF #TABLE-RANK-ABBR = #KEY-RANK-ABBR                                       /* YK2
    IF #FIRST-DISPLAY AND (SPECIAL-PAY-IND = #RESPONSE OR SPECIAL-PAY-IND = 'U')
      MOVE RANK-V.RANK-ID TO #RANK-ID
      MOVE SPECIAL-PAY-IND TO #SPC-PAY-IND
      IF #SPC-PAY-IND = 'U'
        IF #RESPONSE = 'N'
          MOVE 'N' TO #SPC-PAY-IND
        ELSE
          MOVE 'Y' TO #SPC-PAY-IND
        END-IF
      END-IF
      MOVE PAY-GRADE TO #PAY-GRADE
      ESCAPE BOTTOM
    END-IF
    IF #SECOND-DISPLAY AND SPECIAL-PAY-IND = #RESPONSE
      MOVE RANK-V.RANK-ID TO #PREV-RANK-ID
      MOVE PAY-GRADE TO #PREV-PAY-GRADE
      ESCAPE BOTTOM
    END-IF
  END-IF
END-READ
*
SET WINDOW OFF
*
END-SUBROUTINE
***********************************
DEFINE SUBROUTINE ADD/UPDATE-RECORD
**********************************
IF #RANK-MIL-ID = #SAVE-RANK-MIL-ID AND
    #RANK-ID = #SAVE-RANK-ID AND
    #RANK-ABBR = #SAVE-RANK-ABBR AND
    #PAY-GRADE = #SAVE-PAY-GRADE AND
    #RANK-PROTECTION = #SAVE-RANK-PROTECTION AND
    #RANK-STATUS = #SAVE-RANK-STATUS AND
    #DATE-RETIRED = #SAVE-DATE-RETIRED AND
    #DOP = #SAVE-DOP AND
    #PREV-DOP = #SAVE-PDOP AND
    #PREV-RANK-ABBR = #SAVE-PREV-RANK-ABBR AND
    #PREFIX-TITLE = #SAVE-PREFIX-TITLE
  ESCAPE ROUTINE
END-IF
*
MOVE TRUE TO #SYNCHRONIZE
PERFORM UPDATE-STATUS/TRANS-REG
*
END TRANSACTION
*
COMPRESS 'Record successfully updated' #MSG INTO #MSG
*
END-SUBROUTINE
*****************************************
DEFINE SUBROUTINE UPDATE-STATUS/TRANS-REG
*****************************************
*
RESET #MSG
FIND CONTACT-V WITH CONTACT-ID = #CONTACT-ID
**  DISPLAY  #PREV-RANK-ID #SAVE-PREV-RANK-ID #SAVE-RANK-ID
  BEFORE_IMAGE.RANK := CONTACT-V.RANK-ID                      /* YK1
  BEFORE_IMAGE.DATE-LAST-UPDATE := LAST-RANK-DATE-UPD         /* YK1
  IF #PREV-RANK-ID NE #SAVE-PREV-RANK-ID
    MOVE #PREV-RANK-ID TO PREV-RANK-ID
  END-IF
  IF (#RANK-ID NE #NEW-RANK-ID) OR (#RANK-ID NE #SAVE-RANK-ID)
    MOVE CONTACT-V.RANK-ID TO PREV-RANK-ID
    IF #RANK-ID NE #NEW-RANK-ID
      MOVE #NEW-RANK-ID  TO CONTACT-V.RANK-ID #RANK-ID
    ELSE
      MOVE #RANK-ID  TO CONTACT-V.RANK-ID
    END-IF
    MOVE #CLERK-ID TO LAST-RANK-USER-UPD
    MOVE *DATN TO LAST-RANK-DATE-UPD
  END-IF
  MOVE #PREFIX-TITLE TO PREFIX-TITLE            /* RE4
  IF #CONT-ID-NUMBER > 0
    BEFORE_IMAGE.MILITARY-STATUS := CONTACT-V.MILITARY-STATUS /* YK1
    MOVE #RANK-STATUS TO CONTACT-V.MILITARY-STATUS
    MOVE #SPC-PAY-IND TO SPECIAL-PAY-IND
    MOVE #RANK-PROTECTION TO RANK-PROTECT
    IF #RANK-PROTECTION = 'N'
      RESET RANK-PROTECT-DATE
    ELSE
      IF #RANK-PROTECTION NE #SAVE-RANK-PROTECTION
        MOVE *DATN TO RANK-PROTECT-DATE
      END-IF
    END-IF
  END-IF
  MOVE #CLERK-ID TO LAST-USER-UPD
  MOVE *DATN TO LAST-DATE-UPD
  MOVE *TIMN TO LAST-TIME-UPD
  UPDATE (0857)
END-FIND
*
IF (SUBSTR(#RANK-MIL-ID,1,1) = 'V' OR = 'E')  OR
    (SUBSTR(#SAVE-RANK-MIL-ID,1,1) = 'V' OR = 'E') OR
    (#RANK-ABBR = 'MR' OR = 'MRS' OR = 'MS') OR
    (#SAVE-RANK-ABBR = 'MR' OR = 'MRS' OR = 'MS')
  IGNORE
ELSE
  IF #RANK-ID < #SAVE-RANK-ID AND #RANK-ABBR NE #SAVE-RANK-ABBR
    MOVE TRUE TO #RANK-LOWERED
  END-IF
END-IF
*
FIND STATUS-V WITH ID-NUMBER = CONTACT-V.ID-NUMBER
  MOVE *ISN TO #CGA-ISN                                /* RE3
  FIND RANK-V WITH RANK-ID = CONTACT-V.RANK-ID
    IF NO RECORD FOUND                    /* RE5 START
*     IF #RANK-MIL-ID = 'V'               /* NG1
      RESET STATUS-V.RANK
*     END-IF
      ESCAPE BOTTOM
    END-NOREC                             /* RE5 END
    MOVE RANK-IN-SAS TO STATUS-V.RANK
  END-FIND
  MOVE #RANK-MIL-ID TO STATUS-V.MILITARY-SERVICE
  MOVE #RANK-STATUS TO STATUS-V.MILITARY-STATUS
  IF #DATE-RETIRED =  ' '
    RESET STATUS-V.DATE-RETIRED
  ELSE
    COMPRESS #DATE-RETIRED-YY #DATE-RETIRED-MM #DATE-RETIRED-DD TO #DATE-RETIRED-A LEAVING NO
  END-IF
  IF #RANK-ABBR NE #SAVE-RANK-ABBR AND NOT #RANK-LOWERED
    MOVE STATUS-V.DATE-OF-RANK TO STATUS-V.PRIOR-DATE-OF-RANK
    IF #DOP NE ' '
      COMPRESS #DOP-YY #DOP-MM #DOP-DD INTO #YYYYMMDD-2 LEAVING NO
      IF STATUS-V.DATE-OF-RANK NE #YYYYMMDD-2-N
        COMPRESS #DOP-YY #DOP-MM #DOP-DD INTO #DATE-OF-RANK-A LEAVING NO
      ELSE
        RESET #DOP STATUS-V.DATE-OF-RANK
        IF #RANK-MIL-ID NE 'V'
          MOVE '(DOP is missing)' TO #MSG
          SET CONTROL 'T8/11'
        END-IF
      END-IF
    ELSE
      IF #RANK-MIL-ID NE 'V'
        MOVE '(DOP is missing)' TO #MSG
        SET CONTROL 'T8/11'
      END-IF
    END-IF
    RESET #PREV-DOP
  ELSE
    IF #DOP = ' '
      RESET STATUS-V.DATE-OF-RANK
    ELSE
      COMPRESS #DOP-YY #DOP-MM #DOP-DD INTO #DATE-OF-RANK-A LEAVING NO
    END-IF
    IF #PREV-DOP = ' '
      RESET STATUS-V.PRIOR-DATE-OF-RANK
    ELSE
      COMPRESS #PREV-DOP-YY #PREV-DOP-MM #PREV-DOP-DD INTO #PRIOR-DATE-OF-RANK-A LEAVING NO
    END-IF
  END-IF
  DECIDE FOR FIRST CONDITION
    WHEN STATUS-V.CHIEF-OF-STAFF-IND = ' ' AND #RANK-PROTECTION = 'Y'
      MOVE 'R' TO STATUS-V.CHIEF-OF-STAFF-IND
    WHEN STATUS-V.CHIEF-OF-STAFF-IND = 'R' AND #RANK-PROTECTION = 'N'
      RESET STATUS-V.CHIEF-OF-STAFF-IND
    WHEN NONE
      IGNORE
  END-DECIDE
  IF #SPECIAL-GUY
    MOVE 'Y' TO STATUS-V.CHIEF-OF-STAFF-IND
  END-IF
  UPDATE (0905)
END-FIND
*
IF #RANK-LOWERED
  IF #MSG NE ' '
    MOVE '(DOP is missing. Rank is lowered)' TO #MSG
  ELSE
    MOVE '(Rank is lowered)' TO #MSG
  END-IF
END-IF
RESET #RANK-LOWERED #LOG-DESCRIPTION #FIELD-DESCRIPTION(*)
*
* Accumullate all transaction logs entry to call subroutine one
* Call new Transaction log for Rank/Mil status/branch/Abbr
* YK1 Start
RESET #I
*
IF #CONT-ID-NUMBER = 0
  #LOG-DESCRIPTION    := 'CB-RANK-M'
  #TR-NAME            := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  #TR-ID-NUMBER         := #MEMBER-ID-NUMBER
  FIND STATUS-V WITH ID-NUMBER = #MEMBER-ID-NUMBER
  END-FIND
  FIND CONT-REL-V WITH CONTACT-2-ID = #CONTACT-ID
    IF CONTACT-1-ID = STATUS-V.MEMBER-CONTACT-ID
      DECIDE ON FIRST VALUE OF ROLE-2-CD
        VALUE 'S'
          MOVE 'SPS-NAME' TO #FIELD-DESCRIPTION(1)
        VALUE 'X'
          MOVE 'FSP-NAME' TO #FIELD-DESCRIPTION(1)
        VALUE 'K', 'G', 'D'
          MOVE 'DEP-NAME' TO #FIELD-DESCRIPTION(1)
        VALUE 'A','B','E','F','I','P','R','T','O','H','Z'
          MOVE 'OTH-NAME' TO #FIELD-DESCRIPTION(1)
        NONE
          IGNORE
      END-DECIDE
    END-IF
  END-FIND
  #I := #I + 1                                  /* YK1
  #OLD-DATA(#I)          := #TR-NAME            /* YK1
  #NEW-DATA(#I)          := #TR-NAME
* PERFORM WRITE-TRANS-REG
ELSE
  #LOG-DESCRIPTION    := 'CG-RANK-M'
  #TR-NAME            := #CONT-FMTD-NAME
  #TR-ID-NUMBER       := #CONT-ID-NUMBER
END-IF
#TR-CLERK-ID          := #CLERK-ID
#TR-DATE-LAST-UPDATE  := *DATN
*
* IF #RANK-MIL-ID NE #SAVE-RANK-MIL-ID
*   #OLD-DATA(1)          := #SAVE-RANK-MIL-ID
*   #FIELD-DESCRIPTION(#I) := 'MIL-ID'
*   #NEW-DATA(1)          := #RANK-MIL-ID
*   PERFORM WRITE-TRANS-REG
* END-IF
* *
* IF #RANK-ABBR NE #SAVE-RANK-ABBR
*   #OLD-DATA(1)          := #SAVE-RANK-ABBR
*   #FIELD-DESCRIPTION(1) := 'RANK-ABBR'
*   #NEW-DATA(1)          := #RANK-ABBR
*   IF #SAVE-PAY-GRADE NE ' ' OR #PAY-GRADE NE ' '
*     #OLD-DATA(2)          := #SAVE-PAY-GRADE
*     #FIELD-DESCRIPTION(2) := 'PAY-GRADE'
*     #NEW-DATA(2)          := #PAY-GRADE
*   END-IF
*   PERFORM WRITE-TRANS-REG
* END-IF
*
*
IF #DOP NE #SAVE-DOP
  #I := #I + 1
  #OLD-DATA(#I)          := #SAVE-DOP
  #FIELD-DESCRIPTION(#I) := 'DOP'
  #NEW-DATA(#I)          := #DOP
* PERFORM WRITE-TRANS-REG
END-IF
*
* IF #RANK-STATUS NE #SAVE-RANK-STATUS
*   #OLD-DATA(1)          := #SAVE-RANK-STATUS
*   #FIELD-DESCRIPTION(1) := 'MIL-STATU'
*   #NEW-DATA(1)          := #RANK-STATUS
*   PERFORM WRITE-TRANS-REG
* END-IF
*
IF #DATE-RETIRED NE #SAVE-DATE-RETIRED
  #I := #I + 1
  #OLD-DATA(#I)          := #SAVE-DATE-RETIRED
  #FIELD-DESCRIPTION(#I) := 'RET-DATE'
  #NEW-DATA(#I)          := #DATE-RETIRED
* PERFORM WRITE-TRANS-REG
END-IF
*
IF #RANK-PROTECTION NE #SAVE-RANK-PROTECTION
  #I := #I + 1
  #OLD-DATA(#I)          := #SAVE-RANK-PROTECTION
  #FIELD-DESCRIPTION(#I) := 'RANK-PROT'
  #NEW-DATA(#I)          := #RANK-PROTECTION
* PERFORM WRITE-TRANS-REG
END-IF
*
* IF #PREV-RANK-ABBR NE #SAVE-PREV-RANK-ABBR
*   #OLD-DATA(1)          := #SAVE-PREV-RANK-ABBR
*   #FIELD-DESCRIPTION(1) := 'PRV-RANK'
*   #NEW-DATA(1)          := #PREV-RANK-ABBR
*   IF #SAVE-PREV-PAY-GRADE NE ' ' OR #PREV-PAY-GRADE NE ' '
*     #OLD-DATA(2)          := #SAVE-PREV-PAY-GRADE
*     #FIELD-DESCRIPTION(2) := 'PRV-PAY-G'
*     #NEW-DATA(2)          := #PREV-PAY-GRADE
*   END-IF
*   PERFORM WRITE-TRANS-REG
* END-IF
*
IF #PREV-DOP NE #SAVE-PDOP
  #I := #I + 1
  #OLD-DATA(#I)          := #SAVE-PDOP
  #FIELD-DESCRIPTION(#I) := 'PDOP'
  #NEW-DATA(#I)          := #PREV-DOP
* PERFORM WRITE-TRANS-REG
END-IF
*
IF #I NE 0
  PERFORM WRITE-TRANS-REG
END-IF
* Write Transaction log (CG formatted)
IF #RANK-ID NE BEFORE_IMAGE.RANK OR
    #RANK-STATUS NE BEFORE_IMAGE.MILITARY-STATUS
*
  IF #CONT-ID-NUMBER > 0
    MOVE #CONT-ID-NUMBER TO TR_ID_NUMBER
  ELSE
    MOVE #MEMBER-ID-NUMBER TO TR_ID_NUMBER
    FIND STATUS-V WITH ID-NUMBER = #MEMBER-ID-NUMBER
      IF F-CONTACT-INFO(<STATUS-V.MEMBER-CONTACT-ID,F-CONTACT-DATA>)
        IGNORE
      END-IF
    END-FIND
  END-IF
*
  AFTER_IMAGE.RANK            := #RANK-ID
  AFTER_IMAGE.MILITARY-STATUS := #RANK-STATUS
  PERFORM WRITE-TRANSACTION-LOG
END-IF
*
END-SUBROUTINE                                   /* YK1 End
*********************************
DEFINE SUBROUTINE WRITE-TRANS-REG
*********************************
CALLNAT 'GPRN0004'
  #TR-FIELDS #SYNC-FIELD-TABLE(*)
  #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
RESET #FIELD-DESCRIPTION(*)
*
END-SUBROUTINE
*
***************************************
DEFINE SUBROUTINE WRITE-TRANSACTION-LOG
***************************************
*
TR_NAME       := #CONT-FMTD-NAME
TR_CLERK      := #CLERK-ID
CALLNAT 'GPRNTR00' FUNCTION_CODE TRAN_LOG_HEADER BEFORE_IMAGE AFTER_IMAGE
*
END-SUBROUTINE
*
END

