* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSN0004.
/**
/** :author nguentchev
/*
/*  Checks if BD bene match contacts. If all match #FORMATED is set to TRUE
/*
DEFINE DATA
PARAMETER
1 #DRAFT-LEGACY       (L)
1 #ISN                (P8)
1 #MSG                (A) DYNAMIC
1 #FORMATED           (A1/6)
LOCAL USING POSALOAD
**LOCAL USING POSADSGN
LOCAL
1 NT-V
  2 ID-NUMBER  (N6)    /* D = bene desig (1-6)
  2 NOTE-DATE  (N8/60) /* P/A = PRIN/ALT DESIG
  2 REDEFINE NOTE-DATE /* L = linked pols
    3 BENE-DATES-6-POLS (6)
      4 BENE-ISS-DT       (N6/7) /* (D, L)
      4 BENE-SPLIT-IND    (N1/7) /* (D, L)
      4 BENE-LST-UPD      (N8)   /* (D)
      4 BENE-STRT-PEND    (N8)   /* (D)
      4 BENE-FOL-UP-IND   (N1)   /* (D)
      4 BENE-OWN-TRNSF-DT (N8)   /* (D)
      4 BENE-SUC-OWN      (N1)   /* (d)
      4 BENE-NEW-LAYOUT   (N1)   /* (D)/* was -setl-opt
      4 BENE-COM-DIS      (N2)   /* (D)/* Common disaster not used
      4 BENE-PER-STR      (N1)   /* (D)/* Per Stirpes not user
      4 BENE-FIL1         (A1)
  2 NOTE-CLERK-ID       (A3/60)
  2 REDEFINE NOTE-CLERK-ID
    3 BENE-MISC-6-POLS  (6)
      4 BENE-STATUS      (A1)  /* (D)
      4 BENE-RESTRICT    (A1)  /* (D)
      4 BENE-MISC-PRIM-SEC (2)
        5 BENE-TYPE        (A1) /* (D, P/A)
        5 BENE-SHARE-TYP   (A1) /* (D, P/A)
        5 BENE-MISC-FIL-2  (A12)
  2 NOTE-TEXT          (A60/60)
  2 REDEFINE NOTE-TEXT
    3 BENE-TEXTS-6-POLS (6)
      4 BENE-TEXT-PRIM-SEC (2)
        5 BENE-TEXT-OCC    (5)
          6 BENE-DESIG-2    (A60)
          6 REDEFINE BENE-DESIG-2
            7 BENE-BN-NAME        (A25)
            7 BENE-BN-SSN         (A9)
            7 BENE-BN-REL         (A1)
            7 BENE-BN-SHARE       (A10)
            7 BENE-BN-SET-OPT     (A1)
            7 BENE-BN-RESTRICTION (A1)
            7 BENE-BN-DOB-MM      (A2)
            7 BENE-BN-DOB-DD      (A2)
            7 BENE-BN-DOB-YYYY    (A4)
            7 BENE-BN-NEW-FILL    (A5)
  2 BENE-CONTACT-ID         (N8/60)
  2 REDEFINE BENE-CONTACT-ID
    3 BENE-CONTACT-6-POLS    (6)
      4 BENE-CONTACT-PROM-SEC (2)
        5 BENE-CONTACT-OCC     (5)
          6 BENE-CONTACT-ID-R   (N8)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 BENE-F-V VIEW OF A-BENE-FINAL
  2 ID-NUMBER           (N6)
  2 NOTE-DATE           (N8/60)
  2 NOTE-CLERK-ID       (A3/60)
  2 NOTE-TEXT           (A60/60)
  2 BENE-CONTACT-ID     (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 BENE-DRAFT-V VIEW OF A-BENE-DRAFT
  2 ID-NUMBER           (N6)
  2 NOTE-DATE           (N8/60)
  2 NOTE-CLERK-ID       (A3/60)
  2 NOTE-TEXT           (A60/60)
  2 BENE-CONTACT-ID     (N8/60)
  2 LST-BD-FINALIZATION-USER (A3)
*
1 CT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 SSN
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
*
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9)
  2 SPLIT-IND     (9)
*
1 #PSTAT          (A1/7)
1 #PID            (A16/7)
1 #BENE-SPLIT     (A1/7)
1 #PID-NBR        (I2)
1 FIND-ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 SSN
  2 PROCESS-IND
  2 FORMER-SPOUSE-SSN
  2 FORMER-SPOUSE-CONTACT-ID
  2 FORMER-HUSBAND-SSN
  2 FORMER-HUSBAND-CONTACT-ID
  2 SSN-SP
*
1 Bene-rec-is-created             (L)
1 #DESIGNATION-ISN                (N8)
1 #REF-NUMBER                     (N6)
1 #REF-TYPE                       (A20)
1 #SSN-I                          (P9)
1 #CONTACT-ID                     (N8)
1 #SSN                            (N9)
1 REDEFINE #SSN
  2 #SSN-A                        (A9)
1 #DISPLAY-CONTACT-NAME           (A50)
1 #WORK-DATE  (A6)
1 REDEFINE #WORK-DATE
  2 #WORK-YY   (A2)
  2 REDEFINE #WORK-YY
    3 #WORK-YY-N(N2)
  2 #WORK-REST (A4)
1 #WORK-8        (A8) 1 REDEFINE #WORK-8
  2 #WORK-8-N    (N8)
1 #EXCEPTION     (A250)
1 #ADD           (I4) CONST <1>
1 REPLACE-IT     (A) DYNAMIC CONST <'REPLACE'>
1 BLANK          (A1) CONST <' '>
1 #MESSAGE-LOG   (I4) CONST <2>
1 #ERROR-LOG     (I4) CONST <3>
1 #TAB           (A1) CONST <H'09'>
1 #I1         (I2)
1 #I2         (I2)
1 #I3         (I2)
1 J           (I2)
1 K           (I2)
END-DEFINE
*
IF #DRAFT-LEGACY
  GL1.
  GET BENE-DRAFT-V #ISN
  IF *ISN (GL1.) <= 0
    COMPRESS 'BENE-DRAFT record doesnt exist. ISN=' #ISN INTO #MSG
    CALLNAT 'POSLOG' #MESSAGE-LOG #MSG
    ESCAPE ROUTINE
  END-IF
  MOVE BY NAME BENE-DRAFT-V TO NT-V
ELSE
  GL.
  GET BENE-F-V #ISN
  IF *ISN (GL.) <= 0
    COMPRESS 'BENE-FINAL record doesnt exist. ISN=' #ISN INTO #MSG
    CALLNAT 'POSLOG' #MESSAGE-LOG #MSG
    ESCAPE ROUTINE
  END-IF
  MOVE BY NAME BENE-F-V TO NT-V
END-IF
RESET #POWN #FORMATED (*)
PERFORM PROCESS-RECORD
*
DEFINE PROCESS-RECORD
/*     ==============
FIND POLICY WITH ID-NUMBER = NT-V.ID-NUMBER
END-FIND
/*
FOR #I1 1 TO 6
  IF NT-V.BENE-ISS-DT (#I1, 1) EQ 0
    ESCAPE TOP
  END-IF
/*         Collect policy pool for current beneficiary CN
  RESET #PID (*) #PID-NBR
  FOR #I3 1 TO 7  /* Same Designation
    IF NT-V.BENE-ISS-DT (#I1,#I3) = 0
      ESCAPE TOP
    END-IF
    MOVE EDITED NT-V.BENE-ISS-DT (#I1,#I3) (EM=999999) TO #WORK-DATE
    IF #WORK-YY-N LT 16
      #WORK-8 := '20'
    ELSE
      #WORK-8 := '19'
    END-IF
    COMPRESS #WORK-8 #WORK-YY #WORK-REST INTO #WORK-8 LEAVING NO
    FOR J 1 TO POLICY.C*ACTIVE-SEGMENTS
      IF POLICY.ISSUE-DATE (J) = #WORK-8-N
          AND POLICY.SPLIT-IND (J) = NT-V.BENE-SPLIT-IND (#I1,#I3)
        ADD 1 TO #PID-NBR
        #PID (#PID-NBR) := POLICY.POLICY-ID (J)
        #PSTAT (#PID-NBR) := POLICY.STATUS (J)
        #BENE-SPLIT (#PID-NBR) := NT-V.BENE-SPLIT-IND (#I1,#I3)
      END-IF
    END-FOR
  END-FOR
  RESET Bene-rec-is-created
  BeneLoop.
  FOR #I2 1 TO 2
    IF #I2 = 1
      #POWN.RELATION := 5  /* Primary
    ELSE
      #POWN.RELATION := 7  /* Contingent
    END-IF
    FOR #I3 1 TO 5
      IF NT-V.BENE-BN-NAME (#I1,#I2,#I3) = BLANK
        ESCAPE TOP
      END-IF
      IF NT-V.BENE-BN-SSN (#I1,#I2,#I3) = MASK (999999999)
          AND NT-V.BENE-BN-SSN (#I1,#I2,#I3) NE '000000000'
        #SSN-A := NT-V.BENE-BN-SSN (#I1,#I2,#I3)
      ELSE
        RESET #SSN-A
      END-IF
*      #POWN.BENE-NAME := NT-V.BENE-BN-NAME (#I1,#I2,#I3)
      /* Search by SSN
      IF #SSN > 0
        FIND (1) CT-V WITH SSN = #SSN
          IF NO RECORDS FOUND
            ESCAPE BOTTOM
          END-NOREC
          #POWN.CONTACT-ID := CT-V.CONTACT-ID
        END-FIND
      END-IF
      /* Search by DOB and Name
      IF NT-V.BENE-CONTACT-ID-R  (#I1,#I2,#I3) <= 0
        COMPRESS BENE-BN-DOB-YYYY (#I1,#I2,#I3)
          BENE-BN-DOB-MM (#I1,#I2,#I3)
          BENE-BN-DOB-DD (#I1,#I2,#I3)
          INTO #WORK-8 LEAVING NO
        IF #WORK-8 = MASK(NNNNNNNN)
          FIND CT-V WITH DATE-OF-BIRTH = #WORK-8-N
            #DISPLAY-CONTACT-NAME := FUNCTION-DISP-CT-NAME
              (<CT-V.FIRST-NAME,
              CT-V.MIDDLE-NAME,
              CT-V.LAST-NAME,
              CT-V.SUFFIX>)
            IF #DISPLAY-CONTACT-NAME = NT-V.BENE-BN-NAME (#I1,#I2,#I3)
*              RESET #POWN.BENE-NAME
              #POWN.CONTACT-ID := CT-V.CONTACT-ID
              ESCAPE BOTTOM
            END-IF
          END-FIND
        END-IF
      END-IF
/*
      IF #POWN.CONTACT-ID > 0
        #FORMATED (#I1) := 'Y'
        RESET #POWN.CONTACT-ID
      ELSE   /* All or Nothing
        #FORMATED (#I1) := 'N'
        ESCAPE BOTTOM(BeneLoop.)
      END-IF
    END-FOR
  END-FOR
END-FOR
END-SUBROUTINE
*
ON ERROR
  COMPRESS 'ON ERROR BENE FINAL. CN=' NT-V.ID-NUMBER INTO #MSG
  CALLNAT 'POSLOG' #ERROR-LOG #MSG
  ESCAPE ROUTINE
END-ERROR
END
