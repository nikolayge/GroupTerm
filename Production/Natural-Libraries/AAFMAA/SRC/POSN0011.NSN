* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSN0011.
/**
/** :author nguentchev
/* TODO Enter your code here
DEFINE DATA
PARAMETER
1 #REQUEST      (I4)
1 #MSG          (A) DYNAMIC
1 REC-ISN       (N8)
PARAMETER USING POSADSGN
/*
LOCAL USING ERROR_L
LOCAL USING POSL0003
LOCAL
1 BLANK           (A1) CONST <' '>
1 #REL-EXIST      (L)
1 #MESSAGE-LOG    (I4) CONST <2>
1 #ERROR-LOG      (I4) CONST <3>
1 #GET    (I4) CONST <0>
1 #ADD    (I4) CONST <1>
1 #UPDATE (I4) CONST <2>
1 #DELETE (I4) CONST <3>
END-DEFINE
*
RESET #MSG
*
DECIDE ON FIRST VALUE OF #REQUEST
  VALUE #ADD    PERFORM ADD-RECORD
  VALUE #UPDATE PERFORM UPDATE-RECORD
  VALUE #DELETE PERFORM DELETE-RECORD
  VALUE #GET    PERFORM GET-RECORD
  NONE VALUE
    IGNORE
END-DECIDE
/*
DEFINE CHECK-EXIST
/*     -----------
RESET #REL-EXIST
FIND DS-V WITH DESIGNATION-ID = #DSGN.DESIGNATION-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  #REL-EXIST := TRUE
  ESCAPE BOTTOM
END-FIND
*
END-SUBROUTINE
/*
DEFINE GET-RECORD
/*     -----------
#MSG := #NOT-EXIST      /* 'No record exist'
IF REC-ISN > 0
  GET DS-V REC-ISN
  MOVE BY NAME DS-V TO #DSGN
  RESET #MSG
ELSE
  FIND DS-V WITH DESIGNATION-ID = #DSGN.DESIGNATION-ID
    IF NO RECORDS FOUND
      ESCAPE BOTTOM
    END-NOREC
    MOVE BY NAME DS-V TO #DSGN
    REC-ISN := *ISN (0061)
    RESET #MSG
  END-FIND
END-IF
*
END-SUBROUTINE
/*
DEFINE ADD-RECORD
/*     ----------
PERFORM CHECK-EXIST
IF #REL-EXIST
/* 'No duplicate key allowed - record exist'
  #MSG := #DUPLICATE-KEY
ELSE
  MOVE BY NAME #DSGN TO DS-V
  IF DS-V.ADD-USER = BLANK
    DS-V.ADD-USER      := *USER
  END-IF
  IF DS-V.ADD-DATE = 0
    DS-V.ADD-DATE      := *DATN
  END-IF
  IF DS-V.ADD-TIME = 0
    DS-V.ADD-TIME      := *TIMN
  END-IF
  IF DS-V.LAST-UPD-USER = BLANK
    MOVE *USER TO #DSGN.LAST-UPD-USER
      DS-V.LAST-UPD-USER
  END-IF
  IF DS-V.LAST-UPD-DATE = 0
    MOVE *DATN TO #DSGN.LAST-UPD-DATE
      DS-V.LAST-UPD-DATE
  END-IF
  IF DS-V.LAST-UPD-TIME = 0
    MOVE *TIMN TO #DSGN.LAST-UPD-TIME
      DS-V.LAST-UPD-TIME
  END-IF
  STORE DS-V
  RESET #MSG
END-IF
END-SUBROUTINE
/**
DEFINE UPDATE-RECORD
/*     -------------
#MSG := #NOTHING-UPDATE          /* 'No record exist'
FIND DS-V WITH DESIGNATION-ID = #DSGN.DESIGNATION-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME #DSGN TO DS-V
  IF #DSGN.LAST-UPD-USER = BLANK
    MOVE *USER TO DS-V.LAST-UPD-USER #DSGN.LAST-UPD-USER
  END-IF
  IF #DSGN.LAST-UPD-DATE = 0
    MOVE *DATN TO DS-V.LAST-UPD-DATE #DSGN.LAST-UPD-DATE
  END-IF
  IF #DSGN.LAST-UPD-TIME = 0
    MOVE *TIMN TO DS-V.LAST-UPD-TIME #DSGN.LAST-UPD-TIME
  END-IF
  UPDATE
  RESET #MSG
END-FIND
END-SUBROUTINE
/*
DEFINE DELETE-RECORD
/*     -------------
#MSG := #NOTHING-DELETE      /* 'No record to delete'
FIND DS-V WITH DESIGNATION-ID = #DSGN.DESIGNATION-ID
  IF NO RECORDS FOUND
    ESCAPE BOTTOM
  END-NOREC
  DELETE
  RESET #MSG
END-FIND
END-SUBROUTINE
*
ON ERROR
  COMPRESS #INTERNAL-ERROR #DSGN.DESIGNATION-ID
    INTO #MSG
  CALLNAT 'POSLOG' #ERROR-LOG #MSG
  ESCAPE ROUTINE
END-ERROR
END
