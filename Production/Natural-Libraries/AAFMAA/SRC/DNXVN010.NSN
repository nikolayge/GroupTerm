* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* PROGRAM ID DNXVN010                                                  *
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  08/16/2010  None    Program created                             *
* TMT  10/07/2013  TMT     ADDRESS NORMALIZATION                       *
************************************************************************
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 BLANK  (A1)  CONST <' '>
1 #SEX   (A4)  CONST <'Sex:'>
1 #SSN   (A4)  CONST <'SSN:'>
1 #DOB   (A4)  CONST <'DOB:'>
1 #DOD   (A4)  CONST <'DOD:'>
1 #J     (I4)
1 #SSN-F (A11)
1 #DOD-F (A10)
1 #DOB-F (A10)
1 CONTACT VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
  2 GENDER-CD
  2 DATE-OF-BIRTH 2 REDEFINE DATE-OF-BIRTH
    3 DOB-YEAR  (A4)
    3 DOB-MONTH (A2)
    3 DOB-DAY   (A2)
  2 DATE-OF-DEATH 2 REDEFINE DATE-OF-DEATH
    3 DOD-YEAR  (A4)
    3 DOD-MONTH (A2)
    3 DOD-DAY   (A2)
  2 SSN
1 INT-1099-V VIEW OF A-INT-1099                /*TT1 BEGIN
  2 RELATED-ID-NUMBER
  2 CLAIMANT-ID
  2 CONTACT-ID
  2 YEAR
  2 INT-TYPE                                   /*TT1 END

END-DEFINE

RESET #TEXT (*)
IF #ISN = 0
  #TEXT (1) := 'No data'
  ESCAPE ROUTINE
END-IF
GET INT-1099-V #ISN                                              /*TT1 ADDED GET STATEMENT
FIND CONTACT WITH CONTACT-ID = CLAIMANT-ID                       /*TT1 ADDED FIND STATEMENT
  #TEXT (1) := FUNCTION-CONTACT-NAME-2 (< CONTACT.CONTACT-ID >)
* FORMATING
  IF SSN > 0
    MOVE EDITED SSN (EM=999-99-9999) TO #SSN-F
  END-IF
  IF DATE-OF-BIRTH > 0
    COMPRESS DOB-MONTH DOB-DAY DOB-YEAR INTO #DOB-F WITH DELIMITER '/'
  END-IF
  IF DATE-OF-DEATH > 0
      COMPRESS DOD-MONTH DOD-DAY DOD-YEAR INTO #DOD-F WITH DELIMITER '/'
  END-IF
  COMPRESS
    #SEX GENDER-CD
    #SSN #SSN-F
    #DOB #DOB-F
    #DOD #DOD-F
    INTO #TEXT (2)
END-FIND                                                         /*TT1 END
ON ERROR
  COMPRESS 'Application error' *ERROR-NR *ERROR-LINE *PROGRAM
    INTO #TEXT  (1)
  ESCAPE ROUTINE
END-ERROR

END
