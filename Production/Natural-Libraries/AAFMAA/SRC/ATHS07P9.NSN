* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHP07P9    - process NON-CERTIFIED Notices
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  08112005 DW1  Create
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &
*                    replaced it with copycode GABNDCX3
* SAG  12012005 RH1  SS - Replaced the HEX constant '05' (Tab position)
*                    EBCDIC to HEX code '09' ASCII
* SAG-AB 20060820  workaround for zip code truncation of leading zeros in suffix
* DRW  08302006 DW2  Build Non-Certified Notice detail file
* DRW  08302006 DW3  bypass AFSC Notices
* RSE  10012009 RE1  Change 600K logic for Unification
* bz   11/19/2009 bz1  effected but not modified - no Status Record 
*******************************************
*
DEFINE DATA
PARAMETER
1 #AAFM-NOTC          (P5)
1 #CN                 (N6)
1 #MODE               (A1)
1 #RUN-DT-MM-DD-YYYY  (A10)
1 #TOT-AMT-DUE        (A11)
1 #PRM-DUE-MM-DD-YYYY (A11)
1 #AAFM-BAR-CODE-1    (A32)
1 #AAFM-BAR-CODE-2    (A32)
1 #SCR-BAR-CD-SUPPRESSED (A48)
1 #SPS-GEN-NAME      (A29)
1 REDEFINE #SPS-GEN-NAME
 2 #FOR              (A4)  /* 'For '     /* DW3
 2 #INSURED-NAME     (A25)               /* DW3
1 #SPS-GEN-CN        (A14)
1 REDEFINE #SPS-GEN-CN
 2 #WITH             (A8)  /* 'With CN ' /* DW3
 2 #INSURED-CN       (A6)                /* DW3
1 #RID-DISP          (A12)
*
1 #SCR-TEXT          (A73/22)
1 #SCR-TYPE-IND      (A14)
1 REDEFINE #SCR-TYPE-IND
 2 #SCR-TYPE-IND-3   (A3)
 2 REDEFINE #SCR-TYPE-IND-3
  3 #SCR-TYPE-IND-3-X   (A1/3)
*
1 #SCR-MBR-NAME-FULL  (A35)
1 #DEAR-NAME          (A50)
1 #ADDR-1        (A25)
1 #ADDR-2        (A25)
1 #ADDR-3        (A25)
1 #ZIP-5         (A5)
1 #ZIP-4         (A4)
1 #LAST-SEG           (I2)
1 #LST-POL-NUM        (A16)
1 #MULT-NOTC          (A4)
*
LOCAL  USING GONERLXX                       /* SAG RH1 11292005 SS
LOCAL
1 #NOTC-OUT    (A250)  /* DW2
1 #ZIP         (A10)
*
1 #TYPE  (A3)
1 REDEFINE #TYPE
 2 #TYPE-X  (A1/3)
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #POL-NUMBER  (A16)
1 #COMPANY    (A6) INIT <'AAFMAA'>
1 #SCR-####-ENVL  (A6) /* <<<<<<<<<<<<<<<<<<<<<<<< DRW
1 #SCR-ALLT-RQST-LN1 (A26)
1 #SCR-ALLT-RQST-LN2 (A26)
1 #SCR-ALLT-RQST-LN3 (A26)
1 #SCR-AMT-BAR-CODE  (A8)
1 #SCR-AMT-BAR-CODE-2(A8)
1 #SCR-CN-BAR-CODE   (A6)
1 #SCR-TRAN-BAR-CODE (A7)
1 #SCR-TRAN-BAR-CODE-2 (A7)
1 #SCR-CN-NUMBER     (A10)
1 #SCR-DUE-TEXT      (A29/4)
1 #SCR-ENT-BAR-CODE   (A38)
1 #SCR-ENT-BAR-CODE-2 (A38)
1 #SCR-ISS-DT          (A8)
1 #SCR-MBR-ADDR      (A35/3)
1 #SCR-NOTC-NUM   (A6)
1 #SPS-GEN-NAME-X   (A29)
1 #SPS-GEN-CN-X     (A14)
1 #LST-SPS-GEN-NAME-X   (A29)
1 #LST-SPS-GEN-CN-X     (A14)
*
1 #I1              (I2)
1 #I2              (I2)
1 #P-X1            (I2)
1 #P-X2            (I2)
*
1 #DUE-LINES   (A78/16)
1 #TEXT-LINES  (A78/8)
*
1 #FREQ       (A11)
1 #ABEND-CODE  (I4)
1 #SPLIT    (A1)
1 #STREET   (A25)
1 #STREET-2 (A25)
1 #CITY     (A22)
1 #STATE    (A2)
1 #ERR-MSG  (A60)
*
1 #FULL-RECORD
 2 #FULL-REC  (A125/12)  /* WAS 10 now 125 * 12 =1500
 2 REDEFINE #FULL-REC
  3 #FULL-REC-75 (A75/20) /* to write temp
*
1 #REC-250    (A250)
1 REDEFINE #REC-250
 2 #REC-125-A (A125)
 2 REDEFINE #REC-125-A
  3 #REC-62-A (A62) /* to write temp
  3 #REC-63-A (A63) /* to write temp
 2 #REC-125-B (A125)
 2 REDEFINE #REC-125-B
  3 #REC-62-B (A62) /* to write temp
  3 #REC-63-B (A63) /* to write temp
1 REDEFINE #REC-250
 2 #REC-250-X   (A1/250)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* IF #ZIP-4 EQ '0000'    /* EDS-PM 20060919  /* SAG-AB 20060915 START
*   RESET #ZIP-4         /* EDS-PM 20060919
* END-IF                 /* EDS-PM 20060919
IF #ZIP-4 NE ' '
  MOVE RIGHT JUSTIFIED #ZIP-4 TO #ZIP-4
  EXAMINE #ZIP-4 FOR ' ' REPLACE WITH '0'
*  IF #ZIP-4 EQ '0000'   /* EDS-PM 20060919
*    RESET #ZIP-4        /* EDS-PM 20060919
*  END-IF                /* EDS-PM 20060919
END-IF                                      /* SAG-AB 20060915 END
*
RESET #POL-NUMBER
IF #LAST-SEG NE 0
 CALLNAT 'GPLNMSXX' #POL-NUMBER #CN #LAST-SEG
END-IF
*
* WRITE 'In ATHS07P9' #CN #SCR-TYPE-IND #PRM-DUE-MM-DD-YYYY #TOT-AMT-DUE
* IF #SPS-GEN-NAME NE ' '
*   WRITE 'insured' #SPS-GEN-NAME 'payer' #SCR-MBR-NAME-FULL
* END-IF
*
ON ERROR
*    WRITE   / '*E R R O R - N R    ' *ERROR-NR     /* SAG RH1 11292005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     WRITE   / ' FILLER'
*     MOVE +4095 TO #ABEND-CODE
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
  INCLUDE GABNDCX3
  STOP                                                 /* SAG RH1 11292005 SS - END
END-ERROR
*
IF #AAFM-NOTC = 1
  RESET #FULL-REC (*) #P-X2
  COMPRESS
         'TYPE'
    #TAB 'CN'
    #TAB 'POL#'
    #TAB 'FREQ'
    #TAB 'RUN-DATE'
    #TAB 'DUE-DATE'
    #TAB 'TOT-DUE'
    #TAB 'NAME'
    #TAB 'DEAR-NAME'
    #TAB 'STREET'
    #TAB 'STREET-2'
    #TAB 'CITY'
    #TAB 'ST'
    #TAB 'ZIP'
    #TAB 'SPS/GEN NAME'
    #TAB 'SPS/GEN CN'
    #TAB 'BAR-CODE-1'
    #TAB 'BAR-CODE-2'
    #TAB 'BC-SUPPRESSED-MSG'
    #TAB 'RID-DISP'
    #TAB 'LN-1'
    #TAB 'LN-2'
    #TAB 'LN-3'
    #TAB 'LN-4'
    #TAB 'LN-5'
    #TAB 'LN-6'
    #TAB 'LN-7'
    #TAB 'LN-8'
    #TAB 'LN-9'
    #TAB 'LN-10'
    #TAB 'LN-11'
    #TAB 'LN-12'
    #TAB 'LN-13'
      INTO #REC-250 LEAVING NO    /* # 2
*
  PERFORM COMBINE-125
*
  COMPRESS #REC-250
    #TAB 'LN-14'
    #TAB 'LN-15'
    #TAB 'LN-16'
    #TAB 'VARIABLE TEXT'
      INTO #REC-250 LEAVING NO
*
  PERFORM COMBINE-125
*
  IF #REC-125-A NE ' '
    ADD 1 TO #P-X2
    MOVE #REC-125-A TO #FULL-REC (#P-X2)
  END-IF
*
  WRITE WORK FILE 3 #FULL-RECORD   /* Multi Bills
  WRITE WORK FILE 5 #FULL-RECORD   /* Single Bills
*
  COMPRESS 'Insured-CN'    /* DW2
   #TAB 'TYPE'
   #TAB 'TOT-BILL'
   #TAB 'Insured-Name'
   #TAB 'Owner-CN'
   #TAB 'Owner-Name'
   #TAB 'ADDR1'
   #TAB 'ADDR2'
   #TAB 'ADDR3'
   #TAB 'ZIP'
     INTO #NOTC-OUT   LEAVING NO
  WRITE WORK FILE 6 #NOTC-OUT  /* Detail File for AAFM to use
END-IF
*

* IF #CN GE 600000  /* DW3      RE1
IF AFFL-ONLY-CLIENT(<#CN>)   /* RE1
  ESCAPE ROUTINE
END-IF
*
MOVE 'S' TO #SPLIT
RESET #STREET #STREET-2 #CITY #STATE
CALLNAT 'GCTSTSXX'  /* split city/state
 #SPLIT
 #ADDR-1
 #ADDR-2
 #ADDR-3
 #STREET
 #STREET-2
 #CITY
 #STATE
 #ERR-MSG
*
RESET #I2 #DUE-LINES (*) #TEXT-LINES (*)
FOR #I1 3 TO 22    /* WAS 6 to 22
  ADD 1 TO #I2
  MOVE #SCR-TEXT (#I1) TO #DUE-LINES (#I2)
* DISPLAY #I1 #SCR-TEXT (#I1) (EM=XXXXXXXXXXXXXXXXXXXXXX)
*  #I2 #DUE-LINES (#I2) (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
  IF  (#SCR-TEXT (#I1) = MASK ('AMOUNT PAYABLE')) /* Normal Prem Bill
   OR (#SCR-TEXT (#I1) = MASK ('TOTAL OUTSTANDING LOAN')) /* Int Bill
   OR (#SCR-TEXT (#I1) = MASK ('DUE-DATE'))               /* FPF bill
   OR (#I2 GE 16)     /* All other types
    RESET #I2
    ADD 1 TO #I1
    FOR #I1 #I1 TO 22
      ADD 1 TO #I2
      IF #I2 GT 8
        ESCAPE BOTTOM
      END-IF
*
      MOVE #SCR-TEXT (#I1) TO #TEXT-LINES (#I2)
* DISPLAY #I1 #SCR-TEXT (#I1) (EM=XXXXXXXXXXXXXXXXXXXXXX)
*  #I2 #DUE-LINES (#I2) (EM=XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX)
    END-FOR
  END-IF
END-FOR
*
RESET #TYPE
FOR #I1 1 TO 3
  IF #SCR-TYPE-IND-3-X (#I1) = '-'
    ESCAPE BOTTOM
  END-IF
*
  MOVE #SCR-TYPE-IND-3-X (#I1) TO #TYPE-X (#I1)
END-FOR
*
RESET #FREQ
IF  (#TYPE = MASK ('P'))
 OR (#TYPE = 'CPL' OR = 'CAL' OR = 'CFA' OR = 'CAA')
  IF #MODE = 'A'
    MOVE 'Annual' TO #FREQ
  ELSE
    IF #MODE = 'S'
      MOVE 'Semi-annual' TO #FREQ
    ELSE
      IF #MODE = 'Q'
        MOVE 'Quarterly' TO #FREQ
      ELSE
        MOVE 'Monthly' TO #FREQ
      END-IF
    END-IF
  END-IF
END-IF
*
IF #TYPE = 'Q'
  RESET #PRM-DUE-MM-DD-YYYY #TOT-AMT-DUE
END-IF
*
RESET #FULL-REC (*) #P-X2
COMPRESS
  #TYPE
  #TAB #CN
  #TAB #LST-POL-NUM
  #TAB #FREQ
  #TAB #RUN-DT-MM-DD-YYYY
  #TAB #PRM-DUE-MM-DD-YYYY
  #TAB #TOT-AMT-DUE
  #TAB #SCR-MBR-NAME-FULL   /* 35
  #TAB #DEAR-NAME           /* 50
  #TAB #STREET              /* 25
  #TAB #STREET-2            /* 25
  #TAB #CITY                /* 25
  #TAB #STATE               /* 2
  #TAB #ZIP-5 #ZIP-4   /* 9
    INTO #REC-250 LEAVING NO
*
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #SPS-GEN-NAME
  #TAB #SPS-GEN-CN
  #TAB #AAFM-BAR-CODE-1
  #TAB #AAFM-BAR-CODE-2
  #TAB #SCR-BAR-CD-SUPPRESSED
  #TAB #RID-DISP
  #TAB #DUE-LINES (1)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (2)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (3)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (4)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (5)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (6)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (7)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (8)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (9)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (10)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (11)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (12)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (13)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (14)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (15)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #DUE-LINES (16)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
  #TAB #TEXT-LINES (1)
    INTO #REC-250 LEAVING NO
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (2)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (3)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (4)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (5)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (6)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (7)
    INTO #REC-250
PERFORM COMBINE-125
*
COMPRESS #REC-250
      #TEXT-LINES (8)
    INTO #REC-250
PERFORM COMBINE-125
*
IF #REC-125-A NE ' '
  ADD 1 TO #P-X2
  MOVE #REC-125-A TO #FULL-REC (#P-X2)
END-IF
*
IF #MULT-NOTC = '####'
  WRITE WORK FILE 5 #FULL-RECORD   /* Multi Bills
ELSE
  WRITE WORK FILE 3 #FULL-RECORD   /* Single Bills
END-IF
*
MOVE LEFT #TOT-AMT-DUE TO #TOT-AMT-DUE
COMPRESS #ZIP-5 #ZIP-4 INTO #ZIP WITH '-'
IF #INSURED-CN = ' '
  MOVE #CN TO #INSURED-CN
END-IF
*
IF #INSURED-NAME = ' '
  MOVE #SCR-MBR-NAME-FULL TO #INSURED-NAME
END-IF
*
COMPRESS #INSURED-CN       /* DW2
   #TAB #TYPE
   #TAB #TOT-AMT-DUE
   #TAB #INSURED-NAME
   #TAB #CN
   #TAB #SCR-MBR-NAME-FULL
   #TAB #ADDR-1
   #TAB #ADDR-2
   #TAB #ADDR-3
   #TAB #ZIP
  INTO #NOTC-OUT   LEAVING NO
WRITE WORK FILE 6 #NOTC-OUT  /* Detail File for AAFM to use
*
* * * * * * * * * * * * * * *
DEFINE SUBROUTINE COMBINE-125
* * * * * * * * * * * * * * *
*
IF #REC-125-B NE ' '
  ADD 1 TO #P-X2
  MOVE #REC-125-A TO #FULL-REC (#P-X2)
  MOVE #REC-125-B TO #REC-125-A
  RESET #REC-125-B
END-IF
*
END-SUBROUTINE
*
END
