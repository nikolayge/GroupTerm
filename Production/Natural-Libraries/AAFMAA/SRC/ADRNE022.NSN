* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Email Formatting 
* Object name - ADRNE022
**********************************************************
*        MODIFICATION LOG
**********************************************************
* USER   DATE      TAG  REASON
* YK1 -  05102011  Correct e-mail formating to not display 
*                  empty '()'
**********************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 BLANK        (A1) CONST <' '>
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-TYPE
  2 EMAIL-ADDRESS
  2 EMAIL-PREFERRED-IND
  2 EMAIL-UNDELIV
  2 STATUS
  2 REMARKS      (A70/3)

1 #CODE-NAME     (A) DYNAMIC
1 #EML-PREF-TXT  (A20)
1 #EML-TYPE-TXT  (A20)
1 #EML-UNDLV-TXT (A20)
1 #EML-STATUS-TXT (A20)
1 #EMAIL-ATTRIBUTES (A) DYNAMIC          /* YK1
  
END-DEFINE

RESET #TEXT (*)

GET EMAIL-V #ISN

#CODE-NAME   := 'EML-PREF'
#EML-PREF-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,EMAIL-V.EMAIL-PREFERRED-IND>)
IF EMAIL-V.EMAIL-PREFERRED-IND = 'Y'
  #EML-PREF-TXT := 'Preferred'
ELSE
  RESET #EML-PREF-TXT
END-IF
*
#CODE-NAME   := 'EML-TYPE'
#EML-TYPE-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,EMAIL-V.EMAIL-TYPE>)
*
#CODE-NAME   := 'EML-UNDLV'
#EML-UNDLV-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,EMAIL-V.EMAIL-UNDELIV>)

#CODE-NAME   := 'EML-STATUS'
#EML-STATUS-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,EMAIL-V.STATUS>)

IF EMAIL-V.EMAIL-UNDELIV = 'Y'
  #EML-UNDLV-TXT := 'Undeliverable'
ELSE
  RESET #EML-UNDLV-TXT
END-IF
COMPRESS #EML-TYPE-TXT #EML-PREF-TXT #EML-UNDLV-TXT #EML-STATUS-TXT     /* YK1 start
         INTO #EMAIL-ATTRIBUTES                                         
IF #EMAIL-ATTRIBUTES NE ' '                                             
  COMPRESS '(' #EMAIL-ATTRIBUTES ')' INTO #EMAIL-ATTRIBUTES             
END-IF                                                                  
COMPRESS EMAIL-ADDRESS #EMAIL-ATTRIBUTES                                
*  '('                        
*   #EML-TYPE-TXT
*   #EML-PREF-TXT
*   #EML-UNDLV-TXT
*   #EML-STATUS-TXT
*  ')'                                                                  /* YK1 end
  INTO #TEXT (1)

IF REMARKS (1) NE BLANK
  #TEXT (2) := REMARKS (1)
END-IF

END
