* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPGENM   ** Generations Plus Mailing

************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* DRW  12/24/2008 XXX Created
* RSE  10052009   RE1  Change PROCESS-IND logic for Unification
* TMT  05/05/2014 TT1 CONTACT NORMALIZATION
* TMT  09/19/2014 TT2 ADDRESS FORMATING
************************************************************************
*
DEFINE DATA
LOCAL USING GONERLXX           /* WORK FIELDS FOR GONERCXX
LOCAL USING ADDA0001
LOCAL
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
*
1 #JCL-DATE       (A10)
1 REDEFINE #JCL-DATE
  2 #JCL-MM            (A2)   /* DATE PARM 03/01/1999
  2 #JCL-X1            (A1)
  2 #JCL-DD            (A2)
  2 #JCL-X2            (A1)
  2 #JCL-YYYY          (A4)
*
1 #PARM-YYYYMMDD  (N8)
1 REDEFINE #PARM-YYYYMMDD
  2 #PARM-YYYYMMDD-A  (A8)
1 REDEFINE #PARM-YYYYMMDD
  2 #PARM-YYYY      (N4)
  2 #parm-mm        (n2)
  2 #parm-dd        (n2)
*
1 #email-addr (a60)
1 #GEN-LETTER-NEEDED   (l)
*
1 fr-v view of a-firs
  2 DEPN-NAME           (14)
  2 depn-status-cd      (14)
  2 depn-date-of-birth  (14)
*
1  ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A   (A6)
  2 MEMBER-CONTACT-ID
  2 SSN
  2 WIDOW-FLAG
  2 PROCESS-IND
  2 INS-MAIL-CODE
  2 NAME
  2 name-sp
  2 title-sp
  2 INTRO-FLAG
  2 CHIEF-OF-STAFF-IND
  2 RANK
  2 military-status
  2 MILITARY-SERVICE
  2 NUMBER-MEMBERSHIPS
*
1 #SPLIT    (A1)
1 #ERR-MSG  (A60)
1 #STREET   (A25)
1 #STREET-2 (A25)
1 #CITY     (A22)
1 #STATE    (A2)
1 #NAME      (A25)
1 #LAST      (A25)
1 #FIRST-20  (A20)
1 #MI        (A1)
*
1 #FORMATED-NAME  (A45)
* 1 #FIRST-ADDRESS  (A25)         /* TT2 BEG
* 1 #SECOND-ADDRESS (A40)
* 1 #THIRD-ADDRESS  (A40)         /* TT2 END
1 #FORMAT-CN      (A7)
1 #FORMAT-MEMSHIP (A2)
1 #DEAR-NAME    (A28)
1 #LAST-NAME    (A25)
1 #FIRST/MID    (A25)
1 #FIRST-NAME   (A25)
1 #MIDDLE-INIT  (A25)
1 #SUFFIX       (A25)
1 #EXTRA1      (A25)
1 #EXTRA2      (A25)
1 #EXTRA3      (A25)
1 #EXTRA4      (A25)
*
1 #FULL-RANK       (A23)
1 #XXX            (A3) INIT <'XXX'>
1 #EDIT-INVALID (L)
1 #RANK-LITERAL (A23)
1 #HOLD-RANK-5  (A5)
1 #DISPL        (P2)
1 #PASS-COMMAND (A2)   INIT <'AD'>
*
1 #ERROR-MESSAGE  (A25)
1 #EXTRACTS       (P6)
*
1 #NUM-RECS          (P5)
1 #EXTRACT-150    (A200)   /* WAS 150    /* DW6
1 #EXTRACT-125    (A125)
1 #REC-OVERFLOW      (A250)
1 REDEFINE #REC-OVERFLOW
  2 #REC-OVF-1ST-125   (A125)
  2 #REC-OVF-2ND-125   (A125)
*
1 #I1    (I2)
1 #I2    (I2)
1 #I3    (I2)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY   (N4)
  2 #MM     (N2)
  2 #DD     (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYY-A (A4)
  2 #MM-A   (A2)
  2 #DD-A   (A2)
1 #1-YR-AGO     (N8)
*
1 #LETTER-TABLE
  2 #1T (A26) INIT <'AaBbCcDdEeFfGgHhIiJjKkLlMm'>
  2 #2T (A26) INIT <'NnOoPpQqRrSsTtUuVvWwXxYyZz'>
1 REDEFINE #LETTER-TABLE
  2 #LETTER-OCC  (26)
    3 #UPPER     (A1)
    3 #LOWER     (A1)
*
1 #HOLD-LAST-NAME        (A25)
1 REDEFINE #HOLD-LAST-NAME
  2 #HOLD-LAST-NAME-A     (A1/25)
*
1 #USER-SOURCE    (A3)
1 #DATE-RECEIVED  (N8)
1 #CNT-2     (P5)
1 #BASD    (N8)
1 #DATE-OF-BIRTH             (N8.0)
1 REDEFINE #DATE-OF-BIRTH
  2 #DOB-YYYY  (N4)
  2 #DOB-MM    (N2)
  2 #DOB-DD    (N2)
1 #EMAIL-ADDRESS             (A60)
1 #ZIP-5-STRING        (A5)        /* TT2
1 #ZIP-4-STRING        (A4)        /* TT2
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=62
WRITE      TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
FORMAT (2) LS=133 PS=62
WRITE  (2) TITLE LEFT 'PROGRAM:' *PROGRAM  '   Extracted addr info'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
READ WORK 1 ONCE RECORD #JCL-DATE
COMPRESS #JCL-YYYY #JCL-MM #JCL-DD INTO #PARM-YYYYMMDD-A LEAVING NO
WRITE 'parm-date =' #PARM-YYYYMMDD
*
MOVE *DATN TO #YYYYMMDD
SUBTRACT 1 FROM #YYYY
MOVE #YYYYMMDD TO #1-YR-AGO
*
COMPRESS 'CN'    /* <<<<<<<<<<<<<<<<<< EXCEL LABELS
  #TAB 'Formated Name'
  #TAB 'Street-1'
  #TAB 'Street-2'
  #TAB 'City'
  #TAB 'State'
  #TAB 'ZIP-CODE'
  #TAB 'EMail'
  INTO #EXTRACT-150 LEAVING NO
WRITE WORK FILE 2 #EXTRACT-150
*
READ ST-V BY MEMBER-KEY FROM '001900'
*
  RESET #PDA-ADDRESS
  MOVE ST-V.MEMBER-CONTACT-ID TO #ADD-CONTACT-ID
  MOVE 'P' TO #ADD-TYPE
  CALLNAT 'ADDN0001' #PDA-ADDRESS

  IF  (WIDOW-FLAG = 'Y')     /* BYPASS WIDOWS ON MBR RUN
      OR (NUMBER-MEMBERSHIPS = 0) /* BYPASS INACTIVE MBRS ON MEMBER RUN
      OR ((#ADD-LINE-1 = ' ')                        /* NO ADDRESS
      AND (#ADD-LINE-2 = ' ')                        /* NO ADDRESS
      AND (#ADD-LINE-3 = ' '))                        /* NO ADDRESS
*  OR (ZIP-CODE = 0)                           /* FOREIGN
      OR (INS-MAIL-CODE = 'S')  /* NO SALES MAILINGS
      OR (INS-MAIL-CODE = 'E')  /* NO E-MAILS
      OR (INS-MAIL-CODE = 'F')  /* NO B-DAYS/SALES/E-MAILS
      OR (F-UNDEL-ADDRESS(<ST-V.MEMBER-CONTACT-ID>)) /* UNDELIVERABLE
      OR (PROCESS-IND = 'C')                      /* NO CONTACT ENTITIES
*   OR (PROCESS-IND = 'S')                      /* NO AFSC MEMBERS     RE1
      OR AFFL-ONLY-CLIENT(<ID-NUMBER>)                                 /* RE1
      OR (INTRO-FLAG = 'S' OR = 'K' OR = 'G')     /* NO SPS OR GEN
*
    ESCAPE TOP
  END-IF
*
  reset #GEN-LETTER-NEEDED
*
  RESET #DATE-OF-BIRTH                                                 /* TT1
  #DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)        /* TT1
  find fr-v with member-key = st-v.id-number-a
    for #i1 1 to 14
      if   (not fr-v.depn-status-cd (#i1) = 'D')
          and (fr-v.depn-date-of-birth (#i1) ge *datn - 130000) /* Less than age 13
          and (#dob-mm  = #parm-mm)                                    /* TT1
        move true to #GEN-LETTER-NEEDED
        escape bottom
      end-if
    end-for
  end-find
*
  if not #gen-letter-needed
    escape top
  end-if
*
  IF #ADD-COUNTRY-CODE = 'US'                   /* TT2 beg
    IF #ADD-LINE-3 = ' '
      RESET #ADD-LINE-2
    ELSE
      RESET #ADD-LINE-3
    END-IF
  ELSE
    #CITY := #ADD-LINE-3
    RESET #ADD-ZIP-CODE #ADD-STATE
  END-IF                                         /* TT2 END
*
*  below to extract state and city
*   MOVE 'S' TO #SPLIT
*   RESET #STREET #STREET-2 #CITY #STATE
* DISPLAY ST-V.ID-NUMBER ST-V.ADDRESS-1 ST-V.ADDRESS-2 ST-V.ADDRESS-3
* IF NOT ST-V.ID-NUMBER = 590667
*   CALLNAT 'GCTSTSXX'  /* SPLIT CITY/STATE / DO NOT REMOVE IN CASE CITY
*     #SPLIT
*     #ADD-LINE-1     /* AND STATE ARE NEEDED FOR EXTRACT FILE
*     #ADD-LINE-2
*     #ADD-LINE-3
*     #STREET
*     #STREET-2
*     #CITY
*     #STATE
*     #ERR-MSG
*
*   IF #ADD-LINE-3 = ' '      /* DW4
*     RESET #SECOND-ADDRESS
*   ELSE
*     MOVE #ADD-LINE-2 TO #SECOND-ADDRESS
*   END-IF
*
  ADD 1 TO #EXTRACTS
*
  RESET #DISPL #RANK-LITERAL #HOLD-RANK-5                         /* PM1
  CALLNAT 'GRANKSXX' #PASS-COMMAND ST-V.RANK MILITARY-SERVICE
    #EDIT-INVALID #DISPL #RANK-LITERAL #HOLD-RANK-5
  MOVE #RANK-LITERAL TO #FULL-RANK
*
* DISPLAY #DISPL #RANK-LITERAL #HOLD-RANK-5 MILITARY-SERVICE
*
  IF WIDOW-FLAG = 'Y'
    SEPARATE NAME-SP LEFT INTO #LAST-NAME #FIRST/MID
      #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER ','
    SEPARATE #FIRST/MID INTO #FIRST-NAME #MIDDLE-INIT #EXTRA1
      #EXTRA1 #EXTRA2 #EXTRA3 #EXTRA4
      WITH DELIMITER ' '
    COMPRESS TITLE-SP #FIRST/MID #LAST-NAME INTO #FORMATED-NAME
    MOVE TITLE-SP TO #HOLD-RANK-5
  ELSE
    SEPARATE ST-V.NAME LEFT INTO #LAST-NAME #FIRST/MID
      #SUFFIX #EXTRA1 #EXTRA2 WITH DELIMITER ','
    SEPARATE #FIRST/MID INTO #FIRST-NAME #MIDDLE-INIT
      #EXTRA1 #EXTRA2 #EXTRA3 #EXTRA4
      WITH DELIMITER ' '
*
    IF CHIEF-OF-STAFF-IND = 'R' OR = 'Q' /* INDS RANK SECURITY
        OR ST-V.INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      RESET #HOLD-RANK-5
    END-IF
*
    COMPRESS #HOLD-RANK-5 #FIRST/MID #LAST-NAME INTO #FORMATED-NAME
  END-IF
*
  IF #SUFFIX NE ' '
    COMPRESS #FORMATED-NAME ',' INTO #FORMATED-NAME LEAVING NO
    COMPRESS #FORMATED-NAME #SUFFIX INTO #FORMATED-NAME
  END-IF
*
  IF #FULL-RANK = ' '
    COMPRESS #FORMATED-NAME TO #DEAR-NAME
  ELSE
    COMPRESS #FULL-RANK #LAST-NAME INTO #DEAR-NAME
  END-IF
*
  IF  (st-v.military-status = 'R')
      AND (CHIEF-OF-STAFF-IND NE 'X')  /* recalled to act duty
      AND (NOT ST-V.RANK = 'MR' OR = 'MS' OR = 'MRS' OR = 'MIS'
      OR = 'REV' OR = 'DR' OR = ' ')
      AND (WIDOW-FLAG = ' ')     /* NOT RET ON WIDOWS
    COMPRESS #FORMATED-NAME ', RET' INTO #FORMATED-NAME LEAVING NO
  END-IF
*
  reset #email-addr #EMAIL-ADDRESS                                           /* TT1 BEGIN
  #EMAIL-ADDRESS := FUNCTION-PREFFERED-EMAIL(<ST-V.MEMBER-CONTACT-ID>)
  if (#EMAIL-ADDRESS = MASK ('999')) /* INDICATES DO NOT SHOW ON DI
      OR (#EMAIL-ADDRESS = MASK ('?'))    /* INVALID OR NOT FOUND ADDR
      OR (#EMAIL-ADDRESS = MASK (' '))    /* NOT FOUND ADDR
    ignore
  else
    move #EMAIL-ADDRESS to #email-addr                                       /* TT1 END
  end-if
*
  RESET #ZIP-5-STRING #ZIP-4-STRING                      /* TT2 BEGIN

  IF #ADD-ZIP5 = 0
    #ZIP-5-STRING := ' '
  ELSE
    COMPRESS FULL #ADD-ZIP5 INTO #ZIP-5-STRING LEAVING NO
    COMPRESS FULL #ADD-ZIP4 INTO #ZIP-4-STRING LEAVING NO /* TT2 END
  END-IF

  COMPRESS                  /* USe IF NOT COMPRESSED
    ST-V.ID-NUMBER
    #TAB #formated-name
    #TAB #ADD-LINE-1
    #TAB #ADD-LINE-2               /* TT2
    #TAB #ADD-CITY
    #TAB #ADD-STATE
    #TAB #ZIP-5-STRING
    INTO #REC-OVERFLOW LEAVING NO
  IF #ADD-ZIP4 NE 0
    COMPRESS #REC-OVERFLOW '-' #ZIP-4-STRING
      INTO #REC-OVERFLOW LEAVING NO
  END-IF
*
  COMPRESS #REC-OVERFLOW                  /* DW6
    #taB #email-addr
    INTO #REC-OVERFLOW LEAVING NO
*
*  below to use raw address 1/2/3 instead of above
*    #TAB ST-V.ADDRESS-1 #TAB ST-V.ADDRESS-2 #TAB ST-V.ADDRESS-3
*    #TAB ST-V.ZIP-CODE-5-A INTO #REC-OVERFLOW LEAVING NO
* IF ST-V.ZIP-CODE-4 NE 0
*    COMPRESS #REC-OVERFLOW '-' ST-V.ZIP-CODE-4-A
*                               INTO #REC-OVERFLOW LEAVING NO
* END-IF
*
  MOVE #REC-OVERFLOW TO #EXTRACT-150
*
  WRITE WORK FILE 2 #EXTRACT-150
  ADD 1 TO #NUM-RECS
*
END-READ /* (0181)
*
WRITE (2) 'NUMBER MEMBERS  = ' #EXTRACTS
WRITE (2) 'NUMBER RECORDS  = ' #NUM-RECS
*
* * * * * * * * * * *
DEFINE UPPER-LOWER
* * * * * * * * *
*
FOR #I1 2 TO 25
  FOR #I2 1 TO 26
    IF #HOLD-LAST-NAME-A (#I1) = #UPPER (#I2)
      MOVE #LOWER (#I2) TO #HOLD-LAST-NAME-A (#I1)
    END-IF
  END-FOR
END-FOR
*
END-SUBROUTINE
*
END
