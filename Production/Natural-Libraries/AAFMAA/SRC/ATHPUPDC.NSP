* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : ATHPUPDC                                              *
*                                                                      *
* FUNCTION     : Read Accounting register File for a specific date of  *
*                collections and update the CNs on Process-Status file *       *
*                                                                      *
************************************************************************
*        MODIFICATION LOG                                              *
************************************************************************
* USER   DATE        TAG     REASON                                    *
* VXT    02/07/2012  None    Program Written                           *
************************************************************************
*
DEFINE DATA
local using ADDA0001
LOCAL
1 PROC-REQ-V VIEW OF A-PROCESS-REQUEST
  2 LAST-UPD-USER
  2 LAST-UPD-DATE
  2 LAST-UPD-TIME
  2 PROCESS-DESCRIPTION (A250/1:9)
  2 PROCESS-STATUS (A1/1:9)
  2 PROCESS-STATUS-DATE (N8/1:9)
  2 PROCESS-MESSAGE (A250/1:9)
  2 ID-NUMBER
*
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 MILITARY-STATUS
  2 MILITARY-SERVICE
*
1 ACC-REG VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER
  2 TRANSACTION-DATN
  2 TRANSACTION-TIMN
  2 TXN-CODE
  2 ACCT-DATE
  2 DATE-LAST-ACCT-UPDATE
  2 DEBIT-AMOUNT
  2 CREDIT-AMOUNT
  2 DESCRIPTION
  2 CLERK-ID
  2 CASH-CODE
*
1 #cnt                 (p7)
1 #cnt-upd             (p7)
1 #datn                (n8)
1 redefine #datn
  2 #datn-yyyy         (n4)
  2 #datn-mm           (n2)
  2 #datn-dd           (n2)
1 #process-date        (a10)
1 #isn-proc            (p10)
1 #curr-date           (n8)
1 #id-number           (n6)
end-define
*
#curr-date := *datn
#datn   := *datn
* #datn   := 20120206
compress #datn-mm '/' #datn-dd '/' #datn-yyyy into #process-date leaving no
*
f1.
find acc-reg with acc-reg.TRANSACTION-DATN = #datn

  if acc-reg.cash-code = 'T'
    add 1 to #cnt
    #id-number := acc-reg.id-number
*
    f2.
    find proc-req-V with PROC-REQ-V.id-number = acc-reg.id-number
      if no
        escape bottom (f2.)
      end-norec
      move *isn to #isn-proc
      perform update-process-status
    end-find
  END-IF
*
  at end of data
    write
      'number of records selected for processing        ' #cnt /
      'number of records updated on process-status file ' #cnt-upd /
  end-enddata
*
end-find
*
*****************
DEFINE SUBROUTINE update-process-status
*****************
*
GET1.
get proc-req-V #ISn-proc
proc-req-V.LAST-UPD-USER           := 'GPRPUPDT'
proc-req-V.LAST-UPD-DATE           := #curr-date
proc-req-V.LAST-UPD-TIME           := *timn
proc-req-V.PROCESS-STATUS-DATE (2) := #curr-date
proc-req-V.PROCESS-MESSAGE (2)     := 'Included in PeachTree extract'
proc-req-v.process-status (2)      := 'C'
update (get1.)
add 1 to #cnt-upd
*
end-subroutine
*
end
