* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*  GOST0N01 
* 1. Create Dummy STATUS record
* 2. Autorepare Terminal record
* Usage 
* CALLNAT 'GOST0N01' p_CN p_Err p_Err_Dscr p_Mod
* Parameters: 
*  p_CN       (N6)
*  p_Err      (N4)  
*  p_Err_Dscr (A78)   
*  p_Mod      (A1)  
* 
* bz created 05/15/2011
* TT1 MODIFIED 10/07/2013  ADDRESS NORMALIZATION
DEFINE DATA 

PARAMETER 
1 p_PARMS
  2 p_CN       (N6)
  2 p_Err      (N4)   
  2 p_Err_Dscr (A78) 
  2 p_Mod      (A1) 
  
LOCAL
1 ST-V VIEW OF A-STATUS
   2 ID-NUMBER
   2 DTS-DATN                 
   2 DTS-TIMN                 
*
1  TRM-V VIEW OF A-TERMINALS   /*TT1
 2 TERMINAL-ID              (A8)
 2 COMMENTS                 (A20)
 2 REDEFINE COMMENTS
  3 LAST-CN                 (N6)
* 
END-DEFINE
*
  ON ERROR 
     IF p_MOD <> 'R' AND *ERROR-NR = 3145
          RESET p_PARMS
          p_Mod := 'R' 
          F-SLEEP(<>) 
          CALLNAT  'GOST0N01' p_CN p_Err p_Err_Dscr p_Mod
          ESCAPE ROUTINE
     END-IF       
     p_Err := *ERROR-NR
     COMPRESS 'Natural Error:'*PROGRAM *ERROR-NR  'Error Line:' *ERROR-LINE INTO p_Err_Dscr
     ESCAPE ROUTINE 
  END-ERROR 
* 
 FTRM.
 FIND (1) TRM-V WITH TERMINAL-ID = 'CNMASTER'
    READ (1) ST-V DESCENDING  BY ID-NUMBER   END-READ
        IF ID-NUMBER < LAST-CN
          ID-NUMBER := LAST-CN
          LAST-CN := LAST-CN + 1
        ELSE 
          ID-NUMBER := ID-NUMBER + 1  
          LAST-CN   := ID-NUMBER + 1
        END-IF  
         UPDATE(FTRM.)
         STORE ST-V 
    END-FIND
 p_CN := ID-NUMBER
END
