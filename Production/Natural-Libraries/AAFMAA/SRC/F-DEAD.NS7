* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-DEAD                                                *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Find policy status from policy segment                *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
* PAM    07/18/2009  PM1     ACH phase II                              *
************************************************************************
*
DEFINE FUNCTION FUNCTION-DECEASED RETURNS (A1) BY VALUE
*
DEFINE DATA
*
PARAMETER
*
1 #CN-A                  (A6)
1 #SEGMENT               (I2)                                    /* PM1
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 DATE-OF-DEATH
 2 ISSUE-DATE            (9)                                     /* PM1
 2 PLAN                  (9)                                     /* PM1
*
1 FR-V VIEW OF A-FIRS                                            /* PM1
 2 ID-NUMBER                                                     /* PM1
 2 POLICY-TABLE          (45)                                    /* PM1
  3 POL-TYPE                                                     /* PM1                          
  3 POL-DATE                                                     /* PM1
  3 POL-DATE-CLM-SETTLED                                         /* PM1
*
1 #I1                    (I2)
1 #FOUND-FIR             (L)
*
END-DEFINE
************************************************************************
*
RESET FUNCTION-DECEASED
*
FIND (1) ST-V WITH MEMBER-KEY = #CN-A
  IF NO RECORD FOUND
    FIND (1) ST-V WITH WIDOW-KEY = #CN-A
      IF NO RECORD FOUND
        ESCAPE ROUTINE
      END-NOREC
    END-FIND
  END-NOREC
END-FIND
*
IF ST-V.DATE-OF-DEATH NE 0
  FIND (1) FR-V WITH MEMBER-KEY = #CN-A                          /* PM1
    FOR #I1 1 TO 45                                              /* PM1
      IF FR-V.POL-DATE (#I1) = ST-V.ISSUE-DATE (#SEGMENT)        /* PM1
       AND FR-V.POL-TYPE (#I1) = ST-V.PLAN (#SEGMENT)            /* PM1
        #FOUND-FIR    := TRUE                                    /* PM1
        ESCAPE BOTTOM                                            /* PM1
      END-IF                                                     /* PM1
    END-FOR                                                      /* PM1
  END-FIND                                                       /* PM1
*   
  IF #FOUND-FIR                                                  /* PM1
   AND FR-V.POL-DATE-CLM-SETTLED (#I1) NE 0                      /* PM1
    FUNCTION-DECEASED := 'S'                                     /* PM1
  ELSE                                                           /* PM1
    FUNCTION-DECEASED := 'P'                                     /* PM1
  END-IF                                                         /* PM1
ELSE 
  FUNCTION-DECEASED   := 'N'
END-IF
*
END-FUNCTION
*
END
