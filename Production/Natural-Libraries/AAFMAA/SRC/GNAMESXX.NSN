* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*   PGM-ID GNAMESXX
*    DOES THE NAME FIELD EDIT
*
DEFINE DATA PARAMETER
1 #NAME-PASS (A25)
1 #EDIT-FLAG (A1)
*
LOCAL
*
1 #EDIT-ERROR (A1)
1 #INDEX      (I2)
1 #INDEX2     (I2)
1 #NAME-CHARACTER (A1/25)
1 REDEFINE #NAME-CHARACTER
  2 #NAME-ARRAY   (A25)
1 #CONTACT (L)
1 #FOUND-COMMA (L)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IF #EDIT-FLAG = 'C'
  MOVE TRUE TO #CONTACT
END-IF
*
MOVE #NAME-PASS TO #NAME-ARRAY
MOVE 'N'        TO #EDIT-FLAG
MOVE 'N'        TO #EDIT-ERROR
*
REPEAT
*
FOR #INDEX 1 TO 25
  IF   #NAME-CHARACTER(#INDEX) NE MASK(U)
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) ''''
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) '_'
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) ' '
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) '-'
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) '/'
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) ','
   AND #NAME-CHARACTER(#INDEX) NE MASK(X) '.'
    ESCAPE ROUTINE IMMEDIATE      /* bad name contents
  END-IF
END-FOR
*
IF #NAME-CHARACTER (1) = ' ' OR = ','   /* 1ST POS MUST NOT BE SPACE/,
  MOVE 'Y' TO #EDIT-ERROR
  ESCAPE BOTTOM
END-IF
*
RESET #FOUND-COMMA
FOR #INDEX 1 TO 25                   /* FIND FIRST COMMA
  IF #NAME-CHARACTER (#INDEX) = ','
    MOVE TRUE TO #FOUND-COMMA
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #CONTACT AND NOT #FOUND-COMMA
  ESCAPE BOTTOM
END-IF
*
IF #INDEX GE 24                  /* NOT ENOUGH ROOM FOR FIRST/MIDDLE
  MOVE 'Y' TO #EDIT-ERROR
  ESCAPE BOTTOM
END-IF
*
IF #NAME-CHARACTER (#INDEX + 1) NE ' '  /* IS COMMA FOLLOWED BY SPACE
  MOVE 'Y' TO #EDIT-ERROR
  ESCAPE BOTTOM
END-IF
*
IF #NAME-CHARACTER (#INDEX + 2) = ' '  /* IS COMMA FOLLOWED BY 2 SPACES
  MOVE 'Y' TO #EDIT-ERROR
  ESCAPE BOTTOM
END-IF
*
IF #INDEX = 23
  IF #NAME-CHARACTER (25) = MASK(U)
    MOVE 'N' TO #EDIT-ERROR
  ELSE
    MOVE 'Y' TO #EDIT-ERROR
  END-IF
*
  ESCAPE BOTTOM
END-IF
*
COMPUTE #INDEX = #INDEX + 2              /* POINT TO START OF FIRST/MID
*
FOR #INDEX2 FROM #INDEX TO 24
  IF   (#NAME-CHARACTER (#INDEX2)      = ' ')   /* FIND FIRST AND
   AND (#NAME-CHARACTER (#INDEX2 + 1) NE ' ')   /*      MIDDLE
    ESCAPE TOP
  END-IF
  IF #NAME-CHARACTER (#INDEX2) = ',' OR = ' '  /* FIND END OF FIRST/MID
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF #INDEX2 < 25
  IF   (#NAME-CHARACTER (#INDEX2)     = ' ')   /* IS FIRST/MID FOLLOWED
   AND (#NAME-CHARACTER (#INDEX2 + 1) = ' ')   /* BY ' '
    COMPUTE #INDEX2 = #INDEX2 + 1
    FOR #INDEX FROM #INDEX2 TO 25
      IF #NAME-CHARACTER(#INDEX) NE ' '
        MOVE 'Y' TO #EDIT-ERROR
        ESCAPE BOTTOM
      END-IF
    END-FOR
  ELSE
*
*     FOLLOWING CODE REMOVED AS PER MILLIE MITCHELL 10/26/88
*
*   IF   (#NAME-CHARACTER (#INDEX2)      = ',') /* IS FIRST/MID FOLLOWED
*    AND (#NAME-CHARACTER (#INDEX2 + 1) NE ' ') /* BY ','
*     MOVE 'Y' TO #EDIT-ERROR
*     ESCAPE BOTTOM
*   ELSE
      COMPUTE #INDEX2 = #INDEX2 + 2
      FOR #INDEX FROM #INDEX2 TO 25
        IF #NAME-CHARACTER (#INDEX) = ','   /* NO MORE COMMAS ALLOWED
          MOVE 'Y' TO #EDIT-ERROR
          ESCAPE BOTTOM
        END-IF
      END-FOR
      ESCAPE BOTTOM
*   END-IF
  END-IF
END-IF
*
ESCAPE BOTTOM
*
END-REPEAT /* (0035)
*
IF #EDIT-ERROR NE 'Y'
  MOVE 'Y' TO #EDIT-FLAG
END-IF
*
END
