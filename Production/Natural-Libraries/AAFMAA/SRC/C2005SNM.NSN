* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2005SNM
*
DEFINE DATA
PARAMETER
1 #CON-BD-NUM   (N6)
1 #CON-BD-X     (N1)
1 #CON-CN-NUM   (N6)
1 REDEFINE #CON-CN-NUM
 2 #CON-CN-NUM-A (A6)
*
LOCAL
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER   (N6)
 2 NOTE-DATE   (N8/1)
 2 NOTE-TEXT   (A60/60)
 2 REDEFINE NOTE-TEXT
  3 BENE-TEXTS-6-POLS (6)
   4 BENE-TEXT-PRIM-SEC (2)
    5 BENE-TEXT-OCC  (5)
     6 BENE-TEXT     (A60)
     6 REDEFINE BENE-TEXT
      7 BENE-TEXT-1ST-25 (A25)
      7 BENE-TEXT-LST-35 (A35)
*
1 ST-V VIEW OF A-STATUS
 2 LINKED-CN   (1)
*
END-DEFINE
*
* WRITE 'top of c2005snm'
FIND NT-V WITH ID-NUMBER = #CON-BD-NUM
  IF NO RECORDS FOUND
    WRITE 'BD record not found- call EDS'
    ESCAPE BOTTOM
  END-NOREC
*
  IF BENE-TEXT-LST-35 (#CON-BD-X, 2, 5) = '*'
    COMPRESS #CON-CN-NUM '*'
       INTO BENE-TEXT-LST-35 (#CON-BD-X, 2, 5) LEAVING NO
  ELSE
    MOVE #CON-CN-NUM TO BENE-TEXT-LST-35 (#CON-BD-X, 2, 5)
  END-IF
*
  UPDATE (0037)
*
  IF #CON-BD-NUM GT 400000
    SUBTRACT 400000 FROM #CON-BD-NUM
  ELSE
    SUBTRACT 200000 FROM #CON-BD-NUM
  END-IF
*
  WRITE // 'The new Contact CN is' #CON-CN-NUM
        // 'You have just linked it to' #CON-BD-NUM
           'Designation' #CON-BD-X
        // 'Press enter to proceed'
*
  FIND (1) ST-V WITH MEMBER-KEY = #CON-CN-NUM-A
    MOVE #CON-BD-NUM TO ST-V.LINKED-CN (1)
    UPDATE (0063)
  END-FIND
*
  END TRANSACTION
END-FIND
*
END
