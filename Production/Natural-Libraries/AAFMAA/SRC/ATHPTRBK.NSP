* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPTRBK prepare files for 3-day troy processing (backup)
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* SAG  11292005 RH1  SS - Modified the custom coded ON ERROR stmt &    *
*                    replaced it with copycode GABNDCX3                *
************************************************************************
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11292005 SS
LOCAL
*
1 TODAY-FILE  (A80)
1 SAVED-FILE  (A80)
*
1  TM-V VIEW OF A-TERMINALS
 2 TERMINAL-ID     (A8)
 2 COMMENTS        (A20)
 2 REDEFINE COMMENTS
  3 TROY-DAY-LBL     (A4)  /* 'DAY '
  3 TROY-DAY         (N1)   /* 1/2/3
*
1 #SEND-TO-TROY   (L)
1 #DAY-SPREAD   (N3)  INIT <1>   /* number of days apart for printing
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR     /* SAG RH1 11292005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE  
INCLUDE GABNDCX3                                     /* SAG RH1 11292005 SS - END
END-ERROR
*
RESET #SEND-TO-TROY
FIND (1) TM-V WITH TERMINAL-ID = 'TROYDAY'
  IF NO RECORDS FOUND
    WRITE 'TROYDAY record not found'
    END TRANSACTION
    ESCAPE BOTTOM
  END-NOREC
*
  ADD 1 TO TROY-DAY
*
  IF TROY-DAY GT #DAY-SPREAD  /* start count over
    MOVE 1 TO TROY-DAY
  END-IF
*
  IF TROY-DAY GE #DAY-SPREAD
    MOVE TRUE TO #SEND-TO-TROY
  END-IF
*
  UPDATE RECORD (0041)
  END TRANSACTION
  DISPLAY TROY-DAY-LBL TROY-DAY #SEND-TO-TROY
END-FIND
*
IF #SEND-TO-TROY     /* file 4 (saved file) will be empty
  READ WORK 2 SAVED-FILE
    WRITE WORK 3 SAVED-FILE   /* troy file out
  END-WORK
*
  READ WORK 1 TODAY-FILE
    WRITE WORK 3 TODAY-FILE   /* troy file out
  END-WORK
ELSE     /* save input    /* file 3 (troy file) will be empty
  READ WORK 2 SAVED-FILE
    WRITE WORK 4 SAVED-FILE   /* saved file out
  END-WORK
*
  READ WORK 1 TODAY-FILE
    WRITE WORK 4 TODAY-FILE   /* save file out
  END-WORK
END-IF
*
END
