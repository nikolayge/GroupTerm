* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ESGLISDI    ADD SGLI lines
*******************************************
*                       MODIFICATION LOG
* USER DATE     TAG  REASON
* DRW  05272005 DW1  Use Basd/Sps date of Marriag/Dep Dob to calc eff dt
* DRW  09082005 DW2  Chg SGLI for DEP when DEP STATUS is I/J
* DRW  11152005 DW3  add name to sgli sps test
* TMT  06162014 TT1  Contact Normalization - DOB and SEX
*******************************************
DEFINE DATA
PARAMETER
1 #ID          (N6)
1 REDEFINE #ID
 2 #ID-A       (A6)
1 POL-CODE     (A1/45)
1 POL-NAME     (A19/45)
1 POL-NUMBER   (A14/45)
1 POL-TYPE     (A4/45)
1 POL-REMARKS  (A21/45)
1 POL-AMOUNT   (P7.0/45)
1 POL-DATE     (N8/45)
1 #ADDED-SGLI-LINES  (N3)
1 #ADDED-SPS-SGLI  (L)
1 #ADDED-DEP-SGLI  (L)
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 MILITARY-STATUS
 2 MILITARY-SERVICE
 2 NAME-SP
 2 STATUS-CODE-SP
*  2 DATE-OF-BIRTH-SP                 /* TT1
 2 CHIEF-OF-STAFF-IND
 2 ACTIVE-DUTY-BASE-DATE   /* DW1
 2 DATE-OF-MARRIAGE        /* DW1
*
1 FR-V VIEW OF A-FIRS
 2 DEPN-NAME          (14)
 2 DEPN-DATE-OF-BIRTH (14)
 2 DEPN-STATUS-CD     (14)
*
1 #LAST-NAME    (A25)
1 #FIRST-MID    (A25)
1 #FIRST-NAME   (A25)
1 #SUF-1        (A25)
1 #EXTRA        (A25)
*
1 #I1    (I2)
1 #I2    (I2)
1 #HAS-50K-SGLI-NOW (L)
1 #HAS-SPS      (L)
1 #HAS-DEPNS    (L)
1 #SGLI-X       (I2)
1 #NXT-EMPTY-X  (I2)
1 #MBR-SGLI-AMT (P7)
*
1 #YYYYMMDD    (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY    (N4)
 2 #MM      (N2)
 2 #DD      (N2)
*
END-DEFINE
*
* WRITE 'TOP OF ESGLISDI'
FIND (1) ST-V WITH MEMBER-KEY = #ID-A
END-FIND
*
* IF #ID-A = '105376' DISPLAY POL-NAME(*) POL-REMARKS(*) END-IF
IF ((MILITARY-STATUS = 'A')
 AND (NOT MILITARY-SERVICE = 'ACR' OR = 'FCR'))
 OR (CHIEF-OF-STAFF-IND = 'X')
  IGNORE
ELSE
  ESCAPE ROUTINE
END-IF
*
FIND (1) FR-V WITH MEMBER-KEY = #ID-A
END-FIND
*
RESET #ADDED-SGLI-LINES #ADDED-SPS-SGLI #ADDED-DEP-SGLI
MOVE 46 TO #NXT-EMPTY-X
FOR #I1 1 TO 45
  IF  (POL-NAME (#I1) = 'SGLI')
   AND (POL-AMOUNT (#I1) GE 50000)
    MOVE 'SGLI-MBR'       TO POL-NAME (#I1)
    MOVE POL-AMOUNT (#I1) TO #MBR-SGLI-AMT
    MOVE TRUE TO #HAS-50K-SGLI-NOW
    MOVE #I1 TO #SGLI-X
  END-IF
*
  IF POL-NAME (#I1) = ' '
    MOVE #I1 TO #NXT-EMPTY-X
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF NOT #HAS-50K-SGLI-NOW
  ESCAPE ROUTINE
END-IF
*
IF  (NOT STATUS-CODE-SP = ' ' OR = 'F' OR = 'D' OR = 'N')
 AND (NAME-SP NE ' ')   /* DW3
  MOVE TRUE TO #HAS-SPS
END-IF
* DISPLAY STATUS-CODE-SP POL-NAME (1:8) POL-AMOUNT (1:8)
*         POL-REMARKS (1:8)
* WRITE 'SGLI-x=' #SGLI-X '1st-empty-x=' #NXT-EMPTY-X
*
MOVE #SGLI-X      TO #I1
MOVE #NXT-EMPTY-X TO #I2
IF #NXT-EMPTY-X LE 45
  MOVE POL-CODE    (#I1) TO POL-CODE    (#I2)
  MOVE POL-NAME    (#I1) TO POL-NAME    (#I2)
  MOVE POL-NUMBER  (#I1) TO POL-NUMBER  (#I2)
  MOVE POL-TYPE    (#I1) TO POL-TYPE    (#I2)
  MOVE POL-REMARKS (#I1) TO POL-REMARKS (#I2)
  MOVE POL-AMOUNT  (#I1) TO POL-AMOUNT  (#I2)
  MOVE POL-DATE    (#I1) TO POL-DATE    (#I2)
*
  FOR #I1 #SGLI-X TO #NXT-EMPTY-X
    MOVE POL-CODE    (#I1 + 1) TO POL-CODE    (#I1)
    MOVE POL-NAME    (#I1 + 1) TO POL-NAME    (#I1)
    MOVE POL-NUMBER  (#I1 + 1) TO POL-NUMBER  (#I1)
    MOVE POL-TYPE    (#I1 + 1) TO POL-TYPE    (#I1)
    MOVE POL-REMARKS (#I1 + 1) TO POL-REMARKS (#I1)
    MOVE POL-AMOUNT  (#I1 + 1) TO POL-AMOUNT  (#I1)
    MOVE POL-DATE    (#I1 + 1) TO POL-DATE    (#I1)
  END-FOR
END-IF
*
IF #HAS-SPS
 AND (CHIEF-OF-STAFF-IND NE 'B')   /* no sgli for sapouse
  SEPARATE NAME-SP LEFT INTO #LAST-NAME #FIRST-MID #SUF-1
      REMAINDER #EXTRA WITH DELIMITER ','
  SEPARATE #FIRST-MID INTO #FIRST-NAME
      REMAINDER #EXTRA WITH DELIMITER ' '
  MOVE #NXT-EMPTY-X TO #I1
  MOVE 'X'            TO POL-CODE    (#I1)
  MOVE 'SGLI-SPOUSE'  TO POL-NAME    (#I1)
  RESET                  POL-NUMBER  (#I1)
  MOVE 'GPTM'         TO POL-TYPE    (#I1)
  MOVE #FIRST-NAME    TO POL-REMARKS (#I1)
*
  IF #MBR-SGLI-AMT GE 100000
    MOVE 100000 TO POL-AMOUNT (#I1)
  ELSE
    MOVE #MBR-SGLI-AMT TO POL-AMOUNT (#I1)
  END-IF
*   highest of basd / sps dom / 20011101
*
  MOVE 20011101 TO POL-DATE (#I1)
  IF ACTIVE-DUTY-BASE-DATE GT POL-DATE (#I1)    /* DW1
    MOVE ACTIVE-DUTY-BASE-DATE TO POL-DATE (#I1)
  END-IF
*
  IF DATE-OF-MARRIAGE GT POL-DATE (#I1)         /* DW1
    MOVE DATE-OF-MARRIAGE TO POL-DATE (#I1)
  END-IF
*
  ADD 1 TO #NXT-EMPTY-X
  ADD 1 TO #ADDED-SGLI-LINES
  MOVE TRUE TO #ADDED-SPS-SGLI
END-IF
*
FOR #I1 1 TO 14
* DISPLAY DEPN-DATE-OF-BIRTH (#I1) DEPN-STATUS-CD (#I1)
  IF   (DEPN-DATE-OF-BIRTH (#I1) NE 0)
   AND (DEPN-NAME (#I1) NE ' ')
   AND (NOT DEPN-STATUS-CD (#I1) = 'A' OR = 'B' OR = 'D' OR = 'G'
                                       OR = 'M' OR = 'X'
                                       OR = 'J' OR = 'L')   /* DW2
   AND (#NXT-EMPTY-X LE 45)
    MOVE #NXT-EMPTY-X TO #I2
*
*   IF  (DEPN-STATUS-CD (#I1) = 'J' OR = 'K')   ???????      /* DW2
*    OR ((NOT DEPN-STATUS-CD (#I1) = 'J' OR = 'K')
    IF   (DEPN-STATUS-CD (#I1) = 'I')
     OR  (*DATN - DEPN-DATE-OF-BIRTH (#I1) LT 230000)
      SEPARATE DEPN-NAME (#I1) INTO #FIRST-NAME
           REMAINDER #EXTRA WITH DELIMITER ' '
      MOVE 'X'             TO POL-CODE    (#I2)
      MOVE 'SGLI-CHILD'    TO POL-NAME    (#I2)
      RESET                   POL-NUMBER  (#I2)
      MOVE 'GPTM'          TO POL-TYPE    (#I2)
      MOVE 10000           TO POL-AMOUNT  (#I2)
      MOVE #FIRST-NAME     TO POL-REMARKS (#I2)
*   highest of basd / dep dob / 20011101
      MOVE 20011101        TO POL-DATE    (#I2)
      IF ACTIVE-DUTY-BASE-DATE GT POL-DATE (#I2)    /* DW1
        MOVE ACTIVE-DUTY-BASE-DATE TO POL-DATE (#I2)
      END-IF
*
      IF DEPN-DATE-OF-BIRTH (#I1) GT POL-DATE (#I2) /* DW1
        MOVE DEPN-DATE-OF-BIRTH (#I1) TO POL-DATE (#I2)
      END-IF
*
      ADD 1 TO #NXT-EMPTY-X
      ADD 1 TO #ADDED-SGLI-LINES
      MOVE TRUE TO #ADDED-DEP-SGLI
    END-IF
  END-IF
END-FOR
*
* DISPLAY STATUS-CODE-SP POL-NAME (1:8) POL-AMOUNT (1:8)
*         POL-REMARKS (1:8)
*
END
