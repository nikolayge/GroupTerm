* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPICTX - Create AAFMAA DFAS file from CTX summary
*                        and sort it
*   AAFMAA ctx files will have a processs done to them prior to
*      uploading them to create data files fron the ctx summary reports
*
* MODIFICATION LOG
* DRW 03/05/2008 DW1 Add New GM AR Amounts to NT record
* DRW 09/04/2008 DW2 Add slot for APFP on GM AR NT record
* PAM 03/20/2009 PM3 Add slot for DREV on GM AR NT record
* PAM 06/01/2009 PM4 Add slot for ADCR on GM AR NT record
* RE  10/08/2009 RE1 600000/Process-Ind changes
* * * * * * * * * * * * * * * * * * * * * *
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 CTX-FILE
 2 CS-SSN        (A9)
 2 CS-COMP-CODE  (A6)
 2 CS-PAYEE      (A4)
 2 CS-ALOT-DOLS  (A4)
 2 CS-DEC        (A1)
 2 CS-ALOT-CENTS (A2)
*
1 CS-ALOT-AMT    (A6)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER   /* RE1
 2 NAME
 2 PROCESS-IND
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 NAME
 2 INTRO-FLAG
*
1 IM-V VIEW OF A-INTRO-MEMBER
 2 NAME
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER         (N6)
 2 REDEFINE ID-NUMBER
  3 ID-NUMBER-1ST    (N1)
  3 ID-NUMBER-LAST-5 (N5)
 2 NOTE-TEXT          (A60/40)
 2 REDEFINE NOTE-TEXT /*loan reg/acct reg tots-also ATHP08P1/G4200PMS
  3 NT-LN-TOT-AVAIL-TODAY        (N9.2)
  3 NT-LN-TOT-MBRS-ELIG-LNS      (N7)
*
  3 NT-LN-PRIN               (N9.2/2)
  3 NT-LN-DISBURSED          (N9.2/2)
  3 NT-LN-REPAID             (N9.2/2)
  3 NT-LN-ACC-INT            (N9.2/2)
  3 NT-LN-BILLED-AMT         (N9.2/2)
  3 NT-LN-INT-RECV           (N9.2/2)
  3 NT-LN-MBRS-W-LNS         (N7/2)
  3 NT-LN-COMP-INT-RT        (N2.5/2)
  3 NT-LN-CAP-INT-TO-DATE    (N9.2/2)
  3 NT-LN-MBRS-W-CAP-INT     (N7/2)
  3 NT-LN-LNS-BY-INT-RT-TBL  (13)
   4 NT-LN-LNS-BY-INT-RT     (N9.2/2)
  3 NT-LN-TOT-COMP-RT        (N2.5)
*
  3 NT-PR-PMT      (P9.2)
  3 NT-PR-NEW-LN   (P9.2)
  3 NT-PR-ADDL-LN  (P9.2)
  3 NT-PR-CAP      (P9.2)
  3 NT-PR-ADJ      (P9.2)
  3 NT-PR-NET-CHG  (P9.2)
  3 NT-IN-PMT      (P9.2)
  3 NT-IN-ADDL-LN  (P9.2)
  3 NT-IN-CAP      (P9.2)
  3 NT-IN-ADJ      (P9.2)
  3 NT-IN-NET-CHG  (P9.2)
  3 NT-ACC-ADJ     (P9.2)
  3 NT-PREM-PMT    (P9.2)
  3 NT-PREM-REV    (P9.2)
  3 NT-FPF-PMT     (P9.2)
  3 NT-FPF-REV     (P9.2)
  3 NT-DUES-PMT    (P9.2)
  3 NT-DUES-REV    (P9.2)
  3 NT-FPF-DU-PMT  (P9.2)
  3 NT-FPF-DU-REV  (P9.2)
  3 NT-ALOT-START  (P9.2)
  3 NT-ALOT-STOP   (P9.2)
  3 NT-EZ-START    (P9.2)
  3 NT-EZ-ADJ      (P9.2)
  3 NT-DR          (P9.2)
  3 NT-CR          (P9.2)
*
  3 NT-RECEIPTS    (P9.2)
  3 NT-INT-TRAN    (P9.2)
  3 NT-DISBURSEMENTS (P9.2)
  3 NT-ADJ-APL     (P9.2)
  3 NT-ADJ-APL-CAP (P9.2)
  3 NT-ADJ-ETI     (P9.2)
  3 NT-ADJ-ALP     (P9.2)
  3 NT-ADJ-AIP     (P9.2)
  3 NT-ADJ-ELP     (P9.2)
  3 NT-ADJ-EIP     (P9.2)
  3 NT-EZ-PREMS    (P9.2)
  3 NT-ADJ-APL-PREM (P9.2)
  3 NT-ADJ-JI      (P9.2)
  3 NT-ADJ-L       (P9.2)
*
  3 NT-PAD-AMT     (P9.2)
*
  3 NT-PR-PMT-CAP  (P9.2)
  3 NT-PR-ADJ-CAP  (P9.2)
  3 NT-IN-PMT-CAP  (P9.2)
  3 NT-IN-ADJ-CAP  (P9.2)
  3 NT-ACC-ADJ-CAP (P9.2)
  3 NT-ADJ-ALP-CAP (P9.2)
  3 NT-ADJ-AIP-CAP (P9.2)
  3 NT-ADJ-ELP-CAP (P9.2)
  3 NT-ADJ-EIP-CAP (P9.2)
  3 NT-EZPAY-DUES  (P9.2)
  3 NT-ALLOT-DUES  (P9.2)
  3 NT-APL-DUES    (P9.2)
  3 NT-EZPAY-DUES-SO (P9.2)
  3 NT-ALLOT-DUES-SO (P9.2)
  3 NT-FPF-DU-PMT-SO (P9.2)
  3 NT-FPF-DU-REV-SO (P9.2)
  3 NT-DUES-PMT-SO  (P9.2)
  3 NT-DUES-REV-SO  (P9.2)
  3 NT-FPF-RID-PMT  (P9.2)    /* BELOW 6 ADDED 11/6/1999
  3 NT-FPF-RID-REV  (P9.2)
  3 NT-RID-PMT      (P9.2)
  3 NT-RID-REV      (P9.2)
  3 NT-RID-ALLOT    (P9.2)
  3 NT-RID-EZPAY    (P9.2)
  3 NT-ALLOT-PREMS  (P9.2)    /* below 2  added 3/26/2002
  3 NT-CTX-DFAS-TOT (P9.2)
*                            /* below 3  added 7/12/2004
 3 NT-ALLOT-UNDER  (P9.2)       /* amt alot-amt less than amt needed
 3 NT-ALLOT-OVER   (P9.2)       /* amt alot-amt gtr than amt needed
 3 NT-DFAS-NON-MATCH (P9.2)     /* amt dfas ssn non-match to AAFMAA
*                            /* below 10 added 3/5/2008     /* DW1
 3 NT-EZPAY-BADJ      (P9.2)
 3 NT-EZPAY-UNDERAGES (P9.2)
 3 NT-EZPAY-OVERAGES  (P9.2)
 3 NT-CC-RIDER           (P9.2)
 3 NT-CC-CAP-LOANS       (P9.2)
 3 NT-CC-POLICY-LOANS    (P9.2)
 3 NT-CC-CAP-INTEREST    (P9.2)
 3 NT-CC-POLICY-INTEREST (P9.2)
 3 NT-CC-BADJ            (P9.2)
 3 NT-CC-TOTAL           (P9.2)
*                            /* below 1 added 9/1/2008     /* DW2
 3 NT-EZPAY-APFP    (N9.2)
*                            /* below 1 added 03/01/2009   /* PM3
 3 NT-CC-DREV            (P9.2)                            /* PM3
                             /* below 1 added 06/01/2009   /* PM4
 3 NT-CC-OVERAGES        (P9.2)                            /* PM4
*
1 #STAT-FND   (L)
1 #AT-FND     (L)
1 #IM-FND     (L)
*
1 #NOTE-TEXT  (A60/10)
1 #TO-INIT    (A3)
*
1 ALOT-REC     (A45)
1 REDEFINE ALOT-REC
 2 AL-SSN-A    (A09)
 2 REDEFINE AL-SSN-A
  3 AL-SSN     (N09)
 2 AL-SERV     (A01)
 2 AL-STATUS   (A01)
 2 AL-AMOUNT-A (A06)
 2 REDEFINE AL-AMOUNT-A
  3 AL-AMOUNT  (N4.2)
 2 REDEFINE AL-AMOUNT-A
  3 AL-AMOUNT-X  (A1/6)
 2 AL-NAME     (A22)
 2 AL-EFF-YYYYMM (A6)
*
1 #COUNTERS
 2 #BLANKS           (P5)
 2 #RECS-READ        (P5)
 2 #RECS-SELECTED    (P5)
 2 #CNT              (P5)
 2 #CNT-2            (P5)
 2 #CNT-3            (P5)
 2 #CNT-4            (P5)
 2 #CNT-5            (P5)
 2 #CNT-6            (P5)
 2 #CNT-7            (P5)
 2 #I1               (I2)
 2 #I2               (I2)
 2 #I3               (I2)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
  2 #YYYY          (N4)
  2 #MM            (N2)
  2 #DD            (N2)
1 REDEFINE #YYYYMMDD
  2 #YYYYMM-A      (A6)
*
1 #AA-RECS      (N5)
1 #FA-RECS      (N5)
1 #NA-RECS      (N5)
1 #XR-RECS      (N5)
1 #ALOT-RECS    (N5)
1 #TOT-AMT      (N7.2)
1 #LAST-SSN     (A9)
1 #LAST-AMT     (N4.2)
1 #B4           (A9)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT  LS=133 PS=76
WRITE   TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA DFAS Allotment Exceptions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (2) LS=133 PS=76
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA DFAS Allotment Ongoing credits'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (3) LS=133 PS=76
WRITE  (3) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'AAFMAA DFAS Allotment Duplicate SSN"s'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD CTX-FILE
  IF CS-SSN = ' '
    IF CS-ALOT-DOLS NE ' '
      WRITE 'XXXXX' CTX-FILE
    END-IF
*
    ADD 1 TO #BLANKS
    ESCAPE TOP
  END-IF
  ADD 1 TO #RECS-READ
*
* IF #RECS-READ GT 10
*   ESCAPE BOTTOM
* END-IF
*
  COMPRESS CS-ALOT-DOLS CS-ALOT-CENTS INTO CS-ALOT-AMT LEAVING NO
*
  MOVE CS-SSN TO #B4
* IF cs-ssn = '848423413' MOVE '248423413' TO cs-ssn died 10/19/2001
*   WRITE 'SSN' #B4 'switched to' cs-SSN END-IF  /* TRANSPOSED SSN
* IF CS-SSN = '480981133' MOVE '480981137' TO CS-SSN
*   WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* transposed SSN
  IF CS-SSN = '516111395' MOVE '516111195' TO CS-SSN
    WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* transposed SSN
  IF CS-SSN = '509784352' MOVE '420805066' TO CS-SSN
    WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* spouse pays
  IF CS-SSN = '029421701' MOVE '524087921' TO CS-SSN
   WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* spouse pays
  IF CS-SSN = '004685683' MOVE '086387460' TO CS-SSN
   WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* spouse pays
  IF CS-SSN = '119625595' MOVE '119625596' TO CS-SSN
   WRITE 'SSN' #B4 'switched to' CS-SSN  END-IF /* questionable last dig
END-ALL
*
SORT BY CS-SSN
  USING CS-COMP-CODE CS-PAYEE CS-ALOT-AMT
*
  RESET ALOT-REC
*
  MOVE CS-ALOT-AMT TO AL-AMOUNT-A
  ADD AL-AMOUNT TO #TOT-AMT
*
  MOVE CS-SSN TO AL-SSN-A
*
  IF CS-COMP-CODE = 'N06004'
    IF CS-PAYEE = 'NAVY'
      MOVE 'N' TO AL-SERV
      ADD 1 TO #NA-RECS
    ELSE
      MOVE 'F' TO AL-SERV
      ADD 1 TO #FA-RECS
    END-IF
*
    MOVE 'A' TO AL-STATUS
  ELSE
    IF CS-COMP-CODE = 'K00096'
      MOVE 'A' TO AL-SERV
      MOVE 'A' TO AL-STATUS
      ADD 1 TO #AA-RECS
    ELSE
      MOVE '?' TO AL-SERV
      MOVE 'R' TO AL-STATUS
      ADD 1 TO #XR-RECS
    END-IF
  END-IF
*
  RESET #STAT-FND
  FIND ST-V WITH SSN = AL-SSN
*    IF ST-V.PROCESS-IND = 'S' OR = 'C'            RE1
    IF ST-V.PROCESS-IND = 'C'                   /* RE1
      OR AFFL-ONLY-CLIENT(<ST-V.ID-NUMBER>)     /* RE1
      ESCAPE TOP
    END-IF
*
    MOVE ST-V.NAME TO AL-NAME
    MOVE TRUE TO #STAT-FND
    ESCAPE BOTTOM
  END-FIND
*
  IF NOT #STAT-FND
    RESET #AT-FND
    FIND AT-V WITH SSN = AL-SSN
      IF AT-V.INTRO-FLAG = 'V'
        ESCAPE TOP
      END-IF
*
      MOVE TRUE TO #AT-FND
      MOVE AT-V.NAME TO AL-NAME
      ESCAPE BOTTOM
    END-FIND
  END-IF
*
  IF NOT #STAT-FND AND NOT #AT-FND
    MOVE TRUE TO #IM-FND
    FIND (1) IM-V WITH SSN = AL-SSN
      IF NO RECORD FOUND
        RESET #IM-FND
        ESCAPE BOTTOM
      END-NOREC
      MOVE IM-V.NAME TO AL-NAME
    END-FIND
  END-IF
*
  ADD 1 TO #CNT-2
  DISPLAY (2) #CNT-2
    'SSN'      AL-SSN-A
    'MIL/SERV' AL-SERV
    'MIL/STAT' AL-STATUS
    'ALOT/AMT' AL-AMOUNT
    'NAME'     AL-NAME
*
  WRITE WORK 3 ALOT-REC
  ADD 1 TO #ALOT-RECS
*
  IF CS-SSN = #LAST-SSN
    ADD 1 TO #CNT-3
    DISPLAY (3) #CNT-2 CS-SSN AL-SERV AL-STATUS AL-NAME
        'amt-1' AL-AMOUNT
        'amt-2' #LAST-AMT
  END-IF
  MOVE CS-SSN TO #LAST-SSN
  MOVE AL-AMOUNT TO #LAST-AMT
END-SORT
*
FIND (1) NT-V WITH ID-NUMBER = 800105
  IF NO RECORDS FOUND
    WRITE 'no-nx 800105 rec found'
    ESCAPE BOTTOM
  END-NOREC
*
  MOVE #TOT-AMT TO NT-CTX-DFAS-TOT
  UPDATE (0357)
  END TRANSACTION
END-FIND
*
WRITE 'tot blank lines                =' #BLANKS
WRITE 'tot AAFM army active alots     =' #AA-RECS
WRITE 'tot AAFM airforce active alots =' #FA-RECS
WRITE 'tot AAFM navy     active alots =' #NA-RECS
WRITE 'tot AAFM retired         alots =' #XR-RECS
WRITE 'tot                      alots =' #ALOT-RECS
WRITE 'tot                     amount =' #TOT-AMT
*
MOVE 'DFAS files have been FTP"d to "ftp/polserv/dfasMMYY.txt"'
   TO #NOTE-TEXT (1)
MOVE 'ALB' TO #TO-INIT
MOVE 'EDS' TO #CGA-CLERK
MOVE '*NX' TO #CGA-SCR-SUFFIX
FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
*
MOVE 'KHB' TO #TO-INIT
FETCH RETURN 'G4090PNX' #CGA-CLERK #TO-INIT #NOTE-TEXT (1:5)
END TRANSACTION
*
END
