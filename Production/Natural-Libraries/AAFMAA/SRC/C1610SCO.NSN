* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 5
* <Natural Source Header
* PGM-ID: C1610SCO - Conservation Options - process signature scr 1
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE      TAG  REASON                                           *
*                                                                      *
* PAM  05132004  PM1  Changed wording for Resignation Letters          *
*                     as per Rick Browett.                             *
* DRW  10142004  DW2  Remove Survey from batch CO on full termination  *
* PAM  12162004  PM3  Adjusted premium display to 5 positions.         *
* DRW  12272004  DW4  Increase variablw loan to handle 999,999.99.     *
* PAM  02072005  PM5  Add loan reference line to PDUP letters.         *
* PAM  02162005  PM6  Changed wording for Resignation Letters again    *
*                     as per Rick Browett. Eliminated '1' and '2'      *
*                     options. Only 1 letter now.                      *
* DRW  07262005  DW7  Add text re insurability                         *
* DRW  06262006  DW8  Add DUES PMT TO letter                           *
* YAK  04192007  YK1  Change letter text for Retirees                  *
*                     Options: CSV 1,2 RPU, PDUP, RSGN, OL-20, OL-30   *
* YAK  08282007  YK2  Add check for allotment pay mode                 *
* YAK  11282007  YK3  Use all letter changes for Retirees paying by    *
*                     DFAS in letters for active duty members as well  *
* YAK  12052007  YK4  Remove hardcording for interest rate             *
*                     (Read GEN-TABLE through OBJNGENT)                *
* YAK  02052008  YK5  Change code to use FUNCTION-INT-RATE             *
*                         to get rate from GEN-TABLE                   *
* DRW 06/06/2008 DW6  Add ALOT-CHG-AUTH "A" checking                   *
* PAM 04/28/2009 PM7  Expand DB for RPU screen                         *
* vxt 11/02/2011 vt1  Add #salutation to the parm list amd move it to  *
*                     the first line of the letter.                    *
* bz  06/09/2013      Rebranding (Search for AAFMAA)                   *
* TMT 11/08/2013 TT1  1 MILLION DOLLAR POLICY STABILITY PROJECT        *
************************************************************************
*
DEFINE DATA
*
PARAMETER
*
1 #CGA-ISN          (P8)
1 #CGA-SCR-COMMAND  (A2)
1 #CGA-SCR-ID       (N9)
1 #CGA-SCR-SUFFIX   (A3)
1 #CGA-CLERK        (A3)
1 #TODAY-YYYYMMDD   (N8)
1 REDEFINE #TODAY-YYYYMMDD
  2 #TODAY-YYYY       (N4)
  2 #TODAY-MM         (N2)
  2 #TODAY-DD         (N2)
1 #RANK-LITERAL  (A23)
1 #LAST-NAME     (A25)
1 #FORMATED-NAME (A35)
1 #FIRST-LAST    (A25)
1 #COMP-1035     (A50)
1 #SCR-MAX-LINES (P3)
1 #SCR-POLS      (A76)
1 #CERT-INFO
  2 #CERT      (A12)
  2 #CERT-COM  (A13)
  2 #CERT-COL  (A13)
1 #P-X        (I2)
1 #T-X        (I2)
1 #CT-LOAN-AMT (P6.2)    /* DW4
1 #CT-ACC-INT  (P5.2)
*
1 #CONV-TBL-POLS   (9)        /*9 possible certs
  2 #CT-POL-NUM        (A14)
  2 #CT-ISS-DT         (N8)
  2 #CT-CONV-DUR       (N3)
  2 #CT-ISSUE-DATE     (A10)
  2 #CT-CURR-PLAN      (A6)
  2 #CT-FREQ           (A11)
  2 #CT-CURR-FACE      (N7.2)
  2 #CT-CURR-CSV       (N7.2)
  2 #CT-CURR-DB        (N9.2)
  2 #CT-PRORAT-LN-INT  (N6.2)    /* DW4
*
  2 #CONV-TBL-TYPES (16)   /* 16 possible conversion types
    3 #CT-SELECT            (A1)
    3 #CT-TO-PLAN           (A6)
    3 #CT-TERM-FACE         (N7.2)
    3 #CT-NU-PREM           (N5.2)
    3 #CT-NU-DB             (N9.2)
    3 #CT-AMT-LUMP-SUM      (N9.2)
    3 #CT-PREMS-PAID        (N9.2)
    3 #CT-TAXABLE-INC       (N9.2)
    3 #CT-TAMRA-IND         (A1)
    3 #CT-RPU-LOAN-IND      (A1)
    3 #CT-BYPASS            (L)
    3 #INS-REQ              (L)   /* DW7
    3 #CT-DUES-PMT          (N3.2) /* DW8
*
1 #INTRO-FLAG    (A1)
1 #PROCESS-IND   (A1)
1 #INSURED-NAME   (A35)
1 #SCR-ZIP-BAR-CODE  (A14)
1 #FIRST-ADDRESS     (A70)
1 #SECOND-ADDRESS    (A70)
1 #THIRD-ADDRESS     (A70)
1 #DEBIT-CREDIT-AMT  (P7.2)
1 #NUM-OWNERS        (N3)
1 #MULT-OWNERS       (N6/8)
1 REDEFINE #MULT-OWNERS
  2 #MULT-OWNERS-A    (A6/8)
1 #MIL-STATUS        (A1)          /* YK1
1 #ALOT-CHG-AUTH     (A1)          /* YK1
1 #MODE              (A1)          /* YK1
1 #salutation        (a)  DYNAMIC  /* vt1
1 #MEC               (A1)
*
LOCAL
*
1 #CHG-X   (A11)
1 #FLD-X   (A21)
1 #CO-LINES   (A78/48) /* 21 * 2 for 2 scns = 42 is max visable w/co
1 REDEFINE #CO-LINES
  2 #CO-OCC         (48)
    3 #FIL1       (A3)
    3 #CO-ISS-DT  (A10)
    3 #FLL2       (A3)
    3 #CO-FM-TYPE (A6)
    3 #FIL3       (A3)
    3 #CO-TO-TYPE (A6)
    3 #FIL4       (A2)
    3 #CO-DB      (A10)
    3 #FLL5       (A2)
    3 #CO-PREM    (A6)
    3 #FIL6       (A2)
    3 #CO-COST    (A11)
    3 #FIL7       (A4)
    3 #CO-TAX-INC (A10)
*
1 ST-V VIEW OF A-STATUS
  2 MILITARY-SERVICE
  2 MILITARY-STATUS
  2 NAME
*
1 #OWN-NAME      (A25)
1 #LAST          (A25)
1 #FIRST-MID     (A25)
1 #EXT1          (A25)
1 #EXT2          (A25)
1 #EXT3          (A25)
*
1 #1ST-TIME    (L)
1 #CSV-1035   (A16)
1 #ALL-TERMED (L)
1 #NUM-POLS    (N2)
1 #TERMED-POLS (N2)
1 #FORMAT-CN    (A7)
1 #HOLD-ZIP-CODE   (N9)
1 #HOLD-EDIT-ZIP   (A10)
1 REDEFINE #HOLD-EDIT-ZIP
  2 #HOLD-ZIP      (A5)
  2 REDEFINE #HOLD-ZIP
    3 #HOLD-ZIP-5-TBL (N1/5)
  2 #HOLD-ZIP2     (A5)
  2 REDEFINE #HOLD-ZIP2
    3 #HOLD-ZIP2-DSH   (A1)
    3 #HOLD-ZIP-LAST-4 (A4)
  2 REDEFINE #HOLD-ZIP2
    3 #HOLD-ZIP2-DASH  (A1)
    3 #HOLD-ZIP-LAST-4-TBL (N1/4)
*
1 #ZIP-SUM           (N3)
1 REDEFINE #ZIP-SUM
  2 #ZIP-SUM-2        (N2)
  2 #ZIP-SUM-1        (A1)
*
1 #HOLD-ADDRESS-2  (A25)
1 #HOLD-ADDRESS-3  (A25)
*
1 #I1   (I2)
1 #I2   (I2)
1 #I3   (I2)
1 #TOT-CSV   (P7.2)
1 #INVALID         (L)
1 #CLERK-NAME-PASS (A20)
1 #CGA-SCR-ID2     (N9)
*
1 #TAX-REP         (A10)
1 #GET-COMP        (L)
1 #LN-INT-DISP (A10)   /* DW4
1 #PRO-LN-INT-DISP (A12)
1 #CSV-DISP  (A12)         /* TT1 INCREASED TO SUPPORT MILLION DOLLAR POLICIES
1 #DB-DISP   (A12)         /* TT1 INCREASED TO SUPPORT MILLION DOLLAR POLICIES
1 #PREM-DISP (A9) /* PM3
1 #PREM-AMT    (P7.2)   /* DW8
1 #PRM-DISP  (A10)
1 #DUES-DISP (A9)
1 #COST-DISP (A12)         /* TT1 INCREASED TO SUPPORT MILLION DOLLAR POLICIES
1 #TAX-INC-DISP (A10)      
1 #PR-PD-DISP (A12)        /* TT1 INCREASED TO SUPPORT MILLION DOLLAR POLICIES
*
1 #TOT-LN-INT    (P6.2)                   /* vt1 was p5.2
1 #W-AMT-5       (P5.5)
1 #W-AMT         (P7.2)    /* DW4  /*TT1
1 #W-DISP        (A12)     /* TT1
1 #FACE-DISP     (A9)
*
1 #MONTHS (A36)  INIT
  <'JanFebMarAprMayJunJulAugSepOctNovDec'>
1 REDEFINE #MONTHS
  2 #MON       (A3/12)
1 #NEXT-YEAR           (N4)
1 #CURR-GROWTH-RATE-A  (A6)    /* YK4
1 #CURR-GROWTH-RATE    (P2.3)  /* YK5
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
* WRITE 'top c1610sco num-owners=' #NUM-OWNERS
MOVE #CGA-SCR-ID TO #CGA-SCR-ID2
COMPUTE #NEXT-YEAR = #TODAY-YYYY + 1
*
IF *DEVICE NE 'BATCH'
  IF   (#CT-SELECT (#P-X, #T-X) =  '2')
      AND ((#CT-TO-PLAN (#P-X, #T-X) = 'CSV')
      OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$')))
    MOVE TRUE TO #GET-COMP
  END-IF
*
  IF #GET-COMP AND (#COMP-1035 = ' ') AND (*DEVICE NE 'BATCH')
    INPUT /// 'Enter 1035 Exchange Company:' #COMP-1035
  END-IF
END-IF
*
MOVE #CT-POL-NUM (#P-X) TO #SCR-POLS
MOVE #CT-CURR-CSV (#P-X) TO #TOT-CSV
MOVE 'Certificate' TO #CERT
IF #P-X LE 9
  COMPUTE #I1 = #P-X + 1
  MOVE TRUE TO #1ST-TIME
  FOR #I1 = #I1 TO 9
    FOR #I2 = 1 TO 16
      IF   (#CT-SELECT  (#P-X, #T-X) = #CT-SELECT  (#I1, #I2))
          AND (#CT-TO-PLAN (#P-X, #T-X) = #CT-TO-PLAN (#I1, #I2))
          AND (#CT-TAMRA-IND (#P-X, #T-X) = #CT-TAMRA-IND (#I1, #I2))
          AND (#CT-RPU-LOAN-IND(#P-X, #T-X) = #CT-RPU-LOAN-IND(#I1, #I2))
        IF #1ST-TIME
          RESET #1ST-TIME
          SUBTRACT #DEBIT-CREDIT-AMT FROM #TOT-CSV
        END-IF
*
        MOVE 'Certificates' TO #CERT
        MOVE TRUE TO #CT-BYPASS (#I1, #I2)
        ADD #CT-CURR-CSV     (#I1)   TO #TOT-CSV
        ADD #CT-PRORAT-LN-INT(#I1)   TO #CT-PRORAT-LN-INT (#P-X)
        ADD #CT-NU-PREM     (#I1, #I2) TO #CT-NU-PREM     (#P-X, #T-X)
        ADD #CT-NU-DB       (#I1, #I2) TO #CT-NU-DB       (#P-X, #T-X)
        ADD #CT-PREMS-PAID  (#I1, #I2) TO #CT-PREMS-PAID  (#P-X, #T-X)
        ADD #CT-TAXABLE-INC (#I1, #I2) TO #CT-TAXABLE-INC (#P-X, #T-X)
*
        IF  (#CT-TO-PLAN (#P-X, #T-X) = 'CSV')
            OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$'))
          COMPUTE #CT-AMT-LUMP-SUM (#P-X, #T-X) = #TOT-CSV
*            - #CT-LOAN-AMT - #CT-ACC-INT
            - #CT-PRORAT-LN-INT (#P-X)
* DISPLAY #P-X #CT-AMT-LUMP-SUM (#P-X, #T-X) #CT-PRORAT-LN-INT (#P-X)
*         #TOT-CSV
        ELSE
          ADD #CT-AMT-LUMP-SUM(#I1, #I2) TO #CT-AMT-LUMP-SUM(#P-X, #T-X)
        END-IF
*
        COMPRESS #SCR-POLS '/' #CT-POL-NUM (#I1)
          INTO #SCR-POLS LEAVING NO
      END-IF
    END-FOR
  END-FOR
END-IF
*
COMPRESS #CERT ',' INTO #CERT-COM LEAVING NO
COMPRESS #CERT ':' INTO #CERT-COL LEAVING NO
*
IF #CT-TO-PLAN (#P-X, #T-X) = 'CSV' /* hunt for other csv's
  RESET #ALL-TERMED #NUM-POLS #TERMED-POLS
  FOR #I1 1 TO 9
    IF #CT-ISSUE-DATE (#I1) NE ' '
      ADD 1 TO #NUM-POLS
    END-IF
*
    FOR #I2 1 TO 16  /* was 9
      IF   (#CT-TO-PLAN (#I1, #I2) = 'CSV' OR = 'RSGN')
          AND (#CT-SELECT (#I1, #I2) NE ' ')
        ADD 1 TO #TERMED-POLS
        ESCAPE BOTTOM
      END-IF
    END-FOR
  END-FOR
*
  IF #NUM-POLS = #TERMED-POLS
    MOVE TRUE TO #ALL-TERMED
  END-IF
END-IF
*
RESET #I1 #CO-LINES (*)
ADD 1 TO #I1
COMPRESS 'For' #CERT-COL #SCR-POLS INTO #CO-LINES (#I1)
*
ADD 1 TO #I1
COMPRESS 'Insured:' #INSURED-NAME INTO #CO-LINES (#I1)
MOVE *DAT4U TO #CO-TAX-INC (#I1) /* load date into far right of line
*
ADD 1 TO #I1
COMPRESS 'Owner  :' #CO-LINES (#I1) #FORMATED-NAME INTO #CO-LINES (#I1)
*
IF #NUM-OWNERS GT 1
  FOR #I2 1 TO 8
    IF #MULT-OWNERS (#I2) NE 0
      FIND ST-V WITH MEMBER-KEY = #MULT-OWNERS-A (#I2)
        SEPARATE ST-V.NAME LEFT INTO #LAST #FIRST-MID #EXT1 #EXT2 #EXT3
          WITH DELIMITER ','
        COMPRESS #FIRST-MID #LAST #EXT1 #EXT2 #EXT3 INTO #OWN-NAME
        ADD 1 TO #I1
        COMPRESS 'Owner  :' #CO-LINES (#I1) #OWN-NAME
          INTO #CO-LINES (#I1)
      END-FIND
    END-IF
  END-FOR
END-IF
*
* DISPLAY #RANK-LITERAL #LAST-NAME
ADD 2 TO #I1
*  COMPRESS 'Dear' #RANK-LITERAL #LAST-NAME INTO #CO-LINES (#I1)         /* vt1
move #salutation to #co-lines(#i1)                                       /* vt1
COMPRESS #CO-LINES (#I1) ':' INTO #CO-LINES (#I1) LEAVING NO
*
IF  (#CT-TO-PLAN (#P-X, #T-X) = 'RSGN')
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('R$'))
  IF  (#CT-TO-PLAN (#P-X, #T-X) = 'RSGN')                         /* PM6
    ADD 2 TO #I1
    IF #INTRO-FLAG = 'S' OR = 'K' OR = 'G'
      COMPRESS 'To proceed and terminate your'
        'insurance for the above'  INTO #CO-LINES (#I1)
    ELSE
      COMPRESS 'To proceed and terminate your membership and'
        'insurance for the above'  INTO #CO-LINES (#I1)
    END-IF
*
    ADD 1 TO #I1
*   IF #MIL-STATUS = 'R' AND #MODE = 'G'                               /* YK2
    IF #MODE = 'G'                                                     /* YK3
      COMPRESS 'noted Certificate(s), please sign and return this'     /* YK1
        'Resignation Request'    INTO #CO-LINES (#I1)                    /* YK1
      IF (NOT #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A')             /* DW6
        COMPRESS #CO-LINES (#I1) 'and the' INTO #CO-LINES (#I1)        /* YK1
        ADD 1 TO #I1                                                   /* YK1
        COMPRESS 'enclosed allotment change form. AAFMAA will contact' /* YK1
          'DFAS for allotment change.'   INTO #CO-LINES (#I1)          /* YK1
      ELSE
        COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO   /* YK1
        ADD 1 TO #I1
        COMPRESS 'Thank you for your authorization to change your'
          'allotment amount.' INTO #CO-LINES (#I1)                     /* YK1
*     ADD 1 TO #I1
*     COMPRESS 'We will be submitting the above change to DFAS.'
*        INTO #CO-LINES (#I1)
      END-IF
    ELSE                                                               /* YK1
      COMPRESS 'noted Certificate(s), please sign and return this'
        'Resignation Request.'        INTO #CO-LINES (#I1)
    END-IF                                                             /* YK1
  ELSE
    ADD 2 TO #I1
    COMPRESS ' You requested that your AAFMAA insurance be reduced.'
      'Please SIGN, COMPLETE' INTO #CO-LINES (#I1)
* Add text for retirees
*   IF #MIL-STATUS = 'R' AND #MODE = 'G'                               /* YK2
    IF #MODE = 'G'                                                     /* YK3
      IF NOT #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A'       /* DW6
        ADD 1 TO #I1
        COMPRESS 'and RETURN the enclosed allotment change form along with'
          'this letter.' INTO #CO-LINES (#I1)
      ELSE
        ADD 1 TO #I1
        COMPRESS 'and RETURN the form below to us. Thank you for your'
          'authorization to change' INTO #CO-LINES (#I1)
        ADD 1 TO #I1
        COMPRESS 'your allotment amount. We will be submitting'
          'the above change to DFAS.' INTO #CO-LINES (#I1)
      END-IF
    ELSE                                                               /* YK1
      ADD 1 TO #I1
      COMPRESS 'and RETURN the form below to us as soon as possible.'
        INTO #CO-LINES (#I1)
    END-IF
  END-IF
*
* PERFORM CORR-MSG-FORMAT
  ADD 2 TO #I1
  IF #CT-TO-PLAN (#P-X, #T-X) = MASK ('R$')
    COMPRESS '           PARTIAL RESIGNATION REQUEST' TO #CO-LINES (#I1)
    ADD 1 TO #I1
  END-IF
  COMPRESS 'To Secretary, AAFMAA:'
     INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS'I, a certificate holder with AAFMAA, hereby request' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
*
  IF #CT-TO-PLAN (#P-X, #T-X) = 'RSGN'
    COMPRESS 'my insurance under said above Certificate(s) be resigned.'
      'Any prepaid' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE 'premiums will be refunded.' TO #CO-LINES (#I1)
*   ADD 2 TO #I1                                               /* YK1
    ADD 1 TO #I1                                               /* YK1
    COMPRESS ' (  ) I am not applying for additional insurance. Please'
      'resign my' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE '      Certificate(s) immediately.' TO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS ' (  ) I am applying for additional insurance. Please'
      'resign my Certificate(s)' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE '      only when my new insurance is approved.'
      TO #CO-LINES (#I1)
  ELSE
    MOVE EDITED #CT-TERM-FACE (#P-X, #T-X) (EM=Z,ZZZ,ZZ9) TO #FACE-DISP
    COMPRESS '$' #FACE-DISP ' face value of the total'
      INTO #CO-LINES (#I1) LEAVING NO
    MOVE EDITED #CT-CURR-FACE (#P-X) (EM=Z,ZZZ,ZZ9) TO #FACE-DISP
    COMPRESS #CO-LINES (#I1) ' $' #FACE-DISP INTO #CO-LINES (#I1)
      LEAVING NO
    COMPRESS #CO-LINES (#I1) 'face amount of my insurance'
      INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE 'under said Association be resigned.' TO #CO-LINES (#I1)
  END-IF
*
  PERFORM SIGNATURE-LINE
ELSE
  ADD 2 TO #I1
  COMPRESS ' You selected the following change to your existing AAFMAA'
            INTO #CO-LINES (#I1)
END-IF
*
IF  (#CT-TO-PLAN (#P-X, #T-X) = 'OL' OR = '20-Pay' OR = '30-Pay'
    OR = '7-Pay' OR = 'PDUP' OR = 'RPU')
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (..'-Pay'))
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (.'-Pay'))
  ADD 1 TO #I1
* IF #CT-TO-PLAN (#P-X, #T-X) = 'RPU' AND #MIL-STATUS='R'     /* YK1
  IF #CT-TO-PLAN (#P-X, #T-X) = 'RPU'                         /* YK3
      AND (NOT #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A')    /* DW6
      AND #MODE = 'G' /* YK2
    COMPRESS 'Insurance. Please sign this'
      'form and the enclosed' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'Allotment Authorization and return to AAFMAA'
      INTO #CO-LINES (#I1)
  ELSE                                                      /* YK1
    COMPRESS 'Insurance. Please sign below'
      'and return this form' INTO #CO-LINES (#I1)   /* DW8
    ADD 1 TO #I1
    MOVE 'in the enclosed envelope' TO #CO-LINES (#I1)
  END-IF
*
  IF #CT-AMT-LUMP-SUM (#P-X, #T-X) GT 0
    COMPRESS #CO-LINES (#I1) 'with your payment.' INTO #CO-LINES (#I1)
  ELSE
    COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
  END-IF
*
* IF #CT-TO-PLAN (#P-X, #T-X) = 'RPU' AND #MIL-STATUS='R'   /* YK1
  IF #CT-TO-PLAN (#P-X, #T-X) = 'RPU'                       /* YK3
      AND (#ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A')     /* DW6
      AND #MODE = 'G'   /* YK2
    COMPRESS #CO-LINES (#I1) 'Thank you for your authorization'
      'to change your' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'allotment amount. We will be submitting'
      'the above change to DFAS.' INTO #CO-LINES (#I1)
  END-IF                                                    /* YK1
*
  IF   (#CT-TO-PLAN (#P-X, #T-X) NE 'RPU')
      AND (#CT-AMT-LUMP-SUM (#P-X, #T-X) GT 0)
    COMPRESS #CO-LINES (#I1) 'THE CONVERSION CANNOT BE'
      INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'PROCESSED WITHOUT YOUR PAYMENT.' INTO #CO-LINES (#I1)
  END-IF
END-IF
*
IF (#CT-TO-PLAN (#P-X, #T-X) = 'OL' OR = '20-Pay' OR = '30-Pay'
    OR = '7-Pay' OR = 'PDUP')
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (..'-Pay'))
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (.'-Pay'))
    AND (#CT-AMT-LUMP-SUM (#P-X, #T-X) GT 0)
  ADD 1 TO #I1
  COMPRESS ' These cost projections reflect completing this'
    'conversion within the next'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS '30 days. Any refund due to earlier completion will be'
    'added to your cash' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'value. Any additional cost due to delay will be'
    'billed to you.'                 INTO #CO-LINES (#I1)
END-IF
*
IF  (#CT-TO-PLAN (#P-X, #T-X) = 'CSV')       /* full CSV
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$'))  /* csv split
  ADD 1 TO #I1
  COMPRESS 'Insurance. Please SIGN, COMPLETE'
    'and RETURN the' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'attached Cash Surrender Value release form. Cash'
    'Surrendering your'     INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'insurance cannot be processed without the signed release'
    'form.'        INTO #CO-LINES (#I1)
END-IF
*
IF #CT-TO-PLAN (#P-X, #T-X) = 'CSV'      /* full CSV
    AND #ALL-TERMED
  #CURR-GROWTH-RATE := FUNCTION-INT-RATE(<'RATE2AA'>)  /* YK5
  MOVE EDITED #CURR-GROWTH-RATE (EM=Z9.99) TO #CURR-GROWTH-RATE-A /* YK5
  ADD 2 TO #I1
  COMPRESS ' Please remember however, we are available to discuss'
    'other options in' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'lieu of cash surrendering. The longer you have the Value'
    'Added Whole' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'Life Insurance the more it grows in cash value and'
    'death benefit. Our' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
* COMPRESS #TODAY-YYYY 'crediting rate is 7.10%, excellent in today"s'  /* YK4
  COMPRESS #TODAY-YYYY ' crediting rate is ' #CURR-GROWTH-RATE-A
    '%, excellent in today"s industry! Call us to'
    INTO #CO-LINES (#I1) LEAVING NO                      /* YK4
  ADD 1 TO #I1
  MOVE     'find out what YOUR options are at 1-800-336-4538.'
    TO #CO-LINES (#I1)
  IF NOT #INTRO-FLAG = 'S' OR = 'K' OR = 'G'
    ADD 2 TO #I1
    COMPRESS ' Also, look over the enclosed information about our'
      'services. Our mission is'  INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'to make sure your family and loved ones receive the very'
      'best assistance'  INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE     'possible in their time of need.' TO #CO-LINES (#I1)
    ADD 2 TO #I1
  END-IF
END-IF
*
MOVE #CT-PRORAT-LN-INT (#P-X) TO #TOT-LN-INT
* COMPUTE #TOT-LN-INT = #CT-LOAN-AMT + #CT-ACC-INT
MOVE EDITED #TOT-LN-INT (EM=ZZZ,ZZZ.99) TO #LN-INT-DISP
MOVE EDITED #CT-PRORAT-LN-INT (#P-X) (EM=Z,ZZZ,ZZZ.99) TO #PRO-LN-INT-DISP
MOVE EDITED #TOT-CSV  (EM=Z,ZZZ,ZZZ.99) TO #CSV-DISP                          /*TT1
MOVE EDITED #CT-PREMS-PAID (#P-X, #T-X) (EM=Z,ZZZ,ZZZ.99) TO #PR-PD-DISP      /*TT1  
MOVE EDITED #CT-NU-PREM (#P-X, #T-X) (EM=ZZZ,ZZZ.99) TO #PREM-DISP /* PM3
MOVE EDITED #CT-AMT-LUMP-SUM (#P-X, #T-X) (EM=Z,ZZZ,ZZZ.99)     /*tt1 removed negative sign
  TO #COST-DISP   /* this amt may be negative
MOVE EDITED #CT-NU-DB (#P-X, #T-X) (EM=Z,ZZZ,ZZZ.99) TO #DB-DISP    /* PM7
MOVE EDITED #CT-TAXABLE-INC (#P-X, #T-X) (EM=ZZZ,ZZZ.99)                    /*TT1
  TO #TAX-INC-DISP
*
IF #CT-TO-PLAN (#P-X, #T-X) = 'PDUP'
  COMPUTE #PREM-AMT  =                              /* DW8
    #CT-AMT-LUMP-SUM (#P-X, #T-X) - #CT-DUES-PMT (#P-X, #T-X)
  MOVE EDITED #PREM-AMT (EM=ZZ,ZZZ.99) TO #PRM-DISP
  MOVE LEFT #PRM-DISP TO #PRM-DISP
  COMPRESS '$' #PRM-DISP INTO #PRM-DISP LEAVING NO
  MOVE EDITED #CT-DUES-PMT (#P-X, #T-X) (EM=ZZZ.99) TO #DUES-DISP
  MOVE LEFT #DUES-DISP TO #DUES-DISP
  COMPRESS '$' #DUES-DISP INTO #DUES-DISP LEAVING NO   /* DW8
* DISPLAY #PRM-DISP #DUES-DISP
*         #CT-AMT-LUMP-SUM (#P-X, #T-X) #CT-DUES-PMT (#P-X, #T-X) /* DW8
*
  ADD 2 TO #I1
  COMPRESS ' To convert the above' #CERT 'to a fully PAID-UP'
    'INSURANCE will cost' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'you $' #COST-DISP '.' INTO #CO-LINES (#I1) LEAVING NO
*
  IF #CT-DUES-PMT (#P-X, #T-X) NE 0    /* DW8
    COMPRESS #CO-LINES (#I1) '(' #PRM-DISP 'represents premium and'
      #DUES-DISP 'represents'    INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'dues for services.)' INTO #CO-LINES (#I1)
  END-IF
*
  ADD 1 TO #I1
  COMPRESS  'Any outstanding loans are unaffected' /* PM5
    'and remain due.' INTO #CO-LINES (#I1) /* PM5
*
* Add text for retiree
* IF #MIL-STATUS = 'R' AND #MODE = 'G'                      /* YK1
  IF #MODE = 'G'                                            /* YK3
    IF NOT #ALOT-CHG-AUTH = 'Y' OR = 'S'  OR = 'A'   /* DW6
      COMPRESS #CO-LINES (#I1) 'Please complete and'
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'return to AAFMAA the enclosed allotment'
        'change form along with this letter.'
        INTO #CO-LINES (#I1)
    ELSE                                                   /* YK1
      COMPRESS #CO-LINES (#I1) 'Thank you for your'
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'authorization to change your allotment'
        'and we will submit it to DFAS.'
        INTO #CO-LINES (#I1)
    END-IF
  END-IF                                                   /* YK1
END-IF
*
IF #CT-TO-PLAN (#P-X, #T-X) = 'OL' OR = '20-Pay' OR = '30-Pay'
    OR = '7-Pay'
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (..'-Pay'))
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK (.'-Pay'))
  ADD 2 TO #I1
  COMPRESS ' Converting the above' #CERT
    'from'    INTO #CO-LINES (#I1)
*
  IF #CT-CURR-PLAN (#P-X) = 'OL'
    COMPRESS #CO-LINES (#I1) 'Ordinary Life' INTO #CO-LINES (#I1)
  ELSE
    COMPRESS #CO-LINES (#I1) #CT-CURR-PLAN(#P-X) INTO #CO-LINES(#I1)
  END-IF
*
  IF #CT-TO-PLAN (#P-X, #T-X) = 'OL'
    COMPRESS #CO-LINES (#I1) 'to Ordinary Life' INTO #CO-LINES (#I1)
  ELSE
    COMPRESS #CO-LINES (#I1) 'to' #CT-TO-PLAN (#P-X, #T-X)
      INTO #CO-LINES (#I1)
  END-IF
*
  ADD 1 TO #I1
  IF #INS-REQ (#P-X, #T-X)      /* DW7
    COMPRESS 'requires a new application and' INTO #CO-LINES (#I1)
  END-IF
*
  IF #CT-AMT-LUMP-SUM (#P-X, #T-X) LT 0
    COMPRESS #CO-LINES (#I1)
      ' will refund you $' INTO #CO-LINES (#I1) LEAVING NO
  ELSE
    COMPRESS #CO-LINES (#I1)
      ' will cost you $' INTO #CO-LINES (#I1) LEAVING NO
  END-IF
*
  COMPRESS #CO-LINES (#I1) #COST-DISP  '.'   /**TT1    
    INTO #CO-LINES (#I1) LEAVING NO
  ADD 1 TO #I1
  COMPRESS 'Your new' #CT-FREQ (#P-X) 'payment will'
    INTO #CO-LINES (#I1)
  COMPRESS #CO-LINES (#I1) ' be $' #PREM-DISP '.'
    INTO #CO-LINES (#I1) LEAVING NO
*
* Add text for retiree
* IF #MIL-STATUS = 'R' AND #MODE = 'G'                       /* YK1
  IF #MODE = 'G'                                             /* YK3
    IF NOT #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A'    /* DW6
      COMPRESS #CO-LINES (#I1) 'Please complete and'
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'return to AAFMAA the enclosed allotment'
        'change form along with this letter.'
        INTO #CO-LINES (#I1)
    ELSE                                                    /* YK1
      COMPRESS #CO-LINES (#I1) 'Thank you for your'
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'authorization to change your allotment'
        'and we will submit it to DFAS.'
        INTO #CO-LINES (#I1)
    END-IF
  END-IF                                                    /* YK1
END-IF
* PERFORM CORR-MSG-FORMAT
*
IF #CT-TO-PLAN (#P-X, #T-X) = 'RPU'
  ADD 2 TO #I1
  MOVE     ' This conversion' TO #CO-LINES (#I1)
*
  IF #CT-RPU-LOAN-IND (#P-X, #T-X) = 'A'
    COMPUTE #W-AMT = #CT-LOAN-AMT + #CT-ACC-INT
    MOVE #LN-INT-DISP TO #W-DISP
  ELSE
    MOVE #CT-PRORAT-LN-INT (#P-X) TO #W-AMT
    MOVE #PRO-LN-INT-DISP TO #W-DISP
  END-IF
*
  IF #W-AMT NE 0
    COMPRESS #CO-LINES (#I1) ', after deducting the outstanding loa'
      'n and accrued interest'   INTO #CO-LINES (#I1) LEAVING NO
    ADD 1 TO #I1
    COMPRESS 'to date of $' #W-DISP ',' INTO #CO-LINES (#I1) LEAVING NO
  END-IF
*
  COMPRESS #CO-LINES (#I1) 'will create a Paid-up death benefit'
    'of approximately'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS '$' #DB-DISP 'with no further payment required.'
    INTO #CO-LINES (#I1)
  IF #CT-TAXABLE-INC (#P-X, #T-X) GT 0
    ADD 2 TO #I1
    IF  (#CT-PRORAT-LN-INT (#P-X) = #TOT-LN-INT) /* ALL LOAN IS INVOLVED
        OR (#CT-RPU-LOAN-IND (#P-X, #T-X) = 'A')
      COMPRESS ' Your total loan of $' #W-DISP
        INTO #CO-LINES (#I1) LEAVING NO
    ELSE
      COMPRESS ' $' #W-DISP ' of your total loan'
        INTO #CO-LINES (#I1) LEAVING NO
    END-IF
*
    COMPRESS #CO-LINES (#I1) 'will be liquidated and will exceed'
      'premiums' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'paid by $' #TAX-INC-DISP '. Under IRS regulation'
      's, this amount should be reported'
      INTO #CO-LINES (#I1) LEAVING NO
    ADD 1 TO #I1
    COMPRESS 'on your Federal Income Tax Return as ordinary income.'
      'A form 1099 will' INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'be mailed to you in January' #NEXT-YEAR
      INTO #CO-LINES (#I1)
    COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
  END-IF
*
* PERFORM CORR-MSG-FORMAT
  ADD 2 TO #I1
  COMPRESS ' I hereby surrender the above' #CERT 'and request'
    'that I be issued in'       INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'lieu thereof REDUCED PAID-UP INSURANCE as provided'
    'for in the'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  MOVE 'Certificate of Insurance.' TO #CO-LINES (#I1)
*
END-IF
*
IF  (#CT-TO-PLAN (#P-X, #T-X) = 'CSV')
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$'))
  IF #CT-SELECT (#P-X, #T-X) = '2'
    MOVE '1035 Exchanged' TO #CSV-1035
  ELSE
    IF  (#CT-ISS-DT (#P-X) LT 19990101)
        AND (#CT-CONV-DUR (#P-X) = 999)    /* cap
      MOVE 'surrendered' TO #CSV-1035
    ELSE
      MOVE 'cash surrendered' TO #CSV-1035
    END-IF
  END-IF
*
  ADD 1 TO #I1
  IF #CT-TO-PLAN (#P-X, #T-X) = 'CSV'
    COMPRESS ' If you decide to continue with the termination of your'
      'policy(s),'      INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'the above' #CERT 'will be' #CSV-1035 'and'
      INTO #CO-LINES (#I1)
  ELSE
    MOVE EDITED #CT-TERM-FACE (#P-X, #T-X) (EM=Z,ZZZ,ZZ9) TO #FACE-DISP
    COMPRESS ' $' #FACE-DISP ' of the $'
      INTO #CO-LINES (#I1) LEAVING NO
    MOVE EDITED #CT-CURR-FACE (#P-X) (EM=Z,ZZZ,ZZ9) TO #FACE-DISP
    COMPRESS #CO-LINES (#I1) #FACE-DISP ' Face Amount of the above'
      #CERT                INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    COMPRESS 'will be' #CSV-1035 'and' INTO #CO-LINES (#I1)
  END-IF
*
* DISPLAY #TOT-CSV #TOT-LN-INT #CT-AMT-LUMP-SUM (#P-X, #T-X)
  IF  (#TOT-LN-INT NE 0)
      OR (#CT-ACC-INT  NE 0)
*   IF #CT-AMT-LUMP-SUM (#P-X, #T-X) GT 0  /* removed 9/12/2002
*   IF #CT-AMT-LUMP-SUM (#P-X, #T-X) LE 0  /* removed 1/10/99
*   IF #CT-AMT-LUMP-SUM (#P-X, #T-X) = 0   /* removed 1/21/99
*   IF #TOT-CSV LE #TOT-LN-INT   /* added 9/12/2002 then removed 11/2003
    COMPRESS #CO-LINES (#I1) 'its cash value of'
      INTO #CO-LINES (#I1)
    ADD 1 TO #I1
    MOVE #TOT-CSV TO #W-AMT
*
    IF #CT-TO-PLAN (#P-X, #T-X) = MASK ('$')
      COMPUTE ROUNDED #W-AMT-5 = #TOT-CSV *   /* split the csv
*          ((#CT-CURR-FACE (#P-X) - #CT-TERM-FACE (#P-X, #T-X))
        (#CT-TERM-FACE (#P-X, #T-X)
        / #CT-CURR-FACE (#P-X))
      MOVE ROUNDED #W-AMT-5 TO #W-AMT
    END-IF
*
    MOVE EDITED #W-AMT (EM=Z,ZZZ,ZZZ.99) TO #CSV-DISP /*must preced b-lo
*
    IF #W-AMT GT #TOT-LN-INT   /* added 11/2003 KHB
      COMPUTE #W-AMT = #W-AMT - #TOT-LN-INT
      MOVE ' reduced by' TO #CHG-X    /* added 3/2/2003 alb
      MOVE 'net cash value' TO #FLD-X
    ELSE
      COMPUTE #W-AMT = #TOT-LN-INT - #W-AMT
      MOVE ' applied to' TO #CHG-X
      MOVE 'loan/interest balance' TO #FLD-X
    END-IF
*
    IF #W-AMT LT 0 /* never
      RESET #W-AMT
    END-IF
    COMPRESS '$' #CSV-DISP ' will be' #CHG-X ' your outstand'
      'ing loan and accrued interest' INTO #CO-LINES (#I1) LEAVING NO
    ADD 1 TO #I1
    COMPRESS 'to date of $' #LN-INT-DISP
      'leaving a' #FLD-X 'of $' INTO #CO-LINES (#I1)
*
    MOVE EDITED #W-AMT (EM=Z,ZZZ,ZZZ.99) TO #W-DISP
    COMPRESS #CO-LINES (#I1) #W-DISP '.'
      INTO #CO-LINES (#I1) LEAVING NO
*   ELSE    /* removed 11/2003 khb
*     COMPRESS #CO-LINES (#I1) 'after deducting the'
*                        INTO #CO-LINES (#I1)
*     ADD 1 TO #I1
*     COMPRESS 'outstanding loan and accrued interest to date of $'
*       #LN-INT-DISP ',' INTO #CO-LINES (#I1) LEAVING NO
*
*     IF #CT-SELECT (#P-X, #T-X) = '2'
*       COMPRESS #CO-LINES (#I1) 'approximately' INTO #CO-LINES (#I1)
*       ADD 1 TO #I1
*       COMPRESS '$' #COST-DISP 'will be paid to' #COMP-1035
*                      INTO #CO-LINES (#I1)
*       COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
*     ELSE
*       COMPRESS #CO-LINES (#I1) 'will net you' INTO #CO-LINES (#I1)
*       ADD 1 TO #I1
*       COMPRESS 'approximately $' #COST-DISP 'in cash value.'
*       COMPRESS 'approximately $' #COST-DISP
*                                       INTO #CO-LINES (#I1)
*       COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
*     END-IF
*   END-IF
  ELSE
    IF #CT-SELECT (#P-X, #T-X) = '2'   /* 1035-EXCHANGE
      COMPRESS #CO-LINES (#I1) 'approximately $' #COST-DISP
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'will be paid to' #COMP-1035 INTO #CO-LINES (#I1)
      COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
    ELSE
      COMPRESS #CO-LINES (#I1) 'will net you' INTO #CO-LINES (#I1)
      ADD 1 TO #I1
*     COMPRESS 'approximately $' #COST-DISP 'in'
      COMPRESS 'approximately $' #COST-DISP
        INTO #CO-LINES (#I1)
      IF  (#CT-ISS-DT (#P-X) LT 19990101)
          AND (#CT-CONV-DUR (#P-X) = 999)    /* cap
*       COMPRESS #CO-LINES (#I1) 'premium refund. If your policy'
        COMPRESS #CO-LINES (#I1) '. If your policy'
          'lapses you' INTO #CO-LINES (#I1)
        ADD 1 TO #I1
        MOVE 'will forfeit your premium refund.' TO #CO-LINES (#I1)
      ELSE
        COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
      END-IF
    END-IF
  END-IF
*
  IF #CT-TAXABLE-INC (#P-X, #T-X) NE 0
    IF #CT-SELECT (#P-X, #T-X) = '2'   /* 1035-EXCHANGE
      MOVE 'REPORTABLE' TO #TAX-REP
      ADD 1 TO #I1
      COMPRESS ' Your cash surrender value exceeds premiums paid by $'
        #TAX-INC-DISP 'and is NOT'     INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'TAXABLE. However, we are required under present IRS'
        'regulations to REPORT'     INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'this amount to the Internal Revenue Service. An IRS'
        'Form 1099R will be' INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'mailed to you in January' #NEXT-YEAR
        INTO #CO-LINES (#I1)
      COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
    ELSE
      MOVE 'TAXABLE' TO #TAX-REP
      ADD 1 TO #I1
      COMPRESS ' There is' #TAX-REP 'income of approximately $'
        #TAX-INC-DISP                            INTO #CO-LINES (#I1)
      COMPRESS #CO-LINES (#I1) '. An IRS 1099 form'
        INTO #CO-LINES (#I1) LEAVING NO
      ADD 1 TO #I1
      COMPRESS 'will be mailed to you in January' #NEXT-YEAR
        INTO #CO-LINES (#I1)
      COMPRESS #CO-LINES (#I1) '.' INTO #CO-LINES (#I1) LEAVING NO
    END-IF
  END-IF
*
  IF   (#CT-TO-PLAN (#P-X, #T-X) = 'PDUP' OR = 'CSV')
    IF #ALL-TERMED
      ADD 1 TO #I1
      COMPRESS ' Whether you stay with the Association or cancel your'
        'membership, we' INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS 'appreciate the opportunity to have served you.'
        INTO #CO-LINES (#I1)
    END-IF
*
    IF #CT-TAXABLE-INC (#P-X, #T-X) NE 0
      ADD 2 TO #I1
      COMPRESS ' CASH VALUE    -   PREMIUMS PAID   = ' #TAX-REP 'INCOME'
        INTO #CO-LINES (#I1)
      ADD 1 TO #I1
      COMPRESS '$' #CSV-DISP  '  -  $' #PR-PD-DISP '     =   $'
        #TAX-INC-DISP INTO #CO-LINES (#I1) LEAVING NO
    END-IF
  END-IF
END-IF
*
IF   ((#CT-TO-PLAN (#P-X, #T-X) = 'PDUP' OR = 'CSV')
    OR   (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$')))
    AND (#CT-TAMRA-IND (#P-X, #T-X) = 'Y')
  ADD 2 TO #I1
  COMPRESS ' This conversion falls under the provisions of Technical'
    'and Miscellaneous' INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'Revenue Act of 1988 (TAMRA). This act imposes tax'
    'implications on loans'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'taken from and cash surrenders made of policies'
    'issued on or after'       INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS '21 June 1988 where a LUMP SUM PAYMENT HAS BEEN MADE.'
    'Withdrawals made'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'before age 59 1/2, unless disabled, are subject to a '
    '10% penalty tax, as'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'well as ordinary income tax. The IRS priority sequence'
    'for withdrawals is'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'interest earnings first and then principal. Principal'
    'is withdrawn without'  INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  MOVE     'taxation and penalty.'   TO #CO-LINES (#I1)
END-IF
*
* IF  (#CT-TO-PLAN (#P-X, #T-X) = 'CSV' OR = 'PDUP')
*  OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$'))  /* csv split
* PERFORM CORR-MSG-FORMAT
* END-IF
*
IF #CT-TO-PLAN (#P-X, #T-X) = MASK ('$')      /* csv split
*   AND #ALOT-CHG-AUTH = 'Y' AND #MIL-STATUS = 'R' AND #MODE = 'G' /* YK1
    AND #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A'             /* DW6
    AND #MODE = 'G'              /* YK3
  ADD 1 TO #I1
  COMPRESS 'Thank you for your authorization to change your'
    'allotment amount.'      INTO #CO-LINES (#I1)                  /* YK1
  ADD 1 TO #I1
  COMPRESS 'We will be submitting the above change to DFAS.'
    INTO #CO-LINES (#I1)                                           /* YK1
END-IF
IF  (#CT-TO-PLAN (#P-X, #T-X) = 'CSV' OR = 'RSGN')
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('$'))  /* csv split
    OR (#CT-TO-PLAN (#P-X, #T-X) = MASK ('R$')) /* Rsgn split
  IGNORE
ELSE
  PERFORM SIGNATURE-LINE
END-IF
*
IF (#CT-TO-PLAN (#P-X, #T-X) = 'CSV')
    AND (#CT-TAMRA-IND (#P-X, #T-X) NE 'Y')  /* space considerations
  IF #I1 = #SCR-MAX-LINES - 2                                       /* YK1
    ADD 1 TO #I1                                                    /* YK1
  ELSE                                                              /* YK1
    ADD 2 TO #I1                                                    /* YK1
  END-IF
* IF #MIL-STATUS = 'R' AND #MODE = 'G'                              /* YK2
  IF #MODE = 'G'                                                    /* YK3
    IF NOT #ALOT-CHG-AUTH = 'Y' OR = 'S' OR = 'A'     /* DW6
      COMPRESS 'Please complete and return the enclosed allotment'  /* YK1
        'change form along with' INTO #CO-LINES (#I1)               /* YK1
      ADD 1 TO #I1                                                  /* YK1
      COMPRESS 'this letter. AAFMAA has been given authority by DFAS'
        'to process all Retiree' INTO #CO-LINES (#I1)               /* YK1
      ADD 1 TO #I1                                                  /* YK1
      COMPRESS 'AAFMAA allotments. DFAS will not start/stop/change'
        'your AAFMAA allotment.' INTO #CO-LINES (#I1)               /* YK1
    ELSE
      COMPRESS 'Thank you for your authorization to change your'
        'allotment amount.'      INTO #CO-LINES (#I1)               /* YK1
      ADD 1 TO #I1
      COMPRESS 'We will be submitting the above change to DFAS.'
        INTO #CO-LINES (#I1)                                        /* YK1
    END-IF
    ADD 1 TO #I1
  END-IF                                                            /* YK1
  COMPRESS 'Instead of cash surrendering you may want to consider'
    'taking a loan'   INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  COMPRESS 'against your cash value and avoid taxable income'
    'consequences. Call'  INTO #CO-LINES (#I1)
  ADD 1 TO #I1
  MOVE 'for details and to request a loan form.' TO #CO-LINES (#I1)
END-IF
*
* IF (((#CT-TO-PLAN (#P-X, #T-X) = 'CSV') AND #ALL-TERMED) /* full DW2
*  OR ((#CT-TO-PLAN (#P-X, #T-X) = 'RSGN'))               /* csv or rsgn
*        AND (#CT-SELECT (#P-X, #T-X) = '2'))
*   ADD 2 TO #I1
*   COMPRESS 'P.S. PLEASE COMPLETE AND RETURN THE ENCLOSED SURVEY WITH'
*            'THE RELEASE FORM.'   INTO #CO-LINES (#I1)
* END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF *DEVICE = 'BATCH'
  COMPRESS 'CN' #CGA-SCR-ID INTO #FORMAT-CN LEAVING NO
*
  WRITE (8) NOTITLE USING FORM 'C1610FCO'
*
* IF  ((#CT-TO-PLAN (#P-X, #T-X) = 'CSV') AND #ALL-TERMED) /* full DW2
*  OR ((#CT-TO-PLAN (#P-X, #T-X) = 'RSGN'))            /* csv or rsgn
*        AND (#CT-SELECT (#P-X, #T-X) = '2')
*   WRITE (8) NOTITLE USING FORM 'C1612FCO'
* END-IF
ELSE
  INPUT USING MAP 'C1610MCO'
*
  IF #I1 GT #SCR-MAX-LINES + 1  /* now 22 lines on first page
    MOVE '*' TO #CGA-SCR-COMMAND
    INPUT USING MAP 'C1611MCO'
  END-IF
*
  IF #I1 GT #SCR-MAX-LINES + #SCR-MAX-LINES + 1 /* now 22 lines on 1rst
    MOVE '*' TO #CGA-SCR-COMMAND
    INPUT USING MAP 'C1612MCO'
*
    IF #CGA-SCR-COMMAND NE '*'
      FETCH 'G1000PXX'
    END-IF
  END-IF
END-IF
*
* * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE SIGNATURE-LINE
* * * * * * * * * * * * * * * * * * *
ADD 2 TO #I1
COMPRESS '____________________________________________'
  '       (Date) ____________' INTO #CO-LINES (#I1)
*
IF #NUM-OWNERS GT 1
  COMPUTE #I3 = #NUM-OWNERS - 1
  FOR #I2 1 TO #I3
    ADD 2 TO #I1
    COMPRESS '____________________________________________'
      '       (Date) ____________' INTO #CO-LINES (#I1)
  END-FOR
*
  ADD 1 TO #I1
  MOVE '(Signature of Owners)' TO #CO-LINES (#I1)
ELSE
  ADD 1 TO #I1
  COMPRESS '(Signature) ' #FORMATED-NAME INTO #CO-LINES (#I1)
END-IF
*
ADD 1 TO #I1
MOVE #SCR-POLS TO #CO-LINES (#I1)
MOVE #CGA-CLERK TO #CO-TAX-INC (#I1)
*
END-SUBROUTINE
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE CORR-MSG-FORMAT
* * * * * * * * * * * * * * * * *
*
ADD 2 TO #I1
*
MOVE ' ' TO #CLERK-NAME-PASS
CALLNAT 'GCKNMSXX' #CGA-CLERK
  #INVALID
  #CLERK-NAME-PASS
COMPRESS 'This correspondence has been prepared for you by:'
  #CLERK-NAME-PASS INTO #CO-LINES (#I1)
*
END-SUBROUTINE
*
END
