* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ADRNE012 *** Collect phone data                              *
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* NGG  08/16/2010  None    Program created                             *
************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID  (N8)
1 #ISN         (P8)
1 #TEXT        (A67/1:V)
LOCAL
1 BLANK  (A1)  CONST <' '>
1 #J     (I4)

1 PHONE-V VIEW OF A-CONTACT-PHONE
  2 PHONE-ID
  2 CONTACT-ID
  2 PHONE-TYPE-CODE
  2 INTL-ACCESS-CODE
  2 INTL-DIAL-NUMBER
  2 AREA-CODE
  2 DIAL-NUMBER
  2 REDEFINE DIAL-NUMBER
    3 #DIAL-3                (A3)
    3 #DIAL-4                (A4)
  2 EXT
  2 PREFERRED-PHONE-IND
  2 BEST-TIME-TO-CALL
  2 INVALID-IND
  2 REMARKS                  (A70/3)

1 #PH-TYPE-TXT  (A20)
1 #PH-PREF-TXT  (A20)
1 #CODE-NAME    (A) DYNAMIC
1 #INTERNATIONAL (A16)
1 #EXT          (A) DYNAMIC

END-DEFINE

RESET #TEXT (*)

GET PHONE-V #ISN

#EXT := BLANK
IF PHONE-V.EXT > 0
  COMPRESS 'Ext' PHONE-V.EXT into #EXT
END-IF

IF PHONE-V.PREFERRED-PHONE-IND = 'Y'
  #PH-PREF-TXT := 'Preferred'
ELSE
  RESET #PH-PREF-TXT
END-IF
*  'PH-TYPE',                 /* 15 Phone type code
#CODE-NAME   := 'PH-TYPE'
#PH-TYPE-TXT := FUNCTION-ADDR-CODES(<#CODE-NAME,PHONE-V.PHONE-TYPE-CODE>)

IF AREA-CODE = 0
  #INTERNATIONAL := 'International'
  IF #EXT = BLANK
    COMPRESS
      '011'
      INTL-DIAL-NUMBER
      '('
      #INTERNATIONAL
      #PH-PREF-TXT
      #PH-TYPE-TXT
      ')'
      INTO #TEXT (1)
  ELSE
    COMPRESS
      '011'
      INTL-DIAL-NUMBER
      #EXT
      '('
      #INTERNATIONAL
      #PH-PREF-TXT
      #PH-TYPE-TXT
      ')'
      INTO #TEXT (1)
  END-IF
ELSE
  COMPRESS
    #DIAL-3
    #DIAL-4
    INTO #TEXT (1) WITH DELIMITER '-'
  IF #EXT = BLANK
    COMPRESS
      AREA-CODE
      #TEXT (1)
      '('
      #PH-PREF-TXT
      #PH-TYPE-TXT
      ')'
      INTO #TEXT (1)
  ELSE
    COMPRESS
      AREA-CODE
      #TEXT (1)
      #EXT
      '('
      #PH-PREF-TXT
      #PH-TYPE-TXT
      ')'
      INTO #TEXT (1)
  END-IF
END-IF

IF REMARKS (1) NE BLANK
  #TEXT (2) := REMARKS (1)
END-IF

END
