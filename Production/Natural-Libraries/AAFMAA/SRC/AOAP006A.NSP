* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP006A - DRIVER FOR RET SERIES IN BATCH FROM "MR" RET
*
*   THE PARM FOR THIS PROGRAM SHOULD BE LOADED INTO THE NATURAL STACK
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
************************************************************************
*                       MODIFICATION LOG                               *
* USER DATE     TAG  REASON                                            *
* PAM  03112005 PM1  Reset display fields for GRANKSXX                 *
* DRW  08272006 DW2  ADD WL-LT-IND
* TMT  07152014 TT1  Contact Normalization - DOB and Sex               *
************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
* * * * * * * *   T A S   E R R O R   F I L E   * * * * * * * *
1 TAS-ERROR-FILE
  2 TEF-ID-NUMBER   (N6)
  2 TEF-BAD-AD      (L)
  2 TEF-REQUEST     (L)
  2 TEF-MANUAL-REVUE(L)
  2 TEF-BAD-BA      (L)
  2 TEF-NUM-PAGES   (N2)
  2 TEF-WL-LT       (A1)  /* DW99
* * * *   M A N U A L   R E Q U E S T   F I L E   * * * * * * *
1 XX-R01 VIEW OF A-REQUEST
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NUMBER-A  (A6)
  2 MEMBER-NAME    (A20)
  2 CLERK-ID       (A3)
  2 ZIP-CODE       (N5)
  2 REDEFINE ZIP-CODE
    3 FIR-IND       (N1)
    3 MSG-IND       (N1)
    3 RET-IND       (N1)
    3 FIN-IND       (N1)
    3 FAS-IND       (N1)
  2 RQST-TABLE     (A10)
  2 REDEFINE RQST-TABLE
    3 BEN-IND       (A1)
    3 MAL-IND       (A1)
    3 BEN-SHO-IND   (A1)
    3 ACC-IND       (A1)
    3 CAP-IND       (A1)
    3 APP-IND       (A1)
    3 PIL-IND       (A1)
  2 MESSAGE        (A60/20)
  2 REQUEST-DATE   (N4)       /* MMDD
  2 REDEFINE REQUEST-DATE
    3 REQUEST-MM   (A2)
    3 REQUEST-DD   (A2)
  2 ASSUM-RET-DATE (N8)
  2 ASSUM-RET-DATE-2 (N8)
  2 ASSUM-RET-DATE-3 (N8)
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER      (N6)
  2 REDEFINE ID-NUMBER
    3 ID-NBR       (A6)
  2 MEMBER-CONTACT-ID
  2 MILITARY-STATUS (A1)
  2 RANK            (A3)
  2 MILITARY-SERVICE (A3)
  2 NAME            (A25)
*
1 FI-V VIEW OF A-INVESTMENTS
  2 C*INV-GEN-TBL
  2 C*INV-MUTUAL-STOCKS-BONDS-TBL
  2 C*INV-E-BONDS-TBL
  2 C*INV-E-BONDS-TBL-2
*
* * * *   REPORT FIELDS           * * * * * * * * * * * * * * *
1 #REPORT-FIELDS   (3)
  2 #RPT-CN          (A8)
  2 #RPT-NAME        (A20)
  2 #RPT-CLERK       (A3)
  2 #RPT-TYPE        (A5)
  2 #RPT-DATE        (A5)
*
1 #DATN       (N8)
1 REDEFINE #DATN
  2 #YYYY     (N4)
  2 REDEFINE #YYYY
    3 #CENT   (N2)
    3 #YY     (N2)
  2 #MMDD     (N4)
  2 REDEFINE #MMDD
    3 #MM     (N2)
    3 #DD     (N2)
*
1 #COUNTERS
  2 #RET-RQST-CNT   (P5)
  2 #RET-LTR-CNT    (P5)
  2 #MEMBER-CNT     (P5)
  2 #LOOP-CNT       (I2)
1 #RANK-LITERAL (A23)
1 #DISPL        (P2)
1 #CMND         (A2)   INIT <'AD'>
1 #INVALID      (L)
1 #RANK-LIT     (A23)
1 #RANK-DISPLAY (A5)
*
1 #BYPASS           (L)
1 #I1               (I2)
1 #I2               (I2)
1 #ISN              (P8)
1 #FIN-ISN          (P8)
1 #NO-INVESTMENTS   (L)
1 #PASS-FD-TYPE     (A2)   INIT <'FD'>
1 #LAST-NAME       (A25)
1 #FIRST/MID       (A25)
1 #SUFFIX          (A25)
1 #EXTRA1          (A25)
1 #FORMATTED-NAME  (A35)
1 #FOUND-9999      (L)
1 #DATE-OF-BIRTH   (N8.0)          /* TT1
END-DEFINE
*
*
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM 5X '"RET" SERIES DETAIL'
  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
*
MOVE *DATN TO #DATN
*
* * * * * * * * * * * * * * * * *
* RET SERIES REQUEST PROCESSING
* * * * * * * * * * * * * * * * *
*
READ RECORDS XX-R01 PHYSICAL       /* READ ALL REQUESTS
  IF REQUEST-DATE = 9999
    MOVE TRUE TO #FOUND-9999
  END-IF
*
  IF XX-R01.RET-IND = 0        /* BYPASS IF RET NOT REQUESTED
    END TRANSACTION
    ESCAPE TOP
  END-IF
*
  IF #FOUND-9999 AND (REQUEST-DATE NE 9999)   /* bypass if entered after
    ESCAPE TOP                                /* job started
  END-IF
*
  IF   (REQUEST-DATE = 9999)
      AND (XX-R01.MSG-IND = 0)   AND (XX-R01.FAS-IND = 0)
      AND (XX-R01.BEN-IND = ' ') AND (XX-R01.PIL-IND = ' ')
      AND (XX-R01.ACC-IND = ' ') AND (XX-R01.CAP-IND = ' ')
      AND (XX-R01.APP-IND = ' ') AND (XX-R01.MAL-IND = ' ')
      AND (XX-R01.BEN-SHO-IND = ' ')
    DELETE
  END-IF
*
  ADD +1 TO #RET-RQST-CNT
  END TRANSACTION
END-ALL  /* (0137)
*
SORT BY ID-NUMBER
    USING CLERK-ID MEMBER-NAME REQUEST-DATE ZIP-CODE ASSUM-RET-DATE
    MESSAGE (*) ASSUM-RET-DATE-2 ASSUM-RET-DATE-3
*
  RESET *ISN
*
  FIND (1) ST-V WITH MEMBER-KEY = ID-NUMBER-A
    IF NO RECORDS FOUND
      WRITE (2) 'MBR NOT FOUND' ID-NUMBER
      ESCAPE BOTTOM
    END-NOREC
    MOVE *ISN TO #ISN
  END-FIND
*
  IF #ISN = 0
    ESCAPE TOP
  END-IF
*
  IF ST-V.MILITARY-STATUS NE 'A'
    WRITE (2) ID-NUMBER ' MBR MUST BE ACTIVE FOR "RET" SERIES'
    ESCAPE TOP
  END-IF
*
  WRITE (2) ID-NUMBER  ' PRE-RETIREMENT PACKET PRODUCED'
  ADD 1 TO #RET-LTR-CNT
  MOVE ID-NUMBER    TO #CGA-SCR-ID
    #CGA-ID
    #CGA-ORIG-ID
  MOVE #ISN         TO #CGA-ISN
*
  MOVE 'AD'         TO #CGA-SCR-COMMAND
    #CGA-ORIG-COMMAND
  MOVE 'RET'        TO #CGA-SCR-SUFFIX
    #CGA-ORIG-SUFFIX
  MOVE ASSUM-RET-DATE TO #CGA-MF-SCR-PARM-1-N /* FOR "AD" TO USE RET-DT
  MOVE CLERK-ID TO #CGA-CLERK                 /* FOR "AD" CORRESPOND USE
*
  FETCH RETURN 'G3500PAD'
  MOVE #CGA-MF-SCR-PARM-1-N  TO ASSUM-RET-DATE /* RESET RET DT IF CHGED
*
  MOVE 'DI'         TO #CGA-SCR-COMMAND
    #CGA-ORIG-COMMAND
  MOVE 'RET'        TO #CGA-SCR-SUFFIX
    #CGA-ORIG-SUFFIX
  FETCH RETURN 'E4000PDI'
*
  RESET #NO-INVESTMENTS
  FIND (1) FI-V WITH FI-V.ID-NUMBER = #CGA-ID
    IF NO RECORDS FOUND
      MOVE TRUE TO #NO-INVESTMENTS
      ESCAPE BOTTOM
    END-NOREC
*
    MOVE *ISN TO #FIN-ISN
    IF   (C*INV-GEN-TBL = 0)
        AND (C*INV-MUTUAL-STOCKS-BONDS-TBL=0)
        AND (C*INV-E-BONDS-TBL = 0)
        AND (C*INV-E-BONDS-TBL-2 = 0)
      MOVE TRUE TO #NO-INVESTMENTS
    END-IF
  END-FIND
*
  IF NOT #NO-INVESTMENTS
    SEPARATE ST-V.NAME LEFT INTO #LAST-NAME #FIRST/MID #SUFFIX #EXTRA1
      WITH DELIMITER ' '
    RESET #DISPL #RANK-LIT #RANK-DISPLAY                          /* PM1
    CALLNAT 'GRANKSXX' #CMND ST-V.RANK ST-V.MILITARY-SERVICE
      #INVALID #DISPL #RANK-LIT #RANK-DISPLAY
    COMPRESS #RANK-DISPLAY #FIRST/MID #LAST-NAME INTO #FORMATTED-NAME
*
    IF ST-V.MILITARY-STATUS = 'R'
      COMPRESS #FORMATTED-NAME ', RET' INTO #FORMATTED-NAME
    END-IF
*
    MOVE 'FD' TO #CGA-SCR-COMMAND
      #CGA-ORIG-COMMAND
    MOVE 'OVW' TO #CGA-SCR-SUFFIX
      #CGA-ORIG-SUFFIX
    FETCH RETURN 'D3280PFD' #FIN-ISN #PASS-FD-TYPE #FORMATTED-NAME
*
    IF #CGA-SCR-SUFFIX = 'BPD'
      FETCH RETURN 'D3282PFD' #FIN-ISN #PASS-FD-TYPE #FORMATTED-NAME
    END-IF
  END-IF
*
  RESET #DATE-OF-BIRTH                                               /* TT1
  #DATE-OF-BIRTH := GET-DATE-OF-BIRTH(<ST-V.MEMBER-CONTACT-ID>)      /* TT1
*
  IF #DATE-OF-BIRTH NE 0   /* must have dob                          /* TT1
    MOVE 'GT'         TO #CGA-SCR-COMMAND
      #CGA-ORIG-COMMAND
    MOVE 'RET'        TO #CGA-SCR-SUFFIX
      #CGA-ORIG-SUFFIX
    FETCH RETURN 'G4720PGT'
*
    MOVE 'SI'         TO #CGA-SCR-COMMAND
      #CGA-ORIG-COMMAND
    MOVE 'RET'        TO #CGA-SCR-SUFFIX
      #CGA-ORIG-SUFFIX
    MOVE ASSUM-RET-DATE TO #CGA-MF-SCR-PARM-1-N /* FOR "AD" TO USE RET-DT
    FETCH RETURN 'D3000PXX'
*
    MOVE ASSUM-RET-DATE TO #DATN
    PERFORM PROCESS-RET-BAS
*
    IF ASSUM-RET-DATE-2 NE 0
      MOVE 'AD'         TO #CGA-SCR-COMMAND
        #CGA-ORIG-COMMAND
      MOVE 'RET'        TO #CGA-SCR-SUFFIX
        #CGA-ORIG-SUFFIX
      MOVE ASSUM-RET-DATE-2 TO #CGA-MF-SCR-PARM-1-N
      MOVE 'RT*' TO #CGA-CLERK
      FETCH RETURN 'G3500PAD'
*
      MOVE #CGA-MF-SCR-PARM-1-N TO ASSUM-RET-DATE-2
      MOVE ASSUM-RET-DATE-2 TO #DATN
      PERFORM PROCESS-RET-BAS
    END-IF
*
    IF ASSUM-RET-DATE-3 NE 0
      MOVE 'AD'         TO #CGA-SCR-COMMAND
        #CGA-ORIG-COMMAND
      MOVE 'RET'        TO #CGA-SCR-SUFFIX
        #CGA-ORIG-SUFFIX
      MOVE ASSUM-RET-DATE-3 TO #CGA-MF-SCR-PARM-1-N
      MOVE 'RT*' TO #CGA-CLERK
      FETCH RETURN 'G3500PAD'
*
      MOVE #CGA-MF-SCR-PARM-1-N TO ASSUM-RET-DATE-3
      MOVE ASSUM-RET-DATE-3 TO #DATN
      PERFORM PROCESS-RET-BAS
    END-IF
  END-IF
*
  MOVE #CGA-ID TO TEF-ID-NUMBER
  MOVE TRUE TO TEF-REQUEST
  MOVE 99   TO TEF-NUM-PAGES
  WRITE WORK 3 TAS-ERROR-FILE    /* TO MATCH FOR TAS
END-SORT  /* (0164)
*
IF #RET-LTR-CNT = 0        /* TO PREVENT 1502 ERRORS IN ATHP200A
  WRITE NOTITLE '     NO RET PACKETS'
  NEWPAGE
  WRITE NOTITLE '     NO RET PACKETS'
END-IF
*
WRITE (2) / 12T 'TOTAL RET LTR REQUESTS' #RET-RQST-CNT
WRITE (2) / 12T 'TOTAL RET LTRS PRINTED' #RET-LTR-CNT
*
* * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PROCESS-RET-BAS
* * * * * * * * * * * * * * * * *
*
MOVE RET-IND        TO #CGA-MF-SCR-COMMAND  /* FOR DOPMA CODE
*
SUBTRACT 1 FROM #DD
IF #DD = 0
  SUBTRACT 1 FROM #MM
*
  IF #MM = 0
    MOVE 12 TO #MM
    SUBTRACT 1 FROM #YYYY
  END-IF
*
  IF #MM = 02
    MOVE 28 TO #DD
  ELSE
    IF #MM = 04 OR = 06 OR = 09 OR = 11
      MOVE 30 TO #DD
    ELSE
      MOVE 31 TO #DD
    END-IF
  END-IF
END-IF
*
MOVE #DATN TO #CGA-MF-SCR-PARM-1-N     /* FOR ASSUMED RET-DT
MOVE RET-IND        TO #CGA-MF-SCR-COMMAND  /* FOR DOPMA CODE
*
MOVE 'BA'         TO #CGA-SCR-COMMAND
  #CGA-ORIG-COMMAND
MOVE 'RET'        TO #CGA-SCR-SUFFIX
  #CGA-ORIG-SUFFIX
FETCH RETURN 'D3000PXX'
*
END-SUBROUTINE
*
END
