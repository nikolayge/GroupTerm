* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1004SAT   ** AT XXXXXX FBR     FAMILY RIDER PROCESSING
************************************************************************
*     MODIFICATION LOG
* USER DATE       TAG  REASON
* RSE  10/01/2009 RE1  Change PROCESS-IND/600K logic for Unification
* bz   11/19/2009 bz1  Remove Re1. Unification: Identify SAS Only
* PAM  06/07/2011 PM1  Removed STATUS file name from parameters
************************************************************************
DEFINE DATA
PARAMETER
1 #CGA-SCR-COMMAND      (A2)
1 #CGA-SCR-ID           (N9)
1 #CGA-ID-G             (A6)
1 REDEFINE #CGA-ID-G
 2 #CGA-ID              (N6)
1 #CGA-SCR-SUFFIX       (A3)
1 #CGA-ORIG-SUFFIX      (A3)
1 #TEXT                 (A78)
1 #AT-V-INTRO-FLAG      (A1)
1 #AT-V-REMARKS         (A70/3)
1 #AT-V-RIDER-SPS       (A25)
1 #AT-V-RIDER-PLAN      (A2/5)
1 #AT-V-RIDER-ATT-POL   (N8/5)
1 #SCR-FACE-UNITS       (N4)               /* NG99
1 #PLAN                 (A3)
1 #MEMBER-CN            (A6)
1 #AT-V-CONTACT-ID      (N8)               /* PM1
*
LOCAL USING OBJLCTRL                       /* PM1
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 NOTES   (40)
 2 REDEFINE NOTES
  3 NOTE-OCC (40)
   4 NOTE-15  (A15)
   4 NOTE-60  (A60)
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 NAME
 2 PROCESS-IND
 2 INTRO-FLAG
 2 STATUS-CODE-SP
 2 STATUS         (9)
 2 ISSUE-DATE     (9)
 2 PLAN           (9)
 2 FACE-AMOUNT    (9)
 2 MONTH-ALLOC-AMT (9)
*
1 NT-V VIEW OF A-NOTES
 2 ID-NUMBER
 2 NOTE-DATE  (N8/1)
 2 NOTE-TEXT  (A60/40)
 2 REDEFINE NOTE-TEXT
  3 PROSPECTIVE-OCC    (85)
   4 PR-USER        (A3)   /* who initiated it
   4 PR-POLS-INIT   (N5)   /* how many he initiated
*
1 #RID-TYPE          (A2/10)
1 REDEFINE #RID-TYPE
 2 #RID-TYPE-OCC  (10)
  3 #RID-TYPE-1  (A1)
  3 #RID-TYPE-2  (N1)
1 #RID-ATTACH-ISS-DT (N8/10)
1 #RID-ATTACH-SEL-CV (C/10)
1 #RID-ATTACH-TEXT   (A50/10)
1 #ENT-SPS-NAME-C (C)
*
1 #COMMA    (A1) INIT <','>
*
1 #HAS-CHILD-FBR-NOTE (L)
1 #NO-SPOUSE (L)
1 #SCR-TEXT  (A78)
1 #DATE-X    (A10)
1 #FACE      (N7.2)    /* ng99
1 REDEFINE #FACE
 2 #FACE-K    (N3)
 2 #FACE-REST (N3.2)
1 #I1  (I2)
1 #I2  (I2)
1 #I3  (I2)
*
1 #YYYYMMDD   (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY   (N4)
 2 #MM     (N2)
 2 #DD     (N2)
*
1 #TOT-UNITS  (N2)
1 #NUM-SEL    (N2)
1 #FND-ST     (L)
*
* below is linkage to GRIDCSXX
1 #ID         (N6)
1 REDEFINE #ID
 2 #ID-A      (A6)
* THE FOLLOWING input IS PASSED ONLY ON AN "AT" FOR A NEW RIDER
1 #AT-RID-ATTACH-FAC-AMT (N6.2) /* Only if attached to the new "AT"
1 #AT-RID-ATTACH-PLAN    (A3)   /*    policy not yet created
1 #AT-RID-PLAN           (A2/5) /* New rider plan
1 #AT-RID-ATTACH-ISS-DT  (N8/5) /* Only if attached to an existing pol
* output below  (ex = existing)
1 #AT-TOT-FBR-PREM        (N3.2)
1 #EX-FBR-PREM-BY-LINE    (N3.2/9)
1 #EX-SPS-DB-BY-LINE      (N6/9)
1 #EX-DEP-DB-BY-LINE      (N6/9)
1 #EX-EACH-DEP-DB-BY-LINE (N6/9)
1 #NUM-ELIG-CHILDREN      (N2)
1 #NUM-ELIG-CHILDREN-UNDER-6-MON (N2)
1 #TOT-CHILD-DB      (N6)
1 #RID-TEXT          (A78)
* above is linkage to GRIDCSXX
*
END-DEFINE
***************************************************************************
*
FIND AT-V WITH ID-NUMBER = #CGA-ID
END-FIND
*
RESET #FND-ST
FIND (1) ST-V WITH ST-V.MEMBER-CONTACT-ID = #AT-V-CONTACT-ID
  IF ST-V.PROCESS-IND = 'C'
   AND ST-V.INTRO-FLAG NE 'A'
    ESCAPE BOTTOM
  END-IF
*
  #FND-ST := TRUE
END-FIND
*
IF NOT #FND-ST
  MOVE 'Attached MBR CN not found on the FBR' TO #TEXT
  ESCAPE ROUTINE
END-IF
*
MOVE ST-V.ID-NUMBER TO #ID
*
MOVE (AD=P) TO #RID-ATTACH-SEL-CV (*)
*
IF #AT-V-RIDER-SPS = ' '
  FIND CR-V WITH CR-V.CONTACT-1-ID = #AT-V-CONTACT-ID
    IF CR-V.ROLE-2-CD = 'S' AND STATUS-CODE-SP NE 'D'
      #AT-V-RIDER-SPS := FUNCTION-CONTACT-NAME-2(<CR-V.CONTACT-2-ID>)
      ESCAPE BOTTOM
    END-IF
  END-FIND
END-IF
*
MOVE 'New Policy with Rider' TO #RID-ATTACH-TEXT (1)
RESET #RID-ATTACH-SEL-CV (1)
*
IF   (#AT-V-RIDER-PLAN (1) NE ' ')
 AND (#AT-V-RIDER-ATT-POL (1) = 0)
  MOVE #AT-V-RIDER-PLAN (1) TO #RID-TYPE (1)
END-IF
*
MOVE 1 TO #I2
*
IF #AT-V-CONTACT-ID NE 0
  FOR #I1 1 TO 9
    IF   (STATUS (#I1) = 'D')
     AND (NOT PLAN (#I1) = 'SO' OR = 'FSO')
     AND ((ISSUE-DATE (#I1) GE 19970101)
              OR  (MONTH-ALLOC-AMT (#I1) = 999.99))
      ADD 1 TO #I2
      RESET #RID-ATTACH-SEL-CV (#I2)
      MOVE FACE-AMOUNT (#I1) TO #FACE
      MOVE ISSUE-DATE (#I1) TO #RID-ATTACH-ISS-DT (#I2)
      MOVE ISSUE-DATE (#I1) TO #YYYYMMDD
      COMPRESS #MM #DD #YYYY INTO #DATE-X WITH '/'
      COMPRESS "$" #FACE-K 'k' INTO #RID-ATTACH-TEXT (#I2) LEAVING NO
      COMPRESS #RID-ATTACH-TEXT (#I2) PLAN (#I1) 'policy issued' #DATE-X
            INTO #RID-ATTACH-TEXT (#I2)
*
      FOR #I3 1 TO 5
        IF #AT-V-RIDER-ATT-POL (#I3) = ISSUE-DATE (#I1)
          MOVE #AT-V-RIDER-PLAN (#I3) TO #RID-TYPE (#I2)
          ESCAPE BOTTOM
        END-IF
      END-FOR
    END-IF
  END-FOR
END-IF
*
INPUT TEXT #SCR-TEXT MARK *#RID-TYPE (1) USING MAP 'C1004MAT'
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
*
IF   (#AT-V-CONTACT-ID = 0)
 AND (#AT-V-RIDER-SPS = ' ')
 AND ((#RID-TYPE (1) = MASK ('F')) OR  (#RID-TYPE (1) = MASK ('T')))
  REINPUT 'Enter Spouse Name'
  MARK *#AT-V-RIDER-SPS
END-IF
*
RESET #NO-SPOUSE
IF #AT-V-RIDER-SPS = ' '
  MOVE TRUE TO #NO-SPOUSE
END-IF
*
RESET #TOT-UNITS #NUM-SEL
FOR #I1 1 TO 5
  IF #RID-TYPE (#I1) = ' '
    ESCAPE TOP
  END-IF
*
  IF NOT #RID-TYPE-1 (#I1) = 'F' OR = 'T' OR = 'C'
    REINPUT USING HELP
    MARK *#RID-TYPE (#I1)
  END-IF
*
  IF #NO-SPOUSE AND (#RID-TYPE-1 (#I1) = 'F' OR = 'T')
    REINPUT 'Plan invalid - No current spouse'
    MARK *#RID-TYPE (#I1)
  END-IF
*
  IF NOT #RID-TYPE-2 (#I1) = 1 OR = 2 OR = 3 OR = 4 OR = 5
    REINPUT 'Units must be 1 to 5'
    MARK *#RID-TYPE (#I1)
  END-IF
*
  IF #NUM-SEL GE 5
    REINPUT 'Max of 5 can be selected'
    MARK *#RID-TYPE (#I1)
  END-IF
*
  ADD 1 TO #NUM-SEL
  ADD #RID-TYPE-2 (#I1) TO #TOT-UNITS
END-FOR
*
IF #NUM-SEL = 0
  RESET #AT-V-RIDER-PLAN    (*)
  RESET #AT-V-RIDER-ATT-POL (*)
  RESET #AT-V-RIDER-SPS
  ESCAPE ROUTINE
END-IF
*
IF #TOT-UNITS GT 5
  REINPUT 'Units exceed 5'
  MARK *#RID-TYPE (1)
END-IF
*
RESET #I2
FOR #I1 1 TO 10
  IF #RID-TYPE (#I1) NE ' '
    ADD 1 TO #I2
    MOVE #RID-TYPE          (#I1) TO #AT-V-RIDER-PLAN    (#I2)
    MOVE #RID-ATTACH-ISS-DT (#I1) TO #AT-V-RIDER-ATT-POL (#I2)
  END-IF
END-FOR
*
FOR #I1 1 TO 40
  IF   (AT-V.NOTE-60 (#I1) = MASK ('#CHILD NOT COVERED'))
   OR  (AT-V.NOTE-60 (#I1) = MASK ('#Child not covered'))
    MOVE TRUE TO #HAS-CHILD-FBR-NOTE
    ESCAPE BOTTOM
  END-IF
END-FOR
*
IF NOT #HAS-CHILD-FBR-NOTE
  MOVE '#Child not covered by rider:' TO #AT-V-REMARKS (1)
END-IF
*
COMPUTE #AT-RID-ATTACH-FAC-AMT = #SCR-FACE-UNITS * 1000
MOVE #PLAN TO #AT-RID-ATTACH-PLAN
*
FOR #I1 1 TO 5
  MOVE #AT-V-RIDER-PLAN    (#I1) TO #AT-RID-PLAN (#I1)
  MOVE #AT-V-RIDER-ATT-POL (#I1) TO #AT-RID-ATTACH-ISS-DT (#I1)
END-FOR
*
IF #SCR-FACE-UNITS NE 0
  CALLNAT 'GRIDCSXX' USING
    #ID
    #AT-RID-ATTACH-FAC-AMT
    #AT-RID-ATTACH-PLAN
    #AT-RID-PLAN           (1:5)
    #AT-RID-ATTACH-ISS-DT  (1:5)
    #AT-TOT-FBR-PREM
    #EX-FBR-PREM-BY-LINE    (1:9)
    #EX-SPS-DB-BY-LINE      (1:9)
    #EX-DEP-DB-BY-LINE      (1:9)
    #EX-EACH-DEP-DB-BY-LINE (1:9)
    #NUM-ELIG-CHILDREN
    #NUM-ELIG-CHILDREN-UNDER-6-MON
    #TOT-CHILD-DB
    #RID-TEXT
*
  IF #RID-TEXT NE ' '
    REINPUT #RID-TEXT
    MARK *#RID-TYPE (1)
  END-IF
END-IF
*
END
