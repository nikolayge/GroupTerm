* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : ATBPEMAL                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Create email import file for non-member applications  *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    02082008  None    Program Written                             *
* PAM    06032009  PM1     Exclude SP/SD/S7 per Lisa M                 *
************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
*
LOCAL
*
1 AT-V VIEW OF A-APPL-TRACKING
 2 ID-NUMBER
 2 SSN
 2 MEMBER-CN
 2 NAME
 2 ACTION-FLAG
 2 AT-STATUS                                             /* PM1
 2 DATE-OF-BIRTH
 2 INTERNET-ADDR
 2 INTRO-FLAG
 2 APPL-SOURCE
 2 USER-SOURCE
 2 DATE-RECEIVED
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PROCESS-IND
*
1 #WORK-FILE-FIELDS
 2 #EXTRACT-RECORD               (A250)
 2 #TAB                          (A1)   INIT <H'09'>
 2 #HEAD-AT-NUMBER               (A15)  INIT <'AT NUMBER'>
 2 #HEAD-NAME                    (A15)  INIT <'APPLICANT NAME'>
 2 #HEAD-DOB                     (A15)  INIT <'DATE OF BIRTH'>
 2 #HEAD-EMAIL                   (A15)  INIT <'EMAIL ADDRESS'>
 2 #HEAD-APP-SOURCE              (A15)  INIT <'APP SOURCE'>
 2 #HEAD-USER                    (A15)  INIT <'CREATE USER'>
 2 #HEAD-CREATE-DATE             (A15)  INIT <'CREATE DATE'>
*
1 #CONSTANTS
 2 #SALES-STATUS                 (A1)   INIT <'S'>
 2 #SPOUSE                       (A1)   INIT <'S'>
 2 #CHILD                        (A1)   INIT <'K'>
 2 #GRANDCHILD                   (A1)   INIT <'G'>
 2 #CONTACT                      (A1)   INIT <'C'>
 2 #BLANK                        (A1)   INIT <' '>
 2 #NO-EMAILS                    (A3)   INIT <'999'>
 2 #SP                           (A2)   INIT <'SP'>      /* PM1
 2 #SD                           (A2)   INIT <'SD'>      /* PM1
 2 #S7                           (A2)   INIT <'S7'>      /* PM1
*
1 #WORK-FIELDS
 2 #MEMBER                       (L)
 2 #WORK-DATE                    (N8)
 2 REDEFINE #WORK-DATE
  3 #WORK-YYYY                   (N4)
  3 #WORK-MM                     (N2)
  3 #WORK-DD                     (N2)
 2  REDEFINE #WORK-DATE
  3 #WORK-DATE-YYYY-A            (A4)
  3 #WORK-DATE-MM-A              (A2)
  3 #WORK-DATE-DD-A              (A2)
 2 #DATN                         (N8)
 2 #DOB-MM                       (N2)
 2 #WORK-DOB                     (N8)
 2 REDEFINE #WORK-DOB
  3 #WORK-DOB-YYYY               (N4)
  3 #WORK-DOB-MM                 (N2)
  3 #WORK-DOB-DD                 (N2)
*
1 #DISPLAY-FIELDS
 2 #DISP-AT-NUMBER               (A6)
 2 #DISP-EMAIL                   (A75)
 2 #DISP-NAME                    (A25)
 2 #DISP-CREATE-USER             (A20)
 2 #WORK-DATE-DISPLAY            (A10)
 2 #DISP-DOB                     (A10)
 2 #DISP-DATE-CREATE             (A10)
 2 #DISP-SOURCE-CODE             (A7)
*
END-DEFINE
***********************************************************************
*
INCLUDE GONERCXX
*
PERFORM DETERMINE-DATES
*
COMPRESS #HEAD-AT-NUMBER    #TAB
         #HEAD-NAME         #TAB
         #HEAD-DOB          #TAB
         #HEAD-EMAIL        #TAB
         #HEAD-APP-SOURCE   #TAB
         #HEAD-USER         #TAB
         #HEAD-CREATE-DATE INTO #EXTRACT-RECORD LEAVING NO
*
WRITE WORK 1 #EXTRACT-RECORD
RESET #EXTRACT-RECORD
*
READ AT-V BY ID-NUMBER
*
  #WORK-DOB := AT-V.DATE-OF-BIRTH
*
  IF AT-V.ACTION-FLAG NE #SALES-STATUS
   OR AT-V.INTRO-FLAG = #SPOUSE OR = #CHILD OR = #GRANDCHILD
   OR #WORK-DOB-MM NE #DOB-MM
   OR AT-V.DATE-RECEIVED LT #DATN
   OR AT-V.INTERNET-ADDR = #NO-EMAILS OR = #BLANK
   OR AT-V.AT-STATUS = #SP OR = #SD OR = #S7             /* PM1
    ESCAPE TOP
  END-IF
*
  PERFORM DETERMINE-IF-MEMBER
  IF #MEMBER
    ESCAPE TOP
  END-IF
*
  RESET #DISP-AT-NUMBER #DISP-NAME #DISP-DOB #DISP-EMAIL
        #DISP-SOURCE-CODE #DISP-CREATE-USER #DISP-DATE-CREATE
*
  MOVE EDITED AT-V.ID-NUMBER (EM=999999) TO #DISP-AT-NUMBER
  #DISP-EMAIL       := AT-V.INTERNET-ADDR
  #DISP-SOURCE-CODE := AT-V.APPL-SOURCE
*
  #WORK-DATE        := AT-V.DATE-OF-BIRTH
  PERFORM FORMAT-DATE
  #DISP-DOB         := #WORK-DATE-DISPLAY
*
  #WORK-DATE        := AT-V.DATE-RECEIVED
  PERFORM FORMAT-DATE
  #DISP-DATE-CREATE := #WORK-DATE-DISPLAY
*
  #DISP-NAME        := FUNCTION-FULL-NAME (<AT-V.NAME>)
  #DISP-CREATE-USER := FUNCTION-CLERK-NAME (<AT-V.USER-SOURCE>)
*
  COMPRESS #DISP-AT-NUMBER   #TAB
           #DISP-NAME        #TAB
           #DISP-DOB         #TAB
           #DISP-EMAIL       #TAB
           #DISP-SOURCE-CODE #TAB
           #DISP-CREATE-USER #TAB
           #DISP-DATE-CREATE INTO #EXTRACT-RECORD LEAVING NO
*
  WRITE WORK 1 #EXTRACT-RECORD
  RESET #EXTRACT-RECORD
END-READ
*
***********************************************************************
DEFINE SUBROUTINE DETERMINE-DATES
***********************************************************************
*
#WORK-DATE := *DATN
*
#DOB-MM := #WORK-MM
ADD +1 TO #DOB-MM
IF #DOB-MM GT 12
  SUBTRACT 12 FROM #DOB-MM
END-IF
*
SUBTRACT 9 FROM #WORK-MM
IF #WORK-MM LT 1
  ADD +12 TO #WORK-MM
  SUBTRACT +1 FROM #WORK-YYYY
END-IF
#DATN := #WORK-DATE
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE DETERMINE-IF-MEMBER
***********************************************************************
*
RESET #MEMBER
*
IF AT-V.MEMBER-CN NE #BLANK
  FIND (1) ST-V WITH ST-V.MEMBER-KEY = AT-V.MEMBER-CN
    IF ST-V.PROCESS-IND = #CONTACT
      ESCAPE TOP
    END-IF
*
    #MEMBER := TRUE
  END-FIND
ELSE
  FIND ST-V WITH ST-V.SSN = AT-V.SSN
    IF ST-V.PROCESS-IND = #CONTACT
      ESCAPE TOP
    END-IF
*
    #MEMBER := TRUE
    ESCAPE BOTTOM
  END-FIND
END-IF
*
END-SUBROUTINE
*
***********************************************************************
DEFINE SUBROUTINE FORMAT-DATE
***********************************************************************
*
COMPRESS #WORK-DATE-MM-A #WORK-DATE-DD-A #WORK-DATE-YYYY-A
    INTO #WORK-DATE-DISPLAY WITH '/'
*
END-SUBROUTINE
*
END
