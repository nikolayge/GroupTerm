* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2203PCG - Contact Information Maintenance
*
************************************************************************
* MODIFICATION LOG
*
* USER   DATE     TAG  REASON
* RSE  08/19/2010 NONE Creation
************************************************************************
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING ADDA0001
LOCAL USING A-CTDATA
LOCAL
1 STATUS-V VIEW OF A-STATUS
  2 MEMBER-CONTACT-ID
*
1 PHONE-V VIEW OF A-CONTACT-PHONE
  2 PHONE-ID
  2 CONTACT-ID
  2 PHONE-TYPE-CODE
  2 TYPE
  2 INTL-ACCESS-CODE
  2 AREA-CODE
  2 DIAL-NUMBER
  2 REDEFINE DIAL-NUMBER
    3 #DIAL-3                (N3)
    3 #DIAL-4                (N4)
  2 EXT
  2 PREFERRED-PHONE-IND
  2 BEST-TIME-TO-CALL
  2 INVALID-IND
  2 START-DATE
  2 REDEFINE START-DATE
    3 #PSD-YY                 (A4)
    3 #PSD-MM                 (A2)
    3 #PSD-DD                 (A2)
  2 END-DATE
  2 REDEFINE END-DATE
    3 #PED-YY                 (A4)
    3 #PED-MM                 (A2)
    3 #PED-DD                 (A2)
*
1 EMAIL-V VIEW OF A-CONTACT-EMAIL
  2 EMAIL-ID
  2 CONTACT-ID
  2 EMAIL-TYPE
  2 EMAIL-ADDRESS
  2 EMAIL-PROTECTION-IND
  2 EMAIL-UNDELIV
  2 EMAIL-PREFERRED-IND
  2 START-DATE
  2 REDEFINE START-DATE
    3 #ESD-YY                 (A4)
    3 #ESD-MM                 (A2)
    3 #ESD-DD                 (A2)
  2 END-DATE
  2 REDEFINE END-DATE
    3 #EED-YY                 (A4)
    3 #EED-MM                 (A2)
    3 #EED-DD                 (A2)
*
1 ADDR-REL-V VIEW OF A-ADDRESS-RELATIONSHIP
  2 CONTACT-ID
  2 ADDRESS-POOL-ID
  2 ADDR-TYPE-CD
  2 ADDR-LOCATION
  2 ATTENTION-LINE
  2 RETURNED-MAIL-IND
  2 PREVENT-OVER-IND
*
1 ADDR-P-V VIEW OF A-ADDRESS-POOL
  2 ADDRESS-POOL-ID
  2 STREET
  2 UNIT
  2 CITY
  2 ZIP-CODE
  2 POSTAL-CODE
  2 URBANIZATION
  2 ADDR-FORMAT-TYPE-CD
  2 ADDR-VALID-IND
*
1 CONT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
  2 ID-NUMBER
*
1 #CONTACT-ID                (N8)
1 #SEARCH                    (L)
1 #CI                        (N8)
1 #NAME                      (A43)
1 #CN-OR-CI                  (N8)
1 #LINE-ARRAY                (50)
  2 #LINE                    (A77)
  2 #LINE-CI                 (N8)
  2 #LINE-CONTACT-TYPE       (A5)
1 #PHONE-TEXT                (A77) INIT
  <'-------------------------------------PHONE-----------------------------------'>
1 #PHONE-TEXT-2              (A77) INIT
  <'Type Loc  Code ACC Area     Number      Ext  Best   Inv   Start       End'>
1 #EMAIL-TEXT                (A77) INIT
  <'-------------------------------EMAIL/SOCIAL MEDIA----------------------------'>
1 #EMAIL-TEXT-2              (A77) INIT
  <'Type Loc                                         Prot Del'>
1 #ADDRESS-TEXT-1            (A77) INIT
  <'------------------------------------ADDRESS----------------------------------'>
1 #ADDRESS-TEXT-2            (A77) INIT
  <'Type Loc  Code                                                    Val Ovr Ret'>
1 #PHONE-DTL                 (A77)
1 REDEFINE #PHONE-DTL
  2 #PHONE-PREF              (A5)
  2 #PHONE-TYPE-CODE         (A5)
  2 #PHONE-TYPE              (A5)
  2 #PHONE-ACC               (A4)
  2 #PHONE-AREA              (A4)
  2 #PHONE-NUMBER            (A17)
  2 #FILL1                   (A1)
  2 #PHONE-EXT               (A4)
  2 #PHONE-BEST-TIME         (A9)
  2 #PHONE-INVALID           (A2)
  2 #PHONE-START             (A11)
  2 #PHONE-END               (A10)
*
1 #EMAIL-DTL                 (A77)
1 REDEFINE #EMAIL-DTL
  2 #EMAIL-PREF              (A5)
  2 #EMAIL-TYPE-CODE         (A5)
  2 #EMAIL-ADDRESS           (A40)
  2 #FILL2                   (A1)
  2 #EMAIL-PROTECTED         (A3)
  2 #EMAIL-DELIVERABLE       (A2)
  2 #EMAIL-START             (A11)
  2 #EMAIL-END               (A10)
*
1 #ADDRESS-DTL-1             (A77)
1 REDEFINE #ADDRESS-DTL-1
  2 #ADDRESS-PREF            (A5)
  2 #ADDRESS-TYPE-CODE       (A5)
  2 #ADDRESS-TYPE            (A5)
  2 #ADDRESS-ATTN            (A52)
  2 #ADDRESS-VAL             (A4)
  2 #ADDRESS-OVR             (A4)
  2 #ADDRESS-RET             (A2)
*
1 #ADDRESS-DTL-2             (A77)
1 REDEFINE #ADDRESS-DTL-2
  2 #FILL3                   (A10)
  2 #ADDRESS-FULL            (A67)
*
1 #START                     (N2) INIT<18>
1 #I                         (N2)
1 #J                         (N2)
1 #K                         (N2)
1 #CGA-SCR-COMMAND-CV        (C)
1 #CGA-SCR-ID-CV             (C)
1 #CGA-SCR-SUFFIX-CV         (C)
1 #LINE-CV                   (C/50)
1 #EXIT-TEXT                 (A4)
1 #CN-OR-CI-TEXT             (A2)
1 #CI-TEXT                   (A2)
1 #SSN                       (A9)
1 #DOB                       (A10)
1 #DOD                       (A10)
1 #DATE                      (N8)
1 REDEFINE #DATE
  2 #D-YY                    (A4)
  2 #D-MM                    (A2)
  2 #D-DD                    (A2)
1 #CURS-LINE                 (N2)
1 #SYSTEM-DATE               (A10)
1 #SELECTION                 (A1)
1 #MODE                      (A1)
1 #EMAIL-ID-PARM             (N8)
1 #PHONE-ID-PARM             (N8)
1 #MSG                       (A70)
END-DEFINE
*
DEFINE WINDOW Maintain
  SIZE 15 * 27
  BASE 6/5
  FRAMED ON POSITION SYMBOL AUTO
*
INCLUDE GONERCXX
*
INPUT #CGA-SCR-ID #CONTACT-ID #SEARCH
*
SET KEY PF3 PF4 PF12
*
MOVE *DATN TO #DATE
COMPRESS #D-MM '-' #D-DD '-' #D-YY INTO #SYSTEM-DATE LEAVING NO
*
IF #SEARCH
  SET KEY PF3
  MOVE 'Exit' TO #EXIT-TEXT
  MOVE (AD=NP) TO #CGA-SCR-COMMAND-CV #CGA-SCR-ID-CV #CGA-SCR-SUFFIX-CV
  FIND CONT-V WITH CONTACT-ID = #CONTACT-ID
    IF ID-NUMBER > 0
      MOVE 'CN' TO #CN-OR-CI-TEXT
      MOVE ID-NUMBER TO #CN-OR-CI
      MOVE 'CI' TO #CI-TEXT
      MOVE CONTACT-ID TO #CI
    ELSE
      MOVE 'CI' TO #CN-OR-CI-TEXT
      MOVE CONTACT-ID TO #CN-OR-CI
    END-IF
  END-FIND
ELSE
  FIND CONT-V WITH ID-NUMBER = #CGA-SCR-ID
    MOVE 'CI' TO #CN-OR-CI-TEXT
    MOVE CONTACT-ID TO #CN-OR-CI #CONTACT-ID
  END-FIND
END-IF
*
IF F-CONTACT-INFO(<#CONTACT-ID,F-CONTACT-DATA>)
  IF #CONT-SSN > 0
    MOVE #CONT-SSN TO #SSN
  END-IF
  IF #CONT-DOB > 0
    MOVE #CONT-DOB TO #DATE
    COMPRESS #D-MM '/' #D-DD '/' #D-YY INTO #DOB LEAVING NO
  END-IF
  IF #CONT-DOD > 0
    MOVE #CONT-DOD TO #DATE
    COMPRESS #D-MM '/' #D-DD '/' #D-YY INTO #DOD LEAVING NO
  END-IF
  MOVE #CONT-FMTD-NAME TO #NAME
END-IF
*
PERFORM GET-PHONE-INFO
PERFORM GET-EMAIL-INFO
PERFORM GET-ADDRESS-INFO
*
MOVE 1 TO #J
MOVE 18 TO #K
MOVE '*' TO #CGA-SCR-COMMAND
MOVE 'Press PF4 to add or update phone/email/address information.' TO #MSG
REPEAT
  INPUT WITH TEXT #MSG USING MAP 'C2203MCG'
  RESET #MSG
*
  IF #SEARCH AND *PF-KEY = 'PF3'
    ESCAPE ROUTINE
  END-IF
*
  IF NOT #SEARCH AND #CGA-SCR-COMMAND NE '*'
    FETCH 'G1000PXX'
  END-IF
*
  IF *PF-KEY = 'PF4'
    MOVE 'U' TO #MODE
    IF *CURS-LINE > 3
      COMPUTE #CURS-LINE = *CURS-LINE - 3
      DECIDE ON FIRST VALUE OF #LINE-CONTACT-TYPE(#CURS-LINE)
        VALUE 'PHONE'
          REINPUT 'This function is not yet available.'
          CALLNAT 'GPHONSXX' #LINE-CI(#CURS-LINE) PHONE-V.PHONE-ID #CGA-CLERK #MODE #MSG
        VALUE 'EMAIL'
          CALLNAT 'GEMALSXX' #LINE-CI(#CURS-LINE) EMAIL-V.EMAIL-ID #CGA-CLERK #MODE #MSG
        VALUE 'ADDRS'
          CALLNAT 'ADDRN002' #LINE-CI(#CURS-LINE) ADDR-REL-V.ADDRESS-POOL-ID
        NONE
          IGNORE
      END-DECIDE
    ELSE
      PERFORM ADD-CONTACT-INFO
    END-IF
    RESET #I #MODE #LINE(*) #LINE-CI(*) #LINE-CONTACT-TYPE(*) #PHONE-DTL #EMAIL-DTL
      #ADDRESS-DTL-1 #ADDRESS-DTL-2 #LINE-CV(*)
    MOVE 18 TO #START
    PERFORM GET-PHONE-INFO
    PERFORM GET-EMAIL-INFO
    PERFORM GET-ADDRESS-INFO
  END-IF
*
  IF *PF-KEY = 'PF12'
    CALLNAT 'GPF12SXX' #CONTACT-ID
    ESCAPE TOP
  END-IF
*
  IF #LINE (#K+1) = ' '
    MOVE 1 TO #J
    MOVE 18 TO #K
  ELSE
    ADD 18 TO #J
    ADD 18 TO #K
  END-IF
END-REPEAT
*
********************************
DEFINE SUBROUTINE GET-PHONE-INFO
********************************
FIND PHONE-V WITH CONTACT-ID = #CONTACT-ID
  AT END OF DATA
    MOVE 18 TO #START
  END-ENDDATA
  IF #START = 18
    MOVE 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #PHONE-TEXT TO #LINE(#I)
    ADD 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #PHONE-TEXT-2 TO #LINE(#I)
  END-IF
  ADD 1 TO #START
  ADD 1 TO #I
  DECIDE ON FIRST VALUE OF PREFERRED-PHONE-IND
    VALUE 'P'
      MOVE 'Prim' TO #PHONE-PREF
    VALUE 'S'
      MOVE 'Sec' TO #PHONE-PREF
    NONE
      IGNORE
  END-DECIDE
  IF TYPE = 'D'
    MOVE 'USA' TO #PHONE-TYPE
  ELSE
    MOVE 'Intl' TO #PHONE-TYPE
  END-IF
  DECIDE ON FIRST VALUE OF PHONE-TYPE-CODE
    VALUE 'H'
      MOVE 'Home' TO #PHONE-TYPE-CODE
    VALUE 'B'
      MOVE 'Work' TO #PHONE-TYPE-CODE
    VALUE 'C'
      MOVE 'Cell' TO #PHONE-TYPE-CODE
    VALUE 'O'
      MOVE 'Oth' TO #PHONE-TYPE-CODE
    VALUE 'F'
      MOVE 'HFax' TO #PHONE-TYPE-CODE
    VALUE 'A'
      MOVE 'WFax' TO #PHONE-TYPE-CODE
    VALUE 'X'
      MOVE 'OFax' TO #PHONE-TYPE-CODE
    NONE
      IGNORE
  END-DECIDE
  IF INTL-ACCESS-CODE > 0
    MOVE INTL-ACCESS-CODE TO #PHONE-ACC
  END-IF
  IF AREA-CODE > 0
    MOVE AREA-CODE TO #PHONE-AREA
  END-IF
  IF DIAL-NUMBER > 0
    COMPRESS FULL #DIAL-3 '-' #DIAL-4 INTO #PHONE-NUMBER LEAVING NO
  END-IF
  IF EXT > 0
    MOVE EXT TO #PHONE-EXT
  END-IF
  DECIDE ON FIRST VALUE OF BEST-TIME-TO-CALL
    VALUE 'AM'
      MOVE 'Morning' TO #PHONE-BEST-TIME
    VALUE 'PM'
      MOVE 'Afternoon' TO #PHONE-BEST-TIME
    VALUE 'NT'
      MOVE 'Night' TO #PHONE-BEST-TIME
    NONE
      IGNORE
  END-DECIDE
  MOVE INVALID-IND TO #PHONE-INVALID
  IF START-DATE > 0 AND START-DATE NE 99999999
    COMPRESS #PSD-DD '/' #PSD-MM '/' #PSD-YY INTO #PHONE-START
      LEAVING NO
  END-IF
  IF END-DATE > 0 AND END-DATE NE 99999999
    COMPRESS #PED-DD '/' #PED-MM '/' #PED-YY INTO #PHONE-START
      LEAVING NO
  END-IF
  MOVE #PHONE-DTL TO #LINE(#I)
  MOVE CONTACT-ID TO #LINE-CI(#I)
  MOVE 'PHONE' TO #LINE-CONTACT-TYPE(#I)
END-FIND
*
END-SUBROUTINE
********************************
DEFINE SUBROUTINE GET-EMAIL-INFO
********************************
FIND EMAIL-V WITH CONTACT-ID = #CONTACT-ID
  AT END OF DATA
    MOVE 18 TO #START
  END-ENDDATA
  IF #START = 18
    MOVE 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #EMAIL-TEXT TO #LINE(#I)
    ADD 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #EMAIL-TEXT-2 TO #LINE(#I)
  END-IF
  ADD 1 TO #START
  ADD 1 TO #I
  DECIDE ON FIRST VALUE OF EMAIL-PREFERRED-IND
    VALUE 'P'
      MOVE 'Prim' TO #EMAIL-PREF
    VALUE 'S'
      MOVE 'Sec' TO #EMAIL-PREF
    VALUE 'O'
      MOVE 'Oth' TO #EMAIL-PREF
    NONE
      RESET #EMAIL-PREF
  END-DECIDE
  DECIDE ON FIRST VALUE OF EMAIL-TYPE
    VALUE 'P'
      MOVE 'Pers' TO #EMAIL-TYPE-CODE
    VALUE 'B'
      MOVE 'Work' TO #EMAIL-TYPE-CODE
    VALUE 'O'
      MOVE 'Oth' TO #EMAIL-TYPE-CODE
    NONE
      RESET #EMAIL-TYPE-CODE
  END-DECIDE
  MOVE EMAIL-ADDRESS TO #EMAIL-ADDRESS
  MOVE EMAIL-PROTECTION-IND TO #EMAIL-PROTECTED
  IF EMAIL-UNDELIV = 'Y'
    MOVE 'N' TO #EMAIL-DELIVERABLE
  ELSE
    MOVE 'Y' TO #EMAIL-DELIVERABLE
  END-IF
  RESET #EMAIL-START #EMAIL-END
  IF START-DATE > 0 AND START-DATE NE 99999999
    COMPRESS #ESD-MM '/' #ESD-DD '/' #ESD-YY INTO #EMAIL-START
      LEAVING NO
  END-IF
  IF END-DATE > 0 AND END-DATE NE 99999999
    COMPRESS #EED-MM '/' #EED-DD '/' #EED-YY INTO #EMAIL-END
      LEAVING NO
  END-IF
  MOVE #EMAIL-DTL TO #LINE(#I)
  MOVE CONTACT-ID TO #LINE-CI(#I)
  MOVE 'EMAIL' TO #LINE-CONTACT-TYPE(#I)
END-FIND
*
END-SUBROUTINE
**********************************
DEFINE SUBROUTINE GET-ADDRESS-INFO
**********************************
FIND ADDR-REL-V WITH CONTACT-ID = #CONTACT-ID
  AT END OF DATA
    MOVE 18 TO #START
  END-ENDDATA
*
  IF #START = 18
    MOVE 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #ADDRESS-TEXT-1 TO #LINE(#I)
    ADD 1 TO #START
    ADD 1 TO #I
    MOVE (AD=I) TO #LINE-CV(#I)
    MOVE #ADDRESS-TEXT-2 TO #LINE(#I)
  END-IF
*
  ADD 1 TO #START
  ADD 1 TO #I
  DECIDE ON FIRST VALUE OF ADDR-TYPE-CD
    VALUE 'P'
      MOVE 'Prim' TO #ADDRESS-PREF
    VALUE 'S'
      MOVE 'Sec' TO #ADDRESS-PREF
    VALUE 'B'
      MOVE 'Bill' TO #ADDRESS-PREF
    VALUE 'L'
      MOVE 'Loan' TO #ADDRESS-PREF
    NONE
      IGNORE
  END-DECIDE
*
  DECIDE ON FIRST VALUE OF ADDR-LOCATION
    VALUE 'H'
      MOVE 'Home' TO #ADDRESS-TYPE
    VALUE 'B'
      MOVE 'Work' TO #ADDRESS-TYPE
    NONE
      IGNORE
  END-DECIDE
*
  IF ADDR-FORMAT-TYPE-CD  = 'D'
    MOVE 'USA' TO #ADDRESS-TYPE
  ELSE
    MOVE 'Intl' TO #ADDRESS-TYPE
  END-IF
*
  IF ADDR-VALID-IND = ' '
    MOVE '?' TO #ADDRESS-VAL
  ELSE
    MOVE ADDR-VALID-IND TO #ADDRESS-VAL
  END-IF
*
  FIND ADDR-P-V WITH ADDRESS-POOL-ID = ADDR-REL-V.ADDRESS-POOL-ID
    IF NO RECORD FOUND
      MOVE '?' TO #ADDRESS-OVR
      MOVE '?' TO #ADDRESS-RET
      ESCAPE BOTTOM
    END-NOREC
    MOVE ATTENTION-LINE TO #ADDRESS-ATTN
    IF PREVENT-OVER-IND = ' '
      MOVE '?' TO #ADDRESS-OVR
    ELSE
      MOVE PREVENT-OVER-IND TO #ADDRESS-OVR
    END-IF
    IF RETURNED-MAIL-IND = ' '
      MOVE '?' TO #ADDRESS-RET
    ELSE
      MOVE RETURNED-MAIL-IND TO #ADDRESS-RET
    END-IF
  END-FIND
*
  MOVE #CONTACT-ID TO #ADD-CONTACT-ID
  CALLNAT 'ADDN0001' #PDA-ADDRESS
  MOVE #ADD-LINE-1 TO #ADDRESS-FULL
*  write '='#ADD-CONTACT-ID '='ADDR-TYPE-CD '='ADDR-FORMAT-TYPE-CD '='#ADD-LINE-1 '='#ADD-LINE-2
  IF #ADD-LINE-2 > ' '
    COMPRESS #ADD-LINE-1 ',' INTO #ADDRESS-FULL LEAVING NO
    COMPRESS #ADDRESS-FULL #ADD-LINE-2 INTO #ADDRESS-FULL
  END-IF
  IF #ADD-LINE-3 > ' '
    COMPRESS #ADDRESS-FULL ',' INTO #ADDRESS-FULL LEAVING NO
    COMPRESS #ADDRESS-FULL #ADD-LINE-3 INTO #ADDRESS-FULL
  END-IF
*
  MOVE #ADDRESS-DTL-1  TO #LINE(#I)
  MOVE #CONTACT-ID TO #LINE-CI(#I)
  MOVE 'ADDRS' TO #LINE-CONTACT-TYPE(#I)
  ADD 1 TO #START
  ADD 1 TO #I
  MOVE #ADDRESS-DTL-2  TO #LINE(#I)
  MOVE #CONTACT-ID TO #LINE-CI(#I)
  MOVE 'ADDRS' TO #LINE-CONTACT-TYPE(#I)
END-FIND
**write '='#line(1:10) '='#line-ci(1:10)
*
END-SUBROUTINE
*
**********************************
DEFINE SUBROUTINE ADD-CONTACT-INFO
**********************************
RESET #SELECTION
SET WINDOW 'Maintain'
INPUT WINDOW='Maintain' USING MAP 'C2204MCG'
*
IF *PF-KEY = 'PF3'
  SET WINDOW OFF
  ESCAPE ROUTINE
END-IF
*
IF #SELECTION = ' '
  REINPUT 'Pls. enter selection'
    MARK 1
END-IF
*
IF NOT (#SELECTION = '1' OR = '2' OR = '3')
  REINPUT 'Invalid selection'
    MARK 1
END-IF
*
IF #SELECTION = '1'
  REINPUT 'This function is not yet available'
  CALLNAT 'GPHONSXX' #CONTACT-ID #PHONE-ID-PARM #CGA-CLERK #MODE #MSG
END-IF
*
IF #SELECTION = '2'
  CALLNAT 'GEMALSXX' #CONTACT-ID #EMAIL-ID-PARM #CGA-CLERK #MODE #MSG
END-IF
*
IF #SELECTION = '3'
  CALLNAT 'ADDRN002' #CONTACT-ID ADDR-REL-V.ADDRESS-POOL-ID
END-IF
*
END-SUBROUTINE
*
END
