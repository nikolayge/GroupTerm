* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: AOAP000B - TAS SUPERVISOR - MEMBERS CLONE OF AOAP000A
*                                              BUT WITHOUT SORT
*   THIS PROGRAM READS THE TRIGGER FILE CREATED BY AOAP001A
*     AND FETCHES THE APPROPRIATE ONLINE PROGRAMS
*
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
* bz  06/09/2013  Rebranding (Search for AAFMAA)
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX       /* WORK FIELDS FOR GONERCXX
LOCAL
* * * * * * * *   T A S   E R R O R   F I L E   * * * * * * * *
1 TAS-ERROR-FILE
  2 TEF-ID-NUMBER   (N6)
  2 TEF-BAD-AD      (L)
  2 TEF-REQUEST     (L)
  2 TEF-MANUAL-REVUE(L)
  2 TEF-BAD-BA      (L)
  2 TEF-NUM-PAGES   (N2)
* * * * * * * *   T R I G G E R   F I L E   * * * * * * * * * *
1 TRIGGER-FILE
  2 TF-ID-NUMBER   (N6)
  2 TF-ISN         (P8)
  2 TF-ZIP-CODE    (P9)
  2 TF-AGE-62-FLAG (A1)
*
1 #ADDRESS-ERROR-CNT (P3)
1 #FOREIGN-ADDR-CNT  (P3)
1 #FMR-SPS-CNT       (P5)
1 #RSV-RP-CNT        (P5)
1 #TRIGGER-CNT       (P5)
1 #CNT-TOT-BAS       (P5)
1 #CNT-TOT-1PG-BAS   (P5)
1 #CNT-TOT-2PG-BAS   (P5)
1 #CNT-TOT-3PG-BAS   (P5)
1 #CNT-TOT-4PG-BAS   (P5)
1 #CNT-TOT-5PG-BAS   (P5)
1 #CNT-TOT-6PG-BAS   (P5)
1 #CNT-TOT-7PG-BAS   (P5)
1 #CNT-TOT-8PG-BAS   (P5)
1 #CNT-TOT-9PG-BAS   (P5)
1 #CNT-TOT-LOTPG-BAS (P5)
1 #TOT-METERED       (P5)
1 #TOT-METERED-9-DIG (P5)
1 #TOT-BULK          (P5)
1 #TOT-BULK-9-DIG    (P5)
1 #PCT               (P3.4)
1 #ZIP-CODE          (N9)
1 REDEFINE #ZIP-CODE
 2 #ZIP-CODE-5       (N5)
 2 #ZIP-CODE-4       (N4)
*
1 #SECURITY-CODE      (N2)
1 REDEFINE #SECURITY-CODE
 2 #SEC-CD-1          (N1)
 2 #SEC-CD-2          (N1)
*
1 #NUM-SBP            (P5)
1 #NUM-ACTIVE         (P5)
1 #NUM-RET-PRIOR-TO-1957 (P5)
1 #NUM-TTS            (P5)
1 #NUM-SSO            (P5)
1 #NUM-40P            (P5)
1 #NUM-UNK            (P5)
1 #NUM-DEC            (P5)
1 #NUM-NOT-ELIG       (P5)
1 #NUM-NEITHER        (P5)
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
INCLUDE GONERCXX        /* NATURAL ERROR ROUTINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
FORMAT (2) LS=133 PS=66
WRITE (2) TITLE LEFT 'PROGRAM:' *PROGRAM
                  5X 'DATE:' *DAT4U 5X 'TIME:' *TIMX
*
FORMAT (3) LS=80 PS=66
WRITE (3) TITLE
  *PROGRAM 5X 'AAFMAA' 5X *DATU
 / 'MEMBER MAILOUTS WITH SPECIAL CONDITIONS' /
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
READ WORK 1 RECORD TRIGGER-FILE
  ADD +1 TO #TRIGGER-CNT
  WRITE (2) 'AOAP000B ' TF-ID-NUMBER
* END-ALL
*
* SORT BY TF-ID-NUMBER ASCENDING
* SORT BY TF-ZIP-CODE ASCENDING
*     USING TF-ISN TF-AGE-62-FLAG TF-ZIP-CODE
*  USING TF-ISN TF-ID-NUMBER  TF-AGE-62-FLAG
  MOVE TF-ISN       TO #CGA-ISN
  MOVE TF-ID-NUMBER TO #CGA-SCR-ID
  MOVE TF-ID-NUMBER TO #CGA-ID
  MOVE TF-ID-NUMBER TO #CGA-ORIG-ID
  PERFORM FETCH-PROGRAMS
* END-SORT /* (0095)
END-WORK
*
WRITE (3) 'TOTAL BA"S PROCESSED         = ' #CNT-TOT-BAS
WRITE (3) 'TOTAL FORMER SPOUSE MAILOUTS = ' #FMR-SPS-CNT
WRITE (3) 'TOTAL RESV "RP" SCNS PRINTED = ' #RSV-RP-CNT
WRITE (3) 'TOTAL BAD ADDRS (NOT PRTED)  = ' #ADDRESS-ERROR-CNT
WRITE (3) 'TOTAL FOREIGN ADDRS          = ' #FOREIGN-ADDR-CNT
*
COMPUTE ROUNDED #PCT = (#TOT-METERED-9-DIG / #TOT-METERED) * 100
WRITE (3) ' '
WRITE (3) 'TOTAL METERED           = ' #TOT-METERED
WRITE (3) 'TOTAL 9-DIGIT METERED   = ' #TOT-METERED-9-DIG
WRITE (3) 'PERCENT 9-DIGIT METERED = ' #PCT '%'
COMPUTE ROUNDED #PCT = (#TOT-BULK-9-DIG / #TOT-BULK) * 100
WRITE (3) ' '
WRITE (3) 'TOTAL BULK              = ' #TOT-BULK
WRITE (3) 'TOTAL 9-DIGIT BULK      = ' #TOT-BULK-9-DIG
WRITE (3) 'PERCENT 9-DIGIT BULK    = ' #PCT '%'
*
WRITE (2) 'TOTAL BA"S PROCESSED         = ' #CNT-TOT-BAS
WRITE (2) 'TOTAL FORMER SPOUSE MAILOUTS = ' #FMR-SPS-CNT
WRITE (2) 'TOTAL RESV "RP" SCNS PRINTED = ' #RSV-RP-CNT
WRITE (2) 'TOTAL BAD ADDRS (NOT PRTED)  = ' #ADDRESS-ERROR-CNT
WRITE (2) 'TOTAL FOREIGN ADDRS          = ' #FOREIGN-ADDR-CNT
*
COMPUTE ROUNDED #PCT = (#TOT-METERED-9-DIG / #TOT-METERED) * 100
WRITE (2) ' '
WRITE (2) 'TOTAL METERED           = ' #TOT-METERED
WRITE (2) 'TOTAL 9-DIGIT METERED   = ' #TOT-METERED-9-DIG
WRITE (2) 'PERCENT 9-DIGIT METERED = ' #PCT '%'
*
COMPUTE ROUNDED #PCT = (#TOT-BULK-9-DIG / #TOT-BULK) * 100
WRITE (2) ' '
WRITE (2) 'TOTAL BULK              = ' #TOT-BULK
WRITE (2) 'TOTAL 9-DIGIT BULK      = ' #TOT-BULK-9-DIG
WRITE (2) 'PERCENT 9-DIGIT BULK    = ' #PCT '%'
*
* - - - - - - -    END OF MEMBER PROCESSING   - - - - - - - - - - - - *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE FETCH-PROGRAMS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
MOVE 'AD'  TO #CGA-SCR-COMMAND
MOVE 'AD'  TO #CGA-ORIG-COMMAND
MOVE 'VS'  TO #CGA-SCR-SUFFIX
*
RESET TEF-REQUEST                 /* INITIALIZE MAILOUT REQUEST IND
IF TF-AGE-62-FLAG = 'Y'
  MOVE 62 TO #CGA-SECURITY-CODE
ELSE
  IF TF-AGE-62-FLAG = 'F' OR = 'D' OR = 'B' OR = 'Z' /* MAILOUT REQUEST
    MOVE 75 TO #CGA-SECURITY-CODE    /* FOR "AD" TO PRINT REQUEST LINGO
    MOVE TRUE TO TEF-REQUEST         /* TO SEPARATE REQUESTS
  ELSE
    RESET #CGA-SECURITY-CODE
  END-IF
END-IF
*
FETCH RETURN 'G3500PAD'     /* AD PROCESSOR
*
IF #CGA-SECURITY-CODE = 99
  WRITE (3) 15T 'CN' TF-ID-NUMBER (EM=999999)
                       ' BAD ADDRESS - NOT PRINTED'
  WRITE (2) 15T 'CN' TF-ID-NUMBER (EM=999999)
                       ' BAD ADDRESS - NOT PRINTED'
  ADD 1 TO #ADDRESS-ERROR-CNT
  ESCAPE ROUTINE
END-IF
*
IF #CGA-GOOD-TAS
  MOVE FALSE TO TEF-BAD-AD
  MOVE TF-ZIP-CODE TO #ZIP-CODE
*                            ******* ACCUM 9-DIGIT PERCENTAGE
  IF TF-AGE-62-FLAG = 'F' OR = 'D' OR = 'B' OR = 'Z' /* MAILOUT REQUEST
   OR (TEF-NUM-PAGES GT 5)      /* 6 OR GREATER PAGERS
    ADD 1 TO #TOT-METERED
    IF #ZIP-CODE-4 GT 0
      ADD 1 TO #TOT-METERED-9-DIG
    END-IF
  ELSE
    ADD 1 TO #TOT-BULK
    IF #ZIP-CODE-4 GT 0
      ADD 1 TO #TOT-BULK-9-DIG
    END-IF
  END-IF
ELSE
  MOVE TRUE TO TEF-BAD-AD
  WRITE (3) 15T 'CN' TF-ID-NUMBER (EM=999999)
                       ' FOREIGN ADDRESS - PRINTED SEPARATELY'
  WRITE (2) 15T 'CN' TF-ID-NUMBER (EM=999999)
                       ' FOREIGN ADDRESS - PRINTED SEPARATELY'
  ADD 1 TO #FOREIGN-ADDR-CNT
END-IF
*
IF TF-AGE-62-FLAG = 'Y'     /* ONLY PRINT LETTER FROM G3500PAD
  ADD 1 TO #CNT-TOT-BAS
  ADD 1 TO #CNT-TOT-1PG-BAS
  MOVE #CGA-ID TO TEF-ID-NUMBER
  MOVE FALSE TO TEF-MANUAL-REVUE
  MOVE FALSE TO TEF-BAD-BA
  MOVE #CGA-SECURITY-CODE TO TEF-NUM-PAGES
  WRITE WORK 3 TAS-ERROR-FILE
  ESCAPE ROUTINE
END-IF
*
RESET #CGA-SCR-SUFFIX
RESET #CGA-ORIG-SUFFIX
MOVE FALSE TO TEF-MANUAL-REVUE
*
IF TF-AGE-62-FLAG = ' ' OR = 'F' OR = 'D' OR = 'Z' /* B-DAY/FIR/DI RQST
  MOVE 'DI'  TO #CGA-SCR-COMMAND
  FETCH RETURN 'E4000PDI'     /* DI PROCESSOR
*
  IF #CGA-GOOD-TAS-VS
    MOVE FALSE TO TEF-MANUAL-REVUE
  ELSE
    MOVE TRUE TO TEF-MANUAL-REVUE
  END-IF
END-IF
*
IF TF-AGE-62-FLAG = ' ' OR = 'F' OR = 'Z'  /* B-DAY/FIR  RQST
  MOVE 'GT'  TO #CGA-SCR-COMMAND
  MOVE 'GT'  TO #CGA-ORIG-COMMAND
  FETCH RETURN 'G4720PGT'     /* GT PROCESSOR
*
  MOVE 'SI'  TO #CGA-SCR-COMMAND
  MOVE 'SI'  TO #CGA-ORIG-COMMAND
  FETCH RETURN 'D3000PXX'     /* SI PROCESSOR
END-IF
*
IF TF-AGE-62-FLAG = ' ' OR = 'F' OR = 'B' OR = 'Z'  /* B-DAY/FIR/BA RQST
  MOVE 'BA'  TO #CGA-SCR-COMMAND
  MOVE 'BA'  TO #CGA-ORIG-COMMAND
  FETCH RETURN 'D3000PXX'     /* BA PROCESSOR
END-IF
*
**************                          /* SBP PROCESSING ADDED 2/92
ADD 1 TO #NUM-SBP
*
IF #CGA-MF-ORIG-PARM-1 = 'ACTIVE'
  ADD 1 TO #NUM-ACTIVE
ELSE
  IF #CGA-MF-ORIG-PARM-1 = '   (55/55'
    ADD 1 TO #NUM-RET-PRIOR-TO-1957
  ELSE
    IF #CGA-MF-ORIG-PARM-1 = MASK ('TTS')
      ADD 1 TO #NUM-TTS
    ELSE
      IF #CGA-MF-ORIG-PARM-1 = MASK ('SSO')
        ADD 1 TO #NUM-SSO
      ELSE
        IF #CGA-MF-ORIG-PARM-1 = MASK ('40%')
          ADD 1 TO #NUM-40P
        ELSE
          IF #CGA-MF-ORIG-PARM-1 = 'DECLINED'
            ADD 1 TO #NUM-DEC
          ELSE
            IF #CGA-MF-ORIG-PARM-1 = 'UNKNOWN'
              ADD 1 TO #NUM-UNK
            ELSE
              IF #CGA-MF-ORIG-PARM-1 = 'NOT ELIG'
                ADD 1 TO #NUM-NOT-ELIG
              ELSE
                ADD 1 TO #NUM-NEITHER
              END-IF
            END-IF
          END-IF
        END-IF
      END-IF
    END-IF
  END-IF
END-IF
RESET #CGA-MF-ORIG-PARM-1
*
IF #CGA-GOOD-TAS
  MOVE FALSE TO TEF-BAD-BA
ELSE
  MOVE TRUE TO TEF-BAD-BA
END-IF
*
MOVE #CGA-SECURITY-CODE TO #SECURITY-CODE
IF #SEC-CD-1 = 2 OR = 3 OR = 6 OR = 7  /* USED IN BA TO IND RESERVE "RP"
  SUBTRACT 20 FROM #CGA-SECURITY-CODE   /* BACK OUT 20 FOR "RP"
  WRITE (3) 15T 'CN' TF-ID-NUMBER(EM=999999)
                  ' RESERVE "RP" SCREEN PRINTED'
  WRITE (2) 15T 'CN' TF-ID-NUMBER(EM=999999)
                  ' RESERVE "RP" SCREEN PRINTED'
  ADD 1 TO #RSV-RP-CNT
END-IF
*
IF #SEC-CD-1 = 4 OR = 5 OR = 6 OR = 7   /* USED IN BA TO IND FMR SPOUSE
  SUBTRACT 40 FROM #CGA-SECURITY-CODE   /* BACK OUT 40 FOR FMR SPS
  WRITE (3) 15T 'CN' TF-ID-NUMBER(EM=999999)
                  ' MEMBER HAS FORMER SPOUSE OR NON-RES DEPENDANTS'
  WRITE (2) 15T 'CN' TF-ID-NUMBER(EM=999999)
                  ' MEMBER HAS FORMER SPOUSE OR NON-RES DEPENDANTS'
  ADD 1 TO #FMR-SPS-CNT
END-IF
*
ADD 1 TO #CNT-TOT-BAS
*
DECIDE ON FIRST VALUE OF #CGA-SECURITY-CODE
  VALUE 01 ADD 1 TO #CNT-TOT-1PG-BAS
  VALUE 02 ADD 1 TO #CNT-TOT-2PG-BAS
  VALUE 03 ADD 1 TO #CNT-TOT-3PG-BAS
  VALUE 04 ADD 1 TO #CNT-TOT-4PG-BAS
  VALUE 05 ADD 1 TO #CNT-TOT-5PG-BAS
  VALUE 06 ADD 1 TO #CNT-TOT-6PG-BAS
  VALUE 07 ADD 1 TO #CNT-TOT-7PG-BAS
  VALUE 08 ADD 1 TO #CNT-TOT-8PG-BAS
  VALUE 09 ADD 1 TO #CNT-TOT-9PG-BAS
  NONE     ADD 1 TO #CNT-TOT-LOTPG-BAS
END-DECIDE
*
MOVE #CGA-ID TO TEF-ID-NUMBER
* MOVE #CGA-SECURITY-CODE TO TEF-NUM-PAGES
MOVE 3 TO TEF-NUM-PAGES
WRITE WORK 3 TAS-ERROR-FILE
*
END-SUBROUTINE  /* (0324)
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
WRITE (2) 'TOTAL BA"S              = ' #NUM-SBP
WRITE (2) 'TOTAL ACTIVE            = ' #NUM-ACTIVE
WRITE (2) 'TOTAL RET-PRIOR TO 1957 = ' #NUM-RET-PRIOR-TO-1957
WRITE (2) 'TOTAL TTS               = ' #NUM-TTS
WRITE (2) 'TOTAL SSO               = ' #NUM-SSO
WRITE (2) 'TOTAL 40P               = ' #NUM-40P
WRITE (2) 'TOTAL SBP DECLINED      = ' #NUM-DEC
WRITE (2) 'TOTAL SBP OPT UNKNOWN   = ' #NUM-UNK
WRITE (2) 'TOTAL NOT ELIG FOR RET  = ' #NUM-NOT-ELIG
WRITE (2) 'TOTAL NEITHER           = ' #NUM-NEITHER
*
WRITE (3) 'TOTAL BA"S              = ' #NUM-SBP
WRITE (3) 'TOTAL ACTIVE            = ' #NUM-ACTIVE
WRITE (3) 'TOTAL RET-PRIOR TO 1957 = ' #NUM-RET-PRIOR-TO-1957
WRITE (3) 'TOTAL TTS               = ' #NUM-TTS
WRITE (3) 'TOTAL SSO               = ' #NUM-SSO
WRITE (3) 'TOTAL 40P               = ' #NUM-40P
WRITE (3) 'TOTAL SBP DECLINED      = ' #NUM-DEC
WRITE (3) 'TOTAL SBP OPT UNKNOWN   = ' #NUM-UNK
WRITE (3) 'TOTAL NOT ELIG FOR RET  = ' #NUM-NOT-ELIG
WRITE (3) 'TOTAL NEITHER           = ' #NUM-NEITHER
*
WRITE (2) / 5T *PROGRAM 'EOJ' 5X #TRIGGER-CNT 'ACCTS PROCESSED'
WRITE (3) / 5T *PROGRAM 'EOJ' 5X #TRIGGER-CNT 'ACCTS PROCESSED'
WRITE (2) / 'TOTAL 1   PAGE BAS = ' #CNT-TOT-1PG-BAS
WRITE (2) / 'TOTAL 2   PAGE BAS = ' #CNT-TOT-2PG-BAS
WRITE (2) / 'TOTAL 3   PAGE BAS = ' #CNT-TOT-3PG-BAS
WRITE (2) / 'TOTAL 4   PAGE BAS = ' #CNT-TOT-4PG-BAS
WRITE (2) / 'TOTAL 5   PAGE BAS = ' #CNT-TOT-5PG-BAS
WRITE (2) / 'TOTAL 6   PAGE BAS = ' #CNT-TOT-6PG-BAS
WRITE (2) / 'TOTAL 7   PAGE BAS = ' #CNT-TOT-7PG-BAS
WRITE (2) / 'TOTAL 8   PAGE BAS = ' #CNT-TOT-8PG-BAS
WRITE (2) / 'TOTAL 9   PAGE BAS = ' #CNT-TOT-9PG-BAS
WRITE (2) / 'TOTAL LOT PAGE BAS = ' #CNT-TOT-LOTPG-BAS
WRITE (3) / 'TOTAL 1   PAGE BAS = ' #CNT-TOT-1PG-BAS
WRITE (3) / 'TOTAL 2   PAGE BAS = ' #CNT-TOT-2PG-BAS
WRITE (3) / 'TOTAL 3   PAGE BAS = ' #CNT-TOT-3PG-BAS
WRITE (3) / 'TOTAL 4   PAGE BAS = ' #CNT-TOT-4PG-BAS
WRITE (3) / 'TOTAL 5   PAGE BAS = ' #CNT-TOT-5PG-BAS
WRITE (3) / 'TOTAL 6   PAGE BAS = ' #CNT-TOT-6PG-BAS
WRITE (3) / 'TOTAL 7   PAGE BAS = ' #CNT-TOT-7PG-BAS
WRITE (3) / 'TOTAL 8   PAGE BAS = ' #CNT-TOT-8PG-BAS
WRITE (3) / 'TOTAL 9   PAGE BAS = ' #CNT-TOT-9PG-BAS
WRITE (3) / 'TOTAL LOT PAGE BAS = ' #CNT-TOT-LOTPG-BAS
*
END
