* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram POSN0003.
/**
/** :author nguentchev
/*  Payer relation
DEFINE DATA
PARAMETER
1 #ISN     (P8)
1 #MSG     (A) DYNAMIC
LOCAL USING POSALOAD
LOCAL
1 #GET            (I4) CONST <0>
1 #ADD            (I4) CONST <1>
1 #UPDATE         (I4) CONST <2>
1 #DELETE         (I4) CONST <3>
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 MEMBER-CONTACT-ID
  2 DATE-OF-DEATH
  2 C*ACTIVE-SEGMENTS  (N3)
  2 STATUS  (9)
  2 MODE    (9)
1 CNT-V VIEW OF A-CONTACTS
  2 CONTACT-ID
1 NT-V VIEW OF A-NOTES
  2 C*NOTE-TABLE  (N3)
  2 NOTE-TEXT              (A60/60)
  2 REDEFINE NOTE-TEXT
    3 NOTE-TEXT-OCC         (60)
      4 ALOT-PAID-BY         (A17)
      4 ALOT-PAID-BY-CN      (N06)
      4 ALOT-PAID-BY-FL      (A37)
1 I (I2)
1 K (I2)
1 #MESSAGE-LOG   (I4) CONST <2>
1 #ERROR-LOG     (I4) CONST <3>
END-DEFINE
*
GET ST-V #ISN
IF ST-V.DATE-OF-DEATH > 0
  COMPRESS 'Customer is dead. CN' ST-V.ID-NUMBER INTO #MSG
  CALLNAT 'POSLOG' #MESSAGE-LOG #MSG
  ESCAPE ROUTINE
END-IF
*
FOR I 1 TO C*ACTIVE-SEGMENTS
  IF STATUS(I) = 'D'  /* ACTIVE
    IF ST-V.MODE (I) = 'G'
      FIND (1) NT-V WITH ID-NUMBER = ST-V.ID-NUMBER
        FOR K 1 TO C*NOTE-TABLE
          IF NOTE-TEXT (K) = MASK ('*#ALOT:PAID BY CN')
            CALLNAT 'GPLNMSXX' #POWN.POLICY-NUMBER ST-V.ID-NUMBER I
            MOVE ALOT-PAID-BY-CN (K) TO #POWN.REFERENCE-NUMBER
            #POWN.RELATION    := 8    /* 8 = PAYER
            #POWN.REFERENCE-TYPE := 'P'
            FIND (1) CNT-V WITH ID-NUMBER =  #POWN.REFERENCE-NUMBER
              #POWN.CONTACT-ID  := CNT-V.CONTACT-ID
            END-FIND
/*
            #POWN.ADD-DATE := GET-ISSUE-DATE
              (< #POWN.POLICY-NUMBER, ST-V.ID-NUMBER >)
            MOVE 'LOD' TO #POWN.LAST-UPD-USER #POWN.ADD-USER
            #POWN.LAST-UPD-DATE := #POWN.ADD-DATE
/*
            CALLNAT 'POSN0001' #ADD #MSG #POWN
            ESCAPE BOTTOM
          END-IF
        END-FOR
      END-FIND
    END-IF
  END-IF
END-FOR
*
ON ERROR
  COMPRESS 'ON ERROR STATUS. CN=' ST-V.ID-NUMBER INTO #MSG
  CALLNAT 'POSLOG' #ERROR-LOG #MSG
  ESCAPE ROUTINE
END-ERROR
END
