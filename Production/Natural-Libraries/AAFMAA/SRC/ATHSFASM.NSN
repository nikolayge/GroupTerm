* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHSFASM
*    CALLED BY ATHPFAS
*    CALCULATES TOTAL FAS AND LIFE  MEDIAN FROM WORK FILE
DEFINE DATA
*
PARAMETER
*
1 #FAS-TABLE
 2 #AGE-OCCUR          (10)
  3 #OFF-ENLIST        (2)
   4 #MEMBERS          (P5)
   4 #GENERIC-USERS    (P5)
   4 #FAS-USERS        (P5)
   4 #MEDIAN-FAS-AMT   (N12)
   4 #TTL-FAS-AMT      (N12)
   4 #MEDIAN-FAS-LIFE  (N12)
   4 #TTL-FAS-LIFE     (N12)
  3 #TOTAL-MEDIAN      (N12)
  3 #TOTAL             (N12)
*
LOCAL
*
1 #MEDIAN-WORK-FILE
 2 #CN-NUMBER          (N6)
 2 #TYPE               (I2)
 2 #AGE                (I2)
 2 #TOTAL-FAS-LIFE     (N12)
 2 #TOTAL-FAS          (N12)
*
1 #MEDIAN              (L)
1 #EVEN                (L)
1 #MEDIAN-CNT          (P11)
1 #COUNTER             (P11)
1 #CNT                 (P11)
1 #REM                 (N2.4)
*
END-DEFINE
*
READ WORK FILE 1 #MEDIAN-WORK-FILE
*
  ADD +1 TO #CNT
*
END-ALL
*
SORT BY #AGE #TOTAL-FAS-LIFE
  USING #CN-NUMBER #TOTAL-FAS #TYPE
*
AT BREAK #AGE
  RESET #COUNTER #MEDIAN
END-BREAK
*
ADD +1 TO #COUNTER
*
IF NOT #MEDIAN
  RESET #REM
  DIVIDE 2 INTO (#FAS-USERS (#AGE,1) + #FAS-USERS (#AGE,2))
       GIVING #MEDIAN-CNT REMAINDER #REM
  IF #REM NE 0
    COMPUTE ROUNDED #MEDIAN-CNT = (#FAS-USERS (#AGE,1) +
                                   #FAS-USERS (#AGE,2)) / 2
  END-IF
  MOVE TRUE TO #MEDIAN
END-IF
*
IF #EVEN
  ADD #TOTAL-FAS-LIFE TO #TOTAL-MEDIAN (#AGE)
  COMPUTE ROUNDED #TOTAL-MEDIAN (#AGE) = #TOTAL-MEDIAN (#AGE) / 2
  MOVE FALSE TO #EVEN
END-IF
*
IF #COUNTER = #MEDIAN-CNT
  IF #REM NE 0
    MOVE #TOTAL-FAS-LIFE TO #TOTAL-MEDIAN (#AGE)
  ELSE
    ADD #TOTAL-FAS-LIFE  TO #TOTAL-MEDIAN (#AGE)
    MOVE TRUE TO #EVEN
  END-IF
END-IF
*
END-SORT
*
END
