* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHSTMRF - TERM INSURANCE REFUND SUBPROGRAM
* <<<<<<<<<<<<<   SORTS & PRODUCED CHECK LIST & FILE
**************************************************************************
*                       MODIFICATION LOG                                 *
* USER DATE       TAG  REASON                                            *
* PAM  04082005   PM1  Do not use zeros for zipcode on foreign entries   *
* SAG  12012005   RH1  SS - Replaced the HEX constant '05' (Tab position)*
*                      EBCDIC to HEX code '09' ASCII                     *
* SAG  10272006   DRC  Added FORMAT (1)                                  *
* SAG  10272006   AB   documented write to write (1)                     *
* PAM  20070410   PM2  Changed $1 and under to $5 and under              *
* PAM  20080318   PM3  Changed $5 and under to $7 and under              *
* PAM  04/26/2011 PM4  Fixed foreign addresses                           *
**************************************************************************
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL
*
* 1 #TAB     (A1) INIT <H'05'>     /* SAG RH1 12012005 SS
1 #TAB       (A1) INIT <H'09'>     /* SAG RH1 12012005 SS
1 #EXTRACT-FILE    /* LENGTH IS 60
 2 #EXT-FOREIGN-IND (A1)
 2 #EXT-OWNER-NAME  (A25)   /* PERSON GETTING THE CHECK (OWNER)
 2 #EXT-ID-NUMBER   (N6)
 2 #EXT-REFUND      (N5.2)
 2 #EXT-PREMS-PAID  (N7.2)
 2 #EXT-PLAN        (A2)
 2 #EXT-ISS-DATE    (N8)
 2 #EXT-DEAD-IND    (A1)
 2 #EXT-SEG         (N1)  /* segment index of policy
*
1 #RF-AMT-DISP  (A8)     /* MAX IS 9,999.99
1 #CHK-NAME     (A70)    /* PM4
*
1 #RECS-READ        (P5)
1 #RECS-SELECTED    (P5)
1 #CNT-8            (P5)
1 #I1               (I2)
1 #I2               (I2)
1 #I3               (I2)
1 #SPELL-AMT        (A50)
1 #REFUND-WORK      (N5.2)
1 REDEFINE #REFUND-WORK
 2 #REFUND-X  (N1/7)
1 #REFUND           (N5.2)
1 #LINKED-CN        (N6)
1 #NUM-RECS          (N5)
1 #NUM-RECS-LESS-1   (N5)
1 #TOT-EXT-REFUND   (P9.2)
1 #TOT-EXT-REFUND-LESS-1 (P9.2)
1 #NUM-POLS          (P5)
1 #LESS-1            (A4)
*
1 #YYYYMMDD        (N8)
1 REDEFINE #YYYYMMDD
 2 #YYYY          (N4)
 2 #MM            (N2)
 2 #DD            (N2)
1 REDEFINE #YYYYMMDD
 2 #YYYY-A        (A4)
 2 #MM-A          (A2)
 2 #DD-A          (A2)
*
1 #AFTER-FIRST  (L)
1 #DIG  (N2)
1 #DIG-1-UX (A45) INIT<'One  Two  ThreeFour Five Six  SevenEightNine '>
1 REDEFINE #DIG-1-UX
 2 #DIG-1-U (A5/9)
1 #DIG-1-LX (A45) INIT<'one  two  threefour five six  seveneightnine '>
1 REDEFINE #DIG-1-LX
 2 #DIG-1-L (A5/9)
1 #DIG-2-UX (A90) INIT <'Eleven   Twelve   Thirteen Fourteen Fifteen  '
   -'Sixteen  SeventeenEighteen Nineteen Ten      '>
1 REDEFINE #DIG-2-UX
 2 #DIG-2-U (A9/10)
1 #DIG-2-LX (A90) INIT <'eleven   twelve   thirteen fourteen fifteen  '
   -'Sixteen  seventeeneighteen nineteen ten      '>
1 REDEFINE #DIG-2-LX
 2 #DIG-2-L (A9/10)
1 #DIG-3-UX (A63) INIT
 <'xxxxxxxTwenty Thirty Forty  Fifty  Sixty  SeventyEighty Ninety '>
1 REDEFINE #DIG-3-UX
 2 #DIG-3-U (A7/9)
1 #DIG-3-LX (A63) INIT
 <'xxxxxxxtwenty thirty forty  fifty  sixty  seventyeighty ninety '>
1 REDEFINE #DIG-3-LX
 2 #DIG-3-L (A7/9)
*
1 #EXTRACT-CHK (A225)
1 #OWNER-ID    (N6)
*
1 #SPLIT    (A1)
1 #STREET   (A25)
1 #STREET-2 (A25)
1 #CITY     (A22)
1 #STATE    (A2)
1 #ZIP-CODE-9 (N9)
1 REDEFINE #ZIP-CODE-9
 2 #ZIP-5-A  (A5)
 2 #ZIP-4-A  (A4)
1 #FND-ZIP   (L)
1 #ERR-MSG   (A60)
*
END-DEFINE
* * * * * * * * * * * * * * * * *
*
FORMAT LS=133 PS=50
FORMAT (1) LS=133 PS=50 /* SAG added 10272006
WRITE (1) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X      /* SAG 10272006
          '2012 Term Refund Totals' 
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
FORMAT (8) LS=133 PS=50
WRITE  (8) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X
          'Detail List of Term Refund Checks'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD #EXTRACT-FILE
  ADD 1 TO #RECS-READ
END-ALL
*
SORT BY #EXT-FOREIGN-IND #EXT-OWNER-NAME
  USING #EXT-ID-NUMBER
        #EXT-REFUND
        #EXT-PREMS-PAID
        #EXT-PLAN
        #EXT-ISS-DATE
        #EXT-DEAD-IND
        #EXT-SEG
*
  MOVE #EXT-ID-NUMBER TO #CGA-SCR-ID
  MOVE #EXT-SEG       TO #CONTACT-SEG
  CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
*
*  MOVE 'S' TO #SPLIT                       /* PM4
*  RESET #STREET #STREET-2 #CITY #STATE     /* PM4
*  CALLNAT 'GCTSTSXX'  /* split city/state  /* PM4
*   #SPLIT                                  /* PM4
*   #FIRST-ADDRESS                          /* PM4
*   #ADDR-2                                 /* PM4
*   #ADDR-3                                 /* PM4
*   #STREET                                 /* PM4
*   #STREET-2                               /* PM4
*   #CITY                                   /* PM4
*   #STATE                                  /* PM4
*   #ERR-MSG                                /* PM4
*
  MOVE #EXT-FORMATED-NAME TO #CHK-NAME      /* PM4
*
  IF #EXT-DEAD-IND = 'D'   /* DECEASED
    RESET #CHK-NAME
  END-IF
*
  MOVE #EXT-ID-NUMBER TO #OWNER-ID
  RESET #LINKED-CN
  IF #SPS-GEN-ID NE 0
    MOVE #ID-NUM TO #LINKED-CN #OWNER-ID
  END-IF
*
  MOVE #EXT-REFUND TO #REFUND-WORK #REFUND
  PERFORM SPELL-AMT
  MOVE EDITED #EXT-REFUND (EM=Z,ZZZ.99) TO #RF-AMT-DISP
*
  ADD 1 TO #NUM-POLS
  MOVE #EXT-ZIP TO #ZIP-CODE-9                   /* PM4
*
* used below for mailing 10/1998
*
  IF #COUNTRY-CODE = 'US'                        /* PM4
   OR #THIRD-ADDRESS = ' '                       /* PM4
    COMPRESS
          #EXT-ID-NUMBER        /* 6  + 1 = 7
      #TAB #EXT-ISS-DATE        /* 8  + 1 = 9
      #TAB #RF-AMT-DISP         /* 6  + 1 = 7
      #TAB #SPELL-AMT           /* 50 + 1 = 51
      #TAB #CHK-NAME            /* 45 + 1 = 46
      #TAB #EXT-STREET          /* 25 + 1 = 26    /* PM4
      #TAB #EXT-STREET-2        /* 25 + 1 = 26    /* PM4
      #TAB #EXT-CITY            /* 25 + 1 = 26    /* PM4
      #TAB #EXT-STATE           /* 2  + 1 = 3     /* PM4
*   #TAB #ZIP-5-A             /* 5  + 1 = 6 /* PM1
         INTO #EXTRACT-CHK LEAVING NO
  ELSE                                            /* PM4
     RESET #EXT-CITY                              /* PM4
     COMPRESS                                     /* PM4
          #EXT-ID-NUMBER        /* 6  + 1 = 7     /* PM4
      #TAB #EXT-ISS-DATE        /* 8  + 1 = 9     /* PM4
      #TAB #RF-AMT-DISP         /* 6  + 1 = 7     /* PM4
      #TAB #SPELL-AMT           /* 50 + 1 = 51    /* PM4
      #TAB #CHK-NAME            /* 45 + 1 = 46    /* PM4
      #TAB #EXT-STREET          /* 25 + 1 = 26    /* PM4
      #TAB #EXT-STREET-2        /* 25 + 1 = 26    /* PM4
      #TAB #EXT-CITY            /* 25 + 1 = 26    /* PM4
      #TAB #THIRD-ADDRESS       /* 2  + 1 = 3     /* PM4
*   #TAB #ZIP-5-A             /* 5  + 1 = 6       /* PM4
         INTO #EXTRACT-CHK LEAVING NO             /* PM4
  END-IF                                          /* PM4
*
  IF #ZIP-CODE-9 NE 0                                             /* PM1
    COMPRESS #EXTRACT-CHK #TAB #ZIP-5-A INTO #EXTRACT-CHK LEAVING NO
    IF #ZIP-4-A NE '0000'     /* 4      = 4   = 207 (max length)
      COMPRESS #EXTRACT-CHK '-' #ZIP-4-A INTO #EXTRACT-CHK LEAVING NO
    END-IF
  ELSE                                                            /* PM1
    COMPRESS #EXTRACT-CHK #TAB INTO #EXTRACT-CHK LEAVING NO       /* PM1
  END-IF                                                          /* PM1
*
  IF #EXT-FOREIGN-IND = '9'   /* Refund less than 7           /* PM2 PM3
    ADD 1 TO #NUM-RECS-LESS-1
    ADD #EXT-REFUND TO #TOT-EXT-REFUND-LESS-1
    WRITE WORK FILE 4 #EXTRACT-CHK
    MOVE 'UND1' TO #LESS-1
  ELSE
    ADD 1 TO #NUM-RECS
    ADD #EXT-REFUND TO #TOT-EXT-REFUND
    WRITE WORK FILE 3 #EXTRACT-CHK
    RESET #LESS-1
  END-IF
*
  ADD 1 TO #CNT-8
  DISPLAY (8) 'CNT8' #CNT-8 (EM=ZZ,ZZ9)
    'OWNER/ID'        #OWNER-ID
    'Owner/Name'      #EXT-OWNER-NAME
    'Refund'          #EXT-REFUND
    'PLN'             #EXT-PLAN
    'Prems/Paid'      #EXT-PREMS-PAID
    'Issue/Date'      #EXT-ISS-DATE
    'Sps-Gen-Name'    #SPS-GEN-NAME
    'SPS-GEN/ID'      #SPS-GEN-ID
    'Dead/Ind'        #EXT-DEAD-IND
    'Under/$7.01'     #LESS-1                                          /* PM2 PM3
END-SORT
*
WRITE (8) #NUM-RECS-LESS-1 (EM=ZZ,ZZ9) '= # less than 7 Refd'          /* PM2 PM3
          #TOT-EXT-REFUND-LESS-1 (EM=ZZZ,ZZZ.99) '= Tot less than 7 amt'/* PM2 PM3
WRITE (8) #NUM-RECS (EM=ZZ,ZZ9) '= # Refund checks'
          #TOT-EXT-REFUND (EM=ZZZ,ZZZ.99) '= Tot Check Refund Amount'
*
WRITE (1)    #NUM-RECS (EM=ZZ,ZZ9) '= # Refund checks'              /* SAG 10272006
          #TOT-EXT-REFUND (EM=ZZZ,ZZZ.99) '= Tot Check Refund Amount'
* * * * * * * * * * * * * *
DEFINE SUBROUTINE SPELL-AMT
* * * * * * * * * * * * * *
*
RESET #SPELL-AMT #AFTER-FIRST
FOR #I3 1 TO 5
  IF #REFUND-X (#I3) NE 0
    MOVE #REFUND-X (#I3) TO #DIG
    IF #I3 = 2 OR = 3 OR = 5 /* X,XXX.xx/XXX.xx/X.xx
      IF #AFTER-FIRST
        COMPRESS #SPELL-AMT #DIG-1-L (#DIG) INTO #SPELL-AMT
      ELSE
        MOVE #DIG-1-U (#DIG) TO #SPELL-AMT /* One/Two etc
      END-IF
*
      IF #I3 = 2
        COMPRESS #SPELL-AMT 'thousand' INTO #SPELL-AMT
      ELSE
        IF #I3 = 3
          COMPRESS #SPELL-AMT 'hundred' INTO #SPELL-AMT
        END-IF
      END-IF
    ELSE                 /* XX,XXX.xx/XX.xx
      IF #DIG = 1
        MOVE #REFUND-X (#I3 + 1) TO #DIG
        IF #DIG = 0
          MOVE 10 TO #DIG
        END-IF
*
        IF #AFTER-FIRST
          COMPRESS #SPELL-AMT #DIG-2-L (#DIG) INTO #SPELL-AMT
        ELSE
          MOVE #DIG-2-U (#DIG) TO #SPELL-AMT /* Eleven/Twelve etc)
        END-IF
*
        ADD 1 TO #I3
      ELSE
        IF #AFTER-FIRST
          COMPRESS #SPELL-AMT #DIG-3-L (#DIG) INTO #SPELL-AMT
        ELSE
          MOVE #DIG-3-U (#DIG) TO #SPELL-AMT /* Twenty/Thirty etc)
        END-IF
*
        ADD 1 TO #I3
        MOVE #REFUND-X (#I3) TO #DIG
        IF #DIG NE 0
          COMPRESS #SPELL-AMT #DIG-1-L (#DIG) INTO #SPELL-AMT
        END-IF
      END-IF
*
      IF #I3 = 2
        COMPRESS #SPELL-AMT 'thousand' INTO #SPELL-AMT
      END-IF
    END-IF
    MOVE TRUE TO #AFTER-FIRST
  END-IF
END-FOR
*
IF #REFUND LT 1.00   /* A/O 4/1999 WE DO NOT GIVE REFUNDS LESS THAN $1
  MOVE 'No' TO #SPELL-AMT
END-IF
*
COMPRESS #SPELL-AMT 'and' #REFUND-X (6) INTO #SPELL-AMT
COMPRESS #SPELL-AMT #REFUND-X (7) '/100' INTO #SPELL-AMT LEAVING NO
*
END-SUBROUTINE
*
END
