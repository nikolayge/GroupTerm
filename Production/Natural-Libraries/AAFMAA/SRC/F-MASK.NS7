* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : F-MASK                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Mask data for security                                *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* YAK    09202008  None    Program Written                             *
************************************************************************
*
DEFINE FUNCTION FUNCTION-MASK RETURNS (A) DYNAMIC
  DEFINE DATA
  PARAMETER
  1 #DATA                                    (A) DYNAMIC BY VALUE
  1 #MASK-CHAR                               (A1)
  1 Start_Position                           (N1) OPTIONAL 
  LOCAL
  1 #I                                       (I2)
  1 #MASK-LENGTH                             (N2)
  1 #DASH                                    (A1) INIT <'-'>
  1 #BLANK                                   (A1) INIT <' '>
  1 #START-POSITION                          (N1)
*
  END-DEFINE
************************************************************************
*
IF Start_Position SPECIFIED
  #START-POSITION := Start_Position
ELSE  
  #START-POSITION := 1
  IF #MASK-CHAR = '*'              /* MASK RULE FOR CREDIT CARD
    #START-POSITION := 7
  END-IF
END-IF
#DATA := *TRIM(#DATA)
#MASK-LENGTH := *LENGTH(#DATA)
#MASK-LENGTH := #MASK-LENGTH - 4
*
FOR #I = #START-POSITION TO #MASK-LENGTH
  IF SUBSTRING(#DATA,#I,1) = #DASH OR SUBSTRING(#DATA,#I,1) = #BLANK
    ESCAPE TOP
  ELSE
    MOVE #MASK-CHAR TO SUBSTRING(#DATA,#I,1)
  END-IF
END-FOR
*
FUNCTION-MASK := #DATA
*
END-FUNCTION
*
END
