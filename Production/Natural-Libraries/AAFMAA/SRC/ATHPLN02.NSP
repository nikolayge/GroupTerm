* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPLN02 - combine monthly files
*
DEFINE DATA
LOCAL
1 #LOAN-FILE-OUT      /* (a21)
 2 #CN       (N6)
 2 #ISS-DT   (N8)
 2 #ACC-ADJ  (N5.2)
*
1 #LOAN-FILE-IN      /* (a21)
 2 #CN-IN      (N6)
 2 #ISS-DT-IN  (N8)
 2 #ACC-ADJ-IN (N5.2)
 2 #NUM-MONS-IN(A2)
*
1 #RECS-IN       (N5)
1 #RECS-OUT      (N5)
1 #INT-DIFF      (N5.7)
1 #LAST-CN       (N6)
1 #LAST-ISS-DT   (N8)
1 #ACCUM-ACC-ADJ (N5.2)
1 #TOT-ACC-ADJ   (N6.2)
*
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FORMAT     LS=133 PS=66
WRITE      TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'Exceptions'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
FORMAT (2) LS=133 PS=66
WRITE  (2) TITLE LEFT 'PROGRAM: ' *PROGRAM 5X    /* not now used
          'VARIABLE LOAN ACC INT ADJUSTMENT'
                      5X 'DATE: ' *DAT4U 5X 'TIME: ' *TIMX
*
READ WORK 2 RECORD #LOAN-FILE-IN   /* sorted files
  ADD 1 TO #RECS-IN
* DISPLAY #CN-IN #LAST-CN #ISS-DT-IN #LAST-ISS-DT
  IF ((#CN-IN NE #LAST-CN) OR (#ISS-DT-IN NE #LAST-ISS-DT))
   AND (#LAST-CN NE 0)
    MOVE #LAST-CN       TO #CN
    MOVE #LAST-ISS-DT   TO #ISS-DT
    MOVE #ACCUM-ACC-ADJ TO #ACC-ADJ
    WRITE WORK 3 #LOAN-FILE-OUT
  WRITE 'write record out' #LAST-CN #LAST-ISS-DT #ACCUM-ACC-ADJ
    ADD 1 TO #RECS-OUT
    ADD #ACCUM-ACC-ADJ TO #TOT-ACC-ADJ
    RESET #ACCUM-ACC-ADJ
  END-IF
*
  MOVE #CN-IN      TO #LAST-CN
  MOVE #ISS-DT-IN  TO #LAST-ISS-DT
  ADD #ACC-ADJ-IN TO #ACCUM-ACC-ADJ
END-WORK
*
MOVE #LAST-CN       TO #CN
MOVE #LAST-ISS-DT   TO #ISS-DT
MOVE #ACCUM-ACC-ADJ TO #ACC-ADJ
ADD #ACCUM-ACC-ADJ TO #TOT-ACC-ADJ
WRITE WORK 3 #LOAN-FILE-OUT
ADD 1 TO #RECS-OUT
*
WRITE 'recs-in =' #RECS-IN   (EM=ZZZ,ZZ9)
WRITE 'recs-out=' #RECS-OUT  (EM=Z,ZZ9)
WRITE 'tot-adj =' #TOT-ACC-ADJ (EM=ZZZ,ZZZ.99)
*
END
