* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
***************************************************************************
* NAME         : F-RANKID                                                 *
* AUTHOR       : ROLLY ESTABILLO                                          *
* DESCRIPTION  : DETERMINE RANK ID GIVEN THE MIL-ID AND RANK              *
***************************************************************************
*                       MODIFICATION LOG                                  *
* USER   DATE        TAG     REASON                                       *
* RSE    11/23/2010  None    Program Written                              *
* RSE    01/02/2010  RE1     Default to RANK-ID 1616 for Civilians        *
***************************************************************************
*
DEFINE FUNCTION FUNCTION-RANK-ID RETURNS (N6) BY VALUE
*
DEFINE DATA
*
PARAMETER
1 #MIL-ID              (A3)
1 #RANK-ABBR           (A25)
1 #RANK-IN-SAS         (A3)
1 #SPECIAL-PAY-IND     (A1)
1 #MESSAGE-NBR         (N3)
*
LOCAL
1 RANK-V VIEW OF A-RANK-REFERENCE
  2 RANK-ID
  2 MILITARY-ID
  2 SPECIAL-PAY-IND
  2 MESSAGE-NBR
*
1 #MIL-ABBR-KEY       (A26)
1 #MIL-RANK-SAS-KEY   (A4)
END-DEFINE
*
************************************************************************
*
RESET FUNCTION-RANK-ID
*
IF #RANK-ABBR NE ' '
  COMPRESS SUBSTR(#MIL-ID,1,1) #RANK-ABBR INTO #MIL-ABBR-KEY LEAVING NO
  FIND RANK-V WITH MIL-ABBR-KEY = #MIL-ABBR-KEY
    MOVE RANK-ID TO FUNCTION-RANK-ID
    MOVE SPECIAL-PAY-IND TO #SPECIAL-PAY-IND
    MOVE MESSAGE-NBR TO #MESSAGE-NBR
  END-FIND
ELSE
  IF #RANK-IN-SAS NE ' '
    COMPRESS SUBSTR(#MIL-ID,1,1) #RANK-IN-SAS INTO #MIL-RANK-SAS-KEY LEAVING NO
    FIND RANK-V WITH MIL-RANK-SAS-KEY = #MIL-RANK-SAS-KEY SORTED BY RANK-ID
      MOVE RANK-ID TO FUNCTION-RANK-ID
      ESCAPE BOTTOM
    END-FIND
    IF RANK-ID = 0
      FIND RANK-V WITH PAY-GRADE = #RANK-IN-SAS SORTED BY RANK-ID
        WHERE MILITARY-ID = #MIL-ID
        MOVE RANK-ID TO FUNCTION-RANK-ID
        ESCAPE BOTTOM
      END-FIND
      IF RANK-ID = 0                         /* RE1
        MOVE 1616 TO FUNCTION-RANK-ID        /* RE1
      END-IF                                 /* RE1
    END-IF
  END-IF
END-IF
*
END-FUNCTION
*
END
