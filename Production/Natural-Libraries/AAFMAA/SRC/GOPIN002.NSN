* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
**************************************************************************
* SUBPROGRAM-ID: GOPIN002
* DESCRIPTION: Collects personal and family data of member to be passed on
*              to CRM for synchronization.
**************************************************************************
*        MODIFICATION LOG
**************************************************************************
* USER   DATE      TAG  REASON
* RSE  05-04-2011  None Creation
**************************************************************************
DEFINE DATA
PARAMETER USING GOPIA001
LOCAL
1 STATUS-V VIEW OF A-STATUS
  2 SPOUSE-CONTACT-ID
  2 DATE-OF-MARRIAGE
  2 DATE-OF-DIVORCE
  2 STATUS-CODE-SP
  2 FORMER-SPOUSE-CONTACT-ID
  2 FORMER-SPOUSE-DATE-OF-MARRIAGE
  2 FORMER-SPOUSE-DATE-OF-DIVORCE
  2 FORMER-SPOUSE-STATUS
*
1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID                      (N8.0) /* Descriptor
  2 ROLE-1-CD                         (A1)
  2 CONTACT-2-ID                      (N8.0) /* Descriptor
  2 ROLE-2-CD                         (A1)
*
END-DEFINE
*
FIND STATUS-V WITH MEMBER-CONTACT-ID = #CONTACT-ID
  IF NO RECORDS FOUND
    PERFORM GET-SPOUSE
    ESCAPE BOTTOM
  END-NOREC
  MOVE BY NAME STATUS-V TO #PERSONAL-INFO
END-FIND
*
DEFINE GET-SPOUSE
/*     ==========
FIND CNT-REL WITH CONTACT-2-ID = #CONTACT-ID /* LEFT SPOUSE
  IF NO RECORDS
    PERFORM GET-RIGHT-SPOUSE
    ESCAPE BOTTOM
  END-NOREC
  IF ROLE-1-CD = 'S' /* SPOUSE
    FIND (1) STATUS-V WITH MEMBER-CONTACT-ID = CONTACT-1-ID
      MOVE BY NAME STATUS-V TO #PERSONAL-INFO
    END-FIND
    ESCAPE ROUTINE
  END-IF
END-FIND
PERFORM GET-RIGHT-SPOUSE
END-SUBROUTINE
*
DEFINE GET-RIGHT-SPOUSE
/*     ================
FIND CNT-REL WITH CONTACT-1-ID = #CONTACT-ID /* RIGHT SPOUSE
  IF NO RECORDS
    ESCAPE BOTTOM
  END-NOREC
  ACCEPT IF ROLE-1-CD = 'S' /* SPOUSE
  FIND (1) STATUS-V WITH MEMBER-CONTACT-ID = CONTACT-2-ID
    MOVE BY NAME STATUS-V TO #PERSONAL-INFO
  END-FIND
END-FIND
END-SUBROUTINE
*
END


