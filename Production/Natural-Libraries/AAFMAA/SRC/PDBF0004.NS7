* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
DEFINE FUNCTION Policy_Expiration_Date RETURNS (D)
  DEFINE DATA
  PARAMETER
  1 PLAN         (A3) 1 REDEFINE PLAN
    2 #PLAN-A1   (A1)
    2 #PLAN-A2-N (N2)
  1 NICOTINE   (A1)
  1 ISSUE-DATE (N8)
  1 DOB        (N8)
  LOCAL
  1 #YYYYMMDD (A8) 1 REDEFINE #YYYYMMDD
    2 #YYYY  (N4)
    2 #MM    (N2)
    2 #DD    (N2)
  END-DEFINE
*
RESET Policy_Expiration_Date
DECIDE FOR FIRST CONDITION
  WHEN F-LT2-TRUE(<PLAN>)
    #YYYYMMDD := ISSUE-DATE
    ADD #PLAN-A2-N TO #YYYY
  WHEN PLAN = 'EZ'
    #YYYYMMDD := DOB
    ADD 80 TO #YYYY
  WHEN PLAN = 'LT'
    #YYYYMMDD := DOB
    IF NICOTINE = 'S'
      ADD 50 TO #YYYY
    ELSE
      ADD 60 TO #YYYY
    END-IF
  WHEN PLAN = 'RT'
    #YYYYMMDD := ISSUE-DATE
    ADD 5 TO #YYYY
  WHEN ANY
    #DD := 1
    MOVE EDITED #YYYYMMDD TO Policy_Expiration_Date (EM=YYYYMMDD)
  WHEN NONE
    IGNORE
END-DECIDE
END-FUNCTION
END
