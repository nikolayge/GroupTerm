* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : GPOSFILE                                              *
* AUTHOR       : Copyright: Electronic Data Systems - an HP company    *
* DESCRIPTION  : Get Workfile                                          *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE        TAG     REASON                                    *
* PAM    06/18/2009  None    Program Written                           *
************************************************************************
*
DEFINE DATA
PARAMETER USING GPOAFILE
*
LOCAL
1 #CV                                      (C)
1 #LINE-CNT                                (P3)
1 #DISPLAY-FILE-NAME                       (A50)
1 #DISPLAY-FOLDER-NAME                     (A70)
1 #SHCMD-FOLDER                            (A70)
1 #SHCMD                                   (A250)
1 #INPUT-RECORD                            (A250)
*
END-DEFINE
************************************************************************
*
DEFINE WINDOW FILE-BOX
  SIZE 20*76
  BASE CURSOR
*
IF #WORK-FILE-FOLDER = ' '
  #CV := (AD=U)
ELSE
  #CV := (AD=PI)
  #DISPLAY-FOLDER-NAME := #WORK-FILE-FOLDER
END-IF
*
SET KEY PF2  NAMED 'Submt'
SET KEY PF3  NAMED 'Retrn'
RESET #WORKFILE-ERROR
*
SET WINDOW 'FILE-BOX'
  REPEAT
    INPUT MARK *#DISPLAY-FILE-NAME USING MAP 'GPOMFILE'
*
    IF *PF-KEY = 'PF3'
      ESCAPE BOTTOM
    END-IF
*
    IF #DISPLAY-FILE-NAME = ' '
      REINPUT 'Please enter the name of the file to process'
    ELSE
      #WORK-FILE-NAME := #DISPLAY-FILE-NAME
    END-IF
*
    IF #WORK-FILE-FOLDER = ' '
      REINPUT 'Please enter the location of the file'
    ELSE
      #WORK-FILE-FOLDER := #DISPLAY-FOLDER-NAME
    END-IF
*
    IF *PF-KEY = 'PF2'
      PERFORM CHECK-FOR-FILE
      ESCAPE BOTTOM
    END-IF
  END-REPEAT
SET WINDOW OFF
*
************************************************************************
DEFINE SUBROUTINE CHECK-FOR-FILE
************************************************************************
*
COMPRESS '"' #WORK-FILE-FOLDER '/' #WORK-FILE-NAME '"' INTO #SHCMD LEAVING NO
COMPRESS '"' #UNIX-FILE-FOLDER INTO #SHCMD-FOLDER LEAVING NO
COMPRESS #SHCMD #SHCMD-FOLDER INTO #SHCMD
COMPRESS #SHCMD #WORK-FILE-NAME '"' INTO #SHCMD LEAVING NO
COMPRESS '/usr/SAG/cronus/.scripts/ftppatrick.bsh' #SHCMD ' >/dev/null'
  INTO #SHCMD
CALL 'SHCMD' #SHCMD
*
COMPRESS #UNIX-FILE-FOLDER #WORK-FILE-NAME INTO #UNIX-FILE-FOLDER LEAVING NO
DEFINE WORK FILE 1 #UNIX-FILE-FOLDER
*
READ WORK 1 #INPUT-RECORD
  ADD +1 TO #LINE-CNT
  IF #LINE-CNT GT 0
    ESCAPE BOTTOM
  END-IF
END-WORK
*
CLOSE WORK FILE 1
*
ON ERROR
  IF *ERROR-NR = 1599
    SET WINDOW 'FILE-BOX'
      INPUT USING MAP 'GPOMFERR'
    SET WINDOW OFF
    #WORKFILE-ERROR := TRUE
    ESCAPE ROUTINE
  END-IF
END-ERROR
*
END-SUBROUTINE
*
END
