* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: GPBN0001 - Create dependant data for LTI Rider/Family Rider
*                        letters & emails
*************************************************************************
* Created for Letters @ Emails project
*************************************************************************
* MODIFICATION LOG
*************************************************************************
* YAK  03182014       Initial Creation
* bz-Name 07/07/2017     Fine Names
*************************************************************************
*
DEFINE DATA
PARAMETER
1 #CONTACT-ID (N8)
1 FAMILY-INFO (*)
  2 FAMILY-MEMBER-NAME (A) DYNAMIC
  2 FAMILY-MEMBER-ROLE (A) DYNAMIC
  2 FAMILY-MEMBER-DOB  (N8)
*
LOCAL USING OBJLCTRL          /* Contact-Relationships file data
LOCAL
1 C@FAMILY-INFO (N2)
1 #NN           (N2)
1 #NAME         (A) DYNAMIC
1 #SUFFIX       (A3)
*
END-DEFINE
*
FIND NUMBER CR-V WITH CONTACT-1-ID = #CONTACT-ID
IF *NUMBER <> 0
  C@FAMILY-INFO := *NUMBER
  EXPAND ARRAY FAMILY-INFO TO (*:C@FAMILY-INFO)
  FIND CR-V WITH CONTACT-1-ID = #CONTACT-ID
    ACCEPT IF ROLE-2-CD = 'K' OR ROLE-2-CD = 'D' OR ROLE-2-CD = 'S'
      #NN := #NN + 1
      FAMILY-MEMBER-NAME(#NN) := ' '               /* bz-Name
      CALLNAT 'GPRS0CNT' CR-V.CONTACT-2-ID FAMILY-MEMBER-NAME(#NN) FAMILY-MEMBER-DOB(#NN)
      IF FAMILY-MEMBER-NAME(#NN) = ' ' 
       #NN := #NN - 1
        ESCAPE TOP 
      END-IF
      FAMILY-MEMBER-ROLE(#NN) := CR-V.ROLE-2-CD
  END-FIND
*
  C@FAMILY-INFO := #NN
  IF C@FAMILY-INFO <> 0
    RESIZE ARRAY FAMILY-INFO TO (*:C@FAMILY-INFO)
  ELSE
    RESIZE ARRAY FAMILY-INFO TO (*:1)
  END-IF
END-IF
END

