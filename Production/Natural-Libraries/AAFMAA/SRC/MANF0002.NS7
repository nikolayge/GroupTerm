* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MANF0002.
/**
/** :author nguentchev
/* Long Term Care Settlement
/*  Gross Amount: Sum of (each Monthly Payment - 20) in the calendar year
/*
DEFINE FUNCTION GET-AZ-MONTHLY-PAY RETURNS (N7.2)
  DEFINE DATA
  PARAMETER
  1 #ISN      (P8)
  1 #DATE     (N8) BY VALUE
  1 REDEFINE #DATE
    2 PARM-YYYY (N4)
    2 PARM-MM   (N2)
    2 PARM-DD   (N2)  /* EXPECTED LAST DAY OF THE MONTH
  LOCAL
  1 #INTEREST-ONLY (A2) CONST <'IO'>
  1 #LONG-TERM     (A2) CONST <'LT'>
  1 #MONTHLY-PAY    (N7.2)
  1 ANNUITANTS VIEW OF A-ANNUITANTS
    2 POLICY-NUMBER     (A16)
    2 CONTACT-ID        (N8)
    2 SETTLEMENT-OPTION (A2)
    2 ANNUITANT-STATUS  (A1)
    2 MONTHLY-PENSION   (N7.2)
    2 WITHDRAWALS       (N7.2)
    2 INITIAL-PAYMENT   (N7.2)
    2 DEPOSIT           (N7.2)
    2 T13TH-CHECK       (N7.2)
    2 TAXABLE           (N7.2)
    2 START-DATE        (N8.0) 2 REDEFINE START-DATE
      3 START-YYYY (N4)
      3 START-MM   (N2)
      3 START-DD   (N2)
    2 WITHDRAWAL-DATE   (N8)
    2 PAID-UP-DATE      (N8)
    2 ONE-MONTH-PENSION (N7.2)
    2 ONE-MP-DATE       (N8.0) 2 REDEFINE ONE-MP-DATE
      3 ONE-YYYY (N4)
      3 ONE-MM   (N2)
      3 ONE-DD   (N2)
    2 LAST-UPD-DATE     (N8) 2 REDEFINE LAST-UPD-DATE
      3 LUD-YYYY   (N4)
      3 LUD-MM     (N2)
  1 A-ANNU-HISTORY VIEW OF A-ANNU-HISTORY
    2 ADD-DATE          (N8) 2 REDEFINE ADD-DATE
      3 ADD-HR-Y  (N4)
      3 ADD-HR-M  (N2)
      3 ADD-HR-D  (N2)
    2 LAST-UPD-DATE     (N8) 2 REDEFINE LAST-UPD-DATE
      3 HIST-LUD-YYYY   (N4)
      3 HIST-LUD-MM     (N2)
    2 ANNUITANT-STATUS  (A1)
    2 MONTHLY-PENSION   (N7.2)
    2 ONE-MONTH-PENSION (N7.2)
    2 TAXABLE           (N7.2)
    2 GENERATION        (N7)
    2 ONE-MP-DATE       (N8.0) 2 REDEFINE ONE-MP-DATE
      3 ONE-YYYY-H (N4)
      3 ONE-MM-H   (N2)
      3 ONE-DD-H   (N2)
  END-DEFINE
/*
  RESET GET-AZ-MONTHLY-PAY #MONTHLY-PAY
  GET ANNUITANTS #ISN

IF ANNUITANTS.START-DATE <= 0
  ESCAPE ROUTINE
END-IF

IF ANNUITANTS.START-DATE > 0
  IF START-YYYY > #DATE.PARM-YYYY
    ESCAPE ROUTINE
  END-IF
  IF START-YYYY = #DATE.PARM-YYYY
      AND START-MM > #DATE.PARM-MM
    ESCAPE ROUTINE
  END-IF
END-IF

IF ANNUITANTS.PAID-UP-DATE > 0
    AND ANNUITANTS.PAID-UP-DATE < #DATE
  ESCAPE ROUTINE
END-IF

GET-AZ-MONTHLY-PAY := ANNUITANTS.MONTHLY-PENSION

IF ANNUITANTS.ONE-MONTH-PENSION > 0 /* One time monthly
    AND ONE-YYYY = PARM-YYYY AND ONE-MM = PARM-MM  /* Current month
  GET-AZ-MONTHLY-PAY := ANNUITANTS.ONE-MONTH-PENSION
  ESCAPE ROUTINE
END-IF

* IF ANNUITANTS.SETTLEMENT-OPTION = #LONG-TERM
*   GET-AZ-MONTHLY-PAY := GET-AZ-MONTHLY-PAY - 20
* END-IF
IF ANNUITANTS.ANNUITANT-STATUS NE 'A'
*  RESET GET-AZ-MONTHLY-PAY
  IF LUD-YYYY = PARM-YYYY AND LUD-MM <= PARM-MM
    ESCAPE ROUTINE
  END-IF
END-IF
/*                    HISTORY
#DATE.PARM-DD := 31
IF ANNUITANTS.LUD-YYYY > #DATE.PARM-YYYY  /* Updated after parm date
    OR ANNUITANTS.LUD-YYYY = #DATE.PARM-YYYY
    AND ANNUITANTS.LUD-MM > #DATE.PARM-MM
  FIND A-ANNU-HISTORY  /* From Old to New
      WITH POLICY-NUMBER = ANNUITANTS.POLICY-NUMBER
      AND CONTACT-ID = ANNUITANTS.CONTACT-ID
      SORTED BY GENERATION
    IF A-ANNU-HISTORY.ADD-DATE > #DATE
      IF #MONTHLY-PAY = 0
        #MONTHLY-PAY := A-ANNU-HISTORY.MONTHLY-PENSION
        IF ANNUITANTS.SETTLEMENT-OPTION = #LONG-TERM
          #MONTHLY-PAY := #MONTHLY-PAY - 20
        END-IF
      END-IF
      IF A-ANNU-HISTORY.ONE-MONTH-PENSION > 0
          AND A-ANNU-HISTORY.ONE-MM-H = PARM-MM
          AND A-ANNU-HISTORY.ONE-YYYY-H = PARM-YYYY
        #MONTHLY-PAY := A-ANNU-HISTORY.ONE-MONTH-PENSION
      END-IF
    END-IF
  END-FIND
END-IF

IF #MONTHLY-PAY > 0
  GET-AZ-MONTHLY-PAY := #MONTHLY-PAY
END-IF

END-FUNCTION
END
