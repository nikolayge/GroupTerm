* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
************************************************************************
* NAME         : AIOSEZPY                                              *
* AUTHOR       : Copyright: Electronic Data Systems                    *
* DESCRIPTION  : Check for Members Current EZPay status                *
************************************************************************
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
* PAM    10282008  None    Program Written                             *
************************************************************************
*
DEFINE DATA
*
PARAMETER USING AIOAEZPY
*
LOCAL
*
1 ST-V VIEW OF A-STATUS
 2 ID-NUMBER
 2 PAD-CODE
 2 PAD-ABA-NBR
 2 PAD-ACCT-NUM
 2 PAD-CHECK-SAV
 2 NAME
 2 DATE-OTHER-UPDATE
 2 PROCESS-IND
 2 DEBIT-CREDIT-AMT
 2 ISSUE-DATE                 (9)
 2 X-FIRST-PLACE-FEE          (9)
 2 X-BILL-ADJUST              (9)
 2 EZPAY-START                (9)
 2 OWNERSHIP-FLAG             (9)
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
 2 ID-NUMBER                  (N6)
 2 PROCESS-IND                (A1)
 2 NAME                       (A25)
 2 DATE-LAST-UPDATE           (N8)
 2 TERMINAL-ID                (A8)
 2 CLERK-ID                   (A3)
 2 TRANSACTION-DATN           (P9)
 2 TRANSACTION-TIMN           (P7)
 2 OLD-DATA                   (A125/32)
 2 NEW-DATA                   (A125/32)
 2 REDEFINE NEW-DATA
  3 NEW-DATA-OCC              (32)
   4 NEW-DATA-AI              (A3)
   4 NEW-DATA-REST            (A22)
 2 SYNC-FLD-NUM               (60)
 2 SYNC-FLD-OLD-DATA          (60)
 2 SYNC-FLD-NEW-DATA          (60)
*
1 #W-OLD-NEW-DATA             (A125)
1 REDEFINE #W-OLD-NEW-DATA
 2 #W-DESCRIPTION             (A12)
 2 #W-SPACER                  (A1)
 2 #W-DATA-1                  (A3)
 2 #W-DESCRIPTION-2           (A12)
 2 #W-SPACER-2                (A1)
 2 #W-DATA-2                  (A3)
 2 #W-DESCRIPTION-3           (A10)
 2 #W-SPACER-3                (A1)
 2 #W-DATA-3                  (A82)

*
1 #MEMBER-FOUND               (L)
1 #WRITE-TRLOG                (L)
1 #ISN                        (P8)
*
1 #TEXT-CONSTANTS
 2 #PAD-CODE-CHG               (A12) INIT <'AI-PAD-CODE'>
 2 #EZPAY-START-CHANGE         (A12) INIT <'EZPAY-START'>
 2 #ISSUE-DATE-CHANGE          (A10) INIT <'ISSUE-DATE'>
 2 #CHECKING                   (A8)  INIT <'CHECKING'>
 2 #SAVINGS                    (A7)  INIT <'SAVINGS'>
 2 #VERIFIED                   (A10) INIT <'VERIFIED'>
 2 #FORWARD-MESSAGE-1          (A45) INIT <'NO CHANGES HAVE BEEN MADE TO EZPAY ACCOUNT'>
 2 #FORWARD-MESSAGE-2          (A45) INIT <'PLEASE SEND VOIDED CHECK TO POLICY SERVICES'>
 2 #CREDIT-MESSAGE-1           (A45) INIT <'THIS POLICY HAS AN ADJUSTMENT BALANCE OF'>
 2 #CREDIT-MESSAGE-2           (A45) INIT <'THIS BALANCE WILL BE USED TO REDUCE THE'>
 2 #CREDIT-MESSAGE-3           (A45) INIT <'EZPAY ON THE NEXT AVAILABLE DRAFT'>
 2 #FPF-MESSAGE-1              (A45) INIT <'IF THE MEMBER HAS NOT SETTLED THE BALANCE OF'>
 2 #FPF-MESSAGE-2              (A45) INIT <'WITHIN 60 DAYS, THE AMOUNT WILL BE'>
 2 #FPF-MESSAGE-3              (A45) INIT <'PAID VIA EZPAY ON THE NEXT AVAILABLE DRAFT'>
*
1 #MAP-VARIABLES
 2 #MATCH-IND                 (A1)
 2 #DISPLAY-ACCT-NUM          (A17)
 2 #DISPLAY-ABA-NBR           (A9)
 2 #DISPLAY-CHECK-SAV-TEXT    (A10)
 2 #DISPLAY-MESSAGE-1         (A45)
 2 #DISPLAY-MESSAGE-2         (A45)
 2 #DISPLAY-MESSAGE-3         (A45)
 2 #TEXT                      (A60)
*
1 #HOLD-VARIABLES
 2 #HOLD-ISSUE-DATE           (A8)
 2 #HOLD-EZPAY-START          (A1)
 2 #HOLD-CODE                 (A1)
*
END-DEFINE
************************************************************************
*
DEFINE WINDOW EZPAY-BOX
  SIZE 22 * 65
  BASE TOP LEFT
  FRAMED ON POSITION SYMBOL AUTO
*
RESET #MEMBER-FOUND #ISN
FIND (1) ST-V WITH MEMBER-KEY = #MEMBER-NUM-EZPAY-A
  #ISN          := *ISN
  #MEMBER-FOUND := TRUE
END-FIND
*
IF #MEMBER-FOUND
  IGNORE
ELSE
  FIND ST-V WITH SSN = #SSN-EZPAY
    IF ST-V.ID-NUMBER LE 1900
      ESCAPE TOP
    END-IF
*
    #ISN := *ISN
    #MEMBER-FOUND := TRUE
    ESCAPE BOTTOM
  END-FIND
END-IF
*
IF NOT #MEMBER-FOUND
 OR #ISN = 0
  ESCAPE ROUTINE
END-IF
*
GET ST-V #ISN
IF NOT ST-V.PAD-CODE = 'P' OR = 'D' OR = 'L' OR = 'V' OR = 'X' OR = 'Z' OR = 'E'
  ESCAPE ROUTINE
END-IF
*
#HOLD-CODE           := ST-V.PAD-CODE
#HOLD-ISSUE-DATE     := ST-V.ISSUE-DATE (#EZPAY-POL-INDEX)
#HOLD-EZPAY-START    := ST-V.EZPAY-START(#EZPAY-POL-INDEX)
*
SET WINDOW 'EZPAY-BOX'
  #DISPLAY-ABA-NBR   := ST-V.PAD-ABA-NBR
  #DISPLAY-ACCT-NUM  := ST-V.PAD-ACCT-NUM
  PERFORM LOAD-CHECK-SAVE-TEXT
*
  REPEAT
    INPUT WITH TEXT #TEXT MARK *#MATCH-IND
         USING MAP 'AIOMEZPY'
*
    IF NOT #MATCH-IND = 'Y' OR = 'N'
       #TEXT := 'Please select "Y" or "N"'
       ESCAPE TOP
    ELSE
       RESET #TEXT
    END-IF
*
    ESCAPE BOTTOM
  END-REPEAT
*
  IF #MATCH-IND = 'Y'
    DECIDE FOR FIRST CONDITION
      WHEN ST-V.X-FIRST-PLACE-FEE (#EZPAY-POL-INDEX) GT 0
        #DISPLAY-MESSAGE-1  := #FPF-MESSAGE-1
        MOVE EDITED ST-V.X-FIRST-PLACE-FEE (#EZPAY-POL-INDEX) (EM=Z,ZZZ,ZZ9.99)
             TO #DISPLAY-MESSAGE-2
        MOVE LEFT #DISPLAY-MESSAGE-2 TO #DISPLAY-MESSAGE-2
        COMPRESS '$' #DISPLAY-MESSAGE-2 INTO #DISPLAY-MESSAGE-2 LEAVING NO
        COMPRESS #DISPLAY-MESSAGE-2 #FPF-MESSAGE-2 INTO #DISPLAY-MESSAGE-2
        #DISPLAY-MESSAGE-3 := #FPF-MESSAGE-3
*
        INPUT USING MAP 'AIOMEZP2'
*
      WHEN ST-V.X-BILL-ADJUST (#EZPAY-POL-INDEX) NE 0
        #DISPLAY-MESSAGE-1  := #CREDIT-MESSAGE-1
        MOVE EDITED ST-V.X-BILL-ADJUST (#EZPAY-POL-INDEX) (EM=Z,ZZZ,ZZ9.99)
           TO #DISPLAY-MESSAGE-2
        MOVE LEFT #DISPLAY-MESSAGE-2 TO #DISPLAY-MESSAGE-2
        COMPRESS '$' #DISPLAY-MESSAGE-2 '.' INTO #DISPLAY-MESSAGE-2 LEAVING NO
        IF ST-V.X-BILL-ADJUST (#EZPAY-POL-INDEX) LT 0
          COMPRESS '-' #DISPLAY-MESSAGE-2 INTO #DISPLAY-MESSAGE-2 LEAVING NO
        END-IF
        COMPRESS #DISPLAY-MESSAGE-2 #CREDIT-MESSAGE-2 INTO #DISPLAY-MESSAGE-2
        #DISPLAY-MESSAGE-3  := #CREDIT-MESSAGE-3
*
        INPUT USING MAP 'AIOMEZP2'
*
        PERFORM SET-PAD-CODE
      WHEN NONE
        IGNORE
    END-DECIDE
  ELSE
    #DISPLAY-MESSAGE-1    := #FORWARD-MESSAGE-1
    #DISPLAY-MESSAGE-3    := #FORWARD-MESSAGE-2
    INPUT USING MAP 'AIOMEZP2'
  END-IF
*
  IF #HOLD-CODE NE ST-V.PAD-CODE
   OR #HOLD-EZPAY-START NE ST-V.EZPAY-START (#EZPAY-POL-INDEX)
    UPDATE (0140)
*
    PERFORM CREATE-TRANSACTION-LOG
  END-IF
*
SET WINDOW OFF
*
END TRANSACTION
*
************************************************************************
DEFINE SUBROUTINE CREATE-TRANSACTION-LOG
************************************************************************
*
TRLOG.ID-NUMBER            := ST-V.ID-NUMBER
TRLOG.PROCESS-IND          := ST-V.PROCESS-IND
TRLOG.NAME                 := ST-V.NAME
TRLOG.DATE-LAST-UPDATE     := ST-V.DATE-OTHER-UPDATE
TRLOG.CLERK-ID             := #EZPAY-CLERK
TRLOG.TERMINAL-ID          := *INIT-ID
TRLOG.TRANSACTION-DATN     := *DATN
TRLOG.TRANSACTION-TIMN     := *TIMN
*
#W-DESCRIPTION             := #PAD-CODE-CHG
#W-DATA-1                  := #HOLD-CODE
#W-DESCRIPTION-2           := #EZPAY-START-CHANGE
#W-DATA-2                  := #HOLD-EZPAY-START
#W-DESCRIPTION-3           := #ISSUE-DATE-CHANGE
#W-DATA-3                  := #HOLD-ISSUE-DATE
TRLOG.OLD-DATA (1)         := #W-OLD-NEW-DATA
#W-DATA-1                  := ST-V.PAD-CODE
#W-DATA-2                  := ST-V.EZPAY-START (#EZPAY-POL-INDEX)
#W-DATA-3                  := ST-V.ISSUE-DATE (#EZPAY-POL-INDEX)
TRLOG.NEW-DATA (1)         := #W-OLD-NEW-DATA
*
STORE TRLOG
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE LOAD-CHECK-SAVE-TEXT
************************************************************************
*
IF ST-V.PAD-CHECK-SAV = 'S'
  #DISPLAY-CHECK-SAV-TEXT := #SAVINGS
ELSE
  #DISPLAY-CHECK-SAV-TEXT := #CHECKING
END-IF
*
END-SUBROUTINE
*
************************************************************************
DEFINE SUBROUTINE SET-PAD-CODE
************************************************************************
*
DECIDE FOR FIRST CONDITION
  WHEN ST-V.PAD-CODE = 'L'
    ST-V.PAD-CODE                          := 'D'
    ST-V.OWNERSHIP-FLAG (#EZPAY-POL-INDEX) := 'D'
  WHEN ST-V.PAD-CODE = 'Z'
    ST-V.PAD-CODE                          := 'X'
    ST-V.OWNERSHIP-FLAG (#EZPAY-POL-INDEX) := 'D'
  WHEN ST-V.PAD-CODE = 'D' OR = 'X'
    ST-V.OWNERSHIP-FLAG (#EZPAY-POL-INDEX) := 'D'
  WHEN ST-V.PAD-CODE = 'E'
    ST-V.EZPAY-START (#EZPAY-POL-INDEX)    := 'Y'
  WHEN NONE
    IGNORE
END-DECIDE
*
END-SUBROUTINE
*
END
