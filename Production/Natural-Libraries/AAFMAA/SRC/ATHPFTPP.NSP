* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: ATHPFTPP  *** PRODUCES DYNAMIC CNTL CARD FOR FTP PROCESS  ***
*    uses system date for file name
*    used for jobs which run mid-day
DEFINE DATA
LOCAL
*
1 #DATE        (N8)
1 REDEFINE #DATE
 2 #DATE-YYYY  (N4)
 2 #DATE-MMDD  (N4)
 2 REDEFINE #DATE-MMDD
  3 #DATE-MM   (N2)
  3 #DATE-DD   (N2)
 2 REDEFINE #DATE-MMDD
  3 #DATE-MD-S (N1/4)
*
1 #TIME        (N7)
1 REDEFINE #TIME
 2 #TIME-HM (N4)
 2 REDEFINE #TIME-HM
  3 #TIME-HM-S (N1/4)
 2 #TIME-ST      (N3)
*
1 #MONTHS       (A24) INIT <'010203040506070809101112'>
1 REDEFINE #MONTHS
 2 #MONS        (A1/24)
*
1 #WORK-RECORD-1  (A80)
1 REDEFINE #WORK-RECORD-1
 2 #FIRST-3-CHARS  (A3)
 2 #REM-CHARS      (A77)
 2 REDEFINE #REM-CHARS
  3 #SEARCH-REM-CHARS (A1/77)
*
1 #WORK-RECORD-2  (A80)
*
1 #I1             (I2)            /* STEPS THRU THE JCL LINE BY LINE
1 #I2             (I2)            /* STEPS THRU THE TIME SUBSTITUTIONS
1 #I3             (I2)            /* STEPS THRU THE DATE SUBSTITUTIONS
1 #I4             (I2)            /* STEPS THRU THE MONTH ARRAY
*
END-DEFINE
*
MOVE *DATN TO #DATE
COMPUTE #I4 = ((#DATE-MM * 2) - 1)
MOVE *TIMN TO #TIME
DISPLAY #DATE #TIME #DATE-MM #I4
*
READ WORK 1 RECORD #WORK-RECORD-1  /* process daily labels/email/apps
  IF #FIRST-3-CHARS = '///'
    ESCAPE BOTTOM
  END-IF
*
  RESET #I2 #I3
*
  IF #FIRST-3-CHARS = 'put'
    FOR #I1 1 TO 72
      IF #SEARCH-REM-CHARS(#I1) = '#'
        ADD 1 TO #I2
        MOVE #TIME-HM-S(#I2) TO #SEARCH-REM-CHARS(#I1)
      ELSE
        IF #SEARCH-REM-CHARS(#I1) = '&'
          ADD 1 TO #I3
          IF #I3 LE 4
            MOVE #DATE-MD-S(#I3) TO #SEARCH-REM-CHARS(#I1)
          END-IF
        END-IF
      END-IF
    END-FOR
  END-IF
*
  MOVE #WORK-RECORD-1 TO #WORK-RECORD-2
  WRITE WORK 2 #WORK-RECORD-2
END-WORK
*
END
