* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2150PPO - Possible Options for Insurance
*
* vxt 02/08/2017 vt1 /Ticket HD-2280/
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING G3500AAD
LOCAL USING C2150APO
LOCAL USING GONERLXX
LOCAL
1 #AT-APPL-REC   (A100)
1 REDEFINE #AT-APPL-REC
  2 #AT-APPL-LABL-1 (A7)
  2 #AT-APPL-DATA-1 (A28)
  2 #AT-APPL-LABL-2 (A13)
  2 #AT-APPL-DATA-2 (A52)
1 REDEFINE #AT-APPL-REC
  2 #AT-APPL-40   (A40)
  2 #AT-APPL-48   (A48)
  2 REDEFINE #AT-APPL-48
    3 #AT-APPL-AMTS  (A12/4)
*
* 1 AT-V VIEW OF A-APPL-TRACKING
*  2 ADDRESS-1
*  2 ADDRESS-2
*  2 ADDRESS-3
*  2 ZIP-CODE
*
1 #PAGE-TOP  (A1) INIT <H'0C'>
1 #ADDL-LINES   (P3)
1 #ZIP    (A10)
1 #I1     (I2)
1 #I2     (I2)
1 #I3     (I2)
1 #I4     (I2)
1 #CNT-N  (P3)
1 #FND-ZIP (L)
*
1 #SVC-FEE-CV   (C)
1 #SMOK   (A10)
1 #AGE    (A2)
*
END-DEFINE
* * * * * * * * *
INCLUDE GONERCXX
* * * * * * * * *
* WRITE 'top of C2150PPO' #CGA-SCR-COMMAND #CGA-SCR-ID #CGA-SCR-SUFFIX
IF *DEVICE = 'BATCH'
  MOVE #CGA-MF-SCR-PARM-1-N TO #CNT  /* holds count of apps produced
END-IF
*
MOVE TRUE TO #USE-SPS-GEN-INFO   /* get insured info
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
if #id-num ne #PDA-AREA-G3500AAD.#SPS-GEN-ID and #PDA-AREA-G3500AAD.#SPS-GEN-ID ne 0         /* vt1
  move #PDA-AREA-G3500AAD.#sps-gen-name to #pda-area-c2150apo.#insured
else
  move #PDA-AREA-G3500AAD.#FORMATED-NAME to #pda-area-c2150apo.#insured
end-if                                                                                        /* vt1

*
IF #PDA-AREA-G3500AAD.#INTRO-FLAG = 'S' OR = 'K' OR = 'G'
  MOVE #PDA-AREA-G3500AAD.#SPS-GEN-NAME
    TO #PDA-AREA-C2150APO.#SPS-GEN-NAME
  MOVE #PDA-AREA-G3500AAD.#SPS-GEN-ID
    TO #PDA-AREA-C2150APO.#SPS-GEN-ID
END-IF
*
RESET #USE-SPS-GEN-INFO   /* get LINKED-MBRS info
CALLNAT 'G3500SAD' #CGA-MASTER #PDA-AREA-G3500AAD
MOVE #PDA-AREA-G3500AAD.#FORMATED-NAME  TO #PDA-AREA-C2150APO.#FORMATED-NAME
MOVE #PDA-AREA-G3500AAD.#FULL-RANK      TO #PDA-AREA-C2150APO.#FULL-RANK
MOVE #PDA-AREA-G3500AAD.#LAST-NAME      TO #PDA-AREA-C2150APO.#LAST-NAME
*
*  DISPLAY #PDA-AREA-C2150APO.#FORMATED-NAME (EM=XXXXXXXXXXXXXXXXXXXXXXX)
*       #PDA-AREA-C2150APO.#SPS-GEN-NAME (EM=XXXXXXXXXXXXXXXXXX)
*      #PDA-AREA-C2150APO.#FULL-RANK
*      #PDA-AREA-C2150APO.#LAST-NAME (EM=XXXXXXXXXX)
*
*
MOVE #PDA-AREA-G3500AAD.#ZIP-CODE TO #PDA-AREA-C2150APO.#ZIP-CODE
MOVE #FIRST-ADDRESS TO #ADDRESS-1
MOVE #ADDR-2        TO #ADDRESS-2
MOVE #ADDR-3        TO #ADDRESS-3
*
MOVE #CGA-CLERK       TO #USER
MOVE #CGA-SCR-COMMAND TO #PDA-SCR-COMMAND
MOVE #CGA-SCR-ID      TO #PDA-SCR-ID
MOVE #CGA-SCR-SUFFIX  TO #PDA-SCR-SUFFIX
CALLNAT 'C2150SPO' #PDA-AREA-C2150APO
MOVE #PDA-SCR-COMMAND TO #CGA-SCR-COMMAND
MOVE #PDA-SCR-ID      TO #CGA-SCR-ID
MOVE #PDA-SCR-SUFFIX  TO #CGA-SCR-SUFFIX
*
IF *DEVICE NE 'BATCH'  /* if ONLINE - NO MORE TO DO
  IF #CGA-SCR-SUFFIX = 'VP'
    ESCAPE ROUTINE   /* RETURN TO MR VP
  END-IF
*
  IF   (#PDA-AREA-C2150APO.#ERR-TEXT NE ' ')
      AND (#PDA-AREA-C2150APO.#ERR-TEXT NE 'G1000PXX')
    INPUT TEXT #PDA-AREA-C2150APO.#ERR-TEXT USING MAP 'G1000MXX'
  END-IF
*
  FETCH 'G1000PXX'
END-IF
*
IF #PDA-AREA-C2150APO.#ERR-TEXT NE ' '
  WRITE 'Error in C2150PPO' #CGA-SCR-ID
  ESCAPE ROUTINE
END-IF
*
IF #INELIGIBLE
  WRITE 'Not Eligible for APPL' #CGA-SCR-ID
  ESCAPE ROUTINE
END-IF
*
IF   (#CGA-SCR-SUFFIX = 'ATA'   /* AT APPL FROM ATHPATLB
    OR = 'VLT'               /* VGLI app run
    OR = 'BLT'               /* Member b-day app run
    OR = 'AS')               /* Spouse b-day app run
    AND (*DEVICE = 'BATCH')
  CALLNAT 'C2153SPO' #PDA-AREA-C2150APO
  MOVE #CNT TO #CGA-MF-SCR-PARM-1-N  /* holds count of apps produced
  IF (NOT  #CGA-SCR-SUFFIX = 'BLT' OR = 'AS'  /* remove this line later
      OR = 'VLT')
    ESCAPE ROUTINE
  END-IF
END-IF
*
MOVE '*' TO #CGA-SCR-COMMAND
*
IF  (#CGA-SCR-SUFFIX = 'VGL')     /* VGLI LETTER from athpvgli
    OR (#PDA-AREA-C2150APO.#INTRO-FLAG = 'K' OR = 'G') /* gen get no trm pg
    OR (#DESIRED-LINES (1) NE ' ')   /* simple prem text -all sel-vts here
    OR (#SEL-TERM AND (NOT #SEL-WL)) /* only term
  IF #SEL-VT          /* only done in mr
    MOVE 2 TO #PG-LV
    MOVE 3 TO #PG-BN
    MOVE 4 TO #PG-SG
  ELSE
    MOVE 1 TO #PG-TM     /* TERM but not WL is being printed
    MOVE 2 TO #PG-RF
    MOVE 3 TO #PG-BN
    MOVE 4 TO #PG-SG
  END-IF
ELSE
  IF #SEL-WL AND NOT #SEL-TERM   /* only WL
    IF (#DESIRED-LINES (1) = ' ')   /* Not simple prem text
        OR #SEL-VT                     /* requested term
      MOVE 2 TO #PG-LV
      MOVE 3 TO #PG-BN
      MOVE 4 TO #PG-SG
    ELSE
      MOVE 2 TO #PG-BN
      MOVE 3 TO #PG-SG
    END-IF
  ELSE      /* BOTH WL AND TERM
    MOVE 2 TO #PG-TM
    MOVE 3 TO #PG-RF
    MOVE 4 TO #PG-LV
    MOVE 5 TO #PG-BN
    MOVE 6 TO #PG-SG
  END-IF
END-IF
*
IF #CGA-SCR-SUFFIX = 'VGL' /* VGLI LETTER from athpvgli
  WRITE (7) NOTITLE USING FORM 'C2152FPO'  /* benif/health pg
  WRITE (7) NOTITLE USING FORM 'C2153FPO'  /* signature pg
  ESCAPE ROUTINE
END-IF
*
IF #DESIRED-LINES (1) NE ' '
  WRITE (8) NOTITLE USING FORM 'C2150FPX' /* new text prem info
ELSE
  IF #SEL-TERM AND NOT #SEL-WL
    IGNORE
  ELSE
    WRITE (8) NOTITLE USING FORM 'C2150FPO'   /* WL app
  END-IF
*
  IF  (NOT #PDA-AREA-C2150APO.#INTRO-FLAG = 'K' OR = 'G')
      AND (#DESIRED-LINES (1) = ' ')   /* no need for page 2
    IF #SEL-WL AND NOT #SEL-TERM
      IGNORE
    ELSE
      IF #5-YR-REN-LBL = ' '    /* no RT info
          AND NOT #SEL-TERM
        IF #RT-__ = 'S'
          MOVE '  smoker' TO #SMOK
          MOVE '40' TO #AGE
        ELSE
          MOVE 'non-smoker' TO #SMOK
          MOVE '50' TO #AGE
        END-IF
*
        WRITE (8) NOTITLE USING FORM 'C2155FPO' /* tm app special
      ELSE
        IF #LTII- NE ' '   /* show LTII
          WRITE (8) NOTITLE USING FORM 'C215AFPO' /* tm app w LTII
        ELSE
          WRITE (8) NOTITLE USING FORM 'C2151FPO' /* tm app w LT
        END-IF
      END-IF
*
      WRITE (8) NOTITLE USING FORM 'C2156FPO'   /* TM REFUND
    END-IF
  END-IF
END-IF
*
IF (#DESIRED-LINES (1) = ' ')   /* Not simple prem text
    OR #SEL-VT                     /* requested term
  WRITE (8) NOTITLE USING FORM 'C2157FPO' /* LTII lifestyle sheet
END-IF
*
WRITE (8) NOTITLE USING FORM 'C2152FPO' /* benif/health pg
*
IF #PDA-AREA-C2150APO.#INTRO-FLAG = 'S' OR = 'K' OR = 'G'
  WRITE (8) NOTITLE USING FORM 'C2154FPO' /* extra signature
ELSE
  WRITE (8) NOTITLE USING FORM 'C2153FPO' /* signature
END-IF
*
END
