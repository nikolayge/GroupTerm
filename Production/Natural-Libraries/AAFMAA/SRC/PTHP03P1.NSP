* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: PTHP03P0  CLONED FROM ATHP03P0
*
*  THIS PROGRAM RUNS IN BATCH MODE ONLY AND IS USED
*  TO REFORMAT THE TRANSACTION-REGISTER FILE AND
*  THE ACCOUNTING-REGISTER FILE INTO A FORMAT AND
*  WRITE TO A WORK FILE FOR DOWNLOAD TO AAFMAA
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER   DATE      TAG     REASON                                      *
*                                                                      *
* SAG    11282005  RH1  SS - Modified the custom coded ON ERROR stmt & *
*                       replaced it with copycode GABNDCX4             *
************************************************************************
*
DEFINE DATA
LOCAL USING GONERLXX                        /* SAG RH1 11282005 SS
LOCAL
1 #WORK-FIELDS
  2 #ABEND-CODE       (I4) INIT<4095>
  2 #IN-CNT           (P7) INIT<0>
  2 #ACCT-IN-CNT      (P7) INIT<0>
  2 #OUT-CNT          (P7) INIT<0>
*
  2 #TIME-P7          (P7)
  2 REDEFINE #TIME-P7
    3 #TIME-A3        (A3)
*
*
1 TRLOG VIEW OF A-TRANSACTION-REGISTER
  2 ID-NUMBER        (N6)
  2 NAME             (A25)
  2 DATE-LAST-UPDATE (N8)
  2 REDEFINE DATE-LAST-UPDATE
    3 DLU-CENT       (A2)
    3 DLU-YY         (A2)
    3 DLU-MM         (A2)
    3 DLU-DD         (A2)
  2 TERMINAL-ID      (A8)
  2 REDEFINE TERMINAL-ID
    3 TERMINAL-ID-4  (A4)
  2 CLERK-ID         (A3)
  2 TRANSACTION-DATN (N9)
  2 REDEFINE TRANSACTION-DATN
    3 TD-CENT        (A3)
    3 TD-YY          (A2)
    3 TD-MM          (A2)
    3 TD-DD          (A2)
  2 TRANSACTION-TIMN (N7)
  2 REDEFINE TRANSACTION-TIMN
    3 TRANSACTION-TIMN-A6 (A6)
  2 C*CHANGED-DATA-TABLE
  2 OLD-DATA         (A125/32)
  2 NEW-DATA         (A125/32)
1 #TRX               (I2)
*
1 #DATN              (N8)
1 REDEFINE #DATN
  2 #CENT          (A2)
  2 #YYMMDD        (A6)
  2 REDEFINE #YYMMDD
    3 #YY          (A2)
    3 #MM          (A2)
    3 #DD          (A2)
1 REDEFINE #DATN
  2 #DATN-YYYYMM   (N6)
  2 #DATN-DAY      (N2)
*
1 #TIMN            (N7)
1 REDEFINE #TIMN
  2 #TIMN-A6       (A6)
*
1 ACCOUNTING-TRLOG VIEW OF A-ACCOUNTING-REGISTER
  2 ID-NUMBER
  2 TRANSACTION-DATN
  2 TRANSACTION-TIMN
  2 TXN-CODE
  2 SEQUENCE-NUM
  2 NAME
  2 TXN-ACCT-NUMBER
  2 ACCT-DATE
  2 DATE-LAST-ACCT-UPDATE
  2 REDEFINE DATE-LAST-ACCT-UPDATE
    3 DLAU-CENT       (A2)
    3 DLAU-YY         (A2)
    3 DLAU-MM         (A2)
    3 DLAU-DD         (A2)
  2 DEBIT-AMOUNT
  2 CREDIT-AMOUNT
  2 DESCRIPTION
  2 MESSAGE-CODE
  2 AALP-FLAG
  2 CLERK-ID
  2 TERMINAL-ID
  2 REDEFINE TERMINAL-ID
    3 ACT-TERM-ID-4   (A4)
/* END OF VIEW ACCOUNTING-TRLOG
*
*  WORK FILE RECORD - TRANSACTION-REGISTER
*
1 TRAN-PRINT-RECORD  (A220)
1 REDEFINE TRAN-PRINT-RECORD
  2 TRAN-ID          (A1)     /* INIT<'F'>
  2 TRAN-CN-NUMBER
    3 TRAN-CN-LIT    (A2)     /* INIT<'CN'>
    3 TRAN-ID-NUMBER (N6)
    3 TRAN-CN-FILL   (A1)
  2 TRAN-DATE        (A6)     /* FORMAT 'MMDDYY'
  2 TRAN-TIME        (A3)
  2 TRAN-TYPE        (A1)
  2 TRAN-LAST-UPDATE (A6)
  2 TRAN-DATA        (A125)
  2 TRAN-NAME        (A25)
  2 TRAN-FILL1       (A6)
  2 TRAN-FILL2       (A10)
  2 TRAN-FILL3       (A8)
  2 TRAN-CLERK       (A4)
  2 TRAN-TERMINAL    (A4)
  2 TRAN-TRDATE      (A6)
  2 TRAN-TRTIME      (A6)
*
*  WORK FILE RECORD - ACCOUNTING-REGISTER
*
1 ACCT-PRINT-RECORD (A220)
1 REDEFINE ACCT-PRINT-RECORD
  2 ACCT-TYPE        (A1)     /* INIT<'B'>
  2 ACCT-FILL1       (A3)
  2 ACCT-CN-NUMBER
    3 ACCT-CN-LIT    (A2)     /* INIT<'CN'>
    3 ACCT-ID        (N6)
    3 ACCT-CN-FILL   (A1)
  2 ACCT-ACCT-DATE   (A6)     /* FORMAT 'MMDDYY'
  2 ACCT-CODE        (A4)
  2 ACCT-COL         (A1)     /* SEQUENCE NO FOR MULTI TXNS
  2 ACCT-NAME        (A25)
  2 ACCT-FILL2       (A1)
  2 ACCT-CERT        (A5)     /* NOT USED
  2 ACCT-NO          (A3)
  2 ACCT-LDTE        (A6)     /* LAST ACCTG UPDATE MMDDYY
  2 ACCT-DEBIT-AMT   (N7.2)
  2 ACCT-CREDIT-AMT  (N7.2)
  2 ACCT-REF         (A10)    /* ONLY USED IN BATCH
  2 ACCT-DESCRIPTION (A20)
  2 ACCT-MSG-CODE    (A1)
  2 ACCT-PROCESS-DATE (A6)
  2 ACCT-FILL3       (A4)
  2 ACCT-AALP-FLAG   (A1)
  2 ACCT-CLERK       (A4)
  2 ACCT-TRDATE      (A6)     /* MMDDYY
  2 ACCT-TRTIME      (A6)     /* HHMMSS
  2 ACCT-TERMINAL    (A4)
*
1 #DATAIN-REC         (A80)
1 #BATCH-PARM         (A30)
1 REDEFINE #BATCH-PARM
 2 #BATCH-PARM-MM     (N2)
 2 #BATCH-PARM-DD     (N2)
 2 #BATCH-PARM-YY     (N2)
 2 #REST-OF-05P0-PARM (A24)
*
1 #CUTOFF-4-MON-YYYYMMDD-G
 2 #CUTOFF-4-MON-YYYY (N4)
 2 #CUTOFF-4-MON-MM   (N2)
 2 #CUTOFF-4-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-4-MON-YYYYMMDD-G
 2 #CUTOFF-4-MON-YYYYMMDD (N8)
*
1 #CUTOFF-2-MON-YYYYMMDD-G
 2 #CUTOFF-2-MON-YYYY (N4)
 2 #CUTOFF-2-MON-MM   (N2)
 2 #CUTOFF-2-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-2-MON-YYYYMMDD-G
 2 #CUTOFF-2-MON-YYYYMMDD (N8)
*
1 #CUTOFF-1-MON-YYYYMMDD-G
 2 #CUTOFF-1-MON-YYYY (N4)
 2 #CUTOFF-1-MON-MM   (N2)
 2 #CUTOFF-1-MON-DD   (N2) INIT <01>
1 REDEFINE #CUTOFF-1-MON-YYYYMMDD-G
 2 #CUTOFF-1-MON-YYYYMMDD (N8)
*
1 #START-DATE     (N8)
1 #END-DATE       (N8)
END-DEFINE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ON ERROR
*     WRITE   / '*E R R O R - N R    ' *ERROR-NR              /* SAG RH1 11282005 SS - BEGIN
*             / '*E R R O R - L I N E' *ERROR-LINE
*             / 'C U R R E N T  R C D' TRLOG.ID-NUMBER
*             / '                    ' TRLOG.TRANSACTION-DATN
*             / '                    ' TRLOG.TRANSACTION-TIMN
*     CALL 'ABEND' USING #ABEND-CODE  /* USER 4095
INCLUDE GABNDCX4
STOP                                                          /* SAG RH1 11282005 SS - END
END-ERROR
*
ASSIGN #START-DATE = 19981219
ASSIGN #END-DATE   = 19981231
READ WORK 2 ONCE RECORD #BATCH-PARM   /* PROGRAM CALL (ATHP05P0)
READ WORK 2 ONCE RECORD #BATCH-PARM   /* DATE ETC (110188123456789NAB)
*
MOVE #BATCH-PARM-YY TO #CUTOFF-1-MON-YYYY   /* TAKE ATHP05P0 DATE PARM
*                                     /* AND DEVELOP  THE 1ST DAY OF
IF #CUTOFF-1-MON-YYYY GT 70              /* THE PREVIOUS/previous MONTH
  ADD 1900 TO #CUTOFF-1-MON-YYYY
ELSE
  ADD 2000 TO #CUTOFF-1-MON-YYYY
END-IF
*
MOVE #BATCH-PARM-MM TO #CUTOFF-1-MON-MM
MOVE #CUTOFF-1-MON-YYYYMMDD TO #CUTOFF-2-MON-YYYYMMDD
MOVE #CUTOFF-1-MON-YYYYMMDD TO #CUTOFF-4-MON-YYYYMMDD
*
SUBTRACT 4 FROM #CUTOFF-4-MON-MM
*
IF #CUTOFF-4-MON-MM LT 01
  ADD 12 TO #CUTOFF-4-MON-MM
  SUBTRACT 1 FROM #CUTOFF-4-MON-YYYY
END-IF
*
SUBTRACT 2 FROM #CUTOFF-2-MON-MM
*
IF #CUTOFF-2-MON-MM LT 01
  ADD 12 TO #CUTOFF-2-MON-MM
  SUBTRACT 1 FROM #CUTOFF-2-MON-YYYY
END-IF
*
SUBTRACT 1 FROM #CUTOFF-1-MON-MM
*
IF #CUTOFF-1-MON-MM LT 01
  ADD 12 TO #CUTOFF-1-MON-MM
  SUBTRACT 1 FROM #CUTOFF-1-MON-YYYY
END-IF
*
FORMAT LS=133
PS=66
WRITE TITLE LEFT 'PROGRAM:' *PROGRAM
  5X 'DATE:' *DATX 5X 'TIME:' *TIMX
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MOVE 'F'  TO TRAN-ID
MOVE 'CN' TO TRAN-CN-LIT
*
READ TRLOG PHYSICAL
  ADD 1 TO #IN-CNT
*
* IF TRANSACTION-DATN LT #CUTOFF-1-MON-YYYYMMDD /* IGNORE IF NOT IN
  IF TRANSACTION-DATN LT #START-DATE OR TRANSACTION-DATN GT #END-DATE
    END TRANSACTION                             /* current month
    ESCAPE TOP
  END-IF
*
  COMPRESS DLU-MM DLU-DD DLU-YY INTO TRAN-LAST-UPDATE LEAVING NO
  COMPRESS TD-MM TD-DD TD-YY    INTO TRAN-TRDATE      LEAVING NO
  MOVE ID-NUMBER     TO TRAN-ID-NUMBER
  MOVE TRAN-TRDATE   TO TRAN-DATE
  MOVE NAME          TO TRAN-NAME
  MOVE CLERK-ID      TO TRAN-CLERK
  MOVE TERMINAL-ID-4 TO TRAN-TERMINAL
  MOVE TRANSACTION-TIMN-A6 TO TRAN-TRTIME
  MOVE TRANSACTION-TIMN TO #TIME-P7
  MOVE #TIME-A3      TO TRAN-TIME
*
  FOR #TRX 1 TO C*CHANGED-DATA-TABLE
    MOVE '1'                    TO TRAN-TYPE
    MOVE OLD-DATA (#TRX)        TO TRAN-DATA
    WRITE WORK 1 TRAN-PRINT-RECORD
    MOVE '2'                    TO TRAN-TYPE
    MOVE NEW-DATA (#TRX)        TO TRAN-DATA
    WRITE WORK 1 TRAN-PRINT-RECORD
    ADD 2 TO #OUT-CNT
  END-FOR
*
  END TRANSACTION
END-READ
* * * * * * *   E O F   * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
MOVE 'B'  TO ACCT-TYPE
MOVE 'CN' TO ACCT-CN-LIT
*
READ ACCOUNTING-TRLOG PHYSICAL
  ADD 1 TO #ACCT-IN-CNT
*
*
* IF TRANSACTION-DATN LT #CUTOFF-1-MON-YYYYMMDD /* IGNORE IF NOT IN
  IF TRANSACTION-DATN LT #START-DATE OR TRANSACTION-DATN GT #END-DATE
    END TRANSACTION                             /* current month
    ESCAPE TOP
  END-IF
*
  COMPRESS DLAU-MM DLAU-DD DLAU-YY
    INTO ACCT-LDTE LEAVING NO
  MOVE ACCT-DATE           TO #DATN
  COMPRESS #MM #DD #YY   INTO ACCT-TRDATE LEAVING NO
  MOVE ACCT-TRDATE         TO ACCT-ACCT-DATE
  MOVE ID-NUMBER           TO ACCT-ID
  MOVE DEBIT-AMOUNT        TO ACCT-DEBIT-AMT
  MOVE CREDIT-AMOUNT       TO ACCT-CREDIT-AMT
  MOVE TXN-CODE            TO ACCT-CODE
  MOVE DESCRIPTION         TO ACCT-DESCRIPTION
  MOVE AALP-FLAG           TO ACCT-AALP-FLAG
  MOVE MESSAGE-CODE        TO ACCT-MSG-CODE
  MOVE SEQUENCE-NUM        TO ACCT-COL
  MOVE TXN-ACCT-NUMBER     TO ACCT-NO
  MOVE NAME                TO ACCT-NAME
  MOVE CLERK-ID            TO ACCT-CLERK
  MOVE ACT-TERM-ID-4       TO ACCT-TERMINAL
  MOVE TRANSACTION-DATN    TO #DATN
  COMPRESS #MM #DD #YY   INTO ACCT-PROCESS-DATE LEAVING NO
  MOVE TRANSACTION-TIMN    TO #TIMN
  MOVE #TIMN-A6            TO ACCT-TRTIME
*
  WRITE WORK 1 ACCT-PRINT-RECORD
  ADD 1 TO #OUT-CNT
  END TRANSACTION
*
END-READ
* * * * * * *   E O F   * * * * * * * * * * * * *
WRITE 'TRANSACTION-REGISTER READ' 38T #IN-CNT
  / 'ACCOUNTING-REGISTER READ'  38T #ACCT-IN-CNT
  / 'RECORDS WRITTEN'           38T #OUT-CNT
*
END
