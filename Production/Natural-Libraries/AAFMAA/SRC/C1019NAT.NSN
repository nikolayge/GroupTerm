* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: C1019NAT - Collect contact related data
*
************************************************************************
*                       MODIFICATION LOG                               *
* USER     DATE      TAG     REASON                                    *
*                                                                      *
* NGG    03/28/2011  None    Program written                           *
* vxt    06/04/2015  vt1    /Ticket 3580/
************************************************************************
DEFINE DATA parameter
* PARAMETER USING GOADA001
 1 #ADDR-POOL
   2 ADDRESS-POOL-ID (N8)                                    /*  1
   2 STREET (A50)                                              
   2 UNIT (A25)
   2 CITY (A50)
   2 STATE-CODE (A2)
   2 ZIP-CODE (N9)
   2 REDEFINE ZIP-CODE /* BEGIN REDEFINE: ZIP-CODE
     3 ZIP-5 (N5)
     3 ZIP-4 (N4)
   2 COUNTRY-CODE (A2)
   2 INTL-REGION (A50)
   2 POSTAL-CODE (A25)
   2 URBANIZATION (A50)
   2 ADDR-VALID-IND (A1)                                   /* 11
 1 #ADDR-REL
   2 CONTACT-ID (N8)                                       /* 12
   2 ADDRESS-POOL-ID (N8)
   2 ADDR-TYPE-CD (A1)
   2 ADDR-LOCATION (A1)
   2 ATTENTION-LINE (A50)
   2 RETURNED-MAIL-IND (A1)
   2 PREVENT-OVER-IND (A1)
   2 ADDRESS-SOURCE (A1)
   2 STATUS (A1)
   2 STATUS-DATE (N8)
 1 #CLERK-ID (A3)
 1 #MSG (A) DYNAMIC                                      /* 23
* PARAMETER USING GOEMA001
1 #EMAIL
   2 EMAIL-ID (N8)                                       /* 24     
   2 CONTACT-ID (N8)
   2 EMAIL-TYPE (A1)
   2 EMAIL-ADDRESS (A70)
   2 EMAIL-PROTECTION-IND (A1)
   2 EMAIL-UNDELIV (A1)
   2 EMAIL-PREFERRED-IND (A1)
   2 EMAIL-SOURCE (A1)
   2 STATUS (A1)
   2 STATUS-DATE (N8)
   2 REMARKS (A70/3)                                     /* 34
* PARAMETER USING GOCOA001
1 #CONTACT
   2 CONTACT-ID (N8.0)                                  /* 35
   2 ID-NUMBER (N6)
   2 CONTACT-TYPE (A1)
   2 RANK-ID (N6)
   2 MILITARY-STATUS (A1)
   2 PREFIX-TITLE (A25)
   2 FULL-NAME
     3 FIRST-NAME (A25)
     3 MIDDLE-NAME (A25)
     3 LAST-NAME (A25)
     3 SUFFIX (A10)
   2 GENDER-CD (A1)
   2 DATE-OF-BIRTH (N8.0)
   2 SSN (N9.0)
   2 FIRM-ORGANIZATION-NAME (A75)
   2 FIRM-ESTABLISHED-DATE  (N8)
   2 WEBSITE-URL (A70)
   2 CRM-CONTACT-GUI (A50)
   2 #FORCE-NEW (L)                                    /* 50
* PARAMETER USING GOMDA001
1 #MIL
   2 ID-NUMBER (N6)                                    /* 51
   2 CONTACT-ID (N8)
   2 RANK-ID (N6)
   2 MILITARY-STATUS (A1)
   2 MILITARY-ID (A3)
   2 DATE-INIT-ENTRY-MIL-SVC (N8)
   2 BASE-PAY-EF-DATE (N8)
   2 ACTIVE-DUTY-BASE-DATE (N8)
   2 DATE-OF-RANK (N8)
   2 SERVICE-DATE-1405 (N8)
   2 PRIOR-DATE-OF-RANK (N8)
   2 DATE-RETIRED (N8)
   2 VA-CLAIM-NUMBER (A12)
   2 VA-DISABILITY-PCT (P1.2)
   2 VA-DISABLE-DATE (N8)
   2 RSV-NG-20-YR-CERT-LTR-DATE (N8)
   2 RET-RSV-DATE (N8)
   2 RETIRED-PAY-AMT (P5)
   2 MIL-SBP-OPT (A1)
   2 MIL-SBP-AMT (P5)
   2 RANK-ABBR (A25)
   2 RSV-NG-TYPE-ANNUITY (A1)
   2 RSV-NG-RETIRE-PTS (P5)
   2 CHIEF-OF-STAFF-IND (A1)
   2 RSFPP-AMT (P5)
   2 DATE-OF-BIRTH (N8)
   2 WIDOW-FLAG (A1)
   2 DISABILITY-PCT (P1.2)
   2 DATE-OF-DEATH (N8)                             /* 79
* PARAMETER            /* vt1 \/
1 #PH
  2 #HOME
    3 #H_PH1             (N3)                      /* 80
    3 #H_PH2             (N3)
    3 #H_PH3             (N4)
    3 #H_PH4             (N6)
    3 #H-PH-ID           (N8)
  2 #WORK
    3 #W_PH1             (N3)
    3 #W_PH2             (N3)
    3 #W_PH3             (N4)
    3 #W_PH4             (N6)
    3 #W-PH-ID           (N8)
  2 #CELL
    3 #C_PH1             (N3)
    3 #C_PH2             (N3)
    3 #C_PH3             (N4)
    3 #C-PH-ID           (N8)
  2 #preff               (a1)                     /* 94
                         
1 #CONTACT-ID            (N8)
1 #S-ADDR-IND            (A1)
1 #CGA-ISN               (P8)                      /* 97
*
LOCAL USING GOADA002
LOCAL
1 #ZIP-5                 (A5)
1 #ZIP-4                 (A4)
1 PUERTORICO        (A2) CONST <'PR'>
1 USA               (A2) CONST <'US'>
1 CANADA            (A2) CONST <'CA'>
1 BLANK             (A1) CONST <' '>
1 YES               (A1) CONST <'Y'>
1 NO                (A1) CONST <'N'>
*
END-DEFINE
*
IF #CONTACT-ID = 0   /* Old AT record
  CALLNAT 'C1098NAT'
    #ADDR-POOL
    #ADDR-REL
    #CLERK-ID
    #MSG
    #EMAIL
    #CONTACT
    #MIL
    #HOME
    #WORK
    #CGA-ISN
  #CONTACT.#FORCE-NEW := TRUE
END-IF
IF #CONTACT.CONTACT-ID NE 0
  #CONTACT-ID := #CONTACT.CONTACT-ID
ELSE
  ESCAPE ROUTINE
END-IF
*                                Address
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'S'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-REL.ADDR-TYPE-CD = 'S'
    AND #MSG EQ BLANK
  #S-ADDR-IND := YES
ELSE
  #S-ADDR-IND := NO
END-IF
*
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'P'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-POOL.COUNTRY-CODE = USA OR= PUERTORICO
    AND ZIP-5 > 0
  MOVE EDITED ZIP-5 (EM=99999) TO #ZIP-5
  MOVE EDITED ZIP-4 (EM=9999) TO #ZIP-4
  IF #ZIP-4 NE BLANK
    COMPRESS #ZIP-5 #ZIP-4 INTO #ZIP WITH DELIMITER '-'
  END-IF
END-IF
*                                  Contact
RESET #CONTACT
#CONTACT.CONTACT-ID := #CONTACT-ID
CALLNAT 'GOCON002' #CONTACT #MSG
*                                  Phones
RESET #PH
CALLNAT 'C1005NAT' #CONTACT-ID #PH
*                                  eMail
RESET #EMAIL
#EMAIL.CONTACT-ID := #CONTACT-ID
CALLNAT 'GOEMN002' #EMAIL #MSG
*                                  Military data
RESET #MIL
#MIL.CONTACT-ID := #CONTACT-ID
CALLNAT 'GOMDN002' #MIL #MSG
*
END
