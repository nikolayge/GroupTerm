* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* Member data report, via email
* bz
* 1   CI   FETCH DR for Part 1    C2300PCI C2320PCI - FETCH DR for Part 1
* 2.  ST-screen (for each policy) C2600PST
* 3.  NS-screen                   Coded as WRITE (22)
* 4.  RL-screen                   C2600PST
* 5.  NC screen (for each policy) C2060PNC
* 6.  VW-screen (for each policy) C2H30PVW
* 7.  RE-screen (for each policy) C2970PRE
* 8.  DP-screen                   E4400PDP
* ------------------------------------------------
* TESTING DAL  upload_index.xml
* /FTP/EDS/daleTest/<SUBFOLDER>
* Production DAL
* /FTP/EDS/Downloads/dale/<SUBFOLDER>
* Website DAL
* http://206.122.205.22/dal/dal.aspx
* bz1 add identification for DEVELOPMENT and PRODUCTION (Ops# 690)
* ------------------------------------------------
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK.CI-BLOCK
LOCAL  USING GONERLXX
LOCAL

1 TRM-V VIEW OF A-TERMINALS
  2 CLERK-ID              (A3)     /* F D
  2 INIT-USER
  2 COMMENTS
  2 EMAIL                 (A60)

1 NT-V VIEW OF A-NOTES    /* File 38     XNOTES
 2 ID-NUMBER    (N6)
 2 C*NOTE-TABLE
 2 NOTE-TABLE  (500)
   3 NOTE-DATE         (N8)
   3 REDEFINE NOTE-DATE
     4 v_YYYY            (A4)
     4 v_MM              (A2)
     4 v_DD              (A2)
   3 NOTE-CLERK-ID     (A3)
   3 NOTE-LINE-NUM     (P1)
   3 NOTE-TEXT         (A60)
   3 USER-COMPANY      (A1)
*
1 v_CN                     (N6)
1 File_DIR                 (A60)  INIT <"/usr/SAG/AAFMAA/batch/workfiles/WORK">
1 File_Pref                (A20)  INIT <"D-Report">
1 File_Name                (A79)
1 File_ABS_UNIX            (A80)
1 File_ABS_WIN             (A80)
1 File_Email_Name          (A79)
1 File_XML                 (A79)
1 File_XML_WIN             (A35)  INIT <"upload_index.xml">
1 v_FTP_KSH                (A79)  INIT <"/usr/SAG/AAFMAA/batch/workfiles/PROD/PGM/ftpSAS">
1 v_DAL_KSH                (A79)  INIT <"/usr/SAG/AAFMAA/batch/workfiles/PROD/PGM/ftpDAL">
1 v_FTP_DIR_WIN            (A)    DYNAMIC                                       /*         bz1
1 v_FTP_DIR_WIN_PROD       (A)    DYNAMIC INIT <"/ftp/Death_Claims">            /*  --PROD bz1
1 v_FTP_DIR_WIN_DEV        (A)    DYNAMIC INIT <"/ftp/TEST">                    /*  --DEV  bz1
1 v_FTP_DIR_BASE_DAL       (A)    DYNAMIC                                       /*         bz1
1 v_FTP_DIR_BASE_DAL_PROD  (A)    DYNAMIC INIT <"/ftp/EDS/Downloads/dale">  /*  --PROD bz1
1 v_FTP_DIR_BASE_DAL_DEV   (A)    DYNAMIC INIT <"/FTP/EDS/daleTest">        /*  --DEV  bz1
1 v_FTP_BUILD_DAL       (A)    DYNAMIC INIT <" ">
1 v_XML_Title      (A70)  INIT <"Death claims process report for CN:">
*
1 v_EMAIL
  2 v_From_L (A10) INIT <"From:">
  2 v_From  (A60)
  2 v_Lbr1  (A2)   INIT <H'0D0A'>
  2 v_To_L  (A4)   INIT <"To:">
  2 v_To    (A100) INIT <"bzaslavskiy@aafmaa.com">
  2 v_Lbr2  (A2)   INIT <H'0D0A'>
  2 v_Cc_L  (A4)   INIT <"Cc:">
  2 v_Cc    (A100) INIT  <"deathclaim@aafmaa.com,survivorbenefits@aafmaa.com"> /* bz1
*                        blank for DEV
  2 v_Lbr3  (A2)   INIT  <H'0D0A'>
  2 v_S_L   (A10)  INIT <"Subject:">
  2 v_S     (A100) INIT <"Death Report ">
  2 v_Lbr4  (A2)   INIT <H'0D0A'>
  2 v_Empty (A80)  INIT <" ">
  2 v_Lbr5  (A2)   INIT <H'0D0A'>

1 v_shell         (A) DYNAMIC
1 v_Line          (A79)
1 rc              (N3)
1 nl              (A1)     INIT <H'0D'>
1 dm              (A1)     INIT <H'09'>
1 v_MSG           (A79)
1 v_PN            (I1)     INIT  <22>
1 v_RC            (N3)
1 #i              (N6)
1 v_A10           (A10)
1 v_A70           (A70)
1 v_Env           (A20)

1 MB-SAVE
  2 #CGA-SCR-COMMAND_S    (A2)
  2 #CGA-SCR-ID_S         (N9)
  2 #CGA-SCR-SUFFIX_S     (A3)
  2 #CGA-ORIG-COMMAND_S   (A2)
  2 #CGA-ORIG-ID_S        (N6)
  2 #CGA-ORIG-SUFFIX_S    (A3)
  2 #CGA-ID-G_S           (A6)
  2 #CGA-ISN_S            (P8)
1 l_SnapShot       (L)    INIT <FALSE>
1 l_Part1          (L)    INIT <TRUE>
END-DEFINE
* = = =  = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
  IF F-IS-DEV(<>)                                     /* bz1 ->
     v_FTP_DIR_WIN      := v_FTP_DIR_WIN_DEV
     v_FTP_DIR_BASE_DAL := v_FTP_DIR_BASE_DAL_DEV
     v_Cc               := ' '
     COMPRESS 'Development' GET_DBN(<>) INTO v_Env
  ELSE
     v_FTP_DIR_WIN      := v_FTP_DIR_WIN_PROD
     v_FTP_DIR_BASE_DAL := v_FTP_DIR_BASE_DAL_PROD
     v_Env := 'Production'
  END-IF                                              /* bz1 <-
* = = =  = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = = =
      F_CI-V.
       FIND (1) CI-V WITH CI-V.ID-NUMBER = #CGA-SCR-ID END-FIND
*      IF CI-V.DATE-OF-DEATH = 0 AND #CGA-SCR-COMMAND <> 'DR'
*       ESCAPE ROUTINE
*      END-IF
      #CGA-ISN := *ISN (F_CI-V.)

PERFORM SAVE-G
DEFINE WINDOW WIN   SIZE 22 * 80    BASE 2 / 1   TITLE 'Death processing report'
  CONTROL WINDOW   FRAMED ON
INCLUDE GONERCXX

FIND (1) TRM-V WITH CLERK-ID = #CGA-CLERK END-FIND
v_CN := #CGA-SCR-ID
  IF #CGA-SCR-COMMAND = 'DR'
     l_Snapshot  := TRUE
     File_Pref   := 'D-Snapshot'
     v_XML_Title := 'D-Snapshot for CN:'
     v_Cc        := " "
  END-IF
  IF #CGA-SCR-COMMAND = '22'
    l_Part1 := FALSE
  END-IF

COMPRESS File_Pref "_" v_CN "_" TRM-V.CLERK-ID "_" *DATN ".rtf" INTO File_Name         LEAVING NO SPACE
COMPRESS File_DIR "/" File_Name                                 INTO File_ABS_UNIX     LEAVING NO SPACE
COMPRESS "\\192.168.50.71" v_FTP_DIR_WIN "\" File_Name             INTO File_ABS_WIN  LEAVING NO SPACE
EXAMINE File_ABS_WIN FOR "/" REPLACE "\"
COMPRESS File_DIR "/" "EM_"  File_Name                          INTO File_Email_Name   LEAVING NO SPACE
COMPRESS File_DIR "/" "XML_" File_Name                          INTO File_XML          LEAVING NO SPACE
COMPRESS "DR_" v_CN  "_" *DATN                                  INTO v_FTP_BUILD_DAL   LEAVING NO SPACE

MOVE EDITED v_CN (EM=999999) TO #CGA-ID-G
IF TRM-V.EMAIL <> " " v_To := TRM-V.EMAIL END-IF

* - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
*   v_MSG := "Press Enter and check your email or PF3 to exit"
*   SET KEY PF3
*   INPUT  WINDOW='WIN'  (IP=OFF) WITH TEXT v_MSG
*       /
*    3x "Report for:" v_CN (AD=IO AL=6) "will be send to"
*         #CGA-CLERK  (AD=IO) "-"  INIT-USER (AD=IO)           /
*    3x "Email To :" 3x v_To  (AD=IO AL=55 )                   /
*    3x "Email Cc :" 3x v_Cc  (AD=IO AL=55 )                  //
*    3x "Screens included in this report:" (I)                 /
*    3x "[-I-]: ST* NS RL VW* NC* RE* DP + [-II-]: ST* RE* " (I) "(*)"(I) "- for each policy" //
*         "File  "   File_Name     (AD=O AL=71)                /
*         "Report"   File_ABS_UNIX (AD=O AL=71)                /
*         "XML   "   File_XML      (AD=O AL=71)                /
*         "Email "   File_Email_Name(AD=O AL=71)               /
*         "WIN   "   File_ABS_WIN  (AD=O AL=71)                /
*         "DAL   "   v_FTP_DIR_BASE_DAL  (AD=O AL=71)          /
*         "Build "   v_FTP_BUILD_DAL  (AD=O AL=71)             /
*         "SNAP= " l_Snapshot (AD=OI)  "PART1=" l_Part1 (AD=OI)  "#CGA-SCR-COMMAND=" #CGA-SCR-COMMAND (AD=OI)
*    IF *PF-KEY = 'PF3'
*       #CGA-SCR-COMMAND  := 'GM'  #CGA-ORIG-COMMAND := 'GM'  #CGA-SCR-SUFFIX   := ' '
*       CLOSE PRINTER (22)
*       ESCAPE ROUTINE
*     END-IF
* - - - -  - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
IF v_CN = 0 ESCAPE ROUTINE END-IF
  IF l_Part1
     v_RC := PRT_FN_PUT(<v_PN, File_ABS_UNIX>)
     IF v_RC <> 0         /* Error in Get_Printer_Name
         v_MSG := " Warning: Automated process is not available"
         PERFORM DO-ERROR
         #CGA-SCR-COMMAND := "GM"
         ESCAPE ROUTINE
     END-IF
     DEFINE PRINTER (22) OUTPUT 'LPT22'
     FORMAT (22) PS=60
     IF  l_Snapshot
         WRITE  (22) NOTITLE
           "Snapshot for" v_CN /
           "Requested by" INIT-USER  " " *DATV " " *TIME (AL=8) 3x 'Env:'  v_Env
           DEFINE WORK FILE 11 File_ABS_UNIX
     ELSE
        WRITE  (22) NOTITLE
           15x "Death claims report  CN:" v_CN  5x *DATV " " *TIME (AL=8) /
           "Part 1 - Before 'CI UPD' -  Requested by" INIT-USER 3x 'Env:'  v_Env
     END-IF
  ELSE
     WRITE  (22) NOTITLE
       '='(70) /
       "Part 2  (After 'CI UPD') " 10x *DATV " " *TIME (AL=8) /
  END-IF
  SKIP (22) 2
      IF l_PART1
        PERFORM DO-ST             /* 1 Before
        PERFORM DO-NOTES          /* 2 Before
        PERFORM DO-RL             /* 3 Before
        PERFORM DO-VW             /* 4 Before
        PERFORM DO-NC             /* 5 Before
        PERFORM DO-RE             /* 6 After
        PERFORM DO-DP             /* 7 After
      ELSE
        PERFORM DO-ST             /* 1 After
        PERFORM DO-RE             /* 6 After
      END-IF
* ----------------------------------------------------------------------------------
      IF   l_SnapShot
         PERFORM DO-FTP
         PERFORM DO-FTP-DAL       /*   --db
         PERFORM DO-XML           /*   --db
         PERFORM DO-Email

      END-IF
      IF ( NOT l_PArt1 )  AND ( NOT l_SnapShot )
         PERFORM DO-FTP
         PERFORM DO-FTP-DAL
         PERFORM DO-XML
         PERFORM DO-Email
      END-IF

PERFORM RESTORE-G
ESCAPE ROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-ST
      WRITE (22) '-'(33) "ST" '-'(33)   /                   /* ST  1 Before
      #CGA-SCR-COMMAND  := 'ST'
      #CGA-ORIG-COMMAND := 'ST'
      #CGA-SCR-ID       :=  CI-V.ID-NUMBER
      #CGA-ORIG-ID      :=  CI-V.ID-NUMBER
      #CGA-ISN          :=  *ISN (F_CI-V.)
      #CGA-SCR-SUFFIX   := 'P22'
      #CGA-ORIG-SUFFIX  := 'P22'
      DEFINE PRINTER (8) OUTPUT 'LPT22'
      FETCH RETURN 'C2600PST'
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-NOTES
     NEWPAGE (22)
     WRITE (22) / '-'(33) "NS" '-'(33)   /      /* NS
  FIND NT-V WITH NT-V.ID-NUMBER = v_CN
      IF NO RECORD FOUND
         WRITE (22) "No Notes found for" v_CN
         ESCAPE ROUTINE
      END-NOREC
   FOR #i = 1 TO C*NOTE-TABLE
     COMPRESS v_YYYY(#i) v_MM(#i) v_DD(#i) INTO v_A10   WITH "/"
     WRITE (22) v_A10 NT-V.USER-COMPANY(#i) NT-V.NOTE-CLERK-ID(#i) NT-V.NOTE-TEXT(#i)
    END-FOR
  END-FIND
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-RL                      /* RL
       NEWPAGE (22)  WRITE (22) / '-'(33) "RL" '-'(33)   /
       #CGA-SCR-COMMAND := 'RL'
       #CGA-SCR-SUFFIX  := 'P22'
       #CGA-SCR-ID       :=  CI-V.ID-NUMBER
       #CGA-ORIG-ID      :=  CI-V.ID-NUMBER
       #CGA-ISN          :=  *ISN (F_CI-V.)
       CALLNAT 'C2650SST'
           #CGA-SCR-COMMAND
           #CGA-SCR-ID
           #CGA-SCR-SUFFIX
           #CGA-ORIG-ID
           #CGA-MF-SCR-COMMAND
           #CGA-MF-SCR-PARM-1
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-VW                      /* VW
      NEWPAGE (22)  WRITE (22) / '-'(33) "VW" '-'(33)   /
      #CGA-SCR-COMMAND  := 'VW'
      #CGA-ORIG-COMMAND := 'VW'
      #CGA-SCR-ID       :=  CI-V.ID-NUMBER
      #CGA-ORIG-ID      :=  CI-V.ID-NUMBER
      #CGA-SCR-SUFFIX   := 'P22'
      #CGA-ORIG-SUFFIX  := 'P22'
      #CGA-ISN          :=  *ISN (F_CI-V.)
      FETCH RETURN 'C2H30PVW'
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-NC                      /* NC
      NEWPAGE (22) WRITE (22) / '-'(33) "NC" '-'(33)   /
      #CGA-ORIG-COMMAND := 'NC'
      #CGA-SCR-COMMAND  := 'NC'
      #CGA-SCR-SUFFIX   := 'P22'
      #CGA-ORIG-SUFFIX  := 'P22'
      #CGA-ISN          :=  *ISN (F_CI-V.)
      FETCH RETURN 'C2060PNC'
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-RE                    /* RE
      NEWPAGE (22) WRITE (22) / '-'(33) "RE" '-'(33)   /
       #CGA-SCR-ID       :=  CI-V.ID-NUMBER
       #CGA-ORIG-ID      :=  CI-V.ID-NUMBER
       #CGA-ISN          :=  *ISN (F_CI-V.)
      #CGA-ORIG-COMMAND := 'RE'
      #CGA-SCR-COMMAND  := 'RE'
      #CGA-SCR-SUFFIX   := 'P22'
      #CGA-ORIG-SUFFIX  := 'P22'
      FETCH RETURN 'C2970PRE'
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-DP                      /* DP
      NEWPAGE (22) WRITE (22) / '-'(33) "DP" '-'(33)   /
       #CGA-SCR-ID       :=  CI-V.ID-NUMBER
       #CGA-ORIG-ID      :=  CI-V.ID-NUMBER
       #CGA-ISN          :=  *ISN (F_CI-V.)
      #CGA-SCR-COMMAND   := 'DP'
      #CGA-SCR-SUFFIX    := 'P22'
      FETCH RETURN 'W5300PWP'
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-FTP
CLOSE PRINTER (22)
COMPRESS v_FTP_KSH " " File_ABS_UNIX " " File_Name " " v_FTP_DIR_WIN INTO v_shell
call 'shcmd' v_shell
rc := RET('shcmd')
IF rc <> 0
   v_MSG := " Warning: FTP Error. Automated process is not available"
   PERFORM DO-ERROR
   #CGA-SCR-COMMAND := "GM"
   ESCAPE ROUTINE
END-IF
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-FTP-DAL
COMPRESS v_DAL_KSH " " File_ABS_UNIX " "  v_FTP_DIR_BASE_DAL " " v_FTP_BUILD_DAL INTO v_shell
* WRITE File_ABS_UNIX (AL=79) /                                          /*  --db
* v_FTP_DIR_BASE_DAL (AL=79) / v_FTP_BUILD_DAL (AL=79) / '-' (50)       /*  --db
call 'shcmd' v_shell
rc := RET('shcmd')
IF rc <> 0
   v_MSG := " Warning: FTP Error. Automated process is not available"
   PERFORM DO-ERROR
   #CGA-SCR-COMMAND := "GM"
   ESCAPE ROUTINE
END-IF

END-SUBROUTINE

* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-Email
  DEFINE WORK FILE 11 File_Email_Name TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'NOAPPEND'
  v_From := INIT-USER
  IF TRM-V.EMAIL = " "
    THEN  COMPRESS "AIS Email for " INIT-USER "-" COMMENTS " is missing. Contact Support"
    INTO v_MSG
  END-IF
  COMPRESS v_S  "  CN:" v_CN "  from " COMMENTS  *DATV  INTO v_S
  WRITE WORK FILE 11 VARIABLE v_EMAIL
  WRITE WORK FILE 11 VARIABLE nl
    "This is a SYSTEM GENERATED E-MAIL, please do not respond to the E-MAIL address specified above." nl nl nl
    "To access report file click link below" nl nl
    " " File_ABS_WIN nl nl

  IF F-IS-DEV(<>)                                                                                   /* bz1 ->
   COMPRESS  "http://auc.testing.local/dal/?CN=" v_CN  INTO v_MSG LEAVING NO SPACE  /* --DEV
   WRITE WORK FILE 11 VARIABLE nl  "Link to TEST DAL:  " v_MSG                                      /* --DEV
  ELSE
    COMPRESS  "http://auc.aafmaa.com/dal/?CN=" v_CN  INTO v_MSG LEAVING NO SPACE
    WRITE WORK FILE 11 VARIABLE nl "Link to Production DAL:  " v_MSG
  END-IF                                                                                             /* bz1 <-

  CLOSE WORK FILE 11
  COMPRESS "/usr/lib/sendmail -t -oi < " File_Email_Name INTO v_shell

   call 'shcmd' v_shell
   rc := RET('shcmd')
   IF rc <> 0
       WRITE  NOTITLE ' Error - sending Email ' ///
       ' Please look for report file in the folder "Death_Claims"  '
   END-IF
END-SUBROUTINE
DEFINE SUBROUTINE SAVE-G
  #CGA-SCR-COMMAND_S     := #CGA-SCR-COMMAND
  #CGA-SCR-ID_S          := #CGA-SCR-ID
  #CGA-SCR-SUFFIX_S      := #CGA-SCR-SUFFIX
  #CGA-ORIG-COMMAND_S    := #CGA-ORIG-COMMAND
  #CGA-ORIG-ID_S         := #CGA-ORIG-ID
  #CGA-ORIG-SUFFIX_S     := #CGA-ORIG-SUFFIX
  #CGA-ID-G_S            := #CGA-ID-G
  #CGA-ISN_S             := #CGA-ISN
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE RESTORE-G
  #CGA-SCR-COMMAND     := #CGA-SCR-COMMAND_S
  #CGA-SCR-ID          := #CGA-SCR-ID_S
  #CGA-SCR-SUFFIX      := #CGA-SCR-SUFFIX_S
  #CGA-ORIG-COMMAND    := #CGA-ORIG-COMMAND_S
  #CGA-ORIG-ID         := #CGA-ORIG-ID_S
  #CGA-ORIG-SUFFIX     := #CGA-ORIG-SUFFIX_S
  #CGA-ID-G            := #CGA-ID-G_S
  #CGA-ISN             := #CGA-ISN_S
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-ERROR
    WRITE NOTITLE
          5/5 "Sorry, erorr ocured during automated process"
          7/5 v_MSG
          9/5 "Please procceed with Print-Screen"
          15/5 "Press ENTER to continue" (I)
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-XML
DEFINE WORK FILE 12 File_XML  Type 'ASCII-COMPRESSED'
MOVE EDITED *DATN (EM=9999/99/99) TO v_A10
WRITE WORK FILE 12 VARIABLE
      "<DOCUMENTS>"      
WRITE WORK FILE 12 VARIABLE
      "<DOCUMENT>"
WRITE WORK FILE 12 VARIABLE
     "<DOCTITLE>DTH</DOCTITLE>"
WRITE WORK FILE 12 VARIABLE
     "<CN>" v_CN              "</CN>"
WRITE WORK FILE 12 VARIABLE
      "<SUBMITDATE>" v_A10            "</SUBMITDATE>"
COMPRESS File_Name INTO v_A70 LEAVING NO SPACE
EXAMINE v_A70 FOR ".rtf" REPLACE ".pdf"
COMPRESS "<FILENAME>" v_A70 "</FILENAME>" INTO v_A70 LEAVING NO SPACE
WRITE WORK FILE 12 VARIABLE
      v_A70
COMPRESS "<DESCRIPTION>" v_XML_Title v_CN "</DESCRIPTION>" INTO v_XML_Title LEAVING NO SPACE
WRITE WORK FILE 12 VARIABLE
      v_XML_Title
WRITE WORK FILE 12 VARIABLE
   '<MEMBERACCESSIBLE>FALSE</MEMBERACCESSIBLE>'
WRITE WORK FILE 12 VARIABLE
    "</DOCUMENT>"
WRITE WORK FILE 12 VARIABLE
    "</DOCUMENTS>"      

CLOSE WORK FILE 12
COMPRESS v_FTP_DIR_BASE_DAL "/"  v_FTP_BUILD_DAL   INTO v_FTP_BUILD_DAL  LEAVING NO SPACE
*  WRITE File_XML (AL=79) /                                                         /*  --db
*  File_XML_WIN (AL=79) / v_FTP_BUILD_DAL (AL=79) / '-' (50)                        /*  --db
COMPRESS v_FTP_KSH " " File_XML " " File_XML_WIN " " v_FTP_BUILD_DAL INTO v_shell  /* <<<
call 'shcmd' v_shell
rc := RET('shcmd')

IF rc <> 0
   WRITE  " Warning: DAL FTP Error." // "Please Notify support"

END-IF
END-SUBROUTINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
DEFINE SUBROUTINE DO-TEST
WRITE
  "#CGA-SCR-COMMAND"      #CGA-SCR-COMMAND
  "#CGA-ORIG-COMMAND"     #CGA-ORIG-COMMAND /
  "#CGA-SCR-SUFFIX"       #CGA-SCR-SUFFIX
  "#CGA-ORIG-SUFFIX"      #CGA-ORIG-SUFFIX /
  "#CGA-SCR-ID"           #CGA-SCR-ID
  "#CGA-ORIG-ID"          #CGA-ORIG-ID
  "#CGA-ID-G"             #CGA-ID-G
  "#CGA-ISN"              #CGA-ISN
END-SUBROUTINE

END
