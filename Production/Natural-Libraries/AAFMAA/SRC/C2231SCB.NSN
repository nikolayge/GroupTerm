* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PGM-ID: C2231SCB       PROCESES ST/FIRS TRANS REGISTER
DEFINE DATA
*******************************************
*                       MODIFICATION LOG
* USER DATE      TAG  REASON
* DRW  08112005  DW1  Add Special-code to tr log
* DRW  08152005  DW2  Add DEPD Status Code to update test
* DRW  03312006  DW3  Add AFSC/AAFMAA sync processing
* DRW  04212006  DW4  Add FMR SPS INFO & SPS STATUS & DELETE BASD ETC
* DRW  04223006  DW5  Build Nx's on sync'd fields of TRLOG
* DRW  08233006  DW6  Add Access-Auth-Code Updating
* DRW  10033006  DW7  Remove NX412 processing
* DRW  09112007  DW8  Add Date-Retired Updating
* DRW 01/17/2008 DW9  Bypass SpS NX to AFSC if not G/F
* RSE 09/29/2011 RE1  Get description of Status Codes
* TMT 07/15/2014 TT1  Contact Normalization - DOB and Sex   
*******************************************
PARAMETER                       /* CANNOT EXCEED 40 ADDRESSES
*
1 #ST-ISN                (P8)
1 #ST-REC-CHGD           (L)
1 #FIRS-ISN              (P8)
1 #FIRS-REC-CHGD         (L)
1 #ITR1                  (I2)
1 #ITR2                  (I2)
1 #CN                    (N6)
1 #PROCESS-IND           (A1) /* PM - 06/2000 - SPLIT COMPANIES
1 #NAME                  (A25)
1 #LAST-FM-DATE          (N8)
1 #CGA-CLERK             (A3)
1 #HOLD-TIME             (P7)
1 #DEPN-CID              (N8/10)
1 #DEPN-STATUS-CD        (A1/10)
1 #STATUS-CODE-SP         (A1)    /* DW4
1 #FORMER-SPOUSE-STATUS       (A1)
1 #CHIEF-OF-STAFF-IND    (A1)   /* DW1
1 #ACCESS-AUTH-CODE      (A1)   /* DW6
1 #DATE-RETIRED          (N8)   /* DW8
*
LOCAL USING GONERLXX
LOCAL USING GPRA0004
LOCAL
1 FR-V VIEW OF A-FIRS
 2 C*DEPENDENT-TABLE
 2 DEPN-CONTACT-ID (99)
 2 DEPN-STATUS-CD (99)
 2 DEPN-NAME (99)
 2 DEPN-DATE-OF-BIRTH (99)
 2 REDEFINE DEPN-DATE-OF-BIRTH
   3 DEPN-DATE-OF-BIRTH-A (A8/99)
 2 DEPN-SSN (99)
*
1 ST-V VIEW OF A-STATUS
 2 NAME-SP
 2 STATUS-CODE-SP
*  2 DATE-OF-BIRTH-SP
*  2 REDEFINE DATE-OF-BIRTH-SP
*   3 DATE-OF-BIRTH-SP-A (A8)
 2 SSN-SP
 2 REDEFINE SSN-SP
  3 SSN-SP-A   (A9)
 2 DATE-OF-MARRIAGE
 2 REDEFINE DATE-OF-MARRIAGE
  3 DATE-OF-MARRIAGE-A   (A8)
 2 DATE-OF-DIVORCE
 2 REDEFINE DATE-OF-DIVORCE
  3 DATE-OF-DIVORCE-A   (A8)
 2 FORMER-SPOUSE-NAME
 2 REDEFINE FORMER-SPOUSE-NAME
  3 FORMER-SPOUSE-NAME-A   (A8)
 2 FORMER-SPOUSE-STATUS
*  2 FORMER-SPOUSE-DT-OF-BIRTH
*  2 REDEFINE FORMER-SPOUSE-DT-OF-BIRTH
*   3 FORMER-SPOUSE-DT-OF-BIRTH-A (A8)
 2 FORMER-SPOUSE-SSN
 2 REDEFINE FORMER-SPOUSE-SSN
  3 FORMER-SPOUSE-SSN-A   (A9)
 2 FORMER-SPOUSE-DATE-OF-MARRIAGE
 2 REDEFINE FORMER-SPOUSE-DATE-OF-MARRIAGE
  3 FORMER-SPOUSE-DATE-OF-MARRIAGE-A (A8)
 2 FORMER-SPOUSE-DATE-OF-DIVORCE
 2 REDEFINE FORMER-SPOUSE-DATE-OF-DIVORCE
  3 FORMER-SPOUSE-DATE-OF-DIVORCE-A (A8)
 2 CHIEF-OF-STAFF-IND    /* DW1
 2 ACCESS-AUTH-CODE      /* DW6
 2 DATE-RETIRED          /* DW8
 2 MEMBER-CONTACT-ID
 2 SPOUSE-CONTACT-ID
 2 FORMER-SPOUSE-CONTACT-ID
*
1 CNT-V VIEW OF A-CONTACTS
  2 FIRST-NAME
  2 MIDDLE-NAME
  2 LAST-NAME
  2 SUFFIX
*
1 #I          (I2)
1 #I1         (I2)
1 #I2         (I2)
1 #SAVE-FIELD (A1)    /* RE1
1 #OLD-DESC   (A100)  /* RE1
1 #NEW-DESC   (A100)  /* RE1
END-DEFINE
* * * * * * * * * *
* INCLUDE GONERCXX
*
* * * * * * * GET VIEW TO UPDATE AND CHECK CONCURRENT UPDATING * * *
*
GET ST-V #ST-ISN
*
IF #FIRS-ISN NE 0
  GET FR-V #FIRS-ISN
END-IF
*
* * * * * * * * * * TRANS REG PROCESSING * * * * * * * *
*
IF ACCESS-AUTH-CODE = ' '      /* DW6
  MOVE 'N' TO ACCESS-AUTH-CODE
END-IF
*
IF #ACCESS-AUTH-CODE = ' '      /* DW6
  MOVE 'N' TO #ACCESS-AUTH-CODE
END-IF
*
IF ACCESS-AUTH-CODE NE #ACCESS-AUTH-CODE   /* DW6
  MOVE TRUE TO #ST-REC-CHGD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := 'CB-ACCESS'
  #OLD-DATA(#I)          := ACCESS-AUTH-CODE
  #NEW-DATA(#I)          := #ACCESS-AUTH-CODE
END-IF
*
IF CHIEF-OF-STAFF-IND NE #CHIEF-OF-STAFF-IND   /* DW1
  MOVE TRUE TO #ST-REC-CHGD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := 'CB-SP-CD'
  #OLD-DATA(#I)          := CHIEF-OF-STAFF-IND
  #NEW-DATA(#I)          := #CHIEF-OF-STAFF-IND
END-IF
*
IF STATUS-CODE-SP NE #STATUS-CODE-SP
  MOVE TRUE TO #ST-REC-CHGD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := 'CB-SPS'
  FIND CNT-V WITH CONTACT-ID = SPOUSE-CONTACT-ID
    #OLD-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    #NEW-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  END-FIND
  MOVE STATUS-CODE-SP TO #SAVE-FIELD                                                     /* RE1 START
  #OLD-DESC          := FUNCTION-CODE-A-DESC(<'032',#SAVE-FIELD>)     
  #NEW-DESC          := FUNCTION-CODE-A-DESC(<'032',#STATUS-CODE-SP>)
  COMPRESS #OLD-DATA(#I)  '  STATUS-CD' STATUS-CODE-SP '-' #OLD-DESC INTO #OLD-DATA(#I)               
  COMPRESS #NEW-DATA(#I)  '  STATUS-CD' #STATUS-CODE-SP '-' #NEW-DESC INTO #NEW-DATA(#I)                   
*  COMPRESS #OLD-DATA(#I)  '  STATUS-CD'  STATUS-CODE-SP INTO #OLD-DATA(#I)
*  COMPRESS #NEW-DATA(#I)  '  STATUS-CD'  #STATUS-CODE-SP INTO #NEW-DATA(#I)             /* RE1 END
END-IF
*
IF FORMER-SPOUSE-STATUS NE #FORMER-SPOUSE-STATUS
  MOVE TRUE TO #ST-REC-CHGD
  #I := #I + 1
  #FIELD-DESCRIPTION(#I) := 'CB-FSPS'
  FIND CNT-V WITH CONTACT-ID = FORMER-SPOUSE-CONTACT-ID
    #OLD-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
    #NEW-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  END-FIND
  MOVE FORMER-SPOUSE-STATUS TO #SAVE-FIELD                                                     /* RE1 START
  #OLD-DESC          := FUNCTION-CODE-A-DESC(<'032',#SAVE-FIELD>)     
  #NEW-DESC          := FUNCTION-CODE-A-DESC(<'032',#FORMER-SPOUSE-STATUS>)
  COMPRESS #OLD-DATA(#I)  '  STATUS-CD' FORMER-SPOUSE-STATUS '-' #OLD-DESC INTO #OLD-DATA(#I)               
  COMPRESS #NEW-DATA(#I)  '  STATUS-CD' #FORMER-SPOUSE-STATUS '-' #NEW-DESC INTO #NEW-DATA(#I)                   
*  COMPRESS #OLD-DATA(#I)  '  STATUS-CD'  FORMER-SPOUSE-STATUS INTO #OLD-DATA(#I)
*  COMPRESS #NEW-DATA(#I)  '  STATUS-CD'  #FORMER-SPOUSE-STATUS INTO #NEW-DATA(#I)                 /* RE1 END
END-IF
*
FOR #I1 1 TO 10
  IF #DEPN-CID(#I1) = 0
     ESCAPE BOTTOM
  END-IF
  FOR #I2 1 C*DEPENDENT-TABLE
    IF #DEPN-CID(#I1) = DEPN-CONTACT-ID(#I2) AND #DEPN-STATUS-CD (#I1) NE DEPN-STATUS-CD (#I2)   /* DW2
      MOVE TRUE TO #FIRS-REC-CHGD
      #I := #I + 1
      #FIELD-DESCRIPTION(#I) := 'CB-DEPN'
      FIND CNT-V WITH CONTACT-ID = #DEPN-CID(#I1)
        #OLD-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
        #NEW-DATA(#I)          := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
      END-FIND
      MOVE DEPN-STATUS-CD (#I2)  TO #SAVE-FIELD                                                     /* RE1 START
      #OLD-DESC          := FUNCTION-CODE-A-DESC(<'059',#SAVE-FIELD>)     
      #NEW-DESC          := FUNCTION-CODE-A-DESC(<'059',#DEPN-STATUS-CD (#I1)>)
      COMPRESS #OLD-DATA(#I)  '  STATUS-CD' DEPN-STATUS-CD (#I2)  '-' #OLD-DESC INTO #OLD-DATA(#I)               
      COMPRESS #NEW-DATA(#I)  '  STATUS-CD' #DEPN-STATUS-CD (#I1) '-' #NEW-DESC INTO #NEW-DATA(#I)                   
*      COMPRESS #OLD-DATA(#I) '  STATUS-CD' DEPN-STATUS-CD(#I2) INTO #OLD-DATA(#I)
*      COMPRESS #NEW-DATA(#I) '  STATUS-CD' #DEPN-STATUS-CD(#I1) INTO #NEW-DATA(#I)                 /* RE1 END
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FOR
*
* * * * * * * * WRITE OUT TRANS REG * * * * * * * *
*
IF #FIRS-REC-CHGD OR #ST-REC-CHGD
  MOVE #CN TO #TR-ID-NUMBER
  FIND CNT-V WITH CONTACT-ID = MEMBER-CONTACT-ID
    #TR-NAME := FUNCTION-DISP-CT-NAME(<FIRST-NAME,MIDDLE-NAME,LAST-NAME,SUFFIX>)
  END-FIND
  MOVE #LAST-FM-DATE TO #TR-DATE-LAST-UPDATE
  MOVE #CGA-CLERK    TO #TR-CLERK-ID  
  CALLNAT 'GPRN0004'
    #TR-FIELDS #SYNC-FIELD-TABLE(*)
    #OLD-DATA-TABLE(*) #NEW-DATA-TABLE(*)
  RESET #FIELD-DESCRIPTION(*)
END-IF
*
END
