* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
* PROGRAM-ID: ATHPMBRL - THIS PROGRAM PRODUCES THE ANNUAL MEMBER LIST
*      BY ALPHA ORDER. IT WILL IGNORE MEMBERS WITH 0 SHIPS AND THE
*      FOLLOWING ID'S:
*          CN 27311
*          CN 62934
*          CN 62363
*   THIS PROGRAM IS INTENDED FOR BATCH ONLY
*
DEFINE DATA
LOCAL
*
* * * *   S T A T U S   F I L E   * * * * * * * * * * * * * * *
1 ST-V VIEW OF A-STATUS
  2 ID-NUMBER
  2 NAME           (A25)
  2 ANNUAL-RPT-FLAG
  2 NUMBER-MEMBERSHIPS
*
1 #PRINT-TABLE
  2 #PRINT-LINE (78)
    3 #COLUMN-NAME (4)
      4 #PC-NAME (A25)
      4 #PG-FIL (A2)
1 #COL   (I2)
1 #ROW   (I4)
1 #EOF   (L) INIT <FALSE>
*
END-DEFINE
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - *
FORMAT(1)  PS=81
           LS=133
*
ASSIGN #COL = 1
ASSIGN #ROW = 1
WRITE (1) 'AAFMAA Alpha Name Listing as of' *DATX
*
READ ST-V BY NAME-KEY
* IF ID-NUMBER > 90000   /* last cn of applicable year
*   ESCAPE TOP
* END-IF
*
  IF   (NUMBER-MEMBERSHIPS NE 0)
   AND (NOT ID-NUMBER = 27311 OR = 62934 OR = 62363)
    MOVE NAME TO #PC-NAME (#ROW, #COL)
    ADD 1 TO #ROW
*
    IF #ROW > 78
      ADD 1 TO #COL
*
      IF #COL > 3    /* chged from 3 to 4 to fit on std c=2718
        PERFORM PRINT-PAGE
        RESET #PRINT-TABLE
        WRITE (1) 'AAFMAA Alpha Name Listing as of' *DATX
        MOVE 1 TO #COL
      END-IF
*
      MOVE 1 TO #ROW
    END-IF
  END-IF
END-READ
*
PERFORM PRINT-PAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
DEFINE SUBROUTINE PRINT-PAGE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
FOR #ROW 1 TO 78
  WRITE (1) 1T #PC-NAME (#ROW, 1) 28T #PC-NAME (#ROW, 2)
           55T #PC-NAME (#ROW, 3) 82T #PC-NAME (#ROW, 4)
END-FOR
*
END-SUBROUTINE
*
END
