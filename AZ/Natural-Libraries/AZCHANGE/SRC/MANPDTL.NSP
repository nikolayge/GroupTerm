* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Program MANPDTL.
/**
/** :author nguentchev
/* Paging is Down/Up (F8/F7) and Right/Left (F6/F5)
/* Data are in #AMOUNTS (A10/10,12)
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
1 #CALCULATED
  2 #NUMBER-WD       (I4)
  2 #NUM-POL-WD      (I4)
  2 #TOTAL-POL-WD    (N7.2)
  2 #RESIDUAL        (N7.2)
  2 #PAID            (N7.2)
  2 #TOTAL-WD        (N7.2)
  2 #LAST-POL-WD     (N7.2)
  2 #LAST-WITHDRAWAL (N7.2)
  2 #GROSS           (N7.2)
  2 #GROSS-TAXABLE   (N7.2)
  2 #PRO-RATE-MP     (N7.2)
1 Title (A) DYNAMIC
1 BLANK (A1) CONST <' '>
1 #ADDRESS   (A43/4)  /* Like in ST
1 #MSG       (A60)
1 #POS       (I4)
1 #ERRORS    (L)
1 #CONTROL-VARS     (C/10,12)
1 #CV-SAVE          (C/10,12)
1 #SELECT-CV        (C/10)
1 #ERROR-NBR        (I4)
1 #SCR
  2 #AMOUNTS          (A10/10,12)
  2 #AMOUNTS-N        (N7.2/12)
  2 #CODE-DESCRIPTION (A24)
  2 #POLICY-SEL       (A2/10)
  2 #ANN-STATUS       (A1/10)
1 #TI           (I2)
1 #ISN          (P8/11)
1 #ISSUE-DATE-A (N8/11)
/*  Screen mapping
1 #TITLES (A10/2,12) CONST
  (1,1) <'Policy'>
  (1,2) <'Principal'>
  (1,3) <'Monthly'>
  (1,4) <'1099'>
  (1,5) <'13th'>
  (1,6) <'Reserve'>
  (1,7) <'Residual'>
  (1,8) <'Policy'>
  (1,9) <'Annuity'>
  (1,10) <'Monthly'>

  (2,1)  <'Number'>
  (2,3)  <'Payment'>
  (2,4)  <'Amount'>
  (2,5)  <'Check'>
  (2,8)  <'Iss date'>
  (2,9) <'Start'>
  (2,10) <'Tax Amt'>
1 #PRINCIPAL  (I2) CONST <2>
1 #MONTHLY    (I2) CONST <3>
1 #TOTPAID    (I2) CONST <4>
1 #T13TH      (I2) CONST <5>
1 #RESAMT     (I2) CONST <6>
1 #RESDUAL    (I2) CONST <7>
1 #ISSDATE    (I2) CONST <8>
1 #STARTDT    (I2) CONST <9>
1 #TAXAMT     (I2) CONST <10>
/*
1 #ANNU-DATE   (A8) 1 REDEFINE #ANNU-DATE
  2 YYYY  (A4)
1 #ISSUE-DATE  (N8)
1 #DOB               (A10)
1 #DOD               (A10)
1 #JDate             (A5) 1 REDEFINE #JDATE
  2 YY               (A2)
  2 #DDD             (N3)
1 #PR                (N0.5)
1 #AGE (N03.0)
1 #TODAY-A      (A10)
1 #SCR-TERMID   (A12)
1 #TODAY (N8) 1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
*
1 ANNU-DATA (1:10)
  2 POLICY-NUMBER     (A16)
  2 CONTACT-ID        (N8.0)
  2 REFERENCE-CN      (N6.0)
  2 ANNUITANT-STATUS  (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION   (N7.2)
  2 PRINCIPAL         (N7.2)
  2 ANNUAL-PAYMENT    (N7.2)
  2 RESERVE-AMOUNT    (N7.2)
  2 WITHDRAWALS       (N7.2)
  2 INITIAL-PAYMENT   (N7.2)
  2 INITIAL-INTEREST  (N7.2)
  2 DEPOSIT           (N7.2)
  2 T13TH-CHECK       (N7.2)
  2 TAXABLE           (N7.2)
  2 WITHDRAWAL-DATE   (N8)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8.0) 2 REDEFINE ONE-MP-DATE
    3 ONE-YYYY (N4)
    3 ONE-MM   (N2)
    3 ONE-DD   (N2)
  2 START-DATE      (N8) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE (N8.0)
*
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER    (A16)
  2 CONTACT-ID       (N8.0)
  2 REFERENCE-CN     (N6.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION (N7.2)
  2 T13TH-CHECK     (N7.2)
  2 PRINCIPAL       (N7.2)
  2 ANNUAL-PAYMENT  (N7.2)
  2 RESERVE-AMOUNT  (N7.2)
  2 WITHDRAWALS     (N7.2)
  2 WITHDRAWAL-DATE   (N8)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8.0)
  2 INITIAL-PAYMENT (N7.2)
  2 TAXABLE         (N7.2)
  2 START-DATE (N8.0) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE (N8)
  2 NOTES (A70/1:2)
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 LAST-UPD-TIME (N7.0)
*
1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-TYPE (A1)
  2 ID-NUMBER  (N6)
  2 CONTACT-ID (N8)
  2 GENDER-CD  (A1)
  2 SSN        (N9)
  2 DATE-OF-BIRTH (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 YYYY (N4)
    3 MM   (N2)
    3 DD   (N2)
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 DEATH-YYYY (A4)
    3 DEATH-MM   (A2)
    3 DEATH-DD   (A2)
*
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 STATUS        (9)
  2 POLICY-ID     (9)
  2 PLAN          (9)
  2 ISSUE-DATE    (9) 2 REDEFINE ISSUE-DATE
    3 ISSUE-DATE-A (9)
      4 YYYY (A4)
      4 MM   (A2)
      4 DD   (A2)
  2 FACE-AMOUNT   (9)
*
1 #MSG-LIST           (A/4)  DYNAMIC INIT <
  'Not numeric value for Principal',
  'Incorrect settlement option',
  'No policy to annuitize',
  'Only "IO" available for trusts'
  >
1 J  (I2)
1 K  (I2)
1 L  (I2)
1 #SELECT  (I2)
END-DEFINE
*
SET KEY PF3 NAMED "Exit"
SET KEY PF4 NAMED 'Detl'
SET KEY PF7 NAMED 'Left'
SET KEY PF8 NAMED 'Right'
SET KEY PF2 NAMED 'Wdrw'
*
IF #CGA-SCR-SUFFIX IS (N2)
  FETCH 'MANPYYYY'
END-IF

MOVE *DAT4U TO #TODAY-A
COMPRESS *INIT-USER '-' #CGA-CLERK INTO #SCR-TERMID LEAVING NO
MOVE *DATN TO #TODAY
*
MOVE '*' TO #CGA-SCR-COMMAND
CONTACT-V.CONTACT-ID := #CGA-SCR-ID
#CGA-SCR-ID := #CGA-ORIG-ID   /* PASSED CN
MOVE LEFT JUSTIFIED #CGA-SCR-COMMAND TO #CGA-SCR-COMMAND
CALLNAT 'ADRN0016' CONTACT-V.CONTACT-ID
  #ADDRESS (*)
*
FIND CONTACT-V WITH CONTACT-ID = CONTACT-V.CONTACT-ID
  IF NO RECORDS FOUND
    #MSG := 'No contact record found'
    ESCAPE BOTTOM
  END-NOREC
  IF CONTACT-V.CONTACT-TYPE NE 'O'
    #AGE := #TODAY.#YYYY - CONTACT-V.YYYY
    IF #TODAY.#MM < CONTACT-V.MM
      #AGE := #AGE - 1
    ELSE IF #TODAY.#MM = CONTACT-V.MM AND #TODAY.#DD < CONTACT-V.DD
        #AGE := #AGE - 1
      END-IF
    END-IF
    IF CONTACT-V.DATE-OF-DEATH > 0
      COMPRESS CONTACT-V.DEATH-MM CONTACT-V.DEATH-DD CONTACT-V.DEATH-YYYY
        INTO #DOD WITH DELIMITER '/'
    ELSE
      RESET #DOD
    END-IF
  END-IF
  COMPRESS CONTACT-V.MM CONTACT-V.DD CONTACT-V.YYYY INTO #DOB WITH DELIMITER '/'
END-FIND
CALLNAT 'MANNHWD'
  CONTACT-V.CONTACT-ID
  #TODAY
  #TOTAL-WD
  #NUMBER-WD
  #LAST-WITHDRAWAL
*
PERFORM POPULATE-DATA
#POS := POS (#AMOUNTS (1,1))
#TI := 1
*
RPL.
REPEAT
  MOVE (AD=U) TO #SELECT-CV (*)
  MOVE (AD=P) TO #CONTROL-VARS (*,*)
  INPUT TEXT #MSG MARK #POS USING MAP 'MANMDTL'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF2'
      #CGA-SCR-ID := CONTACT-V.CONTACT-ID
      RESET #CGA-POLICY-NUMBER
      FETCH 'MANPHSTF'
    VALUE 'PF3'
*     ESCAPE BOTTOM(RPL.)
      FETCH 'C3000PAZ'
    VALUE 'PF5','PF7'  /* LEFT
      PERFORM SHIFT-LEFT
    VALUE 'PF6','PF8'  /* RIGHT
      PERFORM SHIFT-RIGHT
    VALUE 'PF4'
      PERFORM GET-SELECT
      PERFORM POPULATE-DATA
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      PERFORM DATA-VALIDATION
      IF #ERRORS
        ESCAPE TOP
      END-IF
      PERFORM ANNUITY-EDITS
      PERFORM POPULATE-DATA
      ESCAPE TOP
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
**IF #CGA-SCR-COMMAND NE '*'
FETCH 'G1000PXX'
**END-IF
*
DEFINE POPULATE-DATA
/*     -------------
* We may have more than one record for the same annuitant
*
RESET #ISN (*) #AMOUNTS (*,*)
  K #AMOUNTS-N (*) J
FIND POLICY WITH ID-NUMBER = #CGA-SCR-ID
  IF C*ACTIVE-SEGMENTS = 0
    /* Populate data from Annuitants data
    FIND ANNUITANTS WITH CONTACT-ID = CONTACT-V.CONTACT-ID
      ADD 1 TO K
      ADD 1 TO J
      IF K > 9
        ESCAPE BOTTOM
      END-IF
      #ISN (K) := *ISN (0296)
      MOVE BY NAME ANNUITANTS TO ANNU-DATA (K)
      #AMOUNTS (K,1) := ANNUITANTS.POLICY-NUMBER
      COMPRESS ANNUITANTS.MM
        ANNUITANTS.DD
        ANNUITANTS.YYYY
        INTO #AMOUNTS (K,#ISSDATE) WITH DELIMITER '/'
      POLICY.ISSUE-DATE (J) := ANNUITANTS.START-DATE
      PERFORM POPULATE-SCR-AMOUNTS
    END-FIND
  END-IF
  FOR J 1 TO C*ACTIVE-SEGMENTS
    IF #CGA-SCR-SUFFIX = 'AL'
        AND NOT POLICY.PLAN (J) EQ 'AL' OR= 'AL2'
      ESCAPE TOP
    END-IF
    FOR L 1 TO K
      IF POLICY.POLICY-ID (J) = #AMOUNTS (L,1)
        ESCAPE BOTTOM  /* DUPLICATE POLICY NUMBER
      END-IF
    END-FOR
    IF L <= K
      ESCAPE TOP
    END-IF

    ADD 1 TO K
    IF K > 9
      ESCAPE BOTTOM
    END-IF
    COMPRESS POLICY.MM (J)
      POLICY.DD (J)
      POLICY.YYYY (J)
      INTO #AMOUNTS (K,#ISSDATE) WITH DELIMITER '/'
    #AMOUNTS (K,1) := POLICY.POLICY-ID (J)
    #ISSUE-DATE-A (K) := POLICY.ISSUE-DATE (J)
/*
    FIND ANNUITANTS WITH CONTACT-ID = CONTACT-V.CONTACT-ID
        AND POLICY-NUMBER = POLICY.POLICY-ID (J)
      IF NO RECORDS FOUND  /* No annuitant record for this policy
        IF #CGA-SCR-SUFFIX = 'AL'
          PERFORM POPULATE-CI
        END-IF
        ESCAPE BOTTOM
      END-NOREC
      #ISN (K) := *ISN (0338)
      MOVE BY NAME ANNUITANTS TO ANNU-DATA (K)
      PERFORM POPULATE-SCR-AMOUNTS
    END-FIND
  END-FOR
END-FIND
*
END-SUBROUTINE
*
DEFINE POPULATE-CI
/*     -----------
FIND ANNUITANTS WITH POLICY-NUMBER = POLICY.POLICY-ID (J)
  IF NO RECORDS FOUND  /* No annuitant record for this policy
    #POLICY-SEL (K) := 'A2'
    ANNU-DATA.SETTLEMENT-OPTION (K) := 'A2'
    ANNU-DATA.ANNUITANT-STATUS  (K) := 'A'
    ANNU-DATA.POLICY-NUMBER     (K) := POLICY.POLICY-ID (J)
    ANNU-DATA.CONTACT-ID        (K) := CONTACT-V.CONTACT-ID
    ANNU-DATA.REFERENCE-CN      (K) := #CGA-SCR-ID
    ANNU-DATA.RESERVE-AMOUNT    (K) := POLICY.FACE-AMOUNT (J)
    ANNU-DATA.PRINCIPAL         (K) := POLICY.FACE-AMOUNT (J)
    ANNU-DATA.START-DATE        (K) := POLICY.ISSUE-DATE (J)
    PERFORM POPULATE-SCR-AMOUNTS
    MOVE BY NAME ANNU-DATA (K) TO ANNUITANTS
    STORE ANNUITANTS
    #ISN (K) := *ISN (0370)
    ESCAPE BOTTOM
  END-NOREC
  #ISN (K) := *ISN (0357)
  MOVE BY NAME ANNUITANTS TO ANNU-DATA (K)
  GET ANNUITANTS #ISN (K)
  ANNUITANTS.CONTACT-ID := CONTACT-V.CONTACT-ID
  UPDATE (0376)
  PERFORM POPULATE-SCR-AMOUNTS
END-FIND
END-SUBROUTINE
*
DEFINE POPULATE-SCR-AMOUNTS
/*     --------------------
CALLNAT 'MANNGET'
  #ISN (K)
  #TODAY
  #CALCULATED
  ANNU-DATA (K)
MOVE ANNU-DATA.SETTLEMENT-OPTION (K) TO #POLICY-SEL (K)
*                    Status
#ANN-STATUS (K) := ANNUITANTS.ANNUITANT-STATUS
/*                   Principal
MOVE EDITED ANNU-DATA.PRINCIPAL (K)  (EM=ZZZ,ZZ9.99)
  TO #AMOUNTS (K,#PRINCIPAL)
ADD ANNU-DATA.PRINCIPAL (K)  TO #AMOUNTS-N (#PRINCIPAL)
/*                   Monthly payments
IF ANNU-DATA.ONE-MONTH-PENSION (K) > 0
    AND ANNU-DATA.ONE-MM (K) = #TODAY.#MM
  MOVE EDITED ANNU-DATA.ONE-MONTH-PENSION (K) (EM=ZZZ,ZZ9.99)
    TO #AMOUNTS (K,#MONTHLY)
  ADD ANNU-DATA.ONE-MONTH-PENSION (K) TO #AMOUNTS-N (#MONTHLY)
ELSE
  MOVE EDITED ANNU-DATA.MONTHLY-PENSION(K) (EM=ZZZ,ZZ9.99)
    TO #AMOUNTS (K,#MONTHLY)
  ADD ANNU-DATA.MONTHLY-PENSION(K) TO #AMOUNTS-N (#MONTHLY)
END-IF
/*                   Total paid
MOVE EDITED #GROSS-TAXABLE (EM=ZZZ,ZZ9.99) TO #AMOUNTS (K,#TOTPAID)
ADD #GROSS-TAXABLE TO #AMOUNTS-N (#TOTPAID)
/*                   13th Check
MOVE EDITED ANNU-DATA.T13TH-CHECK(K) (EM=NZZ,ZZ9.99)
  TO #AMOUNTS (K,#T13TH)
ADD ANNU-DATA.T13TH-CHECK(K) TO #AMOUNTS-N (#T13TH)
/*                   Start date
COMPRESS ANNU-DATA.MM (K) ANNU-DATA.DD (K) ANNU-DATA.YYYY (K)
  INTO #AMOUNTS (K,#STARTDT) WITH DELIMITER '/'
*
IF ANNUITANTS.START-DATE = 0
    AND ANNU-DATA.START-DATE (K) = 0
  ANNU-DATA.START-DATE (K) := #TODAY
END-IF
/*
MOVE EDITED #RESIDUAL (EM=ZZZ,ZZ9.99) TO #AMOUNTS (K,#RESDUAL)
ADD #RESIDUAL TO #AMOUNTS-N (#RESDUAL)
/*
MOVE EDITED ANNU-DATA.RESERVE-AMOUNT (K) (EM=ZZZ,ZZ9.99)
  TO #AMOUNTS (K,#RESAMT)
ADD ANNU-DATA.RESERVE-AMOUNT (K) TO #AMOUNTS-N (#RESAMT)
/*
MOVE EDITED ANNU-DATA.TAXABLE(K) (EM=ZZZ,ZZ9.99)
  TO #AMOUNTS (K,#TAXAMT)
ADD ANNU-DATA.TAXABLE(K) TO #AMOUNTS-N (#TAXAMT)
END-SUBROUTINE
/*
DEFINE DATA-VALIDATION
/*     ---------------
RESET #MSG #ERRORS
*
FOR J 1 TO 10
  FOR L 1 TO 12
    IF #CONTROL-VARS ( J,L ) MODIFIED
      #CV-SAVE ( J,L ) := #CONTROL-VARS ( J,L )
    END-IF
  END-FOR
END-FOR
#CONTROL-VARS (*,*) := #CV-SAVE (*,*)
*
FOR J 1 TO 10
  IF #SELECT-CV (J) MODIFIED
    IF #POLICY-SEL (J) = 'A1' /* ANNUITY Life/1
        OR= 'A2' /* ANNUITY Life/2'
        OR= 'IO' /* Interest Only'
        OR= 'LA' /* Life Annuity'
        OR= 'LT' /* Long Term Care'
        OR= 'L1' /* LA Certain 10 years'
        OR= 'L2' /* LA certain 15 years'
        OR= 'L3' /* LA Certain 20 years'
      ANNU-DATA.SETTLEMENT-OPTION (j) := #POLICY-SEL (J)
    ELSE
      #POS := POS (#SELECT-CV (J))
      #ERRORS := TRUE
      #ERROR-NBR := 2
      #MSG  := #MSG-LIST ( #ERROR-NBR )
      ESCAPE ROUTINE
    END-IF
    IF CONTACT-V.CONTACT-TYPE EQ 'O' AND #POLICY-SEL (J) NE 'IO'
      #ERRORS := TRUE
      #ERROR-NBR := 4
      #MSG  := #MSG-LIST ( #ERROR-NBR )
      ESCAPE ROUTINE
    END-IF
    IF #ISN (J) = 0  /* NEW ANNU RECORD
      ANNU-DATA.SETTLEMENT-OPTION (j) := #POLICY-SEL (J)
      ANNU-DATA.ANNUITANT-STATUS  (j) := 'A'
      ANNU-DATA.CONTACT-ID        (j) := CONTACT-V.CONTACT-ID
      ANNU-DATA.REFERENCE-CN      (j) := #CGA-SCR-ID
      IF POLICY.POLICY-ID (J) EQ BLANK
        #MSG := #MSG-LIST (3)
        #POS := POS (#SELECT-CV (J))
        #ERRORS := TRUE
        ESCAPE ROUTINE
      ELSE
        ANNU-DATA.POLICY-NUMBER (J) := POLICY.POLICY-ID (J)
      END-IF
      RESET ANNUITANTS
      MOVE BY NAME ANNU-DATA (J) TO ANNUITANTS
      ANNUITANTS.ADD-USER := *USER
      ANNUITANTS.ADD-DATE := *DATN
      ANNUITANTS.ADD-TIME := *TIMN
      STORE ANNUITANTS
      MOVE *ISN (0491) TO #ISN (J)
      END TRANSACTION
    END-IF
  END-IF
END-FOR
*
END-SUBROUTINE
/*
DEFINE ANNUITY-EDITS
/*     -------------
FOR J 1 TO K
  IF #CONTROL-VARS (J,*) MODIFIED
      AND #POLICY-SEL (J) NE BLANK
    IF #ISN (J) > 0
      GET ANNUITANTS #ISN (J)
      MOVE BY NAME ANNU-DATA (J) TO ANNUITANTS
      ANNUITANTS.LAST-UPD-DATE := *DATN
      ANNUITANTS.LAST-UPD-TIME := *TIMN
      ANNUITANTS.LAST-UPD-USER := *USER
      UPDATE
    ELSE
      RESET ANNUITANTS
      MOVE BY NAME ANNU-DATA (J) TO ANNUITANTS
      ANNUITANTS.ADD-USER := *USER
      ANNUITANTS.ADD-DATE := *DATN
      ANNUITANTS.ADD-TIME := *TIMN
      STORE ANNUITANTS
      MOVE *ISN (0518) TO #ISN (J)
    END-IF
  END-IF
END-FOR
END TRANSACTION
END-SUBROUTINE
/*
DEFINE SHIFT-LEFT
/*     ----------
IF #TI <= 1
  ESCAPE ROUTINE
ELSE
  #TI := 1
END-IF
END-SUBROUTINE
*
DEFINE SHIFT-RIGHT
/*     -----------
IF #TI = 7
  ESCAPE ROUTINE
ELSE
  #TI := 7
END-IF
END-SUBROUTINE
*
DEFINE GET-SELECT
/*     ==========
DECIDE ON FIRST VALUE OF *CURS-LINE
  VALUE 10,11,12,13,14,15,16,17,18
    #SELECT := *CURS-LINE - 9
    #CGA-ISN := #ISN ( #SELECT )
    #CGA-MF-SCR-PARM-1-N := #ISSUE-DATE-A  ( #SELECT )
    IF #CGA-ISN > 0
      #CGA-SCR-ID := CONTACT-V.CONTACT-ID
      FETCH RETURN 'MANPADTL'
    END-IF
  NONE VALUE IGNORE
END-DECIDE
END-SUBROUTINE
/*
END
