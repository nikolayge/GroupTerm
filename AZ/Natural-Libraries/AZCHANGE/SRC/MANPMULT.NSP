* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Program MANPMULT.
/**
/** :author yfayngersh
/* Based on program MANPSRCH, used for CNs with multiple annuitants

DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL USING GONERLXX
LOCAL USING MANASRCH
LOCAL /* USING AUCASIND
* 1 SearchIndividual /*SEQ
*   2 ErrorCode (I4) /*unsignedShort
*   2 ErrorDescription (A) DYNAMIC /*string
*   2 ActionType (I4) /*unsignedShort
*   2 SearchId (A) DYNAMIC /*string
*   2 TotalFound (I4) /*unsignedInt
*   2 FirstName (A) DYNAMIC /*string
*   2 LastName (A) DYNAMIC /*string
*   2 DateOfBirth (N8) /*unsignedLong
*   2 DateOfDeath (N8)
*   2 SSN (A) DYNAMIC /*unsignedLong
*   2 Phone (N20) /*unsignedShort
*   2 PhoneType (A) DYNAMIC
*   2 Email (A) DYNAMIC /*string
*   2 City (A) DYNAMIC /*string
*   2 State (A) DYNAMIC /*string
*   2 ZIP (N9) /*unsignedInt
*   2 MembersOnly (L)
*   2 GenerateReport (L)
*   2 FILENAME (A) DYNAMIC
1 PageIndividual /*SEQ
  2 ErrorCode (I4) /*unsignedShort
  2 ErrorDescription (A) DYNAMIC /*string
  2 SearchId (A) DYNAMIC /*string
  2 PageNumber (I4) /*unsignedShort
  2 LinesPerPage (I4) /*unsignedShort
  2 TotalFound (I4) /*unsignedShort
  2 SortedBy (A) DYNAMIC /*string
  2 SortOrder (A) DYNAMIC /*string
  2 IndList /*SEQ
    3 C@Individual (I4)
    3 Individual (1:*) /*SEQ
      4 CN (N6) /*unsignedInt
      4 ContactID (N8)
      4 FormatedName (A) DYNAMIC /*string
      4 SSN (A9) /*unsignedLong
      4 DateOfBirth (N8) /*unsignedLong
      4 DateOfDeath (N8)
      4 PhoneNumber (A) DYNAMIC
      4 PhoneType (A) DYNAMIC /*string
      4 Email (A) DYNAMIC /*string
      4 Address1 (A) DYNAMIC /*string
      4 Address2 (A) DYNAMIC /*string
      4 Address3 (A) DYNAMIC /*string
LOCAL
1 SEARCH-ISNS                  (P10/1:*)
1 ADDRESS-FLAGS                (A1/1:*)
1 EMAIL-FLAGS                  (N8/1:*)
1 PHONE-FLAGS                  (N8/1:*)
1 #MAX  (N7) CONST <500>
1 BLANK (A1) CONST <' '>
1 #LINES-PER-PAGE     (N7) CONST <15>
1 #MAX-PAGE#          (N7)
1 #FL                 (N7)
1 J                   (N7)
1 K                   (N7)
1 L                   (N7)
1 N                   (N7)
1 BLANK-EXIST         (L)
1 FILL-EXIST          (L)
*
1 #NAMES              (A25/#MAX)
1 #SOCIAL             (A9/#MAX)
1 #BDATE              (A8/#MAX)
1 #SELECT             (A1/#MAX)
1 #CN                 (N6/#MAX)
1 #AFLAG              (A1/#MAX)
1 #POLICY-ID          (A16/#MAX)
1 #CONTACT-ID         (N8/#MAX)
*
1 #PAGE#              (N7)
1 POLICY-ID           (A16)
1 POLICY-OWNER-CN     (N6)
*
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER
  2 CONTACT-ID
*
1 CNT VIEW OF A-CONTACTS
  2 CONTACT-TYPE     (A1)
  2 ID-NUMBER        (N6)
  2 CONTACT-ID       (N8)
  2 LAST-NAME        (A25)
  2 FIRST-NAME       (A25)
  2 DATE-OF-BIRTH    (N8)
  2 SSN              (N9)
  2 FIRM-ORGANIZATION-NAME (A75)
*
END-DEFINE
*
SET KEY PF3 NAMED "Exit"
SET KEY PF7 NAMED 'PgUp'
SET KEY PF8 NAMED 'PgDw'
SET KEY PF4 NAMED 'Add'
*
INCLUDE GONERCXX
*
#CGA-ORIG-ID := #CGA-SCR-ID
#OWNERCN := #CGA-ORIG-ID
#CGA-SCR-COMMAND := '*'
*
PERFORM SEARCH-BY-REFERENCE-CN
PERFORM BROWSE-NAMES
*
FETCH 'G1000PXX'
*
* LOOK FOR NAMES BASED ON THE SEARCH CRITERIA
*
DEFINE SEARCH-BY-REFERENCE-CN
*      --------------
FIND ANNUITANTS WITH REFERENCE-CN = #CGA-SCR-ID
    SORTED BY CONTACT-ID
  IF NO RECORDS FOUND
    FETCH 'MANPSRCH'  /* No annuitants:
    ESCAPE BOTTOM
  END-NOREC
  IF C@Individual = 0
    ADD 1 TO C@Individual
    #CONTACT-ID (C@Individual) := ANNUITANTS.CONTACT-ID
    PERFORM POPULATE-ANNUITANTS
  ELSE
    IF ANNUITANTS.CONTACT-ID NE #CONTACT-ID (C@Individual)
      ADD 1 TO C@Individual
      #CONTACT-ID (C@Individual) := ANNUITANTS.CONTACT-ID
      PERFORM POPULATE-ANNUITANTS
    END-IF
  END-IF
END-FIND
END-SUBROUTINE
/*
DEFINE POPULATE-ANNUITANTS
/*     -------------------
FIND CNT WITH CONTACT-ID = ANNUITANTS.CONTACT-ID
  IF CNT.CONTACT-TYPE NE 'O'
    COMPRESS CNT.LAST-NAME CNT.FIRST-NAME INTO #NAMES(C@Individual)
      WITH DELIMITER ','
  ELSE
    #NAMES(C@Individual) := CNT.FIRM-ORGANIZATION-NAME
  END-IF
  #AFLAG (C@Individual) := 'Y'
  #POLICY-ID(C@Individual) := POLICY-NUMBER
  #SOCIAL(C@Individual) := CNT.SSN
  #BDATE(C@Individual) := CNT.DATE-OF-BIRTH
  #CN(C@Individual) := CNT.ID-NUMBER
END-FIND
END-SUBROUTINE
*
* NAVIGATE THE PAGES OF THE OUTPUT SCREEN
*
DEFINE BROWSE-NAMES
/*     ------------
#FL := 1
#PAGE# := 1
#MAX-PAGE# := C@Individual / #LINES-PER-PAGE
RPL.
REPEAT
  INPUT USING MAP 'MANMLIST' 
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPL.)
    VALUE 'PF8'
      IF #PAGE# < #MAX-PAGE#
        ADD 1 TO #PAGE#
        #FL := ( #PAGE# - 1) * #LINES-PER-PAGE + 1
      END-IF
    VALUE 'PF4'
      FETCH 'MANPSRCH'
    VALUE 'PF7'
      IF #PAGE# > 1
        SUBTRACT 1 FROM #PAGE#
        #FL := ( #PAGE# ) * #LINES-PER-PAGE - 14
      END-IF
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      #FL := ( #PAGE# - 1) * #LINES-PER-PAGE + 1
      FOR J 1 TO #LINES-PER-PAGE
        IF #SELECT (#FL) NE BLANK
          #CGA-SCR-ID := #CONTACT-ID (#FL)
          IF #CGA-ORIG-ID = 0
*            #CGA-ORIG-ID := #CN (#FL)
            IF #CGA-ORIG-ID = 0
              #CGA-ORIG-ID := #OWNERCN
            END-IF
          END-IF
          FETCH RETURN 'MANPDTL'
        END-IF
        ADD 1 TO #FL
      END-FOR
    NONE VALUE
      IGNORE
  END-DECIDE
  UNTIL *PF-KEY = 'PF3'
END-REPEAT
FETCH 'MANPSRCH'
END-SUBROUTINE
*
END
