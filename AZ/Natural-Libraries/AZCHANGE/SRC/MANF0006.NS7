* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MANF0006.
/**
/** :author nguentchev
*
* Total Paid: Initial Payment + all Monthly Payments + all 13th Checks
/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Taxable Amount:
/* Long Term Care Settlement
/*  Taxable Amount: Should be blank; it is determined by the IRS
/*
DEFINE FUNCTION GET-AZ-MONTHLY-TAX RETURNS (N7.2)
  DEFINE DATA
  PARAMETER
  1 #ISN      (P8)
  1 #DATE     (N8) BY VALUE
  1 REDEFINE #DATE
    2 PARM-YYYY (N4)
    2 PARM-MM   (N2)
    2 PARM-DD   (N2)  /* EXPECTED LAST DAY OF THE MONTH
  LOCAL
  1 ANNUITANTS VIEW OF A-ANNUITANTS
    2 POLICY-NUMBER     (A16)
    2 CONTACT-ID        (N8)
    2 SETTLEMENT-OPTION (A2)
    2 MONTHLY-PENSION   (N7.2)
    2 WITHDRAWALS       (N7.2)
    2 INITIAL-PAYMENT   (N7.2)
    2 DEPOSIT           (N7.2)
    2 T13TH-CHECK       (N7.2)
    2 TAXABLE           (N7.2)
    2 PAID-UP-DATE      (N8)
    2 START-DATE        (N8.0) 2 REDEFINE START-DATE
      3 START-YYYY (N4)
      3 START-MM   (N2)
      3 START-DD   (N2)
    2 WITHDRAWAL-DATE   (N8)
    2 REDEFINE WITHDRAWAL-DATE
      3 WD-YYYY (N4)
      3 WD-MM   (N2)
      3 WD-DD   (N2)
  1 #INTEREST-ONLY (A2) CONST <'IO'>
  1 #LONG-TERM     (A2) CONST <'LT'>
  1 #TAXABLE    (N7.2)
  1 A-ANNU-HISTORY VIEW OF A-ANNU-HISTORY
    2 ADD-DATE          (N8) 2 REDEFINE ADD-DATE
      3 ADD-HR-Y  (N4)
      3 ADD-HR-M  (N2)
      3 ADD-HR-D  (N2)
    2 ANNUITANT-STATUS  (A1)
    2 TAXABLE           (N7.2)
  END-DEFINE
/*
  RESET GET-AZ-MONTHLY-TAX
  GET ANNUITANTS #ISN

IF ANNUITANTS.PAID-UP-DATE > 0
    AND ANNUITANTS.PAID-UP-DATE < #DATE
  ESCAPE ROUTINE
END-IF


IF ANNUITANTS.START-DATE <= 0
  ESCAPE ROUTINE
END-IF

IF ANNUITANTS.START-DATE > 0
  IF START-YYYY > #DATE.PARM-YYYY
    ESCAPE ROUTINE
  END-IF
  IF START-YYYY = #DATE.PARM-YYYY
      AND START-MM > #DATE.PARM-MM
    ESCAPE ROUTINE
  END-IF
END-IF

DECIDE FOR FIRST CONDITION
  WHEN ANNUITANTS.SETTLEMENT-OPTION = #LONG-TERM
    RESET GET-AZ-MONTHLY-TAX
  WHEN ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
    GET-AZ-MONTHLY-TAX := GET-AZ-MONTHLY-PAY (< #ISN,#DATE >)
  WHEN NONE
    GET-AZ-MONTHLY-TAX := ANNUITANTS.TAXABLE
END-DECIDE

IF #TAXABLE > 0
  GET-AZ-MONTHLY-TAX := #TAXABLE
END-IF
/*
END-FUNCTION
END
