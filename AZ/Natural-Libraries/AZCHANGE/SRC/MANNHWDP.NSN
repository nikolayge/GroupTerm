* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram MANNHWDP.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 #ISN             (P8)
1 #TODAY              (N8)
1 REDEFINE #TODAY
  2 #YYYY  (A4)
  2 #MM    (N2)
  2 #DD    (N2)
1 #TOTAL-POL-WD    (N7.2)  /* policy
1 #LAST-POL-WD     (N7.2)
1 #NUMBER-WD       (I4)    /* policy current year
LOCAL
1 #POLICY-NUMBER   (A16)
1 #CONTACT-ID      (N8)
1 HISTORY-V VIEW OF A-ANNU-HISTORY
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 WITHDRAWALS     (N7.2)
  2 CHECK-NUMBER    (A10)
  2 LAST-UPD-DATE (N8.0)
  2 REDEFINE LAST-UPD-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
1 AU-V VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 WITHDRAWALS     (N7.2)
  2 CHECK-NUMBER    (A10)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 REDEFINE LAST-UPD-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
*
1 BLANK          (A1) CONST <' '>
1 #CHECK-NUMBER  (A10)
1 #CHECK-DATE    (N8) 1 REDEFINE #CHECK-DATE
  2 #YYYY (A4)
1 #CHECK-WITHDRAWAL (N7.2)
1 #CHECKS           (A10/99)
1 #CHECKS-DATE      (N8/99)
1 #CHECKS-WD        (N7.2/99)
1 #CHECKS-N  (I2)
1 J          (I2)
1 #NEW-CHECK (L)
END-DEFINE
*
RESET
  #TOTAL-POL-WD
  #LAST-POL-WD
  #CHECK-NUMBER
  #CHECKS-N
  #NUMBER-WD

GET AU-V #ISN
#POLICY-NUMBER := AU-V.POLICY-NUMBER
#CONTACT-ID := AU-V.CONTACT-ID
/*      Current records
FIND AU-V WITH POLICY-NUMBER = #POLICY-NUMBER
    SORTED BY CHECK-NUMBER
  IF AU-V.WITHDRAWALS <= 0
      OR AU-V.CHECK-NUMBER = BLANK
    ESCAPE TOP
  END-IF
  IF AU-V.CONTACT-ID = #CONTACT-ID
    #TOTAL-POL-WD := #TOTAL-POL-WD + AU-V.WITHDRAWALS
    IF AU-V.YYYY = #TODAY.#YYYY
      #LAST-POL-WD := AU-V.WITHDRAWALS
      ADD 1 TO #CHECKS-N
      #CHECKS      ( #CHECKS-N) := AU-V.CHECK-NUMBER
      #CHECKS-DATE ( #CHECKS-N) := AU-V.LAST-UPD-DATE
    END-IF
  END-IF
END-FIND
*        History records
RESET #CHECK-NUMBER
FIND HISTORY-V WITH POLICY-NUMBER = #POLICY-NUMBER
    SORTED BY CHECK-NUMBER
/*  Skip if withdrawal is 0
  IF HISTORY-V.CHECK-NUMBER = BLANK
      OR HISTORY-V.WITHDRAWALS = 0
    ESCAPE TOP
  END-IF
  IF HISTORY-V.CONTACT-ID = #CONTACT-ID
    #TOTAL-POL-WD := #TOTAL-POL-WD + HISTORY-V.WITHDRAWALS
    IF HISTORY-V.YYYY = #TODAY.#YYYY
      IF #LAST-POL-WD = 0
        #LAST-POL-WD := HISTORY-V.WITHDRAWALS
      ELSE
        IF #CHECKS-N > 0
            AND HISTORY-V.CHECK-NUMBER > #CHECKS (#CHECKS-N)
          #LAST-POL-WD := HISTORY-V.WITHDRAWALS
        END-IF
      END-IF
      ADD 1 TO #CHECKS-N
      #CHECKS      (#CHECKS-N) := HISTORY-V.CHECK-NUMBER
      #CHECKS-DATE (#CHECKS-N) := HISTORY-V.LAST-UPD-DATE
    END-IF
  END-IF
END-FIND
/*
FOR J 1 TO #CHECKS-N
  #CHECK-DATE := #CHECKS-DATE (J)
  IF #CHECK-DATE.#YYYY = #TODAY.#YYYY
    ADD 1 TO #NUMBER-WD
  END-IF
END-FOR
/*
END
