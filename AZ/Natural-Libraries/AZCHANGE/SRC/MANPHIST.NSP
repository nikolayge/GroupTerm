* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** Program: MANPHIST
/** Purpose: Browse history
/** BY: NGG
/** DATE: 4/20/2015
*
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
*
1 ANNUITANTS VIEW OF A-ANNU-HISTORY
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION (N7.2)
  2 PRINCIPAL       (N7.2)
  2 ANNUAL-PAYMENT  (N7.2)
  2 RESERVE-AMOUNT  (N7.2)
  2 WITHDRAWALS     (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE  (N8) 2 REDEFINE PAID-UP-DATE
    3 YYYY-1 (A4)
    3 MM-1   (A2)
    3 DD-1   (A2)
  2 INITIAL-PAYMENT (N7.2)
  2 NOTES (A70/1:2)
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0) 2 REDEFINE ADD-DATE
    3 AD-YYYY  (A4)
    3 AD-MM    (A2)
    3 AD-DD    (A2)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0) 2 REDEFINE LAST-UPD-DATE
    3 #YYYY (A4)
    3 #MM   (A2)
    3 #DD   (A2)
  2 LAST-UPD-TIME (N7.0)
  2 DEPOSIT         (N7.2)
  2 T13TH-CHECK     (N7.2)
  2 TOTAL-WD        (N7.2)
  2 NUMBER-WD       (N4)
  2 RESIDUAL        (N7.2)
  2 TOTAL-PAID      (N7.2)
  2 TAXABLE         (N7.2)
*
1 AU-V VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
*
1 CONTACT-V VIEW OF A-CONTACTS
  2 ID-NUMBER  (N6)
  2 CONTACT-TYPE (A1)
  2 CONTACT-ID (N8)
  2 GENDER-CD  (A1)
  2 SSN        (N9)
  2 DATE-OF-BIRTH (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 YYYY (N4)
    3 MM   (N2)
    3 DD   (N2)
*
1 #SCR-DATA
  2 POLICY-NBR        (A16)
  2 ISSUE-DATE        (N8)
  2 REDEFINE ISSUE-DATE
    3 #YYYY  (N4)
    3 #MM    (N2)
    3 #DD    (N2)
  2 SETTLEMENT-OPTION (A2)
  2 ANNUITANT-STATUS  (A1)
  2 START-DATE        (A10)
  2 PRINCIPAL         (A12)
  2 MONTHLY-PENSION   (A12)
  2 ANNUAL-PAYMENT    (A12)
  2 ADMIN-COST        (A8)
  2 THIRTHEEN-CHECK   (A12)
  2 RESERVE           (A12)
  2 INITIAL-PAYMENT   (A12)
  2 TOTAL-PAID        (A12)
  2 RESIDUAL          (A12)
  2 PAID-UP-DATE      (A10)
  2 DEPOSIT           (A12)
  2 WITHDRAWAL        (A12)
  2 TOTAL-WD          (A12)
  2 NUMBER-WD         (A2)
  2 NEW-WD            (A12)
  2 TAXABLE-AMT       (A12)
  2 #LUD              (A10)
  2 #ADD-DATE         (A10)
*
1 #ADDRESS            (A43/4)
1 #AGE                (N03.0)
1 #TODAY-A            (A10)
1 #SCR-TERMID         (A12)
1 #TOTAL-WD           (N7.2)
1 #LAST-WITHDRAWAL    (N7.2)
1 #NUMBER-WD          (I4)
*
1 #TODAY              (N8)
1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
*
1 #VALID-TYPES (A2/8) CONST <'A1','A2','IO','LA','LT','L1','L2','L3'>
1 #VALID-STATUS (A1/4) CONST <'A','C','D','P'>
*
1 BLANK (A1) CONST    <' '>
1 #MSG                (A60)
1 #13TH-CHECK         (N7.2)
1 #DEPOSIT            (N7.2)
1 #RESIDUAL           (N7.2)
1 #RESERVE            (N7.2)
1 #DOB                (A10)
1 #SD                 (A10)
1 #ID                 (A10)
1 #PR                 (N0.5)
1 #PAID               (N7.2)
1 #CODE-DESCRIPTION   (A24)
1 J    (I4)
*
1 #FIELD-NAME    (A32)
1 #HIST-ISNS     (P8/99)
1 #HIST-ISNS-N   (I4)
1 #CURRENT-ISN   (P8)
*
END-DEFINE
*
SET KEY PF3 NAMED "Exit"
SET KEY PF7 NAMED 'Up'
SET KEY PF8 NAMED 'Down'
*
MOVE *DAT4U TO #TODAY-A
COMPRESS *INIT-USER '-' #CGA-CLERK INTO #SCR-TERMID LEAVING NO
MOVE *DATN TO #TODAY
MOVE '*' TO #CGA-SCR-COMMAND
*
CONTACT-V.CONTACT-ID := #CGA-SCR-ID
#CGA-SCR-ID := #CGA-ORIG-ID                               /* PASSED CN
#SCR-DATA.ISSUE-DATE := #CGA-MF-SCR-PARM-1-N
MOVE LEFT JUSTIFIED #CGA-SCR-COMMAND TO #CGA-SCR-COMMAND
CALLNAT 'ADRN0016' CONTACT-V.CONTACT-ID
  #ADDRESS (*)
*
FIND CONTACT-V WITH CONTACT-ID = CONTACT-V.CONTACT-ID
  IF NO RECORDS FOUND
    #MSG := 'No contact record found'
    ESCAPE BOTTOM
  END-NOREC
  IF CONTACT-V.CONTACT-TYPE NE 'O'
    #AGE := #TODAY.#YYYY - CONTACT-V.YYYY
    IF #TODAY.#MM < CONTACT-V.MM
      #AGE := #AGE - 1
    ELSE
      IF #TODAY.#MM = CONTACT-V.MM AND #TODAY.#DD < CONTACT-V.DD
        #AGE := #AGE - 1
      END-IF
    END-IF
  END-IF
  COMPRESS CONTACT-V.MM CONTACT-V.DD CONTACT-V.YYYY INTO #DOB
    WITH DELIMITER '/'
END-FIND
*
GET AU-V #CGA-ISN
RESET #HIST-ISNS-N J
FIND ANNUITANTS WITH POLICY-NUMBER = AU-V.POLICY-NUMBER
    SORTED BY GENERATION DESCENDING
  ADD 1 TO J
  #HIST-ISNS (J) := *ISN
END-FIND
IF J <= 0   /* NO HISTORY RECORD
  ESCAPE ROUTINE
END-IF
#HIST-ISNS-N := J
J := 1
#CURRENT-ISN := #HIST-ISNS (J)
GET ANNUITANTS #CURRENT-ISN
*
PERFORM POPULATE-DATA
*
RPT.
REPEAT
  INPUT TEXT #MSG USING MAP 'MANMHIST'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPT.)
    VALUE 'PF7'
      PERFORM HISTORY-UP
    VALUE 'PF8'
      PERFORM HISTORY-DOWN
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
      ESCAPE TOP
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
*
IF #CGA-SCR-COMMAND NE '*'
  FETCH 'G1000PXX'
END-IF
/*
DEFINE POPULATE-DATA
/*     -------------
IF ANNUITANTS.START-DATE = 0
  #SCR-DATA.START-DATE := #TODAY
END-IF
*
ANNUITANTS.ANNUAL-PAYMENT := (ANNUITANTS.MONTHLY-PENSION * 12)
*
COMPRESS CONTACT-V.MM CONTACT-V.DD CONTACT-V.YYYY INTO #DOB WITH DELIMITER '/'
COMPRESS ANNUITANTS.MM ANNUITANTS.DD ANNUITANTS.YYYY INTO
  #SCR-DATA.START-DATE WITH DELIMITER '/'
COMPRESS #SCR-DATA.#MM #SCR-DATA.#DD #SCR-DATA.#YYYY INTO #ID WITH DELIMITER '/'
COMPRESS ANNUITANTS.#MM ANNUITANTS.#DD ANNUITANTS.#YYYY INTO
  #LUD WITH DELIMITER '/'
COMPRESS AD-MM AD-DD AD-YYYY INTO #ADD-DATE WITH DELIMITER '/'
*
MOVE ANNUITANTS.POLICY-NUMBER     TO #SCR-DATA.POLICY-NBR
MOVE ANNUITANTS.SETTLEMENT-OPTION TO #SCR-DATA.SETTLEMENT-OPTION
MOVE ANNUITANTS.ANNUITANT-STATUS  TO #SCR-DATA.ANNUITANT-STATUS
MOVE EDITED ANNUITANTS.PRINCIPAL  (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.PRINCIPAL
MOVE EDITED ANNUITANTS.MONTHLY-PENSION (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.MONTHLY-PENSION
MOVE EDITED ANNUITANTS.ANNUAL-PAYMENT (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.ANNUAL-PAYMENT
MOVE EDITED ANNUITANTS.INITIAL-PAYMENT (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.INITIAL-PAYMENT
MOVE EDITED ANNUITANTS.TOTAL-PAID (EM=Z,ZZZ,ZZ9.99) TO #SCR-DATA.TOTAL-PAID
MOVE EDITED ANNUITANTS.WITHDRAWALS (EM=NZZZ,ZZ9.99)
  TO #SCR-DATA.WITHDRAWAL
MOVE EDITED ANNUITANTS.TOTAL-WD (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.TOTAL-WD
MOVE EDITED ANNUITANTS.NUMBER-WD (EM=Z9)
  TO #SCR-DATA.NUMBER-WD
MOVE EDITED ANNUITANTS.RESIDUAL (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.RESIDUAL
MOVE EDITED ANNUITANTS.DEPOSIT (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.DEPOSIT
MOVE EDITED ANNUITANTS.T13TH-CHECK  (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.THIRTHEEN-CHECK
MOVE EDITED ANNUITANTS.TAXABLE (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.TAXABLE-AMT
*
IF ANNUITANTS.SETTLEMENT-OPTION = 'IO '
  #RESERVE := ANNUITANTS.RESIDUAL
ELSE
  #RESERVE := ANNUITANTS.RESERVE-AMOUNT
END-IF
MOVE EDITED #RESERVE (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.RESERVE
*
COMPRESS ANNUITANTS.MM-1 ANNUITANTS.DD-1 ANNUITANTS.YYYY-1
  INTO #SCR-DATA.PAID-UP-DATE WITH DELIMITER '/'
*
END-SUBROUTINE
*
DEFINE HISTORY-UP
/*     ----------
IF J > 1
  #CURRENT-ISN := #HIST-ISNS (J - 1)
  GET ANNUITANTS #CURRENT-ISN
  PERFORM POPULATE-DATA
  J := J - 1
END-IF
END-SUBROUTINE
*
DEFINE HISTORY-DOWN
/*     ------------
IF J < #HIST-ISNS-N
  #CURRENT-ISN := #HIST-ISNS (J + 1)
  GET ANNUITANTS #CURRENT-ISN
  PERFORM POPULATE-DATA
  J := J + 1
END-IF
END-SUBROUTINE
*
END
