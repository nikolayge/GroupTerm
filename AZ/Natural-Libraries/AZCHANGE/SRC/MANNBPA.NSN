* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram MANNBPA.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 CN                   (N6)
1 POLICY-NUMBER        (A16)
1 SETTLEMENT-OPTION    (A2)
1 PRINCIPAL            (N7.2)
1 GENDER-CD            (A1)
1 #AGE                 (N3)
1 MONTHLY-PENSION      (N7.2)
1 TAXABLE              (N7.2)
1 #MSG                 (A60) 
LOCAL
1 Annuity-amount    (N7.2/5)
1 Expectancy-amount (N7.2/5)
1 Tax-Percent       (N2.7/5)
1 Tax-Monthly       (N7.2/5)
1 Tax-Amount        (N7.2/5)
1 Age               (N4)
1 POLICY VIEW OF A-STATUS
  2 C*ACTIVE-SEGMENTS (N3)
  2 POLICY-ID     (9)
  2 ISSUE-DATE    (9)
1 J  (N3)
END-DEFINE
/*
IF PRINCIPAL = 0
*    OR #AGE = 0
  ESCAPE ROUTINE
END-IF
IF MONTHLY-PENSION > 0
    OR TAXABLE > 0
  ESCAPE ROUTINE
END-IF

Age := #AGE
/*
COMPRESS 'Policy' POLICY-NUMBER 'Not Found' INTO #MSG
FIND POLICY WITH ID-NUMBER = CN
  FOR J 1 TO C*ACTIVE-SEGMENTS
    IF POLICY.POLICY-ID (J) = POLICY-NUMBER
      RESET #MSG
      CALLNAT 'ASON0001'
        PRINCIPAL       /* Amt to annuitize
        GENDER-CD
        Age
        Annuity-amount     (*)
        Expectancy-amount  (*)
        Tax-Percent        (*)
        Tax-Monthly        (*)
        Tax-Amount         (*)
        POLICY.ISSUE-DATE (J)
/* Annuity-amount (1) = LA, (2) = L1 (3) = L2 (4) = L3 (5) = IO
      DECIDE ON FIRST VALUE OF SETTLEMENT-OPTION
        VALUE 'LA'
          TAXABLE := Tax-Monthly (1)
          MONTHLY-PENSION := Annuity-amount (1)
        VALUE 'L1'
          TAXABLE := Tax-Monthly (2)
          MONTHLY-PENSION := Annuity-amount (2)
        VALUE 'L2'
          TAXABLE := Tax-Monthly (3)
          MONTHLY-PENSION := Annuity-amount (3)
        VALUE 'L3'
          TAXABLE := Tax-Monthly (4)
          MONTHLY-PENSION := Annuity-amount (4)
        VALUE 'IO'
          TAXABLE := Tax-Monthly (5)
          MONTHLY-PENSION := Annuity-amount (5)
        NONE VALUE
          RESET TAXABLE MONTHLY-PENSION
      END-DECIDE
      ESCAPE BOTTOM
    END-IF
  END-FOR
END-FIND
END
