* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Function MANF0004.
/**
/** :author nguentchev
DEFINE FUNCTION GET-AZ-ANNUAL-PAY RETURNS (N7.2)
  DEFINE DATA
  PARAMETER
  1 #ISN        (P8)
  1 #DATE       (N8) BY VALUE
  1 REDEFINE #DATE
    2 PARM-YYYY (N4)
    2 PARM-MM   (N2)
    2 PARM-DD   (N2)
  1 #TODAY      (N8) BY VALUE
  1 REDEFINE #TODAY
    2 #YYYY  (N4)
    2 #MM    (N2)
    2 #DD    (N2)
  LOCAL
  1 #INTEREST-ONLY (A2) CONST <'IO'>
  1 ANNUITANTS VIEW OF A-ANNUITANTS
    2 POLICY-NUMBER     (A16)
    2 SETTLEMENT-OPTION (A2)
    2 MONTHLY-PENSION   (N7.2)
    2 WITHDRAWALS       (N7.2)
    2 INITIAL-PAYMENT   (N7.2)
    2 DEPOSIT           (N7.2)
    2 T13TH-CHECK       (N7.2)
    2 TAXABLE           (N7.2)
    2 PAID-UP-DATE      (N8) 2 REDEFINE PAID-UP-DATE
      3 PAID-UP-YYYY (N4)
    2 START-DATE (N8.0) 2 REDEFINE START-DATE
      3 START-YYYY (N4)
      3 START-MM   (N2)
    2 WITHDRAWAL-DATE   (N8)
    2 REDEFINE WITHDRAWAL-DATE
      3 WD-YYYY (N4)
      3 WD-MM   (N2)
      3 WD-DD   (N2)

  1 #NEXT (N8) 1 REDEFINE #NEXT
    2 #NEXT-YYYY    (N4)
    2 #NEXT-MM (N2)
    2 #DD      (N2)
  END-DEFINE
/*
  RESET GET-AZ-ANNUAL-PAY
  GET ANNUITANTS #ISN

IF ANNUITANTS.START-DATE > 0
  IF ANNUITANTS.START-YYYY = PARM-YYYY
    #DATE := ANNUITANTS.START-DATE
    GET-AZ-ANNUAL-PAY := ANNUITANTS.INITIAL-PAYMENT
    ADD 1 TO PARM-MM
  END-IF
  IF #TODAY.#MM < PARM-MM
    #TODAY.#MM := PARM-MM  
  END-IF
ELSE
  ESCAPE ROUTINE
END-IF
WD-MM := 12 - #TODAY.#MM

IF ANNUITANTS.PAID-UP-DATE > 0
  DECIDE FOR FIRST CONDITION
    WHEN ANNUITANTS.PAID-UP-YYYY = #TODAY.#YYYY
      #TODAY := ANNUITANTS.PAID-UP-DATE
      SUBTRACT 1 FROM #TODAY.#MM
      IF #TODAY.#MM < 1
        ESCAPE ROUTINE
      END-IF
      RESET WD-MM
    WHEN ANNUITANTS.PAID-UP-YYYY < #TODAY.#YYYY
      RESET GET-AZ-ANNUAL-PAY
      ESCAPE ROUTINE
    WHEN NONE
      IGNORE
  END-DECIDE
END-IF

* IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
  GET-AZ-ANNUAL-PAY := GET-AZ-MONTHLY-PAY(<#ISN,#DATE>)
  ADD 1 TO #DATE.PARM-MM
  REPEAT UNTIL #DATE.PARM-MM > #TODAY.#MM
    GET-AZ-ANNUAL-PAY := GET-AZ-ANNUAL-PAY
      + GET-AZ-MONTHLY-PAY(<#ISN,#DATE>)
    ADD 1 TO #DATE.PARM-MM
  END-REPEAT
  GET-AZ-ANNUAL-PAY := GET-AZ-ANNUAL-PAY 
    + WD-MM * GET-AZ-MONTHLY-PAY(<#ISN,#DATE>)
* ELSE
*   GET-AZ-ANNUAL-PAY := ANNUITANTS.MONTHLY-PENSION * #TODAY.#MM
* END-IF
END-FUNCTION
END
