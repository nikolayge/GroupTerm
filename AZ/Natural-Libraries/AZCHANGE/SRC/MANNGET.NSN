* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* <Natural Source Header
/** New Subprogram MANNGET.
/**
/** :author nguentchev
DEFINE DATA
PARAMETER
1 #ISN         (P8)
1 #TODAY       (N8) BY VALUE
1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)
1 #CALCULATED
  2 #NUMBER-WD       (I4)
  2 #NUM-POL-WD      (I4)
  2 #TOTAL-POL-WD    (N7.2)
  2 #RESIDUAL        (N7.2)
  2 #PAID            (N7.2)
  2 #TOTAL-WD        (N7.2)
  2 #LAST-POL-WD     (N7.2)
  2 #LAST-WITHDRAWAL (N7.2)
  2 #GROSS           (N7.2)
  2 #GROSS-TAXABLE   (N7.2)
  2 #MONTHLY-PENSION (N7.2)
1 ANNUITANTS
  2 POLICY-NUMBER     (A16)
  2 CONTACT-ID        (N8.0)
  2 REFERENCE-CN      (N6.0)
  2 ANNUITANT-STATUS  (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION   (N7.2)
  2 PRINCIPAL         (N7.2)
  2 ANNUAL-PAYMENT    (N7.2)
  2 RESERVE-AMOUNT    (N7.2)
  2 WITHDRAWALS       (N7.2)
  2 INITIAL-PAYMENT   (N7.2)
  2 INITIAL-INTEREST  (N7.2)
  2 DEPOSIT           (N7.2)
  2 T13TH-CHECK       (N7.2)
  2 TAXABLE           (N7.2)
  2 WITHDRAWAL-DATE   (N8)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8.0) 2 REDEFINE ONE-MP-DATE
    3 ONE-YYYY (N4)
    3 ONE-MM   (N2)
    3 ONE-DD   (N2)
  2 START-DATE      (N8) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE (N8.0) 2 REDEFINE PAID-UP-DATE
    3 PAID-UP-YYYY (N4)
LOCAL
1 #INTEREST-ONLY (A2) CONST <'IO'>
1 #LONG-TERM     (A2) CONST <'LT'>

1 #PRO-RATE-X (N7.2)
1 #PARM-DATE (N8.0)
1 REDEFINE #PARM-DATE
  2 P-YYYY (N4)
  2 P-MM   (N2)
  2 P-DD   (N2)
1 #LAST    (N2/12) INIT <31,28,31,30,31,30,31,31,30,31,30,31>
1 AN-V VIEW OF A-ANNUITANTS
  2 LAST-UPD-DATE 2 REDEFINE LAST-UPD-DATE
    3 LAST-UPD-YEAR (N4)
    3 LAST-UPD-MM   (N2)
1 ANH-V VIEW OF A-ANNU-HISTORY
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 LAST-UPD-TIME (N7.0)
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0) /*   
  2 ANNUITANT-STATUS (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION (N7.2)
  2 PRINCIPAL (N7.2)
  2 ANNUAL-PAYMENT (N7.2)
  2 RESERVE-AMOUNT (N7.2)
  2 WITHDRAWALS (N7.2)
  2 REFERENCE-CN (N6.0)
  2 START-DATE (N8.0)
  2 GENERATION (N7.0)
  2 PAID-UP-DATE (N8.0)
  2 INITIAL-PAYMENT (N7.2)
  2 DEPOSIT (N7.2)
  2 T13TH-CHECK (N7.2)
  2 TOTAL-WD (N7.2)
  2 NUMBER-WD (N4.0)
  2 RESIDUAL (N7.2)
  2 TOTAL-PAID (N7.2)
  2 TAXABLE (N7.2)
  2 CHECK-NUMBER (A10)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE (N8.0)
  2 WITHDRAWAL-DATE (N8.0)
  2 YEAR-END (N4.0)
END-DEFINE
/*
GET AN-V #ISN
/*                             HISTORY
IF AN-V.LAST-UPD-YEAR > #TODAY.#YYYY
    OR AN-V.LAST-UPD-YEAR = #TODAY.#YYYY AND AN-V.LAST-UPD-MM > #TODAY.#MM
  FIND ANH-V WITH POLICY-NUMBER = ANNUITANTS.POLICY-NUMBER
      AND CONTACT-ID = ANNUITANTS.CONTACT-ID
      AND YEAR-END = #TODAY.#YYYY
    IF NO RECORDS FOUND
      FIND ANH-V WITH POLICY-NUMBER = ANNUITANTS.POLICY-NUMBER
          AND CONTACT-ID = ANNUITANTS.CONTACT-ID
          SORTED BY GENERATION DESCENDING
        IF ANH-V.LAST-UPD-DATE <= #TODAY
          MOVE BY NAME ANH-V TO ANNUITANTS
          ESCAPE BOTTOM
        END-IF
      END-FIND
      ESCAPE BOTTOM
    END-NOREC
    MOVE BY NAME ANH-V TO ANNUITANTS
  END-FIND
END-IF

RESET #CALCULATED

/*   Withdrawals
IF #ISN > 0
  CALLNAT 'MANNHWD'  /* Contact level
    ANNUITANTS.CONTACT-ID
    #TODAY
    #TOTAL-WD
    #NUMBER-WD
    #LAST-WITHDRAWAL
  CALLNAT 'MANNHWDP'
    #ISN
    #TODAY
    #TOTAL-POL-WD
    #LAST-POL-WD
    #NUM-POL-WD
END-IF

IF ANNUITANTS.PAID-UP-DATE > 0
  IF ANNUITANTS.PAID-UP-YYYY < #TODAY.#YYYY
    RESET ANNUITANTS.RESERVE-AMOUNT
    RESET ANNUITANTS.ANNUAL-PAYMENT
    ESCAPE ROUTINE
  ELSE
    IF ANNUITANTS.PAID-UP-YYYY = #TODAY.#YYYY
      #TODAY := ANNUITANTS.PAID-UP-DATE
    END-IF
  END-IF
END-IF
* Initial Payment:    If an annuity starts mid-month, AAFMAA makes a prorated Initial Payment
* Manually entered by user

* Reserve IO: Principal - Withdrawals
*         else: to be uploaded quarterly based on values provided by actuaries
 IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
   ANNUITANTS.RESERVE-AMOUNT := ANNUITANTS.PRINCIPAL - #TOTAL-POL-WD
 END-IF

* Residual IO: =Reserve
*         else: to be uploaded quarterly based on values provided by actuaries
IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
  #RESIDUAL := ANNUITANTS.RESERVE-AMOUNT
ELSE
  RESET #RESIDUAL
END-IF

* Deposit:    Same as original Principal
* Should only display IF the annuity started in current calendar year
* In subsequent years, value should be 0

* Taxable Amount: IO Based on Reserve Monthly Payment
*                else Based on Principal From BP

* Monthly Payment: IO Based on Reserve From BP
*                 else Based on Principal From BP
* Prorated Monthly Payment
* There is current month withdrawal: Old-MP*X + New-MP*(1-X)
#PARM-DATE := #TODAY
#PARM-DATE.P-DD := #LAST ( P-MM )
#MONTHLY-PENSION := GET-AZ-MONTHLY-PAY (< #ISN,#PARM-DATE >)
*
* Annual Payment IO: policies issued before 01/01/2015- Principal * 4% else 3.5%
*    For IO, due to Withdrawals, Monthly Payments can vary
*    else: Monthly Payment * 12
#PARM-DATE := #TODAY
#PARM-DATE.P-MM := 1
#PARM-DATE.P-DD := 1
IF #PARM-DATE < ANNUITANTS.START-DATE
  #PARM-DATE := ANNUITANTS.START-DATE
END-IF
/* #PARM-DATE is 1/1/YYYY OR STAT-DATE
ANNUITANTS.ANNUAL-PAYMENT := GET-AZ-ANNUAL-PAY (< #ISN,#PARM-DATE,#TODAY >)
*
/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Taxable Amount: All Taxable Amount in the calendar year + Initial Payment + 13th Check
/* Long Term Care Settlement
/*  Taxable Amount: Should be blank; it is determined by the IRS

#GROSS-TAXABLE := GET-AZ-1099-PAY (< #ISN,#PARM-DATE,#TODAY >)

/* Interest Only, Life Annuity, Life Annuity 10/15/20 Year Certain, ANNUITYLife 1 and 2
/*  Gross Amount: All Monthly Payment in the calendar year + Initial Payment + 13th Check
*                For IO, due to Withdrawals, Monthly Payments can vary
/* Long Term Care Settlement
/*  Gross Amount: Sum of (each Monthly Payment - 20) in the calendar year

#GROSS := ANNUITANTS.ANNUAL-PAYMENT
ADD #LAST-POL-WD TO #GROSS
#PAID := #GROSS

END
