* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Subprogram MANNHWD.
/** Collect Total Withdravals and number of withdrawals for
/** current year
/** Change the logic to be at Annuitant level ?
/** :author nguentchev
DEFINE DATA
PARAMETER
1 #CONTACT-ID      (N8)
1 #TODAY              (N8)
1 REDEFINE #TODAY
  2 #YYYY  (A4)
  2 #MM    (N2)
  2 #DD    (N2)
1 #TOTAL-WD        (N7.2)  /* all
1 #NUMBER-WD       (I4)    /* annuitant current year
1 #LAST-WITHDRAWAL (N7.2)
LOCAL
1 #POLICY-NUMBER   (A16)
1 HISTORY-V VIEW OF A-ANNU-HISTORY
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 WITHDRAWALS     (N7.2)
  2 CHECK-NUMBER    (A10)
  2 LAST-UPD-DATE (N8.0)
  2 REDEFINE LAST-UPD-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
1 AU-V VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER (A16)
  2 CONTACT-ID (N8.0)
  2 WITHDRAWALS     (N7.2)
  2 CHECK-NUMBER    (A10)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 REDEFINE LAST-UPD-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
*
1 BLANK          (A1) CONST <' '>
1 #CHECK-NUMBER  (A10)
1 #CHECK-DATE    (N8) 1 REDEFINE #CHECK-DATE
  2 #YYYY (A4)
1 #CHECK-WITHDRAWAL (N7.2)
1 #CHECKS           (A10/99)
1 #CHECKS-DATE      (N8/99)
1 #CHECKS-WD        (N7.2/99)
1 #CHECKS-N  (I2)
1 J          (I2)
1 #NEW-CHECK (L)
END-DEFINE
*
RESET
  #TOTAL-WD
  #NUMBER-WD
  #LAST-WITHDRAWAL
  #CHECK-NUMBER
  #CHECKS-N
/*      Current records
FIND AU-V WITH CONTACT-ID = #CONTACT-ID
    SORTED BY CHECK-NUMBER

  IF CHECK-NUMBER = BLANK
      OR AU-V.WITHDRAWALS = 0
    ESCAPE TOP
  END-IF

  #TOTAL-WD := #TOTAL-WD + AU-V.WITHDRAWALS

  IF CHECK-NUMBER NE #CHECK-NUMBER
    #CHECK-NUMBER := CHECK-NUMBER
    ADD 1 TO #CHECKS-N
    #CHECKS      ( #CHECKS-N) := #CHECK-NUMBER
    #CHECKS-DATE ( #CHECKS-N) := AU-V.LAST-UPD-DATE
    #CHECKS-WD   ( #CHECKS-N) := AU-V.WITHDRAWALS
  ELSE
    ADD AU-V.WITHDRAWALS TO #CHECKS-WD (#CHECKS-N)
  END-IF
END-FIND
* History records
RESET #CHECK-NUMBER
FIND HISTORY-V WITH CONTACT-ID = #CONTACT-ID
    SORTED BY CHECK-NUMBER
/*  Skip if withdrawal is 0
  IF HISTORY-V.CHECK-NUMBER = BLANK
      OR HISTORY-V.WITHDRAWALS = 0
    ESCAPE TOP
  END-IF

  #TOTAL-WD := #TOTAL-WD + HISTORY-V.WITHDRAWALS

  IF CHECK-NUMBER NE #CHECK-NUMBER
    #CHECK-NUMBER := HISTORY-V.CHECK-NUMBER
    FOR J 1 TO #CHECKS-N
      IF HISTORY-V.CHECK-NUMBER = #CHECKS (J)
        ADD HISTORY-V.WITHDRAWALS TO #CHECKS-WD (J)
        ESCAPE BOTTOM
      END-IF
    END-FOR
    IF J > #CHECKS-N
      ADD 1 TO #CHECKS-N
      #CHECKS      (#CHECKS-N) := #CHECK-NUMBER
      #CHECKS-DATE (#CHECKS-N) := HISTORY-V.LAST-UPD-DATE
      #CHECKS-WD   (#CHECKS-N) := HISTORY-V.WITHDRAWALS
      J := #CHECKS-N
    END-IF
  ELSE
    ADD HISTORY-V.WITHDRAWALS TO #CHECKS-WD (J)
  END-IF
END-FIND
/*
RESET #CHECK-NUMBER
FOR J 1 TO #CHECKS-N
  #CHECK-DATE := #CHECKS-DATE (J)
  IF #CHECK-DATE.#YYYY = #TODAY.#YYYY
    ADD 1 TO #NUMBER-WD
    IF #CHECK-NUMBER NE BLANK
      IF #CHECKS (J) > #CHECK-NUMBER
        #LAST-WITHDRAWAL := #CHECKS-WD (J)
        #CHECK-NUMBER := #CHECKS (J)
      END-IF
    ELSE
      #LAST-WITHDRAWAL := #CHECKS-WD (J)
      #CHECK-NUMBER := #CHECKS (J)
    END-IF
  END-IF
END-FOR
/*
END
