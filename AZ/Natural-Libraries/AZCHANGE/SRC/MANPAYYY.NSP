* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
/** New Program MANPYDT.
/**
/** :author nguentchev
DEFINE DATA
GLOBAL USING BCOMMGXX WITH MASTER-BLOCK
LOCAL
1 #CALCULATED
  2 #NUMBER-WD       (I4)
  2 #NUM-POL-WD      (I4)
  2 #TOTAL-POL-WD    (N7.2)
  2 #RESIDUAL        (N7.2)
  2 #PAID            (N7.2)
  2 #TOTAL-WD        (N7.2)
  2 #LAST-POL-WD     (N7.2)
  2 #LAST-WITHDRAWAL (N7.2)
  2 #GROSS           (N7.2)
  2 #GROSS-TAXABLE   (N7.2)
  2 #PRO-RATE-MP     (N7.2)
1 #SCR-DATA
  2 POLICY-NBR        (A16)
  2 #INTRST-RATE      (A4)
  2 ISSUE-DATE        (N8)
  2 REDEFINE ISSUE-DATE
    3 #YYYY  (N4)
    3 #MM    (N2)
    3 #DD    (N2)
  2 SETTLEMENT-OPTION (A2)
  2 ANNUITANT-STATUS  (A1)
  2 START-DATE        (A10)
  2 PRINCIPAL         (A12)
  2 MONTHLY-PENSION   (A12)
  2 ANNUAL-PAYMENT    (A12)
  2 ADMIN-COST        (A8)
  2 THIRTHEEN-CHECK   (A12)
  2 RESERVE           (A12)
  2 INITIAL-PAYMENT   (A12)
  2 TOTAL-PAID        (A12)
  2 RESIDUAL          (A12)
  2 PAID-UP-DATE      (A10)
  2 DEPOSIT           (A12)
  2 WITHDRAWAL        (A12)
  2 TOTAL-WD          (A12)
  2 NUMBER-WD         (A2) 2 REDEFINE NUMBER-WD
    3 NUMBER-WD-N (N2)
  2 NEW-WD            (A12)
  2 TAXABLE           (A12)
1 ANNUITANTS VIEW OF A-ANNUITANTS
  2 POLICY-NUMBER     (A16)
  2 CONTACT-ID        (N8.0)
  2 REFERENCE-CN      (N6.0)
  2 ANNUITANT-STATUS  (A1)
  2 SETTLEMENT-OPTION (A2)
  2 MONTHLY-PENSION   (N7.2)
  2 PRINCIPAL         (N7.2)
  2 ANNUAL-PAYMENT    (N7.2)
  2 RESERVE-AMOUNT    (N7.2)
  2 WITHDRAWALS       (N7.2)
  2 INITIAL-PAYMENT   (N7.2)
  2 INITIAL-INTEREST  (N7.2)
  2 DEPOSIT           (N7.2)
  2 T13TH-CHECK       (N7.2)
  2 TAXABLE           (N7.2)
  2 WITHDRAWAL-DATE   (N8)
  2 ONE-MONTH-PENSION (N7.2)
  2 ONE-MP-DATE       (N8.0) 2 REDEFINE ONE-MP-DATE
    3 ONE-YYYY (N4)
    3 ONE-MM   (N2)
    3 ONE-DD   (N2)
  2 START-DATE      (N8) 2 REDEFINE START-DATE
    3 YYYY (A4)
    3 MM   (A2)
    3 DD   (A2)
  2 PAID-UP-DATE (N8.0) 2 REDEFINE PAID-UP-DATE
    3 YYYY-1 (A4)
    3 MM-1   (A2)
    3 DD-1   (A2)
1 CONTACT-V VIEW OF A-CONTACTS
  2 CONTACT-TYPE (A1)
  2 ID-NUMBER  (N6)
  2 CONTACT-ID (N8)
  2 GENDER-CD  (A1)
  2 SSN        (N9)
  2 DATE-OF-BIRTH (N8)
  2 REDEFINE DATE-OF-BIRTH
    3 YYYY (N4)
    3 MM   (N2)
    3 DD   (N2)
  2 DATE-OF-DEATH
  2 REDEFINE DATE-OF-DEATH
    3 DEATH-YYYY (A4)
    3 DEATH-MM   (A2)
    3 DEATH-DD   (A2)

1 #AGE          (N3.0)
1 #TODAY-A      (A10)
1 #SCR-TERMID   (A12)
1 #TODAY (N8) 1 REDEFINE #TODAY
  2 #YYYY  (N4)
  2 #MM    (N2)
  2 #DD    (N2)
1 REDEFINE #TODAY
  2 #YYYY-A (A4)

1 BLANK (A1) CONST <' '>
1 #INTEREST-ONLY (A2) CONST <'IO'>

1 #MSG                (A60)
1 #DOB                (A10)
1 #DOD                (A10)
1 #ADDRESS            (A43/4)
1 #TI                 (I2)
1 #CODE-DESCRIPTION   (A24)
1 #ID                 (A10)
1 #DEPOSIT            (N7.2)

1 J    (I4)
1 L    (I4)
END-DEFINE
/* 
SET KEY PF3 NAMED "Exit"
SET KEY PF4 NAMED 'FHist'
SET KEY PF5 NAMED "Hist"
*
COMPRESS *INIT-USER '-' #CGA-CLERK INTO #SCR-TERMID LEAVING NO
CONTACT-V.CONTACT-ID := #CGA-SCR-ID
IF CONTACT-V.CONTACT-ID = 0
  ESCAPE ROUTINE
END-IF
#CGA-SCR-ID := #CGA-ORIG-ID     /* PASSED CN

MOVE LEFT JUSTIFIED #CGA-SCR-COMMAND TO #CGA-SCR-COMMAND
MOVE LEFT JUSTIFIED #CGA-SCR-SUFFIX TO #CGA-SCR-SUFFIX
COMPRESS '20' #CGA-SCR-SUFFIX INTO #YYYY-A LEAVING NO
#TODAY.#MM := 12
#TODAY.#DD := 31
COMPRESS #TODAY.#MM #TODAY.#DD #YYYY-A INTO #TODAY-A
  WITH DELIMITER '/'

#SCR-DATA.ISSUE-DATE := #CGA-MF-SCR-PARM-1-N
IF #SCR-DATA.ISSUE-DATE >= 20150101
  #INTRST-RATE := '3.5%'
ELSE
  #INTRST-RATE := '4.0%'
END-IF

CALLNAT 'ADRN0016' CONTACT-V.CONTACT-ID
  #ADDRESS (*)
*
FIND CONTACT-V WITH CONTACT-ID = CONTACT-V.CONTACT-ID
  IF NO RECORDS FOUND
    #MSG := 'No contact record found'
    ESCAPE BOTTOM
  END-NOREC
  IF CONTACT-V.CONTACT-TYPE NE 'O'
    #AGE := #TODAY.#YYYY - CONTACT-V.YYYY
    IF #TODAY.#MM < CONTACT-V.MM
      #AGE := #AGE - 1
    ELSE IF #TODAY.#MM = CONTACT-V.MM AND #TODAY.#DD < CONTACT-V.DD
        #AGE := #AGE - 1
      END-IF
    END-IF
    IF CONTACT-V.DATE-OF-DEATH > 0
     COMPRESS CONTACT-V.DEATH-MM CONTACT-V.DEATH-DD CONTACT-V.DEATH-YYYY
        INTO #DOD WITH DELIMITER '/'
    ELSE
      RESET #DOD
    END-IF
  END-IF
  COMPRESS CONTACT-V.MM CONTACT-V.DD CONTACT-V.YYYY INTO #DOB
    WITH DELIMITER '/'
END-FIND

CALLNAT 'MANNHWD'
  CONTACT-V.CONTACT-ID
  #TODAY
  #TOTAL-WD
  #NUMBER-WD
  #LAST-WITHDRAWAL
*
PERFORM POPULATE-DATA
#TI := 1

RPT.
REPEAT
  INPUT TEXT #MSG USING MAP 'MANMPYYY'
  DECIDE ON FIRST VALUE OF *PF-KEY
    VALUE 'PF3'
      ESCAPE BOTTOM(RPT.)
    VALUE 'PF4'
      PERFORM FIELD-HISTORY
    VALUE 'PF5'
      PERFORM BROWSE-HISTORY
    VALUE 'ENTR'
      IF #CGA-SCR-COMMAND NE '*'
        FETCH 'G1000PXX'
      END-IF
    NONE VALUE
      IGNORE
  END-DECIDE
END-REPEAT
#CGA-SCR-ID := CONTACT-V.CONTACT-ID
FETCH 'MANPYYYY'

DEFINE POPULATE-DATA
/*     -------------
GET ANNUITANTS #CGA-ISN
/*
CALLNAT 'MANNGET'
  #CGA-ISN
  #TODAY
  #CALCULATED
  ANNUITANTS
/*
MOVE EDITED #NUM-POL-WD (EM=Z9) TO #SCR-DATA.NUMBER-WD
IF ANNUITANTS.START-DATE = 0
  #SCR-DATA.START-DATE := #TODAY
END-IF
*
COMPRESS ANNUITANTS.MM ANNUITANTS.DD ANNUITANTS.YYYY INTO
  #SCR-DATA.START-DATE WITH DELIMITER '/'
COMPRESS #SCR-DATA.#MM #SCR-DATA.#DD #SCR-DATA.#YYYY INTO #ID WITH DELIMITER '/'
MOVE ANNUITANTS.POLICY-NUMBER     TO #SCR-DATA.POLICY-NBR
MOVE ANNUITANTS.SETTLEMENT-OPTION TO #SCR-DATA.SETTLEMENT-OPTION
MOVE ANNUITANTS.ANNUITANT-STATUS  TO #SCR-DATA.ANNUITANT-STATUS
/*
MOVE EDITED ANNUITANTS.PRINCIPAL  (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.PRINCIPAL
/*
IF ANNUITANTS.YYYY = #TODAY.#YYYY-A
  MOVE EDITED ANNUITANTS.INITIAL-PAYMENT (EM=Z,ZZZ,ZZ9.99)
    TO #SCR-DATA.INITIAL-PAYMENT
ELSE
  RESET #SCR-DATA.INITIAL-PAYMENT
END-IF
/*
IF ANNUITANTS.ONE-MONTH-PENSION > 0
    AND ANNUITANTS.ONE-MM = #TODAY.#MM
    AND ANNUITANTS.ONE-YYYY = #TODAY.#YYYY
  MOVE EDITED ANNUITANTS.ONE-MONTH-PENSION (EM=Z,ZZZ,ZZ9.99)
    TO #SCR-DATA.MONTHLY-PENSION
ELSE
  MOVE EDITED ANNUITANTS.MONTHLY-PENSION (EM=Z,ZZZ,ZZ9.99)
    TO #SCR-DATA.MONTHLY-PENSION
END-IF
/*
MOVE EDITED ANNUITANTS.T13TH-CHECK  (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.THIRTHEEN-CHECK
/*
COMPRESS ANNUITANTS.MM-1 ANNUITANTS.DD-1 ANNUITANTS.YYYY-1
  INTO #SCR-DATA.PAID-UP-DATE WITH DELIMITER '/'
/*
IF ANNUITANTS.YYYY = #TODAY.#YYYY-A
  #DEPOSIT := ANNUITANTS.PRINCIPAL
ELSE
  #DEPOSIT := 0
END-IF
MOVE EDITED #DEPOSIT (EM=Z,ZZZ,ZZ9.99) TO #SCR-DATA.DEPOSIT
/*
MOVE EDITED ANNUITANTS.ANNUAL-PAYMENT (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.ANNUAL-PAYMENT
/*
MOVE EDITED ANNUITANTS.RESERVE-AMOUNT (EM=Z,ZZZ,ZZ9.99)
  TO #SCR-DATA.RESERVE
/*
IF ANNUITANTS.SETTLEMENT-OPTION = #INTEREST-ONLY
  #SCR-DATA.TAXABLE := #SCR-DATA.MONTHLY-PENSION
ELSE
  MOVE EDITED ANNUITANTS.TAXABLE  (EM=Z,ZZZ,ZZ9.99)
    TO #SCR-DATA.TAXABLE
END-IF
/*

MOVE EDITED #TOTAL-POL-WD (EM=Z,ZZZ,ZZ9.99) TO #SCR-DATA.TOTAL-WD
MOVE EDITED #LAST-POL-WD (EM=NZZZZ,ZZ9.99) TO #SCR-DATA.WITHDRAWAL
/*
MOVE EDITED #RESIDUAL (EM=Z,ZZZ,ZZ9.99) TO #SCR-DATA.RESIDUAL
*
MOVE EDITED #GROSS-TAXABLE (EM=Z,ZZZ,ZZ9.99) TO #SCR-DATA.TOTAL-PAID
*
END-SUBROUTINE
*
DEFINE BROWSE-HISTORY
/*     --------------
#CGA-SCR-ID := CONTACT-V.CONTACT-ID
FETCH RETURN 'MANPHIST'
END-SUBROUTINE
/*
DEFINE FIELD-HISTORY
/*     --------------
L := *CURS-LINE
J := *CURS-COL
IF L = 14 AND J < 30
  #CGA-SCR-ID := CONTACT-V.CONTACT-ID
  #CGA-MF-SCR-PARM-2 := 'MONTH'
  FETCH RETURN 'MANPHSTM'
END-IF
END-SUBROUTINE
END
