* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram AUCNORDR.
/**
/** :author nguentchev

DEFINE DATA
PARAMETER
1 INPUT-DATA          (A) Dynamic        /* In
1 RESPONSE-DATA       (A) Dynamic        /* Out
LOCAL USING ORDPDA
LOCAL USING ORDARSP
LOCAL
1 BLANK   (A1) CONST <' '>
1 J   (I4)
1 #ORDER
  2 CRMOrderID   (A) DYNAMIC
  2 UserCode     (A) DYNAMIC
  2 HeightFeet   (N2)
  2 HeightInches (N2)
  2 Weight       (N3)

END-DEFINE
/*         Parse the Order request from CRM
CALLNAT 'ORDPARSE' INPUT-DATA IASOrder
RESPONSE-DATA := IASOrder.ErrorDescription
IF IASOrder.ErrorDescription NE BLANK
  PERFORM DO-RESPONSE
  ESCAPE ROUTINE
END-IF
/*

/* All entities are independent and complete transaction
/* In case of error the order request can be resend and only
/*  missing data will be created.
/*         Create contacts
/*         Create contacts relationships
CALLNAT 'ORDNCNTS' IASOrder
FOR J 1 TO IASOrder.C@Contact
  IF IASOrder.ErrorDescription_002 (J) NE BLANK
/* Create an Error
    COMPRESS IASOrder.ErrorDescription IASOrder.ErrorDescription_002 (J)
      INTO IASOrder.ErrorDescription
    BACKOUT TRANSACTION
    PERFORM DO-RESPONSE
    ESCAPE ROUTINE
  END-IF
END-FOR

END TRANSACTION  /* Contacts data are stored in IAS

/*    Create applications 
/*    Create application roles
CALLNAT 'ORDNAPPS' IASOrder
FOR J 1 TO IASOrder.C@Application
  IF IASOrder.ErrorDescription_003 (J) NE BLANK
/* Create an Error
    COMPRESS IASOrder.ErrorDescription IASOrder.ErrorDescription_003 (J)
      INTO IASOrder.ErrorDescription
    BACKOUT TRANSACTION
    PERFORM DO-RESPONSE
    ESCAPE ROUTINE   
  END-IF
END-FOR

END TRANSACTION /* Application data

/*      
/*         Create payment
CALLNAT 'ORDNPAY' IASOrder
IF IASOrder.ErrorDescription NE BLANK
/* Create an Error
  BACKOUT TRANSACTION
ELSE
  END TRANSACTION
END-IF
PERFORM DO-RESPONSE

DEFINE DO-RESPONSE
/*     -----------
IASOrderResponse.ErrorCode := IASOrder.ErrorCode
IASOrderResponse.ErrorDescription := IASOrder.ErrorDescription
IASOrderResponse.ATNumber := IASOrder.ATNumber (1)
CALLNAT 'ORDNSRLZ' RESPONSE-DATA IASOrderResponse
END-SUBROUTINE

/*         Sync CRM with IAS data elements (asynch request). Can be done in NM/AI
/* 
END
