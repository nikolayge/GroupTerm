* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram ORDNROLE.
/**
/** :author nguentchev
*     <PrimaryBD><![CDATA[]]></PrimaryBD>       --> DesignationUpdate.PrimaryBD
*     <ContingentBD><![CDATA[]]></ContingentBD> --> DesignationUpdate.ContingentBD
*     <PrimaryBDType></PrimaryBDType>           --> DesignationUpdate.PrimaryType        : BENE-TYPE
*     <PrimaryBDShare></PrimaryBDShare>         --> DesignationUpdate.PrimaryShareType   : BENE-SHARE-TYPE
*     <ContingentBDType></ContingentBDType>     --> DesignationUpdate.ContingentType     : BENE-TYPE
*     <ContingentBDShare></ContingentBDShare>   --> DesignationUpdate.ContingentShareType: BENE-SHARE-TYPE
DEFINE DATA
PARAMETER
1 #AT-NUMBER     (N6)
1 #POLICY-NUMBER (A16)
1 #CONTACT-ID    (N8)
1 #CGA-CLERK     (A3)
1 #ISSUE-DATE    (N8)
1 #ID-NUMBER     (N6)

LOCAL USING POSADSGN
LOCAL USING POSL0002
LOCAL USING POSLROLE
LOCAL USING OBJLGENT 
LOCAL USING OBJLUSER
LOCAL
1 BLANK  (A1) CONST <' '>
1 APR-V VIEW OF A-APPL-ROLES
  2 ADD-USER (A8)
  2 ADD-DATE (N8.0)
  2 ADD-TIME (N7.0)
  2 LAST-UPD-USER (A8)
  2 LAST-UPD-DATE (N8.0)
  2 LAST-UPD-TIME (N7.0)
  2 AT-GUID (A50)
  2 CONTACT-GUID (A50)
  2 RELATION-ROLE (N2.0)
  2 BENE-RESTRICTED (A1)
  2 BENE-PERCENT (A10)        
  2 BENE-SETTLEMENT (A1)
  2 SETTLEMENT-TYPE (A1)
  2 ALL-CHILDREN (A1)  
  2 CONTACT-ID   (N8)   
1 AT-V VIEW OF A-APPL-TRACKING
  2 AT-GUID (A50)
1 CNT-V VIEW OF A-CONTACTS
  2 CONTACT-ID  (N8)
1 #CLERK-KEY       (A7) INIT <'WEBU'>
1 #ADD             (I4) CONST <1>
1 #MSG             (A) DYNAMIC
1 #DESIGNATION-ISN (N8)
1 PDNumber         (i4)
1 CDNumber         (i4)
1 OWNER-CONTACT-ID (N8)
1 PAYER-CONTACT-ID (N8)
END-DEFINE
/* 
RESET PC-V
PC-V.ADD-DATE := PC-V.LAST-UPD-DATE := #ISSUE-DATE 
PC-V.ADD-TIME := PC-V.LAST-UPD-TIME := *TIMN
PC-V.POLICY-NUMBER := #POLICY-NUMBER
RESET INITIAL #CLERK-KEY
COMPRESS #CLERK-KEY #CGA-CLERK INTO #CLERK-KEY LEAVING NO
FIND(1) GEN-TABLE-V WITH SEC-KEY = #CLERK-KEY
  FIND(1) USER-V WITH CLERK-ID = GEN-SUB-KEY
    PC-V.ADD-USER := PC-V.LAST-UPD-USER := USER-V.TERMINAL-ID
  END-FIND
END-FIND
/*
FIND (1) AT-V WITH ID-NUMBER = #AT-NUMBER
END-FIND
/*
RESET #DSGN
#DSGN.STATUS-CODE  := 'N' /* 'F'
#DSGN.ALL-CHILDREN := 'N'
#DSGN.DESIGNATION-ID := GET-DSGN-ID (< >)
MOVE 'CRM' TO #DSGN.ADD-USER /*  Last update data
CALLNAT 'POSN0011' #ADD #MSG #DESIGNATION-ISN #DSGN
/*
RESET 
  OWNER-CONTACT-ID  
  PAYER-CONTACT-ID  
FIND APR-V WITH AT-GUID = AT-V.AT-GUID
  IF APR-V.CONTACT-ID > 0
    FIND (1) CNT-V WITH CONTACT-ID = APR-V.CONTACT-ID
      IF NO RECORDS FOUND
        ESCAPE BOTTOM
      END-NOREC
      PERFORM CREATE-ROLE
    END-FIND
  ELSE
    FIND (1) CNT-V WITH CRM-CONTACT-GUI = APR-V.CONTACT-GUID
      IF NO RECORDS FOUND
        ESCAPE BOTTOM
      END-NOREC
      PERFORM CREATE-ROLE
    END-FIND
  END-IF
END-FIND
IF PAYER-CONTACT-ID = 0  /* Create Payer = Owner
    AND OWNER-CONTACT-ID > 0
  PC-V.RELATION := PayorRole
  PC-V.CONTACT-ID := OWNER-CONTACT-ID
  reset PC-V.DESIGNATION-ID
  STORE PC-V
END-IF
*
DEFINE CREATE-ROLE
/*
PC-V.RELATION         := APR-V.RELATION-ROLE
PC-V.REFERENCE-NUMBER := #ID-NUMBER
PC-V.SETTLEMENT-TYPE  := APR-V.SETTLEMENT-TYPE
PC-V.BENE-FRACTION    := APR-V.BENE-PERCENT

IF PC-V.RELATION EQ PrimaryBene OR= ContingentBene
  PC-V.DESIGNATION-ID := #DSGN.DESIGNATION-ID
ELSE
  RESET PC-V.DESIGNATION-ID
END-IF
IF PC-V.RELATION EQ PrimaryBene
  ADD 1 TO PDNumber
  PC-V.BENE-TYPE := 'B'
  PC-V.BENE-SHARE-TYPE := 'S'
  PC-V.BENE-FRACTION := APR-V.BENE-PERCENT
  IF PC-V.BENE-FRACTION = BLANK
    PC-V.BENE-FRACTION := '100%'
  END-IF
END-IF
IF PC-V.RELATION EQ ContingentBene
  ADD 1 TO CDNumber
  PC-V.BENE-TYPE := 'B'
  PC-V.BENE-SHARE-TYPE := 'S'
  IF PC-V.BENE-FRACTION = BLANK
    PC-V.BENE-FRACTION := '100%'
  END-IF
END-IF
IF PC-V.RELATION EQ PrimaryOwner
  OWNER-CONTACT-ID := CNT-V.CONTACT-ID
END-IF
IF PC-V.RELATION EQ PayorRole
  PAYER-CONTACT-ID := CNT-V.CONTACT-ID
END-IF

PC-V.CONTACT-ID := CNT-V.CONTACT-ID
STORE PC-V

END-SUBROUTINE
END
