* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/** New Subprogram ORDIASD.
/**
/** :author nguentchev
define data
parameter using ORDPDA

LOCAL USING GOCOA001
LOCAL USING GOPHA001
LOCAL USING GOEMA001
LOCAL USING GOADA001
LOCAL USING GOADA002
LOCAL USING GODIA001
LOCAL USING GOMIA001
LOCAL USING GOPIA001
LOCAL USING LWSAAT
LOCAL
1 #PHONES     (6)                    
  2 PHONE-TYPE-CODE     (A1)
  2 INTL-FLAG           (A1)
  2 PREFERRED-PHONE-IND (A1)
  2 DIAL-NUMBER         (A) DYNAMIC
  2 EXT                 (A6)
  2 REMARKS             (A) DYNAMIC
*
1 #EMAIL1
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
1 #EMAIL2
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
1 #EMAIL3
  2 EMAIL-TYPE (A1)
  2 EMAIL-ADDRESS (A70)
  2 STATUS (A1)
*
1 #PRIMARY
  2 STREET            (A) DYNAMIC
  2 UNIT              (A) DYNAMIC
  2 CITY              (A) DYNAMIC
  2 ZIP-CODE          (A) DYNAMIC
  2 STATE-CODE        (A) DYNAMIC
  2 COUNTRY-CODE      (A2)
  2 RETURNED-MAIL-IND (A1)
  2 ADDR-VALID-IND    (A5) /* TRUE/FALSE
*
1 #SECONDARY
  2 STREET            (A) DYNAMIC
  2 UNIT              (A) DYNAMIC
  2 CITY              (A) DYNAMIC
  2 ZIP-CODE          (A) DYNAMIC
  2 STATE-CODE        (A) DYNAMIC
  2 COUNTRY-CODE      (A2)
  2 RETURNED-MAIL-IND (A1)

1 GEN-TABLE-V VIEW OF A-GEN-TABLE
  2 GEN-KEY (A4)
  2 GEN-SUB-KEY (A3)
  2 GEN-ALPHA-VALUE (A50)
*
1 cnt-v VIEW OF A-CONTACTS
  2 CRM-CONTACT-GUI
*
1 MEMBER VIEW OF A-STATUS
  2 ID-NUMBER       (N6.0)
  2 REDEFINE ID-NUMBER
    3 #ID-NUMBER-S  (A6)
  2 MEMBER-FLAG     (A1)
  2 WIDOW-FLAG      (A1)
  2 APPL-SOURCE        (A7)
  2 MEMBER-CONTACT-ID  (N8)
  2 PROCESS-IND        (A1)
  2 INTRO-FLAG         (A1)

1 CNT-REL VIEW OF A-CONTACT-RELATIONSHIPS
  2 CONTACT-1-ID                      (N8.0) /* Descriptor
  2 ROLE-1-CD                         (A1)
  2 CONTACT-2-ID                      (N8.0) /* Descriptor
  2 ROLE-2-CD                         (A1)
  2 LAST-USER-UPD                     (A8)
  2 ACCESS-AUTH-SVC                   (A1)   /* Insurance IAS database
  2 ACCESS-AUTH-INS                   (A1)   /* Services  SAS database
1 J  (I4)
1 BLANK          (A1) CONST <' '>
1 #TABLE-CODE    (A3) CONST <'041'>
1 #DASH          (A1) CONST <'-'>
1 USA            (A2) CONST <'US'>
1 PUERTORICO     (A2) CONST <'PR'>

1 #SSN    (A9) 1 REDEFINE #SSN
  2 #SSN-3    (A3)
  2 #SSN-2    (A2)
  2 #SSN-4    (A4)
1 #ZIP-5      (A5)
1 #ZIP-4      (A4)
end-define
/* 
RESET AT-DATA
AT-DATA.atnumber := IASOrder.ATNumber(1)
CALLNAT 'LWSNATDA' AT-DATA
  ErrorDescription
IF ErrorDescription NE BLANK
  ESCAPE ROUTINE
END-IF
IASOrder.Weight := AT-DATA.weight
IASOrder.HeightFeet := AT-DATA.height
IF AT-DATA.smoker = 'YES'
  IASOrder.Nicotine := TRUE
END-IF

IASOrder.CrmApplicationID (1) := AT-DATA.applicationtrackingid
IASOrder.ATStatus (1) := AT-DATA.atstatus
IASOrder.PlanCode (1) := AT-DATA.plan
IASOrder.FaceAmount (1) := AT-DATA.faceamount
IASOrder.PremiumClass (1) := AT-DATA.ratingcode
IASOrder.CreatedOn (1) := AT-DATA.datereceived
IF AT-DATA.capindicator = 'YES'
  IASOrder.CapIndicator (1) := TRUE
END-IF
IASOrder.ApplicationSource (1) := AT-DATA.applivationsource

*       Contact relations
RESET J
FIND CNT-REL WITH CONTACT-1-ID = at-data.iascontactid /* Child
  IF NO RECORDS
    ESCAPE BOTTOM
  END-NOREC
  ADD 1 TO J
  EXPAND ARRAY IASOrder.Relationship TO ( 1:J )
  IASOrder.C@Relationship := J
  IASOrder.MemberContactID  (J) := at-data.iascontactid
  IASOrder.PartnerContactID (J) := CNT-REL.CONTACT-2-ID
  IASOrder.MemberRole (J) :=
    FUNCTION-CODE-1-DESC (< #TABLE-CODE, ROLE-1-CD >)
  IASOrder.PartnerRole (J) :=
    FUNCTION-CODE-1-DESC (< #TABLE-CODE, ROLE-2-CD >)
  IASOrder.MemberId (J) := AT-DATA.ContactGUID

  FIND CNT-V WITH CONTACT-ID = CNT-REL.CONTACT-2-ID
    IASOrder.PartnerId (J) := CNT-V.CRM-CONTACT-GUI
  END-FIND
END-FIND

*      Contact Insured
#CONTACT.CONTACT-ID := AT-DATA.iascontactid
#CONTACT.CRM-CONTACT-GUI := AT-DATA.ContactGUID
CALLNAT 'GOCON002' #CONTACT #MSG
IF #CONTACT.CONTACT-TYPE = 'O' AND #CONTACT.LAST-NAME = ' '  
  MOVE #CONTACT.FIRM-ORGANIZATION-NAME TO #CONTACT.LAST-NAME  
END-IF 
*      Personal data
#PERSONAL-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GOPIN002' #PERSONAL-INFO

*      Military data Insured
#DEATH-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GODIN002' #DEATH-INFO
DECIDE FOR EVERY CONDITION             
*   WHEN #MILITARY-INFO.DATE-OF-RANK > 0
*     MOVE #MILITARY-INFO.DATE-OF-RANK TO IASOrder.r
  WHEN #DEATH-INFO.DATE-OF-DEATH > 0
    MOVE #DEATH-INFO.DATE-OF-DEATH TO IASOrder.DateOfDeath (1)
  WHEN #MILITARY-INFO.PRIOR-DATE-OF-RANK > 0
    MOVE #MILITARY-INFO.PRIOR-DATE-OF-RANK TO
      IASOrder.DateOfPreviousPromotion (1)
  WHEN #MILITARY-INFO.DATE-RETIRED > 0
    MOVE #MILITARY-INFO.DATE-RETIRED TO IASOrder.DateRetired (1)
  WHEN #PERSONAL-INFO.DATE-OF-MARRIAGE > 0
    MOVE #PERSONAL-INFO.DATE-OF-MARRIAGE TO IASOrder.DateOfMarriage (1)
*   WHEN #MILITARY-INFO.DATE-INIT-ENTRY-MIL-SVC > 0
* MOVE #MILITARY-INFO.DATE-INIT-ENTRY-MIL-SVC TO DATE-INIT-ENTRY-MIL-SVC-A
*   WHEN #MILITARY-INFO.BASE-PAY-EF-DATE > 0
*     MOVE #MILITARY-INFO.BASE-PAY-EF-DATE TO BASE-PAY-EF-DATE-A
*   WHEN #MILITARY-INFO.ACTIVE-DUTY-BASE-DATE > 0
*     MOVE #MILITARY-INFO.ACTIVE-DUTY-BASE-DATE TO ACTIVE-DUTY-BASE-DATE-A
  WHEN NONE
    IGNORE
END-DECIDE      

IF #CONTACT.GENDER-CD = 'F'
  MOVE 'Female' TO IASOrder.Gender (1)
ELSE
  MOVE 'Male' TO IASOrder.Gender (1)
END-IF

IF #CONTACT.SSN > 0
  MOVE EDITED #CONTACT.SSN (EM=9(9)) TO #SSN
  COMPRESS #SSN-3
    #SSN-2
    #SSN-4
    INTO IASOrder.GovtID (1) WITH DELIMITER #DASH
ELSE
  RESET #SSN
END-IF
IASOrder.DateOfBirth (1) := #CONTACT.DATE-OF-BIRTH
IASOrder.MilitaryRank (1) := F-GET-RANK-ABBR (< #CONTACT.RANK-ID >)
IASOrder.MilitaryRankId (1) := #CONTACT.RANK-ID
IASOrder.MilitaryStatus (1) := #CONTACT.MILITARY-STATUS

*        Phones    Insured
CALLNAT 'MBRDATA1' #CONTACT.CONTACT-ID #PHONES (*)
*        eMail  Insured
CALLNAT 'MBRDATA2' #CONTACT.CONTACT-ID #EMAIL1 #EMAIL2 #EMAIL3
*        Addresses  Insured
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT.CONTACT-ID
*        Secondary Address  Insured
#ADDR-REL.ADDR-TYPE-CD := 'S'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-REL.STATUS NE 'D'
  MOVE BY NAME #ADDR-POOL TO #SECONDARY
  #SECONDARY.RETURNED-MAIL-IND := #ADDR-REL.RETURNED-MAIL-IND
  IF #ADDR-POOL.COUNTRY-CODE = USA OR= PUERTORICO
    IF ZIP-5 > 0 AND ZIP-4 > 0
      MOVE EDITED ZIP-5 (EM=99999) TO #ZIP-5
      MOVE EDITED ZIP-4 (EM=9999)  TO #ZIP-4
    COMPRESS #ZIP-5 #ZIP-4 INTO #SECONDARY.ZIP-CODE WITH DELIMITER #DASH
    ELSE
      MOVE EDITED ZIP-5 (EM=99999) TO #SECONDARY.ZIP-CODE
    END-IF
  ELSE
    #SECONDARY.ZIP-CODE := #ADDR-POOL.POSTAL-CODE
    #SECONDARY.STATE-CODE := #ADDR-POOL.INTL-REGION
  END-IF
END-IF
*        Primary Address Insured
RESET #ADDR-REL #ADDR-POOL
#ADDR-REL.CONTACT-ID := #CONTACT.CONTACT-ID
#ADDR-REL.ADDR-TYPE-CD := 'P'
CALLNAT 'GOADN002' #ADDR-POOL #ADDR-REL #CLERK-ID #MSG
  #ADDRESS-UI
IF #ADDR-REL.STATUS NE 'D'
  MOVE BY NAME #ADDR-POOL TO #PRIMARY
  #PRIMARY.RETURNED-MAIL-IND := #ADDR-REL.RETURNED-MAIL-IND
  IF #ADDR-POOL.COUNTRY-CODE = USA OR= PUERTORICO
    IF ZIP-5 > 0 AND ZIP-4 > 0
      MOVE EDITED ZIP-5 (EM=99999) TO #ZIP-5
      MOVE EDITED ZIP-4 (EM=9999)  TO #ZIP-4
      COMPRESS #ZIP-5 #ZIP-4 INTO #PRIMARY.ZIP-CODE WITH DELIMITER #DASH
    ELSE
      MOVE EDITED ZIP-5 (EM=99999) TO #PRIMARY.ZIP-CODE
    END-IF
  ELSE
    #PRIMARY.ZIP-CODE := #ADDR-POOL.POSTAL-CODE
    #PRIMARY.STATE-CODE := #ADDR-POOL.INTL-REGION
  END-IF
END-IF

#MILITARY-INFO.#CONTACT-ID := #CONTACT.CONTACT-ID
CALLNAT 'GOMIN002' #MILITARY-INFO
*
END
