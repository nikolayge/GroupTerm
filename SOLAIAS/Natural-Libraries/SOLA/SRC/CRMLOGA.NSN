* >Natural Source Header 000000
* :Mode S
* :CP ISO_8859-1:1987
* :LineIncrement 10
* <Natural Source Header
*
* Program ID: CRMLOG
* Logging communication messages with MS DYNAMICS CRM
*
DEFINE DATA
PARAMETER
1 #ERROR-CODE    (I4)
1 #MESSAGE       (A) DYNAMIC
1 #LOG-REQUEST   (I4)
1 #MESSAGE-ID    (A) DYNAMIC OPTIONAL
1 #CRM-Request   (I4) OPTIONAL
LOCAL
1 #ERROR-DESCRIPTION  (A) DYNAMIC
1 ERROR-LOG           (A32000)  /* Have a work log file
1 #TRL-FILE-NAME      (A) DYNAMIC
1 #EFFECTIVE-DATE-A   (A8)
1 BLANK               (A1) CONST <' '>
1 #DATE               (A8) 1 REDEFINE #DATE
  2 #DATE-N           (N8)
1 P_PARMS
  2 P_ERROR_NUM           (I4)
  2 P_ERROR_LINE          (I4)
  2 P_ERROR_PROGRAM       (A32)
  2 P_ERROR_LIB           (A32)
  2 P_ERROR_DBID          (I4)
  2 P_ERROR_FNR           (I4)
  2 P_ERROR_TIME          (T)
  2 P_ERROR_PGM_TYPE      (A1)
  2 P_ERROR_PGM_TYPE_LONG (A11)
  2 P_ERROR_LANG          (I4)
  2 P_ERROR_LEVEL         (I4)
  2 P_ERROR_CLASS         (I1)
  2 P_ERROR_TYPE          (I1)
1 #ERROR-TIME             (A19)
*
1 #J                      (I4)
1 #Timeout          (I4) CONST <-2>
1 #Natural-error    (I4) CONST <-3>
1 #MESSAGE-LOG      (I4) CONST <2>
1 #ERROR-LOG        (I4) CONST <3>
1 #REQUEST-LOG      (I4) CONST <4>
1 #UPDATE-LOG       (I4) CONST <5>
1 #FILE-NAME        (A) DYNAMIC INIT <'/usr/SAG/tmp/'>
**1 #FILE-NAME        (A) DYNAMIC INIT <'C:\temp\'>
*
1 USR1040L
  2 INPUTS
    3 FUNCTION         (A01)
  2 INPUT-OUTPUTS
    3 UDB-PARM         (N05)
  2 OUTPUTS
    3 RESPONSE         (I04)
END-DEFINE
*
MOVE EDITED *DATX (EM=YYYYMMDD) TO #EFFECTIVE-DATE-A
*
compress #FILE-NAME 'CRM_SYNCH' #EFFECTIVE-DATE-A '.txt' into
  #TRL-FILE-NAME leaving no
DEFINE WORK FILE 6 #TRL-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'

compress #FILE-NAME 'CRMOrder_ERRORS' #EFFECTIVE-DATE-A '.txt' into
  #TRL-FILE-NAME leaving no
DEFINE WORK FILE 7 #TRL-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'

compress #FILE-NAME 'CRMOrder_REQUEST' #EFFECTIVE-DATE-A '.txt' into
  #TRL-FILE-NAME leaving no
DEFINE WORK FILE 8 #TRL-FILE-NAME TYPE 'ASCII-COMPRESSED' ATTRIBUTES 'APPEND'
*
ASSIGN FUNCTION = 'G'
CALLNAT 'USR1040N' USR1040L
*
DECIDE ON FIRST VALUE OF #LOG-REQUEST
  VALUE #ERROR-LOG
    PERFORM REPORT-ERROR
  VALUE #MESSAGE-LOG
    MOVE EDITED *TIMX (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS) TO ERROR-LOG
    COMPRESS ERROR-LOG 'DB=' UDB-PARM INTO ERROR-LOG
    WRITE WORK 6 ERROR-LOG
    ERROR-LOG := #MESSAGE
    WRITE WORK 6 ERROR-LOG
    CLOSE WORK 6
  VALUE #REQUEST-LOG
    MOVE EDITED *TIMX (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS) TO ERROR-LOG
    COMPRESS ERROR-LOG 'DB=' UDB-PARM INTO ERROR-LOG
    WRITE WORK 8 ERROR-LOG
    ERROR-LOG := #MESSAGE
    WRITE WORK 8 ERROR-LOG
    CLOSE WORK 8
  NONE VALUE IGNORE
END-DECIDE
*
DEFINE REPORT-ERROR
/*     ============
CALLNAT 'USR2001N' P_PARMS
MOVE EDITED P_ERROR_TIME (EM=YYYY'-'MM'-'DD' 'HH':'II':'SS)
  TO #ERROR-TIME
COMPRESS
  'Error number:' P_ERROR_NUM
  'Error class:' P_ERROR_CLASS
  'Error line:' P_ERROR_LINE
  'Error program:' P_ERROR_PROGRAM
  'Error program type:' P_ERROR_PGM_TYPE
  'Error program type long:' P_ERROR_PGM_TYPE_LONG
  'Error program level:' P_ERROR_LEVEL
  'Language at error time:' P_ERROR_LANG
  'Error library:' P_ERROR_LIB
  'DBID:' P_ERROR_DBID 'FNR:' P_ERROR_FNR
  'Error type:'  P_ERROR_TYPE
  'Error time:' #ERROR-TIME
  INTO #ERROR-DESCRIPTION
IF #ERROR-CODE NE 0  /* Already formated
  COMPRESS #MESSAGE #ERROR-DESCRIPTION INTO #ERROR-DESCRIPTION
ELSE
  IF P_ERROR_NUM = 3009
    #ERROR-CODE := #Timeout
    COMPRESS P_ERROR_NUM P_ERROR_LINE P_ERROR_PROGRAM #ERROR-TIME
      INTO #ERROR-DESCRIPTION
  ELSE
    #ERROR-CODE := #Natural-error
  END-IF
END-IF
COMPRESS #ERROR-CODE #ERROR-DESCRIPTION INTO ERROR-LOG
WRITE WORK 7 ERROR-LOG
CLOSE WORK 7
END-SUBROUTINE
*
END
